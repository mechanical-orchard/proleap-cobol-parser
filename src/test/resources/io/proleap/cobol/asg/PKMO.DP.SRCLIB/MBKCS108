00001 *                                                                 02/05/92
00002 *                                                                 MBKCS108
00003 *                                                                    LV241
00004  IDENTIFICATION DIVISION.                                         MBKCS108
00005                                                                   MBKCS108
00006  PROGRAM-ID.    MBKCS108.                                            CL182
00007  AUTHOR.        BOB LOOS - STRATAGEM.                                CL*13
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MBKCS108
00009  DATE-WRITTEN.  06-26-91.                                            CL*13
00010  DATE-COMPILED.                                                   MBKCS108
00011                                                                   MBKCS108
00012 ***************************************************************** MBKCS108
00013 *    LIST PROCESSOR MERCHANDISE SUB-CLASS                            CL179
00014 *                                                                 MBKCS108
00015 ***************************************************************** MBKCS108
262978* CHANGED 10/22/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
00016                                                                   MBKCS108
00017 *                                                                 MBKCS108
00018 *                                                                 MBKCS108
00019 *                                                                 MBKCS108
00020  ENVIRONMENT DIVISION.                                            MBKCS108
00021 *                                                                 MBKCS108
00022 *                                                                 MBKCS108
00023 *                                                                 MBKCS108
00024  DATA DIVISION.                                                   MBKCS108
00025 *                                                                 MBKCS108
00026 *                                                                 MBKCS108
00027  WORKING-STORAGE SECTION.                                         MBKCS108
00028  01  AM-ABEND-MESSAGES.                                              CL*57
00029      05  AM-ABEND-MESSAGE-8             PIC X(78)    VALUE           CL*57
00030      'ERROR TRYING TO DELETE TEMP STORAGE QUEUE'.                    CL*57
00031      05  AM-ABEND-MESSAGE-9             PIC X(78)    VALUE           CL*57
00032      'ERROR TRYING TO REWRITE TEMP STORAGE QUEUE'.                   CL*57
00033      05  AM-ABEND-MESSAGE-10            PIC X(78)    VALUE           CL*57
00034      'ERROR TRYING TO WRITE TEMP STORAGE QUEUE'.                     CL*57
00035      05  AM-ABEND-MESSAGE-11            PIC X(78)    VALUE           CL*57
00036      'LOGIC ERROR IN TRYING TO WRITE TO TEMP STORAGE QUEUE - BLOCK   CL*57
00037 -    '      '.                                                       CL*57
00038      05  FILLER REDEFINES AM-ABEND-MESSAGE-11.                       CL*57
00039          10  FILLER                     PIC X(61).                   CL*57
00040          10  AM-BLOCK-NUMBER-11         PIC Z(03)9-.                 CL*57
00041          10  FILLER                     PIC X(12).                   CL*57
00042      05  AM-ABEND-MESSAGE-12            PIC X(78)    VALUE           CL*57
00043      'ERROR TRYING TO READ TEMP STORAGE QUEUE'.                      CL*57
00044      05  AM-ABEND-MESSAGE-13            PIC X(78)    VALUE           CL*58
00045      'ERROR TRYING TO SELECT A RECORD FROM TMDSEAR TABLE - KEY       CL*58
00046 -    '      '.                                                       CL*58
00047      05  FILLER REDEFINES AM-ABEND-MESSAGE-13.                       CL*58
00048          10  FILLER                     PIC X(57).                   CL*58
00049          10  AM-OPERCO-NBR-13           PIC X(02).                   CL*58
00050          10  AM-DEPT-NBR-13             PIC X(03).                   CL*58
00051          10  AM-MCL-NBR-13              PIC X(02).                   CL*58
00052          10  AM-SCL-NBR-13              PIC X(03).                   CL*58
00053          10  FILLER                     PIC X(11).                   CL181
00054      05  AM-ABEND-MESSAGE-21            PIC X(78)    VALUE           CL181
00055      'ERROR TRYING TO OPEN MERCH-CURSOR'.                            CL*58
00056      05  AM-ABEND-MESSAGE-22            PIC X(78)    VALUE           CL*58
00057      'ERROR TRYING TO CLOSE MERCH-CURSOR'.                           CL*58
00058  01  PC-PROGRAM-CONSTANTS.                                        MBKCS108
00059      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.   CL**5
00060      05  PC-SELECTED                    PIC X(01)    VALUE 'S'.      CL*50
00061      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072  MBKCS108
00062                                                      COMP SYNC.   MBKCS108
00063      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE        MBKCS108
00064      'MB108A  '.                                                     CL192
00065      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE        MBKCS108
00066      'MBKM108 '.                                                     CL192
00067      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE        MBKCS108
00068      'MBKCS108'.                                                     CL182
00069 ***************************************************************** MBKCS108
00070 *    PC-ITEMS-PER-PANEL = MAXIMUM NUMBER OF ITEMS ON THE SCREEN   MBKCS108
00071 ***************************************************************** MBKCS108
00072      05  PC-ITEMS-PER-PANEL             PIC S9(04)   VALUE +14       CL*14
00073                                                      COMP SYNC.   MBKCS108
00074 ***************************************************************** MBKCS108
00075 *    PC-ITEMS-PER-BLOCK = MAXIMUM NUMBER OF ITEMS IN A BLOCK      MBKCS108
00076 ***************************************************************** MBKCS108
00077      05  PC-ITEMS-PER-BLOCK             PIC S9(04)   VALUE +18       CL*14
00078                                                      COMP SYNC.   MBKCS108
00079 ***************************************************************** MBKCS108
00080 *    PC-MAX-ITEMS-TO-BE-READ = THE MAXIMUM NUMBER OF ITEMS TO     MBKCS108
00081 *    BE READ FROM THE DATABASE AT ONE TIME.  USED WHEN THE USER   MBKCS108
00082 *    CHANGES THE BEGINNING LIST NUMBER.                              CL**4
00083 ***************************************************************** MBKCS108
00084      05  PC-MAX-ITEMS-TO-BE-READ        PIC S9(04)   VALUE +100      CL*15
00085                                                      COMP SYNC.   MBKCS108
00086      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)   VALUE +2     MBKCS108
00087                                                      COMP SYNC.   MBKCS108
00088 ***************************************************************** MBKCS108
00089 *    PC-MAX-ITEMS = MAXIMUM NUMBER OF ITEMS THAT CAN APPEAR ON    MBKCS108
00090 *    THE LIST.                                                    MBKCS108
00091 ***************************************************************** MBKCS108
00092      05  PC-MAX-ITEMS                   PIC S9(09)   VALUE        MBKCS108
00093                                                      +1000000        CL*14
00094                                                      COMP-3.      MBKCS108
00095 ***************************************************************** MBKCS108
00096 *    PC-BLOCK-LENGTH = THE LENGTH OF 1 BLOCK WORTH OF DATA.       MBKCS108
00097 ***************************************************************** MBKCS108
00098      05  PC-BLOCK-LENGTH                PIC S9(04)   VALUE +1225     CL191
00099                                                      COMP SYNC.   MBKCS108
00100 ***************************************************************** MBKCS108
00101 *    PC-FIELD-LENGTH = THE LENGTH OF THE BEGINNING LIST NUMBER.      CL**4
00102 ***************************************************************** MBKCS108
00103      05  PC-FIELD-LENGTH                PIC S9(04)   VALUE +7        CL*49
00104                                                      COMP SYNC.   MBKCS108
00105      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128   MBKCS108
00106                                                      COMP SYNC.   MBKCS108
00107      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                      MBKCS108
00108          10  FILLER                     PIC X(01).                MBKCS108
00109          10  PC-ERASE-EOF               PIC X(01).                MBKCS108
00110                                                                   MBKCS108
00111      05  PC-MAX-QUEUE-ITEMS             PIC S9(03)   VALUE           CL206
00112                                                      +100            CL206
00113                                                      COMP-3.         CL206
00114      05  PC-NO-END-OF-RANGE             PIC 9(02)    VALUE 51.       CL*51
00115      05  PC-END-BEFORE-START            PIC 9(02)    VALUE 52.       CL*51
00116      05  PC-NO-START-OF-RANGE           PIC 9(02)    VALUE 53.       CL*51
00117      05  PC-MULTIPLE-START-RANGES       PIC 9(02)    VALUE 54.       CL*51
00118      05  PC-MULTIPLE-END-RANGES         PIC 9(02)    VALUE 55.       CL*51
00119      05  PC-MAX-RETRYS                  PIC S9(04)   VALUE +3        CL*50
00120                                                      COMP SYNC.      CL*50
00121      05  PC-OPERCO-NBR                  PIC X(02)    VALUE '03'.     CL*50
00122      05  PC-DPT-LITERAL                 PIC X(03)    VALUE 'DPT'.    CL*37
00123      05  PC-MCL-LITERAL                 PIC X(03)    VALUE 'MCL'.    CL*37
00124      05  PC-SCL-LITERAL                 PIC X(03)    VALUE 'SCL'.    CL*37
00125      05  PC-TSYMSG-NUMBERS.                                          CL183
00126          10  PC-TSYMSG-00005            PIC 9(05)    VALUE 00005.    CL183
00127          10  PC-TSYMSG-00008            PIC 9(05)    VALUE 00008.    CL183
00128          10  PC-TSYMSG-00032            PIC 9(05)    VALUE 00032.    CL201
00129          10  PC-TSYMSG-00045            PIC 9(05)    VALUE 00045.    CL206
00130          10  PC-TSYMSG-00050            PIC 9(05)    VALUE 00050.    CL206
00131          10  PC-TSYMSG-00051            PIC 9(05)    VALUE 00051.    CL183
00132          10  PC-TSYMSG-00052            PIC 9(05)    VALUE 00052.    CL183
00133          10  PC-TSYMSG-00053            PIC 9(05)    VALUE 00053.    CL183
00134          10  PC-TSYMSG-00054            PIC 9(05)    VALUE 00054.    CL191
00135          10  PC-TSYMSG-00055            PIC 9(05)    VALUE 00055.    CL183
00136          10  PC-TSYMSG-00056            PIC 9(05)    VALUE 00056.    CL183
00137          10  PC-TSYMSG-00057            PIC 9(05)    VALUE 00057.    CL207
00138          10  PC-TSYMSG-00060            PIC 9(05)    VALUE 00060.    CL183
00139          10  PC-TSYMSG-00069            PIC 9(05)    VALUE 00069.    CL183
00140          10  PC-TSYMSG-00070            PIC 9(05)    VALUE 00070.    CL183
00141          10  PC-TSYMSG-00078            PIC 9(05)    VALUE 00078.    CL201
00142          10  PC-TSYMSG-00079            PIC 9(05)    VALUE 00079.    CL201
00143          10  PC-TSYMSG-00082            PIC 9(05)    VALUE 00082.    CL201
00144          10  PC-TSYMSG-00146            PIC 9(05)    VALUE 00146.    CL221
00145          10  PC-TSYMSG-00207            PIC 9(05)    VALUE 00207.    CL235
00146          10  PC-TSYMSG-99901            PIC 9(05)    VALUE 99901.    CL226
00147                                                                      CL183
00148                                                                      CL*36
00149  01  PC-PROGRAM-COUNTERS.                                            CL*50
00150      05  PC-RETRY-CNTR                  PIC S9(04)   VALUE +0        CL*50
00151                                                      COMP SYNC.      CL*50
00152  01  PM-PROGRAM-MESSAGES.                                         MBKCS108
00153      05  INVALID-NEXT-ACT-MSG           PIC X(78)    VALUE        MBKCS108
00154      'UNRECOGNIZED ACTION ENCOUNTERED BY THIS PROGRAM'.           MBKCS108
00155                                                                   MBKCS108
00156  01   S-PROGRAM-SUBSCRIPTS.                                          CL*63
00157      05  PS-SUB                         PIC S9(04)   VALUE +0        CL*50
00158                                                      COMP SYNC.      CL*50
00159      05  PS-SUB1                        PIC S9(04)   VALUE +0        CL207
00160                                                      COMP SYNC.      CL207
00161      05  PS-SUB2                        PIC S9(04)   VALUE +0        CL207
00162                                                      COMP SYNC.      CL207
00163      05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0        CL*52
00164                                                      COMP-3.         CL*50
00165                                                                      CL**9
00166  01  PS-PROGRAM-SWITCHES.                                            CL**9
00167      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   MBKCS108
00168          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   MBKCS108
00169          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   MBKCS108
00170          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   MBKCS108
00171          88  PS-SEND-DATA-ONLY                       VALUE '4'.   MBKCS108
00172          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   MBKCS108
00173          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   MBKCS108
00174      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   MBKCS108
00175          88  PS-ERROR                                VALUE 'Y'.   MBKCS108
00176          88  PS-NO-ERROR                             VALUE 'N'.   MBKCS108
00177      05  PS-SELECTED-HISTORY-SW         PIC X(01)    VALUE 'N'.      CL221
00178          88  PS-SELECTED-HISTORY                     VALUE 'Y'.      CL221
00179          88  PS-NO-SELECTED-HISTORY                  VALUE 'N'.      CL221
00180      05  PS-TOP-ITEM-IN-ARRAY-SW        PIC X(01)    VALUE 'N'.      CL**8
00181          88  PS-TOP-ITEM-IN-ARRAY                    VALUE 'Y'.      CL**8
00182          88  PS-TOP-ITEM-NOT-IN-ARRAY                VALUE 'N'.      CL**8
00183      05  PS-BOTTOM-ITEM-IN-ARRAY-SW     PIC X(01)    VALUE 'N'.      CL**8
00184          88  PS-BOTTOM-ITEM-IN-ARRAY                 VALUE 'Y'.      CL**8
00185          88  PS-BOTTOM-ITEM-NOT-IN-ARRAY             VALUE 'N'.      CL**8
00186      05  PS-TOP-ITEM-IN-TEMP-SW         PIC X(01)    VALUE 'N'.      CL**8
00187          88  PS-TOP-ITEM-IN-TEMP                     VALUE 'Y'.      CL**8
00188          88  PS-TOP-ITEM-NOT-IN-TEMP                 VALUE 'N'.      CL**8
00189      05  PS-BOTTOM-ITEM-IN-TEMP-SW      PIC X(01)    VALUE 'N'.      CL**8
00190          88  PS-BOTTOM-ITEM-IN-TEMP                  VALUE 'Y'.      CL**8
00191          88  PS-BOTTOM-ITEM-NOT-IN-TEMP              VALUE 'N'.      CL**8
00192      05  PS-LAST-BLOCK-IN-ARRAY-SW      PIC X(01)    VALUE 'N'.      CL**8
00193          88  PS-LAST-BLOCK-IN-ARRAY                  VALUE 'Y'.      CL**8
00194          88  PS-LAST-BLOCK-NOT-IN-ARRAY              VALUE 'N'.      CL**8
00195      05  PS-VALID-SELECTIONS-SW         PIC X(01)    VALUE 'N'.      CL**4
00196          88  PS-VALID-SELECTIONS                     VALUE 'Y'.      CL**4
00197          88  PS-INVALID-SELECTIONS                   VALUE 'N'.      CL**4
00198      05  PS-VALID-BEG-ITEM-SW           PIC X(01)    VALUE 'N'.      CL**4
00199          88  PS-VALID-BEG-ITEM                       VALUE 'Y'.      CL**4
00200          88  PS-INVALID-BEG-ITEM                     VALUE 'N'.      CL**4
00201      05  PS-DB2-RETRY-SW                PIC X(01)    VALUE 'N'.      CL*51
00202          88  PS-DB2-OK                               VALUE 'Y'.      CL*51
00203          88  PS-DB2-RETRY                            VALUE 'N'.      CL*51
00204      05  PS-CURSOR-POSITION-SW          PIC X(01)    VALUE 'N'.      CL*51
00205          88  PS-CURSOR-POSITIONED                    VALUE 'Y'.      CL*51
00206          88  PS-CURSOR-NOT-POSITIONED                VALUE 'N'.      CL*51
00207      05  PS-WARNING-SW                  PIC X(01)    VALUE 'N'.      CL*51
00208          88  PS-WARNING                              VALUE 'Y'.      CL*51
00209          88  PS-NO-WARNING                           VALUE 'N'.      CL*51
00210      05  PS-INFORMATIONAL-SW            PIC X(01)    VALUE 'N'.      CL*53
00211          88  PS-INFORMATIONAL                        VALUE 'Y'.      CL*53
00212          88  PS-NOT-INFORMATIONAL                    VALUE 'N'.      CL*53
00213      05  PS-SAME-ITEMS-DISPLAYED-SW     PIC X(01)    VALUE 'N'.      CL*53
00214          88  PS-SAME-ITEMS-DISPLAYED                 VALUE 'Y'.      CL*51
00215          88  PS-DIFFERENT-ITEMS-DISPLAYED            VALUE 'N'.      CL*56
00216      05  PS-SELECTION-FOUND-SW          PIC X(01)    VALUE 'N'.      CL*56
00217          88  PS-SELECTION-FOUND                      VALUE 'Y'.      CL*58
00218          88  PS-NO-SELECTION-FOUND                   VALUE 'N'.      CL*56
00219      05  PS-MERCH-CURSOR-OPENED-SW      PIC X(01)    VALUE 'N'.      CL*84
00220          88  PS-MERCH-CURSOR-NOT-OPENED              VALUE 'N'.      CL*85
00221          88  PS-MERCH-CURSOR-OPENED                  VALUE 'Y'.      CL*84
00222      05  PS-GMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL*98
00223          88  PS-GMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL*98
00224          88  PS-GMA-CURSOR-OPENED                    VALUE 'Y'.      CL*98
00225      05  PS-DMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00226          88  PS-DMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00227          88  PS-DMA-CURSOR-OPENED                    VALUE 'Y'.      CL100
00228      05  PS-DPT-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00229          88  PS-DPT-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00230          88  PS-DPT-CURSOR-OPENED                    VALUE 'Y'.      CL100
00231      05  PS-MCL-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00232          88  PS-MCL-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00233          88  PS-MCL-CURSOR-OPENED                    VALUE 'Y'.      CL100
00234      05  PS-SCL-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00235          88  PS-SCL-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00236          88  PS-SCL-CURSOR-OPENED                    VALUE 'Y'.      CL100
00237      05  PS-DMA-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL123
00238          88  PS-DO-NOT-HAVE-DMA-ITEM                 VALUE 'N'.      CL123
00239          88  PS-HAVE-DMA-ITEM                        VALUE 'Y'.      CL123
00240      05  PS-DPT-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL129
00241          88  PS-DO-NOT-HAVE-DPT-ITEM                 VALUE 'N'.      CL129
00242          88  PS-HAVE-DPT-ITEM                        VALUE 'Y'.      CL129
00243      05  PS-MCL-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL135
00244          88  PS-DO-NOT-HAVE-MCL-ITEM                 VALUE 'N'.      CL135
00245          88  PS-HAVE-MCL-ITEM                        VALUE 'Y'.      CL135
00246      05  PS-SCL-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL148
00247          88  PS-DO-NOT-HAVE-SCL-ITEM                 VALUE 'N'.      CL148
00248          88  PS-HAVE-SCL-ITEM                        VALUE 'Y'.      CL148
00249      05  PS-SELECTION-SW                PIC X(01)    VALUE 'N'.      CL157
00250          88  PS-NO-SELECTIONS-FOUND                  VALUE 'N'.      CL157
00251          88  PS-SELECTIONS-FOUND                     VALUE 'Y'.      CL157
00252      05  PS-PROCESS-ITEM-SW             PIC X(01)    VALUE 'N'.      CL206
00253          88  NOT-END-OF-ITEMS                        VALUE 'N'.      CL206
00254          88  END-OF-ITEMS                            VALUE 'Y'.      CL206
00255      05  PS-UPDATE-INTER-APPL-SW        PIC X(01)    VALUE 'N'.      CL209
00256          88  DONT-SETUP-INTER-APPL                   VALUE 'N'.      CL209
00257          88  SETUP-INTER-APPL                        VALUE 'Y'.      CL209
00258      05  PS-INTER-APPL-SW               PIC X(01)    VALUE 'N'.      CL227
00259          88  PS-INTER-APPL-VALID                     VALUE 'Y'.      CL227
00260          88  PS-INTER-APPL-INVALID                   VALUE 'N'.      CL227
00261                                                                   MBKCS108
00262  01  PV-PROGRAM-VARIABLES.                                        MBKCS108
00263      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     MBKCS108
00264                                                      COMP SYNC.   MBKCS108
00265      05  PV-HIGHEST-ITEM-TO-BE-READ     PIC S9(09)   VALUE +0     MBKCS108
00266                                                      COMP-3.      MBKCS108
00267      05  PV-ENDING-ITEM-ON-PANEL        PIC S9(09)   VALUE +0     MBKCS108
00268                                                      COMP-3.      MBKCS108
00269      05  PV-ITEM-IN-USE                 PIC S9(09)   VALUE +0     MBKCS108
00270                                                      COMP-3.      MBKCS108
00271      05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0     MBKCS108
00272                                                      COMP-3.      MBKCS108
00273      05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0     MBKCS108
00274                                                      COMP-3.      MBKCS108
00275      05  PV-MAX-ITEMS-TO-BE-READ        PIC S9(09)   VALUE +0     MBKCS108
00276                                                      COMP-3.      MBKCS108
00277      05  PV-FIRST-ITEM-IN-ARRAY         PIC S9(09)   VALUE +0     MBKCS108
00278                                                      COMP-3.      MBKCS108
00279      05  PV-LAST-ITEM-IN-ARRAY          PIC S9(09)   VALUE +0     MBKCS108
00280                                                      COMP-3.      MBKCS108
00281      05  PV-LAST-ITEM-IN-TEMP           PIC S9(09)   VALUE +0     MBKCS108
00282                                                      COMP-3.      MBKCS108
00283      05  PV-BEGINNING-ITEM              PIC S9(09)   VALUE +0     MBKCS108
00284                                                      COMP-3.      MBKCS108
00285      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)   VALUE +0     MBKCS108
00286                                                      COMP-3.      MBKCS108
00287      05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0     MBKCS108
00288                                                      COMP SYNC.   MBKCS108
00289      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0     MBKCS108
00290                                                      COMP SYNC.   MBKCS108
00291      05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0     MBKCS108
00292                                                      COMP SYNC.   MBKCS108
00293      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)   VALUE +0     MBKCS108
00294                                                      COMP SYNC.   MBKCS108
00295      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS108
00296                                                      COMP SYNC.   MBKCS108
00297      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS108
00298                                                      COMP SYNC.   MBKCS108
00299      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)   VALUE +0     MBKCS108
00300                                                      COMP SYNC.   MBKCS108
00301      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)   VALUE +0     MBKCS108
00302                                                      COMP SYNC.   MBKCS108
00303      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)   VALUE +0     MBKCS108
00304                                                      COMP SYNC.   MBKCS108
00305      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)   VALUE +0     MBKCS108
00306                                                      COMP SYNC.   MBKCS108
00307      05  PV-RESP                        PIC S9(04)   VALUE +0        CL207
00308                                                      COMP SYNC.      CL206
00309      05  PV-ITEM                        PIC S9(04)   VALUE +0        CL207
00310                                                      COMP SYNC.      CL207
00311      05  PV-SC-ITEM                     PIC S9(04)   VALUE +0        CL206
00312                                                      COMP SYNC.      CL206
00313      05  PV-SAVE-ITEM                   PIC S9(04)   VALUE +0        CL206
00314                                                      COMP SYNC.      CL206
00315      05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.   CL*74
00316      05  PV-DISPLAY-7                   PIC ZZZZZZ9.                 CL178
00317      05  PV-DB2-DATE.                                                CL*66
00318          10  PV-DB2-CENTURY             PIC X(02)    VALUE SPACES.   CL*66
00319          10  PV-DB2-YEAR                PIC X(02)    VALUE SPACES.   CL*66
00320          10  PV-DB2-DASH-1              PIC X(01)    VALUE SPACES.   CL*66
00321          10  PV-DB2-MONTH               PIC X(02)    VALUE SPACES.   CL*67
00322          10  PV-DB2-DASH-2              PIC X(01)    VALUE SPACES.   CL*66
00323          10  PV-DB2-DAY                 PIC X(02)    VALUE SPACES.   CL*66
00324                                                                      CL*66
00325      05  PV-PANEL-DATE.                                              CL*67
00326          10  PV-PANEL-MONTH             PIC X(02)    VALUE SPACES.   CL*66
00327          10  PV-PANEL-SLASH-1           PIC X(01)    VALUE '/'.      CL*66
00328          10  PV-PANEL-DAY               PIC X(02)    VALUE SPACES.   CL*66
00329          10  PV-PANEL-SLASH-2           PIC X(01)    VALUE '/'.      CL*66
00330          10  PV-PANEL-YEAR              PIC X(02)    VALUE SPACES.   CL*66
00331                                                                      CL*66
00332      05  PV-MSG-NUMBER-X                PIC X(05)    VALUE SPACES.   CL**7
00333      05  PV-GMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00334      05  PV-DMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00335      05  PV-DPT-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00336      05  PV-MCL-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00337      05  PV-SCL-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00338      05  PV-GMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00339      05  PV-DMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00340      05  PV-DPT-NBR                     PIC X(03)    VALUE SPACES.   CL*81
00341      05  PV-MCL-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00342      05  PV-SCL-NBR                     PIC X(03)    VALUE SPACES.   CL*81
00343      05  PV-HOLD-KEY-DATA.                                           CL206
00344          10  PV-HK-ITEM-GMA             PIC X(02).                   CL206
00345          10  PV-HK-ITEM-DMA             PIC X(02).                   CL206
00346          10  PV-HK-ITEM-DEPT            PIC X(03).                   CL206
00347          10  PV-HK-ITEM-MCLASS          PIC X(02).                   CL206
00348          10  PV-HK-ITEM-SCLASS          PIC X(03).                   CL206
00349      05  PV-QUEUE-NAME.                                              CL206
00350          10  PV-TERM-ID                 PIC X(4).                    CL206
00351          10  PV-TASK-NUMBER             PIC S9(7)      COMP-3.       CL206
00352                                                                      CL206
00353  01  PV-TEMP-WORK-AREA.                                              CL206
00354          10  PV-TW-BLOCK-MODIFIED-SW               PIC X(01).        CL206
00355          10  PV-TW-ITEM-ARRAY.                                       CL206
00356              15  PV-TW-ITEM-ENTRIES OCCURS 18 TIMES                  CL207
00357                  INDEXED BY PV-TW-IDX.                               CL207
00358                  20  PV-TW-LIST-ITEM-NUMBER        PIC S9(9)         CL206
00359                                                        COMP-3.       CL206
00360                  20  PV-TW-SELECTED-SW             PIC X(01).        CL206
00361                      88  PV-TW-SELECT                  VALUE 'S'.    CL206
00362                      88  PV-TW-DESELECT                VALUE ' '.    CL206
00363                  20  PV-TW-KEY-DATA.                                 CL206
00364                      25  PV-TW-ITEM-GMA            PIC X(02).        CL206
00365                      25  PV-TW-ITEM-DMA            PIC X(02).        CL206
00366                      25  PV-TW-ITEM-DEPT           PIC X(03).        CL206
00367                      25  PV-TW-ITEM-MCLASS         PIC X(02).        CL206
00368                      25  PV-TW-ITEM-SCLASS         PIC X(03).        CL206
00369                  20  PV-TW-ITEM-DESC               PIC X(25).        CL206
00370                  20  PV-TW-ITEM-POSDESC            PIC X(17).        CL206
00371                  20  PV-TW-ITEM-TAXD               PIC X(01).        CL206
00372                  20  PV-TW-ITEM-ACTION             PIC X(07).        CL206
00373                  20  PV-TW-COLOR                   PIC X(01).        CL212
00374                                                                      CL206
00375  EJECT                                                            MBKCS108
00376 ***************************************************************** MBKCS108
00377 *    STANDARD COMMAREA.                                           MBKCS108
00378 ***************************************************************** MBKCS108
00379                                                                   MBKCS108
00380      COPY DPWS020.                                                   CL207
00381      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                MBKCS108
00382                                                                   MBKCS108
00383 ***************************************************************** MBKCS108
00384 *    INTER-APPLICATION COMMAREA.                                  MBKCS108
00385 ***************************************************************** MBKCS108
00386                                                                   MBKCS108
00387          10  INTER-APPL-COMM-AREA       PIC X(200).               MBKCS108
00388      COPY MBWS005.                                                   CL240
00389                                                                      CL**4
00390 ***************************************************************** MBKCS108
00391 *    SPECIFIC COMMMAREA FOR MBKCS002.                                CL*15
00392 ***************************************************************** MBKCS108
00393                                                                   MBKCS108
00394      COPY MBWS009.                                                   CL240
00395                                                                      CL*16
00396                                                                   MBKCS108
00397 *****************************************************************    CL206
00398 *    WORK AREA FOR THE LIST RECORDS PASSED TO 109.                   CL206
00399 *****************************************************************    CL206
00400  01  SC-LIST-RECORD.                                                 CL206
00401      COPY MBWS003.                                                   CL240
00402                                                                      CL206
00403                                                                      CL206
00404 ***************************************************************** MBKCS108
00405 *    ABEND PROCESSING COPYBOOK.                                   MBKCS108
00406 ***************************************************************** MBKCS108
00407                                                                   MBKCS108
00408      COPY DPWS013.                                                MBKCS108
00409                                                                   MBKCS108
00410 ***************************************************************** MBKCS108
00411 *    ATTRIBUTE SETTINGS COPYBOOK.                                 MBKCS108
00412 ***************************************************************** MBKCS108
00413                                                                   MBKCS108
00414      COPY DPWS015.                                                MBKCS108
00415                                                                   MBKCS108
00416 ***************************************************************** MBKCS108
00417 *    CURRENT DATE AND TIME COPYBOOK.                              MBKCS108
00418 ***************************************************************** MBKCS108
00419                                                                   MBKCS108
00420      COPY DPWS017.                                                MBKCS108
00421                                                                   MBKCS108
00422 ***************************************************************** MBKCS108
00423 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          MBKCS108
00424 ***************************************************************** MBKCS108
00425                                                                   MBKCS108
00426      COPY DPWS009.                                                MBKCS108
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00428 ***************************************************************** MBKCS108
00429 *    FUNCTION KEYS COPYBOOK.                                      MBKCS108
00430 ***************************************************************** MBKCS108
00431                                                                   MBKCS108
00432      COPY DPWS016.                                                MBKCS108
00433                                                                   MBKCS108
00434 ***************************************************************** MBKCS108
00435 *    DPKUT045 COPYBOOK.                                           MBKCS108
00436 *                                                                 MBKCS108
00437 *    DATA AREA NECESSARY TO INTERACT WITH SUB-ROUTINE DPKUT045    MBKCS108
00438 *                                                                 MBKCS108
00439 *    DPKUT045 IS A UTILITY ROUTINE THAT CONVERTS THE EIBAID       MBKCS108
00440 *    CHARACTER RECEIVED FROM THE TERMINAL INTO A INTEGER VALUE    MBKCS108
00441 *    FROM 1 TO 29. THIS INTEGER VALUE IS STORED IN DP020-SRC-AID  MBKCS108
00442 *    AND IS USED TO INTERROGATE THE DP020-FUNCTION-KEY-ARRAY      MBKCS108
00443 *    TABLE IN THE STANDARD COMMAREA TO DETERMINE WHAT TO DO NEXT. MBKCS108
00444 *    IN ADDITION DPKUT045 WILL PARSE THE SCREEN INPUT COMMAND     MBKCS108
00445 *    FIELD AND CONVERT RECOGNIZED SCREEN MANIPULATION COMMANDS    MBKCS108
00446 *    INTO THE DP020-SRC-AID EQUIVALENT.                           MBKCS108
00447 ***************************************************************** MBKCS108
00448                                                                   MBKCS108
00449      COPY DPWS045.                                                MBKCS108
00450                                                                   MBKCS108
00451 ***************************************************************** MBKCS108
00452 *    NUMERIC EDIT LAYOUT.                                         MBKCS108
00453 ***************************************************************** MBKCS108
00454                                                                   MBKCS108
00455      COPY DPWS010I.                                               MBKCS108
00456                                                                   MBKCS108
00457 *****************************************************************    CL**7
00458 *    SYSTEM MESSAGES RECORD LAYOUT                                   CL**7
00459 *****************************************************************    CL**7
00460                                                                      CL**7
00461      COPY DPRD507I.                                                  CL**7
00462                                                                      CL**7
00463 ***************************************************************** MBKCS108
00464 *    STANDARD ATTENTION IDENTIFIER LIST                           MBKCS108
00465 ***************************************************************** MBKCS108
00466                                                                   MBKCS108
00467      COPY DFHAID.                                                 MBKCS108
00468                                                                   MBKCS108
00469 ***************************************************************** MBKCS108
00470 *    MAP LAYOUT                                                   MBKCS108
00471 ***************************************************************** MBKCS108
00472                                                                   MBKCS108
00473      COPY MBKM108.                                                   CL183
00474                                                                   MBKCS108
00475      COPY MBWS010.                                                   CL241
00476                                                                   MBKCS108
00477  EJECT                                                            MBKCS108
00478 *****************************************************************    CL*15
00479 *    DB2 COMMUNICATION AREA                                          CL*15
00480 *****************************************************************    CL*15
00481                                                                      CL*15
00482      EXEC SQL                                                        CL*15
00483          INCLUDE SQLCA                                               CL*15
00484      END-EXEC.                                                       CL*15
00485                                                                      CL*15
00486 *****************************************************************    CL*15
00487 *    MERCHANDISE AREA TABLE - TMDSEAR                                CL*15
00488 *****************************************************************    CL*15
00489                                                                      CL*15
00490      EXEC SQL                                                        CL*15
00491          INCLUDE TMDSEAR                                             CL*15
00492      END-EXEC.                                                       CL*15
00493                                                                      CL*15
00494 *****************************************************************    CL184
00495 *    MERCHANDISE AREA TABLE - TMDSECL                                CL187
00496 *****************************************************************    CL184
00497                                                                      CL184
00498      EXEC SQL                                                        CL184
00499          INCLUDE TMDSECL                                             CL184
00500      END-EXEC.                                                       CL184
00501                                                                      CL184
00502 *****************************************************************    CL165
00503 *    DECLARE FOR THE MERCH-CURSOR                                    CL165
00504 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00505 *       IS FOR ALL GMAS, DMAS, DEPARTMENTS, MAJOR CLASSES,           CL165
00506 *       AND SUB CLASSES                                              CL165
00507 *****************************************************************    CL165
00508                                                                      CL165
00509      EXEC SQL                                                        CL*90
00510          DECLARE MERCH-CURSOR CURSOR FOR                             CL*90
00511              SELECT  OPERCO_NBR                                      CL*90
00512                    , GMA_NBR                                         CL*90
00513                    , DMA_NBR                                         CL*90
00514                    , DEPT_NBR                                        CL*90
00515                    , MAJ_CL_NBR                                      CL*90
00516                    , SUB_CL_NBR                                      CL*90
00517                    , MDSELV_CDE                                      CL*90
00518                    , CHG_DTE                                         CL*90
00519                    , CRE_DTE                                         CL*90
00520                    , STRT_DTE                                        CL*90
00521                    , END_DTE                                         CL*90
00522                    , GEN_MDSE_TYP_CDE                                CL*90
00523                    , MDSEAR_DESC                                     CL*90
00524                    , BYR_NBR                                         CL*90
00525                    , LIST_SEQ_NBR                                    CL107
00526                    , POS_DESC                                        CL187
00527                    , TAX_DFLT_IND                                    CL187
00528                FROM  TMDSEAR A                                       CL187
00529                    , TMDSECL B                                       CL187
00530               WHERE  ((OPERCO_NBR    = :PC-OPERCO-NBR)               CL215
00531                 AND   (MDSELV_CDE    = :PC-SCL-LITERAL)              CL215
00532                 AND   (LIST_SEQ_NBR  > :MB009-SAVE-SEQ-NBR)          CL215
00533                 AND   (END_DTE       > :MB009-DB2-HISTORY-DATE)      CL220
00534                 AND   (A.MDSEAR_DKEY = B.MDSEAR_DKEY))               CL215
00535            ORDER BY  OPERCO_NBR                                      CL*90
00536                    , GMA_NBR                                         CL*90
00537                    , DMA_NBR                                         CL*90
00538                    , DEPT_NBR                                        CL*90
00539                    , MAJ_CL_NBR                                      CL*90
00540                    , SUB_CL_NBR                                      CL*90
00541      END-EXEC.                                                       CL*90
00542                                                                      CL165
00543 *****************************************************************    CL165
00544 *    DECLARE FOR THE DPT-CURSOR                                      CL165
00545 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00546 *       IS FOR A SPECIFIC DPT, AND ALL LEVELS,(DPT AND LOWER),       CL165
00547 *       SELECTED WITHIN THE REQUESTED DPT                            CL165
00548 *****************************************************************    CL165
00549                                                                      CL129
00550      EXEC SQL                                                        CL129
00551          DECLARE DPT-CURSOR CURSOR FOR                               CL129
00552              SELECT  OPERCO_NBR                                      CL129
00553                    , GMA_NBR                                         CL129
00554                    , DMA_NBR                                         CL129
00555                    , DEPT_NBR                                        CL129
00556                    , MAJ_CL_NBR                                      CL129
00557                    , SUB_CL_NBR                                      CL129
00558                    , MDSELV_CDE                                      CL129
00559                    , CHG_DTE                                         CL129
00560                    , CRE_DTE                                         CL129
00561                    , STRT_DTE                                        CL129
00562                    , END_DTE                                         CL129
00563                    , GEN_MDSE_TYP_CDE                                CL129
00564                    , MDSEAR_DESC                                     CL129
00565                    , BYR_NBR                                         CL129
00566                    , LIST_SEQ_NBR                                    CL129
00567                    , POS_DESC                                        CL187
00568                    , TAX_DFLT_IND                                    CL187
00569                FROM  TMDSEAR A                                       CL187
00570                    , TMDSECL B                                       CL187
00571               WHERE  ((OPERCO_NBR    = :PC-OPERCO-NBR)               CL215
00572                 AND   (MDSELV_CDE    = :PC-SCL-LITERAL)              CL215
00573                 AND   (LIST_SEQ_NBR  > :MB009-SAVE-SEQ-NBR)          CL215
00574                 AND   (END_DTE       > :MB009-DB2-HISTORY-DATE)      CL220
00575                 AND   (DEPT_NBR      = :MB009-SAVE-DPT-NBR)          CL215
00576                 AND   (A.MDSEAR_DKEY = B.MDSEAR_DKEY))               CL215
00577            ORDER BY  OPERCO_NBR                                      CL129
00578                    , GMA_NBR                                         CL129
00579                    , DMA_NBR                                         CL129
00580                    , DEPT_NBR                                        CL129
00581                    , MAJ_CL_NBR                                      CL129
00582                    , SUB_CL_NBR                                      CL129
00583      END-EXEC.                                                       CL129
00584                                                                      CL165
00585 *****************************************************************    CL165
00586 *    DECLARE FOR THE MCL-CURSOR                                      CL165
00587 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00588 *       IS FOR A SPECIFIC MCL, WITHIN A DEPARTMENT AND ALL           CL165
00589 *       LEVELS,(MCL AND LOWER), SELECTED WITHIN THE REQUESTED MCL    CL165
00590 *****************************************************************    CL165
00591                                                                      CL*97
00592      EXEC SQL                                                        CL135
00593          DECLARE MCL-CURSOR CURSOR FOR                               CL135
00594              SELECT  OPERCO_NBR                                      CL135
00595                    , GMA_NBR                                         CL135
00596                    , DMA_NBR                                         CL135
00597                    , DEPT_NBR                                        CL135
00598                    , MAJ_CL_NBR                                      CL135
00599                    , SUB_CL_NBR                                      CL135
00600                    , MDSELV_CDE                                      CL135
00601                    , CHG_DTE                                         CL135
00602                    , CRE_DTE                                         CL135
00603                    , STRT_DTE                                        CL135
00604                    , END_DTE                                         CL135
00605                    , GEN_MDSE_TYP_CDE                                CL135
00606                    , MDSEAR_DESC                                     CL135
00607                    , BYR_NBR                                         CL135
00608                    , LIST_SEQ_NBR                                    CL135
00609                    , POS_DESC                                        CL187
00610                    , TAX_DFLT_IND                                    CL187
00611                FROM  TMDSEAR A                                       CL187
00612                    , TMDSECL B                                       CL187
00613               WHERE  ((OPERCO_NBR    = :PC-OPERCO-NBR)               CL216
00614                 AND   (MDSELV_CDE    = :PC-SCL-LITERAL)              CL216
00615                 AND   (LIST_SEQ_NBR  > :MB009-SAVE-SEQ-NBR)          CL216
00616                 AND   (END_DTE       > :MB009-DB2-HISTORY-DATE)      CL220
00617                 AND   (DEPT_NBR      = :MB009-SAVE-DPT-NBR)          CL216
00618                 AND   (MAJ_CL_NBR    = :MB009-SAVE-MCL-NBR)          CL216
00619                 AND   (A.MDSEAR_DKEY = B.MDSEAR_DKEY))               CL216
00620            ORDER BY  OPERCO_NBR                                      CL216
00621                    , GMA_NBR                                         CL135
00622                    , DMA_NBR                                         CL135
00623                    , DEPT_NBR                                        CL135
00624                    , MAJ_CL_NBR                                      CL135
00625                    , SUB_CL_NBR                                      CL135
00626      END-EXEC.                                                       CL135
00627                                                                      CL165
00628 *****************************************************************    CL165
00629 *    DECLARE FOR THE SCL-CURSOR                                      CL165
00630 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00631 *       IS FOR A SPECIFIC SCL, WITHIN A DEPARTMENT AND MAJOR CLASS   CL165
00632 *****************************************************************    CL165
00633                                                                      CL135
00634      EXEC SQL                                                        CL148
00635          DECLARE SCL-CURSOR CURSOR FOR                               CL148
00636              SELECT  OPERCO_NBR                                      CL148
00637                    , GMA_NBR                                         CL148
00638                    , DMA_NBR                                         CL148
00639                    , DEPT_NBR                                        CL148
00640                    , MAJ_CL_NBR                                      CL148
00641                    , SUB_CL_NBR                                      CL148
00642                    , MDSELV_CDE                                      CL148
00643                    , CHG_DTE                                         CL148
00644                    , CRE_DTE                                         CL148
00645                    , STRT_DTE                                        CL148
00646                    , END_DTE                                         CL148
00647                    , GEN_MDSE_TYP_CDE                                CL148
00648                    , MDSEAR_DESC                                     CL148
00649                    , BYR_NBR                                         CL148
00650                    , LIST_SEQ_NBR                                    CL148
00651                    , POS_DESC                                        CL187
00652                    , TAX_DFLT_IND                                    CL187
00653                FROM  TMDSEAR A                                       CL187
00654                    , TMDSECL B                                       CL187
00655               WHERE  ((OPERCO_NBR    = :PC-OPERCO-NBR)               CL223
00656                 AND   (MDSELV_CDE    = :PC-SCL-LITERAL)              CL223
00657                 AND   (LIST_SEQ_NBR  > :MB009-SAVE-SEQ-NBR)          CL223
00658                 AND   (END_DTE       > :MB009-DB2-HISTORY-DATE)      CL223
00659                 AND   (DEPT_NBR      = :MB009-SAVE-DPT-NBR)          CL223
00660                 AND   (MAJ_CL_NBR    = :MB009-SAVE-MCL-NBR)          CL223
00661                 AND   (SUB_CL_NBR    = :MB009-SAVE-SCL-NBR)          CL223
00662                 AND   (A.MDSEAR_DKEY = B.MDSEAR_DKEY))               CL223
00663            ORDER BY  OPERCO_NBR                                      CL223
00664                    , GMA_NBR                                         CL223
00665                    , DMA_NBR                                         CL223
00666                    , DEPT_NBR                                        CL223
00667                    , MAJ_CL_NBR                                      CL223
00668                    , SUB_CL_NBR                                      CL223
00669      END-EXEC.                                                       CL223
00670                                                                      CL223
00671  LINKAGE SECTION.                                                 MBKCS108
00672                                                                   MBKCS108
00673  01  DFHCOMMAREA.                                                 MBKCS108
00674      05  FILLER                         OCCURS  1 TO 4072 TIMES   MBKCS108
00675                                         DEPENDING ON EIBCALEN.    MBKCS108
00676          10  FILLER                     PIC X.                    MBKCS108
00677                                                                   MBKCS108
00678 *                                                                 MBKCS108
00679 *                                                                 MBKCS108
00680 *                                                                 MBKCS108
00681  PROCEDURE DIVISION.                                              MBKCS108
00682                                                                   MBKCS108
00683                                                                   MBKCS108
00684  A100-MAIN-MODULE.                                                MBKCS108
00685                                                                   MBKCS108
00686 ******************************************************************MBKCS108
00687 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           MBKCS108
00688 *                                                                 MBKCS108
00689 ******************************************************************MBKCS108
00690                                                                   MBKCS108
00691      PERFORM DP015-0000-CHECK-COMMAREA.                           MBKCS108
00692                                                                   MBKCS108
00693 ******************************************************************MBKCS108
00694 *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS  MBKCS108
00695 *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER     MBKCS108
00696 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS      MBKCS108
00697 *    PROGRAM WILL RETURN TO CICS.                                 MBKCS108
00698 ******************************************************************MBKCS108
00699                                                                   MBKCS108
00700      IF DP020-SRC-RETURN-W-TRAN-ID                                MBKCS108
00701          PERFORM B100-CONTROL-PROCESSING                          MBKCS108
00702      END-IF.                                                      MBKCS108
00703                                                                   MBKCS108
00704      PERFORM DP003-0000-RETURN-TO-CICS.                           MBKCS108
00705  EJECT                                                            MBKCS108
00706  B100-CONTROL-PROCESSING.                                         MBKCS108
00707                                                                   MBKCS108
00708 ******************************************************************MBKCS108
00709 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT    MBKCS108
00710 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    MBKCS108
00711 *                                                                 MBKCS108
00712 *    PERFORMED BY A100-MAIN-MODULE                                   CL*15
00713 ******************************************************************MBKCS108
00714                                                                   MBKCS108
00715      MOVE LENGTH OF MB009-BLOCK-ENTRIES(1)                           CL213
00716                                TO  PC-BLOCK-LENGTH.                  CL213
00717      EVALUATE TRUE                                                MBKCS108
00718                                                                   MBKCS108
00719          WHEN DP020-NEXT-ACT-INITIAL                                 CL*22
00720              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS108
00721                                                                   MBKCS108
00722              PERFORM C100-INITIALIZE-COMMAREA                        CL**3
00723              PERFORM C200-SAVE-INITIAL-INTERAPPL                     CL**3
00724              IF PS-INTER-APPL-VALID                                  CL227
00725                  PERFORM D100-SETUP-INIT-PANEL                       CL227
00726                  IF PS-NO-SELECTIONS-FOUND                           CL227
00727                      MOVE DP016-PF3             TO DP020-SRC-AID     CL227
00728                      PERFORM DP015-1000-START-NAVIGATOR              CL227
00729                  END-IF                                              CL227
00730              ELSE                                                    CL227
00731                  PERFORM Z400-RETURN-TO-PREV-TRAN                    CL227
00732              END-IF                                                  CL227
00733                                                                      CL157
00734          WHEN DP020-NEXT-ACT-READ-MAP                             MBKCS108
00735              PERFORM C300-PROCESS-PANEL                           MBKCS108
00736                                                                   MBKCS108
00737          WHEN DP020-NEXT-ACT-RETURN                               MBKCS108
00738              SET PS-SEND-MAP-N-DATA-FRSET   TO TRUE                  CL165
00739                                                                   MBKCS108
00740              PERFORM C400-UPDATE-INITIAL-INTERAPPL                   CL**3
00741              PERFORM C500-REFRESH-PANEL-FROM-TEMP                    CL**3
00742                                                                   MBKCS108
00743          WHEN DP020-NEXT-ACT-ERROR                                MBKCS108
00744                                                                   MBKCS108
00745 ******************************************************************MBKCS108
00746 *    THE FOLLOWING CODE HAS BEEN INSERTED IN ORDER TO PRESERVE    MBKCS108
00747 *    THE ERROR MESSAGE BEING RETURNED FROM THE NAVIGATOR. SINCE   MBKCS108
00748 *    THE ERROR BEING RETURNED WILL TYPICALLY BE A SECURITY        MBKCS108
00749 *    VIOLATION OR THE UNAVAILABILITY OF THE REQUESTED TRANSACTION MBKCS108
00750 *    THIS CODE ALSO CHECKS TO SEE HOW  THE NAVIGATION REQUEST WAS MBKCS108
00751 *    INVOKED. IF DP020-NAV-MNEMONIC FIELD IN THE STANDARD         MBKCS108
00752 *    COMMAREA IS SPACES, THE NAVIGATION REQUEST WAS VIA A FUNCTIONMBKCS108
00753 *    KEY. IF NOT, IT WAS VIA A COMMAND ENTERED IN THE COMMAND LINEMBKCS108
00754 *    COMMAND FIELD AND THIS LOGIC SETS THE ATTRIBUTE FOR THIS FIELMBKCS108
00755 *    TO RED, REVERSE VIDEO.                                       MBKCS108
00756 ******************************************************************MBKCS108
00757                                                                   MBKCS108
00758              MOVE DP020-MSG-MESSAGE         TO PV-MESSAGE            CL165
00759                                                                   MBKCS108
00760              IF DP020-NAV-MNEMONIC = SPACES                       MBKCS108
00761                  MOVE SPACES                TO ANAVCMDO              CL165
00762                  MOVE DP015-GREEN           TO ANAVCMDC              CL165
00763                  MOVE DP015-UNDERLINE       TO ANAVCMDH              CL165
00764              ELSE                                                 MBKCS108
00765                  MOVE DP015-RED             TO ANAVCMDC              CL165
00766                  MOVE DP015-REVERSE         TO ANAVCMDH              CL165
00767              END-IF                                               MBKCS108
00768                                                                   MBKCS108
00769              PERFORM E100-EDIT-SELECTIONS                         MBKCS108
00770                                                                   MBKCS108
00771              MOVE -1                        TO ANAVCMDL              CL165
00772              MOVE PV-MESSAGE                TO DP020-MSG-MESSAGE     CL165
00773                                                                   MBKCS108
00774              SET DP020-MSG-FATAL            TO TRUE                  CL165
00775              SET PS-SEND-DATA-ONLY-FRSET    TO TRUE                  CL165
00776              SET PS-SAME-ITEMS-DISPLAYED    TO TRUE                  CL165
00777              SET PS-CURSOR-POSITIONED       TO TRUE                  CL165
00778                                                                   MBKCS108
00779              PERFORM G100-DISPLAY-PANEL                           MBKCS108
00780                                                                   MBKCS108
00781          WHEN OTHER                                               MBKCS108
00782              SET DP013-LOGIC-ABEND          TO TRUE                  CL165
00783              SET DP013-NO-ROLLBACK          TO TRUE                  CL165
00784                                                                   MBKCS108
00785              MOVE 'B100-CONTROL-PROCESSING' TO                    MBKCS108
00786                   DP013-PARAGRAPH                                 MBKCS108
00787              MOVE INVALID-NEXT-ACT-MSG      TO                       CL**3
00788                   DP013-MESSAGE-TEXT(1)                           MBKCS108
00789                                                                   MBKCS108
00790              PERFORM DP013-0000-PROCESS-ABEND                     MBKCS108
00791                                                                   MBKCS108
00792      END-EVALUATE.                                                MBKCS108
00793  EJECT                                                            MBKCS108
00794  C100-INITIALIZE-COMMAREA.                                           CL**3
00795                                                                   MBKCS108
00796 ******************************************************************MBKCS108
00797 *    INITIALIZE THE APPLICATION SPECIFIC AND INTER-APPLICATION    MBKCS108
00798 *    PLACE WHATEVER INITIALIZATION LOGIC IS NECESSARY.            MBKCS108
00799 *                                                                 MBKCS108
00800 ******************************************************************MBKCS108
00801                                                                      CL*17
00802      INITIALIZE MB009-SPECIFIC-COMMAREA.                             CL179
00803                                                                      CL*17
00804  EJECT                                                            MBKCS108
00805  C200-SAVE-INITIAL-INTERAPPL.                                        CL**3
00806                                                                      CL**3
00807 ******************************************************************   CL**3
00808 *                                                                    CL**3
00809 ******************************************************************   CL**3
00810                                                                      CL165
00811      IF DP020-MB-SUB-CLASS-SELECT                                    CL225
00812          MOVE MB005-CO-NUMBER       TO MB009-CO-NUMBER               CL184
00813          MOVE MB005-DEPT-NUMBER     TO MB009-DEPT-NUMBER             CL184
00814          MOVE MB005-MCLASS-NUMBER   TO MB009-MCLASS-NUMBER           CL184
00815          MOVE MB005-SCLASS-NUMBER   TO MB009-SCLASS-NUMBER           CL184
00816          MOVE MB005-HISTORY-FLAG    TO MB009-HISTORY-FLAG            CL214
00817 *        MOVE LOW-VALUE             TO MB005-INTER-APPL-COMMAREA     CL234
00818          SET PS-INTER-APPL-VALID    TO TRUE                          CL227
00819      ELSE                                                            CL225
00820          MOVE PC-TSYMSG-99901       TO DP020-MSG-NUMBER              CL225
00821          SET DP020-MSG-FATAL        TO TRUE                          CL225
00822          SET PS-INTER-APPL-INVALID  TO TRUE                          CL227
00823          MOVE -1                    TO ANAVCMDL                      CL225
00824      END-IF.                                                         CL165
00825                                                                      CL*59
00826  EJECT                                                               CL**3
00827  C300-PROCESS-PANEL.                                              MBKCS108
00828                                                                   MBKCS108
00829 ******************************************************************MBKCS108
00830 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED  MBKCS108
00831 *    ON WHAT FUNCTION KEY WAS SELECTED.                           MBKCS108
00832 *                                                                 MBKCS108
00833 ******************************************************************MBKCS108
00834                                                                   MBKCS108
00835      PERFORM DP007-0000-GET-PANEL.                                MBKCS108
00836                                                                      CL162
00837      SET PS-SEND-DATA-ONLY-FRSET             TO TRUE.                CL165
00838                                                                   MBKCS108
00839      EVALUATE TRUE                                                MBKCS108
00840                                                                   MBKCS108
00841          WHEN DP020-SRC-AID = DP016-CLEAR                         MBKCS108
00842              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
00843              SET PS-SAME-ITEMS-DISPLAYED     TO TRUE                 CL165
00844                                                                   MBKCS108
00845              PERFORM E100-EDIT-SELECTIONS                         MBKCS108
00846              PERFORM G100-DISPLAY-PANEL                           MBKCS108
00847                                                                   MBKCS108
00848          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL224
00849              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
00850              PERFORM Z050-DELETE-TEMP-STORAGE                     MBKCS108
00851              INITIALIZE ANAVCMDO ANAVKEYO                            CL224
00852                         DP020-NAV-KEY-DATA                           CL224
00853              PERFORM D100-SETUP-INIT-PANEL                        MBKCS108
00854                                                                   MBKCS108
00855          WHEN DP020-SRC-AID = DP016-PA2                           MBKCS108
00856              PERFORM DP009-0000-LOAD-FK-LINES                     MBKCS108
00857              PERFORM DP017-0000-GET-CURR-DATE-TIME                MBKCS108
00858                                                                   MBKCS108
00859              SET PS-SEND-DATA-ONLY           TO TRUE                 CL165
00860                                                                   MBKCS108
00861              MOVE -1 TO ANAVCMDL                                  MBKCS108
00862                                                                   MBKCS108
00863              PERFORM DP008-0000-WRITE-PANEL                       MBKCS108
00864                                                                   MBKCS108
00865          WHEN OTHER                                               MBKCS108
00866              SET PS-INVALID-SELECTIONS TO TRUE                       CL*40
00867              PERFORM D200-RECORD-SELECTIONS                       MBKCS108
00868                                                                   MBKCS108
00869              IF PS-VALID-SELECTIONS                               MBKCS108
00870                  PERFORM D300-CHECK-FUNCTION-KEY                  MBKCS108
00871              ELSE                                                 MBKCS108
00872                  SET PS-SAME-ITEMS-DISPLAYED TO TRUE                 CL*71
00873                  PERFORM G100-DISPLAY-PANEL                       MBKCS108
00874              END-IF                                               MBKCS108
00875                                                                   MBKCS108
00876      END-EVALUATE.                                                MBKCS108
00877  EJECT                                                            MBKCS108
00878  C400-UPDATE-INITIAL-INTERAPPL.                                      CL**3
00879                                                                      CL**3
00880 ******************************************************************   CL**3
00881 *    WHENEVER THIS TRANSACTION HAS UPDATED VALUES THAT WERE PASSED   CL**3
00882 *    TO IT THRU THE INTER APPLICATION COMMAREA, THIS PARAGRAPH       CL**3
00883 *    IS INVOKED AND IT UPDATES THE FIELDS IN THE TWO HUNDRED BYTE    CL**3
00884 *    AREA IN THE APPLICATIONS SPECIFIC COMMAREA THAT WAS RESERVED    CL**3
00885 *    FOR THE INITIAL VALUE OF THE ORIGINAL TWO HUNDRED BYTES SENT    CL**3
00886 *    TO THIS TRANSACTION, THIS ALLOWS THE TRANSACTION THAT INVOKED   CL**3
00887 *    THIS TRANSACTION TO RECEIVE UPDATES THRU THE INTER APPL COMM    CL**3
00888 *    AREA WHEN THE USER PRESSES F3 - END ON THIS SCREEN. IN          CL**3
00889 *    ADDITION THIS PARAGRAPH WILL BE INVOKED WHEN THIS TRANSACTION   CL**3
00890 *    IS INVOKED VIA AN F3 - END AID KEY NAVIGATION. CODE MUST BE     CL**3
00891 *    INSERTED IN THIS PARAGRAPH TO HANDLE ANY UPDATES PASSED BACK    CL**3
00892 *    TO THIS TRANSACTION AS WELL. THIS LOGIC MAY BECOME QUITE OMPL   CL**3
00893 *    COMPLEX. FOR AN EXAMPLE OF HOW A SPECIFIC PROGRAM IS DOING      CL**3
00894 *    THIS LOGIC SEE DPKCS019.                                        CL**3
00895 *                                                                    CL**3
00896 *    THIS PARAGRAPH MUST BE CODED IF YOU PLAN ON USING ANY           CL**3
00897 *    DATA IN THE INTER APPLICATIONS DATA AREA OF THE COMMAREA.       CL**3
00898 *    A TWO HUNDRED BYTE AREA IN THE APPLICATIONS SPECIFIC            CL**3
00899 *    COMMAREA MUST BE CREATED TO HOLD THE INITIAL VALUE OF THE       CL**3
00900 *    TWO HUNDRED BYTE  INTER APPLICATION COMMAREA SENT TO THIS       CL**3
00901 *    TRANSACTION.                                                    CL**3
00902 *                                                                    CL**3
00903 *    IF THIS TRANSACTION WILL NOT USE THE INTER APPL COMMAREA        CL**3
00904 *    AT ALL, THIS PARAGRAPH SHOULD MOVE BE REMOVED AS WELL AS        CL**3
00905 *    ANY REFERENCE TO IT. YOU ALSO DO NOT NEED TO DEFINE A 200       CL**3
00906 *    BYTE AREA IN THE APPL SPECIFIC COMMAREA TO SAVE THE INITIAL     CL**3
00907 *    INTER-APPL-COMMAREA.                                            CL**3
00908 *                                                                    CL**3
00909 ******************************************************************   CL**3
00910                                                                      CL*19
00911 *    MOVE LOW-VALUES TO MB005-INTER-APPL-COMMAREA.                   CL206
00912                                                                      CL*19
00913  EJECT                                                               CL**3
00914  C500-REFRESH-PANEL-FROM-TEMP.                                       CL**3
00915                                                                   MBKCS108
00916 ******************************************************************MBKCS108
00917 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS108
00918 *    SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.       MBKCS108
00919 *    EDIT THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL  MBKCS108
00920 *    TO THE USER.                                                 MBKCS108
00921 *                                                                 MBKCS108
00922 ******************************************************************MBKCS108
00923 *                                                                    CL206
00924      IF  WE-MUST-UPDATE-TSQ                                          CL206
00925          MOVE ZERO             TO  PV-SAVE-ITEM                      CL206
00926                                    PV-SC-ITEM                        CL206
00927          SET NOT-END-OF-ITEMS  TO  TRUE                              CL206
00928          PERFORM C600-UPDATE-TSQ                                     CL206
00929                  UNTIL END-OF-ITEMS                                  CL206
00930          PERFORM X900-DELETE-LIST-QUEUE                              CL236
00931          MOVE SPACES           TO  MB005-LIST-QUEUE-NAME             CL236
00932      END-IF.                                                         CL206
00933 *                                                                    CL206
00934      PERFORM D400-REFRESH-BLOCK-ARRAY.                            MBKCS108
00935      PERFORM E100-EDIT-SELECTIONS.                                MBKCS108
00936      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL*71
00937      PERFORM G100-DISPLAY-PANEL.                                  MBKCS108
00938 *                                                                    CL206
00939  EJECT                                                               CL206
00940 *                                                                    CL206
00941 ******************************************************************   CL206
00942 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.          CL206
00943 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER      CL206
00944 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN        CL206
00945 *    THE TEMP QUEUE.                                                 CL206
00946 *                                                                    CL206
00947 *    PERFORMED BY C500                                               CL206
00948 ******************************************************************   CL206
00949 *                                                                    CL206
00950  C600-UPDATE-TSQ.                                                    CL206
00951 *                                                                    CL206
00952      ADD 1                     TO  PV-SC-ITEM.                       CL206
00953      PERFORM X600-READ-LIST-QUEUE.                                   CL206
00954      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL206
00955          SET MB003-IDX         TO  1                                 CL228
00956          PERFORM C700-PROCESS-ITEMS                                  CL206
00957              UNTIL MB003-KEY-DATA (MB003-IDX) EQUAL SPACES           CL228
00958                OR  MB003-IDX GREATER THAN MB003-MAX-ITEMS            CL228
00959          PERFORM X550-WRITE-TS-QUEUE                                 CL206
00960      ELSE                                                            CL206
00961          SET END-OF-ITEMS      TO  TRUE                              CL229
00962      END-IF.                                                         CL229
00963 *                                                                    CL206
00964  EJECT                                                               CL206
00965 *                                                                    CL206
00966 ******************************************************************   CL206
00967 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.    CL206
00968 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE    CL206
00969 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.               CL206
00970 *                                                                    CL206
00971 *    PERFORMED BY C600                                               CL206
00972 ******************************************************************   CL206
00973 *                                                                    CL206
00974  C700-PROCESS-ITEMS.                                                 CL206
00975 *                                                                    CL206
00976      COMPUTE PV-ITEM = 1 + ((MB003-ITEM (MB003-IDX) - 1)             CL228
00977                      / PC-ITEMS-PER-BLOCK).                          CL206
00978 *                                                                    CL206
00979      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                              CL206
00980          IF  PV-SAVE-ITEM NOT EQUAL ZERO                             CL206
00981              PERFORM X550-WRITE-TS-QUEUE                             CL206
00982          END-IF                                                      CL206
00983          MOVE PV-ITEM          TO  PV-SAVE-ITEM                      CL206
00984 *                                                                    CL206
00985          PERFORM X250-READ-TS-QUEUE                                  CL206
00986          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL206
00987              SET END-OF-ITEMS  TO  TRUE                              CL206
00988          END-IF                                                      CL206
00989      END-IF.                                                         CL206
00990 *                                                                    CL206
00991      IF  NOT-END-OF-ITEMS                                            CL206
00992          COMPUTE PS-SUB = (MB003-ITEM (MB003-IDX)                    CL228
00993                         - PV-TW-LIST-ITEM-NUMBER (1)) + 1            CL206
00994          SET PV-TW-DESELECT (PS-SUB)                                 CL206
00995                                TO  TRUE                              CL206
00996          SUBTRACT 1          FROM  MB005-NUMBER-PROCESSED-ITEMS      CL206
00997                                    MB009-NUMBER-OF-ITEMS-SELECTED    CL206
00998          IF  MB005-NUMBER-PROCESSED-ITEMS EQUAL ZERO                 CL206
00999              SET END-OF-ITEMS  TO  TRUE                              CL206
01000              SET MB003-IDX     TO  MB003-MAX-ITEMS                   CL228
01001          END-IF                                                      CL206
01002          SET MB003-IDX      UP BY  1                                 CL228
01003      END-IF.                                                         CL206
01004 *                                                                    CL206
01005  EJECT                                                            MBKCS108
01006  D100-SETUP-INIT-PANEL.                                           MBKCS108
01007                                                                   MBKCS108
01008 ******************************************************************MBKCS108
01009 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                         MBKCS108
01010 *    INITIALIZE ALL COUNTERS AND FLAGS.                           MBKCS108
01011 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.   MBKCS108
01012 *    DISPLAY THE PANEL TO USER.                                   MBKCS108
01013 *                                                                 MBKCS108
01014 *    MB009 - MEMBER NAME USED FOR SPECIFIC COMMAREA.                 CL179
01015 *                                                                 MBKCS108
01016 ******************************************************************MBKCS108
01017                                                                   MBKCS108
01018      MOVE +1                  TO MB009-BLOCK-SUB.                    CL179
01019                                                                   MBKCS108
01020      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS108
01021                                                                   MBKCS108
01022      ADD +1                   TO MB009-BLOCK-SUB.                    CL179
01023                                                                   MBKCS108
01024      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS108
01025                                                                   MBKCS108
01026      MOVE +1                  TO MB009-BLOCK-SUB.                    CL179
01027      MOVE +0                  TO MB009-ITEM-SUB.                     CL179
01028      MOVE +0                  TO MB009-NUMBER-OF-ITEMS-READ.         CL179
01029      MOVE +0                  TO MB009-NUMBER-OF-ITEMS-SELECTED.     CL179
01030      MOVE SPACES              TO MB009-TEMP-QUEUE-NAME.              CL179
01031      MOVE +0                  TO MB009-HIGHEST-BLOCK-WRITTEN.        CL179
01032      MOVE +0                  TO MB009-START-OF-SELECTED-RANGE.      CL179
01033      MOVE +0                  TO MB009-END-OF-SELECTED-RANGE.        CL179
01034      MOVE +0                  TO MB009-SAVE-SEQ-NBR.                 CL179
01035      MOVE PC-ITEMS-PER-PANEL  TO PV-HIGHEST-ITEM-TO-BE-READ.         CL165
01036                                                                      CL138
01037      IF MB009-NO-HISTORY                                             CL214
01038         PERFORM DP017-0000-GET-CURR-DATE-TIME                        CL219
01039         MOVE DP017-EDITED-DATE-YYYYMMDD TO MB009-DB2-HISTORY-DATE    CL219
01040                                            MB009-DB2-CURRENT-DATE    CL219
01041      ELSE                                                            CL214
01042         MOVE '1900-01-01' TO MB009-DB2-HISTORY-DATE                  CL219
01043         PERFORM DP017-0000-GET-CURR-DATE-TIME                        CL219
01044         MOVE DP017-EDITED-DATE-YYYYMMDD TO MB009-DB2-CURRENT-DATE    CL219
01045      END-IF.                                                         CL214
01046                                                                      CL219
01047                                                                      CL214
01048      PERFORM D150-SETUP-DATABASE-KEYS.                               CL214
01049                                                                      CL185
01050      PERFORM D500-OPEN-CURSOR.                                       CL137
01051                                                                      CL137
01052      SET MB009-ITEMS-LEFT-ON-DB TO TRUE.                             CL179
01053                                                                   MBKCS108
01054      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS108
01055                                                                   MBKCS108
01056      MOVE +1 TO MB009-ITEM-AT-TOP-OF-PANEL.                          CL179
01057                                                                      CL*71
01058      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL161
01059                                                                      CL*71
01060      IF PS-NO-SELECTIONS-FOUND                                       CL161
01061          CONTINUE                                                    CL161
01062      ELSE                                                            CL161
01063          PERFORM G100-DISPLAY-PANEL                                  CL157
01064      END-IF.                                                         CL157
01065  EJECT                                                            MBKCS108
01066                                                                      CL214
01067  D150-SETUP-DATABASE-KEYS.                                           CL214
01068 ******************************************************************   CL214
01069 *                                                                    CL214
01070 *                                                                    CL214
01071 *                                                                    CL214
01072 ******************************************************************   CL214
01073      SET MB009-NO-CURSOR-OPEN TO TRUE                                CL214
01074                                                                      CL214
01075      IF MB009-ALL-SCLASSES                                           CL214
01076         CONTINUE                                                     CL214
01077      ELSE                                                            CL214
01078          IF (MB009-SCLASS-NUMBER = SPACES) OR                        CL214
01079             (MB009-SCLASS-NUMBER = LOW-VALUES)                       CL214
01080              MOVE SPACES      TO MB009-SAVE-SCL-NBR                  CL214
01081          ELSE                                                        CL214
01082              IF MB009-NO-CURSOR-OPEN                                 CL214
01083                  MOVE MB009-SCLASS-NUMBER  TO MB009-SAVE-SCL-NBR     CL214
01084                  SET MB009-OPEN-SCL-CURSOR TO TRUE                   CL214
01085              END-IF                                                  CL214
01086          END-IF                                                      CL214
01087      END-IF.                                                         CL214
01088                                                                      CL214
01089      IF MB009-ALL-MCLASSES                                           CL214
01090          CONTINUE                                                    CL214
01091      ELSE                                                            CL214
01092          IF (MB009-MCLASS-NUMBER = SPACES) OR                        CL214
01093             (MB009-MCLASS-NUMBER = LOW-VALUES)                       CL214
01094              MOVE SPACES      TO MB009-SAVE-MCL-NBR                  CL214
01095          ELSE                                                        CL214
01096              MOVE MB009-MCLASS-NUMBER TO MB009-SAVE-MCL-NBR          CL214
01097              IF MB009-NO-CURSOR-OPEN                                 CL214
01098                  SET MB009-OPEN-MCL-CURSOR TO TRUE                   CL214
01099              END-IF                                                  CL214
01100          END-IF                                                      CL214
01101      END-IF.                                                         CL214
01102                                                                      CL214
01103      IF MB009-ALL-DEPARTMENTS                                        CL214
01104          CONTINUE                                                    CL214
01105      ELSE                                                            CL214
01106          IF (MB009-DEPT-NUMBER = SPACES) OR                          CL214
01107             (MB009-DEPT-NUMBER = LOW-VALUES)                         CL214
01108              MOVE SPACES        TO MB009-SAVE-DPT-NBR                CL214
01109          ELSE                                                        CL214
01110              MOVE MB009-DEPT-NUMBER TO MB009-SAVE-DPT-NBR            CL214
01111              IF MB009-NO-CURSOR-OPEN                                 CL214
01112                  SET MB009-OPEN-DPT-CURSOR TO TRUE                   CL214
01113              END-IF                                                  CL214
01114          END-IF                                                      CL214
01115      END-IF.                                                         CL214
01116                                                                      CL214
01117      IF (MB009-ALL-DEPARTMENTS AND                                   CL214
01118          MB009-ALL-MCLASSES    AND                                   CL214
01119          MB009-ALL-SCLASSES)                                         CL214
01120          SET MB009-OPEN-MERCH-CURSOR TO TRUE                         CL214
01121      END-IF.                                                         CL214
01122                                                                      CL214
01123      IF (MB009-ALL-DEPARTMENTS AND                                   CL214
01124          MB009-SAVE-MCL-NBR  = SPACES AND                            CL214
01125          MB009-SAVE-SCL-NBR  = SPACES)                               CL214
01126          SET MB009-OPEN-MERCH-CURSOR TO TRUE                         CL214
01127      END-IF.                                                         CL214
01128                                                                      CL214
01129      IF (MB009-ALL-DEPARTMENTS AND                                   CL214
01130          MB009-ALL-MCLASSES    AND                                   CL214
01131          MB009-SAVE-SCL-NBR  = SPACES)                               CL214
01132          SET MB009-OPEN-MERCH-CURSOR TO TRUE                         CL214
01133      END-IF.                                                         CL214
01134  EJECT                                                               CL214
01135                                                                      CL215
01136  D200-RECORD-SELECTIONS.                                          MBKCS108
01137                                                                   MBKCS108
01138 ******************************************************************MBKCS108
01139 *    IF THE USER HIT THE DESELECT KEY, OR A KEY WHICH INVOLVES    MBKCS108
01140 *    LEAVING THIS TRANSACTION AND GOING TO ANOTHER, SET A FLAG    MBKCS108
01141 *    INDICATING THAT ALL SELECTIONS ARE VALID.  IF ANY OTHER KEY  MBKCS108
01142 *    IS HIT, MOVE THE SELECTIONS MADE TO THE COMMAREA, AND EDIT   MBKCS108
01143 *    THOSE SELECTIONS.                                            MBKCS108
01144 *                                                                 MBKCS108
01145 *    IF THE USER ENTERED SOMETHING IN THE COMMAND LINE OR HIT     MBKCS108
01146 *    AN INVALID FUNCTION KEY, DO NOT EDIT THE SELECTIONS.         MBKCS108
01147 *                                                                 MBKCS108
01148 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01149 ******************************************************************MBKCS108
01150                                                                      CL*87
01151      IF (DP020-FK-DESELECT(DP020-SRC-AID))   OR                   MBKCS108
01152         (DP020-FK-SELECT-ALL(DP020-SRC-AID)) OR                      CL*71
01153         (DP020-FK-RETURN(DP020-SRC-AID))     OR                      CL*71
01154         (DP020-FK-GO-TO-TRAN(DP020-SRC-AID))                         CL*71
01155          SET PS-VALID-SELECTIONS TO TRUE                          MBKCS108
01156      ELSE                                                         MBKCS108
01157          PERFORM E200-MOVE-SELECTS-TO-COMMAREA                    MBKCS108
01158                                                                   MBKCS108
01159          IF (DP020-SRC-AID          = DP016-ENTER) AND            MBKCS108
01160             (DP020-NAV-MNEMONIC NOT = SPACES)      AND            MBKCS108
01161             (DP020-NAV-MNEMONIC NOT = LOW-VALUES)                 MBKCS108
01162              SET PS-VALID-SELECTIONS TO TRUE                      MBKCS108
01163          ELSE                                                     MBKCS108
01164              PERFORM E100-EDIT-SELECTIONS                            CL235
01165              SET PS-VALID-SELECTIONS TO TRUE                         CL235
01166          END-IF                                                   MBKCS108
01167                                                                   MBKCS108
01168      END-IF.                                                      MBKCS108
01169  EJECT                                                            MBKCS108
01170  D300-CHECK-FUNCTION-KEY.                                         MBKCS108
01171                                                                   MBKCS108
01172 ******************************************************************MBKCS108
01173 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION    MBKCS108
01174 *    KEY HAD BEEN SELECTED BY THE USER.                           MBKCS108
01175 *                                                                 MBKCS108
01176 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01177 ******************************************************************MBKCS108
01178                                                                   MBKCS108
01179      EVALUATE TRUE                                                MBKCS108
01180                                                                   MBKCS108
01181          WHEN (DP020-FK-GO-TO-TRAN(DP020-SRC-AID)) OR             MBKCS108
01182               (DP020-FK-RETURN(DP020-SRC-AID))                    MBKCS108
01183              SET NO-UPDATE-TO-TSQ                                    CL206
01184                                TO  TRUE                              CL206
01185              PERFORM E300-NAVIGATE                                MBKCS108
01186                                                                   MBKCS108
01187          WHEN DP020-FK-EDIT-GOTO(DP020-SRC-AID)                      CL206
01188              IF  MB009-NUMBER-OF-ITEMS-SELECTED EQUAL ZERO           CL206
01189                  MOVE PC-TSYMSG-00057                                CL206
01190                                TO  DP020-MSG-NUMBER                  CL206
01191                  SET DP020-MSG-WARNING                               CL239
01192                                TO  TRUE                              CL206
01193                  PERFORM G100-DISPLAY-PANEL                          CL206
01194              ELSE                                                    CL206
01195                                                                      CL221
01196                  PERFORM X100-PREPARE-LIST-QUEUE                     CL206
01197                  IF DP020-SRC-AID = DP016-PF15                       CL222
01198                     IF PS-SELECTED-HISTORY                           CL221
01199                        MOVE PC-TSYMSG-00146 TO DP020-MSG-NUMBER      CL221
01200                        SET DP020-MSG-FATAL  TO TRUE                  CL221
01201                        PERFORM G100-DISPLAY-PANEL                    CL221
01202                     ELSE                                             CL221
01203                        SET WE-MUST-UPDATE-TSQ TO TRUE                CL221
01204                        PERFORM E300-NAVIGATE                         CL221
01205                     END-IF                                           CL221
01206                  ELSE                                                CL221
01207                     SET WE-MUST-UPDATE-TSQ TO TRUE                   CL221
01208                     PERFORM E300-NAVIGATE                            CL221
01209                  END-IF                                              CL221
01210              END-IF                                                  CL208
01211                                                                      CL206
01212          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  MBKCS108
01213              PERFORM E400-DISPLAY-TOP-OF-LIST                     MBKCS108
01214                                                                   MBKCS108
01215          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   MBKCS108
01216              PERFORM E500-BROWSE-BACKWARD                         MBKCS108
01217                                                                   MBKCS108
01218          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   MBKCS108
01219              PERFORM E600-BROWSE-FORWARD                          MBKCS108
01220                                                                   MBKCS108
01221          WHEN (DP020-FK-DESELECT(DP020-SRC-AID))   OR                CL*72
01222               (DP020-FK-SELECT-ALL(DP020-SRC-AID))                   CL*72
01223              PERFORM E700-DESELECT-ALL-ITEMS                      MBKCS108
01224                                                                   MBKCS108
01225          WHEN DP020-SRC-AID = DP016-ENTER                         MBKCS108
01226                                                                   MBKCS108
01227              IF (DP020-NAV-MNEMONIC = LOW-VALUES) OR              MBKCS108
01228                 (DP020-NAV-MNEMONIC = SPACES)                     MBKCS108
01229                  PERFORM E800-CHECK-FOR-JUMP-BROWSE               MBKCS108
01230              ELSE                                                 MBKCS108
01231                  SET NO-UPDATE-TO-TSQ                                CL206
01232                                TO  TRUE                              CL206
01233                  PERFORM E300-NAVIGATE                            MBKCS108
01234              END-IF                                               MBKCS108
01235                                                                   MBKCS108
01236          WHEN OTHER                                               MBKCS108
01237              MOVE PC-TSYMSG-00045                                    CL206
01238                                TO  DP020-MSG-NUMBER                  CL206
01239              SET DP020-MSG-FATAL                                     CL206
01240                                TO  TRUE                              CL206
01241              PERFORM G100-DISPLAY-PANEL                              CL206
01242 *                                                                    CL206
01243      END-EVALUATE.                                                MBKCS108
01244  EJECT                                                            MBKCS108
01245  D400-REFRESH-BLOCK-ARRAY.                                        MBKCS108
01246                                                                   MBKCS108
01247 ******************************************************************MBKCS108
01248 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS108
01249 *    READ TEMPORARY STORAGE FILLING IN BOTH BLOCKS IN THE ARRAY   MBKCS108
01250 *    WITH THE ITEMS THAT WERE AT THE TOP OF THE LIST WHEN WE      MBKCS108
01251 *    HAD LEFT PREVIOUSLY.                                         MBKCS108
01252 *                                                                 MBKCS108
01253 ******************************************************************MBKCS108
01254                                                                   MBKCS108
01255      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS108
01256          1 + ((MB009-LIST-ITEM-NUMBER(1 1) - 1) /                    CL179
01257                PC-ITEMS-PER-BLOCK).                               MBKCS108
01258                                                                   MBKCS108
01259      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS108
01260      MOVE +1                   TO MB009-BLOCK-SUB.                   CL179
01261                                                                   MBKCS108
01262      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS108
01263                                                                   MBKCS108
01264      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS108
01265                                                                   MBKCS108
01266      IF (PV-BLOCK-NUMBER < MB009-HIGHEST-BLOCK-WRITTEN) OR           CL179
01267         (PV-BLOCK-NUMBER = MB009-HIGHEST-BLOCK-WRITTEN)              CL179
01268          ADD +1 TO MB009-BLOCK-SUB                                   CL179
01269                                                                   MBKCS108
01270          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS108
01271      END-IF.                                                      MBKCS108
01272  EJECT                                                            MBKCS108
01273 ******************************************************************   CL165
01274 *    DETERMIN WHETHER A CURSOR NEEDS TO BE OPENED                    CL165
01275 *                                                                    CL165
01276 ******************************************************************   CL165
01277                                                                      CL165
01278  D500-OPEN-CURSOR.                                                   CL137
01279                                                                      CL137
01280      EVALUATE TRUE                                                   CL137
01281                                                                      CL137
01282           WHEN MB009-OPEN-MERCH-CURSOR AND                           CL179
01283                PS-MERCH-CURSOR-NOT-OPENED                            CL152
01284                PERFORM Z200-OPEN-MERCH-CURSOR                        CL137
01285                                                                      CL137
01286           WHEN MB009-OPEN-DPT-CURSOR AND                             CL179
01287                PS-DPT-CURSOR-NOT-OPENED                              CL152
01288                PERFORM Z500-OPEN-DPT-CURSOR                          CL137
01289                                                                      CL137
01290           WHEN MB009-OPEN-MCL-CURSOR AND                             CL179
01291                PS-MCL-CURSOR-NOT-OPENED                              CL152
01292                PERFORM Z600-OPEN-MCL-CURSOR                          CL148
01293                                                                      CL148
01294           WHEN MB009-OPEN-SCL-CURSOR AND                             CL179
01295                PS-SCL-CURSOR-NOT-OPENED                              CL152
01296                PERFORM Z700-OPEN-SCL-CURSOR                          CL148
01297                                                                      CL148
01298           WHEN MB009-NO-CURSOR-OPEN                                  CL197
01299                SET MB009-OPEN-SCL-CURSOR TO TRUE                     CL199
01300                PERFORM Z700-OPEN-SCL-CURSOR                          CL200
01301                                                                      CL196
01302      END-EVALUATE.                                                   CL138
01303                                                                      CL137
01304  EJECT                                                               CL137
01305  E100-EDIT-SELECTIONS.                                            MBKCS108
01306                                                                   MBKCS108
01307 ******************************************************************MBKCS108
01308 *    EDIT EACH SELECTION ENTRY.  IF EACH ENTRY IS CONSIDERED      MBKCS108
01309 *    VALID, CHECK IF A BEGINNING AND ENDING ENTRY WAS ENTERED,    MBKCS108
01310 *    AND CHECK THE FOLLOWING.                                     MBKCS108
01311 *                                                                 MBKCS108
01312 *    1) IF THERE IS A BEGINNING RANGE, THERE MUST BE AN ENDING.   MBKCS108
01313 *    2) IF THERE IS A ENDING RANGE, THERE MUST BE AN BEGINNING.   MBKCS108
01314 *    3) BEGINNING RANGE MUST PRECEDE ENDING RANGE.                MBKCS108
01315 *                                                                 MBKCS108
01316 *    IF A VALID BEGINNING AND ENDING RANGE HAVE BEEN SELECTED,    MBKCS108
01317 *    MARK ALL ITEMS IN THAT RANGE AS BEING SELECTED.              MBKCS108
01318 *                                                                 MBKCS108
01319 *    PERFORMED BY D200-RECORD-SELECTIONS                             CL*92
01320 ******************************************************************MBKCS108
01321                                                                   MBKCS108
01322      SET PS-VALID-SELECTIONS TO TRUE.                             MBKCS108
01323                                                                   MBKCS108
01324      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS108
01325          (MB009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL179
01326                                                                   MBKCS108
01327      MOVE MB009-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL179
01328      MOVE +1                         TO PS-SUB.                   MBKCS108
01329                                                                   MBKCS108
01330      PERFORM F100-EDIT                                            MBKCS108
01331          UNTIL (PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL)    OR   MBKCS108
01332                (PV-ITEM-IN-USE > MB009-NUMBER-OF-ITEMS-READ)         CL179
01333                                                                   MBKCS108
01334      IF PS-VALID-SELECTIONS                                       MBKCS108
01335                                                                   MBKCS108
01336          IF MB009-START-OF-SELECTED-RANGE > +0                       CL179
01337                                                                   MBKCS108
01338              IF MB009-END-OF-SELECTED-RANGE   >                      CL179
01339                 MB009-START-OF-SELECTED-RANGE                        CL179
01340                  PERFORM F200-MARK-RANGE-AS-SELECTED              MBKCS108
01341              ELSE                                                 MBKCS108
01342                                                                   MBKCS108
01343                  IF MB009-END-OF-SELECTED-RANGE = +0                 CL179
01344                      MOVE PC-TSYMSG-00051    TO DP020-MSG-NUMBER     CL186
01345                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*55
01346                      SET PS-WARNING            TO TRUE               CL*55
01347                      IF NOT DP020-NEXT-ACT-ERROR                     CL*19
01348                          SET DP020-MSG-WARNING TO TRUE               CL*55
01349                      END-IF                                          CL*19
01350                  ELSE                                             MBKCS108
01351                      SET PS-INVALID-SELECTIONS TO TRUE            MBKCS108
01352                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
01353                      SET DP020-MSG-FATAL       TO TRUE               CL*19
01354                      MOVE PC-TSYMSG-00052    TO DP020-MSG-NUMBER     CL186
01355                      IF NOT DP020-NEXT-ACT-ERROR                     CL*20
01356                          MOVE MB009-ITEM-AT-TOP-OF-PANEL             CL179
01357                                                TO PV-ITEM-IN-USE     CL*20
01358                          MOVE +1               TO PS-SUB             CL*20
01359                          PERFORM                                     CL*20
01360                              UNTIL (PV-ITEM-IN-USE >                 CL*20
01361                                     PV-ENDING-ITEM-ON-PANEL)  OR     CL*20
01362                                    (PV-ITEM-IN-USE >                 CL*20
01363                                     MB009-NUMBER-OF-ITEMS-READ)      CL179
01364                              PERFORM K100-SET-SUBSCRIPTS             CL*20
01365                              IF (MB009-BEGIN(MB009-BLOCK-SUB         CL179
01366                                              MB009-ITEM-SUB)) OR     CL179
01367                                 (MB009-END(MB009-BLOCK-SUB           CL179
01368                                            MB009-ITEM-SUB))          CL179
01369                                  MOVE -1                      TO     CL*22
01370                                       MB010I-ASELL(PS-SUB)           CL179
01371                                  MOVE PV-ENDING-ITEM-ON-PANEL TO     CL*22
01372                                       PV-ITEM-IN-USE                 CL*22
01373                              END-IF                                  CL*22
01374                              ADD +1            TO PS-SUB             CL*22
01375                              ADD +1            TO PV-ITEM-IN-USE     CL*22
01376                          END-PERFORM                                 CL*22
01377                      END-IF                                          CL*22
01378                  END-IF                                              CL*22
01379              END-IF                                               MBKCS108
01380          ELSE                                                     MBKCS108
01381              IF MB009-END-OF-SELECTED-RANGE > +0                     CL179
01382                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
01383                  SET PS-WARNING            TO TRUE                   CL*22
01384                  MOVE PC-TSYMSG-00053      TO DP020-MSG-NUMBER       CL186
01385                  IF NOT DP020-NEXT-ACT-ERROR                         CL*22
01386                      SET DP020-MSG-WARNING TO TRUE                   CL*55
01387                  END-IF                                              CL*22
01388              END-IF                                               MBKCS108
01389          END-IF                                                   MBKCS108
01390      END-IF.                                                      MBKCS108
01391  EJECT                                                            MBKCS108
01392  E200-MOVE-SELECTS-TO-COMMAREA.                                   MBKCS108
01393                                                                   MBKCS108
01394 ******************************************************************MBKCS108
01395 *    MOVE THE SELECTIONS MADE FROM THE CURRENT PAGE INTO          MBKCS108
01396 *    THE COMMAREA.                                                MBKCS108
01397 *                                                                 MBKCS108
01398 *    PERFORMED BY D200-RECORD-SELECTIONS.                            CL*92
01399 ******************************************************************MBKCS108
01400                                                                   MBKCS108
01401      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS108
01402          (MB009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL179
01403                                                                   MBKCS108
01404      MOVE +1                         TO PS-SUB.                   MBKCS108
01405      MOVE MB009-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL179
01406                                                                   MBKCS108
01407      PERFORM F300-CHECK-SELECTIONS                                MBKCS108
01408          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS108
01409  EJECT                                                            MBKCS108
01410  E300-NAVIGATE.                                                   MBKCS108
01411                                                                   MBKCS108
01412 ******************************************************************MBKCS108
01413 *    DELETE THE TEMPORARY STORAGE IF THE USER HIT PF3.  IF WE ARE    CL*11
01414 *    GOING TO ANOTHER TRANSACTION, BUT PLANNING TO COME BACK,        CL*11
01415 *    WRITE OUT THE LAST 2 BLOCKS FROM THE ARRAY TO TEMPORARY         CL*11
01416 *    STORAGE. IN EITHER CASE, SET UP THE INTER APPLICATION           CL*11
01417 *    COMMAREA, AND START THE NAVIGATOR.                              CL*11
01418 *                                                                 MBKCS108
01419 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01420 ******************************************************************MBKCS108
01421                                                                   MBKCS108
01422      IF  NO-UPDATE-TO-TSQ                                            CL238
01423          PERFORM H400-WRITE-OUT-LAST-BLOCKS                          CL209
01424          PERFORM F400-SETUP-INTER-APPL                               CL209
01425      END-IF.                                                         CL209
01426                                                                      CL230
01427      MOVE MB009-CO-NUMBER                TO                          CL231
01428           MB005-CO-NUMBER.                                           CL231
01429      SET DP020-MB-SUB-CLASS-LIST TO TRUE.                            CL230
01430      PERFORM DP015-1000-START-NAVIGATOR.                          MBKCS108
01431  EJECT                                                            MBKCS108
01432  E400-DISPLAY-TOP-OF-LIST.                                        MBKCS108
01433                                                                   MBKCS108
01434 ******************************************************************MBKCS108
01435 *    IF THE USER HITS THE 1ST PAGE KEY, COMPUTE THE TOP ITEM      MBKCS108
01436 *    NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM           MBKCS108
01437 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS108
01438 *    TO BE READ.                                                  MBKCS108
01439 *                                                                 MBKCS108
01440 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01441 ******************************************************************MBKCS108
01442                                                                   MBKCS108
01443      IF MB009-NUMBER-OF-ITEMS-READ > +0                              CL179
01444          MOVE +1 TO PV-TOP-ITEM                                   MBKCS108
01445                                                                   MBKCS108
01446          PERFORM F500-BROWSE                                      MBKCS108
01447      ELSE                                                         MBKCS108
01448          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*69
01449          PERFORM G100-DISPLAY-PANEL                               MBKCS108
01450      END-IF.                                                      MBKCS108
01451  EJECT                                                            MBKCS108
01452  E500-BROWSE-BACKWARD.                                            MBKCS108
01453                                                                   MBKCS108
01454 ******************************************************************MBKCS108
01455 *    IF THE USER HITS THE PAGE BACKARD KEY, COMPUTE THE TOP       MBKCS108
01456 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS108
01457 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS108
01458 *    TO BE READ.                                                  MBKCS108
01459 *                                                                 MBKCS108
01460 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01461 ******************************************************************MBKCS108
01462                                                                   MBKCS108
01463      IF MB009-NUMBER-OF-ITEMS-READ > +0                              CL179
01464          COMPUTE PV-TOP-ITEM =                                    MBKCS108
01465              MB009-ITEM-AT-TOP-OF-PANEL - PC-ITEMS-PER-PANEL         CL179
01466                                                                   MBKCS108
01467          PERFORM F500-BROWSE                                      MBKCS108
01468      ELSE                                                         MBKCS108
01469          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01470          PERFORM G100-DISPLAY-PANEL                               MBKCS108
01471      END-IF.                                                      MBKCS108
01472  EJECT                                                            MBKCS108
01473  E600-BROWSE-FORWARD.                                             MBKCS108
01474                                                                   MBKCS108
01475 ******************************************************************MBKCS108
01476 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP       MBKCS108
01477 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS108
01478 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS108
01479 *    TO BE READ.                                                  MBKCS108
01480 *                                                                    CL*92
01481 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01482 ******************************************************************MBKCS108
01483                                                                   MBKCS108
01484      IF MB009-NUMBER-OF-ITEMS-READ > +0                              CL179
01485          COMPUTE PV-TOP-ITEM =                                    MBKCS108
01486              MB009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL         CL179
01487                                                                   MBKCS108
01488          PERFORM F500-BROWSE                                      MBKCS108
01489      ELSE                                                         MBKCS108
01490          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01491          PERFORM G100-DISPLAY-PANEL                               MBKCS108
01492      END-IF.                                                      MBKCS108
01493  EJECT                                                            MBKCS108
01494  E700-DESELECT-ALL-ITEMS.                                         MBKCS108
01495                                                                   MBKCS108
01496 ******************************************************************MBKCS108
01497 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION       MBKCS108
01498 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.          MBKCS108
01499 *                                                                 MBKCS108
01500 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01501 ******************************************************************MBKCS108
01502                                                                   MBKCS108
01503      IF MB009-NUMBER-OF-ITEMS-READ > +0                              CL179
01504          MOVE +1 TO MB009-BLOCK-SUB                                  CL179
01505                                                                   MBKCS108
01506          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS108
01507              VARYING MB009-ITEM-SUB FROM 1 BY 1                      CL179
01508              UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL179
01509                                                                   MBKCS108
01510          ADD +1 TO MB009-BLOCK-SUB                                   CL179
01511                                                                   MBKCS108
01512          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS108
01513              VARYING MB009-ITEM-SUB FROM 1 BY 1                      CL179
01514              UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL179
01515                                                                   MBKCS108
01516          MOVE MB009-LIST-ITEM-NUMBER(1 1) TO PV-LIST-ITEM-NUMBER     CL179
01517                                                                      CL**2
01518 ******************************************************************   CL**2
01519 *    CALCULATE THE BLOCK NUMBER OF THE FIRST BLOCK IN WORKING        CL**2
01520 *    STORAGE.  IF THIS BLOCK NUMBER IS NOT = 1, WE KNOW THAT         CL**2
01521 *    THERE ARE BLOCKS WITH SMALLER BLOCK NUMBERS SITTING OUT         CL**2
01522 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01523 *                                                                    CL**2
01524 *    IF THE BLOCK NUMBER DOES = 1, BUT THE HIGHEST BLOCK NUMBER      CL**2
01525 *    WRITTEN TO TEMPORARY STORAGE IS GREATER THAN THE MAXIMUM        CL**2
01526 *    NUMBER OF BLOCKS IN WORKING STORAGE, WE KNOW THAT               CL**2
01527 *    THERE ARE BLOCKS WITH LARGER BLOCK NUMBERS SITTING OUT          CL**2
01528 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01529 ******************************************************************   CL**2
01530                                                                   MBKCS108
01531          COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                        MBKCS108
01532              1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK) MBKCS108
01533                                                                   MBKCS108
01534          IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                 MBKCS108
01535             (MB009-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)   CL179
01536              MOVE +1 TO MB009-BLOCK-SUB                              CL179
01537                                                                   MBKCS108
01538              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS108
01539                                                                   MBKCS108
01540              MOVE +1 TO PV-BLOCK-NUMBER                           MBKCS108
01541                                                                   MBKCS108
01542              COMPUTE PV-PREV-BLOCK-IN-ARRAY =                     MBKCS108
01543                  PV-FIRST-BLOCK-IN-ARRAY - 1                      MBKCS108
01544                                                                   MBKCS108
01545              PERFORM                                              MBKCS108
01546                  UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY   MBKCS108
01547                                                                   MBKCS108
01548                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS108
01549                                                                   MBKCS108
01550                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS108
01551                      VARYING MB009-ITEM-SUB FROM 1 BY 1              CL179
01552                      UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL179
01553                                                                   MBKCS108
01554                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS108
01555                                                                   MBKCS108
01556                  ADD +1 TO PV-BLOCK-NUMBER                        MBKCS108
01557                                                                   MBKCS108
01558              END-PERFORM                                          MBKCS108
01559                                                                   MBKCS108
01560              COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                     MBKCS108
01561                  PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY MBKCS108
01562                                                                   MBKCS108
01563              PERFORM                                              MBKCS108
01564                  UNTIL PV-NEXT-BLOCK-IN-ARRAY >                   MBKCS108
01565                        MB009-HIGHEST-BLOCK-WRITTEN                   CL179
01566                                                                   MBKCS108
01567                  MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER   MBKCS108
01568                                                                   MBKCS108
01569                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS108
01570                                                                   MBKCS108
01571                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS108
01572                      VARYING MB009-ITEM-SUB FROM 1 BY 1              CL179
01573                      UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL179
01574                                                                   MBKCS108
01575                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS108
01576                                                                   MBKCS108
01577                  ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                 MBKCS108
01578                                                                   MBKCS108
01579              END-PERFORM                                          MBKCS108
01580                                                                   MBKCS108
01581              MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER      MBKCS108
01582                                                                   MBKCS108
01583              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS108
01584          END-IF                                                   MBKCS108
01585                                                                   MBKCS108
01586      END-IF.                                                      MBKCS108
01587                                                                   MBKCS108
01588      IF DP020-FK-DESELECT(DP020-SRC-AID)                             CL*73
01589          MOVE +0 TO MB009-NUMBER-OF-ITEMS-SELECTED                   CL179
01590      END-IF.                                                         CL*73
01591                                                                      CL*73
01592      MOVE +0 TO MB009-START-OF-SELECTED-RANGE.                       CL179
01593      MOVE +0 TO MB009-END-OF-SELECTED-RANGE.                         CL179
01594                                                                      CL*92
01595      SET PS-SAME-ITEMS-DISPLAYED TO TRUE.                            CL*92
01596                                                                      CL*92
01597      PERFORM G100-DISPLAY-PANEL.                                  MBKCS108
01598  EJECT                                                            MBKCS108
01599  E800-CHECK-FOR-JUMP-BROWSE.                                      MBKCS108
01600                                                                   MBKCS108
01601 ******************************************************************MBKCS108
01602 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO     MBKCS108
01603 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.    MBKCS108
01604 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST   MBKCS108
01605 *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.       MBKCS108
01606 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT      MBKCS108
01607 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.           MBKCS108
01608 *                                                                 MBKCS108
01609 ******************************************************************MBKCS108
01610                                                                   MBKCS108
01611      SET PS-VALID-BEG-ITEM TO TRUE.                               MBKCS108
01612                                                                   MBKCS108
01613      IF (ABEGITMA                      = PC-ERASE-EOF) OR            CL*57
01614         (MB009-NUMBER-OF-ITEMS-READ    = +0)                         CL179
01615          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL169
01616          PERFORM G100-DISPLAY-PANEL                               MBKCS108
01617      ELSE                                                         MBKCS108
01618                                                                   MBKCS108
01619          MOVE ABEGITMI             TO DP010I-UNEDITED-FIELD          CL*57
01620          MOVE PC-FIELD-LENGTH      TO DP010I-MAXIMUM-DIGITS          CL**2
01621          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS        CL**2
01622          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                  MBKCS108
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262978              USING DP010I-NUMERIC-EDIT-AREA                              
01624                                                                      CL**2
01625 ******************************************************************   CL**2
01626 *    WE MUST MAKE SURE THAT THE NEW ITEM NUMBER THAT THE USER        CL**2
01627 *    WANTS TO JUMP TO WILL NOT REQUIRE US TO READ THE DATABASE       CL**2
01628 *    TOO MANY TIMES.  WE CALCULATE THE MAXIMUM ITEM NUMBER THAT      CL**2
01629 *    CAN BE AT THE TOP OF THE LIST WITHOUT EXCEEDING THE MAXIMUM     CL**2
01630 *    NUMBER OF READS THAT WE HAVE SET.  IF THE NUMBER THAT THE       CL**2
01631 *    USER WANTS AT THE TOP OF THE LIST IS GREATER THAN THE MAXIMUM   CL**2
01632 *    ITEM NUMBER THAT WE HAVE CALCULATED, A MESSAGE IS DISPLAYED,    CL**2
01633 *    AND WE START THE LIST USING THE MAXIMUM ITEM NUMBER.            CL**2
01634 ******************************************************************   CL**2
01635                                                                   MBKCS108
01636          IF NOT DP010I-ERROR-DETECTED                             MBKCS108
01637              MOVE DP010I-NUMERIC-FIELD TO MB010I-ABEGITMI            CL179
01638              MOVE MB010I-ABEGITMI      TO PV-BEGINNING-ITEM          CL179
01639                                                                   MBKCS108
01640              IF (PV-BEGINNING-ITEM >  0)          AND             MBKCS108
01641                 (PV-BEGINNING-ITEM <  PC-MAX-ITEMS)               MBKCS108
01642                  MOVE PV-BEGINNING-ITEM TO PV-TOP-ITEM            MBKCS108
01643                                                                   MBKCS108
01644                  COMPUTE PV-MAX-ITEMS-TO-BE-READ =                MBKCS108
01645                     ((MB009-NUMBER-OF-ITEMS-READ +                   CL179
01646                       PC-MAX-ITEMS-TO-BE-READ)   -                   CL**2
01647                       PC-ITEMS-PER-PANEL)        + 1                 CL**2
01648                                                                   MBKCS108
01649                  IF PV-TOP-ITEM > PV-MAX-ITEMS-TO-BE-READ         MBKCS108
01650                      MOVE PV-MAX-ITEMS-TO-BE-READ TO                 CL**2
01651                           PV-TOP-ITEM                                CL**2
01652                      MOVE -1                      TO ABEGITML        CL*33
01653                      IF MB009-NO-ITEMS-LEFT-ON-DB                    CL179
01654                          MOVE PC-TSYMSG-00070    TO                  CL185
01655                               DP020-MSG-NUMBER                       CL185
01656                      ELSE                                            CL**2
01657                          MOVE PC-TSYMSG-00207    TO                  CL235
01658                               DP020-MSG-NUMBER                       CL185
01659                      END-IF                                       MBKCS108
01660                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*53
01661                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*33
01662                  END-IF                                           MBKCS108
01663                                                                   MBKCS108
01664                  PERFORM F500-BROWSE                              MBKCS108
01665              ELSE                                                 MBKCS108
01666                  MOVE PC-TSYMSG-00005       TO DP020-MSG-NUMBER      CL185
01667                  MOVE -1                    TO ABEGITML              CL*33
01668                  MOVE DP015-REVERSE         TO ABEGITMH              CL*53
01669                  MOVE DP015-RED             TO ABEGITMC              CL*33
01670                  MOVE DP015-UNP-ALP-BRT-MDT TO ABEGITMA              CL167
01671                  SET PS-INVALID-BEG-ITEM    TO TRUE                  CL*33
01672                  SET PS-CURSOR-POSITIONED   TO TRUE                  CL*55
01673                  SET DP020-MSG-FATAL        TO TRUE                  CL*33
01674              END-IF                                               MBKCS108
01675                                                                   MBKCS108
01676          ELSE                                                     MBKCS108
01677              MOVE PC-TSYMSG-00008           TO DP020-MSG-NUMBER      CL185
01678              MOVE -1                        TO ABEGITML              CL*33
01679              MOVE DP015-REVERSE             TO ABEGITMH              CL*53
01680              MOVE DP015-RED                 TO ABEGITMC              CL*33
01681              MOVE DP015-UNP-ALP-BRT-MDT     TO ABEGITMA              CL167
01682              SET PS-INVALID-BEG-ITEM        TO TRUE                  CL*33
01683              SET PS-CURSOR-POSITIONED       TO TRUE                  CL*33
01684              SET DP020-MSG-FATAL            TO TRUE                  CL*33
01685          END-IF                                                   MBKCS108
01686                                                                   MBKCS108
01687      END-IF.                                                      MBKCS108
01688                                                                   MBKCS108
01689      IF PS-INVALID-BEG-ITEM                                       MBKCS108
01690          SET PS-SAME-ITEMS-DISPLAYED        TO TRUE                  CL*73
01691          PERFORM DP017-0000-GET-CURR-DATE-TIME                    MBKCS108
01692          PERFORM I200-FORMAT-LIST-LINES                           MBKCS108
01693          PERFORM I300-FORMAT-NR-SELECTED                          MBKCS108
01694          PERFORM DP005-0000-FORMAT-MSG-LINES                      MBKCS108
01695          PERFORM DP008-0000-WRITE-PANEL                           MBKCS108
01696      END-IF.                                                      MBKCS108
01697  EJECT                                                            MBKCS108
01698  F100-EDIT.                                                       MBKCS108
01699                                                                   MBKCS108
01700 ******************************************************************MBKCS108
01701 *    EDIT THE SELECTION ENTRIES.                                  MBKCS108
01702 *                                                                 MBKCS108
01703 *    1) VALID ENTRIES ARE 'S' 'B' OR 'E'.                         MBKCS108
01704 *    2) THERE CAN ONLY BE 1 BEGINNING AND 1 ENDING RANGE.         MBKCS108
01705 *                                                                 MBKCS108
01706 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
01707 ******************************************************************MBKCS108
01708                                                                   MBKCS108
01709      PERFORM K100-SET-SUBSCRIPTS.                                 MBKCS108
01710                                                                   MBKCS108
01711      IF NOT (MB009-NO-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB)) AND    CL179
01712         NOT (MB009-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB))           CL179
01713                                                                      CL**2
01714 ******************************************************************   CL**2
01715 *   IF THE CURRENT ENTRY IS A 'B', WE MUST MAKE SURE THAT THIS       CL**2
01716 *   IS THE ONLY 'B' ENTERED ON THE SCREEN.  TO VERIFY THIS, THE      CL**2
01717 *   CURRENT ITEM WE ARE POINTING TO SHOULD EQUAL THE START OF        CL**2
01718 *   SELECTED RANGE FIELD.  IF THEY ARE NOT EQUAL, AND THE START OF   CL**2
01719 *   SELECTED RANGE FIELD DOES NOT EQUAL 0, WE KNOW THAT THIS FIELD   CL**2
01720 *   CONTAINS THE LIST ITEM NUMBER THAT CURRENTLY CONTAINS A 'B'.     CL**2
01721 *   IF THE START OF SELECTED RANGE FIELD EQUALS 0, THAN THIS IS      CL**2
01722 *   THE FIRST 'B' ENTERED ON THE SCREEN, AND WE SET THIS FIELD       CL**2
01723 *   EQUAL TO THE CURRENT ITEM WE ARE POINTING TO.                    CL**2
01724 *                                                                    CL**2
01725 *   THE SAME TYPE OF LOGIC IS DONE FOR THE ENDING RANGE 'E'.         CL**2
01726 ******************************************************************   CL**2
01727                                                                   MBKCS108
01728          IF MB009-BEGIN(MB009-BLOCK-SUB MB009-ITEM-SUB)              CL179
01729                                                                   MBKCS108
01730              IF MB009-START-OF-SELECTED-RANGE NOT = PV-ITEM-IN-USE   CL179
01731                                                                   MBKCS108
01732                  IF MB009-START-OF-SELECTED-RANGE = +0               CL179
01733                      MOVE PV-ITEM-IN-USE TO                       MBKCS108
01734                          MB009-START-OF-SELECTED-RANGE               CL179
01735                  ELSE                                             MBKCS108
01736                                                                   MBKCS108
01737                      IF PS-VALID-SELECTIONS                       MBKCS108
01738                          MOVE PC-TSYMSG-00054  TO                    CL185
01739                               DP020-MSG-NUMBER                       CL185
01740                          IF NOT DP020-NEXT-ACT-ERROR                 CL*22
01741                              MOVE -1      TO MB010I-ASELL(PS-SUB)    CL179
01742                          END-IF                                      CL*22
01743                      END-IF                                       MBKCS108
01744                                                                   MBKCS108
01745                      SET DP020-MSG-FATAL       TO TRUE               CL*22
01746                      SET PS-INVALID-SELECTIONS TO TRUE               CL*22
01747                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
01748                  END-IF                                           MBKCS108
01749              END-IF                                               MBKCS108
01750          ELSE                                                     MBKCS108
01751              IF MB009-END(MB009-BLOCK-SUB MB009-ITEM-SUB)            CL179
01752                                                                   MBKCS108
01753                  IF MB009-END-OF-SELECTED-RANGE NOT =                CL179
01754                     PV-ITEM-IN-USE                                MBKCS108
01755                                                                   MBKCS108
01756                      IF MB009-END-OF-SELECTED-RANGE = +0             CL179
01757                          MOVE PV-ITEM-IN-USE TO                   MBKCS108
01758                               MB009-END-OF-SELECTED-RANGE            CL179
01759                      ELSE                                         MBKCS108
01760                                                                   MBKCS108
01761                          IF PS-VALID-SELECTIONS                   MBKCS108
01762                              MOVE PC-TSYMSG-00055  TO                CL185
01763                                   DP020-MSG-NUMBER                   CL185
01764                              IF NOT DP020-NEXT-ACT-ERROR             CL*22
01765                                 MOVE -1 TO MB010I-ASELL(PS-SUB)      CL179
01766                              END-IF                                  CL*22
01767                          END-IF                                   MBKCS108
01768                          SET DP020-MSG-FATAL       TO TRUE           CL*22
01769                          SET PS-INVALID-SELECTIONS TO TRUE        MBKCS108
01770                          SET PS-CURSOR-POSITIONED  TO TRUE           CL*53
01771                      END-IF                                       MBKCS108
01772                  END-IF                                           MBKCS108
01773              ELSE                                                 MBKCS108
01774                  IF PS-VALID-SELECTIONS                           MBKCS108
01775                      MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER        CL185
01776                      IF NOT DP020-NEXT-ACT-ERROR                     CL*22
01777                         MOVE -1   TO MB010I-ASELL(PS-SUB)            CL179
01778                      END-IF                                          CL*22
01779                  END-IF                                              CL*22
01780                  SET DP020-MSG-FATAL       TO TRUE                   CL*22
01781                  SET PS-INVALID-SELECTIONS TO TRUE                   CL*22
01782                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
01783                  MOVE DP015-RED            TO MB010O-ASELC(PS-SUB)   CL179
01784                  MOVE DP015-REVERSE        TO MB010O-ASELH(PS-SUB)   CL179
01785              END-IF                                               MBKCS108
01786          END-IF                                                   MBKCS108
01787      END-IF.                                                      MBKCS108
01788                                                                   MBKCS108
01789      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS108
01790      ADD +1 TO PS-SUB.                                            MBKCS108
01791  EJECT                                                            MBKCS108
01792  F200-MARK-RANGE-AS-SELECTED.                                     MBKCS108
01793                                                                   MBKCS108
01794 ******************************************************************MBKCS108
01795 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE     MBKCS108
01796 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS     MBKCS108
01797 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.              MBKCS108
01798 *                                                                 MBKCS108
01799 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
01800 ******************************************************************MBKCS108
01801                                                                   MBKCS108
01802      MOVE +1 TO MB009-BLOCK-SUB.                                     CL179
01803                                                                   MBKCS108
01804      PERFORM G200-MARK-SELECTED-INDS                              MBKCS108
01805          VARYING MB009-ITEM-SUB FROM 1 BY 1                          CL179
01806          UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL179
01807                                                                   MBKCS108
01808      ADD +1 TO MB009-BLOCK-SUB.                                      CL179
01809                                                                   MBKCS108
01810      PERFORM G200-MARK-SELECTED-INDS                              MBKCS108
01811          VARYING MB009-ITEM-SUB FROM 1 BY 1                          CL179
01812          UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL179
01813                                                                      CL**2
01814 ******************************************************************   CL**2
01815 *    AFTER MARKING THE ENTRIES IN THE BLOCKS IN WORKING STORAGE,     CL**2
01816 *    WE MUST SEE IF ANY OF THE ENTRIES IN THE BLOCKS IN TEMPORARY    CL**2
01817 *    STORAGE SHOULD BE MARKED.  TO DETERMINE THIS, WE CALCULATE      CL**2
01818 *    THE BLOCK NUMBERS OF THE BLOCKS IN WORKING STORAGE AND THE      CL**2
01819 *    BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE THAT FALL      CL**2
01820 *    WITHIN THE STARTING AND ENDING RANGE SPECIFED BY THE USER.      CL**2
01821 *    IF THE BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE         CL**2
01822 *    ARE LESS THAN OR GREATER THAN THE BLOCK NUMBERS OF THE          CL**2
01823 *    BLOCKS IN WORKING STORAGE, WE MUST READ THOSE BLOCKS FROM       CL**2
01824 *    TEMPORARY STORAGE AND MARK THE APPROPRIATE ENTRIES AS BEING     CL**2
01825 *    SELECTED.                                                       CL**2
01826 ******************************************************************   CL**2
01827                                                                   MBKCS108
01828      MOVE MB009-LIST-ITEM-NUMBER(1 1)   TO                           CL179
01829           PV-LIST-ITEM-NUMBER.                                    MBKCS108
01830                                                                   MBKCS108
01831      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            MBKCS108
01832          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         MBKCS108
01833                PC-ITEMS-PER-BLOCK).                               MBKCS108
01834                                                                   MBKCS108
01835      MOVE MB009-START-OF-SELECTED-RANGE TO                           CL179
01836           PV-START-OF-SELECTED-RANGE.                             MBKCS108
01837                                                                   MBKCS108
01838      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            MBKCS108
01839          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  MBKCS108
01840                PC-ITEMS-PER-BLOCK).                               MBKCS108
01841                                                                   MBKCS108
01842      MOVE MB009-END-OF-SELECTED-RANGE   TO                           CL179
01843           PV-END-OF-SELECTED-RANGE.                               MBKCS108
01844                                                                   MBKCS108
01845      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             MBKCS108
01846          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    MBKCS108
01847                PC-ITEMS-PER-BLOCK).                               MBKCS108
01848                                                                   MBKCS108
01849      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             MBKCS108
01850          PV-FIRST-BLOCK-IN-ARRAY + 1.                             MBKCS108
01851                                                                   MBKCS108
01852      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    MBKCS108
01853         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        MBKCS108
01854          MOVE +1 TO MB009-BLOCK-SUB                                  CL179
01855                                                                   MBKCS108
01856          PERFORM Z150-WRITE-TEMP-STORAGE                          MBKCS108
01857                                                                   MBKCS108
01858          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         MBKCS108
01859              PV-FIRST-BLOCK-IN-ARRAY - 1                          MBKCS108
01860                                                                   MBKCS108
01861          PERFORM                                                  MBKCS108
01862              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      MBKCS108
01863                    PV-PREV-BLOCK-IN-ARRAY                         MBKCS108
01864                                                                   MBKCS108
01865              MOVE PV-FIRST-BLOCK-IN-RANGE TO PV-BLOCK-NUMBER      MBKCS108
01866                                                                   MBKCS108
01867              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS108
01868                                                                   MBKCS108
01869              PERFORM G200-MARK-SELECTED-INDS                      MBKCS108
01870                  VARYING MB009-ITEM-SUB FROM 1 BY 1                  CL179
01871                  UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL179
01872                                                                   MBKCS108
01873              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS108
01874                                                                   MBKCS108
01875              ADD +1 TO PV-FIRST-BLOCK-IN-RANGE                    MBKCS108
01876                                                                   MBKCS108
01877          END-PERFORM                                              MBKCS108
01878                                                                   MBKCS108
01879          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         MBKCS108
01880              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     MBKCS108
01881                                                                   MBKCS108
01882          PERFORM                                                  MBKCS108
01883              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       MBKCS108
01884                    PV-LAST-BLOCK-IN-RANGE                         MBKCS108
01885                                                                   MBKCS108
01886              MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER       MBKCS108
01887                                                                   MBKCS108
01888              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS108
01889                                                                   MBKCS108
01890              PERFORM G200-MARK-SELECTED-INDS                      MBKCS108
01891                  VARYING MB009-ITEM-SUB FROM 1 BY 1                  CL179
01892                  UNTIL MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL179
01893                                                                   MBKCS108
01894              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS108
01895                                                                   MBKCS108
01896              ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                     MBKCS108
01897                                                                   MBKCS108
01898          END-PERFORM                                              MBKCS108
01899                                                                   MBKCS108
01900          MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER          MBKCS108
01901                                                                   MBKCS108
01902          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS108
01903      END-IF.                                                      MBKCS108
01904                                                                   MBKCS108
01905      MOVE +0 TO MB009-START-OF-SELECTED-RANGE.                       CL179
01906      MOVE +0 TO MB009-END-OF-SELECTED-RANGE.                         CL179
01907  EJECT                                                            MBKCS108
01908  F300-CHECK-SELECTIONS.                                           MBKCS108
01909                                                                   MBKCS108
01910 ******************************************************************MBKCS108
01911 *    CHECK EACH ENTRY TO DETERMINE IF THE SELECTION CRITERIA IN   MBKCS108
01912 *    THE MAP DIFFERS FROM THAT IN THE ARRAY.  IF THEY DO DIFFER,  MBKCS108
01913 *    AND THE ARRAY ENTRY SHOWS THAT THE ITEM WAS PREVIOUSLY       MBKCS108
01914 *    SELECTED, SUBTRACT 1 FROM THE SELECTED COUNTER.  IF THEY DO  MBKCS108
01915 *    DIFFER, AND THE ARRAY ENTRY SHOWS THAT THIS ITEM WAS MARKED  MBKCS108
01916 *    AS EITHER THE BEGINNING OR ENDING SELECTION POINT, ZERO OUT  MBKCS108
01917 *    THE STARTING RANGE AND/OR ENDING RANGE.  IF THE ENTRY ON     MBKCS108
01918 *    THE MAP SHOWS THAT THIS ITEM WAS SELECTED, ADD 1 TO THE      MBKCS108
01919 *    SELECTED COUNTER.                                            MBKCS108
01920 *                                                                 MBKCS108
01921 *    PERFORMED BY E200-MOVE-SELECTIONS-TO-COMMAREA.                  CL*92
01922 ******************************************************************MBKCS108
01923                                                                   MBKCS108
01924      IF (PV-ITEM-IN-USE < MB009-NUMBER-OF-ITEMS-READ) OR             CL179
01925         (PV-ITEM-IN-USE = MB009-NUMBER-OF-ITEMS-READ)                CL179
01926                                                                   MBKCS108
01927          PERFORM K100-SET-SUBSCRIPTS                              MBKCS108
01928                                                                      CL**2
01929 ******************************************************************   CL**2
01930 *    IF THE ENTRY IN THE BLOCK DOES NOT EQUAL THE ENTRY ON THE       CL**2
01931 *    MAP, SET THE SWITCH SAYING THAT THIS BLOCK HAS BEEN MODIFIED.   CL**2
01932 *                                                                    CL**2
01933 *    IF THE ENTRY IN THE BLOCK EQUALS 'S', SUBTRACT 1 FROM THE       CL**2
01934 *    NUMBER OF SELECTED ITEMS.                                       CL**2
01935 *                                                                    CL**2
01936 *    IF THE ENTRY IN THE BLOCK EQUALS 'B' AND THIS ENTRY WAS EQUAL   CL**2
01937 *    TO THE START OF SELECTED RANGE, MOVE 0 TO THE START OF          CL**2
01938 *    SELECTED RANGE.  IF THERE WAS MORE THAN ONE 'B' ON THE          CL**2
01939 *    SCREEN (ERROR), THE START OF SELECTED RANGE WILL THAN BE        CL**2
01940 *    RESET LATER TO POINT TO ONE OF THE OTHER 'B' ENTRIES.           CL**2
01941 *                                                                    CL**2
01942 *    THE SAME LOGIC APPLIES TO AN 'E' ENTRY.                         CL**2
01943 *                                                                    CL**2
01944 ******************************************************************   CL**2
01945                                                                   MBKCS108
01946          IF MB009-SELECTED-SW(MB009-BLOCK-SUB                        CL179
01947                               MB009-ITEM-SUB) NOT =                  CL179
01948             MB010I-ASELI(PS-SUB)                                     CL179
01949                                                                      CL**2
01950              SET MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB) TO TRUE       CL179
01951                                                                   MBKCS108
01952              IF MB009-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB)         CL179
01953                  SUBTRACT +1 FROM MB009-NUMBER-OF-ITEMS-SELECTED     CL179
01954              ELSE                                                 MBKCS108
01955                                                                   MBKCS108
01956                  IF MB009-BEGIN(MB009-BLOCK-SUB MB009-ITEM-SUB)      CL179
01957                                                                   MBKCS108
01958                      IF MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB       CL179
01959                                                MB009-ITEM-SUB) =     CL179
01960                         MB009-START-OF-SELECTED-RANGE                CL179
01961                                                                      CL**2
01962                          MOVE +0 TO MB009-START-OF-SELECTED-RANGE    CL179
01963                      END-IF                                       MBKCS108
01964                                                                   MBKCS108
01965                  ELSE                                             MBKCS108
01966                                                                   MBKCS108
01967                      IF MB009-END(MB009-BLOCK-SUB MB009-ITEM-SUB)    CL179
01968                                                                   MBKCS108
01969                          IF MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB   CL179
01970                                                    MB009-ITEM-SUB)   CL179
01971                          =  MB009-END-OF-SELECTED-RANGE              CL179
01972                                                                      CL**2
01973                              MOVE +0 TO                           MBKCS108
01974                                   MB009-END-OF-SELECTED-RANGE        CL179
01975                          END-IF                                   MBKCS108
01976                                                                   MBKCS108
01977                      END-IF                                       MBKCS108
01978                                                                   MBKCS108
01979                  END-IF                                           MBKCS108
01980                                                                   MBKCS108
01981              END-IF                                               MBKCS108
01982                                                                      CL**2
01983 ******************************************************************   CL**2
01984 *    IF THE ENTRY IN THE MAP EQUALS 'S', ADD 1 TO THE NUMBER OF      CL**2
01985 *    SELECTED ITEMS.                                                 CL**2
01986 *                                                                    CL**2
01987 *    IF THE ENTRY IN THE MAP EQUALS SPACES OR ERASE EOF, MOVE        CL**2
01988 *    SPACES TO THE ENTRY IN THE BLOCK.                               CL**2
01989 *                                                                    CL**2
01990 *    IF THE ENTRY IN THE MAP DOES NOT EQUAL SPACES OR ERASE EOF,     CL**2
01991 *    MOVE WHATEVER WAS ENTERED ON THE MAP TO THE ENTRY IN THE        CL**2
01992 *    BLOCK.                                                          CL**2
01993 *                                                                    CL**2
01994 ******************************************************************   CL**2
01995                                                                   MBKCS108
01996              IF MB010I-ASELI(PS-SUB) = PC-SELECTED                   CL179
01997                  ADD +1 TO MB009-NUMBER-OF-ITEMS-SELECTED            CL179
01998              END-IF                                               MBKCS108
01999                                                                   MBKCS108
02000              IF (MB010I-ASELA(PS-SUB) = PC-ERASE-EOF) OR             CL179
02001                 (MB010I-ASELI(PS-SUB) = SPACES)       OR             CL179
02002                 (MB010I-ASELI(PS-SUB) = LOW-VALUES)                  CL179
02003                  MOVE SPACES                TO                       CL*73
02004                       MB009-SELECTED-SW(MB009-BLOCK-SUB              CL179
02005                                         MB009-ITEM-SUB)              CL179
02006              ELSE                                                 MBKCS108
02007                  MOVE MB010I-ASELI(PS-SUB) TO                        CL179
02008                       MB009-SELECTED-SW(MB009-BLOCK-SUB              CL179
02009                                         MB009-ITEM-SUB)              CL179
02010              END-IF                                               MBKCS108
02011                                                                   MBKCS108
02012          END-IF                                                   MBKCS108
02013                                                                   MBKCS108
02014      END-IF.                                                      MBKCS108
02015                                                                   MBKCS108
02016      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS108
02017      ADD +1 TO PS-SUB.                                            MBKCS108
02018  EJECT                                                            MBKCS108
02019  F400-SETUP-INTER-APPL.                                           MBKCS108
02020                                                                   MBKCS108
02021 ******************************************************************MBKCS108
02022 *    PLACING THE 4 DIGIT CODE FOR THIS TRANSACTION IN THE            CL**2
02023 *    STANDARD COMMAREA WILL TELL THE TRANSACTION THAT WE ARE         CL**2
02024 *    GOING TO WHETHER OR NOT THAT THEY CAN USE THE INTER             CL**2
02025 *    APPLICATION COMMAREA.                                           CL**2
02026 *                                                                    CL**2
02027 *    IN THE INTER APPLICATION COMMAREA, WE MUST PUT THE TEMPORARY    CL**2
02028 *    STORAGE QUEUE NAME AND THE NUMBER OF SELECTED ITEMS.  THIS      CL**2
02029 *    WILL ALLOW THE TRANSACTION WE ARE GOING TO, TO READ THE         CL**2
02030 *    QUEUE AND PROCESS THE SELECTED ITEMS.                           CL**2
02031 *                                                                    CL**2
02032 *    PERFORMED BY E300-NAVIGATE.                                     CL*92
02033 ******************************************************************MBKCS108
02034                                                                   MBKCS108
02035 *    MOVE LOW-VALUES               TO INTER-APPL-COMM-AREA.          CL232
02036                                                                   MBKCS108
02037      SET DP020-MB-SUB-CLASS-LIST TO TRUE                             CL205
02038                                                                   MBKCS108
02039 *    MOVE MB009-TEMP-QUEUE-NAME          TO                          CL208
02040 *         MB005-TEMP-QUEUE-NAME.                                     CL208
02041      MOVE MB009-NUMBER-OF-ITEMS-SELECTED TO                          CL179
02042           MB005-NUMBER-OF-SELECTED-ITEMS.                            CL179
02043      MOVE MB009-CO-NUMBER                TO                          CL179
02044           MB005-CO-NUMBER.                                           CL179
02045      MOVE MB009-DEPT-NUMBER              TO                          CL179
02046           MB005-DEPT-NUMBER.                                         CL179
02047      MOVE MB009-MCLASS-NUMBER            TO                          CL179
02048           MB005-MCLASS-NUMBER.                                       CL179
02049      MOVE MB009-SCLASS-NUMBER            TO                          CL179
02050           MB005-SCLASS-NUMBER.                                       CL179
02051  EJECT                                                            MBKCS108
02052  F500-BROWSE.                                                     MBKCS108
02053                                                                   MBKCS108
02054 ******************************************************************MBKCS108
02055 *    CALCULATE THE NEW TOP AND BOTTOM ITEM NUMBERS BASED ON WHERE MBKCS108
02056 *    THE USER SPECIFIED TO GO ON THE LIST AND DETERMINE WHERE     MBKCS108
02057 *    THESE ITEMS CAN BE FOUND (I.E. ARRAY, TEMPORARY STORAGE).    MBKCS108
02058 *                                                                 MBKCS108
02059 *    PERFORMED BY E400-DISPLAY-TOP-OF-LIST.                          CL*92
02060 *    PERFORMED BY E500-BROWSE-BACKWARD.                              CL*92
02061 *    PERFORMED BY E600-BROWSE-FORWARD.                               CL*92
02062 *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                        CL*92
02063 ******************************************************************MBKCS108
02064                                                                      CL**2
02065 ******************************************************************   CL**2
02066 *    USER HITS PAGE BACKWARD WHEN ALREADY AT THE TOP OF THE LIST.    CL**2
02067 ******************************************************************   CL**2
02068                                                                   MBKCS108
02069      IF PV-TOP-ITEM < +1                                          MBKCS108
02070          MOVE +1                         TO PV-TOP-ITEM              CL*28
02071                                                                   MBKCS108
02072          IF PS-CURSOR-NOT-POSITIONED                                 CL*28
02073              MOVE PC-TSYMSG-00069        TO DP020-MSG-NUMBER         CL186
02074              SET PS-CURSOR-POSITIONED    TO TRUE                     CL*28
02075              SET PS-INFORMATIONAL        TO TRUE                     CL*28
02076              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL*28
02077          END-IF                                                      CL*28
02078      END-IF.                                                      MBKCS108
02079                                                                      CL**2
02080      IF MB009-NO-ITEMS-LEFT-ON-DB                                    CL179
02081                                                                      CL**2
02082 ******************************************************************   CL**2
02083 *    USER HITS PAGE FORWARD WHEN ALREADY AT THE END OF THE LIST.     CL**2
02084 ******************************************************************   CL**2
02085                                                                   MBKCS108
02086          IF PV-TOP-ITEM > MB009-NUMBER-OF-ITEMS-READ                 CL179
02087              IF  DP020-SRC-AID = DP016-ENTER                         CL237
02088                  MOVE MB009-NUMBER-OF-ITEMS-READ TO PV-TOP-ITEM      CL237
02089              ELSE                                                    CL237
02090                  MOVE MB009-ITEM-AT-TOP-OF-PANEL TO PV-TOP-ITEM      CL237
02091              END-IF                                                  CL237
02092              IF PS-CURSOR-NOT-POSITIONED                             CL*28
02093                  MOVE -1                     TO ANAVCMDL             CL*28
02094                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL*28
02095                  SET PS-CURSOR-POSITIONED    TO TRUE                 CL*52
02096                  SET PS-INFORMATIONAL        TO TRUE                 CL235
02097                  MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER     CL235
02098                                                                      CL*28
02099              END-IF                                                  CL*28
02100                                                                   MBKCS108
02101          END-IF                                                   MBKCS108
02102                                                                   MBKCS108
02103      END-IF.                                                      MBKCS108
02104                                                                   MBKCS108
02105      COMPUTE PV-BOTTOM-ITEM =                                     MBKCS108
02106          (PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1.                  MBKCS108
02107                                                                   MBKCS108
02108      IF MB009-NO-ITEMS-LEFT-ON-DB                                    CL179
02109                                                                      CL**2
02110 ******************************************************************   CL**2
02111 *    USER HITS PAGE FORWARD, AND THE NEXT PAGE WILL CAUSE US         CL**2
02112 *    TO BE AT THE END OF THE LIST.  IF TOP ITEM EQUALS 1, THE        CL**2
02113 *    MESSAGE WOULD HAVE BEEN FLAGGED ALREADY ABOVE.                  CL**2
02114 ******************************************************************   CL**2
02115                                                                   MBKCS108
02116          IF PV-BOTTOM-ITEM > MB009-NUMBER-OF-ITEMS-READ              CL179
02117              MOVE MB009-NUMBER-OF-ITEMS-READ TO PV-BOTTOM-ITEM       CL179
02118                                                                   MBKCS108
02119              IF PS-CURSOR-NOT-POSITIONED                             CL*69
02120                                                                      CL*69
02121                  IF PV-TOP-ITEM NOT = +1                             CL*69
02122                      SET PS-INFORMATIONAL        TO TRUE             CL*69
02123                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*69
02124                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*69
02125                      MOVE PC-TSYMSG-00070        TO                  CL186
02126                           DP020-MSG-NUMBER                           CL*69
02127                  END-IF                                              CL*69
02128                                                                      CL*69
02129              END-IF                                                  CL*69
02130                                                                   MBKCS108
02131          END-IF                                                   MBKCS108
02132                                                                   MBKCS108
02133      END-IF.                                                      MBKCS108
02134                                                                      CL**2
02135 ******************************************************************   CL**2
02136 *    DETEMINE WHETHER THE ITEMS WE NEED ARE IN WORKING STORAGE,      CL**2
02137 *    TEMPORARY STORAGE, OR NEITHER.                                  CL**2
02138 ******************************************************************   CL**2
02139                                                                   MBKCS108
02140      PERFORM G300-DETERMINE-WHAT-IS-WHERE.                        MBKCS108
02141                                                                   MBKCS108
02142      IF PS-TOP-ITEM-IN-ARRAY                                      MBKCS108
02143                                                                   MBKCS108
02144          IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                           MBKCS108
02145                                                                      CL**2
02146 ******************************************************************   CL**2
02147 *    TOP ITEM IS IN WORKING STORAGE AND BOTTOM ITEM IS IN            CL**2
02148 *    TEMPORARY STORAGE.                                              CL**2
02149 ******************************************************************   CL**2
02150                                                                   MBKCS108
02151              IF PS-BOTTOM-ITEM-IN-TEMP                            MBKCS108
02152                  PERFORM G500-SCROLL-FORWARD                      MBKCS108
02153              ELSE                                                 MBKCS108
02154                                                                      CL**2
02155 ******************************************************************   CL**2
02156 *    TOP ITEM IS IN WORKING STORAGE BUT BOTTOM ITEM HAS NOT          CL**2
02157 *    BEEN READ YET.  IF THE LAST LOGICAL BLOCK IS NOT IN WORKING     CL**2
02158 *    STORAGE, READ IT INTO WORKING STORAGE SO WE CAN START FILLING   CL**2
02159 *    IT UP WITH THE NEW ITEMS READ.                                  CL**2
02160 ******************************************************************   CL**2
02161                                                                   MBKCS108
02162                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS108
02163                      PERFORM G500-SCROLL-FORWARD                  MBKCS108
02164                  END-IF                                           MBKCS108
02165                                                                   MBKCS108
02166                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS108
02167                                                                   MBKCS108
02168              END-IF                                               MBKCS108
02169                                                                   MBKCS108
02170          END-IF                                                   MBKCS108
02171                                                                   MBKCS108
02172      ELSE                                                         MBKCS108
02173                                                                      CL**2
02174 ******************************************************************   CL**2
02175 *    BOTTOM ITEM IS IN WORKING STORAGE AND TOP ITEM IS IN            CL**2
02176 *    TEMPORARY STORAGE.                                              CL**2
02177 ******************************************************************   CL**2
02178                                                                   MBKCS108
02179          IF PS-BOTTOM-ITEM-IN-ARRAY                               MBKCS108
02180              PERFORM G600-SCROLL-BACKWARD                         MBKCS108
02181          ELSE                                                     MBKCS108
02182                                                                      CL**2
02183 ******************************************************************   CL**2
02184 *    TOP ITEM AND BOTTOM ITEM ARE NOT IN WORKING STORAGE.            CL**2
02185 ******************************************************************   CL**2
02186                                                                   MBKCS108
02187              IF PS-TOP-ITEM-IN-TEMP                               MBKCS108
02188                  PERFORM G700-GET-TOP-ITEMS-BLOCK                 MBKCS108
02189                  PERFORM G300-DETERMINE-WHAT-IS-WHERE             MBKCS108
02190                                                                      CL**2
02191 ******************************************************************   CL**2
02192 *    TOP ITEM IS IN TEMPORARY STORAGE AND BOTTOM ITEM HAS NOT        CL**2
02193 *    BEEN READ YET.                                                  CL**2
02194 ******************************************************************   CL**2
02195                                                                   MBKCS108
02196                  IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                   MBKCS108
02197                      PERFORM G400-READ-ITEMS-INTO-ARRAY           MBKCS108
02198                  END-IF                                           MBKCS108
02199                                                                   MBKCS108
02200              ELSE                                                 MBKCS108
02201                                                                      CL**2
02202 ******************************************************************   CL**2
02203 *    TOP AND BOTTOM ITEM HAVE NOT BEEN READ YET.  IF THE LAST        CL**2
02204 *    LOGICAL BLOCK IS NOT IN WORKING STORAGE, READ IT INTO           CL**4
02205 *    WORKING STORAGE SO WE CAN START FILLING IT UP WITH THE          CL**4
02206 *    NEW ITEMS READ.                                                 CL**2
02207 ******************************************************************   CL**2
02208                                                                   MBKCS108
02209                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS108
02210                      PERFORM G800-GET-LAST-BLOCK-IN-TEMP          MBKCS108
02211                  END-IF                                           MBKCS108
02212                                                                   MBKCS108
02213                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS108
02214              END-IF                                               MBKCS108
02215                                                                   MBKCS108
02216          END-IF                                                   MBKCS108
02217                                                                   MBKCS108
02218      END-IF.                                                      MBKCS108
02219                                                                   MBKCS108
02220      IF PV-TOP-ITEM = MB009-ITEM-AT-TOP-OF-PANEL                     CL179
02221          SET PS-SAME-ITEMS-DISPLAYED      TO TRUE                    CL*69
02222      ELSE                                                            CL*69
02223          SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE                    CL*69
02224          MOVE PV-TOP-ITEM TO MB009-ITEM-AT-TOP-OF-PANEL              CL179
02225      END-IF.                                                         CL*69
02226                                                                   MBKCS108
02227      PERFORM G100-DISPLAY-PANEL.                                  MBKCS108
02228  EJECT                                                            MBKCS108
02229  F600-CLEAR-SELECTED-INDS.                                        MBKCS108
02230                                                                   MBKCS108
02231 ******************************************************************MBKCS108
02232 *    MARK EACH ITEM AS BEING NOT SELECTED OR SELECTED BASED ON       CL*93
02233 *    WHICH FUNCTION KEY THAT WAS HIT (DESELECT ALL OR SELECT ALL)    CL*93
02234 *                                                                 MBKCS108
02235 *    PERFORMED BY E700-SELECT-DESELECT-ALL.                          CL*93
02236 ******************************************************************MBKCS108
02237                                                                   MBKCS108
02238      IF MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB                       CL179
02239                                MB009-ITEM-SUB) NOT = +0              CL179
02240                                                                   MBKCS108
02241          IF DP020-FK-DESELECT(DP020-SRC-AID)                         CL*32
02242                                                                      CL*32
02243              IF NOT MB009-NO-SELECT(MB009-BLOCK-SUB                  CL179
02244                                     MB009-ITEM-SUB)                  CL179
02245                  SET MB009-NO-SELECT(MB009-BLOCK-SUB                 CL179
02246                                      MB009-ITEM-SUB)   TO TRUE       CL179
02247                                                                   MBKCS108
02248                  SET MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB)           CL179
02249                                                        TO TRUE       CL*32
02250              END-IF                                                  CL*32
02251          ELSE                                                        CL*32
02252              IF NOT MB009-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB)     CL179
02253                  SET MB009-SELECT(MB009-BLOCK-SUB                    CL179
02254                                   MB009-ITEM-SUB)      TO TRUE       CL179
02255                  ADD +1 TO MB009-NUMBER-OF-ITEMS-SELECTED            CL179
02256                  SET MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB)           CL179
02257                                                        TO TRUE       CL*32
02258              END-IF                                                  CL*32
02259                                                                      CL*32
02260          END-IF                                                      CL*32
02261                                                                   MBKCS108
02262      END-IF.                                                      MBKCS108
02263  EJECT                                                            MBKCS108
02264  G100-DISPLAY-PANEL.                                              MBKCS108
02265                                                                   MBKCS108
02266 ******************************************************************MBKCS108
02267 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.       MBKCS108
02268 *                                                                 MBKCS108
02269 ******************************************************************MBKCS108
02270                                                                   MBKCS108
02271      PERFORM DP017-0000-GET-CURR-DATE-TIME.                       MBKCS108
02272      MOVE DP017-EDITED-TIME-HMS                                      CL233
02273                                TO  ATIMEO.                           CL233
02274                                                                   MBKCS108
02275      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS108
02276          PERFORM DP002-0000-FORMAT-TITLE-LINES                    MBKCS108
02277      END-IF.                                                      MBKCS108
02278                                                                   MBKCS108
02279      PERFORM H200-FORMAT-PANEL-BODY.                              MBKCS108
02280                                                                   MBKCS108
02281      PERFORM DP005-0000-FORMAT-MSG-LINES.                         MBKCS108
02282                                                                   MBKCS108
02283      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS108
02284          PERFORM DP009-0000-LOAD-FK-LINES                         MBKCS108
02285      END-IF.                                                      MBKCS108
02286                                                                   MBKCS108
02287      PERFORM DP008-0000-WRITE-PANEL.                              MBKCS108
02288  EJECT                                                            MBKCS108
02289  G200-MARK-SELECTED-INDS.                                         MBKCS108
02290                                                                   MBKCS108
02291 ******************************************************************MBKCS108
02292 *    MARK EACH ITEM THAT FALLS WITHIN THE BEGINNING AND ENDING    MBKCS108
02293 *    RANGE AS BEING SELECTED.                                     MBKCS108
02294 *                                                                 MBKCS108
02295 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL*92
02296 ******************************************************************MBKCS108
02297                                                                   MBKCS108
02298      IF (MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB                      CL179
02299                                 MB009-ITEM-SUB) >                    CL179
02300          MB009-START-OF-SELECTED-RANGE) OR                           CL179
02301                                                                      CL**4
02302         (MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB                      CL179
02303                                 MB009-ITEM-SUB) =                    CL179
02304          MB009-START-OF-SELECTED-RANGE)                              CL179
02305                                                                   MBKCS108
02306          IF (MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB                  CL179
02307                                     MB009-ITEM-SUB) <                CL179
02308              MB009-END-OF-SELECTED-RANGE) OR                         CL179
02309                                                                      CL**4
02310             (MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB                  CL179
02311                                     MB009-ITEM-SUB) =                CL179
02312              MB009-END-OF-SELECTED-RANGE)                            CL179
02313                                                                   MBKCS108
02314              IF NOT MB009-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB)     CL179
02315                                                                      CL**2
02316                  SET MB009-SELECT(MB009-BLOCK-SUB                    CL179
02317                                   MB009-ITEM-SUB)          TO TRUE   CL179
02318                                                                      CL**2
02319                  SET MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB) TO TRUE   CL179
02320                                                                   MBKCS108
02321                  ADD +1 TO MB009-NUMBER-OF-ITEMS-SELECTED            CL179
02322              END-IF                                               MBKCS108
02323                                                                   MBKCS108
02324          END-IF                                                   MBKCS108
02325                                                                   MBKCS108
02326      END-IF.                                                      MBKCS108
02327  EJECT                                                            MBKCS108
02328  G300-DETERMINE-WHAT-IS-WHERE.                                    MBKCS108
02329                                                                   MBKCS108
02330 ******************************************************************MBKCS108
02331 *    DETERMINE WHETHER THE TOP ITEM AND BOTTOM ITEM CAN BE FOUND  MBKCS108
02332 *    IN THE ARRAY, IN TEMPORARY STORAGE, OR NEITHER.              MBKCS108
02333 *                                                                 MBKCS108
02334 *    PERFORMED BY F500-BROWSE.                                       CL*93
02335 ******************************************************************MBKCS108
02336                                                                   MBKCS108
02337 ******************************************************************   CL**2
02338 *    CALCULATE THE FIRST-ITEM-IN-ARRAY (WORKING   STORAGE)           CL**2
02339 *                  LAST-ITEM-IN-ARRAY  (WORKING   STORAGE)           CL**2
02340 *                  LAST-ITEM-IN-TEMP   (TEMPORARY STORAGE)           CL**2
02341 *                                                                    CL**2
02342 *    BY COMPARING THE TOP AND BOTTOM ITEMS TO THESE, WE CAN          CL**2
02343 *    FIGURE OUT WHERE THE ITEMS ARE STORED.                          CL**2
02344 ******************************************************************   CL**2
02345                                                                      CL**2
02346      MOVE MB009-LIST-ITEM-NUMBER(1 1) TO PV-FIRST-ITEM-IN-ARRAY.     CL179
02347                                                                      CL**2
02348                                                                   MBKCS108
02349      COMPUTE PV-LAST-ITEM-IN-ARRAY =                              MBKCS108
02350          (PV-FIRST-ITEM-IN-ARRAY +                                MBKCS108
02351          (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-BLOCK)) - 1.      MBKCS108
02352                                                                   MBKCS108
02353      IF PV-LAST-ITEM-IN-ARRAY > MB009-NUMBER-OF-ITEMS-READ           CL179
02354          MOVE MB009-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-ARRAY    CL179
02355      END-IF.                                                      MBKCS108
02356                                                                   MBKCS108
02357      COMPUTE PV-LAST-ITEM-IN-TEMP =                               MBKCS108
02358          MB009-HIGHEST-BLOCK-WRITTEN * PC-ITEMS-PER-BLOCK.           CL179
02359                                                                   MBKCS108
02360      IF PV-LAST-ITEM-IN-TEMP > MB009-NUMBER-OF-ITEMS-READ            CL179
02361          MOVE MB009-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-TEMP     CL179
02362      END-IF.                                                      MBKCS108
02363                                                                   MBKCS108
02364      IF (PV-TOP-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR                 MBKCS108
02365         (PV-TOP-ITEM = PV-FIRST-ITEM-IN-ARRAY)                    MBKCS108
02366                                                                   MBKCS108
02367          IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-ARRAY) OR              MBKCS108
02368             (PV-TOP-ITEM = PV-LAST-ITEM-IN-ARRAY)                 MBKCS108
02369              SET PS-TOP-ITEM-IN-ARRAY     TO TRUE                 MBKCS108
02370          ELSE                                                     MBKCS108
02371              SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                 MBKCS108
02372          END-IF                                                   MBKCS108
02373                                                                   MBKCS108
02374      ELSE                                                         MBKCS108
02375          SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                     MBKCS108
02376      END-IF.                                                      MBKCS108
02377                                                                   MBKCS108
02378      IF (PV-BOTTOM-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR              MBKCS108
02379         (PV-BOTTOM-ITEM = PV-FIRST-ITEM-IN-ARRAY)                 MBKCS108
02380                                                                   MBKCS108
02381          IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-ARRAY) OR           MBKCS108
02382             (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-ARRAY)              MBKCS108
02383              SET PS-BOTTOM-ITEM-IN-ARRAY     TO TRUE              MBKCS108
02384          ELSE                                                     MBKCS108
02385              SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE              MBKCS108
02386          END-IF                                                   MBKCS108
02387                                                                   MBKCS108
02388      ELSE                                                         MBKCS108
02389          SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE                  MBKCS108
02390      END-IF.                                                      MBKCS108
02391                                                                   MBKCS108
02392      IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-TEMP) OR                   MBKCS108
02393         (PV-TOP-ITEM = PV-LAST-ITEM-IN-TEMP)                      MBKCS108
02394          SET PS-TOP-ITEM-IN-TEMP     TO TRUE                      MBKCS108
02395      ELSE                                                         MBKCS108
02396          SET PS-TOP-ITEM-NOT-IN-TEMP TO TRUE                      MBKCS108
02397      END-IF.                                                      MBKCS108
02398                                                                   MBKCS108
02399      IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-TEMP) OR                MBKCS108
02400         (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-TEMP)                   MBKCS108
02401          SET PS-BOTTOM-ITEM-IN-TEMP     TO TRUE                   MBKCS108
02402      ELSE                                                         MBKCS108
02403          SET PS-BOTTOM-ITEM-NOT-IN-TEMP TO TRUE                   MBKCS108
02404      END-IF.                                                      MBKCS108
02405                                                                   MBKCS108
02406      IF PV-LAST-ITEM-IN-TEMP > PV-LAST-ITEM-IN-ARRAY              MBKCS108
02407          SET PS-LAST-BLOCK-NOT-IN-ARRAY TO TRUE                   MBKCS108
02408      ELSE                                                         MBKCS108
02409          SET PS-LAST-BLOCK-IN-ARRAY     TO TRUE                   MBKCS108
02410      END-IF.                                                      MBKCS108
02411  EJECT                                                            MBKCS108
02412  G400-READ-ITEMS-INTO-ARRAY.                                      MBKCS108
02413                                                                   MBKCS108
02414 ******************************************************************MBKCS108
02415 *    DETERMINE THE SPOT IN THE ARRAY WHERE THE LAST ITEM FROM     MBKCS108
02416 *    THE DATA BASE WAS ADDED SO WE KNOW WHERE TO ADD THE NEW      MBKCS108
02417 *    ITEMS.                                                       MBKCS108
02418 *                                                                 MBKCS108
02419 *    PERFORMED BY F500-BROWSE.                                       CL*93
02420 ******************************************************************MBKCS108
02421                                                                   MBKCS108
02422      COMPUTE PS-ITEM-SUB =                                        MBKCS108
02423          (MB009-NUMBER-OF-ITEMS-READ -                               CL179
02424           MB009-LIST-ITEM-NUMBER(1 1)) + 1.                          CL179
02425                                                                   MBKCS108
02426      MOVE PS-ITEM-SUB TO MB009-ITEM-SUB.                             CL179
02427                                                                   MBKCS108
02428      IF MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL179
02429          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB009-ITEM-SUB             CL179
02430                                                                   MBKCS108
02431          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB009-BLOCK-SUB              CL179
02432      ELSE                                                         MBKCS108
02433          MOVE +1                     TO MB009-BLOCK-SUB              CL179
02434      END-IF.                                                      MBKCS108
02435                                                                   MBKCS108
02436      MOVE PV-BOTTOM-ITEM TO PV-HIGHEST-ITEM-TO-BE-READ.           MBKCS108
02437                                                                      CL164
02438      PERFORM D500-OPEN-CURSOR.                                       CL164
02439                                                                      CL164
02440      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS108
02441                                                                   MBKCS108
02442      IF MB009-NO-ITEMS-LEFT-ON-DB                                    CL179
02443                                                                   MBKCS108
02444          IF PV-TOP-ITEM > MB009-NUMBER-OF-ITEMS-READ                 CL179
02445              MOVE MB009-NUMBER-OF-ITEMS-READ TO PV-TOP-ITEM          CL179
02446          END-IF                                                   MBKCS108
02447                                                                   MBKCS108
02448          MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER             CL185
02449          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL*29
02450          SET PS-INFORMATIONAL        TO TRUE                         CL*29
02451      END-IF.                                                      MBKCS108
02452  EJECT                                                            MBKCS108
02453  G500-SCROLL-FORWARD.                                             MBKCS108
02454                                                                   MBKCS108
02455 ******************************************************************MBKCS108
02456 *    IF THE TOP ITEM WE NEED IS IN THE ARRAY, BUT THE BOTTOM         CL**2
02457 *    ITEM WE NEED IS IN TEMPORARY STORAGE OR HAS NOT BEEN READ       CL**2
02458 *    YET, WRITE OUT THE 1ST BLOCK IN THE ARRAY TO TEMPORARY          CL**2
02459 *    STORAGE.  MOVE THE 2ND BLOCK IN THE ARRAY TO THE 1ST BLOCK      CL**2
02460 *    AND READ THE BLOCK NEEDED INTO THE 2ND BLOCK OF THE ARRAY.      CL**2
02461 *    THE TOP ITEM WILL BE LOCATED IN THE 1ST BLOCK, AND THE          CL**2
02462 *    BOTTOM ITEM WILL EITHER BE LOCATED IN THE 2ND BLOCK OR          CL**2
02463 *    WILL BE PLACED IN THE 2ND BLOCK.                                CL**2
02464 *                                                                 MBKCS108
02465 *    PERFORMED BY F500-BROWSE.                                       CL*93
02466 ******************************************************************MBKCS108
02467                                                                   MBKCS108
02468      MOVE +1 TO MB009-BLOCK-SUB.                                     CL179
02469                                                                   MBKCS108
02470      PERFORM Z150-WRITE-TEMP-STORAGE.                             MBKCS108
02471                                                                   MBKCS108
02472      MOVE MB009-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY) TO             CL179
02473           MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB).                      CL179
02474                                                                   MBKCS108
02475      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS108
02476          1 + ((PV-BOTTOM-ITEM - 1) / PC-ITEMS-PER-BLOCK).         MBKCS108
02477                                                                   MBKCS108
02478      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS108
02479                                                                   MBKCS108
02480      ADD +1 TO MB009-BLOCK-SUB.                                      CL179
02481                                                                   MBKCS108
02482      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS108
02483  EJECT                                                            MBKCS108
02484  G600-SCROLL-BACKWARD.                                            MBKCS108
02485                                                                   MBKCS108
02486 ******************************************************************MBKCS108
02487 *    IF TOP ITEM WE NEED IS NOT IN THE ARRAY, BUT THE BOTTOM      MBKCS108
02488 *    ITEM IS, WRITE BOTH BLOCKS TO TEMPORARY STORAGE.  MOVE       MBKCS108
02489 *    THE 1ST BLOCK IN THE ARRAY TO THE 2ND BLOCK OF THE ARRAY.    MBKCS108
02490 *    READ THE BLOCK THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK MBKCS108
02491 *    OF THE ARRAY.                                                MBKCS108
02492 *                                                                 MBKCS108
02493 *    PERFORMED BY F500-BROWSE.                                       CL*93
02494 ******************************************************************MBKCS108
02495                                                                   MBKCS108
02496      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02497                                                                   MBKCS108
02498      MOVE +1                                   TO                 MBKCS108
02499           MB009-BLOCK-SUB.                                           CL179
02500                                                                   MBKCS108
02501      MOVE MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB) TO                    CL179
02502           MB009-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY).               CL179
02503                                                                   MBKCS108
02504      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS108
02505          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS108
02506                                                                   MBKCS108
02507      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS108
02508                                                                   MBKCS108
02509      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS108
02510  EJECT                                                            MBKCS108
02511  G700-GET-TOP-ITEMS-BLOCK.                                        MBKCS108
02512                                                                   MBKCS108
02513 ******************************************************************MBKCS108
02514 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS108
02515 *    ARRAY, BUT THE TOP ITEM IS IN TEMPORARY STORAGE, WRITE BOTH  MBKCS108
02516 *    BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ THE BLOCK    MBKCS108
02517 *    THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK OF THE ARRAY.  MBKCS108
02518 *    IF THERE IS ANOTHER BLOCK IN TEMPORARY STORAGE FOLLOWING     MBKCS108
02519 *    THE ONE JUST READ, READ THAT ONE INTO THE 2ND BLOCK OF THE   MBKCS108
02520 *    ARRAY, OTHERWISE INITIALIZE THE 2ND BLOCK OF THE ARRAY.      MBKCS108
02521 *                                                                 MBKCS108
02522 *    PERFORMED BY F500-BROWSE.                                       CL*93
02523 ******************************************************************MBKCS108
02524                                                                   MBKCS108
02525      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02526                                                                   MBKCS108
02527      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS108
02528          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS108
02529                                                                   MBKCS108
02530      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS108
02531      MOVE +1                   TO MB009-BLOCK-SUB.                   CL179
02532                                                                   MBKCS108
02533      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS108
02534                                                                   MBKCS108
02535      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS108
02536      ADD +1 TO MB009-BLOCK-SUB.                                      CL179
02537                                                                   MBKCS108
02538      IF (PV-BLOCK-NUMBER < MB009-HIGHEST-BLOCK-WRITTEN) OR           CL179
02539         (PV-BLOCK-NUMBER = MB009-HIGHEST-BLOCK-WRITTEN)              CL179
02540          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS108
02541      ELSE                                                         MBKCS108
02542          PERFORM I100-INITIALIZE-BLOCK                            MBKCS108
02543      END-IF.                                                      MBKCS108
02544  EJECT                                                            MBKCS108
02545  G800-GET-LAST-BLOCK-IN-TEMP.                                     MBKCS108
02546                                                                   MBKCS108
02547 ******************************************************************MBKCS108
02548 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS108
02549 *    ARRAY AND BOTH ITEMS ARE NOT IN TEMPORARY STORAGE (THE       MBKCS108
02550 *    ITEMS HAVE NOT BEEN READ FROM THE DATA BASE YET), AND THE    MBKCS108
02551 *    LAST ITEM READ FROM THE DATA BASE IS IN TEMPORARY STORAGE,   MBKCS108
02552 *    WRITE BOTH BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ   MBKCS108
02553 *    THE HIGHEST BLOCK IN TEMPORARY STORAGE INTO THE 1ST BLOCK    MBKCS108
02554 *    OF THE ARRAY, AND INITIALIZE THE 2ND BLOCK IN THE ARRAY.     MBKCS108
02555 *    THE 2ND BLOCK WILL BE USED FOR THE NEW ITEMS TO BE READ.     MBKCS108
02556 *    THE 1ST BLOCK WILL NOW CONTAIN THE ITEMS THAT WERE LAST      MBKCS108
02557 *    READ FROM THE DATA BASE.                                     MBKCS108
02558 *                                                                 MBKCS108
02559 *    PERFORMED BY F500-BROWSE.                                       CL*93
02560 ******************************************************************MBKCS108
02561                                                                   MBKCS108
02562      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02563                                                                   MBKCS108
02564      MOVE MB009-HIGHEST-BLOCK-WRITTEN TO PV-BLOCK-NUMBER.            CL179
02565      MOVE +1                          TO MB009-BLOCK-SUB.            CL179
02566                                                                   MBKCS108
02567      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS108
02568                                                                   MBKCS108
02569      ADD +1 TO MB009-BLOCK-SUB.                                      CL179
02570                                                                   MBKCS108
02571      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS108
02572  EJECT                                                            MBKCS108
02573  H100-READ-ITEMS-FROM-DB.                                         MBKCS108
02574                                                                   MBKCS108
02575 ******************************************************************MBKCS108
02576 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE     MBKCS108
02577 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,      MBKCS108
02578 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND   MBKCS108
02579 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE      MBKCS108
02580 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                    MBKCS108
02581 *                                                                 MBKCS108
02582 ******************************************************************MBKCS108
02583                                                                   MBKCS108
02584      PERFORM                                                      MBKCS108
02585          UNTIL (MB009-NO-ITEMS-LEFT-ON-DB)   OR                      CL179
02586                (MB009-NUMBER-OF-ITEMS-READ =                         CL179
02587                 PV-HIGHEST-ITEM-TO-BE-READ)                          CL102
02588                                                                   MBKCS108
02589          IF MB009-ITEM-SUB < PC-ITEMS-PER-BLOCK                      CL179
02590              ADD +1 TO MB009-ITEM-SUB                                CL179
02591          ELSE                                                     MBKCS108
02592              ADD +1 TO MB009-BLOCK-SUB                               CL179
02593                                                                   MBKCS108
02594              MOVE +1 TO MB009-ITEM-SUB                               CL179
02595                                                                   MBKCS108
02596              IF MB009-BLOCK-SUB > PC-MAX-BLOCKS-IN-ARRAY             CL179
02597                  MOVE +1 TO MB009-BLOCK-SUB                          CL179
02598                                                                   MBKCS108
02599                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS108
02600                                                                   MBKCS108
02601                  MOVE MB009-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY)    CL179
02602                  TO   MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB)           CL179
02603                                                                   MBKCS108
02604                  ADD +1 TO MB009-BLOCK-SUB                           CL179
02605                                                                   MBKCS108
02606                  PERFORM I100-INITIALIZE-BLOCK                    MBKCS108
02607              END-IF                                               MBKCS108
02608                                                                   MBKCS108
02609          END-IF                                                   MBKCS108
02610                                                                   MBKCS108
02611          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL*41
02612                                                                   MBKCS108
02613          IF MB009-ITEMS-LEFT-ON-DB                                   CL179
02614              ADD +1 TO MB009-NUMBER-OF-ITEMS-READ                    CL179
02615                                                                   MBKCS108
02616              MOVE MB009-NUMBER-OF-ITEMS-READ TO                      CL179
02617                   MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB             CL179
02618                                          MB009-ITEM-SUB)             CL179
02619                                                                   MBKCS108
02620              MOVE MDSEAR-GMA-NBR             TO                      CL*47
02621                   MB009-ITEM-GMA(MB009-BLOCK-SUB                     CL179
02622                                  MB009-ITEM-SUB)                     CL179
02623              MOVE MDSEAR-DMA-NBR             TO                      CL*47
02624                   MB009-ITEM-DMA(MB009-BLOCK-SUB                     CL179
02625                                  MB009-ITEM-SUB)                     CL179
02626              MOVE MDSEAR-DEPT-NBR            TO                      CL*47
02627                   MB009-ITEM-DEPT(MB009-BLOCK-SUB                    CL179
02628                                   MB009-ITEM-SUB)                    CL179
02629              MOVE MDSEAR-MAJ-CL-NBR          TO                      CL*47
02630                   MB009-ITEM-MCLASS(MB009-BLOCK-SUB                  CL179
02631                                     MB009-ITEM-SUB)                  CL179
02632              MOVE MDSEAR-SUB-CL-NBR          TO                      CL*47
02633                   MB009-ITEM-SCLASS(MB009-BLOCK-SUB                  CL179
02634                                     MB009-ITEM-SUB)                  CL179
02635              MOVE MDSEAR-DESC                TO                      CL*48
02636                   MB009-ITEM-DESC(MB009-BLOCK-SUB                    CL179
02637                                   MB009-ITEM-SUB)                    CL179
02638              MOVE MDSECL-POS-DESC            TO                      CL189
02639                   MB009-ITEM-POSDESC(MB009-BLOCK-SUB                 CL189
02640                                      MB009-ITEM-SUB)                 CL189
02641              MOVE MDSECL-TAX-DFLT-IND        TO                      CL189
02642                   MB009-ITEM-TAXD(MB009-BLOCK-SUB                    CL189
02643                                   MB009-ITEM-SUB)                    CL189
02644              IF  MDSEAR-END-DTE LESS THAN MB009-DB2-CURRENT-DATE     CL210
02645                  MOVE DP015-TURQUOISE        TO                      CL211
02646                  MB009-COLOR     (MB009-BLOCK-SUB                    CL210
02647                                   MB009-ITEM-SUB)                    CL210
02648              ELSE                                                    CL210
02649                  MOVE DP015-BLUE             TO                      CL211
02650                  MB009-COLOR     (MB009-BLOCK-SUB                    CL210
02651                                   MB009-ITEM-SUB)                    CL210
02652              END-IF                                                  CL210
02653                                                                      CL**2
02654              SET MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB) TO TRUE       CL179
02655          END-IF                                                   MBKCS108
02656                                                                   MBKCS108
02657      END-PERFORM.                                                 MBKCS108
02658                                                                      CL*10
02659 ******************************************************************   CL*10
02660 *    ONCE WE ARE DONE FILLING UP THE BLOCKS, WE MUST PERFORM         CL*10
02661 *    1 MORE READ AGAINST THE DB TO SEE IF THERE IS ANOTHER           CL*10
02662 *    RECORD OUT THERE.  THIS READ ONLY ALLOWS US TO DETERMINE        CL*10
02663 *    IF WE JUST READ THE LAST ITEM OR IF THERE ARE MORE TO READ.     CL*10
02664 ******************************************************************   CL*10
02665                                                                      CL*10
02666      IF MB009-ITEMS-LEFT-ON-DB                                       CL179
02667          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL110
02668              UNTIL PS-DB2-OK                                         CL110
02669                                                                      CL137
02670          IF PS-DB2-OK                                                CL*82
02671              MOVE MDSEAR-DEPT-NBR     TO MB009-SAVE-DPT-NBR          CL179
02672              MOVE MDSEAR-MAJ-CL-NBR   TO MB009-SAVE-MCL-NBR          CL179
02673              MOVE MDSEAR-SUB-CL-NBR   TO MB009-SAVE-SCL-NBR          CL179
02674              MOVE MDSEAR-LIST-SEQ-NBR TO MB009-SAVE-SEQ-NBR          CL179
02675          END-IF                                                      CL*82
02676                                                                      CL159
02677          IF MB009-NUMBER-OF-ITEMS-READ > +0                          CL179
02678              SET PS-SELECTIONS-FOUND    TO TRUE                      CL159
02679          END-IF                                                      CL159
02680                                                                      CL159
02681      END-IF.                                                         CL102
02682                                                                      CL102
02683      IF MB009-NO-ITEMS-LEFT-ON-DB                                    CL179
02684                                                                      CL112
02685          EVALUATE TRUE                                               CL112
02686                                                                      CL112
02687               WHEN MB009-OPEN-MERCH-CURSOR                           CL179
02688                    PERFORM Z220-CLOSE-MERCH-CURSOR                   CL112
02689                                                                      CL112
02690               WHEN MB009-OPEN-DPT-CURSOR                             CL179
02691                    PERFORM Z520-CLOSE-DPT-CURSOR                     CL129
02692                                                                      CL126
02693               WHEN MB009-OPEN-MCL-CURSOR                             CL179
02694                    PERFORM Z620-CLOSE-MCL-CURSOR                     CL148
02695                                                                      CL148
02696               WHEN MB009-OPEN-SCL-CURSOR                             CL179
02697                    PERFORM Z720-CLOSE-SCL-CURSOR                     CL149
02698                                                                      CL149
02699          END-EVALUATE                                                CL112
02700                                                                      CL112
02701          IF MB009-NUMBER-OF-ITEMS-READ = +0                          CL179
02702              SET DP020-MSG-FATAL TO TRUE                             CL218
02703              SET PS-NO-SELECTIONS-FOUND TO TRUE                      CL157
02704          ELSE                                                        CL158
02705              SET PS-SELECTIONS-FOUND    TO TRUE                      CL158
02706          END-IF                                                      CL157
02707      END-IF.                                                         CL157
02708  EJECT                                                            MBKCS108
02709  H200-FORMAT-PANEL-BODY.                                          MBKCS108
02710                                                                   MBKCS108
02711 ******************************************************************MBKCS108
02712 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.            MBKCS108
02713 *                                                                 MBKCS108
02714 ******************************************************************MBKCS108
02715                                                                   MBKCS108
02716      PERFORM I700-FORMAT-FROM-TO-LINE.                            MBKCS108
02717      PERFORM I200-FORMAT-LIST-LINES.                              MBKCS108
02718      PERFORM I300-FORMAT-NR-SELECTED.                             MBKCS108
02719      PERFORM I800-SET-CURSOR-POSITION.                               CL**4
02720  EJECT                                                               CL*92
02721  H300-READ-ITEMS-FROM-DATABASE.                                      CL*41
02722                                                                      CL*41
02723 ******************************************************************   CL*41
02724 *    READ ITEMS FROM DATABASE.                                       CL*41
02725 *         PERFORMS THE READ BASES ON THE CURSOR THAT IS OPEN         CL165
02726 ******************************************************************   CL*41
02727                                                                      CL*41
02728      SET PS-DB2-RETRY TO TRUE.                                       CL*41
02729                                                                      CL*41
02730      EVALUATE TRUE                                                   CL100
02731                                                                      CL100
02732           WHEN MB009-OPEN-MERCH-CURSOR                               CL179
02733                PERFORM Z210-READ-MERCH-AREA                          CL100
02734                    UNTIL PS-DB2-OK                                   CL100
02735                IF MB009-ITEMS-LEFT-ON-DB                             CL201
02736                    CONTINUE                                          CL201
02737                ELSE                                                  CL201
02738                    IF MB009-NUMBER-OF-ITEMS-READ = +0                CL201
02739                        IF PS-NO-ERROR                                CL201
02740                            MOVE PC-TSYMSG-00082 TO                   CL202
02741                                 DP020-MSG-NUMBER                     CL203
02742                            SET PS-ERROR         TO TRUE              CL201
02743                        END-IF                                        CL201
02744                    END-IF                                            CL201
02745                END-IF                                                CL201
02746                                                                      CL*41
02747           WHEN MB009-OPEN-DPT-CURSOR                                 CL179
02748                PERFORM Z510-READ-DPT-AREA                            CL190
02749                    UNTIL PS-DB2-OK                                   CL190
02750                IF MB009-ITEMS-LEFT-ON-DB                             CL201
02751                    CONTINUE                                          CL201
02752                ELSE                                                  CL201
02753                    IF MB009-NUMBER-OF-ITEMS-READ = +0                CL201
02754                        IF PS-NO-ERROR                                CL201
02755                            MOVE PC-TSYMSG-00050 TO                   CL214
02756                                 DP020-MSG-NUMBER                     CL203
02757                            SET PS-ERROR                              CL218
02758                                DP020-MSG-FATAL  TO TRUE              CL218
02759                        END-IF                                        CL201
02760                    END-IF                                            CL201
02761                END-IF                                                CL201
02762                                                                      CL119
02763           WHEN MB009-OPEN-MCL-CURSOR                                 CL179
02764                PERFORM Z610-READ-MCL-AREA                            CL190
02765                    UNTIL PS-DB2-OK                                   CL190
02766                IF MB009-ITEMS-LEFT-ON-DB                             CL201
02767                    CONTINUE                                          CL201
02768                ELSE                                                  CL201
02769                    IF MB009-NUMBER-OF-ITEMS-READ = +0                CL201
02770                        IF PS-NO-ERROR                                CL201
02771                            MOVE PC-TSYMSG-00050 TO                   CL214
02772                                 DP020-MSG-NUMBER                     CL203
02773                            SET PS-ERROR         TO TRUE              CL201
02774                            SET DP020-MSG-FATAL  TO TRUE              CL218
02775                        END-IF                                        CL201
02776                    END-IF                                            CL201
02777                END-IF                                                CL201
02778                                                                      CL135
02779           WHEN MB009-OPEN-SCL-CURSOR                                 CL179
02780                PERFORM Z710-READ-SCL-AREA                            CL190
02781                    UNTIL PS-DB2-OK                                   CL190
02782                IF MB009-ITEMS-LEFT-ON-DB                             CL201
02783                    CONTINUE                                          CL201
02784                ELSE                                                  CL201
02785                    IF MB009-NUMBER-OF-ITEMS-READ = +0                CL201
02786                        IF PS-NO-ERROR                                CL201
02787                            MOVE PC-TSYMSG-00050 TO                   CL214
02788                                 DP020-MSG-NUMBER                     CL203
02789                            SET PS-ERROR         TO TRUE              CL201
02790                            SET DP020-MSG-FATAL  TO TRUE              CL218
02791                        END-IF                                        CL201
02792                    END-IF                                            CL201
02793                END-IF                                                CL201
02794                                                                      CL148
02795      END-EVALUATE.                                                   CL100
02796  EJECT                                                               CL*41
02797  H400-WRITE-OUT-LAST-BLOCKS.                                         CL*92
02798                                                                      CL*92
02799 ******************************************************************   CL*92
02800 *    WRITE OUT THE 2 BLOCKS CURRENTLY IN WORKING STORAGE TO          CL*92
02801 *    TEMPORARY STORAGE.                                              CL*92
02802 *                                                                    CL*92
02803 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
02804 ******************************************************************   CL*92
02805                                                                      CL*92
02806      MOVE +1 TO MB009-BLOCK-SUB.                                     CL179
02807                                                                      CL*92
02808      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
02809                                                                      CL*92
02810      ADD +1  TO MB009-BLOCK-SUB.                                     CL179
02811                                                                      CL*92
02812      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
02813                                                                      CL*92
02814  EJECT                                                               CL*92
02815  I100-INITIALIZE-BLOCK.                                           MBKCS108
02816                                                                   MBKCS108
02817 ******************************************************************MBKCS108
02818 *    INITIALIZE A BLOCK IN THE ARRAY.                             MBKCS108
02819 *                                                                 MBKCS108
02820 ******************************************************************MBKCS108
02821                                                                   MBKCS108
02822      PERFORM                                                      MBKCS108
02823          VARYING PS-SUB FROM 1 BY 1                               MBKCS108
02824          UNTIL PS-SUB > PC-ITEMS-PER-BLOCK                        MBKCS108
02825                                                                   MBKCS108
02826          MOVE +0 TO MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB PS-SUB)   CL179
02827                                                                   MBKCS108
02828          MOVE SPACES TO MB009-ITEM-GMA(MB009-BLOCK-SUB PS-SUB)       CL179
02829          MOVE SPACES TO MB009-ITEM-DMA(MB009-BLOCK-SUB PS-SUB)       CL179
02830          MOVE SPACES TO MB009-ITEM-DEPT(MB009-BLOCK-SUB PS-SUB)      CL179
02831          MOVE SPACES TO MB009-ITEM-MCLASS(MB009-BLOCK-SUB PS-SUB)    CL179
02832          MOVE SPACES TO MB009-ITEM-SCLASS(MB009-BLOCK-SUB PS-SUB)    CL179
02833          MOVE SPACES TO MB009-ITEM-DESC(MB009-BLOCK-SUB PS-SUB)      CL179
02834          MOVE SPACES TO MB009-ITEM-POSDESC(MB009-BLOCK-SUB PS-SUB)   CL186
02835          MOVE SPACES TO MB009-ITEM-TAXD(MB009-BLOCK-SUB PS-SUB)      CL186
02836          MOVE SPACES TO MB009-ITEM-ACTION(MB009-BLOCK-SUB PS-SUB)    CL186
02837          SET MB009-NO-SELECT(MB009-BLOCK-SUB PS-SUB) TO TRUE         CL179
02838                                                                      CL186
02839      END-PERFORM.                                                 MBKCS108
02840                                                                   MBKCS108
02841      SET MB009-BLOCK-NOT-MODIFIED(MB009-BLOCK-SUB) TO TRUE.          CL179
02842  EJECT                                                            MBKCS108
02843  I200-FORMAT-LIST-LINES.                                          MBKCS108
02844                                                                   MBKCS108
02845 ******************************************************************MBKCS108
02846 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.     MBKCS108
02847 *                                                                 MBKCS108
02848 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02849 ******************************************************************MBKCS108
02850                                                                   MBKCS108
02851      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS108
02852          (MB009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL179
02853                                                                   MBKCS108
02854      MOVE MB009-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL179
02855      MOVE +1                         TO PS-SUB.                   MBKCS108
02856                                                                   MBKCS108
02857      PERFORM J100-PROCESS-ITEMS                                   MBKCS108
02858          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS108
02859  EJECT                                                            MBKCS108
02860  I300-FORMAT-NR-SELECTED.                                         MBKCS108
02861                                                                   MBKCS108
02862 ******************************************************************MBKCS108
02863 *    FORMAT THE FIELD WHICH WILL SHOW THE NUMBER OF ITEMS THAT    MBKCS108
02864 *    WERE SELECTED BY THE USER.                                   MBKCS108
02865 *                                                                 MBKCS108
02866 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02867 ******************************************************************MBKCS108
02868                                                                   MBKCS108
02869      MOVE MB009-NUMBER-OF-ITEMS-SELECTED TO PV-DISPLAY-7.            CL179
02870      MOVE PV-DISPLAY-7                   TO ATOTSELO.                CL178
02871  EJECT                                                            MBKCS108
02872  I700-FORMAT-FROM-TO-LINE.                                        MBKCS108
02873                                                                   MBKCS108
02874 ******************************************************************MBKCS108
02875 *    FORMAT THE LINE THAT WILL SHOW THE TOTAL NUMBER OF ITEMS WE  MBKCS108
02876 *    HAVE ON OUR LIST AND SHOW THE NUMBERS ASSOCIATED WITH THE    MBKCS108
02877 *    FIRST AND LAST ITEMS ON CURRENT PAGE.  THIS WILL HELP THE    MBKCS108
02878 *    USER DETERMINE HOW FAR THEY ARE INTO THE LIST.               MBKCS108
02879 *                                                                 MBKCS108
02880 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02881 ******************************************************************MBKCS108
02882                                                                   MBKCS108
02883 ******************************************************************   CL**3
02884 *    ON MAP      ITEMS 008008X TO 008008X OF 008008X                 CL181
02885 *                        BEG   TO   END   OF   TOT                   CL**3
02886 ******************************************************************   CL**3
02887                                                                      CL**3
02888      MOVE MB009-CO-NUMBER                TO MB010O-AOPERCOO.         CL179
02889                                                                      CL*68
02890      IF MB009-NUMBER-OF-ITEMS-READ > +0                              CL179
02891          MOVE MB009-ITEM-AT-TOP-OF-PANEL TO MB010O-ABEGITMO          CL179
02892      ELSE                                                         MBKCS108
02893          MOVE 0                          TO MB010O-ABEGITMO          CL179
02894      END-IF.                                                      MBKCS108
02895                                                                   MBKCS108
02896      MOVE DP015-GREEN                    TO ABEGITMC.                CL*22
02897      MOVE DP015-UNDERLINE                TO ABEGITMH.                CL*56
02898      MOVE DP015-UNP-NUM-BRT-MDT          TO ABEGITMA.                CL*22
02899                                                                      CL*22
02900      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS108
02901          (MB009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL179
02902                                                                   MBKCS108
02903      IF PV-ENDING-ITEM-ON-PANEL < MB009-NUMBER-OF-ITEMS-READ         CL179
02904          MOVE PV-ENDING-ITEM-ON-PANEL    TO MB010O-AENDITMO          CL179
02905      ELSE                                                         MBKCS108
02906          MOVE MB009-NUMBER-OF-ITEMS-READ TO MB010O-AENDITMO          CL179
02907      END-IF.                                                      MBKCS108
02908                                                                   MBKCS108
02909      MOVE SPACES TO MB010O-AOFO.                                     CL179
02910      MOVE ZEROES TO MB010O-ATOTITMO.                                 CL179
02911                                                                   MBKCS108
02912      IF (MB009-NUMBER-OF-ITEMS-READ > +0) AND                        CL179
02913         (MB009-NO-ITEMS-LEFT-ON-DB)                                  CL179
02914          MOVE 'OF'                       TO MB010O-AOFO              CL179
02915                                                                      CL**3
02916          MOVE MB009-NUMBER-OF-ITEMS-READ TO MB010O-ATOTITMO          CL179
02917      END-IF.                                                      MBKCS108
02918  EJECT                                                            MBKCS108
02919  I800-SET-CURSOR-POSITION.                                        MBKCS108
02920                                                                   MBKCS108
02921 ******************************************************************MBKCS108
02922 *    IF NO ITEMS WERE READ FROM THE DATA BASE, POINT THE CURSOR   MBKCS108
02923 *    TO THE COMMAND LINE, OTHERWISE POINT THE CURSOR TO ONE OF    MBKCS108
02924 *    THE SELECTION ENTRIES.                                       MBKCS108
02925 *                                                                 MBKCS108
02926 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02927 ******************************************************************MBKCS108
02928                                                                   MBKCS108
02929      IF (PS-CURSOR-NOT-POSITIONED) OR                                CL157
02930         (PS-INFORMATIONAL)         OR                                CL157
02931         (PS-WARNING)                                                 CL157
02932          PERFORM J200-FIND-LAST-SELECTION                            CL157
02933      END-IF.                                                      MBKCS108
02934  EJECT                                                            MBKCS108
02935  J100-PROCESS-ITEMS.                                              MBKCS108
02936                                                                   MBKCS108
02937 ******************************************************************MBKCS108
02938 *    PROCESS EACH ITEM IN THE ARRAY AND MOVE IT TO THE MAP.       MBKCS108
02939 *    FILL THE MAP UNTIL WE HAVE REACHED THE MAXIMUM NUMBER OF     MBKCS108
02940 *    MAP ENTRIES.                                                 MBKCS108
02941 *                                                                 MBKCS108
02942 *    PERFORMED BY I200-FORMAT-LIST-LINES.                            CL*93
02943 ******************************************************************MBKCS108
02944                                                                   MBKCS108
02945      IF (PV-ITEM-IN-USE < MB009-NUMBER-OF-ITEMS-READ) OR             CL179
02946         (PV-ITEM-IN-USE = MB009-NUMBER-OF-ITEMS-READ)                CL179
02947          PERFORM K100-SET-SUBSCRIPTS                              MBKCS108
02948                                                                   MBKCS108
02949          MOVE DP015-UNP-ALP-BRT-MDT TO MB010I-ASELA(PS-SUB)          CL179
02950                                                                   MBKCS108
02951          IF (MB009-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB))    OR     CL179
02952             (MB009-NO-SELECT(MB009-BLOCK-SUB MB009-ITEM-SUB))        CL179
02953              MOVE DP015-UNDERLINE TO MB010O-ASELH(PS-SUB)            CL179
02954              MOVE DP015-GREEN     TO MB010O-ASELC(PS-SUB)            CL179
02955          ELSE                                                        CL*44
02956              IF (MB009-BEGIN(MB009-BLOCK-SUB MB009-ITEM-SUB)) OR     CL179
02957                 (MB009-END(MB009-BLOCK-SUB MB009-ITEM-SUB))          CL179
02958                                                                      CL*44
02959                  IF (DP020-MSG-NUMBER = PC-NO-START-OF-RANGE) OR     CL*44
02960                     (DP020-MSG-NUMBER = PC-NO-END-OF-RANGE)          CL*44
02961                      MOVE DP015-REVERSE TO MB010O-ASELH(PS-SUB)      CL179
02962                      MOVE DP015-YELLOW  TO MB010O-ASELC(PS-SUB)      CL179
02963                  END-IF                                              CL*44
02964                                                                      CL*45
02965                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-START-RANGES)    CL*70
02966                 AND (MB009-BEGIN(MB009-BLOCK-SUB MB009-ITEM-SUB))    CL179
02967                      MOVE DP015-REVERSE TO MB010O-ASELH(PS-SUB)      CL179
02968                      MOVE DP015-RED     TO MB010O-ASELC(PS-SUB)      CL179
02969                  END-IF                                              CL*45
02970                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-END-RANGES)      CL*70
02971                 AND (MB009-END(MB009-BLOCK-SUB MB009-ITEM-SUB))      CL179
02972                      MOVE DP015-REVERSE TO MB010O-ASELH(PS-SUB)      CL179
02973                      MOVE DP015-RED     TO MB010O-ASELC(PS-SUB)      CL179
02974                  END-IF                                              CL*70
02975                  IF DP020-MSG-NUMBER = PC-END-BEFORE-START           CL*56
02976                      MOVE DP015-REVERSE TO MB010O-ASELH(PS-SUB)      CL179
02977                      MOVE DP015-RED     TO MB010O-ASELC(PS-SUB)      CL179
02978                  END-IF                                              CL*45
02979              END-IF                                                  CL*45
02980          END-IF                                                      CL*45
02981                                                                   MBKCS108
02982          MOVE MB009-SELECTED-SW(MB009-BLOCK-SUB MB009-ITEM-SUB) TO   CL179
02983               MB010O-ASELO(PS-SUB)                                   CL179
02984                                                                      CL*45
02985          IF (PS-SEND-MAP-N-DATA-FRSET)     OR                        CL*45
02986             (PS-DIFFERENT-ITEMS-DISPLAYED)                           CL*45
02987             MOVE MB009-ITEM-GMA (MB009-BLOCK-SUB                     CL179
02988                                  MB009-ITEM-SUB)           TO        CL179
02989                  MB010O-AGMAO(PS-SUB)                                CL179
02990             MOVE MB009-ITEM-DMA (MB009-BLOCK-SUB                     CL179
02991                                  MB009-ITEM-SUB)           TO        CL179
02992                  MB010O-ADMAO(PS-SUB)                                CL179
02993             MOVE MB009-ITEM-DEPT (MB009-BLOCK-SUB                    CL179
02994                                  MB009-ITEM-SUB)           TO        CL179
02995                  MB010O-ADEPTO(PS-SUB)                               CL179
02996             MOVE MB009-ITEM-MCLASS(MB009-BLOCK-SUB                   CL179
02997                                    MB009-ITEM-SUB)         TO        CL179
02998                  MB010O-AMCLSO(PS-SUB)                               CL179
02999             MOVE MB009-ITEM-SCLASS(MB009-BLOCK-SUB                   CL179
03000                                    MB009-ITEM-SUB)         TO        CL179
03001                  MB010O-ASCLSO(PS-SUB)                               CL179
03002             MOVE MB009-ITEM-DESC (MB009-BLOCK-SUB                    CL179
03003                                   MB009-ITEM-SUB)          TO        CL179
03004                  MB010O-ADESCO(PS-SUB)                               CL179
03005             MOVE MB009-ITEM-POSDESC (MB009-BLOCK-SUB                 CL189
03006                                      MB009-ITEM-SUB)       TO        CL189
03007                  MB010O-APOSDO(PS-SUB)                               CL189
03008             MOVE MB009-ITEM-TAXD (MB009-BLOCK-SUB                    CL189
03009                                   MB009-ITEM-SUB)          TO        CL189
03010                  MB010O-ATAXDO(PS-SUB)                               CL189
03011 *                                                                    CL210
03012             MOVE MB009-COLOR (MB009-BLOCK-SUB MB009-ITEM-SUB)        CL210
03013                                TO  MB010O-AGMAC(PS-SUB)              CL210
03014                                    MB010O-ADMAC(PS-SUB)              CL210
03015                                    MB010O-ADEPTC(PS-SUB)             CL210
03016                                    MB010O-AMCLSC(PS-SUB)             CL210
03017                                    MB010O-ASCLSC(PS-SUB)             CL210
03018                                    MB010O-ADESCC(PS-SUB)             CL210
03019                                    MB010O-APOSDC(PS-SUB)             CL210
03020                                    MB010O-ATAXDC(PS-SUB)             CL210
03021                                    MB010O-AACTNC(PS-SUB)             CL210
03022 *                                                                    CL210
03023             MOVE SPACES                                    TO        CL*45
03024                  MB010O-AACTNO(PS-SUB)                               CL179
03025             MOVE DP015-HL-OFF                              TO        CL*45
03026                  MB010O-AGMAH(PS-SUB)                                CL195
03027             MOVE DP015-HL-OFF                              TO        CL195
03028                  MB010O-ADMAH(PS-SUB)                                CL195
03029             MOVE DP015-HL-OFF                              TO        CL195
03030                  MB010O-ADEPTH(PS-SUB)                               CL195
03031             MOVE DP015-HL-OFF                              TO        CL*93
03032                  MB010O-AMCLSH(PS-SUB)                               CL179
03033             MOVE DP015-HL-OFF                              TO        CL*93
03034                  MB010O-ASCLSH(PS-SUB)                               CL179
03035             MOVE DP015-HL-OFF                              TO        CL*45
03036                  MB010O-ADESCH(PS-SUB)                               CL179
03037             MOVE DP015-HL-OFF                              TO        CL189
03038                  MB010O-APOSDH(PS-SUB)                               CL189
03039             MOVE DP015-HL-OFF                              TO        CL189
03040                  MB010O-ATAXDH(PS-SUB)                               CL189
03041             MOVE DP015-HL-OFF                              TO        CL*45
03042                  MB010O-AACTNH(PS-SUB)                               CL179
03043          END-IF                                                      CL*45
03044      ELSE                                                            CL*45
03045          MOVE DP015-ASK-DRK-OFF                            TO        CL170
03046               MB010I-ASELA(PS-SUB)                                   CL179
03047          MOVE SPACES                                       TO        CL*56
03048               MB010O-ASELO(PS-SUB)                                   CL179
03049          MOVE SPACES                                       TO        CL*45
03050               MB010O-AGMAO(PS-SUB)                                   CL195
03051          MOVE SPACES                                       TO        CL195
03052               MB010O-ADMAO(PS-SUB)                                   CL195
03053          MOVE SPACES                                       TO        CL195
03054               MB010O-ADEPTO(PS-SUB)                                  CL195
03055          MOVE SPACES                                       TO        CL*93
03056               MB010O-AMCLSO(PS-SUB)                                  CL179
03057          MOVE SPACES                                       TO        CL*93
03058               MB010O-ASCLSO(PS-SUB)                                  CL179
03059          MOVE SPACES                                       TO        CL*45
03060               MB010O-ADESCO(PS-SUB)                                  CL179
03061          MOVE SPACES                                       TO        CL189
03062               MB010O-APOSDO(PS-SUB)                                  CL189
03063          MOVE SPACES                                       TO        CL189
03064               MB010O-ATAXDO(PS-SUB)                                  CL189
03065         MOVE SPACES                                        TO        CL*93
03066              MB010O-AACTNO(PS-SUB)                                   CL179
03067      END-IF.                                                         CL*45
03068                                                                      CL*45
03069      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS108
03070      ADD +1 TO PS-SUB.                                            MBKCS108
03071  EJECT                                                            MBKCS108
03072  J200-FIND-LAST-SELECTION.                                        MBKCS108
03073                                                                   MBKCS108
03074 ******************************************************************MBKCS108
03075 *    FIND THE LAST SELECTION ENTRY IN THE MAP THAT IS NOT         MBKCS108
03076 *    SPACES OR LOW VALUES AND POINT THE CURSOR TO THIS ENTRY.     MBKCS108
03077 *    IF ALL ENTRIES ARE SPACES OR LOW VALUES, POINT THE CURSOR    MBKCS108
03078 *    TO THE 1ST SELECTION ENTRY IN THE MAP.                       MBKCS108
03079 *                                                                 MBKCS108
03080 *    PERFORMED BY I800-SET-CURSOR-POSITION.                          CL*93
03081 ******************************************************************MBKCS108
03082                                                                   MBKCS108
03083      MOVE PC-ITEMS-PER-PANEL TO PS-SUB.                           MBKCS108
03084                                                                   MBKCS108
03085      SET PS-NO-SELECTION-FOUND TO TRUE.                           MBKCS108
03086                                                                   MBKCS108
03087      PERFORM                                                      MBKCS108
03088          UNTIL (PS-SUB = +0)        OR                            MBKCS108
03089                (PS-SELECTION-FOUND)                               MBKCS108
03090                                                                   MBKCS108
03091          IF (MB010O-ASELO(PS-SUB) NOT = SPACES)     AND              CL179
03092             (MB010O-ASELO(PS-SUB) NOT = LOW-VALUES)                  CL179
03093              SET PS-SELECTION-FOUND TO TRUE                       MBKCS108
03094          ELSE                                                     MBKCS108
03095              SUBTRACT +1 FROM PS-SUB                              MBKCS108
03096          END-IF                                                   MBKCS108
03097                                                                   MBKCS108
03098      END-PERFORM.                                                 MBKCS108
03099                                                                   MBKCS108
03100      IF PS-SELECTION-FOUND                                        MBKCS108
03101          MOVE -1 TO MB010I-ASELL(PS-SUB)                             CL179
03102      ELSE                                                         MBKCS108
03103          MOVE -1 TO MB010I-ASELL(1)                                  CL179
03104      END-IF.                                                      MBKCS108
03105  EJECT                                                            MBKCS108
03106  K100-SET-SUBSCRIPTS.                                             MBKCS108
03107                                                                   MBKCS108
03108 ******************************************************************MBKCS108
03109 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM  MBKCS108
03110 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL MBKCS108
03111 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                   MBKCS108
03112 *                                                                 MBKCS108
03113 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS    MBKCS108
03114 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT     MBKCS108
03115 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION   MBKCS108
03116 *    US IN THE 2ND BLOCK OF THE ARRAY.                            MBKCS108
03117 *                                                                    CL*92
03118 *    PERFORMED BY F300-CHECK-SELECTIONS                              CL*92
03119 ******************************************************************MBKCS108
03120                                                                   MBKCS108
03121      COMPUTE PS-ITEM-SUB =                                        MBKCS108
03122          (PV-ITEM-IN-USE - MB009-LIST-ITEM-NUMBER(1 1)) + 1.         CL179
03123                                                                   MBKCS108
03124      MOVE PS-ITEM-SUB TO MB009-ITEM-SUB.                             CL179
03125                                                                   MBKCS108
03126      IF MB009-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL179
03127          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB009-ITEM-SUB             CL179
03128                                                                   MBKCS108
03129          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB009-BLOCK-SUB              CL179
03130      ELSE                                                         MBKCS108
03131          MOVE +1                     TO MB009-BLOCK-SUB              CL179
03132      END-IF.                                                      MBKCS108
03133 *                                                                    CL206
03134  EJECT                                                               CL206
03135 ******************************************************************   CL206
03136 *    PREPARE THE TEMPORARY STORAGE QUEUE.  MOVE ALL SELECTED         CL206
03137 *    GROUPS FROM EITHER TEMP STORAGE OR THE ARRAY IN THE COMM        CL206
03138 *    AREA TO THE LIST TS QUEUE RECORD(S).                            CL206
03139 *                                                                    CL206
03140 *    PERFORMED BY D300                                               CL206
03141 ******************************************************************   CL206
03142 *                                                                    CL206
03143  X100-PREPARE-LIST-QUEUE.                                            CL206
03144 *                                                                    CL206
03145      SET DONT-SETUP-INTER-APPL TO  TRUE.                             CL209
03146      MOVE +1                   TO  MB009-BLOCK-SUB.                  CL206
03147      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL206
03148      ADD +1                    TO  MB009-BLOCK-SUB.                  CL206
03149      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL206
03150 *                                                                    CL206
03151      IF  MB009-HIGHEST-BLOCK-WRITTEN = ZERO                          CL206
03152          MOVE EIBTRMID         TO  PV-TERM-ID                        CL206
03153          MOVE EIBTASKN         TO  PV-TASK-NUMBER                    CL206
03154      ELSE                                                            CL206
03155          MOVE MB009-TERM-ID    TO  PV-TERM-ID                        CL206
03156          MOVE MB009-TASK-NUMBER                                      CL206
03157                                TO  PV-TASK-NUMBER                    CL206
03158      END-IF.                                                         CL206
03159      SUBTRACT 1              FROM  PV-TASK-NUMBER.                   CL206
03160 *                                                                    CL206
03161      MOVE PV-QUEUE-NAME        TO  MB005-LIST-QUEUE-NAME             CL206
03162                                    MB009-LIST-QUEUE-NAME.            CL206
03163      MOVE LENGTH OF SC-LIST-RECORD                                   CL206
03164                                TO  MB005-LIST-QUEUE-LENGTH.          CL206
03165      PERFORM X900-DELETE-LIST-QUEUE.                                 CL206
03166 *                                                                    CL206
03167      SET MB003-IDX             TO  1.                                CL228
03168      INITIALIZE SC-LIST-RECORD.                                      CL206
03169      PERFORM X200-PROCESS-TS-QUEUE                                   CL206
03170              VARYING PV-ITEM                                         CL206
03171              FROM 1 BY 1                                             CL206
03172              UNTIL PV-ITEM GREATER MB009-HIGHEST-BLOCK-WRITTEN.      CL206
03173 *                                                                    CL206
03174      PERFORM X500-WRITE-LIST-QUEUE.                                  CL206
03175 *                                                                    CL206
03176  EJECT                                                               CL206
03177 ******************************************************************   CL206
03178 *    OBTAIN A TS QUEUE RECORD, MOVE SELECTED RECORDS TO THE          CL206
03179 *    LIST QUEUE RECORD.                                              CL206
03180 *                                                                    CL206
03181 *    PERFORMED BY X100                                               CL206
03182 ******************************************************************   CL206
03183 *                                                                    CL206
03184  X200-PROCESS-TS-QUEUE.                                              CL206
03185 *                                                                    CL206
03186      PERFORM X250-READ-TS-QUEUE.                                     CL206
03187      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL206
03188          PERFORM                                                     CL206
03189          VARYING PV-TW-IDX                                           CL206
03190          FROM 1 BY 1                                                 CL206
03191          UNTIL PV-TW-IDX GREATER THAN PC-ITEMS-PER-BLOCK             CL206
03192            OR  PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX) EQUAL ZERO         CL206
03193 *                                                                    CL206
03194              IF  PV-TW-SELECT (PV-TW-IDX)                            CL206
03195                  MOVE PV-TW-KEY-DATA (PV-TW-IDX)                     CL206
03196                                TO  MB003-KEY-DATA (MB003-IDX)        CL228
03197                                    PV-HOLD-KEY-DATA                  CL206
03198                  MOVE PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX)             CL206
03199                                TO  MB003-ITEM       (MB003-IDX)      CL228
03200                  MOVE PC-SCL-LITERAL                                 CL206
03201                                TO  MB003-MDSELV-NUMBER               CL228
03202                                                     (MB003-IDX)      CL228
03203                  IF PV-TW-COLOR(PV-TW-IDX) = DP015-TURQUOISE         CL221
03204                      SET PS-SELECTED-HISTORY TO TRUE                 CL221
03205                  END-IF                                              CL221
03206                  SET MB003-IDX UP BY 1                               CL228
03207                  IF  MB003-IDX GREATER PC-MAX-QUEUE-ITEMS            CL228
03208                      PERFORM X500-WRITE-LIST-QUEUE                   CL206
03209                  END-IF                                              CL206
03210              END-IF                                                  CL206
03211 *                                                                    CL206
03212          END-PERFORM                                                 CL206
03213      END-IF.                                                         CL206
03214 *                                                                    CL206
03215  EJECT                                                               CL206
03216 ******************************************************************   CL206
03217 *    READ A TS QUEUE RECORD.                                         CL206
03218 *                                                                    CL206
03219 *    PERFORMED BY X200                                               CL206
03220 ******************************************************************   CL206
03221 *                                                                    CL206
03222  X250-READ-TS-QUEUE.                                                 CL206
03223 *                                                                    CL206
03224      EXEC CICS                                                       CL206
03225          READQ TS                                                    CL206
03226          QUEUE(MB009-TEMP-QUEUE-NAME)                                CL206
03227          INTO(PV-TEMP-WORK-AREA)                                     CL206
03228          LENGTH(PC-BLOCK-LENGTH)                                     CL206
03229          ITEM(PV-ITEM)                                               CL206
03230          RESP(PV-RESP)                                               CL206
03231      END-EXEC.                                                       CL206
03232 *                                                                    CL206
03233  EJECT                                                               CL206
03234 ******************************************************************   CL206
03235 *    FIRST, FIND THE LAST SKU MOVED.  THIS SETS THE SUBSCRIPTS       CL206
03236 *    FOR MOVING FROM THE ARRAY.                                      CL206
03237 *                                                                    CL206
03238 *    PERFORMED BY X100                                               CL206
03239 ******************************************************************   CL206
03240 *                                                                    CL206
03241  X300-PROCESS-ARRAY.                                                 CL206
03242 *                                                                    CL206
03243      MOVE 1                    TO  PS-SUB1                           CL206
03244                                    PS-SUB2.                          CL206
03245      PERFORM                                                         CL206
03246          UNTIL PV-HOLD-KEY-DATA LESS MB009-KEY-DATA (PS-SUB1         CL206
03247                                                      PS-SUB2)        CL206
03248             OR MB009-KEY-DATA (PS-SUB1 PS-SUB2) EQUAL SPACES         CL206
03249             OR PS-SUB1 GREATER PC-MAX-BLOCKS-IN-ARRAY                CL206
03250 *                                                                    CL206
03251              ADD 1             TO  PS-SUB2                           CL206
03252              IF  PS-SUB2 GREATER PC-ITEMS-PER-BLOCK                  CL206
03253                  MOVE 1        TO  PS-SUB2                           CL206
03254                  ADD 1         TO  PS-SUB1                           CL206
03255              END-IF                                                  CL206
03256      END-PERFORM.                                                    CL206
03257 *                                                                    CL206
03258 *    POINTERS ARE ESTABLISHED, NOW MOVE TO THE LIST RECORD.          CL206
03259 *                                                                    CL206
03260      IF (PS-SUB1 GREATER THAN PC-MAX-BLOCKS-IN-ARRAY                 CL206
03261      OR  MB009-KEY-DATA (PS-SUB1 PS-SUB2) = SPACES)                  CL206
03262          CONTINUE                                                    CL206
03263      ELSE                                                            CL206
03264          PERFORM                                                     CL206
03265          UNTIL PS-SUB1 GREATER PC-MAX-BLOCKS-IN-ARRAY                CL206
03266 *                                                                    CL206
03267              IF  MB009-SELECT (PS-SUB1 PS-SUB2)                      CL206
03268                  MOVE MB009-KEY-DATA (PS-SUB1 PS-SUB2)               CL206
03269                                TO  MB003-KEY-DATA (MB003-IDX)        CL228
03270                  MOVE MB009-LIST-ITEM-NUMBER                         CL206
03271                                      (PS-SUB1 PS-SUB2)               CL206
03272                                TO  MB003-ITEM       (MB003-IDX)      CL228
03273                  MOVE PC-SCL-LITERAL                                 CL206
03274                                TO  MB003-MDSELV-NUMBER               CL228
03275                                                     (MB003-IDX)      CL228
03276 *                                                                    CL206
03277                  SET MB003-IDX UP BY 1                               CL228
03278                  IF  MB003-IDX GREATER PC-MAX-QUEUE-ITEMS            CL228
03279                      PERFORM X500-WRITE-LIST-QUEUE                   CL206
03280                  END-IF                                              CL206
03281              END-IF                                                  CL206
03282 *                                                                    CL206
03283              ADD 1             TO  PS-SUB2                           CL206
03284              IF  PS-SUB2 GREATER PC-ITEMS-PER-BLOCK                  CL206
03285                  ADD 1         TO  PS-SUB1                           CL206
03286                  MOVE 1        TO  PS-SUB2                           CL206
03287              END-IF                                                  CL206
03288      END-PERFORM.                                                    CL206
03289 *                                                                    CL206
03290  EJECT                                                               CL206
03291 ******************************************************************   CL206
03292 *    WRITE THE LIST QUEUE RECORD.                                    CL206
03293 *                                                                    CL206
03294 *    PERFORMED BY X100, X300                                         CL206
03295 ******************************************************************   CL206
03296 *                                                                    CL206
03297  X500-WRITE-LIST-QUEUE.                                              CL206
03298 *                                                                    CL206
03299      IF  MB003-IDX GREATER THAN 1                                    CL228
03300          MOVE PC-MAX-QUEUE-ITEMS                                     CL206
03301                                TO  MB003-MAX-ITEMS                   CL228
03302 *        MOVE MB009-NUMBER-OF-ITEMS-SELECTED                         CL206
03303 *                              TO  MB003-ITEMS-SELECTED              CL228
03304 *                                                                    CL206
03305          IF  DP020-SRC-AID EQUAL DP016-PF14                          CL206
03306              SET MB003-INQUIRY TO    TRUE                            CL228
03307          END-IF                                                      CL206
03308          IF  DP020-SRC-AID EQUAL DP016-PF15                          CL206
03309              SET MB003-UPDATE    TO  TRUE                            CL228
03310          END-IF                                                      CL206
03311 *                                                                    CL206
03312          EXEC CICS                                                   CL206
03313               WRITEQ TS                                              CL206
03314               QUEUE(MB005-LIST-QUEUE-NAME)                           CL206
03315               FROM(SC-LIST-RECORD)                                   CL206
03316               LENGTH(MB005-LIST-QUEUE-LENGTH)                        CL206
03317               ITEM(PV-SC-ITEM)                                       CL206
03318               RESP(PV-RESP)                                          CL206
03319          END-EXEC                                                    CL206
03320 *                                                                    CL206
03321          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL206
03322              SET DP013-LOGIC-ABEND TO TRUE                           CL206
03323              SET DP013-NO-ROLLBACK TO TRUE                           CL206
03324              MOVE 'X500-WRITE-LIST-QUEUE'   TO                       CL206
03325                   DP013-PARAGRAPH                                    CL206
03326              MOVE PV-BLOCK-NUMBER           TO                       CL206
03327                   AM-BLOCK-NUMBER-11                                 CL206
03328              MOVE AM-ABEND-MESSAGE-11       TO                       CL206
03329                   DP013-MESSAGE-TEXT(1)                              CL206
03330              PERFORM DP013-0000-PROCESS-ABEND                        CL206
03331          END-IF                                                      CL206
03332 *                                                                    CL206
03333          INITIALIZE SC-LIST-RECORD                                   CL206
03334          SET MB003-IDX         TO  1                                 CL228
03335      END-IF.                                                         CL206
03336 *                                                                    CL206
03337  EJECT                                                               CL206
03338 ******************************************************************   CL206
03339 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C700.                 CL206
03340 *                                                                    CL206
03341 *    PERFORMED BY C600, C700                                         CL206
03342 ******************************************************************   CL206
03343 *                                                                    CL206
03344  X550-WRITE-TS-QUEUE.                                                CL206
03345 *                                                                    CL206
03346      EXEC CICS                                                       CL206
03347          WRITEQ TS                                                   CL206
03348          REWRITE                                                     CL206
03349          QUEUE(MB009-TEMP-QUEUE-NAME)                                CL206
03350          FROM(PV-TEMP-WORK-AREA)                                     CL206
03351          LENGTH(PC-BLOCK-LENGTH)                                     CL206
03352          ITEM(PV-SAVE-ITEM)                                          CL206
03353          RESP(PV-RESP)                                               CL206
03354      END-EXEC.                                                       CL206
03355 *                                                                    CL206
03356  EJECT                                                               CL206
03357 ******************************************************************   CL206
03358 *    READ THE LIST QUEUE RECORD.                                     CL206
03359 *                                                                    CL206
03360 *    PERFORMED BY C600                                               CL206
03361 ******************************************************************   CL206
03362 *                                                                    CL206
03363  X600-READ-LIST-QUEUE.                                               CL206
03364 *                                                                    CL206
03365      EXEC CICS                                                       CL206
03366           READQ TS                                                   CL206
03367           QUEUE(MB005-LIST-QUEUE-NAME)                               CL206
03368           INTO(SC-LIST-RECORD)                                       CL206
03369           LENGTH(MB005-LIST-QUEUE-LENGTH)                            CL206
03370           ITEM(PV-SC-ITEM)                                           CL206
03371           RESP(PV-RESP)                                              CL206
03372      END-EXEC.                                                       CL206
03373 *                                                                    CL206
03374  EJECT                                                               CL206
03375 ******************************************************************   CL206
03376 *    DELETE THE SKU LIST TEMP STORAGE QUEUE.                         CL206
03377 *                                                                    CL206
03378 *    PERFORMED BY X100                                               CL206
03379 ******************************************************************   CL206
03380 *                                                                    CL206
03381  X900-DELETE-LIST-QUEUE.                                             CL206
03382 *                                                                    CL206
03383      EXEC CICS                                                       CL206
03384          DELETEQ TS                                                  CL206
03385          QUEUE(MB005-LIST-QUEUE-NAME)                                CL206
03386          RESP(PV-RESP)                                               CL206
03387      END-EXEC.                                                       CL206
03388 *                                                                    CL206
03389  EJECT                                                            MBKCS108
03390  Z050-DELETE-TEMP-STORAGE.                                        MBKCS108
03391                                                                   MBKCS108
03392 ******************************************************************MBKCS108
03393 *    DELETE THE TEMPORARY STORAGE QUEUE.                          MBKCS108
03394 *                                                                 MBKCS108
03395 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*93
03396 ******************************************************************MBKCS108
03397                                                                   MBKCS108
03398      EXEC CICS                                                    MBKCS108
03399          DELETEQ TS                                               MBKCS108
03400          QUEUE(MB009-TEMP-QUEUE-NAME)                                CL179
03401          RESP(PV-CICS-RESPONSE)                                      CL**4
03402      END-EXEC.                                                    MBKCS108
03403                                                                   MBKCS108
03404      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL**4
03405         (PV-CICS-RESPONSE NOT = DFHRESP(QIDERR))                     CL**4
03406          SET DP013-LOGIC-ABEND           TO TRUE                     CL*22
03407          SET DP013-NO-ROLLBACK           TO TRUE                     CL*22
03408          MOVE 'Z050-DELETE-TEMP-STORAGE' TO DP013-PARAGRAPH          CL*22
03409          MOVE AM-ABEND-MESSAGE-8         TO DP013-MESSAGE-TEXT(1)    CL*22
03410      END-IF.                                                      MBKCS108
03411                                                                   MBKCS108
03412      MOVE +0 TO MB009-HIGHEST-BLOCK-WRITTEN.                         CL179
03413  EJECT                                                            MBKCS108
03414  Z100-READ-TEMP-STORAGE.                                          MBKCS108
03415                                                                   MBKCS108
03416 ******************************************************************MBKCS108
03417 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.          MBKCS108
03418 *                                                                 MBKCS108
03419 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL*92
03420 ******************************************************************MBKCS108
03421                                                                   MBKCS108
03422      EXEC CICS                                                    MBKCS108
03423          READQ TS                                                 MBKCS108
03424          QUEUE(MB009-TEMP-QUEUE-NAME)                                CL179
03425          INTO(MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB))                  CL179
03426          LENGTH(PC-BLOCK-LENGTH)                                  MBKCS108
03427          ITEM(PV-BLOCK-NUMBER)                                    MBKCS108
03428          RESP(PV-CICS-RESPONSE)                                      CL**4
03429      END-EXEC.                                                    MBKCS108
03430                                                                   MBKCS108
03431      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL*69
03432         (PV-CICS-RESPONSE NOT = DFHRESP(ITEMERR))                    CL*69
03433                                                                   MBKCS108
03434          EXEC CICS                                                MBKCS108
03435               DELETEQ TS                                          MBKCS108
03436               QUEUE(MB009-TEMP-QUEUE-NAME)                           CL179
03437               RESP(PV-CICS-RESPONSE)                                 CL**4
03438          END-EXEC                                                 MBKCS108
03439                                                                   MBKCS108
03440          SET DP013-LOGIC-ABEND         TO TRUE                       CL*28
03441          SET DP013-NO-ROLLBACK         TO TRUE                       CL*28
03442          MOVE 'Z100-READ-TEMP-STORAGE' TO DP013-PARAGRAPH            CL*28
03443          MOVE AM-ABEND-MESSAGE-12      TO DP013-MESSAGE-TEXT(1)      CL*57
03444          PERFORM DP013-0000-PROCESS-ABEND                            CL*28
03445                                                                      CL*28
03446      END-IF.                                                      MBKCS108
03447                                                                   MBKCS108
03448      SET MB009-BLOCK-NOT-MODIFIED(MB009-BLOCK-SUB) TO TRUE.          CL179
03449  EJECT                                                            MBKCS108
03450  Z150-WRITE-TEMP-STORAGE.                                         MBKCS108
03451                                                                   MBKCS108
03452 ******************************************************************MBKCS108
03453 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.       MBKCS108
03454 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.     MBKCS108
03455 *                                                                 MBKCS108
03456 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED                        CL*92
03457 ******************************************************************MBKCS108
03458                                                                   MBKCS108
03459      IF MB009-HIGHEST-BLOCK-WRITTEN = +0                             CL179
03460          MOVE EIBTASKN TO DP020-SRC-TASK-NUMBER                   MBKCS108
03461          MOVE EIBTASKN TO MB009-TASK-NUMBER                          CL179
03462          MOVE EIBTRMID TO MB009-TERM-ID                              CL179
03463      END-IF.                                                      MBKCS108
03464                                                                   MBKCS108
03465      MOVE MB009-LIST-ITEM-NUMBER(MB009-BLOCK-SUB 1) TO               CL179
03466           PV-LIST-ITEM-NUMBER.                                    MBKCS108
03467                                                                   MBKCS108
03468      COMPUTE PV-BLOCK-NUMBER =                                    MBKCS108
03469          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK).    MBKCS108
03470                                                                   MBKCS108
03471      IF (PV-BLOCK-NUMBER < MB009-HIGHEST-BLOCK-WRITTEN) OR           CL179
03472         (PV-BLOCK-NUMBER = MB009-HIGHEST-BLOCK-WRITTEN)              CL179
03473                                                                   MBKCS108
03474          IF MB009-BLOCK-MODIFIED(MB009-BLOCK-SUB)                    CL179
03475                                                                   MBKCS108
03476              EXEC CICS                                            MBKCS108
03477                  WRITEQ TS                                        MBKCS108
03478                  QUEUE(MB009-TEMP-QUEUE-NAME)                        CL179
03479                  FROM(MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB))          CL179
03480                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS108
03481                  ITEM(PV-BLOCK-NUMBER)                            MBKCS108
03482                  REWRITE                                          MBKCS108
03483                  RESP(PV-CICS-RESPONSE)                              CL**4
03484              END-EXEC                                             MBKCS108
03485                                                                   MBKCS108
03486              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
03487                  EXEC CICS                                        MBKCS108
03488                      DELETEQ TS                                   MBKCS108
03489                      QUEUE(MB009-TEMP-QUEUE-NAME)                    CL179
03490                      RESP(PV-CICS-RESPONSE)                          CL**4
03491                  END-EXEC                                         MBKCS108
03492                                                                   MBKCS108
03493                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
03494                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
03495                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
03496                       DP013-PARAGRAPH                                CL*26
03497                  MOVE AM-ABEND-MESSAGE-9        TO                   CL*26
03498                       DP013-MESSAGE-TEXT(1)                          CL*26
03499                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
03500              END-IF                                               MBKCS108
03501                                                                   MBKCS108
03502          END-IF                                                   MBKCS108
03503                                                                   MBKCS108
03504      ELSE                                                         MBKCS108
03505                                                                   MBKCS108
03506          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       MBKCS108
03507              MB009-HIGHEST-BLOCK-WRITTEN + 1                         CL179
03508                                                                   MBKCS108
03509          IF PV-BLOCK-NUMBER = PV-HIGHEST-BLOCK-WRITTEN            MBKCS108
03510                                                                   MBKCS108
03511              EXEC CICS                                            MBKCS108
03512                  WRITEQ TS                                        MBKCS108
03513                  QUEUE(MB009-TEMP-QUEUE-NAME)                        CL179
03514                  FROM(MB009-BLOCK-ENTRIES(MB009-BLOCK-SUB))          CL179
03515                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS108
03516                  ITEM(PV-BLOCK-NUMBER)                            MBKCS108
03517                  RESP(PV-CICS-RESPONSE)                              CL**4
03518              END-EXEC                                             MBKCS108
03519                                                                   MBKCS108
03520              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
03521                                                                      CL**3
03522                  EXEC CICS                                           CL**3
03523                      DELETEQ TS                                   MBKCS108
03524                      QUEUE(MB009-TEMP-QUEUE-NAME)                    CL179
03525                      RESP(PV-CICS-RESPONSE)                          CL**4
03526                  END-EXEC                                         MBKCS108
03527                                                                   MBKCS108
03528                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
03529                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
03530                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
03531                       DP013-PARAGRAPH                                CL*26
03532                  MOVE AM-ABEND-MESSAGE-10       TO                   CL*26
03533                       DP013-MESSAGE-TEXT(1)                          CL*26
03534                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
03535              END-IF                                               MBKCS108
03536                                                                   MBKCS108
03537              ADD +1 TO MB009-HIGHEST-BLOCK-WRITTEN                   CL179
03538          ELSE                                                     MBKCS108
03539              SET DP013-LOGIC-ABEND TO TRUE                           CL*26
03540              SET DP013-NO-ROLLBACK TO TRUE                           CL*26
03541              MOVE 'Z150-WRITE-TEMP-STORAGE' TO                       CL*26
03542                   DP013-PARAGRAPH                                    CL*26
03543              MOVE PV-BLOCK-NUMBER           TO                       CL*26
03544                   AM-BLOCK-NUMBER-11                                 CL*26
03545              MOVE AM-ABEND-MESSAGE-11       TO                       CL*26
03546                   DP013-MESSAGE-TEXT(1)                              CL*26
03547              PERFORM DP013-0000-PROCESS-ABEND                        CL*26
03548          END-IF                                                   MBKCS108
03549                                                                   MBKCS108
03550      END-IF.                                                      MBKCS108
03551                                                                   MBKCS108
03552      SET MB009-BLOCK-NOT-MODIFIED(MB009-BLOCK-SUB) TO TRUE.          CL179
03553  EJECT                                                            MBKCS108
03554  Z200-OPEN-MERCH-CURSOR.                                             CL*92
03555                                                                      CL*92
03556 ******************************************************************   CL*39
03557 *    OPEN MERCHANDISE AREA CURSOR                                    CL*39
03558 ******************************************************************   CL*39
03559                                                                      CL*39
03560      EXEC SQL                                                        CL*39
03561          OPEN MERCH-CURSOR                                           CL*39
03562      END-EXEC.                                                       CL*39
03563                                                                      CL*58
03564      IF (SQLCODE = +0)   OR                                          CL*58
03565         (SQLCODE = +100)                                             CL*58
03566          SET PS-MERCH-CURSOR-OPENED     TO TRUE                      CL*84
03567      ELSE                                                            CL*58
03568          SET DP013-DB2-ABEND            TO TRUE                      CL*58
03569          SET DP013-NO-ROLLBACK          TO TRUE                      CL*58
03570          MOVE 'Z200-OPEN-MERCH-CURSOR'  TO                           CL*58
03571               DP013-PARAGRAPH                                        CL*58
03572          MOVE AM-ABEND-MESSAGE-21       TO                           CL*58
03573               DP013-MESSAGE-TEXT(1)                                  CL*58
03574          MOVE SQLCA                     TO                           CL*58
03575               DP013-SQLCA                                            CL*58
03576          PERFORM DP013-0000-PROCESS-ABEND                            CL*58
03577      END-IF.                                                         CL*58
03578  EJECT                                                               CL*39
03579  Z210-READ-MERCH-AREA.                                               CL*39
03580                                                                      CL137
03581 ******************************************************************   CL137
03582 *    READ MERCHANDISE AREA CURSOR                                    CL137
03583 ******************************************************************   CL137
03584                                                                      CL*39
03585      EXEC SQL                                                        CL*39
03586          FETCH MERCH-CURSOR                                          CL*39
03587           INTO :MDSEAR-OPERCO-NBR,                                   CL*62
03588                :MDSEAR-GMA-NBR,                                      CL*61
03589                :MDSEAR-DMA-NBR,                                      CL*47
03590                :MDSEAR-DEPT-NBR,                                     CL*47
03591                :MDSEAR-MAJ-CL-NBR,                                   CL*47
03592                :MDSEAR-SUB-CL-NBR,                                   CL*47
03593                :MDSEAR-MDSELV-CDE,                                   CL*47
03594                :MDSEAR-CHG-DTE,                                      CL*47
03595                :MDSEAR-CRE-DTE,                                      CL*47
03596                :MDSEAR-STRT-DTE,                                     CL*47
03597                :MDSEAR-END-DTE,                                      CL*47
03598                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL*47
03599                :MDSEAR-DESC,                                         CL*48
03600                :MDSEAR-BYR-NBR,                                      CL108
03601                :MDSEAR-LIST-SEQ-NBR,                                 CL190
03602                :MDSECL-POS-DESC,                                     CL190
03603                :MDSECL-TAX-DFLT-IND                                  CL187
03604      END-EXEC.                                                       CL*39
03605                                                                      CL*42
03606      SET PS-DB2-OK TO TRUE                                           CL*58
03607                                                                      CL*42
03608      IF (SQLCODE = -904) OR                                          CL*42
03609         (SQLCODE = -913)                                             CL*42
03610          SET PS-DB2-RETRY TO TRUE                                    CL*42
03611          ADD 1 TO PC-RETRY-CNTR                                      CL*42
03612          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL*42
03613              SET PS-DB2-OK TO TRUE                                   CL*42
03614          END-IF                                                      CL*42
03615      END-IF.                                                         CL*42
03616                                                                      CL*42
03617      IF PS-DB2-OK                                                    CL*42
03618          IF SQLCODE = +0                                             CL*42
03619              CONTINUE                                                CL*42
03620          ELSE                                                        CL*42
03621              IF SQLCODE = +100                                       CL*42
03622                  SET MB009-NO-ITEMS-LEFT-ON-DB TO TRUE               CL179
03623              ELSE                                                    CL*42
03624                  SET DP013-DB2-ABEND   TO TRUE                       CL*42
03625                  SET DP013-NO-ROLLBACK TO TRUE                       CL*42
03626                  MOVE 'Z210-READ-MERCH-CURSOR' TO                    CL*42
03627                       DP013-PARAGRAPH                                CL*42
03628                  MOVE PC-OPERCO-NBR            TO                    CL*42
03629                       AM-OPERCO-NBR-13                               CL*42
03630                  MOVE MB009-SAVE-DPT           TO                    CL179
03631                       AM-DEPT-NBR-13                                 CL*58
03632                  MOVE MB009-SAVE-MCL           TO                    CL179
03633                       AM-MCL-NBR-13                                  CL*58
03634                  MOVE MB009-SAVE-SCL           TO                    CL179
03635                       AM-SCL-NBR-13                                  CL*58
03636                  MOVE AM-ABEND-MESSAGE-13      TO                    CL*42
03637                       DP013-MESSAGE-TEXT(1)                          CL*59
03638                  MOVE SQLCA                    TO                    CL*42
03639                       DP013-SQLCA                                    CL*42
03640                  PERFORM DP013-0000-PROCESS-ABEND                    CL*42
03641              END-IF                                                  CL*42
03642          END-IF                                                      CL*42
03643      END-IF.                                                         CL*42
03644  EJECT                                                               CL*39
03645  Z220-CLOSE-MERCH-CURSOR.                                            CL*42
03646                                                                      CL*42
03647 ******************************************************************MBKCS108
03648 *    CLOSE THE MERCH-CURSOR                                          CL*42
03649 ******************************************************************MBKCS108
03650                                                                   MBKCS108
03651      EXEC SQL                                                        CL*42
03652          CLOSE MERCH-CURSOR                                          CL*42
03653      END-EXEC.                                                       CL*42
03654                                                                      CL*42
03655      IF (SQLCODE = +0)   OR                                          CL*42
03656         (SQLCODE = +100)                                             CL*42
03657          SET PS-MERCH-CURSOR-NOT-OPENED TO TRUE                      CL*84
03658      ELSE                                                            CL*42
03659          SET DP013-DB2-ABEND            TO TRUE                      CL*42
03660          SET DP013-NO-ROLLBACK          TO TRUE                      CL*42
03661          MOVE 'Z210-CLOSE-MERCH-CURSOR' TO                           CL*42
03662               DP013-PARAGRAPH                                        CL*42
03663          MOVE AM-ABEND-MESSAGE-22       TO                           CL*42
03664               DP013-MESSAGE-TEXT(1)                                  CL*42
03665          MOVE SQLCA                     TO                           CL*42
03666               DP013-SQLCA                                            CL*42
03667          PERFORM DP013-0000-PROCESS-ABEND                            CL*42
03668      END-IF.                                                         CL*42
03669  EJECT                                                            MBKCS108
03670                                                                      CL227
03671  Z400-RETURN-TO-PREV-TRAN.                                           CL227
03672 ******************************************************************   CL227
03673 *                                                                    CL227
03674 ******************************************************************   CL227
03675      MOVE DP016-PF3 TO DP020-SRC-AID.                                CL227
03676      PERFORM DP015-1000-START-NAVIGATOR.                             CL227
03677                                                                      CL227
03678  Z500-OPEN-DPT-CURSOR.                                               CL131
03679                                                                      CL129
03680 ******************************************************************   CL129
03681 *    OPEN MERCHANDISE AREA CURSOR -- DPT ONLY                        CL129
03682 ******************************************************************   CL129
03683                                                                      CL129
03684      EXEC SQL                                                        CL129
03685          OPEN DPT-CURSOR                                             CL129
03686      END-EXEC.                                                       CL129
03687                                                                      CL129
03688      IF (SQLCODE = +0)   OR                                          CL129
03689         (SQLCODE = +100)                                             CL129
03690          SET PS-DPT-CURSOR-OPENED       TO TRUE                      CL129
03691      ELSE                                                            CL129
03692          SET DP013-DB2-ABEND            TO TRUE                      CL129
03693          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
03694          MOVE 'Z500-OPEN-DPT-CURSOR'    TO                           CL129
03695               DP013-PARAGRAPH                                        CL129
03696          MOVE AM-ABEND-MESSAGE-21       TO                           CL129
03697               DP013-MESSAGE-TEXT(1)                                  CL129
03698          MOVE SQLCA                     TO                           CL129
03699               DP013-SQLCA                                            CL129
03700          PERFORM DP013-0000-PROCESS-ABEND                            CL129
03701      END-IF.                                                         CL129
03702  EJECT                                                               CL129
03703  Z510-READ-DPT-AREA.                                                 CL129
03704                                                                      CL137
03705 ******************************************************************   CL137
03706 *    READ MERCHANDISE AREA CURSOR -- DPT ONLY                        CL137
03707 ******************************************************************   CL137
03708                                                                      CL129
03709      EXEC SQL                                                        CL129
03710          FETCH DPT-CURSOR                                            CL129
03711           INTO :MDSEAR-OPERCO-NBR,                                   CL129
03712                :MDSEAR-GMA-NBR,                                      CL129
03713                :MDSEAR-DMA-NBR,                                      CL129
03714                :MDSEAR-DEPT-NBR,                                     CL129
03715                :MDSEAR-MAJ-CL-NBR,                                   CL129
03716                :MDSEAR-SUB-CL-NBR,                                   CL129
03717                :MDSEAR-MDSELV-CDE,                                   CL129
03718                :MDSEAR-CHG-DTE,                                      CL129
03719                :MDSEAR-CRE-DTE,                                      CL129
03720                :MDSEAR-STRT-DTE,                                     CL129
03721                :MDSEAR-END-DTE,                                      CL129
03722                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL129
03723                :MDSEAR-DESC,                                         CL129
03724                :MDSEAR-BYR-NBR,                                      CL129
03725                :MDSEAR-LIST-SEQ-NBR,                                 CL190
03726                :MDSECL-POS-DESC,                                     CL190
03727                :MDSECL-TAX-DFLT-IND                                  CL187
03728      END-EXEC.                                                       CL129
03729                                                                      CL129
03730      SET PS-DB2-OK TO TRUE                                           CL129
03731                                                                      CL129
03732      IF (SQLCODE = -904) OR                                          CL129
03733         (SQLCODE = -913)                                             CL129
03734          SET PS-DB2-RETRY TO TRUE                                    CL129
03735          ADD 1 TO PC-RETRY-CNTR                                      CL129
03736          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL129
03737              SET PS-DB2-OK TO TRUE                                   CL129
03738          END-IF                                                      CL129
03739      END-IF.                                                         CL129
03740                                                                      CL129
03741      IF PS-DB2-OK                                                    CL129
03742          IF SQLCODE = +0                                             CL129
03743              CONTINUE                                                CL129
03744          ELSE                                                        CL129
03745              IF SQLCODE = +100                                       CL129
03746                  SET MB009-NO-ITEMS-LEFT-ON-DB TO TRUE               CL179
03747              ELSE                                                    CL129
03748                  SET DP013-DB2-ABEND   TO TRUE                       CL129
03749                  SET DP013-NO-ROLLBACK TO TRUE                       CL129
03750                  MOVE 'Z510-READ-DPT-CURSOR'   TO                    CL129
03751                       DP013-PARAGRAPH                                CL129
03752                  MOVE PC-OPERCO-NBR            TO                    CL129
03753                       AM-OPERCO-NBR-13                               CL129
03754                  MOVE MB009-SAVE-DPT           TO                    CL179
03755                       AM-DEPT-NBR-13                                 CL129
03756                  MOVE MB009-SAVE-MCL           TO                    CL179
03757                       AM-MCL-NBR-13                                  CL129
03758                  MOVE MB009-SAVE-SCL           TO                    CL179
03759                       AM-SCL-NBR-13                                  CL129
03760                  MOVE AM-ABEND-MESSAGE-13      TO                    CL129
03761                       DP013-MESSAGE-TEXT(1)                          CL129
03762                  MOVE SQLCA                    TO                    CL129
03763                       DP013-SQLCA                                    CL129
03764                  PERFORM DP013-0000-PROCESS-ABEND                    CL129
03765              END-IF                                                  CL129
03766          END-IF                                                      CL129
03767      END-IF.                                                         CL129
03768  EJECT                                                               CL129
03769  Z520-CLOSE-DPT-CURSOR.                                              CL129
03770                                                                      CL129
03771 ******************************************************************   CL129
03772 *    CLOSE THE DPT-CURSOR                                            CL148
03773 ******************************************************************   CL129
03774                                                                      CL129
03775      EXEC SQL                                                        CL129
03776          CLOSE DPT-CURSOR                                            CL129
03777      END-EXEC.                                                       CL129
03778                                                                      CL129
03779      IF (SQLCODE = +0)   OR                                          CL129
03780         (SQLCODE = +100)                                             CL129
03781          SET PS-DPT-CURSOR-NOT-OPENED TO TRUE                        CL129
03782      ELSE                                                            CL129
03783          SET DP013-DB2-ABEND            TO TRUE                      CL129
03784          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
03785          MOVE 'Z520-CLOSE-DPT-CURSOR'   TO                           CL148
03786               DP013-PARAGRAPH                                        CL129
03787          MOVE AM-ABEND-MESSAGE-22       TO                           CL129
03788               DP013-MESSAGE-TEXT(1)                                  CL129
03789          MOVE SQLCA                     TO                           CL129
03790               DP013-SQLCA                                            CL129
03791          PERFORM DP013-0000-PROCESS-ABEND                            CL129
03792      END-IF.                                                         CL129
03793  EJECT                                                               CL129
03794  Z600-OPEN-MCL-CURSOR.                                               CL148
03795                                                                      CL148
03796 ******************************************************************   CL148
03797 *    OPEN MERCHANDISE AREA CURSOR -- MCL ONLY                        CL148
03798 ******************************************************************   CL148
03799                                                                      CL148
03800      EXEC SQL                                                        CL148
03801          OPEN MCL-CURSOR                                             CL148
03802      END-EXEC.                                                       CL148
03803                                                                      CL148
03804      IF (SQLCODE = +0)   OR                                          CL148
03805         (SQLCODE = +100)                                             CL148
03806          SET PS-MCL-CURSOR-OPENED       TO TRUE                      CL148
03807      ELSE                                                            CL148
03808          SET DP013-DB2-ABEND            TO TRUE                      CL148
03809          SET DP013-NO-ROLLBACK          TO TRUE                      CL148
03810          MOVE 'Z600-OPEN-MCL-CURSOR'    TO                           CL148
03811               DP013-PARAGRAPH                                        CL148
03812          MOVE AM-ABEND-MESSAGE-21       TO                           CL148
03813               DP013-MESSAGE-TEXT(1)                                  CL148
03814          MOVE SQLCA                     TO                           CL148
03815               DP013-SQLCA                                            CL148
03816          PERFORM DP013-0000-PROCESS-ABEND                            CL148
03817      END-IF.                                                         CL148
03818  EJECT                                                               CL148
03819  Z610-READ-MCL-AREA.                                                 CL148
03820                                                                      CL148
03821 ******************************************************************   CL148
03822 *    READ MERCHANDISE AREA CURSOR -- MCL ONLY                        CL148
03823 ******************************************************************   CL148
03824                                                                      CL148
03825      EXEC SQL                                                        CL148
03826          FETCH MCL-CURSOR                                            CL148
03827           INTO :MDSEAR-OPERCO-NBR,                                   CL148
03828                :MDSEAR-GMA-NBR,                                      CL148
03829                :MDSEAR-DMA-NBR,                                      CL148
03830                :MDSEAR-DEPT-NBR,                                     CL148
03831                :MDSEAR-MAJ-CL-NBR,                                   CL148
03832                :MDSEAR-SUB-CL-NBR,                                   CL148
03833                :MDSEAR-MDSELV-CDE,                                   CL148
03834                :MDSEAR-CHG-DTE,                                      CL148
03835                :MDSEAR-CRE-DTE,                                      CL148
03836                :MDSEAR-STRT-DTE,                                     CL148
03837                :MDSEAR-END-DTE,                                      CL148
03838                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL148
03839                :MDSEAR-DESC,                                         CL148
03840                :MDSEAR-BYR-NBR,                                      CL148
03841                :MDSEAR-LIST-SEQ-NBR,                                 CL190
03842                :MDSECL-POS-DESC,                                     CL190
03843                :MDSECL-TAX-DFLT-IND                                  CL187
03844      END-EXEC.                                                       CL148
03845                                                                      CL148
03846      SET PS-DB2-OK TO TRUE                                           CL148
03847                                                                      CL148
03848      IF (SQLCODE = -904) OR                                          CL148
03849         (SQLCODE = -913)                                             CL148
03850          SET PS-DB2-RETRY TO TRUE                                    CL148
03851          ADD 1 TO PC-RETRY-CNTR                                      CL148
03852          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL148
03853              SET PS-DB2-OK TO TRUE                                   CL148
03854          END-IF                                                      CL148
03855      END-IF.                                                         CL148
03856                                                                      CL148
03857      IF PS-DB2-OK                                                    CL148
03858          IF SQLCODE = +0                                             CL148
03859              CONTINUE                                                CL148
03860          ELSE                                                        CL148
03861              IF SQLCODE = +100                                       CL148
03862                  SET MB009-NO-ITEMS-LEFT-ON-DB TO TRUE               CL179
03863              ELSE                                                    CL148
03864                  SET DP013-DB2-ABEND   TO TRUE                       CL148
03865                  SET DP013-NO-ROLLBACK TO TRUE                       CL148
03866                  MOVE 'Z610-READ-MCL-CURSOR'   TO                    CL148
03867                       DP013-PARAGRAPH                                CL148
03868                  MOVE PC-OPERCO-NBR            TO                    CL148
03869                       AM-OPERCO-NBR-13                               CL148
03870                  MOVE MB009-SAVE-DPT           TO                    CL179
03871                       AM-DEPT-NBR-13                                 CL148
03872                  MOVE MB009-SAVE-MCL           TO                    CL179
03873                       AM-MCL-NBR-13                                  CL148
03874                  MOVE MB009-SAVE-SCL           TO                    CL179
03875                       AM-SCL-NBR-13                                  CL148
03876                  MOVE AM-ABEND-MESSAGE-13      TO                    CL148
03877                       DP013-MESSAGE-TEXT(1)                          CL148
03878                  MOVE SQLCA                    TO                    CL148
03879                       DP013-SQLCA                                    CL148
03880                  PERFORM DP013-0000-PROCESS-ABEND                    CL148
03881              END-IF                                                  CL148
03882          END-IF                                                      CL148
03883      END-IF.                                                         CL148
03884  EJECT                                                               CL148
03885  Z620-CLOSE-MCL-CURSOR.                                              CL148
03886                                                                      CL148
03887 ******************************************************************   CL148
03888 *    CLOSE THE MCL-CURSOR                                            CL148
03889 ******************************************************************   CL148
03890                                                                      CL148
03891      EXEC SQL                                                        CL148
03892          CLOSE MCL-CURSOR                                            CL148
03893      END-EXEC.                                                       CL148
03894                                                                      CL148
03895      IF (SQLCODE = +0)   OR                                          CL148
03896         (SQLCODE = +100)                                             CL148
03897          SET PS-MCL-CURSOR-NOT-OPENED TO TRUE                        CL148
03898      ELSE                                                            CL148
03899          SET DP013-DB2-ABEND            TO TRUE                      CL148
03900          SET DP013-NO-ROLLBACK          TO TRUE                      CL148
03901          MOVE 'Z620-CLOSE-MCL-CURSOR'   TO                           CL148
03902               DP013-PARAGRAPH                                        CL148
03903          MOVE AM-ABEND-MESSAGE-22       TO                           CL148
03904               DP013-MESSAGE-TEXT(1)                                  CL148
03905          MOVE SQLCA                     TO                           CL148
03906               DP013-SQLCA                                            CL148
03907          PERFORM DP013-0000-PROCESS-ABEND                            CL148
03908      END-IF.                                                         CL148
03909  EJECT                                                               CL148
03910  Z700-OPEN-SCL-CURSOR.                                               CL150
03911                                                                      CL150
03912 ******************************************************************   CL150
03913 *    OPEN MERCHANDISE AREA CURSOR -- SCL ONLY                        CL150
03914 ******************************************************************   CL150
03915                                                                      CL150
03916      EXEC SQL                                                        CL150
03917          OPEN SCL-CURSOR                                             CL150
03918      END-EXEC.                                                       CL150
03919                                                                      CL150
03920      IF (SQLCODE = +0)   OR                                          CL150
03921         (SQLCODE = +100)                                             CL150
03922          SET PS-SCL-CURSOR-OPENED       TO TRUE                      CL150
03923      ELSE                                                            CL150
03924          SET DP013-DB2-ABEND            TO TRUE                      CL150
03925          SET DP013-NO-ROLLBACK          TO TRUE                      CL150
03926          MOVE 'Z700-OPEN-SCL-CURSOR'    TO                           CL150
03927               DP013-PARAGRAPH                                        CL150
03928          MOVE AM-ABEND-MESSAGE-21       TO                           CL150
03929               DP013-MESSAGE-TEXT(1)                                  CL150
03930          MOVE SQLCA                     TO                           CL150
03931               DP013-SQLCA                                            CL150
03932          PERFORM DP013-0000-PROCESS-ABEND                            CL150
03933      END-IF.                                                         CL150
03934  EJECT                                                               CL150
03935  Z710-READ-SCL-AREA.                                                 CL150
03936                                                                      CL150
03937 ******************************************************************   CL150
03938 *    READ MERCHANDISE AREA CURSOR -- SCL ONLY                        CL150
03939 ******************************************************************   CL150
03940                                                                      CL150
03941      EXEC SQL                                                        CL150
03942          FETCH SCL-CURSOR                                            CL151
03943           INTO :MDSEAR-OPERCO-NBR,                                   CL150
03944                :MDSEAR-GMA-NBR,                                      CL150
03945                :MDSEAR-DMA-NBR,                                      CL150
03946                :MDSEAR-DEPT-NBR,                                     CL150
03947                :MDSEAR-MAJ-CL-NBR,                                   CL150
03948                :MDSEAR-SUB-CL-NBR,                                   CL150
03949                :MDSEAR-MDSELV-CDE,                                   CL150
03950                :MDSEAR-CHG-DTE,                                      CL150
03951                :MDSEAR-CRE-DTE,                                      CL150
03952                :MDSEAR-STRT-DTE,                                     CL150
03953                :MDSEAR-END-DTE,                                      CL150
03954                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL150
03955                :MDSEAR-DESC,                                         CL150
03956                :MDSEAR-BYR-NBR,                                      CL150
03957                :MDSEAR-LIST-SEQ-NBR,                                 CL190
03958                :MDSECL-POS-DESC,                                     CL190
03959                :MDSECL-TAX-DFLT-IND                                  CL187
03960      END-EXEC.                                                       CL150
03961                                                                      CL150
03962      SET PS-DB2-OK TO TRUE                                           CL150
03963                                                                      CL150
03964      IF (SQLCODE = -904) OR                                          CL150
03965         (SQLCODE = -913)                                             CL150
03966          SET PS-DB2-RETRY TO TRUE                                    CL150
03967          ADD 1 TO PC-RETRY-CNTR                                      CL150
03968          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL150
03969              SET PS-DB2-OK TO TRUE                                   CL150
03970          END-IF                                                      CL150
03971      END-IF.                                                         CL150
03972                                                                      CL150
03973      IF PS-DB2-OK                                                    CL150
03974          IF SQLCODE = +0                                             CL150
03975              CONTINUE                                                CL150
03976          ELSE                                                        CL150
03977              IF SQLCODE = +100                                       CL150
03978                  SET MB009-NO-ITEMS-LEFT-ON-DB TO TRUE               CL179
03979              ELSE                                                    CL150
03980                  SET DP013-DB2-ABEND   TO TRUE                       CL150
03981                  SET DP013-NO-ROLLBACK TO TRUE                       CL150
03982                  MOVE 'Z710-READ-SCL-CURSOR'   TO                    CL150
03983                       DP013-PARAGRAPH                                CL150
03984                  MOVE PC-OPERCO-NBR            TO                    CL150
03985                       AM-OPERCO-NBR-13                               CL150
03986                  MOVE MB009-SAVE-DPT           TO                    CL179
03987                       AM-DEPT-NBR-13                                 CL150
03988                  MOVE MB009-SAVE-MCL           TO                    CL179
03989                       AM-MCL-NBR-13                                  CL150
03990                  MOVE MB009-SAVE-SCL           TO                    CL179
03991                       AM-SCL-NBR-13                                  CL150
03992                  MOVE AM-ABEND-MESSAGE-13      TO                    CL150
03993                       DP013-MESSAGE-TEXT(1)                          CL150
03994                  MOVE SQLCA                    TO                    CL150
03995                       DP013-SQLCA                                    CL150
03996                  PERFORM DP013-0000-PROCESS-ABEND                    CL150
03997              END-IF                                                  CL150
03998          END-IF                                                      CL150
03999      END-IF.                                                         CL150
04000  EJECT                                                               CL150
04001  Z720-CLOSE-SCL-CURSOR.                                              CL150
04002                                                                      CL150
04003 ******************************************************************   CL150
04004 *    CLOSE THE SCL-CURSOR                                            CL150
04005 ******************************************************************   CL150
04006                                                                      CL150
04007      EXEC SQL                                                        CL150
04008          CLOSE SCL-CURSOR                                            CL150
04009      END-EXEC.                                                       CL150
04010                                                                      CL150
04011      IF (SQLCODE = +0)   OR                                          CL150
04012         (SQLCODE = +100)                                             CL150
04013          SET PS-SCL-CURSOR-NOT-OPENED TO TRUE                        CL150
04014      ELSE                                                            CL150
04015          SET DP013-DB2-ABEND            TO TRUE                      CL150
04016          SET DP013-NO-ROLLBACK          TO TRUE                      CL150
04017          MOVE 'Z720-CLOSE-SCL-CURSOR'   TO                           CL150
04018               DP013-PARAGRAPH                                        CL150
04019          MOVE AM-ABEND-MESSAGE-22       TO                           CL150
04020               DP013-MESSAGE-TEXT(1)                                  CL150
04021          MOVE SQLCA                     TO                           CL150
04022               DP013-SQLCA                                            CL150
04023          PERFORM DP013-0000-PROCESS-ABEND                            CL150
04024      END-IF.                                                         CL150
04025  EJECT                                                               CL150
04026 ******************************************************************   CL*42
04027 *    FILL IN SCREEN HEADER MODULE                                    CL*42
04028 ******************************************************************   CL*42
04029                                                                      CL*42
04030      COPY DPPD002.                                                   CL*42
04031  EJECT                                                               CL*42
04032 ******************************************************************MBKCS108
04033 *    RETURN TO CICS MODULE                                        MBKCS108
04034 ******************************************************************MBKCS108
04035                                                                   MBKCS108
04036      COPY DPPD003.                                                   CL**3
04037  EJECT                                                            MBKCS108
04038 ******************************************************************MBKCS108
04039 *    FORMAT MESSAGE LINES MODULE                                  MBKCS108
04040 ******************************************************************MBKCS108
04041                                                                   MBKCS108
04042      COPY DPPD005.                                                   CL181
04043  EJECT                                                            MBKCS108
04044 ******************************************************************MBKCS108
04045 *    GET PANEL MODULE                                             MBKCS108
04046 ******************************************************************MBKCS108
04047                                                                   MBKCS108
04048      COPY DPPD007.                                                   CL**3
04049  EJECT                                                            MBKCS108
04050 ******************************************************************MBKCS108
04051 *    WRITE PANEL MODULE                                           MBKCS108
04052 ******************************************************************MBKCS108
04053                                                                   MBKCS108
04054      COPY DPPD008.                                                   CL**3
04055  EJECT                                                            MBKCS108
04056 ******************************************************************MBKCS108
04057 *    TOGGLE THE FUNCTION KEY LINE MODULE                          MBKCS108
04058 ******************************************************************MBKCS108
04059                                                                   MBKCS108
04060      COPY DPPD009.                                                   CL**3
04061  EJECT                                                            MBKCS108
04062 ******************************************************************MBKCS108
04063 *    ABEND PROCESSOR MODULE                                       MBKCS108
04064 ******************************************************************MBKCS108
04065                                                                   MBKCS108
04066      COPY DPPD013.                                                   CL**3
04067  EJECT                                                            MBKCS108
04068 ******************************************************************MBKCS108
04069 *    CHECK COMMUNICATION AREA MODULE                              MBKCS108
04070 ******************************************************************MBKCS108
04071                                                                   MBKCS108
04072      COPY DPPD015.                                                   CL**3
04073  EJECT                                                            MBKCS108
04074 ******************************************************************MBKCS108
04075 *    UPDATE TIME AND DATE MODULE                                  MBKCS108
04076 ******************************************************************MBKCS108
04077                                                                   MBKCS108
04078      COPY DPPD017.                                                   CL**3
04079  EJECT                                                            MBKCS108
