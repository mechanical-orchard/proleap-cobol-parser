       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP014.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050006
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070006
      **       VSM ITEM MESSAGE - NUMBER 060 - SKU PACKAGING ATTR CHANGE00080006
      ******************************************************************00090006
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100017
      ***     ATTRIBUTES ON XST_SKU:                                    00110017
      ***     1. WD_UM_QTY                                              00120017
      ***     2. HT_UM_QTY                                              00130017
      ***     3. DPTH_UM_QTY                                            00140017
      ***     4. SIZE_UM_CDE                                            00150017
      ***     5. WT_UM_CDE                                              00160017
      ***     6. WT_UM_QTY                                              00170017
091004***     7. POG_IND                                                00180020
      ******************************************************************00190006
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00200006
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00210015
022103**02/21/2003 CHANGED BY:JULIE SMITH  COMPARE INTRL UPC ID AND     00220018
022103**           UPC ID ON MESSAGE AND IF EQUAL PROCESS MESSAGE       00230018
022103**           ELSE DO NOT PROCESS BECAUSE IT IS A RENUMBER SKU     00240018
091004**09/10/2004 CHANGED BY:JULIE TEWES  ADD POG_IND TO TABLE         00250019
      ******************************************************************00260006
       ENVIRONMENT DIVISION.                                            00270000
       CONFIGURATION SECTION.                                           00280000
       SOURCE-COMPUTER.  IBM-3090.                                      00290000
       OBJECT-COMPUTER.  IBM-3090.                                      00300000
       DATA DIVISION.                                                   00310000
                                                                        00320004
       WORKING-STORAGE SECTION.                                         00330000
      ******************************************************************00340000
      **PROGRAM SWITCHES                                                00350000
      ******************************************************************00360000
       01  PS-PROGRAM-SWITCHES.                                         00370000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01) VALUE 'N'.         00380007
               88  PS-PROCESS-CONTINUE               VALUE 'Y'.         00390007
               88  PS-PROCESS-COMPLETE               VALUE 'N'.         00400007
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00410011
               88  PS-ACTION-REVERSE                  VALUE 01.         00420011
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00430011
               88  PS-ACTION-NOTHING                  VALUE 02.         00440011
      ******************************************************************00450000
      **PROGRAM COUNTERS                                                00460008
      ******************************************************************00470000
       01  PC-PROGRAM-COUNTERS.                                         00480008
           05  PC-MAX-RETRIES              PIC 9(01) VALUE 5.           00490008
           05  PC-RETRIES                  PIC 9(01) VALUE ZEROS.       00500008
      ******************************************************************00510004
      **PROGRAM CALCULATIONS                                            00520004
      ******************************************************************00530004
       01  PC-PROGRAM-CALCULATIONS.                                     00540004
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00550004
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00560004
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00570004
      ******************************************************************00580000
      **PROGRAM VARIABLES                                               00590008
      ******************************************************************00600000
       01  PV-PROGRAM-VARIABLES.                                        00610008
           05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACE.       00620008
      ******************************************************************00630000
      **COPYBOOKS                                                       00640000
      ******************************************************************00650000
      ***ABEND COPYBOOK                                                 00660000
           COPY DPWS004.                                                00670019
      ***DB2 MESSAGE AREA                                               00680000
           COPY SQLCA2.                                                 00690019
      ***ITEM COPYBOOK                                                  00700006
           COPY IMRD008.                                                00710019
      ******************************************************************00720000
      **TABLE INCLUSIONS                                                00730000
      ******************************************************************00740000
      ***DB2 COMMUNICATIONS AREA                                        00750000
           EXEC SQL                                                     00760000
                INCLUDE SQLCA                                           00770000
           END-EXEC.                                                    00780000
      ***XST_SKU TABLE                                                  00790000
           EXEC SQL                                                     00800000
               INCLUDE XST00010                                         00810000
           END-EXEC.                                                    00820000
                                                                        00830000
       LINKAGE SECTION.                                                 00840000
      ******************************************************************00850000
      **LINKAGE VARIABLES                                               00860000
      ******************************************************************00870000
       01  LV-IMRD008-RECORD               PIC X(450).                  00880006
       01  LV-RETURN-CODE                  PIC S9(04).                  00890004
       01  LV-ACTION-IND                   PIC 9(02).                   00900011
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00910011
                                                                        00920000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00930006
                                LV-RETURN-CODE,                         00940011
                                LV-ACTION-IND,                          00950011
                                LV-CURRENT-TIMESTAMP.                   00960011
      ******************************************************************00970000
       0000-MAINLINE.                                                   00980000
                                                                        00990000
            PERFORM 1000-INITIALIZATION.                                01000000
            PERFORM 2000-PROCESS-COMPLETE                               01010006
                    UNTIL PS-PROCESS-COMPLETE.                          01020007
            PERFORM 9999-WRAPUP.                                        01030000
                                                                        01040000
       EJECT.                                                           01050005
      ***************************************************************** 01060000
      **INITIALIZATION AREA                                             01070000
      ***************************************************************** 01080000
       1000-INITIALIZATION.                                             01090000
                                                                        01100000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01110008
                                                                        01120000
           INITIALIZE PV-PROGRAM-VARIABLES.                             01130009
           MOVE LV-IMRD008-RECORD       TO IMRD008-RECORD.              01140010
                                                                        01150004
           MOVE IM008-SKU-PKG-WD-UM-QTY-N   TO XST00010-WD-UM-QTY.      01160005
           MOVE IM008-SKU-PKG-HT-UM-QTY-N   TO XST00010-HT-UM-QTY.      01170005
           MOVE IM008-SKU-PKG-DPTH-UM-QTY-N TO XST00010-DPTH-UM-QTY.    01180005
           MOVE IM008-SKU-PKG-SIZE-UM-CDE-N TO XST00010-SIZE-UM-CDE.    01190005
           MOVE IM008-SKU-PKG-WT-UM-CDE-N   TO XST00010-WT-UM-CDE.      01200005
           MOVE IM008-SKU-PKG-WT-UM-QTY-N   TO XST00010-WT-UM-QTY.      01210005
091004     MOVE IM008-SKU-PKG-POG-IND-N     TO XST00010-POG-IND.        01220019
121402     MOVE LV-CURRENT-TIMESTAMP        TO XST00010-LAST-UPD-TMST.  01230015
121402     MOVE IM008-CRE-TMST           TO XST00010-SOR-LAST-MSG-TMST. 01240015
                                                                        01250005
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01260007
      *     COMP FIELDS.                                                01270007
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01280005
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01290005
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01300005
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01310005
                                                                        01320005
022103*DETERMINES IF MESSAGE SHOULD BE PROCESSED OR PASSED IF FIELDS    01330018
022103*    ARE NOT EQUAL THEN MESSAGE IS FOR THE RENUMBERED SKU         01340018
022103*    AND SHOULD NOT BE PROCESSED AT THE SKU LEVEL                 01350018
022103     IF  IM008-ITEM-KEY-INTR-UPC-ID = IM008-ITEM-KEY-UPC-ID       01360018
022103         SET PS-PROCESS-CONTINUE TO TRUE                          01370018
022103     ELSE                                                         01380018
022103         SET PS-PROCESS-COMPLETE TO TRUE                          01390018
022103     END-IF.                                                      01400018
                                                                        01410018
       EJECT.                                                           01420005
      ***************************************************************** 01430000
      **UPDATES PACKAGING ATTRITUBES ON XST_SKU TABLE                   01440017
      ***************************************************************** 01450000
       2000-PROCESS-COMPLETE.                                           01460006
                                                                        01470000
           MOVE '2000-PROCESS-COMPLETE'       TO PV-PARAGRAPH-NAME.     01480008
                                                                        01490000
           EXEC SQL                                                     01500005
              UPDATE XST_SKU                                            01510005
                  SET   WD_UM_QTY    = :XST00010-WD-UM-QTY              01520005
                      , HT_UM_QTY    = :XST00010-HT-UM-QTY              01530005
                      , DPTH_UM_QTY  = :XST00010-DPTH-UM-QTY            01540005
                      , SIZE_UM_CDE  = :XST00010-SIZE-UM-CDE            01550005
                      , WT_UM_CDE    = :XST00010-WT-UM-CDE              01560005
                      , WT_UM_QTY    = :XST00010-WT-UM-QTY              01570005
                      , LAST_UPD_TMST = :XST00010-LAST-UPD-TMST         01580015
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01590015
091004                , POG_IND      = :XST00010-POG-IND                01600019
                  WHERE INTR_UPC_ID  = :XST00010-INTR-UPC-ID            01610005
           END-EXEC.                                                    01620005
                                                                        01630005
           EVALUATE TRUE                                                01640005
      ***SUCCESSFUL PROCESSING                                          01650017
              WHEN SQLCODE = ZERO                                       01660005
                   SET PS-PROCESS-COMPLETE TO TRUE                      01670007
      ***ROW/TABLE LOCK                                                 01680017
              WHEN SQLCODE = -911                                       01690005
              WHEN SQLCODE = -913                                       01700005
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     01710008
                       MOVE 'N'               TO PS-PROCESS-CONTINUE-SW 01720007
                   ELSE                                                 01730005
                       ADD +1                 TO PC-RETRIES             01740008
                       SET PS-PROCESS-CONTINUE TO TRUE                  01750007
                END-IF                                                  01760005
              WHEN OTHER                                                01770005
                   SET PS-PROCESS-COMPLETE TO TRUE                      01780007
           END-EVALUATE.                                                01790005
                                                                        01800000
       EJECT.                                                           01810005
      ******************************************************************01820000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01830000
      ******************************************************************01840000
       9999-WRAPUP.                                                     01850000
                                                                        01860000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME.             01870014
                                                                        01880000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01890014
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01900011
                                                                        01910011
           EXIT PROGRAM.                                                01920000
                                                                        01930000
       EJECT.                                                           01940005
