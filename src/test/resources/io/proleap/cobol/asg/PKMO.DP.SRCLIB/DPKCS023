      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    DPKCS023.                                                 
       AUTHOR.        MIKE FURLICK.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10-10-91.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    Z023 - MNEMONIC ATTRIBUTE INQUIRY                           *        
      *                                                                *        
      *    THIS PROGRAM DISPLAYS THE MNEMONIC, PROGRAM, TRANSACTION,   *        
      *    PANEL TITLE, AND GLOBAL ACCESS.  THIS PROGRAM READS TSQ "B" *        
      *    FOR INPUT WHICH ALLOWS "SCROLLING" FROM ONE ITEM SELECTED   *        
      *    FROM A LIST SCREEN TO ANOTHER.                              *        
      *                                                                *        
      *    03/14/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED MAP (DPKM023) AND PGM TO     *        
      *                          SET THE UPD-VSAM-IND, DISPLAY IT ON   *        
      *                          THE SCREEN, AND TO ADD IT TO THE      *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *                       2) MODIFIED MAP (DPKM023) AND PGM TO     *        
      *                          DISPLAY PROGRAM-NAME, FROM THE        *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *    09/09/99  TKMA464  ADD ACCURATE LOCATION REQUIRED INDICATOR.*        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.        
           05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072          
                                                           COMP SYNC.           
           05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE                
           'DP023A  '.                                                          
           05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE                
           'DPKM023 '.                                                          
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE                
           'DPKCS023'.                                                          
           05  PC-ZERO                        PIC X(3)     VALUE '000'.         
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
                                                           COMP-3.              
       01  PC-COMPANY                         PIC XX       VALUE '03'.          
                                                                                
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00005                PIC 9(5)    VALUE 00005.          
           05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.          
           05  PC-TSYMSG-00045                PIC 9(5)    VALUE 00045.          
           05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.          
           05  PC-TSYMSG-00051                PIC 9(5)    VALUE 00051.          
           05  PC-TSYMSG-00052                PIC 9(5)    VALUE 00052.          
           05  PC-TSYMSG-00053                PIC 9(5)    VALUE 00053.          
           05  PC-TSYMSG-00054                PIC 9(5)    VALUE 00054.          
           05  PC-TSYMSG-00055                PIC 9(5)    VALUE 00055.          
           05  PC-TSYMSG-00057                PIC 9(5)    VALUE 00057.          
           05  PC-TSYMSG-00061                PIC 9(5)    VALUE 00061.          
           05  PC-TSYMSG-00062                PIC 9(5)    VALUE 00062.          
           05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
           05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.          
           05  PC-TSYMSG-99901                PIC 9(5)    VALUE 99901.          
                                                                                
       01  PM-PROGRAM-MESSAGES.                                                 
           05  PM-PROGRAM-MESSAGE-12          PIC X(78)    VALUE                
           'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                        
           05  PM-PROGRAM-MESSAGE-22          PIC X(78)    VALUE                
           'LOGIC ERROR IN TRYING TO READ MD LIST QUEUE - RESP = '.             
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-22.                          
               10  FILLER                     PIC X(61).                        
               10  PM-RESP-22                 PIC 9999.                         
               10  FILLER                     PIC X(12).                        
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PS-DP-SUB1                     PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PS-DP-SUB2                     PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0             
                                                           COMP-3.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.           
               88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.           
               88  PS-SEND-DATA-ONLY                       VALUE '4'.           
               88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.           
               88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.           
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
           05  PS-INTER-APPL-VALID-SW         PIC X(01)    VALUE 'N'.           
               88  PS-INTER-APPL-VALID                     VALUE 'Y'.           
               88  PS-INTER-APPL-NOT-VALID                 VALUE 'N'.           
           05  PS-PROCESSING-SW               PIC X(01)    VALUE 'N'.           
               88  PS-CONTINUE                             VALUE 'Y'.           
               88  PS-DONT-PROCESS                         VALUE 'N'.           
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           EJECT                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-RESP                        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-ITEM                        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-DP-ITEM                     PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.        
           05  PV-SQL-SUB                     PIC S9(4)    COMP.                
           05  PV-RETRY-COUNTER               PIC S9(3)    COMP-3.              
           05  PV-WORK-DATE.                                                    
               10  PV-WD-MM                   PIC XX.                           
               10  FILLER                     PIC X        VALUE '/'.           
               10  PV-WD-DD                   PIC XX.                           
               10  FILLER                     PIC X        VALUE '/'.           
               10  PV-WD-YY                   PIC XX.                           
                                                                                
       01  PV-DB2-DATE.                                                         
           05  FILLER                         PIC XX.                           
           05  PV-DD-YY                       PIC XX.                           
           05  FILLER                         PIC X.                            
           05  PV-DD-MM                       PIC XX.                           
           05  FILLER                         PIC X.                            
           05  PV-DD-DD                       PIC XX.                           
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *        
      *----------------------------------------------------------------*        
           COPY DPWS006.                                                        
       EJECT                                                                    
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR DPKCS023.                                     
      *****************************************************************         
               10  DP023-SPECIFIC-COMMAREA.                                     
                   15  DP023-APPL-CURSOR-POSITION          PIC S9(04)           
                                                           COMP.                
                   15  DP023-MN-SUB                        PIC S9(4)            
                                                               COMP.            
                   15  DP023-MN-ITEM                       PIC S9(4)            
                                                               COMP.            
                   15  DP023-MNEMONIC                      PIC X(08).           
                   15  DP023-PROGRAM                       PIC X(08).           
                   15  DP023-TRAN                          PIC X(04).           
                   15  DP023-TITLE                         PIC X(30).           
                   15  DP023-TRAN-AVAIL                    PIC X(1).            
                   15  DP023-STANDARD-COMMAREA             PIC X(1).            
                   15  DP023-SAVE-COMMAREA                 PIC X(1).            
                   15  DP023-COMMAREA-LENGTH               PIC 9(4).            
                   15  DP023-GLOBAL-ACCESS                 PIC X(1).            
                   15  DP023-SCREEN-TYPE-CODE              PIC X(1).            
                   15  DP023-UPD-VSAM-IND                  PIC X(1).            
                   15  DP023-DB2-PKG-CDE                   PIC X(01).           
                   15  DP023-CMNT-TXT                      PIC X(40).           
                   15  DP023-ACCURATE-LOCATION-REQ         PIC X(1).            
                   15  DP023-TEMP-QUEUE-NAME.                                   
                       20  DP023-TERM-ID                   PIC X(4).            
                       20  DP023-TASK-NUMBER               PIC S9(7)            
                                                               COMP-3.          
                   15  DP023-SAVE-INTER-APPL-COMMAREA      PIC X(200).          
                   15  DP023-SAVE-LIST-RECORD              PIC X(2100).         
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    MNEMONIC SELECTION TSQ B RECORD LAYOUT.                     *        
      *----------------------------------------------------------------*        
           COPY DPWS002.                                                        
                                                                                
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    CURRENT DATE AND TIME COPYBOOK.                             *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS017.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    TOGGLE FUNCTION KEY LINES COPYBOOK.                         *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS009.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK.                                     *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    DPKUT045 COPYBOOK.                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS045.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    NUMERIC EDIT LAYOUT.                                        *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *----------------------------------------------------------------*        
      *    MESSAGE RECORD LAYOUT                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPRD507I.                                                       
                                                                                
      *----------------------------------------------------------------*        
      *    STANDARD ATTENTION IDENTIFIER LIST                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DFHAID.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DFHBMSCA.                                                       
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPKM023.                                                        
       01  MD-MAP-DATA-AREA  REDEFINES DP023AO PIC X(1400).                     
                                                                                
       EJECT                                                                    
                                                                                
      *    DB2 AREA FOR TMNEMONIC                                               
                                                                                
           EXEC SQL                                                             
                INCLUDE TMNMNC                                                  
           END-EXEC.                                                            
                                                                                
      *    DB2 AREA FOR COMMUNICATIONS                                          
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.          *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
           IF DP020-SRC-RETURN-W-TRAN-ID                                        
               PERFORM 1000-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *                                                                *        
      *    PERFORMED BY 0000-MAIN-MODULE.                              *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           SET PS-CONTINUE           TO  TRUE.                                  
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   PERFORM 4100-VALIDATE-COMMAREA                               
                   IF  PS-COMMAREA-NOT-VALID                                    
                       MOVE PC-TSYMSG-99901                                     
                                     TO  DP020-MSG-NUMBER                       
                       MOVE DP016-PF3                                           
                                     TO  DP020-SRC-AID                          
                       SET DP020-MSG-FATAL                                      
                                     TO  TRUE                                   
                       PERFORM DP015-1000-START-NAVIGATOR                       
                   ELSE                                                         
                       INITIALIZE DP023-SPECIFIC-COMMAREA                       
                       SET PS-SEND-MAP-N-DATA-FRSET                             
                                     TO  TRUE                                   
                       SET DP020-NEXT-ACT-READ-MAP                              
                                     TO  TRUE                                   
                       PERFORM 2000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 3000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                     TO  TRUE                                   
                   PERFORM 2300-UPDATE-TSQB                                     
                   MOVE DP023-SAVE-INTER-APPL-COMMAREA                          
                                     TO  DP055-INTER-APPL-COMMAREA              
                   PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
               WHEN DP020-NEXT-ACT-APPL-ERROR                                   
                   MOVE DP023-SAVE-INTER-APPL-COMMAREA                          
                                     TO  DP055-INTER-APPL-COMMAREA              
                   MOVE DP020-MSG-MESSAGE TO PV-MESSAGE                         
                   IF DP020-NAV-MNEMONIC = SPACES                               
                       MOVE SPACES   TO  ANAVCMDO                               
                       MOVE DP015-UNDERLINE                                     
                                     TO  ANAVCMDH                               
                       MOVE DP015-GREEN                                         
                                     TO  ANAVCMDC                               
                   ELSE                                                         
                       MOVE DP015-REVERSE                                       
                                     TO  ANAVCMDH                               
                       MOVE DP015-RED                                           
                                     TO  ANAVCMDC                               
                   END-IF                                                       
                                                                                
                   MOVE PV-MESSAGE   TO  DP020-MSG-MESSAGE                      
                                                                                
                   SET DP020-MSG-FATAL                                          
                                     TO  TRUE                                   
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                     TO  TRUE                                   
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE 'B100-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE PM-PROGRAM-MESSAGE-12                                   
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *        
      *    ENTRY.                                                      *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2000-BUILD-INITIAL-PANEL.                                                
                                                                                
           IF  DP020-INT-APPL-FORMAT-IND EQUAL '1'                              
               MOVE DP055-MNEMONIC   TO  DP023-MNEMONIC                         
           ELSE                                                                 
               IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                          
                   MOVE 1            TO  DP023-MN-ITEM                          
                                         DP023-MN-SUB                           
                   PERFORM 2150-READ-LIST-QUEUE                                 
                   MOVE DP002-MNEMONIC (1)                                      
                                     TO  DP023-MNEMONIC                         
                   SET DP002-INQ (1) TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP023-SAVE-LIST-RECORD                 
               ELSE                                                             
                   MOVE DP020-NAV-KEY-DATA                                      
                                     TO  DP023-MNEMONIC                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2100-RETRIEVE-DATA-FOR-PANEL.                                
                                                                                
           IF  NOT DP020-NEXT-ACT-APPL-ERROR                                    
               PERFORM 2200-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE WHAT LEVEL OF DATA WAS PASSED AND PERFORM THE     *        
      *    READ FOR EACH LEVEL.                                        *        
      *                                                                *        
      *    PERFORMED BY 2000, 2160, 3000                               *        
      *----------------------------------------------------------------*        
                                                                                
       2100-RETRIEVE-DATA-FOR-PANEL.                                            
                                                                                
           PERFORM 2110-READ-TMNEMONIC.                                         
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               PERFORM 2101-MOVE-DATA                                           
               PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                             
           ELSE                                                                 
               MOVE PC-TSYMSG-00148  TO  DP020-MSG-NUMBER                       
               SET DP020-MSG-FATAL   TO  TRUE                                   
               SET DP020-NEXT-ACT-APPL-ERROR                                    
                                     TO  TRUE                                   
               PERFORM 4000-NAVIGATE                                            
           END-IF.                                                              
                                                                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MOVE THE DATA FROM THE TABLE TO THE COMMAREA.               *        
      *                                                                *        
      *    PERFORMED BY 2100.                                          *        
      *----------------------------------------------------------------*        
                                                                                
       2101-MOVE-DATA.                                                          
                                                                                
           MOVE TMNEMONIC-MNEMONIC-CODE                                         
                                     TO  DP023-MNEMONIC.                        
           MOVE TMNEMONIC-PGM-NAME   TO  DP023-PROGRAM.                         
           MOVE TMNEMONIC-TRANS-ID-CODE                                         
                                     TO  DP023-TRAN.                            
           MOVE TMNEMONIC-PANEL-TITLE-NAME                                      
                                     TO  DP023-TITLE.                           
           MOVE TMNEMONIC-AVAIL-INDCT                                           
                                     TO  DP023-TRAN-AVAIL.                      
                                                                                
           IF  TMNEMONIC-CMAREA-FRMT-CODE EQUAL 'K'                             
               MOVE 'Y'              TO  DP023-STANDARD-COMMAREA                
           ELSE                                                                 
               MOVE TMNEMONIC-CMAREA-FRMT-CODE                                  
                                     TO  DP023-STANDARD-COMMAREA                
           END-IF.                                                              
                                                                                
           MOVE TMNEMONIC-SAVE-CMAREA-INDCT                                     
                                     TO  DP023-SAVE-COMMAREA.                   
           MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                                    
                                     TO  DP023-COMMAREA-LENGTH.                 
           MOVE TMNEMONIC-GLBL-ACCS-IND                                         
                                     TO  DP023-GLOBAL-ACCESS.                   
           MOVE TMNEMONIC-SCRN-TYP-CDE                                          
                                     TO  DP023-SCREEN-TYPE-CODE.                
           MOVE TMNEMONIC-UPD-VSAM-IND                                          
                                     TO  DP023-UPD-VSAM-IND.                    
           MOVE TMNEMONIC-DB2-PKG-CDE                                           
                                     TO  DP023-DB2-PKG-CDE.                     
           MOVE TMNEMONIC-CMNT-TXT   TO  DP023-CMNT-TXT.                        
           MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                     
                                     TO  DP023-ACCURATE-LOCATION-REQ.           
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TMNEMONIC TABLE HERE.                              *        
      *                                                                *        
      *    PERFORMED BY 2100.                                          *        
      *----------------------------------------------------------------*        
                                                                                
       2110-READ-TMNEMONIC.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                              MNEMONIC_CODE                                     
                           ,  TRANS_ID_CODE                                     
                           ,  AVAIL_INDCT                                       
                           ,  CMAREA_FRMT_CODE                                  
                           ,  SAVE_CMAREA_INDCT                                 
                           ,  CMAREA_LNGTH_MSRMT                                
                           ,  PANEL_TITLE_NAME                                  
                           ,  LAST_UPDATE_TMESTP                                
                           ,  GLBL_ACCS_IND                                     
                           ,  PGM_NAME                                          
                           ,  SCRN_TYP_CDE                                      
                           ,  UPD_VSAM_IND                                      
                           ,  DB2_PKG_CDE                                       
                           ,  CMNT_TXT                                          
                           ,  ACRT_LOC_REQR_IND                                 
                       INTO                                                     
                              :TMNEMONIC-MNEMONIC-CODE                          
                           ,  :TMNEMONIC-TRANS-ID-CODE                          
                           ,  :TMNEMONIC-AVAIL-INDCT                            
                           ,  :TMNEMONIC-CMAREA-FRMT-CODE                       
                           ,  :TMNEMONIC-SAVE-CMAREA-INDCT                      
                           ,  :TMNEMONIC-CMAREA-LNGTH-MSRMT                     
                           ,  :TMNEMONIC-PANEL-TITLE-NAME                       
                           ,  :TMNEMONIC-LAST-UPDATE-TMESTP                     
                           ,  :TMNEMONIC-GLBL-ACCS-IND                          
                           ,  :TMNEMONIC-PGM-NAME                               
                           ,  :TMNEMONIC-SCRN-TYP-CDE                           
                           ,  :TMNEMONIC-UPD-VSAM-IND                           
                           ,  :TMNEMONIC-DB2-PKG-CDE                            
                           ,  :TMNEMONIC-CMNT-TXT                               
                           ,  :TMNEMONIC-ACRT-LOC-REQR-IND                      
                       FROM                                                     
                              TMNEMONIC                                         
                       WHERE  MNEMONIC_CODE  =  :DP023-MNEMONIC                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '2110-READ-TMNEMONIC'                              
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'READ TMNEMONIC'                                   
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '2110-READ-TMNEMONIC'                                       
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR READ TMNEMONIC'                   
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE THAT CONTAINS THE SELECTED *        
      *    ITEMS FROM THE LIST PROGRAM.                                *        
      *                                                                *        
      *    PERFORMED BY 2000, 2160                                     *        
      *----------------------------------------------------------------*        
                                                                                
       2150-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE(DP055-TSQB-QUEUE-NAME)                                     
               INTO(DP002-ARCH-MAINT-TSQB-REC)                                  
               LENGTH(DP055-TSQB-QUEUE-LENGTH)                                  
               ITEM(DP023-MN-ITEM)                                              
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '2150-READ-LIST-QUEUE'                                      
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMP STORAGE QUEUE, GET THE NEXT MNEMONIC.         *        
      *    IF AT END, ISSUE MESSAGE, OTHERWISE CONTINUE PROCESSING.    *        
      *                                                                *        
      *    PERFORMED BY 3000                                           *        
      *----------------------------------------------------------------*        
                                                                                
       2160-GET-NEXT-MNEMONIC.                                                  
                                                                                
           ADD 1                     TO  DP023-MN-SUB.                          
           IF  DP023-MN-SUB GREATER THAN DP002-MAX-ITEMS                        
               PERFORM 2170-REWRITE-LIST-QUEUE                                  
               ADD 1                 TO  DP023-MN-ITEM                          
               MOVE 1                TO  DP023-MN-SUB                           
               PERFORM 2150-READ-LIST-QUEUE                                     
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP023-SAVE-LIST-RECORD                 
           ELSE                                                                 
               MOVE DP023-SAVE-LIST-RECORD                                      
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
           END-IF.                                                              
                                                                                
           IF  DP002-MNEMONIC (DP023-MN-SUB) GREATER THAN SPACES                
               MOVE DP002-MNEMONIC (DP023-MN-SUB)                               
                                     TO  DP023-MNEMONIC                         
               SET DP002-INQ (DP023-MN-SUB)                                     
                                     TO  TRUE                                   
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP023-SAVE-LIST-RECORD                 
               PERFORM 2100-RETRIEVE-DATA-FOR-PANEL                             
           ELSE                                                                 
               SUBTRACT 1          FROM  DP023-MN-SUB                           
               MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                       
               SET DP020-MSG-INFORMATIONAL                                      
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE THE LIST QUEUE TO REFLECT INQUIRE STATUS.            *        
      *                                                                *        
      *    PERFORMED BY 2150, 3100.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       2170-REWRITE-LIST-QUEUE.                                                 
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                QUEUE(DP055-TSQB-QUEUE-NAME)                                    
                FROM(DP023-SAVE-LIST-RECORD)                                    
                LENGTH(DP055-TSQB-QUEUE-LENGTH)                                 
                ITEM(DP023-MN-ITEM)                                             
                RESP(PV-RESP)                                                   
                REWRITE                                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                                
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '2170-REWRITE-LIST-QUEUE'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.      *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY 3000-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       2200-DISPLAY-PANEL.                                                      
                                                                                
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
           MOVE DP017-EDITED-TIME-HMS                                           
                                     TO  ATIMEO.                                
           MOVE -1                   TO  ANAVCMDL.                              
                                                                                
           IF  PS-SEND-MAP-N-DATA-FRSET                                         
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
               PERFORM DP009-0000-LOAD-FK-LINES                                 
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHEN WE RETURN FROM ANOTHER PROGRAM, CHECK IF ANY UPDATES   *        
      *    WERE PERFORMED.  IF SO, UPDATE THE TSQB ENTRY ACCORDINGLY.  *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2300-UPDATE-TSQB.                                                        
                                                                                
           IF DP023-MN-SUB = ZERO                                               
              MOVE 1 TO DP023-MN-SUB.                                           
                                                                                
           IF  DP055-UPDATED                                                    
               MOVE DP023-SAVE-LIST-RECORD                                      
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
               SET DP002-UPD (DP023-MN-SUB)                                     
                                     TO  TRUE                                   
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP023-SAVE-LIST-RECORD                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       3000-PROCESS-PANEL.                                                      
                                                                                
           PERFORM DP007-0000-GET-PANEL.                                        
           SET PS-SEND-DATA-ONLY-FRSET                                          
                                     TO TRUE.                                   
           PERFORM 3100-CHECK-LOCAL-FKEYS.                                      
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *   DETERMINE THE PROCESSING PATH HERE.                          *        
      *                                                                *        
      *   PERFORMED BY 3000.                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-CHECK-LOCAL-FKEYS.                                                  
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   MOVE SPACES       TO  ANAVCMDO                               
                                         ANAVKEYO                               
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                     TO  TRUE                                   
                   PERFORM 2100-RETRIEVE-DATA-FOR-PANEL                         
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
      *    "NEXT" PROCESSING - POSITION TO NEXT MNEMONIC                        
                                                                                
               WHEN DP020-FK-NEXT-SELECTION(DP020-SRC-AID)                      
                   IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                      
                       PERFORM 2160-GET-NEXT-MNEMONIC                           
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                     TO  DP020-MSG-NUMBER                       
                       SET DP020-MSG-INFORMATIONAL                              
                                     TO  TRUE                                   
                   END-IF                                                       
                   IF  NOT DP020-NEXT-ACT-APPL-ERROR                            
                       PERFORM 2200-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN DP020-SRC-AID = DP016-PA2                                   
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                        
                   SET PS-SEND-DATA-ONLY                                        
                                     TO  TRUE                                   
                   MOVE -1           TO  ANAVCMDL                               
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN DP020-FK-RETURN(DP020-SRC-AID)                              
                    PERFORM 4000-NAVIGATE                                       
                                                                                
               WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID)                          
                    MOVE DP023-MNEMONIC                                         
                                     TO  DP055-MNEMONIC                         
                    PERFORM 4000-NAVIGATE                                       
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   IF  DP020-NAV-MNEMONIC GREATER THAN SPACES                   
                       PERFORM 4000-NAVIGATE                                    
                   ELSE                                                         
                       PERFORM 2200-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00045                                         
                                     TO  DP020-MSG-NUMBER                       
                   SET DP020-MSG-FATAL                                          
                                     TO  TRUE                                   
                   MOVE -1           TO  ANAVCMDL                               
                   PERFORM 2200-DISPLAY-PANEL                                   
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    HERE WE SIMPLY MOVE DATA ENTERED ON THE SCREEN TO THE COMM  *        
      *    AREA.                                                       *        
      *                                                                *        
      *    PERFORMED BY 3000                                           *        
      *----------------------------------------------------------------*        
                                                                                
      *3200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
      *EJECT                                                                    
      *----------------------------------------------------------------*        
      *    HERE WE SIMPLY ENSURE THE DATA IS CORRECT.  IF NOT, ISSUE   *        
      *    APPROPRIATE ERRORS.                                         *        
      *                                                                *        
      *    PERFORMED BY 3000                                           *        
      *----------------------------------------------------------------*        
                                                                                
      *3300-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *                                                                *        
      *    PERFORMED BY 2000, 2160.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       3400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE DP023-MNEMONIC       TO  AMNEMO.                                
           MOVE DP023-PROGRAM        TO  APROGO.                                
           MOVE DP023-TRAN           TO  ATRANIDO.                              
                                                                                
           MOVE DP023-TITLE          TO  ATRNTLEO.                              
           MOVE DP023-TRAN-AVAIL     TO  AAVAILO.                               
           MOVE DP023-STANDARD-COMMAREA                                         
                                     TO  AFORMATO.                              
           MOVE DP023-SAVE-COMMAREA  TO  ASAVCOMO.                              
                                                                                
           MOVE DP023-COMMAREA-LENGTH                                           
                                     TO  ACOMLENO.                              
           MOVE DP023-GLOBAL-ACCESS  TO  AGLBACCO.                              
           MOVE DP023-SCREEN-TYPE-CODE                                          
                                     TO  ASCRTYPO.                              
           MOVE DP023-UPD-VSAM-IND   TO  AUVINDO.                               
           MOVE DP023-DB2-PKG-CDE    TO  APKGO.                                 
           MOVE DP023-CMNT-TXT       TO  ACMNTO.                                
           MOVE DP023-ACCURATE-LOCATION-REQ                                     
                                     TO  ALOCO.                                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS A KEY THAT REQUIRES US TO GO TO ANOTHER    *        
      *    TRANSACTION, SAVE THE INTER APPLICATION COMMUNICATIONS AREA *        
      *    AND START THE NAVIGATOR.                                    *        
      *                                                                *        
      *    PERFORMED BY 3000                                           *        
      *----------------------------------------------------------------*        
                                                                                
       4000-NAVIGATE.                                                           
                                                                                
           IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                              
                                                                                
               IF  DP020-NEXT-ACT-APPL-ERROR                                    
                   MOVE DP023-SAVE-LIST-RECORD                                  
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
                   SET DP002-ERR (DP023-MN-SUB)                                 
                                     TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP023-SAVE-LIST-RECORD                 
               END-IF                                                           
                                                                                
               PERFORM 2170-REWRITE-LIST-QUEUE                                  
               COMPUTE DP055-NUMBER-PROCESSED-ITEMS =                           
                   ((DP023-MN-ITEM - 1) * DP002-MAX-ITEMS) +                    
                     DP023-MN-SUB                                               
           END-IF.                                                              
                                                                                
           MOVE DP055-INTER-APPL-COMMAREA                                       
                                     TO  DP023-SAVE-INTER-APPL-COMMAREA.        
           PERFORM DP015-1000-START-NAVIGATOR.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ENSURE PROPER ENTRY INTO THIS PROGRAM.                      *        
      *                                                                *        
      *    PERFORMED BY 1000                                           *        
      *----------------------------------------------------------------*        
                                                                                
       4100-VALIDATE-COMMAREA.                                                  
                                                                                
           SET PS-COMMAREA-VALID     TO  TRUE.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FILL IN SCREEN HEADER MODULE                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD022.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RETURN TO CICS MODULE                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD003.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT MESSAGE LINES MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD005.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    GET PANEL MODULE                                            *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD007.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE PANEL MODULE                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD008.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    TOGGLE THE FUNCTION KEY LINE MODULE                         *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD009.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CHECK COMMUNICATION AREA MODULE                             *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD015.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE TIME AND DATE MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
