      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT092.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           MARCH 2003.                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT092 -                                                  *        
      *                                                                *        
      *    LOG ONTO ORACLE.  IF THIS FAILS ABEND OUT OF THE PROGRAM    *        
      *    OTHERWISE, OPEN THE COSA INPUT FILE AND THE SALES DATE      *        
      *    OUTPUT FILE.  NEXT READ THE COSA FILE LINE BY LINE.  IF     *        
      *    THERE IS NOTHING IN THE FILE DISPLAY A MESSAGE IN THE       *        
      *    OUTPUT AND ABEND THE PROGRAM.  IF THERE IS DATA AND THIS    *        
      *    IS THE FIRST RECORD READ THEN WRITE OUT THE DATE FROM THE   *        
      *    FILE TO THE SALES DATE OUTPUT.  IF THIS IS NOT THE FIRST    *        
      *    RECORD READ THEN CHECK TO MAKE SURE THE CURRENT SALES DATE  *        
      *    READ IS EQUAL TO THE PREVIOUS SALES DATE READ.  IF THEY ARE *        
      *    DIFFERENT THEN ABEND THE PROGRAM.  NEXT CHECK THE SALE      *        
      *    RETURN CODE AND IF IT IS NOT AN A, B, OR S THEN DISPLAY A   *        
      *    MESSAGE IN THE OUTPUT SAYING THE RECORD WAS IGNORED.  IF    *        
      *    THE SALE RETURN CODE IS AN A OR S CHECK TO                  *        
      *    SEE IF THE PROMO-INTFC-ID IS GREATER THEN ZERO.  IF IT IS   *        
      *    THEN CHECK THE SCHM-CDE AND IF IT IS A 100, 101, 200, OR    *        
      *    500 THEN CHECK THE ITM-STAT-CDE.  IF THAT IS EQUAL TO 30    *        
      *    THEN GET THE PROMO ID AND MDSE SELN LINE ID FROM THE        *        
      *    CLEARANCE TABLES.  IF NOTHING IS FOUND HERE THEN SKIP THE   *        
      *    RECORD.  IF THE ITM-STAT-CDE IS NOT EQUAL TO 30 THEN CHECK  *        
      *    THE SIMPLE TABLES FOR THE PROMO ID, PROMO ITM ID, AND MDSE  *        
      *    SELN LINE ID.  IF NOT FOUND THEN CHECK THE CMPTV TABLES     *        
      *    FOR THE PROMO ID AND MDSE SELN LINE ID.  IF NOT FOUND ON    *        
      *    EITHER OF THESE TABLES THEN SKIP THE RECORD.  IF THE        *        
      *    SCHM-CDE IS NOT 100, 101, 200, OR 500 THEN CHECK TO SEE IF  *        
      *    IT IS 300, 301, 800, 801, 802.     IF SO - THEN CHECK THE   *        
      *    GROUP TABLES FOR PROMO-ID, PROMO-ITM-ID & MDSE-SELN-ID.     *        
      *    IF NOT FOUND THEN SKIP THE RECORD.  IF THE SCHME CDE IS NOT *        
      *    EQUAL TO ANY OF THE PREVIOUS OPTION THEN ABEND THE PROGRAM. *        
      *    ONCE THE SCHM CDE CHECKS ARE DONE AND THE PROMO ID, PROMO   *        
      *    ITM ID AND MDSE SELN ID ARE FOUND THEN DO THE CALCULATIONS. *        
      *    THEN IF THE PROMO ITM ID IS GREATER THEN ZERO INSERT A      *        
      *    RECORD TO THE PMTT_PROMO_ITM_SLS_RSLT_LOD BUT IF A RECORD   *        
      *    ALREADY EXISTS THEN YOU WILL HAVE TO DO AN UPDATE ON THE    *        
      *    TABLE INSTEAD.  IF THE PROMO ITM ID IS LESS THEN OR EQUAL   *        
      *    TO ZERO THEN INSERT A RECORD INTO THE PMTT_SLS_RSLT_LOD     *        
      *    TABLE INSTEAD BUT, IF A RECORD ALREADY EXISTS THEN UPDATE   *        
      *    THE TABLE INSTEAD.  NEXT CHECK THE TIERED-INTFC-SUB-ID TO   *        
      *    SEE IF IT IS GREATER THEN ZERO.  IF SO MOVE THE T TO THE    *        
      *    SALES TYPE CODE AND CHECK ON THE PROMO-INTFC-ID.  IF IT IS  *        
      *    EQUAL TO ZERO THEN SET THE TIERED NOT ON SALE SWITCH TO     *        
      *    TRUE AND LOOK IN THE ADMIN TIERED TABLES FOR THE PROMO ID   *        
      *    AND MDSE SELN ID.  IF NOT FOUND THEN SKIP THE RECORD.  IF   *        
      *    THE PROMO INTFC ID IS NOT EQUAL TO ZERO THEN SET THE TIERED *        
      *    ON SALE TO TRUE.  NEXT DO THE CALCULATIONS AND CHECK THE    *        
      *    PROMO ITM ID FOR A VALUE, IN THIS CASE THERE WILL NOT BE    *        
      *    ONE.   SINCE THE PROMO ITM ID IS EQUAL TO ZERO INSERT A     *        
      *    RECORD INTO THE PMTT_SLS_RSLT_LOD TABLE BUT, IF A RECORD    *        
      *    ALREADY EXISTS THEN UPDATE THE TABLE INSTEAD.  IF THE PROMO *        
      *    INTFC ID AND THE TIERED PROMO INTFC ID ARE ZERO THEN SKIP   *        
      *    THE RECORD.  WHEN THIS CHECKING IS DONE READ THE NEXT       *        
      *    RECORD ON THE COSA FILE AND REPEAT ALL OF THIS CHECKING.    *        
      *    WHEN DONE DO A COMMIT TO BOTH TABLES AND CLOSE THE COSA     *        
      *    INPUT FILE AND THE SALES DATE OUTPUT FILE.  THEN DISPLAY    *        
      *    THE TOTALS ACCUMLATED THROUGH THIS PROGRAM INTO THE OUTPUT. *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    07/17/2003 - AMANDA NEWHART - WR21770                       *        
      *     - IF COSA FILE IS EMPTY CREATE AN EMPTY SALES DATE FILE.   *        
      *                                                                *        
      *    07/23/2003 - COLLEEN CUMMINGS - PM00568778                  *        
      *     - ADDED DISPLAYS ON UPDATE ABEND.                          *        
      *     - ADDED 'MOVE 'S' TO PV-SAL-RET-CDE' AND CHANGED           *        
      *       INSERT/UPDATE STMTS TO USE PV-SAL-RET-CDE INSTEAD OF     *        
      *       SAWS046-SAL-RET-CDE.  OTHERWISE ABEND ON UPDATE BECAUSE  *        
      *       INDEX IS ON PROMO-SLS-TYP-CDE (WHICH IS AN 'S' IN THESE  *        
      *       CASES).                                                  *        
      *                                                                *        
      *    08/14/2003 - AMANDA NEWHART - PM00575567                    *        
      *     - ADDED DISPLAYS IN THE CASE WHERE IT IS NOT AN S, A, OR   *        
      *       A B WITHOUT AN A.                                        *        
      *                                                                *        
      *    08/14/2003 - AMANDA NEWHART - PM00575567                    *        
      *     - ADDED CODE IN THE CALCULATE PARAGRAPH SO THAT IF THE     *        
      *       RECORD IS A TIERED-NOT-ON-SALE AND AN A THEN IT CAN      *        
      *       HANDLE THE B AS WELL.                                    *        
      *                                                                *        
      *    08/18/2003 - AMANDA NEWHART - WR21770                       *        
      *     - DO NOT WANT THE PROGRAM TO ABEND IF THERE IS NO B FOR AN *        
      *       A RECORD.  JUST SKIP OVER THE A RECORD.                  *        
      *                                                                *        
      *    08/20/2003 - AMANDA NEWHART - WR21770                       *        
      *     - IF THE PROMO INTFC ID IS NOT FOUND IN GROUP GET THE VS ID*        
      *       AND CHECK AGAIN.  CREATED PMKUT099, PMRD802, AND PMDB090 *        
      *       FOR THIS CHANGE.                                         *        
      *                                                                *        
      *    08/21/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *     - ADDED 'MOVE TO PREV' AFTER UPDATE OF ITM TABLE.          *        
      *       OTHERWISE TIERED PRICING WAS GETTING ALLOCATED TO WRONG  *        
      *       PROMO ITEM, ETC.                                         *        
      *                                                                *        
      *    09/11/2003 - COLLEEN CUMMINGS - P000585096                  *        
      *     - ADDED LOTS OF 'ON SIZE ERROR MOVE ZERO' STATEMENTS       *        
      *       WHEN DIVIDING AND IF STATEMENTS BEFORE DIVISION COMPUTES *        
      *       TO MAKE SURE DIVISOR IS NOT ZERO.                        *        
      *       HAD AN DECIMAL-DIVIDE EXCEPTION.                         *        
      *                                                                *        
      *    09/29/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *     - CHANGES FOR BOGO - FILE CHANGED, ETC.                    *        
      *                                                                *        
      *    10/12/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *     - CHECKING FOR BOGO IN NORMAL INTFC-ID FIELD - NOT         *        
      *       SUPPOSED TO BE IN THIS FIELD - BUT NEEDED TO GET THE     *        
      *       FILE PROCESSED.                                          *        
      *                                                                *        
      *    10/13/2003 - COLLEEN CUMMINGS - P000597119                  *        
      *     - ADDED COMMIT EVERY 100,000 RECORDS - TAKING TOO LONG     *        
      *       TO ROLLBACK IN THE EVENT OF AN ABEND.                    *        
      *       SUGGESTION IS TO COMMIT EVERY 10 MIN.                    *        
      *                                                                *        
      *    11/05/2003 - COLLEEN CUMMINGS - P000597119                  *        
      *     - CHANGED WHERE READS ARE HAPPENING TO BE IN ONE CENTRAL   *        
      *       PARAGRAPH TO CONTROL COMMITS.  WAS NOT INCREMENTING      *        
      *       COUNTERS CORRECTLY - SO COMMIT WAS NOT HAPPENING.        *        
      *       ALSO CHANGED COMMIT TO BE EVERY 50,000.                  *        
      *       ALSO ADDED 'FIRST_ROWS' HINT TO GROUP SQL (LONG-RUNNING  *        
      *       BOGO DATA).                                              *        
      *                                                                *        
      *    11/05/2003 - COLLEEN CUMMINGS - P000597119                  *        
      *     - REMOVED 'FIRST_ROWS' HINT BECAUSE STILL RUNNING LONG -   *        
      *       NEED TO SEE IF THIS IS MAKING IT RUN LONGER.             *        
      *                                                                *        
      *    11/05/2003 - COLLEEN CUMMINGS - P000597119                  *        
      *     - ADDED 'FIRST_ROWS' HINT BACK IN - MAINFRAME PEGGED.      *        
      *                                                                *        
      *    11/24/2003 - COLLEEN CUMMINGS - P000611310                  *        
      *     - LOOPING WHEN THERE WAS AN 'A' RECORD - NO 'B' RECORD,    *        
      *       AND THE NEXT RECORD WAS A BOGO BY ITSELF THAT WAS A      *        
      *       NORMAL 'S' RECORD.  REMOVED '(PS-B-FOR-A)' AFTER         *        
      *       'IF (SA046-BOGO-INTFC-ID > 0) AND' IN PARA B300-.        *        
      *       PS-B-FOR-A SWITCH WAS NEVER GETTING RESET AND WAS NEVER  *        
      *       GETTING INTO THIS LOGIC - SO KEPT LOOPING THROUGH.       *        
      *                                                                *        
      *    12/02/2003 - COLLEEN CUMMINGS - P000613254                  *        
      *     - LOOPING WHEN THERE WAS AN 'A' RECORD - NO 'B' RECORD,    *        
      *       AND THE NEXT RECORD WAS A TIER RECORD BY ITSELF.         *        
      *       ADDED LOGIC IN B300-PROCESS-FILE:                        *        
      *              "OR                                               *        
      *                (SA046-PM-PROMO-INTFC-ID = 0) AND               *        
      *                (SA046-TIERED-INTFC-SUB-ID > 0))"               *        
      *       THIS ALLOWS IT TO GET THROUGH THAT CODE AND KEEP         *        
      *       PROCESSING IF IT IS TIER WITH NO REGULAR INTFC-ID.       *        
      *                                                                *        
      *    01/15/2004 - COLLEEN CUMMINGS - WR25854                     *        
      *     - ADDED CHECK FOR SCHM-CDE OF 801, 802 FOR NEW BOGO.       *        
      *                                                                *        
      *    02/02/2004 - COLLEEN CUMMINGS - PM00629256                  *        
      *     - REMOVED C180- CALL FROM Z100 AND Z910 ABEND PARAGRAPHS - *        
      *       INSTEAD JUST DISPLAY CURRENT RECORD INFORMATION.         *        
      *       WAS INCORRECTLY DISPLAYING THAT A COMMIT HAPPENED WHEN   *        
      *       IT HADN'T.                                               *        
      *                                                                *        
      *    02/22/2004 - COLLEEN CUMMINGS - PM00629256                  *        
      *     - RECOMPILE ONLY DUE TO INVALID NUMBER ORACLE ERROR.       *        
      *       (DIDN'T FIX IT - WAITED A FEW HOURS - RESUBMITTED - FINE)*        
      *                                                                *        
      *    02/23/2004 - COLLEEN CUMMINGS - PM00635067                  *        
      *     - ADDED ALTER SESSION STATEMENT DUE TO WEIRD INVALID NUMBER*        
      *       ORACLE ERROR.                                            *        
      *                                                                *        
      *    08/03/2004 - COLLEEN CUMMINGS - PM00672718                  *        
      *     - ADDED C110-, C115- COUNTER DISPLAYS (F 'E200-CPU-TIME'). *        
      *                                                                *        
      *    08/09/2004 - COLLEEN CUMMINGS - P000672718                  *        
      *     - BOGO PERFORMANCE ENHANCEMENT.  CHANGED TO ONLY LOOK UP   *        
      *       PROMO-ITM-ID, MDSE-SELN-ID, ETC. WHEN SKU OR INTFC-ID    *        
      *       CHANGES FOR GROUP/BOGO SCHEME CODES ONLY.                *        
      *                                                                *        
      *    08/17/2004 - COLLEEN CUMMINGS - P000682994                  *        
      *     - FIX TO NOT LOOK-UP DELETED GROUP/BOGO PRICING.           *        
      *       (C110-, C115-)                                           *        
      *       INCORRECTLY RECORDING SALES TO WRONG PROMO ITEM IF       *        
      *       SKU IN A PROMO ITEM THAT IS BOTH DELETE AND SCHEDULED.   *        
      *       (EXAMPLE 8/15/04, PROMO ITM IDS = 80062, 70028).         *        
      *                                                                *        
      *    10/18/2004 - COLLEEN CUMMINGS - P000702913                  *        
      *     - CHANGE "C115-FIND-GROUP-VS" TO REMOVE THE "FIRST_ROWS"   *        
      *       HINT AND REPLACE THAT WITH "ORDERED(A,D,E,F,C,B)".       *        
      *       PLAN IS EXECUTING DIFFERENTLY WITH ORACLE 9.             *        
      *       (SLOW FOR A BOGO AD AGAIN).                              *        
      *                                                                *        
      *    07/17/2008 - DARYL STEWART - INC000000445186                *        
      *     - CHANGE "C115-FIND-GROUP-VS" TO REMOVE THE "(A,D,E,F,C,B)"*        
      *       HINT AND REPLACE THAT WITH "ORDERED. CHANGED ALTER STMT  *        
      *       FROM "EXACT" TO "SIMILAR"                                *        
      *       PLAN IS EXECUTING DIFFERENTLY WITH ORACLE 10.            *        
      *                                                                *        
      *    04/15/2010 - PAUL KEBER    - PKE000000001894                *        
      *     - REMOVED CALL TO PROGRAM PMKUT099 WHICH RETRIEVES DB2 DATA*        
      *       AND REPLACED WITH XKT_VND_STYL AND XKT_SKU READS IN THE  *        
      *       C115-FIND-GROUP-VS ROUTINE.                              *        
      *                                                                *        
      *    06/09/2010 - PAUL KEBER    - INC000001279324                *        
      *     - BACKED OUT 04/15/2010 PROGRAM CHANGES.                            
      *                                                                *        
      *    06/10/2010 - PAUL KEBER    - PKE000000001894                *        
      *     - READDED 04/15/2010 PROGRAM CHANGES.                      *        
      *                                                                *        
      *    10/20/2010 - PAUL KEBER    - INC000001443940                *        
      *     - ADDED ALTER SESSION SET "_OPTIM_PEEK_USER_BINDS" = FALSE *        
      *       STATEMENT PER DBA.  CHANGED "C115-FIND-GROUP-VS" CALL TO *        
      *       USE /*+ FIRST_ROWS (10) */ HINT.                         *        
      *                                                                *        
CPR   *    07/22/2011 - COGNIZANT - PKE000000008409                             
|     *     - ADDED CHECKPOINT RESTART LOGIC AND CHECKPOINT INFORMATION*        
|     *       CONTAINING NUMBER OF RECORDS PROCESSED PER CHECKPOINT SO *        
|     *       THAT JOBS CAN BE EASILY RESTARTED WHEN IT ABENDS.        *        
CPR   *                                                                *        
      *    01/16/2015 - COGNIZANT     - PRB51356                       *        
      *     - MODIFIED THE PROGRAM TO HAVE TWO COMMITS ONE FOR ORACLE  *        
      *       AND ONE FOR DB2. ALSO INCLUDED DCLGEN FOR CHECKPOINT     *        
      *       RESTART TABLE                                            *        
      *                                                                *        
      ******************************************************************        
SCHCDE*    08/21/2013 - COGNIZANT - CHG0060933                                  
SCHCDE*    ADDED A LOGIC TO INCLUDE THE GROUP PRICING PROMO SCHEMA              
SCHCDE*    CODES '400' '401' '402' & '403' FOR PROCESSING.                      
      ******************************************************************        
      *    07/30/2018 - COGNIZANT - CHG0204126                                  
      *    ORACLE LOGON STANDARDS PROGRAM CHANGE                                
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-RESULT-FILE                                             
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT SALES-DATE-FILE                                               
               ASSIGN                       TO OUT01.                           
CPR                                                                             
CPR        SELECT CONTROL-FILE-1                                                
CPR            ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-RESULT-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-RESULT-LINE                PIC  X(250).                        
                                                                                
       FD  SALES-DATE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-DATE-LINE                  PIC X(80).                          
CPR                                                                             
|      FD  CONTROL-FILE-1                                                       
|          LABEL RECORDS ARE STANDARD                                           
|          RECORDING MODE IS F                                                  
|          BLOCK CONTAINS 0 RECORDS                                             
|          DATA RECORD IS CONTROL-FILE-1-REC.                                   
|      01  CONTROL-FILE-1-REC.                                                  
|          05  CTL1-JOB-NAME                PIC X(8).                           
CPR        05  FILLER                       PIC X(72).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
070941                                                                          
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-SALES-RESULT-FILE-SW      PIC  X(01)  VALUE 'N'.              
               88  PS-SALES-RESULT-FILE-END             VALUE 'Y'.              
               88  PS-SALES-RESULT-FILE-START           VALUE 'N'.              
           05  PS-SALES-READ-SW             PIC X(01)   VALUE 'N'.              
               88  PS-SALES-READ                        VALUE 'Y'.              
               88  PS-NO-SALES-READ                     VALUE 'N'.              
           05  PS-INSERT-RECORD-SW          PIC X(01)   VALUE 'Y'.              
               88  PS-INSERT-RECORD                     VALUE 'Y'.              
               88  PS-NO-INSERT-RECORD                  VALUE 'N'.              
           05  PS-INSERT-RECORD-PREV-SW     PIC X(01)   VALUE 'Y'.              
               88  PS-INSERT-RECORD-PREV                VALUE 'Y'.              
               88  PS-NO-INSERT-RECORD-PREV             VALUE 'N'.              
           05  PS-SAME-SKU-INTFC-ID-SW      PIC X(01)   VALUE 'Y'.              
               88  PS-SAME-SKU-INTFC-ID                 VALUE 'Y'.              
               88  PS-NOT-SAME-SKU-INTFC-ID             VALUE 'N'.              
           05  PS-B-FOR-A-SW                PIC X(01)   VALUE 'Y'.              
               88  PS-B-FOR-A                           VALUE 'Y'.              
               88  PS-NO-B-FOR-A                        VALUE 'N'.              
           05  PS-TIERED                    PIC X(01).                          
               88  PS-TIERED-ON-SALE                    VALUE 'S'.              
               88  PS-TIERED-NOT-ON-SALE                VALUE 'T'.              
               88  PS-NOT-TIERED                        VALUE 'N'.              
           05  PS-TYPE                      PIC X(01).                          
               88  PS-CLRNC-TYP                         VALUE 'C'.              
               88  PS-REG-TYP                           VALUE 'R'.              
CPR        05  PS-FIRST-COMMIT-SW           PIC  X(01)  VALUE 'Y'.              
CPR            88  PS-FIRST-COMMIT                      VALUE 'Y'.              
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)   VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC  X(01)  VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
       01  CC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
                                                           'PMKUT092'.          
CPR        05  PC-JOB-NAME                  PIC  X(08)  VALUE SPACES.           
           05  PC-THREE                     PIC  9(02)  VALUE 03.               
           05  PC-ZERO                      PIC  9(01)  VALUE 0.                
           05  PC-SIMPLE                    PIC  X(01)  VALUE 'S'.              
           05  PC-SALE                      PIC  X(01)  VALUE 'S'.              
           05  PC-CLRNC                     PIC  X(01)  VALUE 'C'.              
           05  PC-TIERED                    PIC  X(01)  VALUE 'T'.              
           05  PC-ADJ-SALE                  PIC  X(01)  VALUE 'A'.              
           05  PC-ADJ-RET                   PIC  X(01)  VALUE 'B'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-PREV-SLS-DTE              PIC  X(10)  VALUE SPACES.           
           05  PV-SAL-RET-CDE               PIC  X(01)  VALUE SPACES.           
           05  PV-GP-PROMO-INTFC-ID         PIC  9(10)  VALUE ZEROS.            
           05  PV-RECS-DIV                  PIC S9(09)  VALUE 0.                
           05  PV-RECS-REM                  PIC S9(09)  VALUE 0.                
CPR        05  PV-COMMIT-INTERVAL           PIC S9(09)  COMP-3                  
|                                                       VALUE +50000.           
|          05  PV-COMMIT-CNTR               PIC S9(09)  VALUE +1                
|                                                       COMP-3.                 
CPR        05  PV-COMMIT-RECORDS            PIC  9(11)  VALUE ZEROES.           
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
                                                                                
       01  PV-INSERT-VARIABLES.                                                 
           05  PV-PROMO-ID                  PIC  9(10)  VALUE ZEROS.            
           05  PV-PROMO-ITM-ID              PIC  9(10)  VALUE ZEROS.            
           05  PV-PROMO-INTFC-SUB-ID        PIC  9(10)  VALUE ZEROS.            
           05  PV-MDSE-SELN-ID              PIC  9(10)  VALUE ZEROS.            
           05  PV-MDSE-SELN-LNE-ID          PIC  9(10)  VALUE ZEROS.            
           05  PV-STYL-ID                   PIC S9(08)V COMP-3.                 
           05  PV-PROMO-SLS-TYP-CDE         PIC  X(01)  VALUE SPACES.           
           05  PV-PROMO-ITM-ID-NULL         PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
                                                                                
       01  PV-CALCULATED-VARIABLES.                                             
           05  PV-SLS-RTL-AMT               PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-TIERED-MKDN-AMT           PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-BOGO-PRORTD-MKDN-AMT      PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-MKDN-RTL-AMT              PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-SLS-COST-AMT              PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-UNT-PROMO-RTL-AMT         PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-UNT-MKDN-RTL-PCT          PIC S9(03)V9(3) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-UNT-MKDN-RTL-AMT          PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-UNT-MKUP-RTL-PCT          PIC S9(03)V9(3) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-UNT-MKDN-RTL-PCT-CALC     PIC S9(03)V9(3) COMP-3              
                                                        VALUE ZEROS.            
                                                                                
009200 01  CT-CPU-TIME.                                                         
009300     05  CT-CPU-HR                        PIC  9(02) VALUE ZERO.          
009400     05  CT-CPU-MIN                       PIC  9(02) VALUE ZERO.          
009500     05  CT-CPU-SEC                       PIC  9(02) VALUE ZERO.          
009600     05  CT-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
009700                                                                          
009800 01  PV-CPU-TIME.                                                         
009900     05  PV-CPU-HR                        PIC  9(02) VALUE ZERO.          
010000     05  FILLER                           PIC  X(01) VALUE ":".           
010100     05  PV-CPU-MIN                       PIC  9(02) VALUE ZERO.          
010200     05  FILLER                           PIC  X(01) VALUE ":".           
010300     05  PV-CPU-SEC                       PIC  9(02) VALUE ZERO.          
010400     05  FILLER                           PIC  X(01) VALUE ":".           
010500     05  PV-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
010600                                                                          
00009  01  PV-HOLD-SALES-RESULTS-REC.                                           
00013      05  PV-HOLD-STR-NBR              PIC  9(04)  VALUE ZEROS.            
00022      05  PV-HOLD-RGST-ID              PIC  9(04)  VALUE ZEROS.            
00023      05  PV-HOLD-TRN-NBR              PIC  9(04)  VALUE ZEROS.            
00014      05  PV-HOLD-SLS-DTE              PIC  X(10)  VALUE SPACES.           
00017      05  PV-HOLD-SKU-NBR              PIC  9(08)  VALUE ZEROS.            
           05  PV-HOLD-DEPT-NBR             PIC  9(04)  VALUE ZEROS.            
00018      05  PV-HOLD-MAJ-CL-NBR           PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-SUB-CL-NBR           PIC  9(04)  VALUE ZEROS.            
00021      05  PV-HOLD-ITM-STAT-CDE         PIC  X(02)  VALUE SPACES.           
00024      05  PV-HOLD-LNE-ITM-SEQ-NBR      PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-LIV-RSLU-CDE         PIC  X(01)  VALUE SPACES.           
           05  PV-HOLD-SAL-RET-CDE          PIC  X(01)  VALUE SPACES.           
00028      05  PV-HOLD-SLD-QTY              PIC S9(07)  VALUE ZEROS.            
00026      05  PV-HOLD-REG-RTL-AMT          PIC S9(05)V99  VALUE ZEROS.         
00027      05  PV-HOLD-OWND-UNT-RTL-AMT     PIC S9(05)V99  VALUE ZEROS.         
00029      05  PV-HOLD-UNT-COST-AMT         PIC S9(04)V99  VALUE ZEROS.         
00030      05  PV-HOLD-CUST-CHRGD-AMT       PIC S9(07)V99  VALUE ZEROS.         
           05  PV-HOLD-NET-CHRGD-AMT        PIC S9(07)V99  VALUE ZEROS.         
           05  PV-HOLD-PM-PROMO-INTFC-ID    PIC  9(10)  VALUE ZEROS.            
           05  PV-HOLD-PM-SCHM-CDE          PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-PM-MKDN-AMT          PIC S9(05)V99  VALUE ZEROS.         
           05  PV-HOLD-PM-DISC-PCT          PIC S9(02)V999 VALUE ZEROS.         
           05  PV-HOLD-TIERED-INTFC-SUB-ID  PIC  9(10)  VALUE ZEROS.            
           05  PV-HOLD-TIERED-SCHM-CDE      PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-TIERED-MKDN-AMT      PIC S9(05)V99  VALUE ZEROS.         
           05  PV-HOLD-TIERED-DISC-PCT      PIC S9(02)V999 VALUE ZEROS.         
           05  PV-HOLD-ADOAPO-PROMO-INTFC-ID PIC 9(10)  VALUE ZEROS.            
           05  PV-HOLD-ADOAPO-SCHM-CDE      PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-ADOAPO-MKDN-AMT      PIC S9(05)V99  VALUE ZEROS.         
           05  PV-HOLD-APO-DISC-PCT         PIC S9(02)V999 VALUE ZEROS.         
           05  PV-HOLD-GWPPWP-INTFC-ID      PIC  9(10)  VALUE ZEROS.            
           05  PV-HOLD-GWPPWP-INTFC-SUB-ID  PIC  9(10)  VALUE ZEROS.            
           05  PV-HOLD-GWPPWP-SCHM-CDE      PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-GWPPWP-GET-MKDN-AMT  PIC S9(05)V99  VALUE ZEROS.         
           05  PV-HOLD-BOGO-INTFC-ID        PIC  9(10)  VALUE ZEROS.            
           05  PV-HOLD-BOGO-SCHM-CDE        PIC  9(04)  VALUE ZEROS.            
           05  PV-HOLD-BOGO-PRORTD-MKDN-AMT PIC S9(05)V99  VALUE ZEROS.         
                                                                                
       01  PV-PREV-VARIABLES.                                                   
           05  PV-PREV-PROMO-ID             PIC  9(10)  VALUE ZEROS.            
           05  PV-PREV-PROMO-ITM-ID         PIC  9(10)  VALUE ZEROS.            
           05  PV-PREV-MDSE-SELN-ID         PIC  9(10)  VALUE ZEROS.            
           05  PV-PREV-PROMO-SLS-TYP-CDE    PIC  X(01)  VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-NBR-OF-COMMITS            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-READ-COUNTER              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-GRP-SKIP-SQL-COUNTER      PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-BOGO-SKIP-SQL-COUNTER     PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-C110-GRP-SQL-COUNTER      PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-C110-BOGO-SQL-COUNTER     PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-C115-SQL-COUNTER          PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-SIMPLE-CNT                PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-GROUP-SKU-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-GROUP-VS-CNT              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-CLRNC-CNT                 PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-CMPTV-CNT                 PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-ADMIN-TIER-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-INSERT-NONITM-CNT         PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-INSERT-ITM-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-UPDATE-NONITM-CNT         PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-UPDATE-ITM-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
CPR    01  CR-CHECKPOINT-RESTART-AREA.                                          
|          05  CR-AREA-LEN                  PIC S9(04)  VALUE +12               
|                                           COMP.                               
|          05  CR-CHECKPOINT-RESTART-VAR.                                       
|              10  CR-ACT-READ-CNT          PIC 9(11)   VALUE ZEROES.           
|              10  CR-ACT-DONE-SW           PIC X       VALUE 'N'.              
|                88 CR-ACT-DONE                         VALUE 'Y'.              
|                88 CR-ACT-NOT-DONE                     VALUE 'N'.              
CPR                                                                             
      ******************************************************************        
      *    COPYBOOK FOR THE SALES RESULTS FILE FROM COSA                        
      ******************************************************************        
           COPY SAWS046.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
CPR                                                                             
|     ******************************************************************        
|     * COBOL DECLARATION FOR TABLE TCKRSTINF                          *        
|     ******************************************************************        
|     *01  DCLTCKRSTINF.                                                        
|     *    10 TCKRSTINF-JOB-NAME          PIC X(8).                             
|     *    10 TCKRSTINF-PLAN-NAME         PIC X(8).                             
|     *    10 TCKRSTINF-RST-TRANS-SEQ-NMBR                                      
|     *                                   PIC S999999999V USAGE COMP-3.         
|     *    10 TCKRSTINF-SEQ-NMBR          PIC S999V USAGE COMP-3.               
|     *    10 TCKRSTINF-WORK-STRG-DESC    PIC X(102).                           
|     *    10 FILLER REDEFINES TCKRSTINF-WORK-STRG-DESC.                        
|     *       15 TCKRSTINF-WORK-STRG-DESC-LEN                                   
|     *                                   PIC S9(4) USAGE COMP.                 
|     *       15 TCKRSTINF-WORK-STRG-DESC-TEXT                                  
|     *                                   PIC X(0100).                          
|                                                                               
|          EXEC SQL                                                             
|              INCLUDE TCKRSTIN                                                 
|          END-EXEC.                                                            
|                                                                               
|     ******************************************************************        
|     * COBOL DECLARATION FOR TABLE TCKRSTCNTL                         *        
|     ******************************************************************        
|     *01  DCLTCKRSTCNTL.                                                       
|     *    10 TCKRSTCNTL-JOB-NAME         PIC X(8).                             
|     *    10 TCKRSTCNTL-PLAN-NAME        PIC X(8).                             
|     *    10 TCKRSTCNTL-CKPT-STAT-CODE   PIC X(1).                             
|     *    10 TCKRSTCNTL-CKPT-CNTL-CODE   PIC X(1).                             
|     *    10 TCKRSTCNTL-CKPT-TYPE-CODE   PIC X(1).                             
|     *    10 TCKRSTCNTL-CKPT-FRQNCY-QTY  PIC S99999V USAGE COMP-3.             
|     *    10 TCKRSTCNTL-TRANS-QTY        PIC S999999999V USAGE COMP-3.         
|     *    10 TCKRSTCNTL-PREP-TMESTP      PIC X(26).                            
|     *    10 TCKRSTCNTL-CKPT-TAKEN-QTY   PIC S999999999V USAGE COMP-3.         
|     *    10 TCKRSTCNTL-TRANS-PRCSD-QTY  PIC S999999999V USAGE COMP-3.         
|     *    10 TCKRSTCNTL-RST-QTY          PIC S99999V USAGE COMP-3.             
|     *    10 TCKRSTCNTL-PRCSD-TMESTP     PIC X(26).                            
|     *    10 TCKRSTCNTL-CKPT-DURATION-TIME                                     
|     *                                        PIC X(8).                        
|          EXEC SQL                                                             
|              INCLUDE TCKRSTCN                                                 
|          END-EXEC.                                                            
|                                                                               
CPR                                                                             
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - OPEN INPUT FILE                                            *         
      *  - READ THE SALES RESULT FILE FROM COSA UNTIL THE END OF      *         
      *     THE FILE                                                  *         
      *  - CLOSE FILES                                                *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-SALES-RESULT-FILE-START TO TRUE.                              
           SET PS-NO-SALES-READ TO TRUE.                                        
           SET PS-NOT-TIERED TO TRUE.                                           
070941     PERFORM B110-ORACLE-SETUP.                                           
           PERFORM B100-OPEN-FILES.                                             
CPR        PERFORM B101-INITIAL-RESTART-CHECK.                                  
           PERFORM B300-PROCESS-FILE                                            
               UNTIL PS-SALES-RESULT-FILE-END.                                  
                                                                                
           PERFORM C170-COMMIT.                                                 
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           IF PS-SALES-READ                                                     
               DISPLAY 'RECORDS READ FROM SALES FILE = ' PA-READ-COUNTER        
               DISPLAY 'RECORDS FROM SIMPLE CSR = '      PA-SIMPLE-CNT          
               DISPLAY 'RECORDS FROM GROUP SKU CSR = '  PA-GROUP-SKU-CNT        
               DISPLAY 'RECORDS FROM GROUP VS CSR = '    PA-GROUP-VS-CNT        
               DISPLAY 'RECORDS FROM CLEARANCE CSR = '   PA-CLRNC-CNT           
               DISPLAY 'RECORDS FROM CMPTV CSR = '       PA-CMPTV-CNT           
               DISPLAY 'RECORDS FROM ADMIN TIER CSR = '                         
                                                   PA-ADMIN-TIER-CNT            
               DISPLAY 'RECORDS INSERTED ON NONITM TBL = '                      
                                                   PA-INSERT-NONITM-CNT         
               DISPLAY 'RECORDS UPDATED ON NONITM TBL = '                       
                                                   PA-UPDATE-NONITM-CNT         
               DISPLAY 'RECORDS INSERTED ON ITM TBL = '                         
                                                   PA-INSERT-ITM-CNT            
               DISPLAY 'RECORDS UPDATED ON ITM TBL = ' PA-UPDATE-ITM-CNT        
               DISPLAY 'NUMBER OF TIMES COMMIT RAN = ' PA-NBR-OF-COMMITS        
           END-IF.                                                              
           DISPLAY ' **** END OF PMKUT092 **** '.                               
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE INPUT FILE THAT WAS RECIEVED FROM COSA            *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  SALES-RESULT-FILE                                        
CPR                    CONTROL-FILE-1                                           
                OUTPUT SALES-DATE-FILE.                                         
070941                                                                          
070941 B110-ORACLE-SETUP.                                                       
070941                                                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     PERFORM B120-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ORACLE LOGON CONTROL CARD '                          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME                                     
070941              ' - LOGON TO ORACLE AS USER '                               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ERROR LOGGING ON TO ORACLE'                          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941******************************************************************        
070941* NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
070941* OTHERWISE GET AN ORACLE ERROR (IE: 3113).                               
070941******************************************************************        
070941     EXEC SQL                                                             
070941      ALTER SESSION                                                       
070941            SET CURSOR_SHARING = 'EXACT'                                  
070941     END-EXEC.                                                            
070941                                                                          
070941                                                                          
070941*****************************************************************         
070941*  B120-READ-ORACLE-LOGON.                                      *         
070941* ==> PROCESSES:                                                *         
070941*   - READ ORCALE CONTROL CARD                                  *         
070941* ==> PERFORMED FROM:  B110-ORACLE-SETUP                        *         
070941*****************************************************************         
070941 B120-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
070941                                                                          
CPR                                                                             
|     ******************************************************************        
|     * B101-INITIAL-RESTART-CHECK.                                    *        
|     ******************************************************************        
|      B101-INITIAL-RESTART-CHECK.                                              
|          MOVE 'B101-INITIAL-RESTART-CHECK' TO PV-PARAGRAPH-NAME.              
|                                                                               
|          READ CONTROL-FILE-1                                                  
|            AT END                                                             
|              DISPLAY 'NO CTL CARD FOUND'                                      
|              PERFORM Z910-ABEND.                                              
|                                                                               
|          MOVE CTL1-JOB-NAME TO  PC-JOB-NAME.                                  
|          PERFORM B102-INIT-CHECKPOINT-SELECT.                                 
|          MOVE TCKRSTCNTL-CKPT-FRQNCY-QTY TO PV-COMMIT-INTERVAL.               
|     ******************************************************************        
|     *  IF THE PROGRAM IS IN A RESTART STATUS, SELECT SAVED DATA               
|     *  FROM THE RESTART INFO TABLE AND ONLY PROCESS NON-COMPLETED             
|     *  RECORDS AS OF THE LAST COMMIT. IF NOT, THEN THE VARIABLES WILL         
|     *  CONTAIN THEIR INITIAL VALUES AND THE PROGRAM WILL PROCESS ALL          
|     *  RECORDS ON ALL INPUT FILES. IF A FILE WAS COMPLETELY PROCESSED         
|     *  IN THE PREVIOUS RUN BEFORE AN ABEND, IT WILL NOT BE OPENED OR          
|     *  READ AT ALL.                                                           
|     ******************************************************************        
|          IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
|               PERFORM B103-SELECT-RESTART-INFO                                
|               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                              
|                      TO CR-CHECKPOINT-RESTART-VAR                             
|               MOVE CR-ACT-READ-CNT TO PV-COMMIT-RECORDS                       
|               ADD +1 TO PV-COMMIT-RECORDS                                     
|               DISPLAY SPACES                                                  
|               DISPLAY '** PMKUT092 IS BEING RESTARTED **'                     
|               DISPLAY 'RESTARTED RECORDS FROM :'                              
|                              PV-COMMIT-RECORDS                                
|               DISPLAY SPACES                                                  
|           END-IF.                                                             
|                                                                               
|          IF CR-ACT-NOT-DONE                                                   
|            IF PS-B-FOR-A                                                      
|             PERFORM B350-READ-SALES-RESULT-FILE CR-ACT-READ-CNT TIMES         
|            END-IF                                                             
|          END-IF.                                                              
                                                                                
|     *----------------------------------------------------------               
|     * ISSUING A COMMIT TO CLOSE THE DB2 THREAD                                
      *----------------------------------------------------------               
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
|                                                                               
|                                                                               
|     *----------------------------------------------------------               
|     * B102-INIT-CHECKPOINT-SELECT.                                            
|     *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF             
|     *  THE PROGRAM IS IN A RESTART CONDITION.                                 
|     *----------------------------------------------------------               
|      B102-INIT-CHECKPOINT-SELECT.                                             
|                                                                               
|          EXEC SQL                                                             
|            SELECT                                                             
|                 CKPT_STAT_CODE                                                
|                ,CKPT_FRQNCY_QTY                                               
|              INTO                                                             
|                :TCKRSTCNTL-CKPT-STAT-CODE                                     
|               ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                    
|              FROM  TCKRSTCNTL                                                 
|             WHERE  JOB_NAME  = :PC-JOB-NAME                                   
|               AND  PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                       
|          END-EXEC.                                                            
|                                                                               
|          EVALUATE TRUE                                                        
|             WHEN SQLCODE = 0                                                  
|                 CONTINUE                                                      
|             WHEN OTHER                                                        
|              MOVE SQLCODE               TO PV-SQL                             
|              MOVE 'B102-INIT-CHECKPOINT-SELECT'                               
|                                         TO PV-PARAGRAPH-NAME                  
|              MOVE 'TCKRSTCNTL'          TO PV-TABLE-NAME                      
|              STRING 'RESTART CONTROL TBL SQLCODE= '                           
|                   PV-SQL DELIMITED BY SIZE INTO                               
|                      PV-OPERATION-MSG                                         
|              END-STRING                                                       
|              PERFORM Z100-ABEND                                               
|          END-EVALUATE.                                                        
|                                                                               
|     ******************************************************************        
|     *B103-SELECT-RESTART-INFO.                                                
|     *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO                
|     *  OBTAIN THE PROGRAMS SAVED VARIABLES.                                   
|     ******************************************************************        
|      B103-SELECT-RESTART-INFO.                                                
|                                                                               
|          EXEC SQL                                                             
|              SELECT                                                           
|                       WORK_STRG_DESC                                          
|                INTO                                                           
|                      :TCKRSTINF-WORK-STRG-DESC                                
|                FROM                                                           
|                      TCKRSTINF                                                
|                WHERE                                                          
|                      JOB_NAME  = :PC-JOB-NAME                                 
|                  AND PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                     
|          END-EXEC.                                                            
|                                                                               
|          EVALUATE TRUE                                                        
|             WHEN SQLCODE = 0                                                  
|                 CONTINUE                                                      
|             WHEN OTHER                                                        
|              MOVE SQLCODE               TO PV-SQL                             
|              MOVE 'B103-SELECT-RESTART-INFO'                                  
|                                         TO PV-PARAGRAPH-NAME                  
|              MOVE 'TCKRSTINF'           TO PV-TABLE-NAME                      
|              STRING 'RESTART CONTROL INFORMATION TBL SQLCODE= '               
|                   PV-SQL DELIMITED BY SIZE INTO                               
|                      PV-OPERATION-MSG                                         
|              END-STRING                                                       
|              PERFORM Z100-ABEND                                               
|          END-EVALUATE.                                                        
CPR                                                                             
CPR   ******************************************************************        
|     *B104-UPDATE-CHECKPOINT-STATUS.                                           
|     *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE                      
|     ******************************************************************        
|      B104-UPDATE-CHECKPOINT-STATUS.                                           
|                                                                               
|          EXEC SQL                                                             
|            UPDATE  TCKRSTCNTL                                                 
|               SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
|             WHERE  JOB_NAME  = :PC-JOB-NAME                                   
|               AND  PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                       
|          END-EXEC.                                                            
|                                                                               
|          EVALUATE TRUE                                                        
|             WHEN SQLCODE = 0                                                  
|                 CONTINUE                                                      
|             WHEN OTHER                                                        
|              MOVE SQLCODE               TO PV-SQL                             
|              MOVE 'B104-UPDATE-CHECKPOINT-STATUS'                             
|                                         TO PV-PARAGRAPH-NAME                  
|              MOVE 'TCKRSTCNTL'          TO PV-TABLE-NAME                      
|              STRING 'RESTART CONTROL TBL SQLCODE= '                           
|                   PV-SQL DELIMITED BY SIZE INTO                               
|                      PV-OPERATION-MSG                                         
|              END-STRING                                                       
|              PERFORM Z100-ABEND                                               
CPR        END-EVALUATE.                                                        
|                                                                               
CPR   ******************************************************************        
|     *B105-UPDATE-RESTART-INFOR.                                               
|     *  UPDATE THE STATUS ON THE CHECKPOINT RESTART INFORMATION TABLE          
|     ******************************************************************        
|      B105-UPDATE-RESTART-INFOR.                                               
|                                                                               
|          EXEC SQL                                                             
|            UPDATE TCKRSTINF                                                   
|               SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
|             WHERE JOB_NAME  = :PC-JOB-NAME                                    
|               AND PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                        
|          END-EXEC.                                                            
|                                                                               
|          EVALUATE TRUE                                                        
|             WHEN SQLCODE = 0                                                  
|                 CONTINUE                                                      
|             WHEN OTHER                                                        
|              MOVE SQLCODE               TO PV-SQL                             
|              MOVE 'B105-UPDATE-RESTART-INFOR'                                 
|                                         TO PV-PARAGRAPH-NAME                  
|              MOVE 'TCKRSTINF'           TO PV-TABLE-NAME                      
|              STRING 'RESTART CONTROL INFORMATION TBL SQLCODE= '               
|                   PV-SQL DELIMITED BY SIZE INTO                               
|                      PV-OPERATION-MSG                                         
|              END-STRING                                                       
|              PERFORM Z100-ABEND                                               
CPR        END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * B200-WRITE-TO-SALESDATE-FILE                                  *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE SALES DATE TO A FILE THAT WILL BE READ FROM      *         
      *    OTHER SALES RESULT LOAD PROGRAMS.                          *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-WRITE-TO-SALESDATE-FILE.                                            
                                                                                
           MOVE SA046-SLS-DTE TO SALES-DATE-LINE.                               
           WRITE SALES-DATE-LINE.                                               
           MOVE SA046-SLS-DTE TO PV-PREV-SLS-DTE.                               
                                                                                
      *****************************************************************         
      * B300-PROCESS-FILE                                             *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE SALES RESULT FILE.                             *         
      *  - IF THIS IS THE FIRST TIME THROUGH AND NOTHING WAS READ     *         
      *    ABEND THE PROGRAM.                                         *         
      *  - IF WE HAVE NOT HIT THE END OF THE FILE THEN PROCESS THE    *         
      *    ROW THAT WAS READ.                                         *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-PROCESS-FILE.                                                       
                                                                                
CPR        IF CR-ACT-NOT-DONE                                                   
              IF PS-B-FOR-A                                                     
                 PERFORM B350-READ-SALES-RESULT-FILE                            
              END-IF                                                            
CPR        END-IF.                                                              
                                                                                
           IF PS-NO-SALES-READ                                                  
              IF PS-SALES-RESULT-FILE-END                                       
                   MOVE SPACES TO SALES-DATE-LINE                               
                   WRITE SALES-DATE-LINE                                        
                   DISPLAY 'NOTHING IN THE COSA FILE'                           
                   DISPLAY '**CREATE EMPTY FILE**'                              
               ELSE                                                             
                   SET PS-SALES-READ TO TRUE                                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-SALES-READ AND PS-SALES-RESULT-FILE-START                      
                                                                                
      **ONLY WRITE SALES DATE OUT ON THE FIRST RECORD**                         
               IF PA-READ-COUNTER = 0                                           
                   PERFORM B200-WRITE-TO-SALESDATE-FILE                         
               ELSE                                                             
                   IF NOT(SA046-SLS-DTE = PV-PREV-SLS-DTE)                      
                       MOVE 'B300-PROCESS-FILE' TO                              
                                                     PV-PARAGRAPH-NAME          
                       DISPLAY 'THERE IS MORE THEN ONE TRN-SLS-DTE IN '         
                       DISPLAY 'FILE. INVESTIGATE. CALL COSA ON-CALL.'          
                       DISPLAY 'DATE ONE ' SA046-SLS-DTE                        
                       DISPLAY 'DATE TWO ' PV-PREV-SLS-DTE                      
                       MOVE 'MORE THEN ONE DATE' TO PV-OPERATION-MSG            
                       PERFORM Z910-ABEND                                       
                   END-IF                                                       
               END-IF                                                           
               IF (SA046-SAL-RET-CDE = PC-SALE                                  
                  OR SA046-SAL-RET-CDE = PC-ADJ-SALE)                           
      *  MOVE 'S' TO PV-SAL-RET-CDE HERE BECAUSE ALWAYS NEED TO                 
      *  INSERT/UPDATE WITH THIS VALUE - OTHERWISE ABENDS.                      
      *  VALID VALUES FROM COSA ARE 'S', 'A', 'B'.                              
                   MOVE 'S' TO PV-SAL-RET-CDE                                   
                   IF SA046-PM-PROMO-INTFC-ID > 0                               
                       IF (SA046-PM-SCHM-CDE = 100 OR                           
                          SA046-PM-SCHM-CDE = 101 OR                            
                          SA046-PM-SCHM-CDE = 200 OR                            
                          SA046-PM-SCHM-CDE = 500)                              
                           IF SA046-ITM-STAT-CDE = '30'                         
                               PERFORM C120-FIND-CLRNC                          
                           ELSE                                                 
                               PERFORM C100-FIND-SIMPLE                         
                           END-IF                                               
                       ELSE                                                     
                           IF (SA046-PM-SCHM-CDE = 300 OR                       
                              SA046-PM-SCHM-CDE = 301 OR                        
                              SA046-PM-SCHM-CDE = 800 OR                        
                              SA046-PM-SCHM-CDE = 801 OR                        
SCHCDE                        SA046-PM-SCHM-CDE = 802 OR                        
SCHCDE                        SA046-PM-SCHM-CDE = 400 OR                        
SCHCDE                        SA046-PM-SCHM-CDE = 401 OR                        
SCHCDE                        SA046-PM-SCHM-CDE = 402 OR                        
SCHCDE                        SA046-PM-SCHM-CDE = 403)                          
                               MOVE SA046-PM-PROMO-INTFC-ID TO                  
                                    PV-GP-PROMO-INTFC-ID                        
                               IF ((SA046-SKU-NBR NOT = PV-HOLD-SKU-NBR)        
                                    OR                                          
                                   (SA046-PM-PROMO-INTFC-ID NOT =               
                                    PV-HOLD-PM-PROMO-INTFC-ID))                 
                                   SET PS-NOT-SAME-SKU-INTFC-ID TO TRUE         
                                   ADD +1 TO PA-C110-GRP-SQL-COUNTER            
                                   PERFORM C110-FIND-GROUP-SKU                  
                               ELSE                                             
                                   SET PS-SAME-SKU-INTFC-ID TO TRUE             
                                   MOVE PV-PREV-PROMO-ID TO                     
                                        PV-PROMO-ID                             
                                   MOVE PV-PREV-PROMO-ITM-ID TO                 
                                        PV-PROMO-ITM-ID                         
                                   MOVE PV-PREV-MDSE-SELN-ID TO                 
                                        PV-MDSE-SELN-ID                         
                                   MOVE PC-SIMPLE                               
                                         TO PV-PROMO-SLS-TYP-CDE                
                                   IF PS-NO-INSERT-RECORD-PREV                  
                                       SET PS-NO-INSERT-RECORD TO TRUE          
                                   END-IF                                       
                                   ADD +1 TO PA-GRP-SKIP-SQL-COUNTER            
                               END-IF                                           
                           ELSE                                                 
                               IF SA046-PM-SCHM-CDE = SPACES                    
                                   MOVE 'B300-PROCESS-FILE'                     
                                                 TO PV-PARAGRAPH-NAME           
                                   MOVE 'NO SCHM CODE FOR RECORD'               
                                                 TO PV-OPERATION-MSG-1          
                               ELSE                                             
                                   MOVE 'B300-PROCESS-FILE'                     
                                                 TO PV-PARAGRAPH-NAME           
                                   MOVE 'SCHM CODE NOT RECOGNIZED'              
                                                 TO PV-OPERATION-MSG-1          
                                   DISPLAY 'SA046-PM-SCHM-CDE = '               
                                            SA046-PM-SCHM-CDE                   
                               END-IF                                           
                               PERFORM Z910-ABEND                               
                           END-IF                                               
                       END-IF                                                   
                       PERFORM D100-CALC-INSERT-CONTROL                         
                   ELSE                                                         
                       IF (SA046-BOGO-INTFC-ID > 0)                             
                           IF (SA046-BOGO-SCHM-CDE = 800 OR                     
                               SA046-BOGO-SCHM-CDE = 801 OR                     
SCHCDE                         SA046-BOGO-SCHM-CDE = 802 OR                     
SCHCDE                         SA046-BOGO-SCHM-CDE = 400 OR                     
SCHCDE                         SA046-BOGO-SCHM-CDE = 401 OR                     
SCHCDE                         SA046-BOGO-SCHM-CDE = 402 OR                     
SCHCDE                         SA046-BOGO-SCHM-CDE = 403)                       
                               MOVE SA046-BOGO-INTFC-ID TO                      
                                    PV-GP-PROMO-INTFC-ID                        
                               IF ((SA046-SKU-NBR NOT = PV-HOLD-SKU-NBR)        
                                    OR                                          
                                   (SA046-BOGO-INTFC-ID NOT =                   
                                    PV-HOLD-BOGO-INTFC-ID))                     
                                   SET PS-NOT-SAME-SKU-INTFC-ID TO TRUE         
                                   ADD +1 TO PA-C110-BOGO-SQL-COUNTER           
                                   PERFORM C110-FIND-GROUP-SKU                  
                               ELSE                                             
                                   SET PS-SAME-SKU-INTFC-ID TO TRUE             
                                   MOVE PV-PREV-PROMO-ID TO                     
                                        PV-PROMO-ID                             
                                   MOVE PV-PREV-PROMO-ITM-ID TO                 
                                        PV-PROMO-ITM-ID                         
                                   MOVE PV-PREV-MDSE-SELN-ID TO                 
                                        PV-MDSE-SELN-ID                         
                                   MOVE PC-SIMPLE                               
                                         TO PV-PROMO-SLS-TYP-CDE                
                                   IF PS-NO-INSERT-RECORD-PREV                  
                                       SET PS-NO-INSERT-RECORD TO TRUE          
                                   END-IF                                       
                                   ADD +1 TO PA-BOGO-SKIP-SQL-COUNTER           
                               END-IF                                           
                               PERFORM D100-CALC-INSERT-CONTROL                 
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
      ***********************************************************               
      *** ONLY GET INTO THIS LOGIC IF:                                          
      ***  1. TIER + NORMAL INTFC-ID + MATCHING A/B RECORDS                     
      ***  - OR -                                                               
      ***  2. TIER + NO NORMAL INTFC-ID FILLED IN (REGARDLESS OF                
      ***     MATCHING A/B RECORDS - OTHERWISE GOES INTO A LOOP                 
      ***********************************************************               
                   IF (((SA046-TIERED-INTFC-SUB-ID > 0) AND                     
                        (PS-B-FOR-A))                                           
                      OR                                                        
                       ((SA046-TIERED-INTFC-SUB-ID > 0) AND                     
                        (SA046-PM-PROMO-INTFC-ID = 0)))                         
                       MOVE PC-TIERED TO PV-PROMO-SLS-TYP-CDE                   
      *** THIS IS ADMIN TIERED WHERE MDSE WAS NOT ALREADY ON SALE.              
                       IF SA046-PM-PROMO-INTFC-ID = 0                           
                           SET PS-TIERED-NOT-ON-SALE TO TRUE                    
                           PERFORM C140-FIND-ADMIN-TIER                         
                       ELSE                                                     
                           SET PS-TIERED-ON-SALE TO TRUE                        
                       END-IF                                                   
                       PERFORM D100-CALC-INSERT-CONTROL                         
                       SET PS-NOT-TIERED TO TRUE                                
                   END-IF                                                       
      *** WILL NEED TO ADD A CHECK FOR ADD'L AMT OFF AND PWP/GWP                
      *** INTERFACE ID'S FOR PHASE 2.                                           
      *** THIS CHECK WILL ALSO NEED TO BE UPDATED FOR ADD'L AMT OFF             
      *** AND PWP/GWP.                                                          
                   IF SA046-PM-PROMO-INTFC-ID = 0 AND                           
                      SA046-TIERED-INTFC-SUB-ID = 0 AND                         
                      SA046-BOGO-INTFC-ID = 0 AND                               
                      PS-B-FOR-A                                                
                       DISPLAY 'IGNORED THIS RECORD BECAUSE THE '               
                       DISPLAY 'INTERFACE IDS WERE ZERO.'                       
                       DISPLAY ' SALES DATE    = ' SA046-SLS-DTE                
                       DISPLAY ' TRANSACTION NBR = ' SA046-TRN-NBR              
                       DISPLAY ' REGISTER ID   = ' SA046-RGST-ID                
                       DISPLAY ' SEQ NBR      = ' SA046-LNE-ITM-SEQ-NBR         
                       DISPLAY ' SKU NBR       = ' SA046-SKU-NBR                
                       DISPLAY ' STORE NBR     = ' SA046-STR-NBR                
                       DISPLAY ' SLS-RTN-CDE   = ' SA046-SAL-RET-CDE            
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY 'SALES DATE    = ' SA046-SLS-DTE                     
                   DISPLAY 'TRANSACTION NBR = ' SA046-TRN-NBR                   
                   DISPLAY 'REGISTER ID   = ' SA046-RGST-ID                     
                   DISPLAY 'SEQ NBR      = ' SA046-LNE-ITM-SEQ-NBR              
                   DISPLAY 'SKU NBR       = ' SA046-SKU-NBR                     
                   DISPLAY 'STORE NBR     = ' SA046-STR-NBR                     
                   DISPLAY 'SLS-RTN-CDE   = ' SA046-SAL-RET-CDE                 
                   DISPLAY 'IGNORED THIS RECORD BECAUSE THE '                   
                   DISPLAY 'SA046-SAL-RET-CDE WAS NOT S,A,OR IT WAS '           
                   DISPLAY 'A B WITHOUT AN A.'                                  
                   MOVE 'B300-PROCESS-FILE' TO                                  
                                                  PV-PARAGRAPH-NAME             
                   MOVE 'INVALID SALES RETURN CDE' TO PV-OPERATION-MSG          
                   PERFORM Z910-ABEND                                           
               END-IF                                                           
           ELSE                                                                 
               IF PS-SALES-READ AND PS-SALES-RESULT-FILE-END                    
                   DISPLAY 'END OF FILE'                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PS-INSERT-RECORD-SW     TO PS-INSERT-RECORD-PREV-SW.            
           SET PS-INSERT-RECORD         TO TRUE.                                
           SET PS-NOT-SAME-SKU-INTFC-ID TO TRUE.                                
           INITIALIZE PV-INSERT-VARIABLES.                                      
           INITIALIZE PV-CALCULATED-VARIABLES.                                  
           MOVE SA046-SLS-DTE TO PV-PREV-SLS-DTE.                               
           MOVE SA046-PM-SALES-RESULTS-REC TO                                   
            PV-HOLD-SALES-RESULTS-REC.                                          
                                                                                
      *****************************************************************         
      * B350-READ-SALES-RESULT-FILE                                   *         
      * ==> PROCESSES:                                                *         
      *  - COMMIT LOGIC.                                              *         
      *  - READ IN THE SALES RESULT FILE.                             *         
      *  - ADD +1 TO RECORD COUNTER.                                  *         
      *  - IF WE HAVE NOT HIT THE END OF THE FILE THEN PROCESS THE    *         
      *    ROW THAT WAS READ.                                         *         
      *****************************************************************         
       B350-READ-SALES-RESULT-FILE.                                             
                                                                                
           IF PS-SALES-READ AND PS-SALES-RESULT-FILE-START                      
               ADD +1 TO PA-READ-COUNTER                                        
CPR            ADD 1  TO PV-COMMIT-CNTR                                         
CPR            IF PV-COMMIT-CNTR > PV-COMMIT-INTERVAL                           
CPR                ADD PV-COMMIT-INTERVAL TO CR-ACT-READ-CNT                    
                   PERFORM C170-COMMIT                                          
                   PERFORM E200-CPU-TIME                                        
               END-IF                                                           
               DIVIDE PA-READ-COUNTER BY 10000                                  
                   GIVING PV-RECS-DIV                                           
                   REMAINDER PV-RECS-REM                                        
               IF PV-RECS-REM = 0                                               
                   PERFORM E200-CPU-TIME                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           READ SALES-RESULT-FILE INTO SA046-PM-SALES-RESULTS-REC               
              AT END SET PS-SALES-RESULT-FILE-END TO TRUE                       
CPR                  SET CR-ACT-DONE TO TRUE                                    
CPR        END-READ.                                                            
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SALES RESULT FILE RECIEVED FROM COSA             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  SALES-RESULT-FILE                                             
                  SALES-DATE-FILE                                               
CPR               CONTROL-FILE-1.                                               
                                                                                
      *****************************************************************         
      * C100-FIND-SIMPLE.                                             *         
      * ==> PROCESSES:                                                *         
      *  - FIND PROMO_ID, PROMO_ITM_ID, AND MDSE_SELN_ID OF THE       *         
      *    PROMO_INTFC_ID.  THERE MAY BE MULTIPLE ROWS BROUGHT BACK   *         
      *    BUT THAT IS OKAY BECAUSE THERE SHOULD ONLY BE ONE COMBO OF *         
      *    WHAT WE WANT.  IF NOTHING WAS FOUND HERE LOOK AT CMPTV.    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      *  - 1403 = A ROW WAS NOT FOUND.                                *         
      *  - 2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C100-FIND-SIMPLE.                                                        
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    D.PROMO_ID                                                  
                   ,D.PROMO_ITM_ID                                              
                   ,C.MDSE_SELN_ID                                              
               INTO                                                             
                    :PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-MDSE-SELN-ID                                            
               FROM                                                             
                    PMT_MDSE_LNE_PRICG A                                        
                   ,PMT_MDSE_SELN_LNE B                                         
                   ,PMT_MDSE_SELN C                                             
                   ,PMT_PROMO_ITM D                                             
               WHERE                                                            
                    A.SIMP_PROMO_INTFC_ID = :SA046-PM-PROMO-INTFC-ID            
                AND A.MDSE_SELN_LNE_ID = B.MDSE_SELN_LNE_ID                     
                AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                             
                AND C.PROMO_ITM_ID = D.PROMO_ITM_ID                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
             WHEN SQLCODE = 2112                                                
               ADD +1                    TO PA-SIMPLE-CNT                       
               MOVE PC-SIMPLE            TO PV-PROMO-SLS-TYP-CDE                
                                                                                
             WHEN SQLCODE = 1403                                                
               PERFORM C130-FIND-CMPTV                                          
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'PMT_MDSE_LNE_PRCG'   TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SIMPLE'      TO PV-PARAGRAPH-NAME                
               STRING 'FIND SIMPLE ERROR - SQLCODE= '                           
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-FIND-GROUP-SKU                                           *         
      * ==> PROCESSES:                                                *         
      *  - FIND PROMO_ID, PROMO_ITM_ID, AND MDSE_SELN_ID OF THE       *         
      *    PROMO_INTFC_ID.  THERE MAY BE MULTIPLE ROWS BROUGHT BACK   *         
      *    BUT THAT IS OKAY BECAUSE THERE SHOULD ONLY BE ONE COMBO OF *         
      *    WHAT WE WANT.  IF NOTHING WAS FOUND HERE DISPLAY A MESSAGE *         
      *    IN THE OUTPUT.                                             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      *  - 1403 = A ROW WAS NOT FOUND.                                *         
      *  - 2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C110-FIND-GROUP-SKU.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT /*+ FIRST_ROWS */                                         
                    F.PROMO_ID                                                  
                   ,F.PROMO_ITM_ID                                              
                   ,E.MDSE_SELN_ID                                              
               INTO                                                             
                    :PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-MDSE-SELN-ID                                            
               FROM                                                             
                    PMT_GP_PRIC A                                               
                   ,PMT_MDSE_LNE_GP_PRIC B                                      
                   ,PMT_MDSE_LNE_PRICG C                                        
                   ,PMT_MDSE_SELN_LNE D                                         
                   ,PMT_MDSE_SELN E                                             
                   ,PMT_PROMO_ITM F                                             
               WHERE                                                            
                    A.GP_PROMO_INTFC_ID = :PV-GP-PROMO-INTFC-ID                 
                AND D.SKU_NBR = :SA046-SKU-NBR                                  
                AND E.MDSE_HIER_CDE = 'SKU'                                     
                AND A.GP_PRIC_ID = B.GP_PRIC_ID                                 
                AND B.MDSE_LNE_PRICG_ID = C.MDSE_LNE_PRICG_ID                   
                AND C.MDSE_SELN_LNE_ID = D.MDSE_SELN_LNE_ID                     
                AND D.MDSE_SELN_ID = E.MDSE_SELN_ID                             
                AND E.PROMO_ITM_ID = F.PROMO_ITM_ID                             
                AND C.STAT_CDE    <> 'D'                                        
                AND D.STAT_CDE    <> 'D'                                        
                AND E.STAT_CDE    <> 'D'                                        
                AND F.STAT_CDE    <> 'D'                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
             WHEN SQLCODE = 2112                                                
               ADD +1                    TO PA-GROUP-SKU-CNT                    
               MOVE PC-SIMPLE            TO PV-PROMO-SLS-TYP-CDE                
                                                                                
             WHEN SQLCODE = 1403                                                
               ADD +1 TO PA-C115-SQL-COUNTER                                    
               PERFORM C115-FIND-GROUP-VS                                       
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                TO PV-SQL                            
               MOVE 'PMT_GP_PRIC'          TO PV-TABLE-NAME                     
               MOVE 'C110-FIND-GROUP-SKU'  TO PV-PARAGRAPH-NAME                 
               STRING 'FIND GROUP ERROR - SQLCODE= '                            
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C115-FIND-GROUP-VS.                                           *         
      * ==> PROCESSES:                                                *         
      * ==> SQLCODE:                                                  *         
      * ==> PERFORMED FROM:                                           *         
      *****************************************************************         
       C115-FIND-GROUP-VS.                                                      
                                                                                
           EXEC SQL                                                             
               SELECT /*+ FIRST_ROWS (10) */                                    
                    F.PROMO_ID                                                  
                   ,F.PROMO_ITM_ID                                              
                   ,E.MDSE_SELN_ID                                              
               INTO                                                             
                    :PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-MDSE-SELN-ID                                            
               FROM                                                             
                    PMT_GP_PRIC A                                               
                   ,PMT_MDSE_LNE_GP_PRIC B                                      
                   ,PMT_MDSE_LNE_PRICG C                                        
                   ,PMT_MDSE_SELN_LNE D                                         
                   ,PMT_MDSE_SELN E                                             
                   ,PMT_PROMO_ITM F                                             
                   ,XKT_SKU G                                                   
                   ,XKT_VND_STYL H                                              
               WHERE                                                            
                    A.GP_PROMO_INTFC_ID = :PV-GP-PROMO-INTFC-ID                 
                AND G.SKU_NBR = :SA046-SKU-NBR                                  
                AND H.STYL_ID = G.STYL_ID                                       
                AND D.DEPT_NBR = H.DEPT_NBR                                     
                AND D.MAJ_CL_NBR = H.MAJ_CL_NBR                                 
                AND D.SUB_CL_NBR = H.SUB_CL_NBR                                 
                AND D.VS_ID = H.VS_ID                                           
                AND E.MDSE_HIER_CDE = 'STY'                                     
                AND A.GP_PRIC_ID = B.GP_PRIC_ID                                 
                AND B.MDSE_LNE_PRICG_ID = C.MDSE_LNE_PRICG_ID                   
                AND C.MDSE_SELN_LNE_ID = D.MDSE_SELN_LNE_ID                     
                AND D.MDSE_SELN_ID = E.MDSE_SELN_ID                             
                AND E.PROMO_ITM_ID = F.PROMO_ITM_ID                             
                AND C.STAT_CDE    <> 'D'                                        
                AND D.STAT_CDE    <> 'D'                                        
                AND E.STAT_CDE    <> 'D'                                        
                AND F.STAT_CDE    <> 'D'                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
             WHEN SQLCODE = 2112                                                
               ADD +1                    TO PA-GROUP-VS-CNT                     
               MOVE PC-SIMPLE            TO PV-PROMO-SLS-TYP-CDE                
                                                                                
             WHEN SQLCODE = 1403                                                
               SET PS-NO-INSERT-RECORD TO TRUE                                  
               DISPLAY 'INTFC ID CANNOT BE FOUND FROM GROUP LOGIC:'             
               DISPLAY ' PROMO INTFC ID  = ' PV-GP-PROMO-INTFC-ID               
               DISPLAY ' STORE NBR       = ' SA046-STR-NBR                      
               DISPLAY ' REGISTER ID     = ' SA046-RGST-ID                      
               DISPLAY ' TRANSACTION NBR = ' SA046-TRN-NBR                      
               DISPLAY ' SALES DATE      = ' SA046-SLS-DTE                      
               DISPLAY ' SKU NBR         = ' SA046-SKU-NBR                      
               DISPLAY ' SEQUENCE NBR    = ' SA046-LNE-ITM-SEQ-NBR              
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                TO PV-SQL                            
               MOVE 'PMT_GP_PRIC'          TO PV-TABLE-NAME                     
               MOVE 'C115-FIND-GROUP-VS'   TO PV-PARAGRAPH-NAME                 
               STRING 'FIND GROUP ERROR - SQLCODE= '                            
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-FIND-CLRNC                                               *         
      * ==> PROCESSES:                                                *         
      *  - FIND PROMO_ID, PROMO_ITM_ID, AND MDSE_SELN_ID OF THE       *         
      *    PROMO_INTFC_ID.  THERE MAY BE MULTIPLE ROWS BROUGHT BACK   *         
      *    BUT THAT IS OKAY BECAUSE THERE SHOULD ONLY BE ONE COMBO OF *         
      *    WHAT WE WANT.  IF NOTHING WAS FOUND HERE DISPLAY A MESSAGE *         
      *    IN THE OUTPUT.                                             *         
      * ==> SQLCODE:                                                  *         
      *    0 = A ROW WAS FOUND.                                       *         
      *    1403 = A ROW WAS NOT FOUND.                                *         
      *    2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C120-FIND-CLRNC.                                                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    B.CLRNC_PROMO_ID                                            
                   ,B.MDSE_SELN_LNE_ID                                          
               INTO                                                             
                    :PV-PROMO-ID                                                
                  , :PV-MDSE-SELN-LNE-ID                                        
               FROM                                                             
                    PMT_CLRNC_PRICG A                                           
                   ,PMT_CLRNC_MDSE_LNE B                                        
               WHERE                                                            
                    A.CLRNC_PROMO_INTFC_ID = :SA046-PM-PROMO-INTFC-ID           
                AND A.MDSE_SELN_LNE_ID = B.MDSE_SELN_LNE_ID                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
               ADD +1                        TO PA-CLRNC-CNT                    
               MOVE PC-CLRNC                 TO PV-PROMO-SLS-TYP-CDE            
                                                                                
             WHEN SQLCODE = 1403                                                
               SET PS-NO-INSERT-RECORD TO TRUE                                  
               DISPLAY 'INTFC ID CANNOT BE FOUND FROM CLRNC LOGIC:'             
               DISPLAY ' PROMO INTFC ID  = ' SA046-PM-PROMO-INTFC-ID            
               DISPLAY ' STORE NBR       = ' SA046-STR-NBR                      
               DISPLAY ' REGISTER ID     = ' SA046-RGST-ID                      
               DISPLAY ' TRANSACTION NBR = ' SA046-TRN-NBR                      
               DISPLAY ' SALES DATE      = ' SA046-SLS-DTE                      
               DISPLAY ' SKU NBR         = ' SA046-SKU-NBR                      
               DISPLAY ' SEQUENCE NBR    = ' SA046-LNE-ITM-SEQ-NBR              
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                  TO PV-SQL                          
               MOVE 'PMT_CLRNC_PRICG'        TO PV-TABLE-NAME                   
               MOVE 'C120-FIND-CLRNC'        TO PV-PARAGRAPH-NAME               
               STRING 'FIND PRICING ERROR - SQLCODE= '                          
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C130-FIND-CMPTV                                               *         
      * ==> PROCESSES:                                                *         
      *  - FIND PROMO_ID, PROMO_ITM_ID, AND MDSE_SELN_ID OF THE       *         
      *    PROMO_INTFC_ID.  THERE MAY BE MULTIPLE ROWS BROUGHT BACK   *         
      *    BUT THAT IS OKAY BECAUSE THERE SHOULD ONLY BE ONE COMBO OF *         
      *    WHAT WE WANT.  IF NOTHING WAS FOUND HERE DISPLAY A MESSAGE *         
      *    IN THE OUTPUT.                                             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      *    1403 = A ROW WAS NOT FOUND.                                *         
      *  - 2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: C100-FIND-SIMPLE                          *         
      *****************************************************************         
       C130-FIND-CMPTV.                                                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    B.PROMO_ID                                                  
                   ,A.PROMO_ITM_ID                                              
               INTO                                                             
                    :PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
               FROM                                                             
                    PMT_CMPTV_PRICG A                                           
                   ,PMT_PROMO_ITM B                                             
               WHERE                                                            
                    A.CMPTV_PROMO_INTFC_ID = :SA046-PM-PROMO-INTFC-ID           
                AND A.PROMO_ITM_ID = B.PROMO_ITM_ID                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
               ADD +1                    TO PA-CMPTV-CNT                        
               MOVE PC-SIMPLE            TO PV-PROMO-SLS-TYP-CDE                
                                                                                
             WHEN SQLCODE = 1403                                                
               SET PS-NO-INSERT-RECORD TO TRUE                                  
               DISPLAY 'INTFC ID CANNOT BE FOUND FROM SIMP/CMPTV LOGIC:'        
               DISPLAY ' PROMO INTFC ID  = ' SA046-PM-PROMO-INTFC-ID            
               DISPLAY ' STORE NBR       = ' SA046-STR-NBR                      
               DISPLAY ' REGISTER ID     = ' SA046-RGST-ID                      
               DISPLAY ' TRANSACTION NBR = ' SA046-TRN-NBR                      
               DISPLAY ' SALES DATE      = ' SA046-SLS-DTE                      
               DISPLAY ' SKU NBR         = ' SA046-SKU-NBR                      
               DISPLAY ' SEQUENCE NBR    = ' SA046-LNE-ITM-SEQ-NBR              
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                TO PV-SQL                            
               MOVE 'PMT_CMPTV_PRICG'      TO PV-TABLE-NAME                     
               MOVE 'C130-FIND-CMPTV'      TO PV-PARAGRAPH-NAME                 
               STRING 'FIND CMPTV - SQLCODE= '                                  
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C140-FIND-ADMIN-TIER.                                         *         
      * ==> PROCESSES:                                                *         
      *  - FIND PROMO_ID, PROMO_ITM_ID, AND MDSE_SELN_ID OF THE       *         
      *    PROMO_INTFC_ID.  THERE MAY BE MULTIPLE ROWS BROUGHT BACK   *         
      *    BUT THAT IS OKAY BECAUSE THERE SHOULD ONLY BE ONE COMBO OF *         
      *    WHAT WE WANT.  IF NOTHING WAS FOUND HERE DISPLAY A MESSAGE *         
      *    IN THE OUTPUT.                                             *         
      * ==> SQLCODE:                                                  *         
      *    0 = A ROW WAS FOUND.                                       *         
      *    1403 = A ROW WAS NOT FOUND.                                *         
      *    2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C140-FIND-ADMIN-TIER.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    C.PROMO_ID                                                  
                   ,D.PROMO_TIER_MDSE_ID                                        
               INTO                                                             
                    :PV-PROMO-ID                                                
                   ,:PV-MDSE-SELN-LNE-ID                                        
               FROM                                                             
                    PMT_TIER_PRICG_GP_TIER A                                    
                   ,PMT_TIER_PRICG_GP B                                         
                   ,PMT_TIER_PRICG C                                            
                   ,PMT_PROMO_TIER_MDSE D                                       
               WHERE                                                            
                    D.TIER_PRICG_ID = C.TIER_PRICG_ID                           
                AND C.TIER_PRICG_ID = B.TIER_PRICG_ID                           
                AND B.TIER_PRICG_GP_ID = A.TIER_PRICG_GP_ID                     
                AND A.TIER_PROMO_INTFC_SUB_ID =                                 
                                          :SA046-TIERED-INTFC-SUB-ID            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
               ADD +1                        TO PA-ADMIN-TIER-CNT               
               MOVE PC-TIERED                TO PV-PROMO-SLS-TYP-CDE            
                                                                                
             WHEN SQLCODE = 1403                                                
               SET PS-NO-INSERT-RECORD TO TRUE                                  
               DISPLAY 'INTFC ID CANNOT BE FOUND FROM TIERED LOGIC:'            
               DISPLAY ' TIERED INTFC ID = ' SA046-TIERED-INTFC-SUB-ID          
               DISPLAY ' STORE NBR       = ' SA046-STR-NBR                      
               DISPLAY ' REGISTER ID     = ' SA046-RGST-ID                      
               DISPLAY ' TRANSACTION NBR = ' SA046-TRN-NBR                      
               DISPLAY ' SALES DATE      = ' SA046-SLS-DTE                      
               DISPLAY ' SKU NBR         = ' SA046-SKU-NBR                      
               DISPLAY ' SEQUENCE NBR    = ' SA046-LNE-ITM-SEQ-NBR              
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                  TO PV-SQL                          
               MOVE 'PMT_TIER_PRICG_GP_TIER' TO PV-TABLE-NAME                   
               MOVE 'C140-FIND-ADMIN-TIER'   TO PV-PARAGRAPH-NAME               
               STRING 'FIND TIER ERROR - SQLCODE= '                             
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C150-INSERT-INTO-NONITM-TBL                                   *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE DATA INTO THE TEMP TABLE                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL                                            *         
      *  - 0001 = UNIQUE CONSTRAINT VIOLATED. ROW ALREADY EXISTS.     *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C150-INSERT-INTO-NONITM-TBL.                                             
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMTT_SLS_RSLT_LOD                                    
                   (TRN_SLS_DTE                                                 
                   ,STR_NBR                                                     
                   ,PROMO_ID                                                    
                   ,PROMO_INTFC_SUB_ID                                          
                   ,PROMO_SLS_TYP_CDE                                           
                   ,MDSE_SELN_LNE_ID                                            
                   ,DEPT_NBR                                                    
                   ,MAJ_CL_NBR                                                  
                   ,SUB_CL_NBR                                                  
                   ,SKU_NBR                                                     
                   ,SLS_RTL_AMT                                                 
                   ,SLS_UNT_QTY                                                 
                   ,MKDN_RTL_AMT                                                
                   ,UNT_REG_RTL_AMT                                             
                   ,UNT_REG_COST_AMT                                            
                   ,OWND_UNT_RTL_AMT                                            
                   ,NET_CHRGD_AMT                                               
                   ,SLS_TRN_TYP_CDE                                             
                   ,SLS_COST_AMT                                                
                   ,MIN_UNT_PROMO_RTL_AMT                                       
                   ,MAX_UNT_PROMO_RTL_AMT                                       
                   ,UNT_MKDN_MIN_RTL_AMT                                        
                   ,UNT_MKDN_MAX_RTL_AMT                                        
                   ,UNT_MKDN_MIN_RTL_PCT                                        
                   ,UNT_MKDN_MAX_RTL_PCT                                        
                   ,UNT_MKUP_MIN_RTL_PCT                                        
                   ,UNT_MKUP_MAX_RTL_PCT)                                       
               VALUES                                                           
                   (TO_DATE(:SA046-SLS-DTE, 'YYYY-MM-DD')                       
                   ,:SA046-STR-NBR                                              
                   ,:PV-PROMO-ID                                                
                   ,:PV-PROMO-INTFC-SUB-ID                                      
                   ,:PV-PROMO-SLS-TYP-CDE                                       
                   ,:PV-MDSE-SELN-LNE-ID                                        
                   ,:SA046-DEPT-NBR                                             
                   ,:SA046-MAJ-CL-NBR                                           
                   ,:SA046-SUB-CL-NBR                                           
                   ,:SA046-SKU-NBR                                              
                   ,:PV-SLS-RTL-AMT                                             
                   ,:SA046-SLD-QTY                                              
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:SA046-REG-RTL-AMT                                          
                   ,:SA046-UNT-COST-AMT                                         
                   ,:SA046-OWND-UNT-RTL-AMT                                     
                   ,:SA046-NET-CHRGD-AMT                                        
                   ,:PV-SAL-RET-CDE                                             
                   ,:PV-SLS-COST-AMT                                            
                   ,:PV-UNT-PROMO-RTL-AMT                                       
                   ,:PV-UNT-PROMO-RTL-AMT                                       
                   ,:PV-UNT-MKDN-RTL-AMT                                        
                   ,:PV-UNT-MKDN-RTL-AMT                                        
                   ,:PV-UNT-MKDN-RTL-PCT                                        
                   ,:PV-UNT-MKDN-RTL-PCT                                        
                   ,:PV-UNT-MKUP-RTL-PCT                                        
                   ,:PV-UNT-MKUP-RTL-PCT)                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               ADD +1                     TO PA-INSERT-NONITM-CNT               
      *** DON'T NEED THE PREV VARIABLES HERE BECUASE THERE WILL NEVER           
      *** BE A TIERED NONITM.                                                   
                                                                                
             WHEN SQLCODE = -00001                                              
               PERFORM C160-UPDATE-NONITM-TBL                                   
                                                                                
             WHEN OTHER                                                         
               DISPLAY ' C150-INSERT-INTO-NONITM-TBL'                           
               DISPLAY ' SLS DATE           = ' SA046-SLS-DTE                   
               DISPLAY ' STR NBR            = ' SA046-STR-NBR                   
               DISPLAY ' PROMO ID           = ' PV-PROMO-ID                     
               DISPLAY ' PROMO INTFC SUB ID = ' PV-PROMO-INTFC-SUB-ID           
               DISPLAY ' PROMO SLS TYP CDE  = ' PV-PROMO-SLS-TYP-CDE            
               DISPLAY ' SELN LNE ID        = ' PV-MDSE-SELN-LNE-ID             
               DISPLAY ' DEPT NBR           = ' SA046-DEPT-NBR                  
               DISPLAY ' MAJ CL NBR         = ' SA046-MAJ-CL-NBR                
               DISPLAY ' SUB CL NBR         = ' SA046-SUB-CL-NBR                
               DISPLAY ' SKU NBR            = ' SA046-SKU-NBR                   
               DISPLAY ' SLS RTL AMT        = ' PV-SLS-RTL-AMT                  
               DISPLAY ' SLD QTY            = ' SA046-SLD-QTY                   
               DISPLAY ' MKDN RTL AMT       = ' PV-MKDN-RTL-AMT                 
               DISPLAY ' REG RTL AMT        = ' SA046-REG-RTL-AMT               
               DISPLAY ' UNT COST AMT       = ' SA046-UNT-COST-AMT              
               DISPLAY ' OWND UNT RTL AMT   = ' SA046-OWND-UNT-RTL-AMT          
               DISPLAY ' NET CHARGED AMT    = ' SA046-NET-CHRGD-AMT             
               DISPLAY ' PV SAL RET CDE     = ' PV-SAL-RET-CDE                  
               DISPLAY ' SAL RETURN CDE     = ' SA046-SAL-RET-CDE               
               DISPLAY ' SLS COST AMT       = ' PV-SLS-COST-AMT                 
               DISPLAY ' UNT PROMO RTL AMT  = ' PV-UNT-PROMO-RTL-AMT            
               DISPLAY ' UNT MKDN RTL AMT   = ' PV-UNT-MKDN-RTL-AMT             
               DISPLAY ' UNT MKDN RTL PCT   = ' PV-UNT-MKDN-RTL-PCT             
               DISPLAY ' UNT MKUP RTL PCT   = ' PV-UNT-MKUP-RTL-PCT             
               MOVE SQLCODE                       TO PV-SQL                     
               MOVE 'PMTT_SLS_RSLT_LOD'           TO PV-TABLE-NAME              
               MOVE 'C150-INSERT-INTO-NONITM-TBL' TO PV-PARAGRAPH-NAME          
               STRING 'INSERT ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C155-INSERT-INTO-ITM-TBL                                      *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE DATA INTO THE TEMP TABLE                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL                                            *         
      *  - 0001 = UNIQUE CONSTRAINT VIOLATED. ROW ALREADY EXISTS.     *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C155-INSERT-INTO-ITM-TBL.                                                
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMTT_PROMO_ITM_SLS_RSLT_LOD                          
                   (TRN_SLS_DTE                                                 
                   ,STR_NBR                                                     
                   ,PROMO_ID                                                    
                   ,PROMO_ITM_ID                                                
                   ,PROMO_INTFC_SUB_ID                                          
                   ,PROMO_SLS_TYP_CDE                                           
                   ,MDSE_SELN_ID                                                
                   ,DEPT_NBR                                                    
                   ,MAJ_CL_NBR                                                  
                   ,SUB_CL_NBR                                                  
                   ,SKU_NBR                                                     
                   ,SLS_RTL_AMT                                                 
                   ,SLS_UNT_QTY                                                 
                   ,MKDN_RTL_AMT                                                
                   ,UNT_REG_RTL_AMT                                             
                   ,UNT_REG_COST_AMT                                            
                   ,OWND_UNT_RTL_AMT                                            
                   ,NET_CHRGD_AMT                                               
                   ,SLS_TRN_TYP_CDE                                             
                   ,SLS_COST_AMT                                                
                   ,MIN_UNT_PROMO_RTL_AMT                                       
                   ,MAX_UNT_PROMO_RTL_AMT                                       
                   ,UNT_MKDN_MIN_RTL_AMT                                        
                   ,UNT_MKDN_MAX_RTL_AMT                                        
                   ,UNT_MKDN_MIN_RTL_PCT                                        
                   ,UNT_MKDN_MAX_RTL_PCT                                        
                   ,UNT_MKUP_MIN_RTL_PCT                                        
                   ,UNT_MKUP_MAX_RTL_PCT)                                       
               VALUES                                                           
                   (TO_DATE(:SA046-SLS-DTE, 'YYYY-MM-DD')                       
                   ,:SA046-STR-NBR                                              
                   ,:PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-PROMO-INTFC-SUB-ID                                      
                   ,:PV-PROMO-SLS-TYP-CDE                                       
                   ,:PV-MDSE-SELN-ID                                            
                   ,:SA046-DEPT-NBR                                             
                   ,:SA046-MAJ-CL-NBR                                           
                   ,:SA046-SUB-CL-NBR                                           
                   ,:SA046-SKU-NBR                                              
                   ,:PV-SLS-RTL-AMT                                             
                   ,:SA046-SLD-QTY                                              
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:SA046-REG-RTL-AMT                                          
                   ,:SA046-UNT-COST-AMT                                         
                   ,:SA046-OWND-UNT-RTL-AMT                                     
                   ,:SA046-NET-CHRGD-AMT                                        
                   ,:PV-SAL-RET-CDE                                             
                   ,:PV-SLS-COST-AMT                                            
                   ,:PV-UNT-PROMO-RTL-AMT                                       
                   ,:PV-UNT-PROMO-RTL-AMT                                       
                   ,:PV-UNT-MKDN-RTL-AMT                                        
                   ,:PV-UNT-MKDN-RTL-AMT                                        
                   ,:PV-UNT-MKDN-RTL-PCT                                        
                   ,:PV-UNT-MKDN-RTL-PCT                                        
                   ,:PV-UNT-MKUP-RTL-PCT                                        
                   ,:PV-UNT-MKUP-RTL-PCT)                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               ADD +1                     TO PA-INSERT-ITM-CNT                  
               INITIALIZE PV-PREV-VARIABLES                                     
               MOVE  PV-PROMO-ID          TO  PV-PREV-PROMO-ID                  
               MOVE  PV-PROMO-ITM-ID      TO  PV-PREV-PROMO-ITM-ID              
               MOVE  PV-PROMO-SLS-TYP-CDE TO  PV-PREV-PROMO-SLS-TYP-CDE         
               MOVE  PV-MDSE-SELN-ID      TO  PV-PREV-MDSE-SELN-ID              
                                                                                
             WHEN SQLCODE = -00001                                              
               PERFORM C165-UPDATE-ITM-TBL                                      
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                       TO PV-SQL                     
               MOVE 'PMTT_PROMO_ITM_SLS_RSLT_LOD' TO PV-TABLE-NAME              
               MOVE 'C155-INSERT-INTO-ITM-TBL'    TO PV-PARAGRAPH-NAME          
               DISPLAY ' SLS DATE           = ' SA046-SLS-DTE                   
               DISPLAY ' STR NBR            = ' SA046-STR-NBR                   
               DISPLAY ' PROMO ID           = ' PV-PROMO-ID                     
               DISPLAY ' PROMO INTFC SUB ID = ' PV-PROMO-INTFC-SUB-ID           
               DISPLAY ' PROMO SLS TYP CDE  = ' PV-PROMO-SLS-TYP-CDE            
               DISPLAY ' SELN LNE ID        = ' PV-MDSE-SELN-LNE-ID             
               DISPLAY ' DEPT NBR           = ' SA046-DEPT-NBR                  
               DISPLAY ' MAJ CL NBR         = ' SA046-MAJ-CL-NBR                
               DISPLAY ' SUB CL NBR         = ' SA046-SUB-CL-NBR                
               DISPLAY ' SKU NBR            = ' SA046-SKU-NBR                   
               DISPLAY ' SLS RTL AMT        = ' PV-SLS-RTL-AMT                  
               DISPLAY ' SLD QTY            = ' SA046-SLD-QTY                   
               DISPLAY ' MKDN RTL AMT       = ' PV-MKDN-RTL-AMT                 
               DISPLAY ' REG RTL AMT        = ' SA046-REG-RTL-AMT               
               DISPLAY ' UNT COST AMT       = ' SA046-UNT-COST-AMT              
               DISPLAY ' OWND UNT RTL AMT   = ' SA046-OWND-UNT-RTL-AMT          
               DISPLAY ' NET CHARGED AMT    = ' SA046-NET-CHRGD-AMT             
               DISPLAY ' PV SAL RET CDE     = ' PV-SAL-RET-CDE                  
               DISPLAY ' SAL RETURN CDE     = ' SA046-SAL-RET-CDE               
               DISPLAY ' SLS COST AMT       = ' PV-SLS-COST-AMT                 
               DISPLAY ' UNT PROMO RTL AMT  = ' PV-UNT-PROMO-RTL-AMT            
               DISPLAY ' UNT MKDN RTL AMT   = ' PV-UNT-MKDN-RTL-AMT             
               DISPLAY ' UNT MKDN RTL PCT   = ' PV-UNT-MKDN-RTL-PCT             
               DISPLAY ' UNT MKUP RTL PCT   = ' PV-UNT-MKUP-RTL-PCT             
               STRING 'INSERT ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C160-UPDATE-NONITM-TBL                                        *         
      * ==> PROCESSES:                                                *         
      *  - WHEN UPDATING NEED TO EITHER ADD WHAT IS THERE WITH THE    *         
      *    VALUE, INSERT THE MIN VALUE BETWEEN WHAT IS THERE AND THE  *         
      *    NEW VALUE, OR INSERT THE MAX VALUE BETWEEN WHAT IS THERE   *         
      *    AND THE NEW VALUE.                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL                                            *         
      * ==> PERFORMED FROM: C150-INSERT-INTO-NONITM-TBL               *         
      *****************************************************************         
       C160-UPDATE-NONITM-TBL.                                                  
                                                                                
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PMTT_SLS_RSLT_LOD                                            
               SET                                                              
                   SLS_RTL_AMT   = (SLS_RTL_AMT + :PV-SLS-RTL-AMT)              
                  ,SLS_UNT_QTY   = (SLS_UNT_QTY + :SA046-SLD-QTY)               
                  ,MKDN_RTL_AMT  = (MKDN_RTL_AMT + :PV-MKDN-RTL-AMT)            
                  ,NET_CHRGD_AMT =                                              
                     (NET_CHRGD_AMT + :SA046-NET-CHRGD-AMT)                     
                  ,SLS_COST_AMT  = (SLS_COST_AMT + :PV-SLS-COST-AMT)            
                  ,MIN_UNT_PROMO_RTL_AMT =                                      
                     (LEAST(MIN_UNT_PROMO_RTL_AMT,                              
                                  :PV-UNT-PROMO-RTL-AMT))                       
                  ,MAX_UNT_PROMO_RTL_AMT =                                      
                     (GREATEST(MAX_UNT_PROMO_RTL_AMT,                           
                                  :PV-UNT-PROMO-RTL-AMT))                       
                  ,UNT_MKDN_MIN_RTL_AMT =                                       
                     (LEAST(UNT_MKDN_MIN_RTL_AMT,                               
                                  :PV-UNT-MKDN-RTL-AMT))                        
                  ,UNT_MKDN_MAX_RTL_AMT =                                       
                     (GREATEST(UNT_MKDN_MAX_RTL_AMT,                            
                                  :PV-UNT-MKDN-RTL-AMT))                        
                  ,UNT_MKDN_MIN_RTL_PCT =                                       
                     (LEAST(UNT_MKDN_MIN_RTL_PCT,                               
                                  :PV-UNT-MKDN-RTL-PCT))                        
                  ,UNT_MKDN_MAX_RTL_PCT =                                       
                     (GREATEST(UNT_MKDN_MAX_RTL_PCT,                            
                                  :PV-UNT-MKDN-RTL-PCT))                        
                  ,UNT_MKUP_MIN_RTL_PCT =                                       
                     (LEAST(UNT_MKUP_MIN_RTL_PCT,                               
                                  :PV-UNT-MKUP-RTL-PCT))                        
                  ,UNT_MKUP_MAX_RTL_PCT =                                       
                      (GREATEST(UNT_MKUP_MAX_RTL_PCT,                           
                                  :PV-UNT-MKUP-RTL-PCT))                        
                 WHERE                                                          
                       TRN_SLS_DTE =                                            
                       TO_DATE(:SA046-SLS-DTE, 'YYYY-MM-DD')                    
                   AND STR_NBR = :SA046-STR-NBR                                 
                   AND PROMO_ID = :PV-PROMO-ID                                  
                   AND PROMO_SLS_TYP_CDE = :PV-PROMO-SLS-TYP-CDE                
                   AND SLS_TRN_TYP_CDE = :PV-SAL-RET-CDE                        
                   AND MDSE_SELN_LNE_ID = :PV-MDSE-SELN-LNE-ID                  
                   AND SKU_NBR = :SA046-SKU-NBR                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               ADD +1                     TO PA-UPDATE-NONITM-CNT               
                                                                                
             WHEN OTHER                                                         
               MOVE 'PMTT_SLS_RSLT_LOD'      TO PV-TABLE-NAME                   
               MOVE 'C160-UPDATE-NONITM-TBL' TO PV-PARAGRAPH-NAME               
               DISPLAY 'SALES DATE        = ' SA046-SLS-DTE                     
               DISPLAY 'STORE NBR         = ' SA046-STR-NBR                     
               DISPLAY 'PROMO INTFC SUB ID = '                                  
                   SA046-PM-PROMO-INTFC-ID                                      
               DISPLAY 'PROMO SLS TYP CDE = ' PV-PROMO-SLS-TYP-CDE              
               DISPLAY 'PV SAL RET CDE    = ' PV-SAL-RET-CDE                    
               DISPLAY 'SLS TRN TYP CDE   = ' SA046-SAL-RET-CDE                 
               DISPLAY 'PROMO ID          = ' PV-PROMO-ID                       
               DISPLAY 'MDSE SELN ID      = ' PV-MDSE-SELN-ID                   
               DISPLAY 'SKU NBR           = ' SA046-SKU-NBR                     
               MOVE SQLCODE                  TO PV-SQL                          
               STRING 'UPDATE ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C165-UPDATE-ITM-TBL                                           *         
      * ==> PROCESSES:                                                *         
      *  - WHEN UPDATING NEED TO EITHER ADD WHAT IS THERE WITH THE    *         
      *    VALUE, INSERT THE MIN VALUE BETWEEN WHAT IS THERE AND THE  *         
      *    NEW VALUE, OR INSERT THE MAX VALUE BETWEEN WHAT IS THERE   *         
      *    AND THE NEW VALUE.                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL                                            *         
      * ==> PERFORMED FROM: C155-INSERT-INTO-ITM-TBL                  *         
      *****************************************************************         
       C165-UPDATE-ITM-TBL.                                                     
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PMTT_PROMO_ITM_SLS_RSLT_LOD                                  
               SET                                                              
                   SLS_RTL_AMT   = (SLS_RTL_AMT + :PV-SLS-RTL-AMT)              
                  ,SLS_UNT_QTY   = (SLS_UNT_QTY + :SA046-SLD-QTY)               
                  ,MKDN_RTL_AMT  = (MKDN_RTL_AMT + :PV-MKDN-RTL-AMT)            
                  ,NET_CHRGD_AMT =                                              
                     (NET_CHRGD_AMT + :SA046-NET-CHRGD-AMT)                     
                  ,SLS_COST_AMT  = (SLS_COST_AMT + :PV-SLS-COST-AMT)            
                  ,MIN_UNT_PROMO_RTL_AMT =                                      
                     (LEAST(MIN_UNT_PROMO_RTL_AMT,                              
                                  :PV-UNT-PROMO-RTL-AMT))                       
                  ,MAX_UNT_PROMO_RTL_AMT =                                      
                     (GREATEST(MAX_UNT_PROMO_RTL_AMT,                           
                                  :PV-UNT-PROMO-RTL-AMT))                       
                  ,UNT_MKDN_MIN_RTL_AMT =                                       
                     (LEAST(UNT_MKDN_MIN_RTL_AMT,                               
                                  :PV-UNT-MKDN-RTL-AMT))                        
                  ,UNT_MKDN_MAX_RTL_AMT =                                       
                     (GREATEST(UNT_MKDN_MAX_RTL_AMT,                            
                                  :PV-UNT-MKDN-RTL-AMT))                        
                  ,UNT_MKDN_MIN_RTL_PCT =                                       
                     (LEAST(UNT_MKDN_MIN_RTL_PCT,                               
                                  :PV-UNT-MKDN-RTL-PCT))                        
                  ,UNT_MKDN_MAX_RTL_PCT =                                       
                     (GREATEST(UNT_MKDN_MAX_RTL_PCT,                            
                                  :PV-UNT-MKDN-RTL-PCT))                        
                  ,UNT_MKUP_MIN_RTL_PCT =                                       
                     (LEAST(UNT_MKUP_MIN_RTL_PCT,                               
                                  :PV-UNT-MKUP-RTL-PCT))                        
                  ,UNT_MKUP_MAX_RTL_PCT =                                       
                      (GREATEST(UNT_MKUP_MAX_RTL_PCT,                           
                                  :PV-UNT-MKUP-RTL-PCT))                        
               WHERE                                                            
                       TRN_SLS_DTE =                                            
                       TO_DATE(:SA046-SLS-DTE, 'YYYY-MM-DD')                    
                   AND STR_NBR = :SA046-STR-NBR                                 
                   AND PROMO_ID = :PV-PROMO-ID                                  
                   AND PROMO_ITM_ID = :PV-PROMO-ITM-ID                          
                   AND PROMO_SLS_TYP_CDE = :PV-PROMO-SLS-TYP-CDE                
                   AND SLS_TRN_TYP_CDE = :PV-SAL-RET-CDE                        
                   AND MDSE_SELN_ID = :PV-MDSE-SELN-ID                          
                   AND SKU_NBR = :SA046-SKU-NBR                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               ADD +1                     TO PA-UPDATE-ITM-CNT                  
               INITIALIZE PV-PREV-VARIABLES                                     
               MOVE  PV-PROMO-ID          TO  PV-PREV-PROMO-ID                  
               MOVE  PV-PROMO-ITM-ID      TO  PV-PREV-PROMO-ITM-ID              
               MOVE  PV-PROMO-SLS-TYP-CDE TO  PV-PREV-PROMO-SLS-TYP-CDE         
               MOVE  PV-MDSE-SELN-ID      TO  PV-PREV-MDSE-SELN-ID              
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                       TO PV-SQL                     
               MOVE 'PMTT_PROMO_ITM_SLS_RSLT_LOD' TO PV-TABLE-NAME              
               MOVE 'C165-UPDATE-ITM-TBL'         TO PV-PARAGRAPH-NAME          
               DISPLAY 'SALES DATE        = ' SA046-SLS-DTE                     
               DISPLAY 'STORE NBR         = ' SA046-STR-NBR                     
               DISPLAY 'PROMO INTFC SUB ID = '                                  
                   SA046-PM-PROMO-INTFC-ID                                      
               DISPLAY 'PROMO SLS TYP CDE = ' PV-PROMO-SLS-TYP-CDE              
               DISPLAY 'PV SAL RET CDE    = ' PV-SAL-RET-CDE                    
               DISPLAY 'SLS TRN TYP CDE   = ' SA046-SAL-RET-CDE                 
               DISPLAY 'PROMO ID          = ' PV-PROMO-ID                       
               DISPLAY 'PROMO ITM ID      = ' PV-PROMO-ITM-ID                   
               DISPLAY 'MDSE SELN ID      = ' PV-MDSE-SELN-ID                   
               DISPLAY 'SKU NBR           = ' SA046-SKU-NBR                     
               STRING 'INSERT ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C170-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = COMMIT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C170-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZEROES                                                  
               PERFORM C180-COMMIT-DISPLAYS                                     
               DISPLAY 'ORACLE COMMIT SUCCESSFUL'                               
               ADD +1 TO PA-NBR-OF-COMMITS                                      
           ELSE                                                                 
               MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
               DISPLAY 'ORACLE COMMIT FAILURE'                                  
               MOVE 'C170-COMMIT'               TO PV-PARAGRAPH-NAME            
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE 1 TO PV-COMMIT-CNTR.                                            
                                                                                
CPR        MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
|             IF PV-COMMIT-CNTR < PV-COMMIT-INTERVAL                            
|                 SUBTRACT 1 FROM PV-COMMIT-CNTR                                
|                 ADD PV-COMMIT-CNTR TO CR-ACT-READ-CNT                         
|             END-IF.                                                           
|                                                                               
|          IF PS-FIRST-COMMIT                                                   
|              MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                     
|              PERFORM B104-UPDATE-CHECKPOINT-STATUS                            
|              MOVE 'N'        TO PS-FIRST-COMMIT-SW                            
|          END-IF.                                                              
|                                                                               
|          PERFORM B105-UPDATE-RESTART-INFOR.                                   
|                                                                               
|          IF PS-SALES-RESULT-FILE-END                                          
|              MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE                            
|              PERFORM B104-UPDATE-CHECKPOINT-STATUS                            
CPR        END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
067400                                                                          
067500     IF SQLCODE = ZEROES                                                  
               DISPLAY 'CHKPOINT RESTART DB COMMIT SUCCESSFUL'                  
               ADD +1 TO PA-NBR-OF-COMMITS                                      
067700     ELSE                                                                 
067800         MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
               DISPLAY 'CHECKPOINT RESTART DB COMMIT FAILURE'                   
067900         MOVE 'C170-COMMIT'               TO PV-PARAGRAPH-NAME            
068000         PERFORM Z910-ABEND                                               
068100     END-IF.                                                              
068200                                                                          
CPR        MOVE 1 TO PV-COMMIT-CNTR.                                            
CPR                                                                             
      *****************************************************************         
      * C180-COMMIT-DISPLAYS                                          *         
      *****************************************************************         
       C180-COMMIT-DISPLAYS.                                                    
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**********************************************'.            
CPR        DISPLAY 'CURRENT COMMITTED RECORD CNT = ' CR-ACT-READ-CNT.           
           DISPLAY ' '.                                                         
           DISPLAY 'WE HAVE COMMITTED THIS MANY RECORDS.'.                      
CPR        DISPLAY 'IF PROGRAM ABENDS, RECORDS MENTIONED ABOVE'.                
|          DISPLAY 'WILL BE SKIPPED AUTOMATICALLY AND REMAINING'.               
|          DISPLAY 'RECORDS IN THE INPUT FILE WILL BE PROCESSED!'.              
CPR   *    DISPLAY 'SEE TKMA.PM.PROMO.JOBLIB(CHOPFILE) FOR JCL'.                
|          DISPLAY ' '.                                                         
|     *    DISPLAY 'IF THERE IS NO DISPLAY OF CURRENT COMMITTED '.              
|     *    DISPLAY 'RECORD COUNT, NOTHING HAS BEEN COMMITTED. '.                
|     *    DISPLAY 'YOU CAN JUST RESTART THE JOB - THE FILE '.                  
|     *    DISPLAY 'DOES NOT NEED TO BE MANIPULATED.          '.                
|     *    DISPLAY ' '.                                                         
|     *    DISPLAY 'IF YOU NEED TO DELETE RECORDS PER THE CURRENT '.            
|     *    DISPLAY 'COMMITTED RECORD CNT, THE FOLLOWING RECORD SHOULD '.        
|     *    DISPLAY 'BE THE LAST RECORD YOU DELETED FROM THE FILE.'.             
|     *    DISPLAY '(UNLESS READ-COUNTER IS ZERO)  '                            
|     *    DISPLAY ' SA046-STR-NBR         = ' SA046-STR-NBR.                   
|     *    DISPLAY ' SA046-RGST-ID         = ' SA046-RGST-ID.                   
|     *    DISPLAY ' SA046-TRN-NBR         = ' SA046-TRN-NBR.                   
|     *    DISPLAY ' SA046-SLS-DTE         = ' SA046-SLS-DTE.                   
|     *    DISPLAY ' SA046-SKU-NBR         = ' SA046-SKU-NBR.                   
|     *    DISPLAY ' SA046-DEPT-NBR        = ' SA046-DEPT-NBR.                  
|     *    DISPLAY ' SA046-MAJ-CL-NBR      = ' SA046-MAJ-CL-NBR.                
|     *    DISPLAY ' SA046-SUB-CL-NBR      = ' SA046-SUB-CL-NBR.                
|     *    DISPLAY ' SA046-ITM-STAT-CDE    = ' SA046-ITM-STAT-CDE.              
|     *    DISPLAY ' SA046-LNE-ITM-SEQ-NBR = ' SA046-LNE-ITM-SEQ-NBR.           
|     *    DISPLAY ' SA046-LIV-RSLU-CDE    = ' SA046-LIV-RSLU-CDE.              
|     *    DISPLAY ' SA046-SAL-RET-CDE     = ' SA046-SAL-RET-CDE.               
|     *    DISPLAY '**********************************************'.            
CPR   *    DISPLAY ' '.                                                         
                                                                                
      *****************************************************************         
      * D100-CALC-INSERT-CONTROL                                      *         
      * ==> PROCESSES:                                                *         
      *  - PERFORM CALCULATE PARAGRAPH                                *         
      *  - DETERMINE WHICH INSERT PARAGRAPH TO USE.                   *         
      *  - ONLY INSERT IF THERE IS A ROW TO INSERT.                   *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       D100-CALC-INSERT-CONTROL.                                                
                                                                                
           SET PS-B-FOR-A TO TRUE.                                              
           IF PS-INSERT-RECORD                                                  
               PERFORM D110-CALCULATE                                           
               IF PS-B-FOR-A                                                    
                   IF PV-PROMO-ITM-ID > 0                                       
                       PERFORM C155-INSERT-INTO-ITM-TBL                         
                   ELSE                                                         
                       PERFORM C150-INSERT-INTO-NONITM-TBL                      
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
      *IF SKIPPING A RECORD NEED TO SEE IF IT IS AN A.  IF IT IS THEN           
      *WE WANT TO SKIP THE B RECORD AS WELL.                                    
               IF SA046-SAL-RET-CDE = PC-ADJ-SALE                               
                   PERFORM B350-READ-SALES-RESULT-FILE                          
                   IF (SA046-SAL-RET-CDE = PC-ADJ-RET)                          
                       SET PS-B-FOR-A TO TRUE                                   
                   ELSE                                                         
                       SET PS-NO-B-FOR-A TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * D110-CALCULATE                                                *         
      * ==> PROCESSES:                                                *         
      *  - IF THE ROW IS NOT TIERED THEN COMPUTE ALL THE DIFFERENT    *         
      *    COLUMNS.  IF THE ROW IS TIERED ON SALE THEN MOVE SOME OF   *         
      *    THE PREVIOUS SELECT VARIABLES INTO THE CURRENT VARIABLES.  *         
      *    ALSO SUBTRACT TIERED AMOUNT TO GET MKDN-RTL-AMT INSTEAD OF *         
      *    SLS-RTL-AMT.  IF THE ROW IS TIERED AND NOT ON SALE THEN    *         
      *    MOVE 0 TO THE MKDN-RTL-AMT.                                *         
      * ==> PERFORMED FROM: D100-CALC-INSERT-CONTROL                  *         
      *****************************************************************         
       D110-CALCULATE.                                                          
                                                                                
           MOVE SA046-TIERED-MKDN-AMT TO PV-TIERED-MKDN-AMT.                    
           MOVE SA046-BOGO-PRORTD-MKDN-AMT TO PV-BOGO-PRORTD-MKDN-AMT.          
                                                                                
           IF PV-TIERED-MKDN-AMT < 0                                            
              COMPUTE PV-TIERED-MKDN-AMT =                                      
                  (PV-TIERED-MKDN-AMT * -1)                                     
           END-IF.                                                              
                                                                                
           IF PV-BOGO-PRORTD-MKDN-AMT < 0                                       
              COMPUTE PV-BOGO-PRORTD-MKDN-AMT =                                 
                  (PV-BOGO-PRORTD-MKDN-AMT * -1)                                
           END-IF.                                                              
                                                                                
           IF (SA046-SAL-RET-CDE = PC-SALE)                                     
              AND (SA046-LIV-RSLU-CDE = '-')                                    
               MULTIPLY -1 BY SA046-SLD-QTY                                     
               MULTIPLY -1 BY SA046-CUST-CHRGD-AMT                              
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
             WHEN PS-NOT-TIERED                                                 
                                                                                
                 COMPUTE PV-SLS-RTL-AMT =                                       
                  (SA046-CUST-CHRGD-AMT - PV-TIERED-MKDN-AMT -                  
                   PV-BOGO-PRORTD-MKDN-AMT)                                     
                 END-COMPUTE                                                    
                                                                                
                 COMPUTE PV-SLS-COST-AMT =                                      
                  (SA046-UNT-COST-AMT * SA046-SLD-QTY)                          
                 END-COMPUTE                                                    
                                                                                
                 IF SA046-SLD-QTY NOT = 0                                       
                     COMPUTE PV-UNT-PROMO-RTL-AMT =                             
                      (PV-SLS-RTL-AMT / SA046-SLD-QTY)                          
                       ON SIZE ERROR MOVE 0 TO PV-UNT-PROMO-RTL-AMT             
                     END-COMPUTE                                                
                     COMPUTE PV-UNT-MKDN-RTL-PCT-CALC =                         
                      (PV-SLS-RTL-AMT /                                         
                      (SA046-OWND-UNT-RTL-AMT * SA046-SLD-QTY))                 
                       ON SIZE ERROR MOVE 0 TO PV-UNT-MKDN-RTL-PCT-CALC         
                     END-COMPUTE                                                
                 ELSE                                                           
                     MOVE 0 TO PV-UNT-PROMO-RTL-AMT                             
                     MOVE 0 TO PV-UNT-MKDN-RTL-PCT-CALC                         
                 END-IF                                                         
                                                                                
      *** DON'T WANT UNIT PROMO RETAIL AMOUNT TO BE A NEGATIVE NUMBER.          
                 IF PV-UNT-PROMO-RTL-AMT < 0                                    
                     COMPUTE PV-UNT-PROMO-RTL-AMT =                             
                      (-1 * PV-UNT-PROMO-RTL-AMT)                               
                     END-COMPUTE                                                
                 END-IF                                                         
                                                                                
                 COMPUTE PV-UNT-MKDN-RTL-PCT ROUNDED =                          
                  ((1 - PV-UNT-MKDN-RTL-PCT-CALC) * 100)                        
                 END-COMPUTE                                                    
                                                                                
                 IF PV-SLS-RTL-AMT NOT = 0                                      
                     COMPUTE PV-UNT-MKUP-RTL-PCT ROUNDED =                      
                      (((PV-SLS-RTL-AMT - PV-SLS-COST-AMT)                      
                           / PV-SLS-RTL-AMT) * 100)                             
                       ON SIZE ERROR MOVE 0 TO PV-UNT-MKUP-RTL-PCT              
                     END-COMPUTE                                                
                 ELSE                                                           
                     MOVE 0 TO PV-UNT-MKUP-RTL-PCT                              
                 END-IF                                                         
                                                                                
                 IF SA046-SAL-RET-CDE = PC-ADJ-SALE                             
                     PERFORM D120-PROCESS-B-REC                                 
                 ELSE                                                           
                     COMPUTE PV-MKDN-RTL-AMT =                                  
                      ((SA046-OWND-UNT-RTL-AMT * SA046-SLD-QTY) -               
                      PV-SLS-RTL-AMT)                                           
                     END-COMPUTE                                                
                                                                                
                     IF SA046-SLD-QTY NOT = 0                                   
                         COMPUTE PV-UNT-MKDN-RTL-AMT =                          
                          (PV-MKDN-RTL-AMT / SA046-SLD-QTY)                     
                           ON SIZE ERROR MOVE 0 TO PV-UNT-MKDN-RTL-AMT          
                         END-COMPUTE                                            
                     ELSE                                                       
                         MOVE 0 TO PV-UNT-MKDN-RTL-AMT                          
                     END-IF                                                     
                 END-IF                                                         
                                                                                
             WHEN PS-TIERED-ON-SALE                                             
                 MOVE PV-PREV-PROMO-ID          TO PV-PROMO-ID                  
                 MOVE PV-PREV-PROMO-ITM-ID      TO PV-PROMO-ITM-ID              
                 MOVE PV-PREV-MDSE-SELN-ID      TO PV-MDSE-SELN-ID              
                 MOVE SA046-TIERED-INTFC-SUB-ID TO                              
                                              PV-PROMO-INTFC-SUB-ID             
                                                                                
                 MOVE PV-TIERED-MKDN-AMT        TO PV-MKDN-RTL-AMT              
                                                                                
             WHEN PS-TIERED-NOT-ON-SALE                                         
                 MOVE SA046-TIERED-INTFC-SUB-ID TO                              
                                              PV-PROMO-INTFC-SUB-ID             
                                                                                
                 IF SA046-SAL-RET-CDE = PC-ADJ-SALE                             
                     PERFORM D120-PROCESS-B-REC                                 
                 ELSE                                                           
                     MOVE PV-TIERED-MKDN-AMT        TO PV-MKDN-RTL-AMT          
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D120-PROCESS-B-REC                                            *         
      * ==> PROCESSES:                                                *         
      *  - IF THE CURRENT RECORD IS AN A THEN MAKE SURE THE NEXT      *         
      *    RECORD IS A B.  IF NOT THEN SKIP THE CURRENT OTHER RECORD. *         
      *    OTHERWISE PROCESS BOTH RECORDS AT THE SAME TIME.           *         
      * ==> PERFORMED FROM: D110-CALCULATE                            *         
      *****************************************************************         
       D120-PROCESS-B-REC.                                                      
                                                                                
           MOVE SA046-PM-SALES-RESULTS-REC TO                                   
            PV-HOLD-SALES-RESULTS-REC.                                          
                                                                                
           PERFORM B350-READ-SALES-RESULT-FILE.                                 
                                                                                
           IF (SA046-SAL-RET-CDE = PC-ADJ-RET)                                  
               IF ((PV-HOLD-CUST-CHRGD-AMT >  PV-HOLD-OWND-UNT-RTL-AMT)         
                  AND (SA046-CUST-CHRGD-AMT <= PV-HOLD-CUST-CHRGD-AMT))         
                                                                                
                   COMPUTE  PV-MKDN-RTL-AMT ROUNDED =                           
                    ((SA046-CUST-CHRGD-AMT * -1) -                              
                    PV-HOLD-OWND-UNT-RTL-AMT)                                   
                                                                                
                   MOVE 0 TO PV-UNT-MKDN-RTL-AMT                                
               ELSE                                                             
                   IF (PV-HOLD-CUST-CHRGD-AMT <=                                
                       PV-HOLD-OWND-UNT-RTL-AMT)                                
                                                                                
                       COMPUTE PV-MKDN-RTL-AMT ROUNDED =                        
                        ((SA046-CUST-CHRGD-AMT * -1) -                          
                        PV-HOLD-CUST-CHRGD-AMT)                                 
                       END-COMPUTE                                              
                                                                                
                       IF SA046-SLD-QTY NOT = 0                                 
                           COMPUTE PV-UNT-MKDN-RTL-AMT =                        
                            ((PV-HOLD-OWND-UNT-RTL-AMT -                        
                            PV-HOLD-CUST-CHRGD-AMT) / SA046-SLD-QTY)            
                            ON SIZE ERROR MOVE 0 TO PV-UNT-MKDN-RTL-AMT         
                           END-COMPUTE                                          
                       ELSE                                                     
                           MOVE 0 TO PV-UNT-MKDN-RTL-AMT                        
                       END-IF                                                   
                   ELSE                                                         
                       MOVE 0 TO PV-MKDN-RTL-AMT                                
                       MOVE 0 TO PV-UNT-MKDN-RTL-AMT                            
                   END-IF                                                       
               END-IF                                                           
               MOVE 0 TO SA046-SLD-QTY                                          
           ELSE                                                                 
               PERFORM E100-ERROR-LOGIC                                         
           END-IF.                                                              
           MOVE 0 TO PV-SLS-RTL-AMT.                                            
           MOVE 0 TO PV-SLS-COST-AMT.                                           
                                                                                
      *****************************************************************         
      * E100-ERROR-LOGIC                                              *         
      * ==> PROCESSES:                                                *         
      *  - ERROR LOGIC FOR WHEN THE RECORD AFTER THE ADJUSTED SALE    *         
      *    IS NOT AN ADJUSTED RETURN.                                 *         
      * ==> PERFORMED FROM: D120-PROCESS-B-REC                        *         
      *****************************************************************         
       E100-ERROR-LOGIC.                                                        
                                                                                
           DISPLAY 'A - STR-NBR         = ' PV-HOLD-STR-NBR.                    
           DISPLAY 'A - RGST-ID         = ' PV-HOLD-RGST-ID.                    
           DISPLAY 'A - TRN-NBR         = ' PV-HOLD-TRN-NBR.                    
           DISPLAY 'A - SLS-DTE         = ' PV-HOLD-SLS-DTE.                    
           DISPLAY 'A - SKU-NBR         = ' PV-HOLD-SKU-NBR.                    
           DISPLAY 'A - DEPT-NBR        = ' PV-HOLD-DEPT-NBR.                   
           DISPLAY 'A - MAJ-CL-NBR      = ' PV-HOLD-MAJ-CL-NBR.                 
           DISPLAY 'A - SUB-CL-NBR      = ' PV-HOLD-SUB-CL-NBR.                 
           DISPLAY 'A - ITM-STAT-CDE    = ' PV-HOLD-ITM-STAT-CDE.               
           DISPLAY 'A - LNE-ITM-SEQ-NBR = ' PV-HOLD-LNE-ITM-SEQ-NBR.            
           DISPLAY 'A - LIV-RSLU-CDE    = ' PV-HOLD-LIV-RSLU-CDE.               
           DISPLAY 'A - SAL-RET-CDE     = ' PV-HOLD-SAL-RET-CDE.                
           DISPLAY 'A -PROMO INTFC SUB ID = ' PV-HOLD-PM-PROMO-INTFC-ID         
           DISPLAY 'RECORD NUMBER       = ' PA-READ-COUNTER.                    
                                                                                
           SET PS-NO-B-FOR-A TO TRUE.                                           
           DISPLAY 'SKIP RECORD - "A" SALE ADJUSTMENT RECORD DID '.             
           DISPLAY 'NOT HAVE MATCHING "B" RETURN ADJUSTMENT'.                   
                                                                                
      *****************************************************************         
      * E200-CPU-TIME                                                 *         
      * ==> PROCESSES:                                                *         
      * ==> PERFORMED FROM:                                           *         
      *****************************************************************         
       E200-CPU-TIME.                                                           
                                                                                
           ACCEPT CT-CPU-TIME FROM TIME.                                        
           MOVE CT-CPU-HR     TO  PV-CPU-HR.                                    
           MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                   
           MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                   
           MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                  
           DISPLAY 'TIME ELAPSED = ' PV-CPU-TIME.                               
           DISPLAY 'RECORDS READ = ' PA-READ-COUNTER.                           
           DISPLAY 'C110- GROUP SQL COUNTER      = '                            
                    PA-C110-GRP-SQL-COUNTER.                                    
           DISPLAY 'C110- GROUP SKIP SQL COUNTER = '                            
                    PA-GRP-SKIP-SQL-COUNTER.                                    
           DISPLAY 'C110- BOGO SQL COUNTER       = '                            
                    PA-C110-BOGO-SQL-COUNTER.                                   
           DISPLAY 'C110- BOGO SKIP SQL COUNTER  = '                            
                    PA-BOGO-SKIP-SQL-COUNTER.                                   
           DISPLAY 'C115- SQL COUNTER            = '                            
                    PA-C115-SQL-COUNTER.                                        
           DISPLAY '****************************************'.                  
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      ******************************************************************        
      * NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
      * OTHERWISE GET AN ORACLE ERROR (IE: 1722 (INVALID NUMBER)).              
      ******************************************************************        
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET CURSOR_SHARING = 'SIMILAR'                                
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET "_OPTIM_PEEK_USER_BINDS" = FALSE                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * F100-ABEND-DISPLAYS.                                          *         
      *  - DISPLAYS ABEND INFORMATION ABOUT CURRENT RECORD.           *         
      *****************************************************************         
       F100-ABEND-DISPLAYS.                                                     
                                                                                
           DISPLAY '**********************************************'.            
           DISPLAY '!!!!!!!!! RESTART INSTRUCTIONS !!!!!!!!!'.                  
           DISPLAY '1.  FIND THE LAST CURRENT COMMMITTED RECORD COUNT'.         
           DISPLAY '    DISPLAY IN THIS 4.S OUTPUT (PAGE UP).'.                 
           DISPLAY '2.  IF THERE IS NO DISPLAY LIKE THIS, THAT MEANS'.          
           DISPLAY '    NOTHING HAS BEEN COMMITTED.  YOU CAN JUST'.             
           DISPLAY '    RESTART THE JOB - THE INPUT FILE DOES NOT'.             
           DISPLAY '    NEED TO BE MANIPULATED.'.                               
CPR        DISPLAY '3.  IF YOU FIND THAT TEXT, THEN THE RECORDS.'.              
|          DISPLAY '    MENTIONED IN THE CURRENT COMMITTED RECORD'.             
|          DISPLAY '    COUNT WILL BE SKIPPED AND THE REMAINING .'.             
|          DISPLAY '    RECORDS WILL BE PROCESSED WHILE RESTARTING'.            
|          DISPLAY '    THE JOB.'.                                              
|     *    DISPLAY '6.  LOCATE (L) THE MEMBER NAMED CHOPFILE.'.                 
|     *    DISPLAY '7.  EDIT (E) THIS MEMBER - FOLLOW INSTRUCTIONS'.            
CPR   *    DISPLAY '    IN THAT JOB.'.                                          
           DISPLAY ' '.                                                         
           DISPLAY 'CURRENT RECORD INFORMATION BELOW: '                         
           DISPLAY ' SA046-STR-NBR         = ' SA046-STR-NBR.                   
           DISPLAY ' SA046-RGST-ID         = ' SA046-RGST-ID.                   
           DISPLAY ' SA046-TRN-NBR         = ' SA046-TRN-NBR.                   
           DISPLAY ' SA046-SLS-DTE         = ' SA046-SLS-DTE.                   
           DISPLAY ' SA046-SKU-NBR         = ' SA046-SKU-NBR.                   
           DISPLAY ' SA046-DEPT-NBR        = ' SA046-DEPT-NBR.                  
           DISPLAY ' SA046-MAJ-CL-NBR      = ' SA046-MAJ-CL-NBR.                
           DISPLAY ' SA046-SUB-CL-NBR      = ' SA046-SUB-CL-NBR.                
           DISPLAY ' SA046-ITM-STAT-CDE    = ' SA046-ITM-STAT-CDE.              
           DISPLAY ' SA046-LNE-ITM-SEQ-NBR = ' SA046-LNE-ITM-SEQ-NBR.           
           DISPLAY ' SA046-LIV-RSLU-CDE    = ' SA046-LIV-RSLU-CDE.              
           DISPLAY ' SA046-SAL-RET-CDE     = ' SA046-SAL-RET-CDE.               
           DISPLAY '**********************************************'.            
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '** RECORDS READ   ** = ' PA-READ-COUNTER.                   
           DISPLAY '                       '.                                   
                                                                                
           PERFORM F100-ABEND-DISPLAYS.                                         
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '** RECORDS READ    ** = ' PA-READ-COUNTER.                  
                                                                                
           PERFORM F100-ABEND-DISPLAYS.                                         
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
