       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT440.                                                 
       AUTHOR.        SCOTT JACKSON.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-08-1999.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      * MLKUT440 - DESCRIPTION.............                                     
      *                                                                         
      * THIS PROGRAM WILL PROCESS DEPT 099-10 TRANSACTIONS FOR REVERSAL.        
      *                                                                         
      * AN INPUT CONTROL CARD WILL BE READ TO DETERMINE IF AN OUTPUT            
      * FILE IS TO BE PRODUCED. THIS WILL OCCUR ONLY IF THE CC CONTAINS         
      * THE CURRENT DATE AND THE FLAG IS SET TO A 'Y'.                          
      *                                                                         
      * ALL GENERATIONS THAT ARE CREATED TO CONTAIN 09910 TRANSACTIONS          
      * (FROM  MLK133) ARE BROUGHT IN AND PROCESSED AS FOLLOWS....              
      *                                                                         
      *     SALES   -   REVERSE SIGN ON ML020-SALE-EXTD-RTL-AMT         00200000
      *   MARKDOWN  -   EXCHANGE VALUES FOR PC-OLD-PR-AMT/PC-NEW-PR-AMT 00210000
      *    MARKUP   -   EXCHANGE VALUES FOR PC-OLD-PR-AMT/PC-NEW-PR-AMT 00220000
      *   MARKDOWNS AND MARKUPS WILL HAVE THEIR BATCH NMBR CHG TO '794' 00230000
      *                                                                 00240000
      *CHANGE HISTORY:                                                  00241021
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00242021
      * ------- ---------- ----------- -------------------------------- 00243021
      *   OUTPUT FORMAT IS IN MLRD020 FORMAT                            00250000
      * 10/26/99  AML  CONTROL CARD IS NO LONGER BEING USED TO CONTROL  00250117
      *                PROCESSING.                                      00250217
442415* 01/10/2002 RWJ SB37 ABEND ON THE OUPUT FILE. REMOVE SOME        00250919
442415*                DISPLAYS FROM THE PROGRAM.                       00251019
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00252021
W29145* W29145  01-05-2005 V. LEE YANG - FIX TRUNCATION WITH EXTENDED   00253023
      *                    REVERSAL AMOUNT                              00254023
TA4654* PKE4654 08-05-2010 THASLIM ARIFF - FOR SALES TRANSACTIONS,              
TA4654*     ML020-SALE-DTE IS FORCED TO HAVE FISCAL MONTH END DATE.             
TA4654*     INCLUDED TMLCNTL TABLE FOR GETTING FISCAL MONTH END DATE.           
PM3403* PMO3403 04-20-2012 S.JACKSON  LARGE RETAIL MLRD020 RECOMPILE            
      ******************************************************************00260000
                                                                        00270021
       ENVIRONMENT DIVISION.                                            00290000
       CONFIGURATION SECTION.                                           00310000
       SOURCE-COMPUTER.        IBM-3090.                                00330000
       OBJECT-COMPUTER.        IBM-3090.                                00340000
       INPUT-OUTPUT SECTION.                                            00360000
       FILE-CONTROL.                                                    00370000
                                                                        00380000
           SELECT CONTROL-DATE-FILE                                     00390004
               ASSIGN TO INP01.                                         00400000
                                                                        00410000
           SELECT CONTROL-CARD-FILE                                     00420004
               ASSIGN TO INP02.                                         00430004
                                                                        00440004
           SELECT TOPTEN-SALES-FILE                                     00450004
               ASSIGN TO INP03.                                         00460004
                                                                        00470000
           SELECT REVERSAL-TRANSACTION-FILE                             00480004
               ASSIGN TO INP04.                                         00490004
                                                                        00500004
           SELECT REVERSAL-OUTPUT-FILE                                  00510000
               ASSIGN TO OUT01.                                         00520000
                                                                        00530000
      ******************************************************************00540000
       DATA DIVISION.                                                   00550000
      ******************************************************************00560000
                                                                        00570000
       FILE SECTION.                                                    00580000
                                                                        00590000
       FD  CONTROL-DATE-FILE                                            00600008
           RECORDING MODE IS F                                          00610000
           LABEL RECORDS ARE STANDARD                                   00620000
           BLOCK CONTAINS 0 RECORDS.                                    00630000
      *                                                                 00640000
       01  CONTROL-DATE-RECORD         PIC X(80).                       00650004
                                                                        00660000
       FD  CONTROL-CARD-FILE                                            00670008
           RECORDING MODE IS F                                          00680004
           LABEL RECORDS ARE STANDARD                                   00690004
           BLOCK CONTAINS 0 RECORDS.                                    00700004
      *                                                                 00710004
       01  CONTROL-CARD-RECORD         PIC X(80).                       00720004
                                                                        00730004
       FD  TOPTEN-SALES-FILE                                            00740008
           RECORDING MODE IS F                                          00750004
           LABEL RECORDS ARE STANDARD                                   00760004
           BLOCK CONTAINS 0 RECORDS.                                    00770004
      *                                                                 00780004
       01  TOPTEN-SALES-RECORD         PIC X(80).                       00790004
                                                                        00800004
       FD  REVERSAL-TRANSACTION-FILE                                    00810008
           RECORDING MODE IS F                                          00820000
           LABEL RECORDS ARE STANDARD                                   00830000
           BLOCK CONTAINS 0 RECORDS.                                    00840000
      *                                                                 00850000
W26603 01  REVERSAL-INPUT-RECORD       PIC X(175).                      00860022
                                                                        00870000
       FD  REVERSAL-OUTPUT-FILE                                         00880000
           RECORDING MODE IS F                                          00890000
           LABEL RECORDS ARE STANDARD                                   00900000
           BLOCK CONTAINS 0 RECORDS.                                    00910000
      *                                                                 00920000
W26603 01  REVERSAL-OUTPUT-RECORD      PIC X(175).                      00930022
                                                                        00940000
      ******************************************************************00960000
       WORKING-STORAGE SECTION.                                         00970000
      ******************************************************************00980000
      *                                                                 00990000
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     01000008
                                                           COMP SYNC.   01010008
                                                                        01020008
       01  CC1-JOBTRAC-DATE.                                            01030008
           05  CC1-JTRAC-DTE         PIC X(10)          VALUE SPACES.   01040008
           05  FILLER                PIC X(70)          VALUE SPACES.   01050004
      *                                                                 01060004
       01  CC2-PROCESS-REC.                                             01070004
           05  CC2-WEEK-END-DTE      PIC X(10)          VALUE SPACES.   01080004
           05  CC2-CURRENT-DTE       PIC X(10)          VALUE SPACES.   01090004
           05  CC2-NO-PROCESS-IND    PIC X(01)          VALUE 'N'.      01100004
               88  CC2-PROCESS-DEPTSCL                  VALUE 'Y'.      01110004
           05  FILLER                PIC X(59)          VALUE SPACES.   01120004
      *                                                                 01130000
       01  PC-PROGRAM-CONSTANTS.                                        01140007
           05  PC-REVERSAL-BATCH     PIC X(03)          VALUE '794'.    01150007
           05  PC-JOB-NAME           PIC  X(06)        VALUE 'MLK490'.  01160011
           05  PC-PROGRAM-NAME       PIC  X(08)        VALUE 'MLKUT440'.01170011
      *                                                                 01180007
       01  PS-SWITCHES.                                                 01190007
           05  PS-END-OF-CC1-FILE    PIC X(01)          VALUE 'N'.      01200007
               88  PS-EMPTY-CC1-FILE                    VALUE 'Y'.      01210004
           05  PS-END-OF-CC2-FILE    PIC X(01)          VALUE 'N'.      01220004
               88  PS-EMPTY-CC2-FILE                    VALUE 'Y'.      01230004
           05  PS-UPDATES-REQD       PIC X(01)          VALUE 'N'.      01240004
               88  PS-UPDATE-BATCH-794                  VALUE 'Y'.      01250004
           05  PS-REVERSE-TRANS      PIC X(01)          VALUE 'N'.      01260004
               88  PS-PROCESSING-COMPLETE               VALUE 'Y'.      01270006
           05  PS-BYPASS-SALE-SW     PIC X(01)          VALUE 'N'.      01280014
               88  PS-BYPASS-SALE-REC                   VALUE 'Y'.      01290014
      *                                                                 01300004
       01  PV-PROGRAM-VARIABLES.                                        01310007
W29145     05  PV-REVERSE-OLD-PR-AMT   PIC S9(11)V99     VALUE ZEROES.  01320023
W29145     05  PV-REVERSE-NEW-PR-AMT   PIC S9(11)V99     VALUE ZEROES.  01330023
TA4654     05  PV-PARAGRAPH-NAME       PIC X(30)         VALUE SPACE.           
TA4654     05  PV-DB2-OPER-ATTEMPTED   PIC X(30)         VALUE SPACE.           
TA4654******************************************************************        
TA4654*  TMLCNTL - ML'S DATE CONTROL TABLE.                                     
TA4654******************************************************************        
TA4654     EXEC SQL                                                             
TA4654         INCLUDE TMLCNTL                                                  
TA4654     END-EXEC.                                                            
      *                                                                 01340007
      *                                                                 01350007
      ****************************************************              01360004
      *  SALES TRANSACTIONS FILE LAYOUT INPUT/OUTPUT     *              01370000
      ****************************************************              01380000
           COPY MLRD020.                                                01390000
                                                                        01400000
TA4654****************************************************************          
TA4654*  ERROR MESSAGE AREA FOR DB2                                             
TA4654****************************************************************          
TA4654                                                                          
TA4654     COPY DPWS004.                                                        
TA4654******************************************************************        
TA4654*  COMMUNICATIONS AREA FOR DB2                                   *        
TA4654******************************************************************        
TA4654     EXEC SQL                                                             
TA4654         INCLUDE SQLCA                                                    
TA4654     END-EXEC.                                                            
      *-------------------*                                             01440000
       PROCEDURE DIVISION.                                              01450000
      *-------------------*                                             01460000
                                                                        01470000
       0000-MAIN-MODULE.                                                01480000
                                                                        01490000
           PERFORM 1000-INITIALIZATION.                                 01500000
TA4654     PERFORM 1500-GET-OPN-FSCL-MN.                                        
                                                                        01510004
           PERFORM 2000-PROCESS-REVERSALS                               01520004
               UNTIL PS-PROCESSING-COMPLETE.                            01530006
                                                                        01540006
           STOP RUN.                                                    01550006
      *                                                                 01570000
      ******************************************************************01580000
      * OPEN INPUT AND OUTPUT DATASETS.                                 01590002
      ******************************************************************01600000
       1000-INITIALIZATION.                                             01610000
      *                                                                 01620000
           OPEN  INPUT CONTROL-DATE-FILE                                01630004
                       CONTROL-CARD-FILE                                01640004
                       TOPTEN-SALES-FILE                                01650004
                       REVERSAL-TRANSACTION-FILE                        01660000
                OUTPUT REVERSAL-OUTPUT-FILE.                            01670000
                                                                        01680004
           MOVE 'N' TO PS-END-OF-CC1-FILE                               01690004
                       PS-END-OF-CC2-FILE                               01700004
                       PS-UPDATES-REQD                                  01710004
                       PS-BYPASS-SALE-SW                                01720014
                       PS-REVERSE-TRANS.                                01730014
      *                                                                 01740011
           DISPLAY 'PROGRAM : ', PC-PROGRAM-NAME.                       01750011
      *                                                                 01760011
           PERFORM 4000-PROCESS-CONTROL-CARDS.                          01770006
      *                                                                 01780004
      *                                                                 01790004
TA4654*    *------------------------------------------------------------   CL**5
TA4654*    * GET THE OPEN FISCAL MONTH DATE FROM DB2 TABLE - TMLCNTL       CL**5
TA4654*    *------------------------------------------------------------   CL**5
TA4654 1500-GET-OPN-FSCL-MN.                                                    
TA4654                                                                          
TA4654         EXEC SQL                                                         
TA4654              SELECT                                                      
TA4654                  OPN_FSCL_MN_DTE                                         
TA4654              INTO                                                        
TA4654                 :MLCNTL-OPN-FSCL-MN-DTE                                  
TA4654              FROM                                                        
TA4654                  TMLCNTL                                                 
TA4654         END-EXEC                                                         
TA4654                                                                          
TA4654         EVALUATE TRUE                                                    
TA4654             WHEN SQLCODE        = +000                                   
TA4654             WHEN SQLCODE        = -904                                   
TA4654             WHEN SQLCODE        = -911                                   
TA4654                 CONTINUE                                                 
TA4654             WHEN OTHER                                                   
TA4654                 MOVE    'GET-OPN-FSCL-MN'                                
TA4654                                          TO PV-PARAGRAPH-NAME            
TA4654                 MOVE    'SELECT ROW FROM TMLCNTL'                        
TA4654                                          TO PV-DB2-OPER-ATTEMPTED        
TA4654                 PERFORM Z900-SQL-ABEND                                   
TA4654         END-EVALUATE.                                                    
      ******************************************************************01800006
      * MAIN PROCESSING LOOP FOR DEPT 099-10 TRANSACTION REVERSALS.     01810006
      ***            TRANSACTION CODES (ML020-TRANS-CDE)                01820007
      ***     '20' SALES        '40' MARKDOWNS         '50' MARKUPS     01830007
      ******************************************************************01840007
       2000-PROCESS-REVERSALS.                                          01850006
      *                                                                 01860007
           EVALUATE ML020-TRANS-CDE                                     01870007
               WHEN '20'                                                01880007
                   PERFORM 2100-SALES-REVERSAL                          01890007
               WHEN '40'                                                01900007
               WHEN '50'                                                01910007
                   PERFORM 2200-PRICE-CHG-REVERSAL                      01920008
               WHEN OTHER                                               01930007
                   DISPLAY '*** INVALID INPUT FIELD : ', ML020-SORT-KEY 01940007
                   PERFORM 9999-ABEND                                   01950007
           END-EVALUATE.                                                01960007
      *                                                                 01970007
           PERFORM 5000-READ-REVERSAL-FILE.                             01980007
      *                                                                 01990007
      *                                                                 02000006
      ******************************************************************02010007
      * REVERSALS OF SALES RECORDS                                      02020007
      ******************************************************************02030007
       2100-SALES-REVERSAL.                                             02040007
      *                                                                 02050006
           IF ML020-SALE-EXTD-RTL-AMT NOT EQUAL 0                       02060011
               COMPUTE ML020-SALE-EXTD-RTL-AMT =                        02070011
                     ( ML020-SALE-EXTD-RTL-AMT * (-1) )                 02080011
TA4654         MOVE MLCNTL-OPN-FSCL-MN-DTE(3:2) TO ML020-SALE-DTE-YY            
TA4654         MOVE MLCNTL-OPN-FSCL-MN-DTE(6:2) TO ML020-SALE-DTE-MM            
TA4654         MOVE MLCNTL-OPN-FSCL-MN-DTE(9:2) TO ML020-SALE-DTE-DD            
               PERFORM 5100-WRITE-OUTPUT-RECORD                         02090011
           ELSE                                                         02100011
               IF PS-BYPASS-SALE-REC                                    02110014
442415             NEXT SENTENCE                                        02120019
442415********     DISPLAY ML020-COMPOSITE-RECORD                       02121019
               ELSE                                                     02130014
442415********     DISPLAY 'SALE REVERSAL BYPASSED FOR 0 DOLLAR RECORD' 02140020
442415********     DISPLAY ML020-COMPOSITE-RECORD                       02141019
                   SET PS-BYPASS-SALE-REC TO TRUE                       02160014
           END-IF.                                                      02170011
      *                                                                 02180007
      *                                                                 02190007
      ******************************************************************02200007
      * REVERSALS OF MARKDOWN AND MARKUP RECORDS                        02210007
      ***********                                                       02220015
      * CREATE A RECORD WITH EXTENDED VALUES (QTY * PRICE)              02230015
      * THEN SWITCH OLD AND NEW PRICE VALUES AS REVERSAL DOLLARS        02240015
      ******************************************************************02250015
       2200-PRICE-CHG-REVERSAL.                                         02260007
      *                                                                 02270007
SJ         COMPUTE PV-REVERSE-NEW-PR-AMT = ( ML020-PC-OLD-PR-AMT *      02280015
SJ                                           ML020-PC-QTY ).            02290015
SJ         COMPUTE PV-REVERSE-OLD-PR-AMT = ( ML020-PC-NEW-PR-AMT *      02300015
SJ                                           ML020-PC-QTY ).            02310015
      *                                                                 02320015
SJ    *    MOVE ML020-PC-OLD-PR-AMT TO PV-REVERSE-NEW-PR-AMT.           02330015
SJ    *    MOVE ML020-PC-NEW-PR-AMT TO PV-REVERSE-OLD-PR-AMT.           02340015
      *                                                                 02350007
           MOVE PV-REVERSE-NEW-PR-AMT TO ML020-PC-NEW-PR-AMT.           02360007
           MOVE PV-REVERSE-OLD-PR-AMT TO ML020-PC-OLD-PR-AMT.           02370007
           MOVE +1                    TO ML020-PC-QTY.                  02380016
           MOVE PC-REVERSAL-BATCH     TO ML020-PC-BATCH-NBR-3.          02390007
      *                                                                 02400007
           PERFORM 5100-WRITE-OUTPUT-RECORD .                           02410007
      *                                                                 02420006
      *                                                                 02430006
      ******************************************************************02440004
      * VALUES IN CONTROL CARDS DETERMINES PROCESSING OPTIONS           02450006
      ******************************************************************02460004
       4000-PROCESS-CONTROL-CARDS.                                      02470006
      *                                                                 02480006
           PERFORM 4100-PROCESS-DATE-FILE.                              02490006
           PERFORM 4200-PROCESS-CNTL-FILE.                              02500006
           PERFORM 4300-PROCESS-OPTIONS.                                02510006
      *                                                                 02520006
      * IF UPDATE CRITERIA MET, PROCESS REVERSALS, OTHERWISE EXIT       02530007
                                                                        02531018
           PERFORM 5000-READ-REVERSAL-FILE.                             02531118
                                                                        02531218
      *AML                                                              02532018
      *AML IF PS-UPDATE-BATCH-794                                       02540018
      *AML     PERFORM 5000-READ-REVERSAL-FILE                          02550018
      *AML ELSE                                                         02560018
      *AML     SET PS-PROCESSING-COMPLETE TO TRUE                       02570018
      *AML END-IF.                                                      02580018
      *                                                                 02590006
      ******************************************************************02600006
      * READ IN CONTROL CARD CONTAINING CURRENT JOBTRAC DATE            02610006
      ******************************************************************02620006
       4100-PROCESS-DATE-FILE.                                          02630006
                                                                        02640002
           READ CONTROL-DATE-FILE   INTO CC1-JOBTRAC-DATE               02650008
             AT END                                                     02660004
               SET PS-EMPTY-CC1-FILE TO TRUE                            02670004
           END-READ.                                                    02680005
                                                                        02690004
           IF  PS-EMPTY-CC1-FILE                                        02700004
               DISPLAY '** PROGRAM ABEND **'                            02710004
               DISPLAY '** JOBTRAC CONTROL CARD INVALID ',              02720004
               MOVE +4003              TO ABEND-CODE                    02730004
               PERFORM 9999-ABEND                                       02740007
           END-IF                                                       02750004
      *                                                                 02760004
           DISPLAY '  CURRENT DATE : ', CC1-JTRAC-DTE.                  02770005
      *                                                                 02780005
      *                                                                 02790005
      *                                                                 02800004
      ******************************************************************02810002
      * ENSURE CONTROL EXISTS                                           02820002
      ******************************************************************02830002
       4200-PROCESS-CNTL-FILE.                                          02840006
                                                                        02850002
           READ CONTROL-CARD-FILE INTO CC2-PROCESS-REC                  02860004
             AT END                                                     02870002
               SET PS-EMPTY-CC2-FILE TO TRUE                            02880004
           END-READ.                                                    02890005
                                                                        02900002
           IF  PS-EMPTY-CC2-FILE                                        02910004
               DISPLAY '** PROGRAM ABEND **'                            02920002
               DISPLAY '** CONTROL CARD INVALID ',                      02930002
               MOVE +4003              TO ABEND-CODE                    02940002
               PERFORM 9999-ABEND                                       02950007
           END-IF                                                       02960002
      *                                                                 02970005
           DISPLAY ' POS CNTL DATE : ', CC2-CURRENT-DTE.                02980014
      *                                                                 02990005
      *                                                                 03000002
      *                                                                 03010005
      ******************************************************************03020002
      * DETERMINE IF REVERSALS SHOULD BE PROCESSED BASED ON INPUTS.     03030006
      * PROCESS ONLY IF DATES ARE EQUAL AND INIDICATOR IS SET TO 'Y' .  03040006
      *                                                                 03041017
      * CONTROL CARD IS NO LONGER BEING USED TO CONTROL PROCESSING.     03042017
      ******************************************************************03050002
       4300-PROCESS-OPTIONS.                                            03060006
      *                                                                 03070004
           SET PS-UPDATE-BATCH-794 TO TRUE.                             03071017
                                                                        03072017
      *AML IF CC1-JTRAC-DTE  EQUAL  CC2-CURRENT-DTE                     03080017
      *AML     IF CC2-PROCESS-DEPTSCL                                   03090017
      *AML         SET PS-UPDATE-BATCH-794 TO TRUE                      03100017
      *AML     ELSE                                                     03110017
      *AML         DISPLAY '*** CONTROL FILE INDICATOR MUST EQUAL ',    03120017
      *AML         , '''Y'' -- NO REVERSALS WILL BE PROCESSED ***'      03130017
      *AML     END-IF                                                   03140017
      *AML ELSE                                                         03150017
      *AML     DISPLAY '*** CONTROL DATES MUST BE EQUAL - NO REVERSALS '03160017
      *AML           , 'WILL BE PROCESSED FOR THIS RUN ***'             03170017
      *AML END-IF.                                                      03180017
                                                                        03190002
      *                                                                 03200006
      *                                                                 03210006
      ******************************************************************03220007
      * READ A RECORD FROM COMPOSITE FILE                               03230007
      ******************************************************************03240007
       5000-READ-REVERSAL-FILE.                                         03250006
      *                                                                 03260006
           READ REVERSAL-TRANSACTION-FILE                               03270009
               INTO ML020-COMPOSITE-RECORD                              03280007
           AT END SET PS-PROCESSING-COMPLETE TO TRUE.                   03290007
      *                                                                 03300007
      *                                                                 03310007
      ******************************************************************03320007
      * WRITE A RECORD TO THE OUTPUT FILE                               03330007
      ******************************************************************03340007
       5100-WRITE-OUTPUT-RECORD.                                        03350007
      *                                                                 03360007
           WRITE REVERSAL-OUTPUT-RECORD FROM ML020-COMPOSITE-RECORD.    03370007
      *                                                                 03380007
      *                                                                 03390007
      ******************************************************************03400006
      *                                                                 03410006
      *                     ABEND ROUTINE                               03420006
      *                                                                 03430007
      ******************************************************************03440006
       9999-ABEND.                                                      03450007
                                                                        03460002
           CALL 'ILBOABN0'             USING ABEND-CODE.                03470002
                                                                        03480002
TA4654 Z900-SQL-ABEND.                                                          
TA4654                                                                          
TA4654     DISPLAY '** ABEND     **'.                                           
TA4654     DISPLAY '** PROGRAM   ** = MLKUT440'.                                
TA4654     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
TA4654     DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
TA4654                                                                          
TA4654******************************************************************        
TA4654* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
TA4654* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
TA4654* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
TA4654* FORMAT.                                                                 
TA4654******************************************************************        
TA4654     COPY DPPD004.                                                        
TA4654                                                                          
TA4654     MOVE +4013 TO ABEND-CODE.                                            
TA4654     CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                        03490000
