       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP301.                                         00030000
       AUTHOR.        BOB MCASEY.                                       00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  1998-05-26.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP301 - MONTHLY SUB-CLASS STORE MERCHANDISE       *00100000
      *                      LEDGER TABLE UPDATE (TMNSCLS)             *00110000
      ******************************************************************00120000
      *    WR/PITS#     DATE        DESCRIPTION                        *00130000
      *    --------   --------     ---------------------------------   *00140000
      *    5701       11/17/1998   RECOMPILED PROGRAM TO PICKUP UP     *00150000
      *                            CHANGES MADE TO DCLGEN AND MLRD104. *00160000
      *                            ALL "AMT" FIELDS WERE CHANGED FROM  *00170000
      *                            PIC S9(07)V99 TO S9(09)V99.         *00180000
      *                            THIS WAS DONE DUE TO AN OVERFLOW    *00190000
      *                            THAT OCCURRED WITHIN TMNSCLS ON     *00200000
      *                            XFER_IN_RTL_AMT & XFER_OUT_RTL_AMT  *00210000
      *                            DEPT 127; SCLS 80 ON 11/15/1998.    *00220000
W26603******************************************************************00230000
W26603* CHG ID   DATE        DESCRIPTION                               *00240000
W26603* ------   ----------  ----------------------------------------- *00250000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00260000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00270000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00280000
W26603*                      A DB2 SELECT.                             *00290000
W26603*                                                                *00300000
W26603*                      - RONNA MCDONALD                          *00310000
      ******************************************************************00320000
                                                                        00330000
                                                                        00340000
       ENVIRONMENT DIVISION.                                            00350000
                                                                        00360000
       CONFIGURATION SECTION.                                           00370000
       SOURCE-COMPUTER.        IBM-3090.                                00380000
       OBJECT-COMPUTER.        IBM-3090.                                00390000
       INPUT-OUTPUT SECTION.                                            00400000
       FILE-CONTROL.                                                    00410000
           SELECT CONDENSED-FILE                                        00420000
               ASSIGN TO INP01.                                         00430000
                                                                        00440000
                                                                        00450000
       DATA DIVISION.                                                   00460000
       FILE SECTION.                                                    00470000
                                                                        00480000
       FD  CONDENSED-FILE                                               00490000
           LABEL RECORDS ARE STANDARD                                   00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS.                                    00520000
       01  CONDENSED-RECORD            PIC X(100).                      00530000
                                                                        00540000
       WORKING-STORAGE SECTION.                                         00550000
                                                                        00560000
       01  FILLER                      PIC X(28)   VALUE                00570000
                'BEGINNING OF WORKING STORAGE'.                         00580000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00590000
                                                                        00600000
       01  PC-PROGRAM-CONSTANTS.                                        00610000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK215'.    00620000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP301'.  00630000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00640000
                                                                        00650000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00660000
           05  PE-MSG-01                       PIC X(50) VALUE          00670000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00680000
           05  PE-MSG-02                       PIC X(50) VALUE          00690000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNSCLS FAILED    '.    00700000
           05  PE-MSG-03                       PIC X(50) VALUE          00710000
                'ATTEMPT TO INSERT ROW ON TABLE TMNSCLS FAILED    '.    00720000
           05  PE-MSG-04                       PIC X(50) VALUE          00730000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00740000
           05  PE-MSG-05                       PIC X(50) VALUE          00750000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00760000
           05  PE-MSG-06                       PIC X(50) VALUE          00770000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00780000
           05  PE-MSG-07                       PIC X(50) VALUE          00790000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00800000
      *                                                                 00810000
       01  PS-PROGRAM-SWITCHES.                                         00820000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00830000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00840000
           05  PS-MNSCLS-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00850000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00860000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00870000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00880000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00890000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00900000
                                                                        00910000
       01  PI-PROGRAM-INDICATORS.                                       00920000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00930000
               88  PI-UPDATE-MNSCLS-ROW                   VALUE 'U'.    00940000
               88  PI-INSERT-MNSCLS-ROW                   VALUE 'I'.    00950000
      *                                                                 00960000
       01  PROGRAM-VARIABLES.                                           00970000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00980000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00990000
      *                                                                 01000000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01010000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01020000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01030000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01040000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01050000
      *                                                                 01060000
       01  PA-PROGRAM-ACCUMULATORS.                                     01070000
           05  PA-RECORD-COUNTS.                                        01080000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE 0.           01090000
               10  PA-INSERTED-COUNT     PIC  9(09)  VALUE 0.           01100000
               10  PA-UPDATED-COUNT      PIC  9(09)  VALUE 0.           01110000
                                                                        01120000
       01  PD-PROGRAM-DISPLAYS.                                         01130000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01140000
       01  SAVE-AREA.                                                   01150000
           05  SV-PRIMARY-KEY.                                          01160000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACE.        01170000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACE.        01180000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACE.        01190000
               10  SV-SUB-CL-NBR         PIC X(02)  VALUE SPACE.        01200000
               10  SV-STR-NBR            PIC S9(04) COMP VALUE 0.       01210000
           05  SV-UPDATE-FIELDS.                                        01220000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01230000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01240000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01260000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01270000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01280000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01290000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01300000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01310000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01320000
      *                                                                 01330000
      ******************************************************************01340000
      * MLRD104 - M/L MONTHLY SCL DATABASE UPDATE TRANSACTION LAYOUT    01350000
      ******************************************************************01360000
           COPY MLRD104.                                                01370000
      *                                                                 01380000
      ******************************************************************01390000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01400000
      ******************************************************************01410000
           COPY DPWS004.                                                01420000
      *                                                                 01430000
      ******************************************************************01440000
      *  USED FOR DB2 ERRORS                                            01450000
      ******************************************************************01460000
           EXEC SQL                                                     01470000
               INCLUDE SQLCA                                            01480000
           END-EXEC.                                                    01490000
      *                                                                 01500000
      ******************************************************************01510000
      *  M/L MONTH-TO-DATE SUB-CLASS STORE TABLE                        01520000
      ******************************************************************01530000
           EXEC SQL                                                     01540000
               INCLUDE TMNSCLS                                          01550000
           END-EXEC.                                                    01560000
      *                                                                 01570000
      ***************************************************************** 01580000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01590000
      ***************************************************************** 01600000
           EXEC SQL                                                     01610000
               INCLUDE TCKRSTCN                                         01620000
           END-EXEC.                                                    01630000
      *                                                                 01640000
           EXEC SQL                                                     01650000
               INCLUDE TCKRSTIN                                         01660000
           END-EXEC.                                                    01670000
      *                                                                 01680000
      ******************************************************************01690000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01700000
      ******************************************************************01710000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01720000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +30  COMP. 01730000
           05  CR-CHECKPOINT-RESTART-VAR.                               01740000
               10  CR-PREV-DTL-KEY          PIC  X(19) VALUE SPACES.    01750000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01760000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01770000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01780000
                   15  CR-DEPT-NBR          PIC  X(03).                 01790000
                   15  CR-SUB-CL-NBR        PIC  X(02).                 01800000
                   15  CR-STR-NBR           PIC  S9(04) COMP.           01810000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01820000
      *                                                                 01830000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01840000
           05  CK-SAVE-PREV-KEY         PIC  X(19) VALUE SPACES.        01850000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01860000
               10  CK-FISCAL-NBR        PIC  X(02).                     01870000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01880000
               10  CK-DEPT-NBR          PIC  X(03).                     01890000
               10  CK-SUB-CL-NBR        PIC  X(02).                     01900000
               10  CK-STR-NBR           PIC  S9(04) COMP.               01910000
                                                                        01920000
      *-------------------*                                             01930000
       PROCEDURE DIVISION.                                              01940000
      *-------------------*                                             01950000
                                                                        01960000
       0000-MLKUP301.                                                   01970000
                                                                        01980000
           PERFORM 1000-INITIALIZATION.                                 01990000
      *                                                                 02000000
           PERFORM 2000-PROCESS-INPUT                                   02010000
                UNTIL PS-EOF-CONDENSED-FILE.                            02020000
      *                                                                 02030000
           PERFORM 8000-EOJ-ROUTINE.                                    02040000
                                                                        02050000
           STOP RUN.                                                    02060000
                                                                        02070000
      ******************************************************************02080000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02090000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02100000
      ******************************************************************02110000
      *                                                                 02120000
       1000-INITIALIZATION.                                             02130000
      *                                                                 02140000
           OPEN INPUT CONDENSED-FILE.                                   02150000
      *                                                                 02160000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02170000
      *                                                                 02180000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02190000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02200000
                PERFORM 7500-SELECT-RESTART-INFO                        02210000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02220000
                                             CR-CHECKPOINT-RESTART-VAR  02230000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02240000
           ELSE                                                         02250000
               SET PS-FIRST-COMMIT TO TRUE                              02260000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02270000
           END-IF.                                                      02280000
      *                                                                 02290000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02300000
              (PA-INPUT-COUNT > CR-INPUT-READ-COUNT)                    02310000
               OR                                                       02320000
               PS-EOF-CONDENSED-FILE.                                   02330000
      *                                                                 02340000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02350000
               DISPLAY 'FISCAL MONTH NBR      ' ML104-FSCL-MN-NBR       02360000
               DISPLAY 'FISCAL MONTH END DATE ' ML104-FSCL-MN-END-DTE   02370000
               DISPLAY 'DEPARTMENT NBR        ' ML104-DEPT-NBR          02380000
               DISPLAY 'SUB CLASS NBR         ' ML104-SUB-CL-NBR        02390000
               DISPLAY 'STORE NBR             ' ML104-STR-NBR           02400000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02410000
                        PA-INPUT-COUNT                                  02420000
           END-IF.                                                      02430000
                                                                        02440000
           IF PS-EOF-CONDENSED-FILE                                     02450000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02460000
                   CONTINUE                                             02470000
               ELSE                                                     02480000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02490000
           ELSE                                                         02500000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02510000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02520000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02530000
           END-IF.                                                      02540000
      *                                                                 02550000
      ******************************************************************02560000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02570000
      ******************************************************************02580000
       2000-PROCESS-INPUT.                                              02590000
      *                                                                 02600000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02610000
      *                                                                 02620000
           PERFORM 7000-SELECT-DB-ROW.                                  02630000
      *                                                                 02640000
           IF PS-PROGRAM-DEADLOCKED                                     02650000
               CONTINUE                                                 02660000
           ELSE                                                         02670000
               EVALUATE TRUE                                            02680000
                   WHEN PI-UPDATE-MNSCLS-ROW                            02690000
                       PERFORM 7100-UPDATE-MNSCLS-ROW                   02700000
                   WHEN PI-INSERT-MNSCLS-ROW                            02710000
                       PERFORM 7200-INSERT-MNSCLS-ROW                   02720000
                   WHEN OTHER                                           02730000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02740000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02750000
                       PERFORM 9999-SQL-ABEND                           02760000
               END-EVALUATE                                             02770000
           END-IF.                                                      02780000
      *                                                                 02790000
           IF PS-PROGRAM-DEADLOCKED                                     02800000
               CONTINUE                                                 02810000
           ELSE                                                         02820000
               PERFORM 7700-COMMIT-PROCESSING                           02830000
               PERFORM 4000-READ-CONDENSED-FILE                         02840000
           END-IF.                                                      02850000
      *                                                                 02860000
      *                                                                 02870000
      ******************************************************************02880000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02890000
      ******************************************************************02900000
       2100-CALCULATE-UPDATES.                                          02910000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02920000
      *                                                                 02930000
           COMPUTE SV-SLS-RTL-AMT       =                               02940000
                   MNSCLS-SLS-RTL-AMT      + ML104-SLS-RTL-AMT.         02950000
           COMPUTE SV-MKDN-RTL-AMT      =                               02960000
                   MNSCLS-MKDN-RTL-AMT     + ML104-MKDN-RTL-AMT.        02970000
           COMPUTE SV-MKUP-RTL-AMT      =                               02980000
                   MNSCLS-MKUP-RTL-AMT     + ML104-MKUP-RTL-AMT.        02990000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03000000
                   MNSCLS-XFER-IN-RTL-AMT  + ML104-XFER-IN-RTL-AMT.     03010000
           COMPUTE SV-XFER-OUT-RTL      =                               03020000
                   MNSCLS-XFER-OUT-RTL-AMT + ML104-XFER-OUT-RTL-AMT.    03030000
           COMPUTE SV-RCPT-RTL-AMT      =                               03040000
                   MNSCLS-RCPT-RTL-AMT + ML104-RCPT-RTL-AMT.            03050000
           COMPUTE SV-RCPT-NET-COST     =                               03060000
                   MNSCLS-RCPT-NET-COST-AMT + ML104-RCPT-NET-COST-AMT.  03070000
           COMPUTE SV-PADJ-RTL-AMT      =                               03080000
                   MNSCLS-PADJ-RTL-AMT     + ML104-PADJ-RTL-AMT.        03090000
           COMPUTE SV-PADJ-NET-COST     =                               03100000
                   MNSCLS-PADJ-NET-COST-AMT + ML104-PADJ-NET-COST-AMT   03110000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03120000
                    MNSCLS-INV-ADJ-RTL-AMT + ML104-INV-ADJ-RTL-AMT.     03130000
      *                                                                 03140000
      ******************************************************************03150000
      * READS THE CONDENSED FILE INTO THE TMNSCLS LAYOUT                03160000
      ******************************************************************03170000
       4000-READ-CONDENSED-FILE.                                        03180000
           READ CONDENSED-FILE INTO ML104-UPDATE-REC                    03190000
              AT END                                                    03200000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03210000
     *                                                                  03220000
           IF PS-EOF-CONDENSED-FILE                                     03230000
               MOVE ML104-FSCL-MN-NBR         TO CR-FISCAL-NBR          03240000
               MOVE ML104-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03250000
               MOVE ML104-DEPT-NBR            TO CR-DEPT-NBR            03260000
               MOVE ML104-SUB-CL-NBR          TO CR-SUB-CL-NBR          03270000
               MOVE ML104-STR-NBR             TO CR-STR-NBR             03280000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03290000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03300000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03310000
                                             TCKRSTINF-WORK-STRG-DESC   03320000
               EXEC SQL                                                 03330000
                 UPDATE TCKRSTINF                                       03340000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03350000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03360000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03370000
               END-EXEC                                                 03380000
               IF SQLCODE = 0                                           03390000
                   CONTINUE                                             03400000
               ELSE                                                     03410000
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED  03420000
                   MOVE '* 4000-READ-CONDENSED-FILE *' TO               03430000
                        PV-PARAGRAPH-NAME                               03440000
                   PERFORM 9999-SQL-ABEND                               03450000
               END-IF                                                   03460000
           ELSE                                                         03470000
               ADD 1                         TO PA-INPUT-COUNT          03480000
           END-IF.                                                      03490000
      *                                                                 03500000
      ******************************************************************03510000
      * SELECT A ROW FROM THE MONTHLY MCL  TABLE THAT MATCHES INPUT KEY 03520000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03530000
      ******************************************************************03540000
       7000-SELECT-DB-ROW.                                              03550000
      *                                                                 03560000
           EXEC SQL                                                     03570000
               SELECT SLS_RTL_AMT                                       03580000
                     ,MKDN_RTL_AMT                                      03590000
                     ,MKUP_RTL_AMT                                      03600000
                     ,XFER_IN_RTL_AMT                                   03610000
                     ,XFER_OUT_RTL_AMT                                  03620000
                     ,RCPT_RTL_AMT                                      03630000
                     ,RCPT_NET_COST_AMT                                 03640000
                     ,PADJ_RTL_AMT                                      03650000
                     ,PADJ_NET_COST_AMT                                 03660000
                     ,INV_ADJ_RTL_AMT                                   03670000
                 INTO :MNSCLS-SLS-RTL-AMT                               03680000
                     ,:MNSCLS-MKDN-RTL-AMT                              03690000
                     ,:MNSCLS-MKUP-RTL-AMT                              03700000
                     ,:MNSCLS-XFER-IN-RTL-AMT                           03710000
                     ,:MNSCLS-XFER-OUT-RTL-AMT                          03720000
                     ,:MNSCLS-RCPT-RTL-AMT                              03730000
                     ,:MNSCLS-RCPT-NET-COST-AMT                         03740000
                     ,:MNSCLS-PADJ-RTL-AMT                              03750000
                     ,:MNSCLS-PADJ-NET-COST-AMT                         03760000
                     ,:MNSCLS-INV-ADJ-RTL-AMT                           03770000
               FROM TMNSCLS                                             03780000
              WHERE   FSCL_MN_NBR         = :ML104-FSCL-MN-NBR          03790000
                AND   FSCL_MN_END_DTE     = :ML104-FSCL-MN-END-DTE      03800000
                AND   DEPT_NBR            = :ML104-DEPT-NBR             03810000
                AND   SUB_CL_NBR          = :ML104-SUB-CL-NBR           03820000
                AND   STR_NBR             = :ML104-STR-NBR              03830000
W26603        WITH UR                                                   03840000
           END-EXEC.                                                    03850000
      *                                                                 03860000
           EVALUATE SQLCODE                                             03870000
               WHEN 0                                                   03880000
                   SET PI-UPDATE-MNSCLS-ROW       TO TRUE               03890000
               WHEN +100                                                03900000
                   SET PI-INSERT-MNSCLS-ROW       TO TRUE               03910000
               WHEN -911                                                03920000
                   ADD +1             TO PV-DEADLOCK-COUNT              03930000
                   DISPLAY 'DEADLOCK -911 IN 7000-SELECT-DB-ROW'        03940000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03950000
                       MOVE '7000-SELECT-DB-ROW' TO                     03960000
                                                    PV-PARAGRAPH-NAME   03970000
                       PERFORM 9000-DEADLOCK-ERROR                      03980000
                   ELSE                                                 03990000
                       PERFORM 7999-RESTART-PROCESS                     04000000
                   END-IF                                               04010000
               WHEN OTHER                                               04020000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04030000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04040000
                   PERFORM 9999-SQL-ABEND                               04050000
           END-EVALUATE.                                                04060000
                                                                        04070000
                                                                        04080000
      ***************************************************************** 04090000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04100000
      * SUB CLASS AND STORE. CURRENT TABLE VALUES UPDATED TO INCLUDE    04110000
      * VALUES FROM INPUT REC MLRD105                                   04120000
      ***************************************************************** 04130000
       7100-UPDATE-MNSCLS-ROW.                                          04140000
                                                                        04150000
                                                                        04160000
           PERFORM 2100-CALCULATE-UPDATES.                              04170000
                                                                        04180000
           EXEC SQL                                                     04190000
               UPDATE TMNSCLS                                           04200000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04210000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          04220000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04230000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          04240000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            04250000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04260000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04270000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04280000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           04290000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04300000
              WHERE   FSCL_MN_NBR          = :ML104-FSCL-MN-NBR         04310000
                AND   FSCL_MN_END_DTE      = :ML104-FSCL-MN-END-DTE     04320000
                AND   DEPT_NBR             = :ML104-DEPT-NBR            04330000
                AND   SUB_CL_NBR           = :ML104-SUB-CL-NBR          04340000
                AND   STR_NBR              = :ML104-STR-NBR             04350000
           END-EXEC                                                     04360000
      *                                                                 04370000
           EVALUATE SQLCODE                                             04380000
               WHEN 0                                                   04390000
                   CONTINUE                                             04400000
               WHEN -911                                                04410000
                   ADD +1             TO PV-DEADLOCK-COUNT              04420000
                   DISPLAY 'DEADLOCK -911 IN 7100-UPDATE-MNSCLS-ROW'    04430000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04440000
                       MOVE '7100-UPDATE-MNSCLS-ROW' TO                 04450000
                                                    PV-PARAGRAPH-NAME   04460000
                       PERFORM 9000-DEADLOCK-ERROR                      04470000
                   ELSE                                                 04480000
                       PERFORM 7999-RESTART-PROCESS                     04490000
                   END-IF                                               04500000
               WHEN OTHER                                               04510000
                   MOVE '7100-UPDATE-MNSCLS-ROW' TO PV-PARAGRAPH-NAME   04520000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04530000
                   PERFORM 9999-SQL-ABEND                               04540000
           END-EVALUATE.                                                04550000
                                                                        04560000
           ADD 1 TO PA-UPDATED-COUNT.                                   04570000
                                                                        04580000
      ***************************************************************** 04590000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY MCL  TABLE. INITIAL  * 04600000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04610000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04620000
      ***************************************************************** 04630000
       7200-INSERT-MNSCLS-ROW.                                          04640000
      *                                                                 04650000
      *                                                                 04660000
           EXEC SQL                                                     04670000
               INSERT INTO TMNSCLS                                      04680000
                   (   FSCL_MN_NBR                                      04690000
                     , FSCL_MN_END_DTE                                  04700000
                     , DEPT_NBR                                         04710000
                     , SUB_CL_NBR                                       04720000
                     , STR_NBR                                          04730000
                     , RCPT_RTL_AMT                                     04740000
                     , RCPT_NET_COST_AMT                                04750000
                     , PADJ_RTL_AMT                                     04760000
                     , PADJ_NET_COST_AMT                                04770000
                     , SLS_RTL_AMT                                      04780000
                     , MKDN_RTL_AMT                                     04790000
                     , MKUP_RTL_AMT                                     04800000
                     , XFER_IN_RTL_AMT                                  04810000
                     , XFER_OUT_RTL_AMT                                 04820000
                     , INV_ADJ_RTL_AMT                                  04830000
                     , SHNK_RTL_AMT                                     04840000
                     , END_INV_RTL_AMT                                  04850000
                     , CUM_MKUP_PCT                                     04860000
                     , END_INV_COST_AMT                                 04870000
                     , SLS_COST_AMT                                     04880000
                     , GRO_MRGN_AMT      )                              04890000
               VALUES                                                   04900000
                   (   :ML104-FSCL-MN-NBR                               04910000
                     , :ML104-FSCL-MN-END-DTE                           04920000
                     , :ML104-DEPT-NBR                                  04930000
                     , :ML104-SUB-CL-NBR                                04940000
                     , :ML104-STR-NBR                                   04950000
                     , :ML104-RCPT-RTL-AMT                              04960000
                     , :ML104-RCPT-NET-COST-AMT                         04970000
                     , :ML104-PADJ-RTL-AMT                              04980000
                     , :ML104-PADJ-NET-COST-AMT                         04990000
                     , :ML104-SLS-RTL-AMT                               05000000
                     , :ML104-MKDN-RTL-AMT                              05010000
                     , :ML104-MKUP-RTL-AMT                              05020000
                     , :ML104-XFER-IN-RTL-AMT                           05030000
                     , :ML104-XFER-OUT-RTL-AMT                          05040000
                     , :ML104-INV-ADJ-RTL-AMT                           05050000
                     , 0                                                05060000
                     , 0                                                05070000
                     , 0                                                05080000
                     , 0                                                05090000
                     , 0                                                05100000
                     , 0                       )                        05110000
           END-EXEC.                                                    05120000
      *                                                                 05130000
           EVALUATE SQLCODE                                             05140000
               WHEN 0                                                   05150000
                   CONTINUE                                             05160000
               WHEN -911                                                05170000
                   ADD +1             TO PV-DEADLOCK-COUNT              05180000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05190000
                       MOVE '7200-INSERT-MNSCLS-ROW' TO                 05200000
                                                    PV-PARAGRAPH-NAME   05210000
                       PERFORM 9000-DEADLOCK-ERROR                      05220000
                   ELSE                                                 05230000
                       PERFORM 7999-RESTART-PROCESS                     05240000
                   END-IF                                               05250000
               WHEN OTHER                                               05260000
                   MOVE '7200-INSERT-MNSCLS-ROW' TO PV-PARAGRAPH-NAME   05270000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05280000
                   PERFORM 9999-SQL-ABEND                               05290000
           END-EVALUATE.                                                05300000
      *                                                                 05310000
           ADD 1 TO PA-INSERTED-COUNT.                                  05320000
                                                                        05330000
      ******************************************************************05340000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05350000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05360000
      *            CONTROL TABLE                                       *05370000
      ******************************************************************05380000
       7300-GET-COMMIT-TIMESTAMP.                                       05390000
                                                                        05400000
      * CHECKPOINT TAKEN BASED ON DB2 CHECKPOINT RESTART TABLE          05410000
           EXEC SQL                                                     05420000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05430000
               INTO :PV-COMMIT-TIMESTAMP                                05440000
               FROM TCKRSTCNTL                                          05450000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05460000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05470000
           END-EXEC.                                                    05480000
                                                                        05490000
           EVALUATE TRUE                                                05500000
               WHEN SQLCODE = 0                                         05510000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05520000
                   CONTINUE                                             05530000
               WHEN SQLCODE NOT EQUAL ZERO                              05540000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05550000
                   PERFORM 9999-SQL-ABEND                               05560000
           END-EVALUATE.                                                05570000
      *                                                                 05580000
      *                                                                 05590000
      ******************************************************************05600000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05610000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05620000
      *            IF WE ARE IN A RESTART CONDITION.                   *05630000
      ******************************************************************05640000
       7400-INIT-CHECKPOINT-SELECT.                                     05650000
                                                                        05660000
           EXEC SQL                                                     05670000
             SELECT  CKPT_STAT_CODE                                     05680000
                    ,CKPT_FRQNCY_QTY                                    05690000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05700000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05710000
               FROM  TCKRSTCNTL                                         05720000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05730000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05740000
           END-EXEC.                                                    05750000
                                                                        05760000
           IF SQLCODE = 0                                               05770000
               CONTINUE                                                 05780000
           ELSE                                                         05790000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05800000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05810000
               PERFORM 9999-SQL-ABEND                                   05820000
           END-IF.                                                      05830000
      *                                                                 05840000
      *                                                                 05850000
      ******************************************************************05860000
      * 7500-SELECT-RESTART-INFO                                       *05870000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05880000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05890000
      ******************************************************************05900000
       7500-SELECT-RESTART-INFO.                                        05910000
                                                                        05920000
           EXEC SQL                                                     05930000
             SELECT  WORK_STRG_DESC                                     05940000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05950000
               FROM  TCKRSTINF                                          05960000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05970000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05980000
           END-EXEC.                                                    05990000
                                                                        06000000
           IF SQLCODE = 0                                               06010000
               CONTINUE                                                 06020000
           ELSE                                                         06030000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06040000
               PERFORM 9999-SQL-ABEND                                   06050000
           END-IF.                                                      06060000
                                                                        06070000
      ******************************************************************06080000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06090000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06100000
      ******************************************************************06110000
       7600-SET-CURRENT-TIMESTAMP.                                      06120000
                                                                        06130000
           EXEC SQL                                                     06140000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06150000
           END-EXEC.                                                    06160000
           IF SQLCODE = 0                                               06170000
               CONTINUE                                                 06180000
           ELSE                                                         06190000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06200000
               PERFORM 9999-SQL-ABEND                                   06210000
           END-IF.                                                      06220000
                                                                        06230000
      ******************************************************************06240000
      * 7700-COMMIT-PROCESSING.                                        *06250000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06260000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06270000
      ******************************************************************06280000
       7700-COMMIT-PROCESSING.                                          06290000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06300000
                                                                        06310000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06320000
                                                                        06330000
      * PREPARE COMMIT RECORD FOR UPDATE                                06340000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06350000
               MOVE ML104-FSCL-MN-NBR         TO CR-FISCAL-NBR          06360000
               MOVE ML104-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   06370000
               MOVE ML104-DEPT-NBR            TO CR-DEPT-NBR            06380000
               MOVE ML104-SUB-CL-NBR          TO CR-SUB-CL-NBR          06390000
               MOVE ML104-STR-NBR             TO CR-STR-NBR             06400000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06410000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06420000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06430000
                                             TCKRSTINF-WORK-STRG-DESC   06440000
               IF PS-FIRST-COMMIT                                       06450000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06460000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06470000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06480000
               END-IF                                                   06490000
               PERFORM 7800-COMMIT-CHANGES                              06500000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06510000
           END-IF.                                                      06520000
      *                                                                 06530000
      *                                                                 06540000
      ******************************************************************06550000
      * 7800-COMMIT-CHANGES.                                            06560000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06570000
      *            TAKE A COMMIT.                                       06580000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06590000
      ******************************************************************06600000
       7800-COMMIT-CHANGES.                                             06610000
                                                                        06620000
           EXEC SQL                                                     06630000
             UPDATE TCKRSTINF                                           06640000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06650000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06660000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           IF SQLCODE = 0                                               06700000
               CONTINUE                                                 06710000
           ELSE                                                         06720000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06730000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06740000
               PERFORM 9999-SQL-ABEND                                   06750000
           END-IF.                                                      06760000
                                                                        06770000
           EXEC SQL                                                     06780000
               COMMIT                                                   06790000
           END-EXEC.                                                    06800000
                                                                        06810000
           IF SQLCODE = 0                                               06820000
               CONTINUE                                                 06830000
           ELSE                                                         06840000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06850000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06860000
               PERFORM 9999-SQL-ABEND                                   06870000
           END-IF.                                                      06880000
      *                                                                 06890000
      *                                                                 06900000
      ******************************************************************06910000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06920000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06930000
      *                                                                 06940000
      ******************************************************************06950000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06960000
                                                                        06970000
           EXEC SQL                                                     06980000
             UPDATE  TCKRSTCNTL                                         06990000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07000000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07010000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07020000
           END-EXEC.                                                    07030000
                                                                        07040000
           IF SQLCODE = 0                                               07050000
               CONTINUE                                                 07060000
           ELSE                                                         07070000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07080000
               PERFORM 9999-SQL-ABEND                                   07090000
           END-IF.                                                      07100000
                                                                        07110000
           EJECT                                                        07120000
      *                                                                 07130000
      *                                                                 07140000
      ******************************************************************07150000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNSCL ROW FOR UPDATE 07160000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07170000
      ******************************************************************07180000
       7999-RESTART-PROCESS.                                            07190000
      *                                                                 07200000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07210000
      *                                                                 07220000
           CLOSE CONDENSED-FILE.                                        07230000
      *                                                                 07240000
           PERFORM 7500-SELECT-RESTART-INFO.                            07250000
      *                                                                 07260000
           DISPLAY '**** RESTART **** '.                                07270000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07280000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07290000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07300000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07310000
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                       07320000
           IF PS-FIRST-COMMIT                                           07330000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07340000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07350000
                      ' BEGINNING'                                      07360000
           ELSE                                                         07370000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07380000
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 07390000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07400000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07410000
                    CR-CHECKPOINT-RESTART-AREA                          07420000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07430000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 07440000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          07450000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07460000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 07470000
               DISPLAY 'STORE NBR       ' CR-STR-NBR                    07480000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07490000
           END-IF.                                                      07500000
                                                                        07510000
           OPEN INPUT CONDENSED-FILE.                                   07520000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07530000
      *                                                                 07540000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07550000
           PERFORM 4000-READ-CONDENSED-FILE                             07560000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07570000
               OR PS-EOF-CONDENSED-FILE.                                07580000
                                                                        07590000
           IF PS-EOF-CONDENSED-FILE                                     07600000
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07610000
           ELSE                                                         07620000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07630000
                        PA-INPUT-COUNT                                  07640000
               DISPLAY 'FISCAL MONTH NBR      ' ML104-FSCL-MN-NBR       07650000
               DISPLAY 'FISCAL MONTH END DATE ' ML104-FSCL-MN-END-DTE   07660000
               DISPLAY 'DEPARTMENT NBR        ' ML104-DEPT-NBR          07670000
               DISPLAY 'SUB CLASS NBR         ' ML104-SUB-CL-NBR        07680000
               DISPLAY 'STORE NBR             ' ML104-STR-NBR           07690000
           END-IF.                                                      07700000
                                                                        07710000
      ******************************************************************07720000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07730000
      ******************************************************************07740000
       8000-EOJ-ROUTINE.                                                07750000
      *                                                                 07760000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07770000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07780000
      *                                                                 07790000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07800000
           DISPLAY 'ROWS INSERTED            ', PA-INSERTED-COUNT.      07810000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATED-COUNT.       07820000
      *                                                                 07830000
           CLOSE CONDENSED-FILE.                                        07840000
      *                                                                 07850000
      *                                                                 07860000
      ******************************************************************07870000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07880000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07890000
      ******************************************************************07900000
       9000-DEADLOCK-ERROR.                                             07910000
      *                                                                 07920000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07930000
               PERFORM 9999-SQL-ABEND.                                  07940000
      *                                                                 07950000
      ******************************************************************07960000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07970000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07980000
      ******************************************************************07990000
       9999-SQL-ABEND.                                                  08000000
                                                                        08010000
           MOVE +4013                 TO ABEND-CODE.                    08020000
           DISPLAY '**  ABEND     **'.                                  08030000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08040000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08050000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08060000
                                                                        08070000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08080000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08090000
                                                                        08100000
           COPY DPPD004.                                                08110000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08120000
