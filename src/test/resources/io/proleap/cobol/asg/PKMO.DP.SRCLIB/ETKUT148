       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT148.                                                 
       AUTHOR.        JON FIEDLER.                                              
       DATE-WRITTEN.  08/23/02.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  CREATE AN EXTRACT OF VENDOR IDS AND QUALIFIERS FOR A SPECIFIED         
      *  DOCUMENT TYPE AND PROD/TEST STATUS (THIS IS INDICATED BY A             
      *  CONTROL CARD).                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ET-PARM-CARD                                          ETKUT018
               ASSIGN TO INPARM.                                        ETKUT018
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ET-PARM-CARD                                                 ETKUT018
           RECORDING MODE IS F                                          ETKUT018
           LABEL RECORDS ARE STANDARD                                   ETKUT018
           BLOCK CONTAINS 0 RECORDS                                     ETKUT018
           RECORD CONTAINS 80 CHARACTERS                                ETKUT018
           DATA RECORD IS ET-PARM-RECORD.                               ETKUT018
                                                                        ETKUT018
       01  ET-PARM-RECORD                     PIC X(80).                ETKUT018
                                                                        ETKUT018
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 18 CHARACTERS                                        
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD               PIC X(18).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  END-OF-TTRDPRT-TBL-SW     PIC X(1)  VALUE 'N'.                       
           88  END-OF-TTRDPRT-TBL              VALUE 'Y'.                       
                                                                                
       01  PV-PARAGRAPH-NAME         PIC X(30).                                 
       01  PV-DB2-OPER-ATTEMPTED     PIC X(30).                                 
       01  PV-PARM-REC.                                                 WRAP1   
           05  PV-TRAN-SET-CDE                 PIC X(03).               WRAP1   
           05  FILLER                          PIC X(01).                       
           05  PV-PROD-TEST-IND                PIC X(01).                       
           05  FILLER                          PIC X(75).               WRAP1   
                                                                                
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  OUTPUT-DTL-LINE.                                                     
           05  OUTPUT-QUALIFIER      PIC X(2)   VALUE SPACES.                   
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  OUTPUT-PARTNER-ID     PIC X(15)  VALUE SPACES.                   
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              DECLARE VEND_CURSOR CURSOR FOR                                    
                 SELECT DISTINCT A.VEND_INCHG_ID                                
                      , A.VEND_INCHG_ID_DESC                                    
                 FROM   TTRDPRT A                                               
                      , TTRDTRN B                                               
                 WHERE A.INACTV_DTE = '9999-09-09'                              
                   AND A.TP_DKEY = B.TP_DKEY                                    
                   AND B.INACTV_DTE  = '9999-09-09'                             
                   AND B.TRAN_SET_CDE = :PV-TRAN-SET-CDE                        
                   AND B.PROD_TEST_IND = :PV-PROD-TEST-IND                      
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT ET-PARM-CARD.                                             
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           READ ET-PARM-CARD INTO PV-PARM-REC.                          ETKUT018
                                                                                
           EXEC SQL                                                             
              OPEN VEND_CURSOR                                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION' TO PV-PARAGRAPH-NAME                    
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 1400-SQL-ERROR.                                           
                                                                                
           PERFORM 1000-PROCESS-TABLE                                           
              THRU 1000-PROCESS-TABLE-EXIT                                      
                 UNTIL END-OF-TTRDPRT-TBL.                                      
                                                                                
           EXEC SQL                                                             
              CLOSE VEND_CURSOR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION' TO PV-PARAGRAPH-NAME                    
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 1400-SQL-ERROR.                                           
                                                                                
           CLOSE OUTPUT-FILE                                                    
                 ET-PARM-CARD.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-PROCESS-TABLE.                                                      
                                                                                
           EXEC SQL                                                             
               FETCH VEND_CURSOR                                                
                INTO :TRDPRT-VEND-INCHG-ID                                      
                   , :TRDPRT-VEND-INCHG-ID-DESC                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-TTRDPRT-TBL-SW                              
                 GO TO 1000-PROCESS-TABLE-EXIT                                  
              ELSE                                                              
                 MOVE '1000-PROCESS-TABLE' TO PV-PARAGRAPH-NAME                 
                 MOVE 'FETCH ROWS' TO PV-DB2-OPER-ATTEMPTED                     
                 PERFORM 1400-SQL-ERROR.                                        
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO OUTPUT-QUALIFIER.                  
           MOVE TRDPRT-VEND-INCHG-ID      TO OUTPUT-PARTNER-ID.                 
                                                                                
           WRITE OUTPUT-RECORD FROM OUTPUT-DTL-LINE.                            
                                                                                
       1000-PROCESS-TABLE-EXIT.                                                 
           EXIT.                                                                
                                                                                
       1400-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
