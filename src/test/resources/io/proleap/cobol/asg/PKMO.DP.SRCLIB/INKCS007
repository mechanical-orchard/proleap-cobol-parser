      *-----------------------*                                         03/24/99
       IDENTIFICATION DIVISION.                                         INKCS007
      *-----------------------*                                            LV001
       PROGRAM-ID.    INKCS007.                                         INKCS007
       AUTHOR.        PAUL KEBER - STRATAGEM                            INKCS007
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS007
       DATE-WRITTEN.  03/24/99.                                         INKCS007
       DATE-COMPILED.                                                   INKCS007
                                                                        INKCS007
      *---------------------------------------------------------------* INKCS007
      *    I007 - PHYSICAL INVENTORY INVALID DATA COMPARISON PRINT      INKCS007
      *                                                                 INKCS007
      *    THIS PROGRAM GENERATES A PRINT OF ALL ITEMS WITH THE         INKCS007
      *    INVENTORIED DATA AND THE CORRECTED DATA FOR THE PASSED       INKCS007
      *    STORE AND SELECTED AREAS.  EITHER UP TO 50 SELECTED AREAS    INKCS007
      *    MAY BE PASSED TO THE PROGRAM FOR SEPARATE REPORTS ON EACH    INKCS007
      *    AREA, OR ALL AREAS MAY BE SELECTED.  A PAGE BREAK WILL OCCUR INKCS007
      *    FOR EACH AREA.  THE DRS REPORTING SYSTEM IS USED TO SEND     INKCS007
      *    THE REPORT TO THE DESTINATION PRINTER.                       INKCS007
      *                                                                 INKCS007
      *    ANY CHANGES THAT MAY EFFECT THE LAYOUT OF THIS REPORT                
      *    SHOULD BE TESTED ON EVERY MODEL OF PRINTER.  THERE MIGHT BE          
      *    DIFFERENCES IN CARRIAGE CONTROL AND GRAPHICS PRINTING FOR            
      *    EACH MODEL.  ALSO BE AWARE THAT WHEN USING PROGRAM DRS1INTC          
      *    TO PRINT THE REPORT, A NON-BLANK CHARACTER IN PRINT POSITION         
      *    132 WILL CAUSE A DOUBLE-SPACE TO OCCUR REGARDLESS OF THE             
      *    VALUE OF THE CARRIAGE CONTROL.                                       
      *---------------------------------------------------------------* INKCS007
W21732******************** HISTORY OF CHANGES **************************        
W21732* CHG ID/                                                        *        
W21732* WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
W21732* -------  ----------  ----------------------------------------  *        
W21732* 21732    10/24/2002  ABRAHAM JACOB - IBM GLOBAL SERVICES       *        
W21732*                      NI-SKU PROJECT. DEPARTMENT AND CLASS      *        
W21732*                      ARE NO LONGER PRESENT IN TINVDTL TABLE.   *        
W21732*                      DEPARTMENT AND CLASS ARE OBTAINED FROM    *        
W21732*                      TSUMINV AND TSKXREF TABLES.               *        
W21732*                      REFORMATTED OUTPUT PRINTER RECORDS.       *        
W26600* W26600   06-29-2004  ROD JANSSEN INV STORE EXPANSION                    
W21732******************************************************************        
                                                                        INKCS007
       ENVIRONMENT DIVISION.                                            INKCS007
                                                                        INKCS007
       DATA DIVISION.                                                   INKCS007
                                                                        INKCS007
       WORKING-STORAGE SECTION.                                         INKCS007
                                                                        INKCS007
       01  PC-PROGRAM-CONSTANTS.                                        INKCS007
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS007
                                                          'INKCS007'.   INKCS007
           05  PC-LINES-PER-PAGE              PIC S9(04)  VALUE +62             
                                                          COMP SYNC.    INKCS007
           05  PC-MAX-AREA-NBRS               PIC S9(04)  VALUE +50     INKCS007
                                                          COMP SYNC.    INKCS007
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE +3      INKCS007
                                                          COMP-3.       INKCS007
           05  PC-CORPORATE                   PIC  X(02)  VALUE 'HQ'.           
W26600     05  PC-CORPORATE-NBR               PIC  X(04)  VALUE '0090'. INKCS007
W21732     05  PC-00000000                    PIC  9(08)                INKCS006
W21732                                        VALUE 00000000.           INKCS006
           05  PC-TOP-OF-PAGE                 PIC  X(01)  VALUE '1'.            
           05  PC-SINGLE-SPACE                PIC  X(01)  VALUE ' '.            
           05  PC-DOUBLE-SPACE                PIC  X(01)  VALUE '0'.            
           05  PC-TRIPLE-SPACE                PIC  X(01)  VALUE '-'.            
           05  PC-INIT                        PIC  X(04)  VALUE 'INIT'.         
           05  PC-PUT                         PIC  X(03)  VALUE 'PUT'.          
           05  PC-TERM                        PIC  X(04)  VALUE 'TERM'.         
      *                                                                 INKCS007
       01  PS-PROGRAM-SWITCHES.                                         INKCS007
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS007
               88  PS-NO-ERROR                            VALUE 'Y'.    INKCS007
               88  PS-ERROR                               VALUE 'N'.    INKCS007
           05  PS-STORE-REC-SW                PIC  X(01)  VALUE 'N'.       CL*18
               88  PS-STORE-REC                           VALUE 'Y'.       CL*18
               88  PS-NO-STORE-REC                        VALUE 'N'.       CL*18
           05  PS-ITEMS-LEFT-ON-DB-SW         PIC  X(01)  VALUE 'Y'.    INKCS007
               88  PS-ITEMS-LEFT-ON-DB                    VALUE 'Y'.    INKCS007
               88  PS-NO-ITEMS-LEFT-ON-DB                 VALUE 'N'.    INKCS007
      *                                                                 INKCS007
       01  PV-PROGRAM-VARIABLES.                                        INKCS007
           05  PV-LOC-NBR                     PIC X(04)  VALUE SPACES.          
           05  PV-LOC-NBR-NUM REDEFINES                                         
               PV-LOC-NBR                     PIC 9(04).                        
           05  PV-ABSTIME                     PIC S9(16)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HOLD-AREA-NBR               PIC S9(09)  VALUE +0      INKCS007
                                                          COMP SYNC.    INKCS007
           05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS007
                                                          COMP SYNC.    INKCS007
           05  PV-LINE-COUNT                  PIC S9(04)  VALUE +90             
                                                          COMP SYNC.            
           05  PV-PAGE-COUNT                  PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-AREA-SUB                    PIC S9(04)  VALUE +0      INKCS007
                                                          COMP SYNC.    INKCS007
           05  PV-CICS-RESPONSE               PIC S9(04)  VALUE +0      INKCS007
                                                          COMP SYNC.    INKCS007
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      INKCS007
                                                          COMP SYNC.    INKCS007
           05  PV-NBR-FALLOUTS                PIC S9(05)  VALUE ZERO       CL**3
                                                          COMP-3.          CL**3
           05  PV-NBR-TO-CORRECT              PIC S9(05)  VALUE ZERO       CL**3
                                                          COMP-3.          CL**3
           05  PV-DEST-ID.                                                      
W26600         10  FILLER                      PIC X(01) VALUE 'S'.             
W26600         10  PV-STORE-ID                 PIC 9(05).                       
W26600         10  FILLER                      PIC X(02) VALUE '61'.            
W26600                                                                  INKCS007
W26600     05  PV-USER-ID                      PIC X(08) VALUE SPACES.          
W26600                                                                  INKCS007
           05  PV-SYSTEM-ID.                                                    
               10  FILLER                      PIC  X(04)  VALUE SPACES.        
               10  PV-REGION-IND               PIC  X(01)  VALUE SPACE.         
                   88  PV-TEST-CICS                 VALUE 'T'.                  
                   88  PV-QA-CICS                   VALUE 'Q'.                  
                   88  PV-PROD-CICS                 VALUE 'P'.                  
                   88  PV-LEARN-CICS                VALUE 'L'.                  
               10  FILLER                      PIC  X(03)  VALUE SPACES.        
                                                                                
           05  PV-RUN-DATE                     PIC  X(10)  VALUE SPACES.        
           05  FILLER  REDEFINES  PV-RUN-DATE.                                  
               10  PV-RUN-CENTURY-YEAR         PIC  X(04).                      
               10  FILLER                      PIC  X.                  INKCS007
               10  PV-RUN-MONTH                PIC  X(02).                      
               10  FILLER                      PIC  X.                  INKCS007
               10  PV-RUN-DAY                  PIC  X(02).                      
                                                                                
           05  PV-DRS-ABEND-DISPLAY-LINE1.                                      
               10  FILLER                      PIC  X(14)  VALUE                
                   'DRRB ID = DRRB'.                                            
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(12)  VALUE                
                   'DRRB FUNC = '.                                              
               10  PV-DRS-DRRB-FUNC            PIC  X(04)  VALUE SPACE.         
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(11)  VALUE                
                   'DRRB RID = '.                                               
               10  PV-DRS-DRRB-RID             PIC  X(08)  VALUE SPACE.         
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(15)  VALUE                
                   'DRRB OPTIONS = '.                                           
               10  PV-DRS-DRRB-OPTIONS         PIC  X(08)  VALUE SPACE.         
                                                                                
           05  PV-DRS-ABEND-DISPLAY-LINE2.                                      
               10  FILLER                      PIC  X(10)  VALUE                
                   'DRRB RC = '.                                                
               10  PV-DRS-DRRB-RC              PIC  X(04)  VALUE SPACE.         
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(18)  VALUE                
                   'DRRB ATTR-ERROR = '.                                        
               10  PV-DRS-ATTR-ERROR           PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(12)  VALUE                
                   'DRRB DRDC = '.                                              
               10  PV-DRS-DRRB-DRDC            PIC  X(04)  VALUE SPACE.         
               10  FILLER                      PIC  X(02)  VALUE SPACE.         
               10  FILLER                      PIC  X(13)  VALUE                
                   'DRRB LINES = '.                                             
               10  PV-DRS-DRRB-LINES           PIC  X(04)  VALUE SPACE.         
                                                                        INKCS007
       01  HL1-HEADING-LINE-1.                                                  
           05  HL1-RUN-DATE.                                                    
               10  HL1-RUN-MONTH               PIC  9(02)  VALUE ZERO.          
               10  FILLER                      PIC  X(01)  VALUE '/'.           
               10  HL1-RUN-DAY                 PIC  9(02)  VALUE ZERO.          
               10  FILLER                      PIC  X(01)  VALUE '/'.           
               10  HL1-RUN-YEAR                PIC  9(04)  VALUE ZERO.          
           05  FILLER                          PIC  X(02)  VALUE SPACE.         
           05  HL1-RUN-TIME                    PIC  X(08)  VALUE SPACE.         
           05  FILLER                          PIC  X(27)  VALUE SPACE.         
           05  FILLER                          PIC  X(38)  VALUE                
               'PHYSICAL INVENTORY INVALID COMPARISONS'.                        
           05  FILLER                          PIC  X(26)  VALUE SPACE.         
           05  HL1-PROGRAM-NAME                PIC  X(08)  VALUE SPACE.         
           05  FILLER                          PIC  X(01)  VALUE '-'.           
           05  HL1-RPT-NBR                     PIC  9(01)  VALUE ZERO.          
           05  FILLER                          PIC  X(02)  VALUE SPACE.         
           05  FILLER                          PIC  X(05)  VALUE                
               'PAGE '.                                                         
           05  HL1-PAGE-NBR                    PIC  9(04)  VALUE ZERO.          
           05  FILLER                          PIC  X(07)  VALUE SPACE.         
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
W26600     05  FILLER                          PIC X(49)   VALUE SPACE.         
           05  FILLER                          PIC X(04)   VALUE 'FOR '.        
W26600     05  HL2-LOC-NBR                     PIC X(04)   VALUE SPACE.         
W26600     05  FILLER                          PIC X(01)   VALUE SPACE.         
           05  HL2-STORE-NAME                  PIC X(25)   VALUE SPACE.         
W26600     05  FILLER                          PIC X(46)   VALUE SPACE.         
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                          PIC X(13)   VALUE                
               'AREA NUMBER: '.                                                 
           05  HL3-AREA-NUMBER                 PIC Z(5)9   VALUE ZERO.          
           05  FILLER                          PIC X(113)  VALUE SPACE.         
                                                                                
W21732 01  DH1-DETAIL-HEADING-LINE-1.                                           
W21732     05  FILLER                          PIC  X(15)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(26)                       
W21732                  VALUE '         INVENTORY        '.                     
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(26)                       
W21732                  VALUE '        CORRECTION        '.                     
W21732     05  FILLER                          PIC  X(05)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(09)                       
W21732                  VALUE 'INVENTORY'.                                      
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE 'CORRECT'.                                        
W21732     05  FILLER                          PIC  X(05)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(09)                       
W21732                  VALUE 'INVENTORY'.                                      
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE 'CORRECT'.                                        
W21732     05  FILLER                          PIC  X(02)  VALUE SPACE.         
                                                                                
W21732 01  DH2-DETAIL-HEADING-LINE-2.                                           
W21732     05  FILLER                          PIC  X(01)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE '   LINE'.                                        
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(12)                       
W21732                  VALUE '  SKU OR UPC'.                                   
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(04)                       
W21732                  VALUE 'DEPT'.                                           
W21732     05  FILLER                          PIC  X(02)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(05)                       
W21732                  VALUE 'CLASS'.                                          
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(12)                       
W21732                  VALUE '  SKU OR UPC'.                                   
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(04)                       
W21732                  VALUE 'DEPT'.                                           
W21732     05  FILLER                          PIC  X(02)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(05)                       
W21732                  VALUE 'CLASS'.                                          
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE '    QTY'.                                        
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE '    QTY'.                                        
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE '  PRICE'.                                        
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(07)                       
W21732                  VALUE '  PRICE'.                                        
W21732     05  FILLER                          PIC  X(02)  VALUE SPACE.         
      *                                                                         
       01  DL-DETAIL-LINE.                                                      
W21732     05  FILLER                          PIC  X(01)  VALUE SPACE.         
           05  DL-SCAN-LINE-NBR                PIC  Z(6)9.                      
W21732     05  FILLER                          PIC  X(06)  VALUE SPACE.         
W21732     05  DL-INV-SKU-OR-UPC               PIC  X(13).                      
W21732     05  DL-INV-UPC REDEFINES DL-INV-SKU-OR-UPC                           
W21732                                         PIC  Z(13).                      
W21732     05  DL-INV-SKU REDEFINES DL-INV-SKU-OR-UPC.                          
W21732         10  DL-INV-FILLER               PIC  X(05).                      
W21732         10  DL-INV-SKU-NBR              PIC  9(08).                      
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  DL-INV-DEPT                     PIC  Z(04)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  DL-INV-CLASS                    PIC  Z(04)  VALUE SPACE.         
W21732     05  FILLER                          PIC  X(06)  VALUE SPACE.         
W21732     05  DL-CORR-SKU-OR-UPC              PIC  X(13).                      
W21732     05  DL-CORR-UPC REDEFINES DL-CORR-SKU-OR-UPC                         
W21732                                         PIC  Z(13).                      
W21732     05  DL-CORR-SKU REDEFINES DL-CORR-SKU-OR-UPC.                        
W21732         10  DL-CORR-FILLER              PIC  X(05).                      
W21732         10  DL-CORR-SKU-NBR             PIC  9(08).                      
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  DL-CORR-DEPT                    PIC  Z(04).                      
W21732     05  FILLER                          PIC  X(03)  VALUE SPACE.         
W21732     05  DL-CORR-CLASS                   PIC  Z(04).                      
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
           05  DL-INV-QTY                      PIC  Z(6)9.                      
W21732     05  FILLER                          PIC  X(07)  VALUE SPACE.         
           05  DL-CORR-QTY                     PIC  Z(6)9.                      
W21732     05  FILLER                          PIC  X(06)  VALUE SPACE.         
           05  DL-INV-PRICE                    PIC  Z(5).99.                    
W21732     05  FILLER                          PIC  X(06)  VALUE SPACE.         
           05  DL-CORR-PRICE                   PIC  Z(5).99.                    
W21732     05  FILLER                          PIC  X(02)  VALUE SPACE.         
                                                                                
       01  DL-END-OF-REPORT-LINE.                                               
           05  FILLER                          PIC X(50) VALUE SPACES.          
           05  FILLER                          PIC X(31)                        
               VALUE '*** E N D  O F  R E P O R T ***'.                         
           05  FILLER                          PIC X(51) VALUE SPACES.          
                                                                                
       01  ER-ERROR-MSGS.                                                       
           05  ER-NO-AREA-LINES                PIC X(36) VALUE                  
           ' NO DETAIL LINES FOUND FOR THIS AREA'.                              
                                                                                
      /                                                                         
      *---------------------------------------------------------------* INKCS007
      *  THIS IS THE WORKING STORAGE LAYOUT USED TO PASS DATA TO THE  *         
      *  PHYSICAL INVENTORY INVALID DATA COMPARISON PRINT PROGRAM.    *         
      *---------------------------------------------------------------* INKCS007
W26600     COPY INWS007.                                                INKCS007
                                                                                
      *---------------------------------------------------------------* INKCS007
      *    LAYOUT FOR THE DRS (DYNAMIC REPORT SYSTEM) COMMAREA        *         
      *---------------------------------------------------------------* INKCS007
           COPY DRRD001O.                                                       
                                                                                
      *---------------------------------------------------------------* INKCS007
      *    LAYOUT FOR THE DRS (DYNAMIC REPORT SYSTEM) DETAIL LINES    *         
      *---------------------------------------------------------------* INKCS007
           COPY DRRD002O.                                                       
                                                                                
      /                                                                 INKCS007
      *---------------------------------------------------------------* INKCS007
      *    ABEND PROCESSING COPYBOOK.                                 * INKCS007
      *---------------------------------------------------------------* INKCS007
                                                                        INKCS007
           COPY DPWS013.                                                INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *---------------------------------------------------------------* INKCS007
      *    DB2 AREA FOR TINVDTL                                       * INKCS007
      *---------------------------------------------------------------* INKCS007
           EXEC SQL                                                     INKCS007
                INCLUDE TINVDTL                                         INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
      *---------------------------------------------------------------* INKCS007
      *    DB2 AREA FOR TINVLOC                                       * INKCS007
      *---------------------------------------------------------------* INKCS007
           EXEC SQL                                                     INKCS007
                INCLUDE TINVLOC                                         INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732*    DB2 AREA FOR TSUMINV                                       * INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732     EXEC SQL                                                     INKCS007
W21732          INCLUDE TSUMINV                                         INKCS007
W21732     END-EXEC.                                                    INKCS007
W21732                                                                  INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732*    DB2 AREA FOR TUPCSKU                                       * INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732     EXEC SQL                                                     INKCS007
W21732          INCLUDE TUPCSKU                                         INKCS007
W21732     END-EXEC.                                                    INKCS007
W21732                                                                  INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732*    DB2 AREA FOR TSKXREF                                       * INKCS007
W21732*---------------------------------------------------------------* INKCS007
W21732     EXEC SQL                                                     INKCS007
W21732          INCLUDE TSKXREF                                         INKCS007
W21732     END-EXEC.                                                    INKCS007
W21732                                                                  INKCS007
W26600*---------------------------------------------------------------* INKCS011
W26600*    DB2 AREA FOR USER PRINTER DEFAULT ID                      *  INKCS011
W26600*---------------------------------------------------------------* INKCS011
W26600     EXEC SQL                                                     INKCS011
W26600          INCLUDE TUSER                                           INKCS011
W26600     END-EXEC.                                                    INKCS011
W26600                                                                  INKCS007
      *---------------------------------------------------------------* INKCS007
      *    DB2 AREA FOR COMMUNICATIONS                                * INKCS007
      *---------------------------------------------------------------* INKCS007
           EXEC SQL                                                     INKCS007
                INCLUDE SQLCA                                           INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
      *---------------------------------------------------------------* INKCS007
      *    TINVDTL SINGLE AREA CURSOR DECLARE.                        * INKCS007
      *---------------------------------------------------------------* INKCS007
           EXEC SQL                                                     INKCS007
                DECLARE SEL-AREA-LINE-CSR CURSOR                        INKCS007
                FOR SELECT                                              INKCS007
                      REC_UPC_NBR                                       INKCS007
W21732              , REC_SKU_NBR                                               
                    , REC_QTY                                           INKCS007
                    , REC_PR_AMT                                        INKCS007
                    , CORR_UPC_NBR                                      INKCS007
W21732              , CORR_SKU_NBR                                      INKCS007
                    , CORR_QTY                                          INKCS007
                    , CORR_PR_AMT                                       INKCS007
                    , SCAN_LINE_NBR                                     INKCS007
                FROM                                                    INKCS007
                      TINVDTL                                           INKCS007
                WHERE                                                   INKCS007
W26600                LOC_NBR    =   :INVDTL-LOC-NBR                    INKCS007
                  AND                                                   INKCS007
                      AREA_NBR     =   :INVDTL-AREA-NBR                 INKCS007
                ORDER BY                                                INKCS007
                      SCAN_LINE_NBR                                     INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
      *---------------------------------------------------------------* INKCS007
      *    TINVDTL ALL AREAS CURSOR DECLARE.                          * INKCS007
      *---------------------------------------------------------------* INKCS007
           EXEC SQL                                                     INKCS007
                DECLARE ALL-AREAS-LINE-CSR CURSOR                       INKCS007
                FOR SELECT                                              INKCS007
                      ID.AREA_NBR                                       INKCS007
                    , IL.TOT_REC_CORR_QTY                                  CL*18
                    , IL.TOT_ITM_CORR_QTY                                  CL*18
                    , IL.TOT_ITM_INVLID_QTY                                CL*18
                    , IL.TOT_REC_INVLID_QTY                                CL*18
                    , ID.REC_UPC_NBR                                    INKCS007
W21732              , ID.REC_SKU_NBR                                    INKCS007
                    , ID.REC_QTY                                        INKCS007
                    , ID.REC_PR_AMT                                     INKCS007
                    , ID.CORR_UPC_NBR                                   INKCS007
W21732              , ID.CORR_SKU_NBR                                   INKCS007
                    , ID.CORR_QTY                                       INKCS007
                    , ID.CORR_PR_AMT                                    INKCS007
                    , ID.SCAN_LINE_NBR                                  INKCS007
                FROM                                                    INKCS007
                      TINVDTL ID                                        INKCS007
                    , TINVLOC IL                                        INKCS007
                WHERE                                                   INKCS007
W26600                ID.LOC_NBR    =   :INVDTL-LOC-NBR                 INKCS007
W26600           AND  ID.LOC_NBR    =   IL.LOC_NBR                      INKCS007
                 AND  ID.AREA_NBR   =   IL.AREA_NBR                     INKCS007
                ORDER BY                                                INKCS007
                      ID.AREA_NBR                                       INKCS007
                    , ID.SCAN_LINE_NBR                                  INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
       LINKAGE SECTION.                                                 INKCS007
                                                                        INKCS007
       01  DFHCOMMAREA.                                                 INKCS007
           05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS007
                                              DEPENDING ON EIBCALEN.    INKCS007
               10  FILLER                     PIC X.                    INKCS007
                                                                        INKCS007
       PROCEDURE DIVISION.                                              INKCS007
                                                                        INKCS007
       0000-MAIN-MODULE.                                                INKCS007
                                                                        INKCS007
           SET PS-NO-ERROR          TO TRUE.                                    
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           MOVE PC-TERM TO DRS01-DRRB-FUNC.                                     
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      * RETRIEVE PASSED DATA AND SET UP REPORT HEADING LINE 1.                  
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
                                                                                
           PERFORM 1100-RETRIEVE-STORED-DATA.                                   
                                                                                
           PERFORM 1200-GET-DATE-TIME.                                          
      *                                                                         
      *    SET UP THE INIT AND REQUEST BLOCKS FOR DRS                           
           PERFORM 1300-INITIALIZE-DRS.                                         
                                                                                
      *    FORMAT HEADING LINE 1                                                
           MOVE PV-RUN-MONTH               TO  HL1-RUN-MONTH.                   
           MOVE PV-RUN-DAY                 TO  HL1-RUN-DAY.                     
           MOVE PV-RUN-CENTURY-YEAR        TO  HL1-RUN-YEAR.                    
           MOVE PC-CURRENT-PROGRAM-NAME    TO  HL1-PROGRAM-NAME.                
           MOVE 1                          TO  HL1-RPT-NBR.                     
      /                                                                         
      *----------------------------------------------------------------*        
      * RETRIEVE DATA PASSED FROM THE STARTING PROGRAM.                         
      *----------------------------------------------------------------*        
       1100-RETRIEVE-STORED-DATA.                                               
                                                                                
           EXEC CICS                                                            
               RETRIEVE                                                         
                   INTO  (IN007-COMMAREA)                                       
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1100-SET-KEY-FIELDS'    TO DP013-PARAGRAPH             
                   MOVE 'BAD COMMAREA'     TO DP013-MESSAGE-TEXT (1)            
                   SET DP013-CICS-ABEND                                         
                       DP013-NO-ROLLBACK                                        
                       DP013-LINK-RETURN          TO TRUE                       
                   PERFORM DP014-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
W26600     MOVE IN007-STORE-NBR     TO HL2-LOC-NBR.                             
           MOVE IN007-STORE-NAME    TO HL2-STORE-NAME.                          
                                                                                
      *----------------------------------------------------------------*        
      * FORMAT THE REPORT HEADING DATE AND TIME FIELDS.                         
      *----------------------------------------------------------------*        
       1200-GET-DATE-TIME.                                                      
                                                                                
           EXEC CICS                                                            
               ASKTIME ABSTIME (PV-ABSTIME)                                     
           END-EXEC.                                                            
                                                                                
           EXEC CICS                                                            
               FORMATTIME ABSTIME  (PV-ABSTIME)                                 
                          YYYYMMDD (PV-RUN-DATE)                                
                          DATESEP                                               
                          TIME     (HL1-RUN-TIME)                               
                          TIMESEP  (':')                                        
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  INITIALIZE THE DRS COMM AREAS OPEN DRS CONNECTION                      
      *----------------------------------------------------------------*        
       1300-INITIALIZE-DRS.                                                     
                                                                                
           MOVE PC-INIT              TO  DRS01-DRRB-FUNC.                       
           MOVE LOW-VALUES           TO  DRS01-DRRD-OPTIONS.                    
           MOVE 'A'                  TO  DRS02-DRIB-CLASS.                      
                                                                                
           EXEC CICS ASSIGN                                                     
                     APPLID(PV-SYSTEM-ID)                                       
W26600               USERID(PV-USER-ID)                                         
           END-EXEC.                                                            
                                                                                
W26600***  SET DEFAULT PRINTER ID FOR CORPORATE STORE, TEST CICS,               
W26600***  AND CORPORATE USERS....                                              
           IF  IN007-STORE-NBR = PC-CORPORATE-NBR                               
             OR PV-TEST-CICS                                                    
W26600         MOVE 'LOCAL'          TO DRS02-DRIB-DEST                         
           ELSE                                                                 
W26600         MOVE IN007-STORE-NBR  TO PV-LOC-NBR                              
W26600         MOVE PV-LOC-NBR-NUM   TO PV-STORE-ID                             
               MOVE PV-DEST-ID       TO DRS02-DRIB-DEST                         
           END-IF.                                                              
                                                                                
W26600***  CAPTURE THE USER'S PRINTER ID, USE THE DEFAULT IF NONE               
W26600***  EXISTS....                                                           
W26600     MOVE PV-USER-ID           TO USER-USER-CODE.                         
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         SELECT DFLT_PRTR_ID                                              
W26600         INTO :USER-DFLT-PRTR-ID                                          
W26600         FROM TUSER                                                       
W26600         WHERE USER_CODE = :USER-USER-CODE                                
W26600         WITH UR                                                          
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600         WHEN SQLCODE = ZERO                                              
W26600             IF USER-DFLT-PRTR-ID NOT = SPACES                            
W26600                 MOVE USER-DFLT-PRTR-ID TO DRS02-DRIB-DEST                
W26600             END-IF                                                       
W26600         WHEN OTHER                                                       
W26600             CONTINUE                                                     
W26600     END-EVALUATE.                                                        
W26600                                                                          
           MOVE 'N'                  TO DRS02-DRIB-HOLD                         
           MOVE DRS02-DRIB-HEADER    TO DRS01-COMM-DRIB-HDR.                    
           MOVE DRS02-DRIB-ATTRIB    TO DRS01-COMM-DRIB-AT1.                    
           MOVE 'SSTD1'              TO DRS02-DRIB-WRTR.                        
                                                                                
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE PC-PUT                     TO  DRS01-DRRB-FUNC.                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*INKCS007
      *    DETERMINE IF THE REPORT REQUEST IS FOR ALL AREAS OR FOR     *        
      *    SELECTED AREAS AND PROCESS ACCORDINGLY.                     *        
      *----------------------------------------------------------------*INKCS007
       2000-MAIN-PROCESS.                                                       
                                                                                
           IF IN007-PRINT-ALL-AREAS                                             
               PERFORM 3000-PROCESS-ALL-AREAS                                   
           ELSE                                                                 
               PERFORM 4000-PROCESS-SEL-AREAS                                   
                   VARYING PV-AREA-SUB                                  INKCS007
                   FROM 1 BY 1                                          INKCS007
                   UNTIL PV-AREA-SUB > PC-MAX-AREA-NBRS                 INKCS007
                     OR  IN007-AREA-NBR (PV-AREA-SUB) = ZERO            INKCS007
           END-IF.                                                              
                                                                                
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    CREATE A REPORT FOR ALL AREA NUMBERS BY READING THE AREAS   *        
      *    AND LINES FROM THE DATA BASE, MOVING THEM TO REPORT DETAIL           
      *    LINES, AND THEN PRINTING THESE LINES.                       *        
      *----------------------------------------------------------------*INKCS007
       3000-PROCESS-ALL-AREAS.                                          INKCS007
                                                                        INKCS007
           MOVE ZERO                 TO PV-ITEMS-PROCESSED.             INKCS007
           MOVE ZERO                 TO PV-PAGE-COUNT.                  INKCS007
           MOVE +90                  TO PV-LINE-COUNT.                          
                                                                        INKCS007
           PERFORM 3010-OPEN-ALL-CURSOR.                                INKCS007
                                                                        INKCS007
           SET PS-ITEMS-LEFT-ON-DB   TO TRUE.                                   
           PERFORM                                                              
               UNTIL  PS-NO-ITEMS-LEFT-ON-DB                                    
                                                                                
               PERFORM 3030-FETCH-LOOP                                          
                                                                                
               IF  PS-ITEMS-LEFT-ON-DB                                          
                   IF (PV-NBR-TO-CORRECT GREATER THAN ZERO                      
                     OR (IN007-DISPLAY-COMPLETE                                 
                         AND PV-NBR-FALLOUTS GREATER THAN ZERO))                
                       IF PV-HOLD-AREA-NBR = INVDTL-AREA-NBR                    
                           PERFORM 5000-FORMAT-ITEM-LINE                        
                       ELSE                                                     
                           MOVE +90  TO PV-LINE-COUNT                           
                           MOVE INVDTL-AREA-NBR TO HL3-AREA-NUMBER          INKC
                                                   PV-HOLD-AREA-NBR             
                           PERFORM 5000-FORMAT-ITEM-LINE                        
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                        INKCS007
           PERFORM 3020-CLOSE-ALL-CURSOR.                               INKCS007
                                                                        INKCS007
           IF  PV-ITEMS-PROCESSED = ZERO                                INKCS007
               PERFORM 5100-NO-AREA-LINES                                       
           END-IF.                                                      INKCS007
                                                                        INKCS007
           PERFORM 5200-END-OF-RPT-LINE.                                        
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      * OPEN THE SELECT ALL AREA LINES CURSOR.                         *INKCS007
      *----------------------------------------------------------------*INKCS007
       3010-OPEN-ALL-CURSOR.                                            INKCS007
                                                                        INKCS007
           PERFORM                                                      INKCS007
               WITH TEST AFTER                                          INKCS007
               VARYING PV-RETRY-COUNTER                                 INKCS007
               FROM 1 BY 1                                              INKCS007
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               INKCS007
                         OR (SQLCODE = ZERO))                           INKCS007
                                                                        INKCS007
W26600         MOVE IN007-STORE-NBR TO PV-LOC-NBR                               
W26600         MOVE PV-LOC-NBR-NUM  TO INVDTL-LOC-NBR                           
W26600                                                                          
               EXEC SQL                                                 INKCS007
                    OPEN ALL-AREAS-LINE-CSR                             INKCS007
               END-EXEC                                                 INKCS007
                                                                        INKCS007
               EVALUATE TRUE                                            INKCS007
                   WHEN SQLCODE = ZERO                                  INKCS007
                   WHEN SQLCODE = -904                                  INKCS007
                   WHEN SQLCODE = -913                                  INKCS007
                       CONTINUE                                         INKCS007
                   WHEN SQLWARN0 NOT = SPACES                           INKCS007
                   WHEN SQLCODE < ZERO                                  INKCS007
                   WHEN SQLCODE > ZERO                                  INKCS007
                       MOVE '3010-OPEN-ALL-CURSOR'                      INKCS007
                                         TO DP013-PARAGRAPH             INKCS007
                       MOVE 'OPEN SELECT ALL AREA LINES CURSOR - '      INKCS007
                                         TO DP013-MESSAGE-TEXT (1)      INKCS007
                       MOVE SQLCA        TO DP013-SQLCA                 INKCS007
                       MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)    INKCS007
                       SET DP013-LINK-RETURN                                    
                           DP013-DB2-ABEND                                      
                           DP013-NO-ROLLBACK TO TRUE                            
                       PERFORM DP014-0000-PROCESS-ABEND                         
               END-EVALUATE                                             INKCS007
           END-PERFORM.                                                 INKCS007
                                                                        INKCS007
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        INKCS007
               MOVE '3010-OPEN-ALL-CURSOR' TO DP013-PARAGRAPH           INKCS007
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'   INKCS007
                                         TO DP013-MESSAGE-TEXT (1)      INKCS007
               MOVE SQLCA                TO DP013-SQLCA                 INKCS007
               MOVE 'TINVDTL '           TO DP013-DB2-TABLE-NAME (1)    INKCS007
               SET DP013-LINK-RETURN                                            
                   DP013-DB2-ABEND                                              
                   DP013-NO-ROLLBACK TO TRUE                                    
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                      INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *                                                                *INKCS007
      * CLOSE THE SELECT ALL AREA LINES CURSOR.                        *INKCS007
      *                                                                *INKCS007
      *----------------------------------------------------------------*INKCS007
       3020-CLOSE-ALL-CURSOR.                                           INKCS007
                                                                        INKCS007
           EXEC SQL                                                     INKCS007
                CLOSE ALL-AREAS-LINE-CSR                                INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
           EVALUATE TRUE                                                INKCS007
               WHEN SQLCODE = ZERO                                      INKCS007
                   CONTINUE                                             INKCS007
               WHEN SQLWARN0 NOT = SPACES                               INKCS007
               WHEN SQLCODE < ZERO                                      INKCS007
               WHEN SQLCODE > ZERO                                      INKCS007
                   MOVE '3020-CLOSE-ALL-CURSOR'                         INKCS007
                                     TO DP013-PARAGRAPH                 INKCS007
                   MOVE 'CLOSE SELECT ALL AREA LINES CURSOR'            INKCS007
                                     TO DP013-MESSAGE-TEXT (1)          INKCS007
                   MOVE SQLCA        TO DP013-SQLCA                     INKCS007
                   MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS007
                   SET DP013-LINK-RETURN                                        
                       DP013-DB2-ABEND                                          
                       DP013-NO-ROLLBACK TO TRUE                                
                   PERFORM DP014-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    LOOP THE FETCH TO ENSURE ONLY CERTAIN DATA RETURNED FOR      INKCS007
      *    STORE PERSONNEL.                                             INKCS007
      *----------------------------------------------------------------*INKCS007
       3030-FETCH-LOOP.                                                 INKCS007
                                                                        INKCS007
           IF  IN007-LOCATION-TYPE EQUAL PC-CORPORATE                      CL*18
               PERFORM 3100-FETCH-ALL-ITEMS                                CL*18
               MOVE INVLOC-TOT-REC-INVLID-QTY                              CL*18
                                     TO  PV-NBR-FALLOUTS                   CL*18
               COMPUTE PV-NBR-TO-CORRECT =                                 CL*18
                       INVLOC-TOT-REC-INVLID-QTY -                         CL*18
                       INVLOC-TOT-REC-CORR-QTY                             CL*18
           ELSE                                                            CL*18
               SET PS-NO-STORE-REC   TO  TRUE                              CL*18
               PERFORM UNTIL PS-STORE-REC                                  CL*18
                   PERFORM 3100-FETCH-ALL-ITEMS                            CL*18
                   IF  INVLOC-TOT-ITM-INVLID-QTY GREATER THAN ZERO         CL*34
                       COMPUTE PV-NBR-TO-CORRECT =                         CL*18
                               INVLOC-TOT-ITM-INVLID-QTY -                 CL*18
                               INVLOC-TOT-ITM-CORR-QTY                     CL*18
                       MOVE INVLOC-TOT-ITM-INVLID-QTY                      CL*18
                                     TO  PV-NBR-FALLOUTS                   CL*18
                       SET PS-STORE-REC                                    CL*18
                                     TO  TRUE                              CL*18
                   END-IF                                                  CL*18
               END-PERFORM                                                 CL*18
           END-IF.                                                         CL*18
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    FETCH THE SELECT ALL AREA LINES CURSOR.                      INKCS007
      *----------------------------------------------------------------*INKCS007
       3100-FETCH-ALL-ITEMS.                                            INKCS007
                                                                        INKCS007
           EXEC SQL                                                     INKCS007
                FETCH                                                   INKCS007
                      ALL-AREAS-LINE-CSR                                INKCS007
                INTO                                                    INKCS007
                     :INVDTL-AREA-NBR                                   INKCS007
                   , :INVLOC-TOT-REC-CORR-QTY                           INKCS007
                   , :INVLOC-TOT-ITM-CORR-QTY                           INKCS007
                   , :INVLOC-TOT-ITM-INVLID-QTY                         INKCS007
                   , :INVLOC-TOT-REC-INVLID-QTY                         INKCS007
                   , :INVDTL-REC-UPC-NBR                                INKCS007
W21732             , :INVDTL-REC-SKU-NBR                                INKCS007
                   , :INVDTL-REC-QTY                                    INKCS007
                   , :INVDTL-REC-PR-AMT                                 INKCS007
                   , :INVDTL-CORR-UPC-NBR                               INKCS007
W21732             , :INVDTL-CORR-SKU-NBR                               INKCS007
                   , :INVDTL-CORR-QTY                                   INKCS007
                   , :INVDTL-CORR-PR-AMT                                INKCS007
                   , :INVDTL-SCAN-LINE-NBR                              INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
           EVALUATE TRUE                                                INKCS007
               WHEN SQLCODE = ZERO                                      INKCS007
                    ADD +1 TO PV-ITEMS-PROCESSED                        INKCS007
               WHEN SQLCODE = +100                                      INKCS007
                    SET PS-NO-ITEMS-LEFT-ON-DB TO TRUE                  INKCS007
                    SET PS-STORE-REC           TO TRUE                  INKCS007
               WHEN SQLWARN0 NOT = SPACE                                INKCS007
               WHEN SQLCODE NOT = ZERO                                  INKCS007
                    MOVE '3110-FETCH-ALL-ITEMS'                         INKCS007
                                     TO DP013-PARAGRAPH                 INKCS007
                    MOVE 'FETCH SELECT ALL AREA LINES CURSOR'           INKCS007
                                     TO DP013-MESSAGE-TEXT (1)          INKCS007
                    MOVE SQLCA       TO DP013-SQLCA                     INKCS007
                    SET DP013-LINK-RETURN                                       
                        DP013-DB2-ABEND                                         
                        DP013-NO-ROLLBACK TO TRUE                               
                    PERFORM DP014-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    CREATE A REPORT FOR A SELECTED AREA NUMBER BY READING THE   *        
      *    LINES FROM THE DATA BASE, MOVING THEM TO REPORT DETAIL      *        
      *    LINES, AND THEN PRINTING THESE LINES.                       *        
      *----------------------------------------------------------------*INKCS007
       4000-PROCESS-SEL-AREAS.                                          INKCS007
                                                                        INKCS007
           MOVE ZERO                 TO PV-ITEMS-PROCESSED.             INKCS007
           MOVE ZERO                 TO PV-PAGE-COUNT.                  INKCS007
           MOVE +90                  TO PV-LINE-COUNT.                          
           MOVE IN007-AREA-NBR (PV-AREA-SUB)                            INKCS007
                                     TO INVDTL-AREA-NBR                 INKCS007
                                        HL3-AREA-NUMBER.                        
                                                                        INKCS007
           PERFORM 4010-OPEN-SEL-CURSOR.                                INKCS007
                                                                        INKCS007
           SET PS-ITEMS-LEFT-ON-DB   TO TRUE.                                   
           PERFORM                                                              
               UNTIL  PS-NO-ITEMS-LEFT-ON-DB                                    
                                                                                
               PERFORM 4100-FETCH-SEL-ITEMS                                     
                                                                                
               IF  PS-ITEMS-LEFT-ON-DB                                          
                   PERFORM 5000-FORMAT-ITEM-LINE                                
               END-IF                                                           
           END-PERFORM.                                                         
                                                                        INKCS007
           PERFORM 4020-CLOSE-SEL-CURSOR.                               INKCS007
                                                                        INKCS007
           IF  PV-ITEMS-PROCESSED = ZERO                                INKCS007
               PERFORM 5100-NO-AREA-LINES                                       
           END-IF.                                                      INKCS007
                                                                        INKCS007
           PERFORM 5200-END-OF-RPT-LINE.                                        
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      * OPEN THE SELECT AREA LINES CURSOR.                             *INKCS007
      *----------------------------------------------------------------*INKCS007
       4010-OPEN-SEL-CURSOR.                                            INKCS007
                                                                        INKCS007
           PERFORM                                                      INKCS007
               WITH TEST AFTER                                          INKCS007
               VARYING PV-RETRY-COUNTER                                 INKCS007
               FROM 1 BY 1                                              INKCS007
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               INKCS007
                         OR (SQLCODE = ZERO))                           INKCS007
                                                                        INKCS007
W26600         MOVE IN007-STORE-NBR TO PV-LOC-NBR                               
W26600         MOVE PV-LOC-NBR-NUM  TO INVDTL-LOC-NBR                           
W26600                                                                          
               EXEC SQL                                                 INKCS007
                    OPEN SEL-AREA-LINE-CSR                              INKCS007
               END-EXEC                                                 INKCS007
                                                                        INKCS007
               EVALUATE TRUE                                            INKCS007
                   WHEN SQLCODE = ZERO                                  INKCS007
                   WHEN SQLCODE = -904                                  INKCS007
                   WHEN SQLCODE = -913                                  INKCS007
                       CONTINUE                                         INKCS007
                   WHEN SQLWARN0 NOT = SPACES                           INKCS007
                   WHEN SQLCODE < ZERO                                  INKCS007
                   WHEN SQLCODE > ZERO                                  INKCS007
                       MOVE '4010-OPEN-SEL-CURSOR'                      INKCS007
                                         TO DP013-PARAGRAPH             INKCS007
                       MOVE 'OPEN SELECT AREA LINES CURSOR - '          INKCS007
                                         TO DP013-MESSAGE-TEXT (1)      INKCS007
                       MOVE SQLCA        TO DP013-SQLCA                 INKCS007
                       MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)    INKCS007
                       SET DP013-LINK-RETURN                                    
                           DP013-DB2-ABEND                                      
                           DP013-NO-ROLLBACK TO TRUE                            
                       PERFORM DP014-0000-PROCESS-ABEND                         
               END-EVALUATE                                             INKCS007
           END-PERFORM.                                                 INKCS007
                                                                        INKCS007
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        INKCS007
               MOVE '4010-OPEN-SEL-CURSOR' TO DP013-PARAGRAPH           INKCS007
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'   INKCS007
                                         TO DP013-MESSAGE-TEXT (1)      INKCS007
               MOVE SQLCA                TO DP013-SQLCA                 INKCS007
               MOVE 'TINVDTL '           TO DP013-DB2-TABLE-NAME (1)    INKCS007
               SET DP013-LINK-RETURN                                            
                   DP013-DB2-ABEND                                              
                   DP013-NO-ROLLBACK TO TRUE                                    
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                      INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *                                                                *INKCS007
      * CLOSE THE SELECT AREA LINES CURSOR.                            *INKCS007
      *                                                                *INKCS007
      *----------------------------------------------------------------*INKCS007
       4020-CLOSE-SEL-CURSOR.                                           INKCS007
                                                                        INKCS007
           EXEC SQL                                                     INKCS007
                CLOSE SEL-AREA-LINE-CSR                                 INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
           EVALUATE TRUE                                                INKCS007
               WHEN SQLCODE = ZERO                                      INKCS007
                   CONTINUE                                             INKCS007
               WHEN SQLWARN0 NOT = SPACES                               INKCS007
               WHEN SQLCODE < ZERO                                      INKCS007
               WHEN SQLCODE > ZERO                                      INKCS007
                   MOVE '4020-CLOSE-SEL-CURSOR'                         INKCS007
                                     TO DP013-PARAGRAPH                 INKCS007
                   MOVE 'CLOSE SELECT AREA LINES CURSOR'                INKCS007
                                     TO DP013-MESSAGE-TEXT (1)          INKCS007
                   MOVE SQLCA        TO DP013-SQLCA                     INKCS007
                   MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS007
                   SET DP013-LINK-RETURN                                        
                       DP013-DB2-ABEND                                          
                       DP013-NO-ROLLBACK TO TRUE                                
                   PERFORM DP014-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    FETCH THE SELECT AREA LINES CURSOR.                          INKCS007
      *----------------------------------------------------------------*INKCS007
       4100-FETCH-SEL-ITEMS.                                            INKCS007
                                                                        INKCS007
           EXEC SQL                                                     INKCS007
                FETCH                                                   INKCS007
                      SEL-AREA-LINE-CSR                                 INKCS007
                INTO                                                    INKCS007
                     :INVDTL-REC-UPC-NBR                                INKCS007
W21732             , :INVDTL-REC-SKU-NBR                                INKCS007
                   , :INVDTL-REC-QTY                                    INKCS007
                   , :INVDTL-REC-PR-AMT                                 INKCS007
                   , :INVDTL-CORR-UPC-NBR                               INKCS007
W21732             , :INVDTL-CORR-SKU-NBR                               INKCS007
                   , :INVDTL-CORR-QTY                                   INKCS007
                   , :INVDTL-CORR-PR-AMT                                INKCS007
                   , :INVDTL-SCAN-LINE-NBR                              INKCS007
           END-EXEC.                                                    INKCS007
                                                                        INKCS007
           EVALUATE TRUE                                                INKCS007
               WHEN SQLCODE = ZERO                                      INKCS007
                    ADD +1 TO PV-ITEMS-PROCESSED                        INKCS007
               WHEN SQLCODE = +100                                      INKCS007
                    SET PS-NO-ITEMS-LEFT-ON-DB TO TRUE                  INKCS007
               WHEN SQLWARN0 NOT = SPACE                                INKCS007
               WHEN SQLCODE NOT = ZERO                                  INKCS007
                    MOVE '4100-FETCH-SEL-ITEMS'                         INKCS007
                                     TO DP013-PARAGRAPH                 INKCS007
                    MOVE 'FETCH SELECT AREA LINES CURSOR'               INKCS007
                                     TO DP013-MESSAGE-TEXT (1)          INKCS007
                    MOVE SQLCA       TO DP013-SQLCA                     INKCS007
                    SET DP013-LINK-RETURN                                       
                        DP013-DB2-ABEND                                         
                        DP013-NO-ROLLBACK TO TRUE                               
                    PERFORM DP014-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                INKCS007
                                                                        INKCS007
      /                                                                 INKCS007
      *----------------------------------------------------------------*INKCS007
      *    FORMAT AND PRINT A REPORT DETAIL ITEM LINE.                 *        
      *----------------------------------------------------------------*INKCS007
       5000-FORMAT-ITEM-LINE.                                           INKCS007
                                                                        INKCS007
           MOVE INVDTL-SCAN-LINE-NBR     TO  DL-SCAN-LINE-NBR.          INKCS007
W21732     IF INVDTL-REC-SKU-NBR GREATER THAN PC-00000000               INKCS007
W21732         MOVE INVDTL-REC-SKU-NBR   TO  DL-INV-SKU-NBR             INKCS007
W21732         MOVE SPACES               TO  DL-INV-FILLER              INKCS007
W21732         PERFORM 5030-GET-INV-FROM-TSUMINV                        INKCS007
           ELSE                                                         INKCS007
W21732         MOVE INVDTL-REC-UPC-NBR   TO  DL-INV-UPC                 INKCS007
W21732*        MOVE INVDTL-REC-UPC-NBR   TO  TUPCSKU-UPC-NBR                    
W21732         PERFORM 5020-GET-DEPT-CLASS-FROM-UPC                     INKCS007
W21732         IF  SQLCODE = 0                                          INKCS007
W21732             MOVE SKXREF-DEPT          TO  DL-INV-DEPT            INKCS007
W21732             MOVE SKXREF-SUBCLASS      TO  DL-INV-CLASS           INKCS007
               ELSE                                                             
                   MOVE 0                    TO  DL-INV-DEPT,                   
                                                 DL-INV-CLASS                   
               END-IF                                                           
           END-IF.                                                      INKCS007
                                                                                
W21732     IF  INVDTL-CORR-SKU-NBR GREATER THAN PC-00000000             INKCS007
W21732         MOVE INVDTL-CORR-SKU-NBR  TO  DL-CORR-SKU-NBR            INKCS007
W21732         MOVE SPACES               TO  DL-CORR-FILLER             INKCS007
W21732         PERFORM 5010-GET-CORR-FROM-TSKXREF                       INKCS007
           ELSE                                                         INKCS007
W21732         MOVE INVDTL-CORR-UPC-NBR  TO  DL-CORR-UPC                INKCS007
W21732         MOVE INVDTL-CORR-UPC-NBR  TO  TUPCSKU-UPC-NBR                    
W21732         PERFORM 5020-GET-DEPT-CLASS-FROM-UPC                     INKCS007
W21732         IF  SQLCODE = 0                                          INKCS007
W21732             MOVE SKXREF-DEPT          TO  DL-CORR-DEPT           INKCS007
W21732             MOVE SKXREF-SUBCLASS      TO  DL-CORR-CLASS          INKCS007
W21732         ELSE                                                     INKCS007
W21732             MOVE 0                    TO  DL-CORR-DEPT,          INKCS007
W21732                                           DL-CORR-CLASS          INKCS007
W21732         END-IF                                                   INKCS007
           END-IF.                                                      INKCS007
                                                                                
           MOVE INVDTL-REC-QTY           TO  DL-INV-QTY.                INKCS007
           MOVE INVDTL-REC-PR-AMT        TO  DL-INV-PRICE.              INKCS007
           MOVE INVDTL-CORR-QTY          TO  DL-CORR-QTY.               INKCS007
           MOVE INVDTL-CORR-PR-AMT       TO  DL-CORR-PRICE.             INKCS007
                                                                                
           IF (PV-LINE-COUNT + 1) > PC-LINES-PER-PAGE                           
               PERFORM 7000-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           ADD 1                         TO PV-LINE-COUNT.                      
           MOVE DL-DETAIL-LINE           TO DRS01-COMM-DATA.                    
           MOVE PC-SINGLE-SPACE          TO DRS01-COMM-CTL.                     
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                        INKCS007
       EJECT                                                                    
                                                                                
W21732*----------------------------------------------------------------*INKCS007
W21732* GET DEPARTMENT AND CLASS FROM TSKXREF TABLE SINCE THESE FIELDS *        
W21732* ARE NOT PRESENT IN TINVDTL TABLE.                              *        
W21732*----------------------------------------------------------------*INKCS007
W21732 5010-GET-CORR-FROM-TSKXREF.                                              
W21732                                                                          
W21732     MOVE  INVDTL-CORR-SKU-NBR      TO  SKXREF-SKU.                       
W21732                                                                          
W21732     EXEC SQL                                                             
W21732          SELECT DEPT                                                     
W21732               , SUBCLASS                                                 
W21732            INTO :SKXREF-DEPT                                             
W21732               , :SKXREF-SUBCLASS                                         
W21732            FROM TSKXREF                                                  
W21732           WHERE SKU             = :SKXREF-SKU                            
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EVALUATE TRUE                                                INKCS007
W21732         WHEN SQLCODE = ZERO                                      INKCS007
W21732              MOVE SKXREF-DEPT          TO  DL-CORR-DEPT          INKCS007
W21732              MOVE SKXREF-SUBCLASS      TO  DL-CORR-CLASS         INKCS007
W21732         WHEN SQLCODE = 100                                       INKCS007
W21732              MOVE ZEROES               TO  DL-CORR-DEPT          INKCS007
W21732              MOVE ZEROES               TO  DL-CORR-CLASS         INKCS007
W21732         WHEN OTHER                                               INKCS007
W21732              MOVE '5010-GET-CORR-FROM-TSKXREF'                   INKCS007
W21732                               TO DP013-PARAGRAPH                 INKCS007
W21732              MOVE 'NO MATCHING ROW IN TSKXREF TABLE'             INKCS007
W21732                               TO DP013-MESSAGE-TEXT (1)          INKCS007
W21732              MOVE SQLCA       TO DP013-SQLCA                     INKCS007
W21732              SET DP013-LINK-RETURN                                       
W21732                  DP013-DB2-ABEND                                         
W21732                  DP013-NO-ROLLBACK TO TRUE                               
W21732              PERFORM DP014-0000-PROCESS-ABEND                            
W21732     END-EVALUATE.                                                INKCS007
W21732                                                                          
W21732*----------------------------------------------------------------*INKCS007
W21732* GET DEPARTMENT AND CLASS BY JOINING TSKXREF AND TUPCSKU TABLE  *        
W21732* SINCE DEPARTMENT AND SUBCLASS ARE NOT PRESENT IN TINVDTL TABLE.*        
W21732*----------------------------------------------------------------*INKCS007
W21732 5020-GET-DEPT-CLASS-FROM-UPC.                                            
W21732                                                                          
W21732     EXEC SQL                                                             
W21732          SELECT A.DEPT                                                   
W21732               , A.SUBCLASS                                               
W21732            INTO :SKXREF-DEPT                                             
W21732               , :SKXREF-SUBCLASS                                         
W21732            FROM TSKXREF A                                                
W21732               , TUPCSKU B                                                
W21732           WHERE B.UPC_NBR       = :TUPCSKU-UPC-NBR                       
W21732             AND A.ITM_NBR       = B.ITEM_NBR                             
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EVALUATE TRUE                                                INKCS007
W21732         WHEN SQLCODE = ZERO                                      INKCS007
               WHEN SQLCODE = 100                                               
                    CONTINUE                                                    
W21732         WHEN OTHER                                               INKCS007
W21732              MOVE '5020-GET-DEPT-CLASS-FROM-UPC'                 INKCS007
W21732                               TO DP013-PARAGRAPH                 INKCS007
W21732              MOVE 'NO MATCHING ROW FOUND IN TABLE'               INKCS007
W21732                               TO DP013-MESSAGE-TEXT (1)          INKCS007
W21732              MOVE SQLCA       TO DP013-SQLCA                     INKCS007
W21732              SET DP013-LINK-RETURN                                       
W21732                  DP013-DB2-ABEND                                         
W21732                  DP013-NO-ROLLBACK TO TRUE                               
W21732              PERFORM DP014-0000-PROCESS-ABEND                            
W21732     END-EVALUATE.                                                INKCS007
                                                                                
W21732*----------------------------------------------------------------*INKCS007
W21732* GET DEPARTMENT AND CLASS FROM TSUMINV TABLE SINCE THESE FIELDS *        
W21732* ARE NOT PRESENT IN TINVDTL TABLE.                              *        
W21732*----------------------------------------------------------------*INKCS007
W21732 5030-GET-INV-FROM-TSUMINV.                                               
W21732                                                                          
W26600     MOVE  IN007-STORE-NBR         TO  PV-LOC-NBR.                        
W26600     MOVE  PV-LOC-NBR-NUM          TO  SUMINV-LOC-NBR.                    
W21732     MOVE  INVDTL-REC-SKU-NBR      TO  SUMINV-SKU-NBR.                    
W21732                                                                          
W21732     EXEC SQL                                                             
W21732          SELECT DEPT_NBR                                                 
W21732               , SUB_CL_NBR                                               
W21732            INTO :SUMINV-DEPT-NBR                                         
W21732               , :SUMINV-SUB-CL-NBR                                       
W21732            FROM TSUMINV                                                  
W21732           WHERE SKU_NBR         = :SUMINV-SKU-NBR                        
W26600             AND LOC_NBR       = :SUMINV-LOC-NBR                          
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EVALUATE TRUE                                                INKCS007
W21732         WHEN SQLCODE = ZERO                                      INKCS007
W21732              MOVE SUMINV-DEPT-NBR      TO  DL-INV-DEPT           INKCS007
W21732              MOVE SUMINV-SUB-CL-NBR    TO  DL-INV-CLASS          INKCS007
W21732         WHEN SQLCODE = 100                                       INKCS007
W21732              MOVE ZEROES               TO  DL-INV-DEPT           INKCS007
W21732              MOVE ZEROES               TO  DL-INV-CLASS          INKCS007
W21732         WHEN OTHER                                               INKCS007
W21732              MOVE '5030-GET-INV-FROM-TSUMINV'                    INKCS007
W21732                               TO DP013-PARAGRAPH                 INKCS007
W21732              MOVE 'NO MATCHING ROW IN TSUMINV TABLE'             INKCS007
W21732                               TO DP013-MESSAGE-TEXT (1)          INKCS007
W21732              MOVE SQLCA       TO DP013-SQLCA                     INKCS007
W21732              SET DP013-LINK-RETURN                                       
W21732                  DP013-DB2-ABEND                                         
W21732                  DP013-NO-ROLLBACK TO TRUE                               
W21732              PERFORM DP014-0000-PROCESS-ABEND                            
W21732     END-EVALUATE.                                                INKCS007
W21732                                                                          
      *----------------------------------------------------------------*INKCS007
      *    PRINT A REPORT ERROR LINE WHEN THERE ARE NO AREA LINES.     *        
      *----------------------------------------------------------------*INKCS007
       5100-NO-AREA-LINES.                                              INKCS007
                                                                                
           IF (PV-LINE-COUNT + 2) > PC-LINES-PER-PAGE                           
               PERFORM 7000-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           ADD 2                         TO PV-LINE-COUNT.                      
           MOVE PC-DOUBLE-SPACE          TO DRS01-COMM-CTL.                     
           MOVE ER-NO-AREA-LINES         TO DRS01-COMM-DATA.            INKCS007
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                        INKCS007
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------------*INKCS007
      *    PRINT THE END OF REPORT LINE.                               *        
      *----------------------------------------------------------------*INKCS007
       5200-END-OF-RPT-LINE.                                            INKCS007
                                                                                
           MOVE PC-DOUBLE-SPACE          TO DRS01-COMM-CTL.                     
           MOVE DL-END-OF-REPORT-LINE    TO DRS01-COMM-DATA.                    
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                        INKCS007
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------------*        
      *  WRITE THE DATA TO THE HEADING LINES VIA DRS.                           
      *----------------------------------------------------------------*        
       7000-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1                           TO  PV-PAGE-COUNT.                   
           MOVE PV-PAGE-COUNT              TO  HL1-PAGE-NBR.                    
                                                                                
           MOVE HL1-HEADING-LINE-1         TO  DRS01-COMM-DATA.                 
           MOVE PC-TOP-OF-PAGE             TO DRS01-COMM-CTL.                   
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE HL2-HEADING-LINE-2         TO  DRS01-COMM-DATA.                 
           MOVE PC-SINGLE-SPACE            TO  DRS01-COMM-CTL.                  
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE HL3-HEADING-LINE-3         TO  DRS01-COMM-DATA.                 
           MOVE PC-SINGLE-SPACE            TO  DRS01-COMM-CTL.                  
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE DH1-DETAIL-HEADING-LINE-1  TO  DRS01-COMM-DATA.                 
           MOVE PC-DOUBLE-SPACE            TO  DRS01-COMM-CTL                   
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE DH2-DETAIL-HEADING-LINE-2  TO  DRS01-COMM-DATA.                 
           MOVE PC-SINGLE-SPACE            TO  DRS01-COMM-CTL                   
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE SPACES                     TO  DRS01-COMM-DATA.                 
           MOVE PC-SINGLE-SPACE            TO  DRS01-COMM-CTL                   
           PERFORM 8000-LINK-TO-DRS.                                            
                                                                                
           MOVE +7                         TO  PV-LINE-COUNT.                   
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE EXECUTES A LINK TO DRS (DYNAMIC REPORT SYSTEM).             
      *----------------------------------------------------------------*        
       8000-LINK-TO-DRS.                                                        
                                                                                
      *  CAUTION:  ANY NON-BLANK CHARACTER IN PRINT POSITION 132                
      *            WILL CAUSE DOUBLE-SPACING REGARDLESS OF THE                  
      *            CONTENTS OF CARRIAGE CONTROL CHARACTER.                      
           EXEC CICS                                                            
               LINK                                                             
                   PROGRAM   ('DRS1INTC')                                       
                   COMMAREA  (DRS01-COMMAREA)                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DRS01-DRRB-RC = ZERO                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8000-LINK-TO-DRS'                                      
                                           TO DP013-PARAGRAPH                   
                   MOVE 'DRS ERROR'        TO DP013-MESSAGE-TEXT (1)            
                   SET PS-ERROR                                                 
                       DP013-LINK-RETURN                                        
                       DP013-OTHER-ABEND                                        
                       DP013-NO-ROLLBACK   TO TRUE                              
                   MOVE DRS01-DRRB-FUNC    TO PV-DRS-DRRB-FUNC                  
                   MOVE DRS01-DRRB-RID     TO PV-DRS-DRRB-RID                   
                   MOVE DRS01-DRRD-OPTIONS TO PV-DRS-DRRB-OPTIONS               
                   MOVE DRS01-DRRB-RC      TO PV-DRS-DRRB-RC                    
                   MOVE DRS01-DRRB-ATTR-ERROR                                   
                                           TO PV-DRS-ATTR-ERROR                 
                   MOVE DRS01-DRRB-DRDC    TO PV-DRS-DRRB-DRDC                  
                   MOVE DRS01-DRRB01-LINES TO PV-DRS-DRRB-LINES                 
                   MOVE PV-DRS-ABEND-DISPLAY-LINE1                              
                                           TO DP013-MESSAGE-TEXT (2)            
                   MOVE PV-DRS-ABEND-DISPLAY-LINE2                              
                                           TO DP013-MESSAGE-TEXT (3)            
                   PERFORM DP014-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      *    NON-CICS ARCHITECTURE ABEND ROUTINE CALL                             
      *                                                                         
                                                                                
           COPY DPPD014.                                                        
