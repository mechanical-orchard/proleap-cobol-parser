000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             RCKRP035.                                        
000600 AUTHOR.                 PATRICK BURKE.                                   
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN.           JUNE 2001.                                       
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*  THIS PROGRAM TAKES THE EXTRACT FILE CREATED IN RCKUT112 AND  *         
001300*  CREATES A WEEKLY REPORT TO LIST UNITS VERIFIED FOR SPECIFIC  *         
001400*  LOCATION (LIKE A CENTRAL STOCK LOCATION OR E-COMMERCE STORE).*         
001500*****************************************************************         
001520*                    C H A N G E     L O G                      *         
001531*================================================================         
092012* DATE: 09/20/12   CRQ32716/WR38972                                       
092012* WHO: STEVEN NOWAK (REFER TO '092012' FOR ALL CHANGES)                   
092012* WHY: ADD PARM FOR REPORT HEADING                                        
001300*****************************************************************         
140332* DATE: 04/08/16   CHG140332                                              
140332* WHO: JIM HUNTER   (REFER TO '140332' FOR ALL CHANGES)                   
140332* WHY: ADD STORE AND DEPT TO DETAIL LINE SO USERS WILL HAVE               
140332*      THEM AVAILABLE WHEN THEY EXPORT THE DETAIL LINES TO                
140332*      EXCEL.  THIS IS THE RESULT OF ADDING THE 12 FILA OUTLET            
140332*      STORES TO THE REPORT.                                              
140332*****************************************************************         
001600 ENVIRONMENT DIVISION.                                                    
001700*****************************************************************         
001800 CONFIGURATION SECTION.                                                   
001900 SOURCE-COMPUTER.                        IBM-3090.                        
002000 OBJECT-COMPUTER.                        IBM-3090.                        
002100 INPUT-OUTPUT SECTION.                                                    
002200 FILE-CONTROL.                                                            
002300                                                                          
002400     SELECT EXTRACT-FILE                                                  
002500         ASSIGN TO INP01.                                                 
002600                                                                          
092012* WR38972 NEXT 2 LINES ARE NEW - SJN                                      
092012     SELECT HEADING-FILE                                                  
092012         ASSIGN TO INP02.                                                 
092012                                                                          
002700     SELECT REPORT-FILE                                                   
002800         ASSIGN TO RPT01.                                                 
002900                                                                          
003000*****************************************************************         
003100 DATA DIVISION.                                                           
003200*****************************************************************         
003300 FILE SECTION.                                                            
003400                                                                          
003500 FD  EXTRACT-FILE                                                         
003600     RECORDING MODE IS F                                                  
003700     LABEL RECORDS ARE STANDARD                                           
003800     BLOCK CONTAINS 0 RECORDS.                                            
003900 01  EXTRACT-RECORD                   PIC X(300).                         
004000                                                                          
092012* WR38972 NEXT 5 LINES ARE NEW - SJN                                      
092012 FD  HEADING-FILE                                                         
092012     RECORDING MODE IS F                                                  
092012     LABEL RECORDS ARE STANDARD                                           
092012     BLOCK CONTAINS 0 RECORDS.                                            
092012 01  HEADING-RECORD                   PIC X(80).                          
092012                                                                          
004100 FD  REPORT-FILE                                                          
004200     RECORDING MODE IS F                                                  
004300     BLOCK CONTAINS 0 RECORDS.                                            
004400 01  REPORT-RECORD                    PIC X(132).                         
004500/                                                                         
004600*****************************************************************         
004700 WORKING-STORAGE SECTION.                                                 
004800*****************************************************************         
004900                                                                          
005000**************************************************************            
005100* FORMAT OF THE CONTROL CARD INFORMATION PASSED INTO THE PGM *            
005200**************************************************************            
005300 01  CC-CONTROL-CARD.                                                     
005400     05  CC-RUN-TYPE                  PIC  X(01).                         
005500          88  CC-RUN-DAILY                       VALUE 'D'.               
005600          88  CC-RUN-WEEKLY                      VALUE 'W'.               
005700     05  FILLER                       PIC  X(79) VALUE SPACES.            
005800                                                                          
005900 01  PS-SWITCHES.                                                         
006000     05  PS-PRINT-DETAILS-SW          PIC X(01) VALUE 'N'.                
006100         88  PS-PRINT-DETAILS                   VALUE 'Y'.                
006200         88  PS-NOT-PRINT-DETAILS               VALUE 'N'.                
006300     05  PS-PRINT-PO-INFO-SW          PIC X(01) VALUE 'N'.                
006400         88  PS-PRINT-PO-INFO                   VALUE 'Y'.                
006500         88  PS-NO-PRINT-PO-INFO                VALUE 'N'.                
006600     05  PS-END-OF-INPUT-FILE-SW      PIC X(01) VALUE 'N'.                
006700         88  PS-END-OF-INPUT                    VALUE 'Y'.                
006800         88  PS-NOT-END-OF-INPUT                VALUE 'N'.                
006900     05  PS-PAGE-SW                  PIC X(01) VALUE 'N'.                 
007000         88  PS-NEW-PAGE                       VALUE 'Y'.                 
007100         88  PS-SAME-PAGE                      VALUE 'N'.                 
007200     05  PS-PRINT-LINE-SW             PIC X(01) VALUE 'N'.                
007300         88  PS-PRINT-LINE                      VALUE 'Y'.                
007400                                                                          
007500 01  DK-DB2-KEYS.                                                         
007600     05  DK-RUN-DATE                  PIC X(10).                          
007700     05  DK-WEEK-END-DATE             PIC X(10).                          
007800                                                                          
007900                                                                          
008000 01  PV-DATE-RECORD.                                                      
008100     05  PV-FROM-DATE                 PIC X(10) VALUE SPACES.             
008200     05  FILLER                       PIC X(01) VALUE SPACES.             
008300     05  PV-TO-DATE                   PIC X(10) VALUE SPACES.             
008400                                                                          
008500 01  SD-SYSTEM-DATE.                                                      
008600     05  SD-SYSTEM-DATE-YY          PIC  9(02)   VALUE ZERO.              
008700     05  SD-SYSTEM-DATE-MM          PIC  9(02)   VALUE ZERO.              
008800     05  SD-SYSTEM-DATE-DD          PIC  9(02)   VALUE ZERO.              
008900                                                                          
009000 01  ST-SYSTEM-TIME.                                                      
009100     05  ST-SYSTEM-HOUR             PIC  9(02)   VALUE ZERO.              
009200     05  ST-SYSTEM-MINUTE           PIC  9(02)   VALUE ZERO.              
009300     05  FILLER                     PIC  9(04)   VALUE ZERO.              
009400                                                                          
009500 01  PV-VARIABLES.                                                        
009600     05  PV-READ-COUNT                PIC S9(7) COMP-3                    
009700                                                VALUE ZERO.               
009800     05  PV-REC-READ-QTY              PIC S9(7) COMP-3                    
009900                                                VALUE ZERO.               
010000     05  PV-WRITE-COUNT               PIC S9(7) COMP-3                    
010100                                                VALUE ZERO.               
010200     05  PV-PAGE-COUNT                PIC S9(3) COMP-3                    
010300                                                VALUE ZERO.               
010400     05  PV-LINE-COUNT                PIC S9(04) VALUE ZERO               
010500                                                 COMP SYNC.               
010600     05  PV-SPACE-COUNT               PIC S9(05) VALUE ZERO               
010700                                                 COMP-3.                  
010800     05  PV-CHAR-COUNT                PIC S9(05) VALUE ZERO               
010900                                                 COMP-3.                  
011000     05  PV-REMAINDER                 PIC S9(05) VALUE ZERO               
011100                                                 COMP-3.                  
011200     05  PV-RESULT                    PIC S9(05)V99 VALUE ZERO            
011300                                                 COMP-3.                  
011400     05  PV-SKU-NUMBER.                                                   
011500         10  PV-SKU-DEPT              PIC 9(03) VALUE ZERO.               
011600         10  PV-SKU-CLASS             PIC X(02) VALUE SPACES.             
011700         10  PV-SKU-SEQ               PIC 9(03) VALUE ZERO.               
011800     05  PV-CLASS-SUB.                                                    
011900         10  PV-FILLER                PIC X(02) VALUE '00'.               
012000         10  PV-CLASS                 PIC X(01) VALUE SPACES.             
012100         10  PV-FILLER                PIC X(01) VALUE '0'.                
012200         10  PV-FILLER                PIC X(01) VALUE '-'.                
012300         10  PV-FILLER                PIC X(02) VALUE '00'.               
012400         10  PV-SUB-CLASS             PIC X(02) VALUE SPACES.             
012500     05  PV-PO-RECEIVER.                                                  
012600         10  PV-PO-NBR                PIC 9(08) VALUE ZEROS.              
012700         10  PV-FILLER                PIC X(01) VALUE '-'.                
012800         10  PV-RECEIVER              PIC 9(03) VALUE ZEROS.              
012900     05  PV-DEPT                      PIC 9(03) VALUE ZEROS.              
013000     05  PV-STORE                     PIC 9(04) VALUE ZEROS.              
013100     05  PV-LOCATION-NBR              PIC 9(04) VALUE ZEROS.              
013200     05  PV-UNIT-RETAIL               PIC X(14) VALUE SPACES.             
013300     05  PV-LOCATION-DESC             PIC X(25) VALUE SPACES.             
013400     05  PV-DEPT-DESC                 PIC X(25) VALUE SPACES.             
013500     05  PV-EXTENDED-RETAIL           PIC S9(12)V99.                      
013600     05  PV-EXTENDED-RETAIL-Z         PIC ZZZ,ZZZ,ZZZ.ZZ-                 
013700                                                 VALUE ZEROS.             
013800                                                                          
013900     05  PV-DB2-FUNCTION              PIC X(30) VALUE SPACES.             
014000     05  PV-PARAGRAPH                 PIC X(30) VALUE SPACES.             
014100     05  PV-DISP-NUMBER               PIC Z,ZZZ,ZZ9.                      
014200                                                                          
014300     05  PV-RUN-DATE                  PIC X(10) VALUE SPACES.             
014400     05  PV-SAVE-START-DATE           PIC X(10) VALUE SPACES.             
014500     05  PV-ERROR-MESSAGE-1           PIC  X(60)  VALUE SPACES.           
014600     05  PV-ERROR-MESSAGE-2           PIC  X(60)  VALUE SPACES.           
014700                                                                          
014800 01  PV-CONTROL-KEYS.                                                     
014900     05  PV-PREVIOUS-KEY.                                                 
015000         10  PV-PREV-CLASS-SUB.                                           
015100           15  PV-PREV-CLASS          PIC X(04) VALUE SPACES.             
015200           15  FILLER                 PIC X(01) VALUE SPACES.             
015300           15  PV-PREV-SUB            PIC X(04) VALUE SPACES.             
015400         10  PV-PREV-DEPT             PIC 9(03) VALUE ZEROS.              
015500         10  PV-PREV-STORE            PIC 9(04) VALUE ZEROS.              
015600         10  PV-PREV-PO-RECEIVER      PIC X(13) VALUE SPACES.             
015700         10  PV-PREV-SKU-NUMBER       PIC 9(08) VALUE ZEROS.              
015800         10  PV-PREV-UNIT-RETAIL      PIC 9(12)V99 VALUE ZEROS.           
015900                                                                          
016000     05  PV-DB2-YEAR                  PIC S9(08) COMP                     
016100                                          VALUE ZERO.                     
016200     05  PV-DB2-MONTH                 PIC S9(08) COMP                     
016300                                          VALUE ZERO.                     
016400     05  PV-DB2-DAY                   PIC S9(08) COMP                     
016500                                          VALUE ZERO.                     
016600     05  PV-DB2-HOUR                  PIC S9(08) COMP                     
016700                                          VALUE ZERO.                     
016800     05  PV-DB2-MINUTE                PIC S9(08) COMP                     
016900                                          VALUE ZERO.                     
017000     05  PV-DB2-SECOND                PIC S9(08) COMP                     
017100                                          VALUE ZERO.                     
017200                                                                          
017300 01  PV-VERIFIED-DATE.                                                    
017400     05  PV-VERIFIED-MM             PIC  X(02)   VALUE SPACES.            
017500     05  FILLER                     PIC  X(01)   VALUE '/'.               
017600     05  PV-VERIFIED-DD             PIC  X(02)   VALUE SPACES.            
017700     05  FILLER                     PIC  X(01)   VALUE '/'.               
017800     05  PV-VERIFIED-CC             PIC  X(02)   VALUE '20'.              
017900     05  PV-VERIFIED-YY             PIC  X(02)   VALUE SPACES.            
018000                                                                          
018100 01  PV-FORMAT-DATE.                                                      
018200     05  PV-FORMAT-CCYY           PIC  X(04)   VALUE SPACES.              
018300     05  FILLER                   PIC  X(01)   VALUE '/'.                 
018400     05  PV-FORMAT-MM             PIC  X(02)   VALUE SPACES.              
018500     05  FILLER                   PIC  X(01)   VALUE '/'.                 
018600     05  PV-FORMAT-DD             PIC  X(02)   VALUE SPACES.              
018700                                                                          
018800 01  PV-BUCKETS COMP-3.                                                   
018900     05  PV-CLASS-EXPTD-UNITS      PIC S9(7) VALUE ZERO.                  
019000     05  PV-CLASS-TOTAL-UNITS      PIC S9(7) VALUE ZERO.                  
019100     05  PV-CLASS-TOTAL-RETAIL     PIC S9(10)V99 VALUE ZERO.              
019200                                                                          
019300     05  PV-DEPT-EXPTD-UNITS       PIC S9(7) VALUE ZERO.                  
019400     05  PV-DEPT-TOTAL-UNITS       PIC S9(7) VALUE ZERO.                  
019500     05  PV-DEPT-TOTAL-RETAIL      PIC S9(10)V99 VALUE ZERO.              
019600                                                                          
019700     05  PV-LOC-EXPTD-UNITS        PIC S9(7) VALUE ZERO.                  
019800     05  PV-LOC-TOTAL-UNITS        PIC S9(7) VALUE ZERO.                  
019900     05  PV-LOC-TOTAL-RETAIL       PIC S9(10)V99 VALUE ZERO.              
020000                                                                          
020100 01  PV-ABEND-FIELDS.                                                     
020200     05  PV-ABEND-CODE                PIC S9(04) VALUE +0                 
020300                                                 COMP SYNC.               
020400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'RCKRP035'.        
020500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
020600     05  PV-ABEND-OPERATION           PIC  X(51) VALUE SPACES.            
020700     05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.            
020800     05  PV-ABEND-STRUCTURE-2         PIC X(8)   VALUE SPACES.            
020900     05  PV-ABEND-STRUCTURE-3         PIC X(8)   VALUE SPACES.            
021000     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
021100     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
021200     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
021300                                                                          
021400 01  PC-CONSTANTS.                                                        
021500     05  PC-RCKUT112                  PIC X(08) VALUE 'RCKUT112'.         
021600     05  PC-DPKUT500                  PIC X(08) VALUE 'DPKUT500'.         
021700     05  PC-CURRENT-PROGRAM-NAME      PIC X(08) VALUE 'RCKRP035'.         
021800     05  PC-END-OF-PAGE               PIC S9(3) COMP-3 VALUE +60.         
021900     05  PC-MAX-LINE-COUNT            PIC S9(3) VALUE +60                 
022000                                                COMP-3.                   
022100     05  PC-MPS-PROGRAM-NAME          PIC  X(08)    VALUE                 
022200         'DPKUT039'.                                                      
022300     05  PC-SYSTEM-CODE              PIC  X(02)    VALUE 'MP'.            
022400     05  PC-CURRENT-OPERATING-COMPANY                                     
022500                                     PIC  X(02)    VALUE '03'.            
022600                                                                          
092012* WR38972 NEXT 6 LINES ARE NEW - SJN                                      
092012     05  PC-DAILY-HEADING.                                                
092012         10  PC-DAILY-NAME            PIC  X(56) VALUE SPACES.            
092012         10  FILLER                   PIC  X(24) VALUE SPACES.            
092012     05  PC-WEEKLY-HEADING.                                               
092012         10  PC-WEEKLY-NAME           PIC  X(72) VALUE SPACES.            
092012         10  FILLER                   PIC  X(08) VALUE SPACES.            
092012                                                                          
022700******************************************************************        
022800*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
022900******************************************************************        
023000     COPY DPWS132O.                                                       
023100*                                                                         
023100*WEEKLY                                                                   
023200 01  HL-HEADING-LINE-1.                                                   
092012     05  FILLER                       PIC X(25)  VALUE SPACES.            
092012* WR38972 NEXT LINE IS NEW - SJN                                          
092012     05  HL1-NAME                     PIC X(72)  VALUE SPACES.            
023700     05  HL1-WEEK-END-DATE.                                               
023800         10  HL1-MM                   PIC 99.                             
023900         10  FILLER                   PIC X VALUE '/'.                    
024000         10  HL1-DD                   PIC 99.                             
024100         10  FILLER                   PIC X VALUE '/'.                    
024200         10  HL1-CCYY                 PIC 9(04).                          
024300     05  FILLER                       PIC X(25)  VALUE SPACES.            
024400                                                                          
023100*DAILY                                                                    
024500 01  HL-HEADING-LINE-1A.                                                  
092012     05  FILLER                      PIC X(33)  VALUE SPACES.             
092012* WR38972 NEXT LINE IS NEW - SJN                                          
092012     05  HL1A-NAME                   PIC X(56)  VALUE SPACES.             
025000     05  HL1A-DATE.                                                       
025100         10  HL1A-MM                 PIC 99.                              
025200         10  FILLER                  PIC X VALUE '/'.                     
025300         10  HL1A-DD                 PIC 99.                              
025400         10  FILLER                  PIC X VALUE '/'.                     
025500         10  HL1A-CCYY               PIC 9(04).                           
025600     05  FILLER                      PIC X(33)  VALUE SPACES.             
025700                                                                          
025800 01  HL-HEADING-LINE-2.                                                   
025900     05  FILLER                      PIC  X(23)  VALUE                    
026000            'VERIFIED TO LOCATION:  '.                                    
026100     05  HL2-LOC-NBR                 PIC  9(04) VALUE ZEROS.              
026200     05  FILLER                      PIC  X(03) VALUE ' - '.              
026300     05  HL2-LOC-DESC                PIC  X(25).                          
026400     05  FILLER                      PIC  X(67)  VALUE SPACES.            
026500                                                                          
026600 01  HL-HEADING-LINE-3.                                                   
026700     05  FILLER                      PIC  X(23)  VALUE                    
026800            'DEPARTMENT NUMBER:     '.                                    
026900     05  HL3-DEPT-NBR                PIC  9(04).                          
027000     05  FILLER                      PIC  X(03) VALUE ' - '.              
027100     05  HL3-DEPT-DESC               PIC  X(25).                          
027200     05  FILLER                      PIC  X(67)  VALUE SPACES.            
027300                                                                          
027400 01  HL-HEADING-LINE-4.                                                   
140332     05  FILLER                      PIC X(8)  VALUE SPACES.              
027600     05  FILLER                      PIC X(03) VALUE 'SKU'.               
027700     05  FILLER                      PIC X(36) VALUE SPACES.              
027800     05  FILLER                      PIC X(06) VALUE 'VERIFY'.            
140332     05  FILLER                      PIC X(03) VALUE SPACES.              
028000     05  FILLER                      PIC X(06) VALUE 'VERIFY'.            
140332     05  FILLER                      PIC X(15) VALUE SPACES.              
028200     05  FILLER                      PIC X(08) VALUE 'EXPECTED'.          
140332     05  FILLER                      PIC X(03) VALUE SPACES.              
028400     05  FILLER                      PIC X(08) VALUE 'VERIFIED'.          
140332     05  FILLER                      PIC X(36) VALUE SPACES.              
028600                                                                          
028700 01  HL-HEADING-LINE-5.                                                   
028800     05  FILLER                      PIC X(01) VALUE SPACES.              
140332     05  FILLER                      PIC X(03) VALUE 'LOC'.               
140332     05  FILLER                      PIC X(03) VALUE SPACES.              
028900     05  FILLER                      PIC X(06) VALUE 'NUMBER'.            
140332     05  FILLER                      PIC X(03) VALUE SPACES.              
140332     05  FILLER                      PIC X(04) VALUE 'DEPT'.              
140332     05  FILLER                      PIC X(02) VALUE SPACES.              
029100     05  FILLER                      PIC X(09) VALUE 'CLASS/SUB'.         
140332     05  FILLER                      PIC X(03) VALUE SPACES.              
029300     05  FILLER                      PIC X(11) VALUE                      
029400                                                 'PO/RECEIVER'.           
140332     05  FILLER                      PIC X(02) VALUE SPACES.              
029600     05  FILLER                      PIC X(06) VALUE 'DC NBR'.            
140332     05  FILLER                      PIC X(04) VALUE SPACES.              
029800     05  FILLER                      PIC X(04) VALUE 'DATE'.              
140332     05  FILLER                      PIC X(06) VALUE SPACES.              
030000     05  FILLER                      PIC X(04) VALUE 'TYPE'.              
140332     05  FILLER                      PIC X(08) VALUE SPACES.              
030200     05  FILLER                      PIC X(05) VALUE 'UNITS'.             
140332     05  FILLER                      PIC X(06) VALUE SPACES.              
030400     05  FILLER                      PIC X(05) VALUE 'UNITS'.             
140332     05  FILLER                      PIC X(07) VALUE SPACES.              
030600     05  FILLER                      PIC X(11) VALUE                      
030700                                                 'UNIT RETAIL'.           
140332     05  FILLER                      PIC X(02) VALUE SPACES.              
030900     05  FILLER                      PIC X(15) VALUE                      
031000                                                '     EXT RETAIL'.        
140332     05  FILLER                      PIC X(02) VALUE SPACES.              
031200                                                                          
031300 01  DL-DETAIL-LINE-1.                                                    
140332     05  DL1-LOC                      PIC 9(04).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
031400     05  DL1-SKU-NBR                  PIC 9(08).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
140332     05  DL1-DEPT                     PIC 9(04).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
031600     05  DL1-CLASS-SUB                PIC X(09).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
031800     05  DL1-PO-RECEIVER              PIC X(13).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
032000     05  DL1-VERIFY-DC-NBR            PIC 9(04).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
032200     05  DL1-VERIFY-DATE.                                                 
032300         10  DL1-MM                   PIC 99.                             
032400         10  FILLER                   PIC X VALUE '/'.                    
032500         10  DL1-DD                   PIC 99.                             
032600         10  FILLER                   PIC X VALUE '/'.                    
032700         10  DL1-CCYY                 PIC 9(04).                          
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
032900     05  DL1-TYPE                     PIC X(07).                          
140332     05  FILLER                       PIC X(05) VALUE SPACES.             
033100     05  DL1-EXPTD-UNITS              PIC ZZZ,ZZ9-.                       
140332     05  FILLER                       PIC X(03) VALUE SPACES.             
033300     05  DL1-VERIFIED-UNITS           PIC ZZZ,ZZ9-.                       
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
033500     05  DL1-UNIT-RETAIL              PIC ZZZ,ZZZ,ZZ9.99-.                
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
033700     05  DL1-EXTENDED-RETAIL          PIC ZZZ,ZZZ,ZZ9.99-.                
140332     05  FILLER                       PIC X(01) VALUE SPACES.             
033900                                                                          
034000                                                                          
034100 01  TL1-CLASS-LINE.                                                      
140332     05  FILLER                       PIC X(51) VALUE SPACES.             
034300     05  FILLER                       PIC X(10) VALUE                     
034400         'SUB CLASS '.                                                    
034500     05  TL1-SUB-CLASS                PIC X(04).                          
034600     05  FILLER                       PIC X(1) VALUE SPACES.              
034700     05  FILLER                       PIC X(10) VALUE                     
034800         'TOTAL:    '.                                                    
034900     05  TL1-EXPTD-UNITS              PIC Z,ZZZ,ZZ9.                      
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
035100     05  TL1-VERIFIED-UNITS           PIC Z,ZZZ,ZZ9-.                     
140332     05  FILLER                       PIC X(19) VALUE SPACES.             
035300     05  TL1-EXTENDED-RETAIL          PIC ZZZ,ZZZ,ZZ9.99-.                
140332     05  FILLER                       PIC X(01) VALUE SPACES.             
035400                                                                          
035500 01  TL2-DEPT-LINE.                                                       
140332     05  FILLER                       PIC X(50) VALUE SPACES.             
035700     05  FILLER                       PIC X(11) VALUE                     
035800         'DEPARTMENT '.                                                   
035900     05  TL2-DEPT                     PIC 9(04).                          
036000     05  FILLER                       PIC X(1) VALUE SPACES.              
036100     05  FILLER                       PIC X(10) VALUE                     
036200         'TOTAL:    '.                                                    
036300     05  TL2-EXPTD-UNITS              PIC Z,ZZZ,ZZ9.                      
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
036500     05  TL2-VERIFIED-UNITS           PIC Z,ZZZ,ZZ9-.                     
140332     05  FILLER                       PIC X(19) VALUE SPACES.             
036700     05  TL2-EXTENDED-RETAIL          PIC ZZZ,ZZZ,ZZ9.99-.                
140332     05  FILLER                       PIC X(01) VALUE SPACES.             
036800                                                                          
036900 01  TL3-LOCATION-LINE.                                                   
140332     05  FILLER                       PIC X(42) VALUE SPACES.             
037100     05  FILLER                       PIC X(25) VALUE                     
037200         'FINAL TOTAL FOR LOCATION '.                                     
037300     05  TL3-LOCATION                 PIC 9(04).                          
037400     05  FILLER                       PIC X(05) VALUE ':    '.            
037500     05  TL3-EXPTD-UNITS              PIC Z,ZZZ,ZZ9.                      
140332     05  FILLER                       PIC X(02) VALUE SPACES.             
037700     05  TL3-VERIFIED-UNITS           PIC Z,ZZZ,ZZ9-.                     
140332     05  FILLER                       PIC X(19) VALUE SPACES.             
037900     05  TL3-EXTENDED-RETAIL          PIC ZZZ,ZZZ,ZZ9.99-.                
140332     05  FILLER                       PIC X(01) VALUE SPACES.             
038000                                                                          
038100                                                                          
038200 01  ER-END-OF-REPORT-LINE.                                               
038300     05  FILLER                       PIC X(19)  VALUE                    
038400         '** END OF REPORT **'.                                           
038500     05  FILLER                       PIC X(113)  VALUE SPACES.           
038600                                                                          
038700 01  EP-EMPTY-REPORT-LINE.                                                
038800     05  FILLER                       PIC X(34)  VALUE SPACES.            
038900     05  FILLER                       PIC X(59)  VALUE                    
039000      '**************** EMPTY INPUT FILE *********************'.          
039100     05  FILLER                       PIC X(49)  VALUE SPACES.            
039200 01      DL-BLANK-LINE                PIC X(132) VALUE SPACES.            
039300/                                                                         
039400***********************************************************               
039500*  COPYBOOK USED BY MBS SYSTEM                            *               
039600***********************************************************               
039700     COPY DPWS003.                                                        
039800*                                                                         
039900******************************************************************        
040000* CORPORATE DATE ROUTINES                                                 
040100******************************************************************        
040200*    COPY DPWS500.                                                        
040300/                                                                         
040400******************************************************************        
040500*  WORKING STORAGE AREA FOR NAME REFORMAT DPPD024                         
040600******************************************************************        
040700     COPY DPWS024.                                                        
040800/                                                                         
040900******************************************************************        
041000*  RECORD LAYOUT FOR INPUT FILE                                           
041100******************************************************************        
041200     COPY STOREEXT.                                                       
041300/                                                                         
041400*  DOMAIN LOCATION MODULE                                                 
041500     COPY XLWS071.                                                        
041600/                                                                         
041700******************************************************************        
041800*  DB2 FORMATTED MESSAGE AREA                                             
041900******************************************************************        
042000     COPY DPWS004.                                                        
042100/                                                                         
042200******************************************************************        
042300* CORPORATE DATE ROUTINES                                                 
042400******************************************************************        
042500     COPY DPWS500.                                                        
042600/                                                                         
042700******************************************************************        
042800*   DB2 SQL COMMUNICATION AREA                                            
042900******************************************************************        
043000     EXEC SQL                                                             
043100         INCLUDE SQLCA                                                    
043200     END-EXEC.                                                            
043300*                                                                         
043400     COPY SQLCA2.                                                         
043500*                                                                         
043600******************************************************************        
043700*   DB2 TABLE                                                             
043800******************************************************************        
043900     EXEC SQL                                                             
044000          INCLUDE TCOCNTL                                                 
044100     END-EXEC.                                                            
044200*                                                                         
044300******************************************************************        
044400 PROCEDURE DIVISION.                                                      
044500******************************************************************        
044600 0000-MAINLINE.                                                           
044700                                                                          
044800*OPEN FILES.                                                              
044900     OPEN INPUT  EXTRACT-FILE                                             
092012* WR38972 NEXT LINE IS NEW - SJN                                          
092012          INPUT  HEADING-FILE                                             
045000          OUTPUT REPORT-FILE.                                             
045100                                                                          
045200*READ FIRST DETAIL RECORD.                                                
045300     PERFORM 2100-READ-EXTRACT.                                           
045400                                                                          
045500     PERFORM 1000-INITIALIZE.                                             
045600                                                                          
045700*PROCESS INPUT FILE.                                                      
045800     IF PS-END-OF-INPUT                                                   
045900         PERFORM 1300-NO-DETAIL-LINE                                      
046000     ELSE                                                                 
046100         PERFORM 8100-WRITE-SUB-HEADINGS                                  
046200         SET PS-PRINT-PO-INFO       TO TRUE                               
046300         PERFORM 2000-PROCESS-EXTRACT                                     
046400             UNTIL PS-END-OF-INPUT                                        
046500        PERFORM 4200-CLASS-TOTALS                                         
046600        PERFORM 4300-DEPT-TOTALS                                          
046700        PERFORM 4400-LOCATION-TOTALS                                      
046800     END-IF.                                                              
046900                                                                          
047000     PERFORM 9000-EOJ.                                                    
047100                                                                          
047200     GOBACK.                                                              
047300                                                                          
047400******************************************************************        
047500 1000-INITIALIZE.                                                         
047600******************************************************************        
047700*                                                                         
047800* LOAD SYSTEM DATE INTO HEADINGS                                          
047900     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
048000     ACCEPT ST-SYSTEM-TIME FROM TIME.                                     
048100                                                                          
048200     MOVE SD-SYSTEM-DATE-YY      TO DP132O-RUN-YEAR.                      
048300     MOVE SD-SYSTEM-DATE-MM      TO DP132O-RUN-MONTH.                     
048400     MOVE SD-SYSTEM-DATE-DD      TO DP132O-RUN-DAY.                       
048500     MOVE ST-SYSTEM-HOUR         TO DP132O-RUN-HOUR.                      
048600     MOVE ST-SYSTEM-MINUTE       TO DP132O-RUN-MINUTE.                    
048700                                                                          
048800     MOVE PC-CURRENT-PROGRAM-NAME                                         
048900                                 TO DP132O-PROGRAM-NAME.                  
049000     MOVE 1                      TO DP132O-REPORT-NUMBER                  
049100                                    DP132O-PAGE-NUMBER.                   
049200                                                                          
049300     PERFORM 1500-BUILD-CLASS-SUB.                                        
049400     MOVE PV-CLASS-SUB TO PV-PREV-CLASS-SUB.                              
049500     MOVE SE-LOC-NBR   TO PV-PREV-STORE.                                  
049600     MOVE SE-DEPT      TO PV-PREV-DEPT.                                   
049700                                                                          
049800     PERFORM 7200-SELECT-TCOCNTL.                                         
049900     PERFORM 1110-CALL-CALENDAR.                                          
050000                                                                          
050100     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
050200     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
050300                                                                          
092012* WR38972 NEXT 7 LINES ARE NEW - SJN                                      
092012     IF  CC-RUN-DAILY                                                     
092012         READ HEADING-FILE INTO PC-DAILY-HEADING                          
092012         MOVE PC-DAILY-NAME TO HL1A-NAME                                  
092012     ELSE                                                                 
092012         READ HEADING-FILE INTO PC-WEEKLY-HEADING                         
092012         MOVE PC-WEEKLY-NAME TO HL1-NAME                                  
092012     END-IF.                                                              
092012                                                                          
050400     PERFORM 8000-WRITE-MAIN-HEADINGS.                                    
050500     INITIALIZE PV-BUCKETS.                                               
050600                                                                          
050610                                                                          
050700*****************************************************************         
050800 1110-CALL-CALENDAR.                                                      
050900*****************************************************************         
051000*                                                                         
051100     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
051200*                                                                         
051300     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
051400         DPG52-LK-DTE-ISO-FMT         TO TRUE                             
051500     MOVE COCNTL-BTCH-PRC-DTE         TO DPG52-LK-DATE-INPUT              
051600                                                                          
051700     CALL PC-DPKUT500 USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.          
051800                                                                          
051900     IF DPG54-NO-ERROR                                                    
052000         MOVE DPG55-1ST-WKDY-DB2ISO-FMT                                   
052100                                      TO PV-SAVE-START-DATE               
052200*        PERFORM 1115-DECR-TO-PREV-WEEK                                   
052300*        MOVE DPG55-1ST-WKDY-DB2ISO-FMT                                   
052400*                                     TO DK-FROM-DATE                     
052500         MOVE DPG55-LAST-WKDY-DB2ISO-FMT                                  
052600                                      TO DK-WEEK-END-DATE                 
052700         MOVE DPG55-LAST-WKDY-GREG-CC-MO                                  
052800                                      TO HL1-MM                           
052900         MOVE DPG55-LAST-WKDY-GREG-CC-DY                                  
053000                                      TO HL1-DD                           
053100         MOVE DPG55-LAST-WKDY-GREG-CC-YR                                  
053200                                      TO HL1-CCYY                         
053300     END-IF.                                                              
053400                                                                          
053500*****************************************************************         
053600 1115-DECR-TO-PREV-WEEK.                                                  
053700*****************************************************************         
053800* FIND THE START AND END DATES FOR THE PREVIOUS WEEK                      
053900                                                                          
054000     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
054100                                                                          
054200     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
054300         DPG51-DECREMENT-DATE                                             
054400         DPG52-LK-DTE-ISO-FMT         TO TRUE                             
054500     MOVE PV-SAVE-START-DATE          TO DPG52-LK-DATE-INPUT              
054600     MOVE 1                           TO DPG51-INCR-DECR-DAYS-9           
054700                                                                          
054800     CALL PC-DPKUT500 USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.          
054900                                                                          
055000     IF DPG54-NO-ERROR                                                    
055100*        MOVE DPG55-1ST-WKDY-DB2ISO-FMT                                   
055200*                                     TO DK-FROM-DATE                     
055300         MOVE DPG55-LAST-WKDY-DB2ISO-FMT                                  
055400                                      TO DK-WEEK-END-DATE                 
055500         MOVE DPG55-LAST-WKDY-GREG-CC-MO                                  
055600                                      TO HL1-MM                           
055700         MOVE DPG55-LAST-WKDY-GREG-CC-DY                                  
055800                                      TO HL1-DD                           
055900         MOVE DPG55-LAST-WKDY-GREG-CC-YR                                  
056000                                      TO HL1-CCYY                         
056100*        DISPLAY 'START DATE = ' DK-FROM-DATE                             
056200         DISPLAY 'END DATE   = ' DK-WEEK-END-DATE                         
056300     END-IF.                                                              
056400/                                                                         
056500*****************************************************************         
056600 1250-SELECT-LOCATION-DESC.                                               
056700*****************************************************************         
056800*                                                                         
056900     INITIALIZE XL071-COMMAREA-REC                                        
057000*    MOVE SE-LOC-NBR                  TO XL071-LOCATION-NUMBER            
057100     MOVE PV-STORE                    TO XL071-LOCATION-NUMBER            
057200     PERFORM 8500-CALL-XLKUT071                                           
057300     MOVE XL071-LOC-NM                TO PV-LOCATION-DESC.                
057400                                                                          
057500*                                                                         
057600******************************************************************        
057700*  PRINT A 'NO INPUT' REPORT LINE.                                        
057800******************************************************************        
057900 1300-NO-DETAIL-LINE.                                                     
058000                                                                          
058100     WRITE REPORT-RECORD                                                  
058200       FROM EP-EMPTY-REPORT-LINE                                          
058300         AFTER ADVANCING 3 LINES.                                         
058400*                                                                         
058500******************************************************************        
058600*  BUILD CLASS/SUB NUMBER.                                                
058700******************************************************************        
058800 1500-BUILD-CLASS-SUB.                                                    
058900*                                                                         
059000     MOVE SE-CLASS                TO PV-SUB-CLASS.                        
059100     MOVE SE-CLASS                TO PV-CLASS.                            
059200*                                                                         
059300******************************************************************        
059400*  CHECK IF THERE A MULTIPLE ENTRIES ON THE FILE FOR THE SAME             
059500*    SKU NUMBER/UNIT RETAIL WITHIN A PO/RECEIVER.                         
059600******************************************************************        
059700 1600-CHECK-MULTIPLES.                                                    
059800*                                                                         
059900     IF PV-PO-RECEIVER   =   PV-PREV-PO-RECEIVER  AND                     
060000        PV-SKU-NUMBER    =   PV-PREV-SKU-NUMBER   AND                     
060100        SE-LOWEST-RETAIL =   PV-PREV-UNIT-RETAIL                          
060200        SET PS-NOT-PRINT-DETAILS  TO TRUE                                 
060300     ELSE                                                                 
060400        SET PS-PRINT-DETAILS      TO TRUE.                                
060500*                                                                         
060600******************************************************************        
060700 2000-PROCESS-EXTRACT.                                                    
060800******************************************************************        
060900*                                                                         
061000       IF PV-CLASS-SUB = PV-PREV-CLASS-SUB  AND                           
061100          SE-LOC-NBR   = PV-PREV-STORE      AND                           
061200          SE-DEPT      = PV-PREV-DEPT                                     
061300          PERFORM 1600-CHECK-MULTIPLES                                    
061400            IF PS-PRINT-DETAILS                                           
061500              PERFORM 3000-PRINT-DETAILS                                  
061600            END-IF                                                        
061700       ELSE                                                               
061800          PERFORM 4200-CLASS-TOTALS                                       
061900          IF SE-DEPT   = PV-PREV-DEPT   AND                               
062000             SE-LOC-NBR = PV-PREV-STORE                                   
062100             PERFORM 3000-PRINT-DETAILS                                   
062200          ELSE                                                            
062300             PERFORM 4300-DEPT-TOTALS                                     
062400             IF SE-LOC-NBR  = PV-PREV-STORE                               
062500                PERFORM 8000-WRITE-MAIN-HEADINGS                          
062600                PERFORM 8100-WRITE-SUB-HEADINGS                           
062700                PERFORM 3000-PRINT-DETAILS                                
062800             ELSE                                                         
062900                PERFORM 4400-LOCATION-TOTALS                              
063000                PERFORM 8000-WRITE-MAIN-HEADINGS                          
063100                PERFORM 8100-WRITE-SUB-HEADINGS                           
063200                PERFORM 3000-PRINT-DETAILS                                
063300             END-IF                                                       
063400          END-IF                                                          
063500       END-IF.                                                            
063600                                                                          
063700       MOVE PV-CLASS-SUB    TO PV-PREV-CLASS-SUB.                         
063800       MOVE SE-DEPT         TO PV-PREV-DEPT.                              
063900       MOVE SE-LOC-NBR      TO PV-PREV-STORE.                             
064000       MOVE PV-PO-RECEIVER  TO PV-PREV-PO-RECEIVER.                       
064100       MOVE PV-SKU-NUMBER   TO PV-PREV-SKU-NUMBER.                        
064200       MOVE SE-LOWEST-RETAIL TO PV-PREV-UNIT-RETAIL.                      
064300                                                                          
064400       PERFORM 2100-READ-EXTRACT.                                         
064500       PERFORM 1500-BUILD-CLASS-SUB.                                      
064600*                                                                         
064700******************************************************************        
064800*  READ NEXT INPUT FILE RECORD.                                           
064900******************************************************************        
065000 2100-READ-EXTRACT.                                                       
065100                                                                          
065200     READ EXTRACT-FILE INTO STORE-EXTRACT-LINE                            
065300        AT END                                                            
065400           SET PS-END-OF-INPUT TO TRUE                                    
065500        NOT AT END                                                        
065600           ADD +1               TO PV-READ-COUNT                          
065700           MOVE SE-DEPT         TO PV-DEPT                                
065800           MOVE SE-LOC-NBR      TO PV-STORE                               
065900                                                                          
066000*************************************************                         
066100*  BUILD SKU NUMBER FOR PROCESSING.                                       
066200*************************************************                         
066300           MOVE SE-SKU-NBR      TO PV-SKU-NUMBER                          
066400                                                                          
066500*************************************************                         
066600*  BUILD PO/RECEIVER NUMBER FOR PROCESSING.                               
066700*************************************************                         
066800     MOVE SE-PO                   TO PV-PO-NBR                            
066900     MOVE SE-REC-SEQ-NBR          TO PV-RECEIVER                          
067000     END-READ.                                                            
067100*                                                                         
067200******************************************************************        
067300*  FILL AND PRINT DETAIL REPORT LINE.                                     
067400******************************************************************        
067500 3000-PRINT-DETAILS.                                                      
067600*                                                                         
140332     MOVE SE-LOC-NBR              TO DL1-LOC.                             
140332                                                                          
067700     MOVE PV-SKU-NUMBER           TO DL1-SKU-NBR.                         
140332                                                                          
140332     MOVE SE-DEPT                 TO DL1-DEPT.                            
067800                                                                          
067900     MOVE PV-CLASS-SUB            TO DL1-CLASS-SUB.                       
068000                                                                          
068100     MOVE PV-PO-RECEIVER          TO DL1-PO-RECEIVER.                     
068200                                                                          
068300     MOVE SE-CURR-OPER-UNT-ID     TO DL1-VERIFY-DC-NBR.                   
068400                                                                          
068500     MOVE SE-DV-MM                TO PV-VERIFIED-MM.                      
068600     MOVE SE-DV-DD                TO PV-VERIFIED-DD.                      
068700     MOVE SE-DV-YY                TO PV-VERIFIED-YY.                      
068800     MOVE PV-VERIFIED-DATE        TO DL1-VERIFY-DATE.                     
068900                                                                          
069000     IF SE-REC-SRC-CDE = 'A'                                              
069100         MOVE ' ADJUST'           TO DL1-TYPE                             
069200     ELSE                                                                 
069300         MOVE 'RECEIPT'           TO DL1-TYPE.                            
069400                                                                          
069500     MOVE SE-RETAIL-UNITS-RECVD   TO DL1-EXPTD-UNITS.                     
069600     ADD  SE-RETAIL-UNITS-RECVD   TO PV-CLASS-EXPTD-UNITS.                
069700                                                                          
069800     MOVE SE-RETAIL-UNITS-MARK    TO DL1-VERIFIED-UNITS.                  
069900     ADD  SE-RETAIL-UNITS-MARK    TO PV-CLASS-TOTAL-UNITS.                
070000                                                                          
070100     MOVE SE-LOWEST-RETAIL        TO DL1-UNIT-RETAIL.                     
070200                                                                          
070300     MULTIPLY  SE-RETAIL-UNITS-MARK  BY  SE-LOWEST-RETAIL                 
070400        GIVING  PV-EXTENDED-RETAIL.                                       
070500     MOVE PV-EXTENDED-RETAIL      TO DL1-EXTENDED-RETAIL.                 
070600     ADD  PV-EXTENDED-RETAIL      TO PV-CLASS-TOTAL-RETAIL.               
070700                                                                          
070800                                                                          
070900*CHECK FOR OVERFLOW.                                                      
071000     ADD 1 TO PV-LINE-COUNT.                                              
071100     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
071200         SET PS-NEW-PAGE TO TRUE                                          
071300         PERFORM 8000-WRITE-MAIN-HEADINGS                                 
071400         PERFORM 8100-WRITE-SUB-HEADINGS                                  
071500     END-IF.                                                              
071600                                                                          
071700*IF NEW PAGE, DISPLAY WEEK-END DATE.                                      
071800     IF PS-NEW-PAGE                                                       
071900         SET PS-SAME-PAGE             TO TRUE                             
072000     END-IF.                                                              
072100                                                                          
072200*WRITE DETAIL LINE.                                                       
072300     WRITE REPORT-RECORD                                                  
072400       FROM DL-DETAIL-LINE-1                                              
072500         AFTER ADVANCING 1 LINES.                                         
072600*                                                                         
072700******************************************************************        
072800*  GET THE DEPARTMENT DESCRIPTION FROM THE MBS TABLES                     
072900*  USING SUBROUTINE 'DPKUT038'.                                           
073000******************************************************************        
073100 3100-GET-DEPT-DESC.                                                      
073200*                                                                         
073300     INITIALIZE DP003-MBS-SUBROUTINE-FIELDS.                              
073400     SET DP003-DPT-LOOKUP              TO TRUE.                           
073500     MOVE PV-DEPT                      TO DP003-DPT-INPUT.                
073600     MOVE PC-SYSTEM-CODE               TO DP003-SYSTEM-CODE.              
073700     MOVE PC-CURRENT-OPERATING-COMPANY TO DP003-OP-CO-NMBR.               
073800     MOVE SD-SYSTEM-DATE               TO DP003-CURRENT-DATE              
073900                                          DP003-EFF-DATE.                 
074000*                                                                         
074100     CALL PC-MPS-PROGRAM-NAME   USING DP003-MBS-SUBROUTINE-FIELDS.        
074200*                                                                         
074300     EVALUATE TRUE                                                        
074400        WHEN DP003-SUCCESSFUL                                             
074500           MOVE DP003-DPT-DESCRIPTION TO PV-DEPT-DESC                     
074600        WHEN DP003-DPT-NOT-FOUND                                          
074700           MOVE 'UNKNOWN'             TO PV-DEPT-DESC                     
074800        WHEN DP003-DPT-DB2-ERROR                                          
074900           MOVE DP003-RETURN-MSG TO PV-ABEND-STRUCTURE-1                  
075000           MOVE 'CURRENT DEPARTMENT BEING PROCESSED IS: '                 
075100                                 TO PV-ABEND-STRUCTURE-2                  
075200           MOVE DP003-DPT-INPUT  TO PV-ABEND-STRUCTURE-3                  
075300           MOVE DP003-SQLCA      TO SQLCA                                 
075400           PERFORM 9200-SQL-ABEND                                         
075500        WHEN DP003-DPT-MAX-RETRIES                                        
075600           MOVE DP003-RETURN-MSG TO PV-ABEND-STRUCTURE-1                  
075700           MOVE 'CURRENT DEPARTMENT BEING PROCESSED IS: '                 
075800                                 TO PV-ABEND-STRUCTURE-2                  
075900           MOVE DP003-DPT-INPUT  TO PV-ABEND-STRUCTURE-3                  
076000           MOVE DP003-SQLCA      TO SQLCA                                 
076100           PERFORM 9200-SQL-ABEND                                         
076200        WHEN DP003-NO-INPUT-SELECTION                                     
076300           MOVE 'CURRENT INPUT FIELDS ARE: '                              
076400                                 TO PV-ERROR-MESSAGE-1                    
076500           MOVE DP003-INPUT-FIELDS                                        
076600                                 TO PV-ERROR-MESSAGE-2                    
076700           PERFORM 9400-ABEND                                             
076800        WHEN DP003-SYSTEM-CODE-ERROR                                      
076900           MOVE 'THE SYSTEM CODE IS: '                                    
077000                                 TO PV-ERROR-MESSAGE-1                    
077100           MOVE DP003-SYSTEM-CODE                                         
077200                                 TO PV-ERROR-MESSAGE-2                    
077300           PERFORM 9400-ABEND                                             
077400        WHEN DP003-CURRENT-DATE-ERROR                                     
077500           MOVE 'THE CURRENT DATE USED IS:'                               
077600                                 TO PV-ERROR-MESSAGE-1                    
077700           MOVE DP003-CURRENT-DATE                                        
077800                                 TO PV-ERROR-MESSAGE-2                    
077900           PERFORM 9400-ABEND                                             
078000        WHEN DP003-EFF-DATE-ERROR                                         
078100           MOVE 'THE EFFECTIVE DATE USED IS:'                             
078200                                 TO PV-ERROR-MESSAGE-1                    
078300           MOVE DP003-EFF-DATE   TO PV-ERROR-MESSAGE-2                    
078400           PERFORM 9400-ABEND                                             
078500        WHEN DP003-DPT-MUST-BE-NUMERIC                                    
078600           MOVE 'CURRENT DEPARTMENT BEING PROCESSED IS: '                 
078700                                 TO PV-ERROR-MESSAGE-1                    
078800           MOVE DP003-DPT-INPUT  TO PV-ERROR-MESSAGE-2                    
078900           PERFORM 9400-ABEND                                             
079000        WHEN OTHER                                                        
079100           MOVE 'ERROR UNIDENTIFIED -  CURRENT INPUT FIELDS ARE: '        
079200                                 TO PV-ERROR-MESSAGE-1                    
079300           MOVE DP003-INPUT-FIELDS                                        
079400                                 TO PV-ERROR-MESSAGE-2                    
079500           PERFORM 9400-ABEND                                             
079600     END-EVALUATE.                                                        
079700/                                                                         
079800******************************************************************        
079900 4200-CLASS-TOTALS.                                                       
080000******************************************************************        
080100*                                                                         
080200     MOVE PV-CLASS-EXPTD-UNITS        TO TL1-EXPTD-UNITS.                 
080300     ADD  PV-CLASS-EXPTD-UNITS        TO PV-DEPT-EXPTD-UNITS.             
080400                                                                          
080500     MOVE PV-CLASS-TOTAL-UNITS        TO TL1-VERIFIED-UNITS.              
080600     ADD  PV-CLASS-TOTAL-UNITS        TO PV-DEPT-TOTAL-UNITS.             
080700                                                                          
080800     MOVE PV-CLASS-TOTAL-RETAIL       TO TL1-EXTENDED-RETAIL.             
080900     ADD  PV-CLASS-TOTAL-RETAIL       TO PV-DEPT-TOTAL-RETAIL.            
081000                                                                          
081100     MOVE PV-PREV-SUB                 TO TL1-SUB-CLASS.                   
081200                                                                          
081300     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
081400         AFTER ADVANCING 1 LINE.                                          
081500     ADD 1                            TO PV-LINE-COUNT.                   
081600                                                                          
081700     WRITE REPORT-RECORD FROM TL1-CLASS-LINE                              
081800         AFTER ADVANCING 1 LINE.                                          
081900     ADD 1                            TO PV-LINE-COUNT                    
082000     INITIALIZE PV-CLASS-TOTAL-UNITS                                      
082100                PV-CLASS-EXPTD-UNITS                                      
082200                PV-CLASS-TOTAL-RETAIL.                                    
082300                                                                          
082400     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
082500         AFTER ADVANCING 1 LINE.                                          
082600     ADD 1                            TO PV-LINE-COUNT.                   
082700*                                                                         
082800******************************************************************        
082900 4300-DEPT-TOTALS.                                                        
083000******************************************************************        
083100*                                                                         
083200     MOVE PV-DEPT-EXPTD-UNITS           TO TL2-EXPTD-UNITS.               
083300     ADD  PV-DEPT-EXPTD-UNITS           TO PV-LOC-EXPTD-UNITS.            
083400                                                                          
083500     MOVE PV-DEPT-TOTAL-UNITS           TO TL2-VERIFIED-UNITS.            
083600     ADD  PV-DEPT-TOTAL-UNITS           TO PV-LOC-TOTAL-UNITS.            
083700                                                                          
083800     MOVE PV-DEPT-TOTAL-RETAIL          TO TL2-EXTENDED-RETAIL.           
083900     ADD  PV-DEPT-TOTAL-RETAIL          TO PV-LOC-TOTAL-RETAIL.           
084000                                                                          
084100     MOVE PV-PREV-DEPT                  TO TL2-DEPT.                      
084200     WRITE REPORT-RECORD FROM TL2-DEPT-LINE                               
084300         AFTER ADVANCING 1 LINE.                                          
084400     ADD 1                            TO PV-LINE-COUNT                    
084500     INITIALIZE  PV-DEPT-TOTAL-UNITS                                      
084600                 PV-DEPT-EXPTD-UNITS                                      
084700                 PV-DEPT-TOTAL-RETAIL                                     
084800                                                                          
084900     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
085000         AFTER ADVANCING 1 LINE.                                          
085100     ADD 1                            TO PV-LINE-COUNT.                   
085200*                                                                         
085300******************************************************************        
085400 4400-LOCATION-TOTALS.                                                    
085500******************************************************************        
085600*                                                                         
085700     MOVE PV-LOC-EXPTD-UNITS            TO TL3-EXPTD-UNITS.               
085800     MOVE PV-LOC-TOTAL-UNITS            TO TL3-VERIFIED-UNITS.            
085900     MOVE PV-LOC-TOTAL-RETAIL           TO TL3-EXTENDED-RETAIL.           
086000                                                                          
086100     MOVE  PV-PREV-STORE                TO TL3-LOCATION.                  
086200     WRITE REPORT-RECORD FROM TL3-LOCATION-LINE                           
086300         AFTER ADVANCING 1 LINE.                                          
086400     ADD 1                            TO PV-LINE-COUNT.                   
086500     INITIALIZE PV-LOC-TOTAL-UNITS                                        
086600                PV-LOC-EXPTD-UNITS                                        
086700                PV-LOC-TOTAL-RETAIL.                                      
086800     INITIALIZE PV-PAGE-COUNT.                                            
086900                                                                          
087000     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
087100         AFTER ADVANCING 1 LINE.                                          
087200     ADD 1                            TO PV-LINE-COUNT.                   
087300*                                                                         
087400******************************************************************        
087500 7200-SELECT-TCOCNTL.                                                     
087600******************************************************************        
087700*                                                                         
087800     EXEC SQL                                                             
087900         SELECT                                                           
088000                BTCH_PRC_DTE                                              
088100         INTO  :COCNTL-BTCH-PRC-DTE                                       
088200         FROM TCOCNTL                                                     
088300         WHERE OPER_UNT_ID = 90                                           
088400     END-EXEC.                                                            
088500                                                                          
088600     EVALUATE TRUE                                                        
088700         WHEN SQLCODE = +0                                                
088800         WHEN SQLCODE = +100                                              
088900         WHEN SQLCODE = -811                                              
089000             CONTINUE                                                     
089100         WHEN SQLCODE < ZERO                                              
089200             MOVE '7200-SELECT-TCOCNTL'                                   
089300                                      TO PV-PARAGRAPH                     
089400             MOVE 'SELECT'            TO PV-DB2-FUNCTION                  
089500             PERFORM 9200-SQL-ABEND                                       
089600         WHEN SQLCODE > ZERO                                              
089700         WHEN SQLWARN0 NOT = SPACES                                       
089800             MOVE '7200-SELECT-TCOCNTL'                                   
089900                                      TO PV-PARAGRAPH                     
090000             MOVE 'SELECT'            TO PV-DB2-FUNCTION                  
090100             PERFORM 9300-SQL-WARNING                                     
090200     END-EVALUATE.                                                        
090300/                                                                         
090400******************************************************************        
090500 8000-WRITE-MAIN-HEADINGS.                                                
090600******************************************************************        
090700*                                                                         
090800     MOVE PV-STORE                   TO HL2-LOC-NBR.                      
090900     PERFORM 1250-SELECT-LOCATION-DESC.                                   
091000     MOVE PV-LOCATION-DESC           TO HL2-LOC-DESC.                     
091100                                                                          
091200     MOVE PV-DEPT                    TO HL3-DEPT-NBR.                     
091300     IF  PV-DEPT     = 0                                                  
091400        MOVE SPACES        TO HL3-DEPT-DESC                               
091500     ELSE                                                                 
091600        PERFORM 3100-GET-DEPT-DESC.                                       
091700                                                                          
091800     MOVE PV-DEPT-DESC                TO HL3-DEPT-DESC.                   
091900     ADD 1                            TO PV-PAGE-COUNT.                   
092000     MOVE PV-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
092100     MOVE ZERO                        TO PV-LINE-COUNT.                   
092200     MOVE COCNTL-BTCH-PRC-DTE         TO PV-FORMAT-DATE.                  
092300     MOVE PV-FORMAT-MM                TO HL1A-MM.                         
092400     MOVE PV-FORMAT-DD                TO HL1A-DD.                         
092500     MOVE PV-FORMAT-CCYY              TO HL1A-CCYY.                       
092600                                                                          
092700     WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS              
092800                 AFTER ADVANCING PAGE.                                    
092900                                                                          
093000     EVALUATE TRUE                                                        
093100       WHEN CC-RUN-WEEKLY                                                 
093200          WRITE REPORT-RECORD FROM HL-HEADING-LINE-1                      
093300             AFTER ADVANCING 2 LINES                                      
093400                                                                          
093500       WHEN CC-RUN-DAILY                                                  
093600          WRITE REPORT-RECORD FROM HL-HEADING-LINE-1A                     
093700             AFTER ADVANCING 2 LINES                                      
093800     END-EVALUATE.                                                        
093900                                                                          
094000     WRITE REPORT-RECORD FROM HL-HEADING-LINE-2                           
094100        AFTER ADVANCING 2 LINES.                                          
094200     WRITE REPORT-RECORD FROM HL-HEADING-LINE-3                           
094300        AFTER ADVANCING 2 LINES.                                          
094400     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
094500         AFTER ADVANCING 1 LINE.                                          
094600     ADD 7                            TO PV-LINE-COUNT.                   
094700*                                                                         
094800******************************************************************        
094900 8100-WRITE-SUB-HEADINGS.                                                 
095000******************************************************************        
095100*                                                                         
095200     WRITE REPORT-RECORD FROM HL-HEADING-LINE-4                           
095300        AFTER ADVANCING 1 LINES.                                          
095400                                                                          
095500     WRITE REPORT-RECORD FROM HL-HEADING-LINE-5                           
095600        AFTER ADVANCING 1 LINES.                                          
095700                                                                          
095800     WRITE REPORT-RECORD FROM DL-BLANK-LINE                               
095900         AFTER ADVANCING 1 LINE.                                          
096000     ADD 3                            TO PV-LINE-COUNT.                   
096100*                                                                         
096200 8500-CALL-XLKUT071.                                                      
096300     MOVE '8500-CALL-XLKUT071'        TO PV-ABEND-PARAGRAPH               
096400                                                                          
096500     CALL XL071-LOCATION-MODULE USING  XL071-COMMAREA-REC.                
096600                                                                          
096700     EVALUATE TRUE                                                        
096800         WHEN XL071-NO-ERRORS                                             
096900             CONTINUE                                                     
097000         WHEN XL071-LOC-NOT-FOUND                                         
097100         WHEN XL071-DC-ASGMT-NOT-FND                                      
097200             CONTINUE                                                     
097300         WHEN OTHER                                                       
097400             MOVE XL071-SQLCA         TO SQLCA                            
097500             MOVE '1250-SELECT-LOCATION-DESC'                             
097600                                      TO PV-PARAGRAPH                     
097700             MOVE 'SELECT'            TO PV-DB2-FUNCTION                  
097800             PERFORM 9200-SQL-ABEND                                       
097900         END-EVALUATE.                                                    
098000******************************************************************        
098100 9000-EOJ.                                                                
098200******************************************************************        
098300*                                                                         
098400     WRITE REPORT-RECORD FROM ER-END-OF-REPORT-LINE                       
098500        AFTER ADVANCING 2 LINES.                                          
098600                                                                          
098700*CLOSE FILES.                                                             
098800     CLOSE EXTRACT-FILE                                                   
092012* WR38972 NEXT LINE IS NEW - SJN                                          
092012           HEADING-FILE                                                   
098900           REPORT-FILE.                                                   
099000                                                                          
099100******************************************************************        
099200 9200-SQL-ABEND.                                                          
099300******************************************************************        
099400                                                                          
099500     MOVE SQLCODE                     TO SQLCODE2.                        
099600     MOVE SQLERRD(3)                  TO SQLERRD3.                        
099700                                                                          
099800     DISPLAY '** ABEND **         ** = SQLERROR'.                         
099900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
100000     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
100100     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
100200     DISPLAY '*** MESSAGE     = ' PV-DB2-OPERATION-MESSAGE-1.             
100300     DISPLAY '***             = ' PV-DB2-OPERATION-MESSAGE-2.             
100400     DISPLAY '***             = ' PV-DB2-OPERATION-MESSAGE-3.             
100500                                                                          
100600     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
100700     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
100800     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
100900     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
101000                                                                          
101100     COPY DPPD004.                                                        
101200                                                                          
101300     MOVE +4013                       TO PV-ABEND-CODE.                   
101400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
101500                                                                          
101600******************************************************************        
101700 9300-SQL-WARNING.                                                        
101800******************************************************************        
101900                                                                          
102000     DISPLAY '9300-SQL-WARNING'.                                          
102100     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
102200     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
102300     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
102400                                                                          
102500     MOVE SQLCODE                     TO SQLCODE2.                        
102600     MOVE SQLERRD(3)                  TO SQLERRD3.                        
102700     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
102800     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
102900     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
103000     DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                          
103100     DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                          
103200     DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                          
103300     DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                          
103400     DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                          
103500     DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                          
103600     DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                          
103700     DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                          
103800                                                                          
103900     COPY DPPD004.                                                        
104000                                                                          
104100     MOVE +4013                       TO PV-ABEND-CODE.                   
104200     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
104300*                                                                         
104400******************************************************************        
104500*  PERFORMS A NON-SQL ABEND FOR THE PROGRAM                               
104600******************************************************************        
104700 9400-ABEND.                                                              
104800*                                                                         
104900     DISPLAY '***************************'.                               
105000     DISPLAY '**       ABEND           **'.                               
105100     DISPLAY '**  IN PROGRAM RCKRP035  **'.                               
105200     DISPLAY '***************************'.                               
105300     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH.                     
105400     DISPLAY '**   ERROR         ** = ' DP003-RETURN-MSG.                 
105500     DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-1.               
105600     DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-2.               
105700                                                                          
105800     MOVE +4014                  TO PV-ABEND-CODE.                        
105900     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
106000*                                                                         
106100******************************************************************        
