/* REXX */                                                                      
/* SEKRX007 -                                                         */        
/*                                                                    */        
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = 'SEKRX007 - '                                                         
RCFILE_FILE_END_OF_FILE = 'Y'                                                   
RCFILE_FILE_NOT_END_OF_FILE = 'N'                                               
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
RCFILE_FILE_EOF = RCFILE_FILE_NOT_END_OF_FILE                                   
INACTIVE_DEFAULT_GROUP = 'INACTIVE'                                             
DELUSER_DEFAULT_GROUP_OWNER = 'DELUSER '                                        
HARD_DELETE = 'DEL'                                                             
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM                                                                     
CALL READ_RCFILE_RECORD                                                         
DO WHILE (RCFILE_FILE_EOF = RCFILE_FILE_NOT_END_OF_FILE)                        
    PARSE PULL INP01_RECORD                                                     
    RCFILE_USERID = SUBSTR(INP01_RECORD,1,8)                                    
    RCFILE_DEFAULT_GROUP = SUBSTR(INP01_RECORD,57,8)                            
    RCFILE_DEFAULT_GROUP_OWNER = SUBSTR(INP01_RECORD,65,8)                      
    RCFILE_LAST_USED_DATE = SUBSTR(INP01_RECORD,82,10)                          
    RCFILE_HARD_DELETE_IND = SUBSTR(INP01_RECORD,110,3)                         
    SELECT                                                                      
        WHEN (RCFILE_DEFAULT_GROUP_OWNER = DELUSER_DEFAULT_GROUP_OWNER),        
                                                                  THEN          
            SAY CURRPGM || STRIP(RCFILE_USERID) || ' CURRENTLY ' ||,            
                      'STATUSED FOR DATASET CLEANUP AND DELETION (' ||,         
                      'LAST USED ' || RCFILE_LAST_USED_DATE || ')'              
        WHEN (RCFILE_HARD_DELETE_IND = HARD_DELETE) THEN DO                     
            RCFILE_DEL_RECORD = SUBSTR(INP01_RECORD,1,56) ||,                   
                      INACTIVE_DEFAULT_GROUP ||,                                
                      DELUSER_DEFAULT_GROUP_OWNER ||,                           
                      SUBSTR(INP01_RECORD,73,37)                                
            CALL WRITE_RCFILE_DEL_RECORD                                        
        END                                                                     
        OTHERWISE nop                                                           
    END                                                                         
    CALL READ_RCFILE_RECORD                                                     
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_RCFILE_RECORD                                                */         
/*                                                                   */         
/* Read a RACF status file record.                                   */         
/* ----------------------------------------------------------------- */         
READ_RCFILE_RECORD:                                                             
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN                                                              
        RCFILE_FILE_EOF = RCFILE_FILE_END_OF_FILE                               
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RCFILE_DEL_RECORD                                           */         
/*                                                                   */         
/* Write a RACF status file delete record.                           */         
/* ----------------------------------------------------------------- */         
WRITE_RCFILE_DEL_RECORD:                                                        
    OUT01_RECORD = LEFT(RCFILE_DEL_RECORD,109)                                  
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
