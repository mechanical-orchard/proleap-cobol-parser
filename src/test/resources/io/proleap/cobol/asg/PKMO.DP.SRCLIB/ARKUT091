000100******************************************************************00010000
000200 IDENTIFICATION DIVISION.                                         00020000
000300******************************************************************00030000
000400                                                                  00040000
000500 PROGRAM-ID.             ARKUT091.                                00050021
000600 AUTHOR.                 JILL ZIMBAL.                             00060000
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
000800 DATE-WRITTEN            01-14-2010.                              00080000
000900 DATE-COMPILED.                                                   00090000
001000                                                                  00100000
001100******************************************************************00110000
001200*   THIS PROGRAM CREATES AN EXTRACT FILE OF ACTIVE EMPLOYEES.     00120000
001300*   THE DATA WILL BE USED ON THE TANDEM TO MATCH ON THE RP7100    00130000
001400*   REPORT.                                                       00140000
006200******************************************************************00150000
006300                                                                  00160000
003800******************************************************************        
003900* CHANGE LOG:                                                    *        
004000*                                                                *        
004100* 03/04/12  UPDATE PROGRAM TO PULL ASSOCIATE FIRST AND LAST NAME *        
 04200*           FOR THE NEW ARC APPLICATION.  UPDATED JOB TO FTP THE *        
004300*           ACTIVE ASSOCIATES FILE TO THE ARC SERVER INSTEAD OF  *        
004400*           TANDEM.                                                       
004500*           BY: PAUL STROEDE               WR/IR#: CRQ000000021993        
P10237******************************************************************        
P10237* CHANGE LOG:                                                             
P10237*                                                                         
P10237* DATE       : 08/11/2015                                                 
P10237* WR/IR#     : CHG0125054                                                 
P10237* PROGRAMMER : COGNIZANT                                                  
P10237* DESCRIPTION: CHANGES TO DECRYPT EMPLOYEE FIRST NAME AND                 
P10237*              LAST NAME AND WRITE INTO OUTPUT FILE AS CLEAR DATA         
P10237*              AS PART OF EMPLOYEE DISCOUNT CHANGES IN COSA               
P10237*              SECURITY PROJECT.                                          
P10237******************************************************************        
      * CHANGE LOG:                                                             
      *                                                                         
      * DATE       : 06/29/2017                                                 
      * WR/IR#     : CHG0168613                                                 
      * PROGRAMMER : BASKARAN S                                                 
      * DESCRIPTION: XST TABLE REPLACEMENT WITH FILE FROM WORKDAY               
      *              MAKING IT A NON-DB2 COBOL PROGRAM                          
P10237******************************************************************        
P10237******************************************************************        
      * CHANGE LOG:                                                             
      *                                                                         
      * DATE       : 02/06/2018                                                 
      * WR/IR#     : CHG0XXXXXX                                                 
      * PROGRAMMER : COGNIZANT                                                  
      * DESCRIPTION: CHANGED THE JOB CODE AS PART OF RESHAPING WORK             
      *              FORCE PROJECT                                              
P10237******************************************************************        
006400******************************************************************00170000
006500 ENVIRONMENT DIVISION.                                            00180000
006600******************************************************************00190000
006700                                                                  00200000
006800 CONFIGURATION SECTION.                                           00210000
006900                                                                  00220000
007000 SOURCE-COMPUTER.        IBM-3090.                                00230000
007100 OBJECT-COMPUTER.        IBM-3090.                                00240000
007200                                                                  00250000
007300 INPUT-OUTPUT SECTION.                                            00260000
007400                                                                  00270000
007500 FILE-CONTROL.                                                    00280000
007900                                                                  00290000
           SELECT WORKDY-EMPLOYEE-FILE                                  00300000
               ASSIGN TO INP01.                                         00310000
                                                                                
008000     SELECT ACTIVE-EMPLOYEE-FILE                                  00300000
008100         ASSIGN TO OUT01.                                         00310000
008200                                                                  00320000
008300******************************************************************00330000
008400 DATA DIVISION.                                                   00340000
008500******************************************************************00350000
008600                                                                  00360000
008700 FILE SECTION.                                                    00370000
008800                                                                  00380000
       FD WORKDY-EMPLOYEE-FILE                                          00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS                                     00410000
           LABEL RECORDS ARE STANDARD.                                  00420000
                                                                        00421000
       01 WORKDY-EMPLOYEE-REC               PIC X(090).                 00430022
                                                                        00490000
010400 FD ACTIVE-EMPLOYEE-FILE                                          00390000
010500     RECORDING MODE IS F                                          00400000
010600     BLOCK CONTAINS 0 RECORDS                                     00410000
010700     LABEL RECORDS ARE STANDARD.                                  00420000
                                                                        00421000
010900 01 ACTIVE-EMPLOYEE-REC               PIC X(137).                 00430022
                                                                        00490000
011000                                                                  00500000
011100 WORKING-STORAGE SECTION.                                         00510000
011800                                                                  00520000
011900 01 ABEND-CODE                        PIC S9(4)  VALUE ZEROES     00530000
012000                                                 COMP.            00540000
P10237    88  ABEND-4008-CONVERSION-ERROR              VALUE +4008.             
P10237                                                                          
012100 01 PS-PROGRAM-SWITCHES.                                          00550000
012200    05 PS-PROCESSING-SW               PIC X      VALUE SPACES.    00560000
012300       88 PS-CONTINUE-PROCESSING                 VALUE ' '.       00570000
012400       88 PS-STOP-PROCESSING                     VALUE 'S'.       00580000
P10237    05 PS-FIRST-LAST                  PIC X(05)  VALUE SPACES.            
P10237       88  PS-FIRST                              VALUE 'FIRST'.           
P10237       88  PS-LAST                               VALUE 'LAST'.            
          05 WS-WD-EOF                      PIC X      VALUE "N".               
             88 WD-EOF                                 VALUE "Y".               
                                                                        00590009
P10237******************************************************************        
P10237*                        VARIABLES                                        
P10237******************************************************************        
P10237 01 PV-PROGRAM-VARIABLES.                                                 
P10237    05  PV-DISP-FRST-LST              PIC X(48)  VALUE SPACES.            
P10237    05  PV-INPUT-LEN                  PIC 9(09)  VALUE 0.                 
P10237    05  PV-INPUT-TEXT                 PIC X(48).                          
P10237    05  PV-OUTPUT-TEXT                PIC X(34) VALUE SPACES.             
          05  PV-CURR-TMST                  PIC X(26)  VALUE SPACES.            
P10237*****************************************************************         
P10237*                        CONSTANTS                                        
P10237******************************************************************        
P10237 01 PC-PROGRAM-CONSTANTS.                                                 
P10237    05  PC-PROGRAM-NAME               PIC X(8)  VALUE 'ARKUT091'.         
030300                                                                  00840000
      ******************************************************************        
      * LAYOUT FOR WORKDAY FILE                                                 
      ******************************************************************        
       01 WS-WORKDAY-REC.                                                       
          05  WD-ASSOC-ID            PIC X(11).                                 
          05  WD-LAST-NAME           PIC X(30).                                 
          05  WD-FRST-NAME           PIC X(30).                                 
          05  WD-STORE-LOC           PIC X(04).                                 
          05  WD-JOB-CODE            PIC X(06).                                 
          05  WD-DEPT-NBR            PIC X(08).                                 
          05  WD-ASSOC-STATUS        PIC X.                                     
      *                                                                         
P10237******************************************************************        
P10237* LAYOUT FOR THE PARAMETERS NEEDED TO INVOKE PROTEGRITY MODULE   *        
P10237******************************************************************        
P10237     COPY PTYCCSIP.                                                       
P10237                                                                          
P10237******************************************************************        
      ******************************************************************00840100
      *  ARDEPT-REC                                                    *00840200
      ******************************************************************00840300
           COPY ARRD064.                                                00840420
                                                                        00840500
043000******************************************************************01180000
043100 PROCEDURE DIVISION.                                              01190000
043200******************************************************************01200000
043300                                                                  01210000
043400*--------------*                                                  01220000
043500 0000-MAINLINE.                                                   01230000
043600*--------------*                                                  01240000
032400     PERFORM 1000-INITIALIZATION.                                 01241005
049400                                                                  01260000
049632     PERFORM 2000-PROCESS-EMPL-USER-FILE                          01300000
             UNTIL WD-EOF.                                              01310000
049634                                                                  01320000
032400     PERFORM 9000-CLOSE.                                          01321005
049639     STOP RUN.                                                    01370000
049640                                                                  01380000
035800*----------------------------------------------------------       01381005
035900* 1000-INITIALIZATION.                                            01382005
036000*     INTIALIZES OR MOVES ANY VARIABLES BEFORE IT WILL            01383005
036100*     UPDATE OR INSERT A RECORD.                                  01384005
036200*----------------------------------------------------------       01385005
036300 1000-INITIALIZATION.                                             01386005
                                                                        01386105
           OPEN INPUT  WORKDY-EMPLOYEE-FILE.                            01386205
049300     OPEN OUTPUT ACTIVE-EMPLOYEE-FILE.                            01386205
                                                                        01386305
           PERFORM 4000-READ-WD-FILE.                                           
           IF WD-EOF                                                            
               DISPLAY ALL '*'                                                  
               DISPLAY '**  WORKDAY EMPLOYEE FILE IS EMPTY **'                  
               DISPLAY ALL '*'                                                  
           END-IF.                                                              
      *                                                                         
037251                                                                  01391805
059600*-------------------------------*                                 01392000
059700 2000-PROCESS-EMPL-USER-FILE.                                     01400000
059800*-------------------------------*                                 01410000
059900                                                                  01420000
063800     PERFORM 5000-WRITE-EMPL-DISC-REC.                            01430004
063801                                                                  01440000
           PERFORM 4000-READ-WD-FILE.                                           
                                                                                
      *-----------------------------*                                           
       4000-READ-WD-FILE.                                                       
      *-----------------------------*                                           
                                                                                
           READ WORKDY-EMPLOYEE-FILE                                            
           INTO WS-WORKDAY-REC                                                  
             AT END                                                             
                SET WD-EOF TO TRUE.                                             
                                                                                
072500*---------------------------------*                               01470000
072600 5000-WRITE-EMPL-DISC-REC.                                        01480000
072700*---------------------------------*                               01490000
072800     MOVE SPACES                   TO ARDEPT-REC.                 01500000
072900                                                                  01510000
                                                                        01510511
           IF (WD-ASSOC-ID NOT = SPACES)                                01511016
073000       MOVE WD-ASSOC-ID            TO ARDEPT-ASSOC-ID             01520005
P10237       MOVE WD-FRST-NAME           TO ARDEPT-ASSOC-FRST-NM                
P10237       MOVE WD-LAST-NAME           TO ARDEPT-ASSOC-LAST-NM                
073200       MOVE WD-ASSOC-STATUS        TO ARDEPT-STATUS-CDE           01531005
073300       MOVE WD-STORE-LOC           TO ARDEPT-STORE-NBR            01540023
                                                                        01540422
             IF WD-DEPT-NBR(5:3) = '962'                                01540516
                MOVE 'P'                 TO ARDEPT-DEPT-CDE             01540616
             ELSE                                                       01540716
                IF WD-DEPT-NBR(5:3) = '961' OR '578'                    01540816
                   MOVE 'D'              TO ARDEPT-DEPT-CDE             01541119
                ELSE                                                    01541216
                   IF WD-JOB-CODE      = '599401' OR '599404' OR        01541316
                                         '592401' OR '599402' OR        01541416
                                         '570301'                       01541417
                      MOVE 'D'           TO ARDEPT-DEPT-CDE             01541519
                   ELSE                                                 01541616
                      MOVE 'O'           TO ARDEPT-DEPT-CDE             01542016
                   END-IF                                               01542216
                END-IF                                                  01542316
             END-IF                                                     01542416
                                                                        01543016
073400       WRITE ACTIVE-EMPLOYEE-REC   FROM ARDEPT-REC                01550005
           END-IF.                                                      01560005
                                                                        01582005
107400******************************************************************02160000
107500*  END OF JOB ROUTINES.                                          *02170000
107600******************************************************************02180000
059800*----------------------------------------------------------       02180105
073900 9000-CLOSE.                                                      02180205
059800*----------------------------------------------------------       02180305
074000                                                                  02180405
049636                                                                  02182005
049637     CLOSE ACTIVE-EMPLOYEE-FILE.                                  02183007
           CLOSE WORKDY-EMPLOYEE-FILE.                                  02183007
                                                                        02187005
P10237*----------------------------------------------------------       02190005
P10237 Z999-ABEND.                                                      02200005
P10237*----------------------------------------------------------       02210005
P10237                                                                          
P10237         CALL 'ILBOABN0' USING ABEND-CODE.                                
P10237                                                                          
