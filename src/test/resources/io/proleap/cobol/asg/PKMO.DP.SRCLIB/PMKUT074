      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT074.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT074 - ADHOC STORE GROUP BUSINESS EVENT INTEGRITY CHECK.*        
      *                                                                *        
      *    THIS PROGRAM CREATES THE ADHOC STORE GROUP BUSINSESS FILE   *        
      *    USED IN THE DATA INTEGRITY PROCESS.  FIRST OPEN THE TWO     *        
      *    INPUT FILES AND ONE OUTPUT FILE.  ONCE OPEN READ THE TWO    *        
      *    INPUT FILES.  IF NOTHING IS IN EITHER OF THESE FILES ABEND  *        
      *    THE PROGRAM.  NEXT  TRY TO LOG ON TO ORACLE.  IF SUCCESSFUL *        
      *    THEN FIND THE CURRENT SYS DATE USING A SELECT STATEMENT.    *        
      *    ONCE THE SYS DATE HAS BEEN FOUND THEN SELECT THE AD HOC     *        
      *    RECORDS FROM THE PMT_ADHC_STR_GP AND PMT_STR_SELN TABLES    *        
      *    THAT HAVE NOT BEEN UPDATED SINCE THE TIMESTAMP FILE TIME.   *        
      *    MOVE THE SELECTED VARIABLES TO A COPYBOOK.  THEN IF THE     *        
      *    RUSBL_IND IS Y AND THE START AND END DATE FALL WITHING THE  *        
      *    DATERANGE PASSED IN FROM THE DATERANGE FILE WRITE ON THE    *        
      *    OUTPUT FILE.  IF NOT THEN CHECK TO SEE IF THE RECORD IS ON  *        
      *    THE PMT_PROMO_ITM_ASN. IF SO THEN WRITE THE RECORD TO THE   *        
      *    FILE, IF NOT THEN CHECK THE CLEARANCE TABLES.  IF ON THESE  *        
      *    TABLES THEN WRITE TO THE FILE OTHERWISE CHECK THE TIERED    *        
      *    TABLES.  IF FOUND THEN WRITE THE RECORD TO THE FILE, IF NOT *        
      *    THEN CHECK THE CMPTV TABLES.  IF FOUND HERE THEN WRITE THE  *        
      *    RECORD TO THE FILE OTHERWISE SKIP THIS RECORD AND GO THE    *        
      *    NEXT RECORD IN THE ADHOC CURSOR.  AFTER THE LAST REOCRD HAS *        
      *    BEEN WRITTEN TO THE FILE WRITE A TOTAL LINE TO THE FILE.    *        
      *    FINALLY CLOSE THE TWO INPUT FILES AND THE ONE OUTPUT FILE.  *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *    07/01/2003 - AMANDA NEWHART - WR 21770                      *        
      *      - ADDED A TIMESTAMP TO ALL THE WHERE CLAUSES THAT WERE    *        
      *        LOOKING FOR A DATE.                                     *        
      *                                                                *        
      *    08/05/2003 - AMANDA NEWHART - WR 21770                      *        
      *      - IN C120 CHANGED ON OF THE END_TMST TO STRT_TMST    E    *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT ADHOC-STORE-GROUP-FILE                                        
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  ADHOC-STORE-GROUP-FILE                                               
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  ADHOC-STR-GROUP-LINE             PIC X(150).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)   VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)   VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
           05  PS-DATE-RANGE-FILE-SW        PIC X(01)   VALUE 'N'.              
               88  PS-DATE-RANGE-FILE-END               VALUE 'Y'.              
               88  PS-DATE-RANGE-FILE-START             VALUE 'N'.              
           05  PS-TMST-FILE-SW              PIC X(01)   VALUE 'N'.              
               88  PS-TMST-FILE-END                     VALUE 'Y'.              
               88  PS-TMST-FILE-START                   VALUE 'N'.              
           05  PS-END-OF-ADHOC-CSR-SWITCH   PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-ADHOC-CSR                  VALUE 'Y'.              
               88  PS-NOT-END-OF-ADHOC-CSR              VALUE 'N'.              
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT074'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 040.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PC-AUTO-LOGON                PIC  X(01)  VALUE '/'.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SYS-DATE                  PIC  X(26)  VALUE SPACES.           
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-TEMP-VALUE                PIC  X(01)  VALUE SPACES.           
           05  PV-NEW-STRT-DATE             PIC  X(26)     VALUE SPACES.        
           05  PV-NEW-END-DATE              PIC  X(26)     VALUE SPACES.        
                                                                                
       01 PV-SELECT-VARIABLES.                                                  
           05  PV-STR-SELN-ID               PIC  9(10)  VALUE ZEROS.            
           05  PV-ADHC-STR-GP-ID            PIC  9(10)  VALUE ZEROS.            
           05  PV-ADHC-STR-GP-NM            PIC  X(25)  VALUE SPACES.           
           05  PV-RUSBL-IND                 PIC  X(01)  VALUE SPACES.           
           05  PV-RUSBL-STRT-TMST           PIC  X(19)  VALUE SPACES.           
           05  PV-RUSBL-END-TMST            PIC  X(19)  VALUE SPACES.           
           05  PV-RUSBL-STRT-DTE            PIC  X(10)  VALUE SPACES.           
           05  PV-RUSBL-END-DTE             PIC  X(10)  VALUE SPACES.           
           05  PV-STAT-CDE                  PIC  X(01)  VALUE SPACES.           
           05  PV-LAST-UPD-TMST             PIC  X(19)  VALUE SPACES.           
           05  PV-STRT-DTE-NULL             PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-END-DTE-NULL              PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-STRT-DTE-FMT-NULL         PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-END-DTE-FMT-NULL          PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-STR-SELN-ID-NULL          PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-ADHOC-CNT                 PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE ADHOC STORE GROUP BUSINESS EVENT.                   
      ******************************************************************        
           COPY PMRD704.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE FILE                                   *         
      *  - READ THE TIMESTAMP FILE                                    *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RETRIEVE ADHOC INFORMATION FROM TABLES PMT_ADHC_STR_GP     *         
      *    AND PMT_STR_SELN                                           *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C200-DECLARE-ADHOC-CURSOR.                                   
           PERFORM C300-OPEN-ADHOC-CURSOR.                                      
           PERFORM C400-FETCH-ADHOC-CURSOR.                                     
           PERFORM C500-PROCESS-ADHOC-CURSOR                                    
               UNTIL PS-END-OF-ADHOC-CSR                                        
           PERFORM C600-CLOSE-ADHOC-CURSOR.                                     
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'ADHOC CSR FETCHES FROM ORACLE  = ' PA-ADHOC-CNT.            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
                OUTPUT ADHOC-STORE-GROUP-FILE.                                  
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-TMST-FILE.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  ADHOC-STORE-GROUP-FILE.                                       
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-FIND-ITM-ASSN                                            *         
      * ==> PROCESSES:                                                *         
      *  - IF THE STR-SELN-ID IS FOUND HERE WRITE TO FILE OTHERWISE   *         
      *    CONTINUE ON TO THE CLEARANCE CHECK.                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND SO WRITE TO FILE                    *         
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                     *         
      *  - 1403 = NO ROW WAS RETURNED                                 *         
      * ==> PERFORMED FROM:  C500-PROCESS-ADHOC-CURSOR                *         
      *****************************************************************         
       C110-FIND-ITM-ASSN.                                                      
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   '1'                                                          
               INTO :PV-TEMP-VALUE                                              
               FROM PMT_PROMO_ITM_ASSN A                                        
               WHERE                                                            
                      A.STR_SELN_ID = :PV-STR-SELN-ID                           
                  AND A.PROMO_ITM_END_TMST >= TO_DATE(                          
                         :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                  AND A.PROMO_ITM_STRT_TMST <= TO_DATE(                         
                         :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
               PERFORM W100-WRITE-DETAIL                                        
                                                                                
             WHEN SQLCODE = 1403                                                
               PERFORM C120-FIND-CLRNC                                          
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'PMT_PROMO_ITM_ASSN'  TO PV-TABLE-NAME                      
               MOVE 'C110-FIND-ITM-ASSN'  TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-FIND-CLRNC                                               *         
      * ==> PROCESSES:                                                *         
      *  - IF THE STR-SELN-ID IS FOUND HERE WRITE TO FILE OTHERWISE   *         
      *    CONTINUE ON TO THE TIERED CHECK.                           *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND SO WRITE TO FILE                    *         
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                     *         
      *  - 1403 = NO ROW WAS RETURNED                                 *         
      * ==> PERFORMED FROM: C110-FIND-ITM-ASSN                        *         
      *****************************************************************         
       C120-FIND-CLRNC.                                                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    '1'                                                         
               INTO :PV-TEMP-VALUE                                              
               FROM PMT_CLRNC_PRICG A                                           
                   ,PMT_CLRNC_MDSE_LNE B                                        
                   ,PMT_CLRNC_PROMO C                                           
               WHERE                                                            
                     A.STR_SELN_ID = :PV-STR-SELN-ID                            
                 AND A.MDSE_SELN_LNE_ID = B.MDSE_SELN_LNE_ID                    
                 AND B.CLRNC_PROMO_ID = C.CLRNC_PROMO_ID                        
                 AND C.END_TMST >= TO_DATE(                                     
                        :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')             
                 AND C.STRT_TMST <= TO_DATE(                                    
                        :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
               PERFORM W100-WRITE-DETAIL                                        
                                                                                
             WHEN SQLCODE = 1403                                                
               PERFORM C130-FIND-TIERED                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'PMT_CLRNC_PROMO   '  TO PV-TABLE-NAME                      
               MOVE 'C120-FIND-CLRNC   '  TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C130-FIND-TIERED                                              *         
      * ==> PROCESSES:                                                *         
      *  - IF THE STR-SELN-ID IS FOUND HERE WRITE TO FILE OTHERWISE   *         
      *    CONTINUE ON TO THE CMPTV CHECK.                            *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND SO WRITE TO FILE                    *         
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                     *         
      *  - 1403 = NO ROW WAS RETURNED                                 *         
      * ==> PERFORMED FROM: C120-FIND-CLRNC                           *         
      *****************************************************************         
       C130-FIND-TIERED.                                                        
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    '1'                                                         
               INTO :PV-TEMP-VALUE                                              
               FROM PMT_TIER_PRICG A                                            
                   ,PMT_PROMO B                                                 
                   ,PMT_TIER_PRICG_GP_ASSN C                                    
                   ,PMT_TIER_PRICG_GP D                                         
               WHERE                                                            
                     C.STR_SELN_ID = :PV-STR-SELN-ID                            
                 AND C.TIER_PRICG_GP_ID = D.TIER_PRICG_GP_ID                    
                 AND D.TIER_PRICG_ID = A.TIER_PRICG_ID                          
                 AND A.PROMO_ID = B.PROMO_ID                                    
                 AND B.DFLT_END_TMST >= TO_DATE(                                
                        :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')             
                 AND B.DFLT_STRT_TMST <= TO_DATE(                               
                        :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
               PERFORM W100-WRITE-DETAIL                                        
                                                                                
             WHEN SQLCODE = 1403                                                
               PERFORM C140-FIND-CMPTV-PRICE                                    
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'PMT_TIER_PRICG    '  TO PV-TABLE-NAME                      
               MOVE 'C130-FIND-TIERED  '  TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C140-FIND-CMPTV-PRICE                                         *         
      * ==> PROCESSES:                                                *         
      *  - IF THE STR-SELN-ID IS FOUND HERE WRITE TO FILE OTHERWISE   *         
      *    DO NOT WRITE IT TO THE FILE.                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND SO WRITE TO FILE                    *         
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                     *         
      *  - 1403 = NO ROW WAS RETURNED                                 *         
      * ==> PERFORMED FROM: C130-FIND-TIERED                          *         
      *****************************************************************         
       C140-FIND-CMPTV-PRICE.                                                   
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    '1'                                                         
               INTO :PV-TEMP-VALUE                                              
               FROM PMT_CMPTV_PRICG A                                           
               WHERE                                                            
                     A.STR_SELN_ID = :PV-STR-SELN-ID                            
                 AND A.CMPTV_PRICG_END_TMST >= TO_DATE(                         
                        :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')             
                 AND A.CMPTV_PRICG_STRT_TMST <= TO_DATE(                        
                        :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
               PERFORM W100-WRITE-DETAIL                                        
                                                                                
             WHEN SQLCODE = 1403                                                
               CONTINUE                                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                TO PV-SQL                            
               MOVE 'PMT_TIER_PRICG    '   TO PV-TABLE-NAME                     
               MOVE 'C140-FIND-CMPTV-PRICE' TO PV-PARAGRAPH-NAME                
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-ADHOC-CURSOR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES REUSBLE AND NONREUSBLES.        *         
      *  - JOIN TO THE PMT_STR_SELN AND PMT_ADHC_STR_GP TABLE TO GET  *         
      *    THE START AND END DATES IF THERE ARE NO START OR END DATES *         
      *    ON THE ADHC_STR_GP TABLE                                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-ADHOC-CURSOR.                                               
                                                                                
           EXEC SQL                                                             
                DECLARE ADHOCCSR                                                
                CURSOR FOR                                                      
                    SELECT                                                      
                         B.STR_SELN_ID                                          
                        ,A.ADHC_STR_GP_ID                                       
                        ,A.ADHC_STR_GP_NM                                       
                        ,A.RUSBL_IND                                            
                        ,TO_CHAR(A.RUSBL_STRT_TMST,                             
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.RUSBL_END_TMST,                              
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.RUSBL_STRT_TMST,                             
                                           'YYYY-MM-DD')                        
                        ,TO_CHAR(A.RUSBL_END_TMST,                              
                                           'YYYY-MM-DD')                        
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMT_ADHC_STR_GP A                                      
                        ,PMT_STR_SELN B                                         
                    WHERE A.ADHC_STR_GP_ID = B.ADHC_STR_GP_ID(+)                
                     AND A.LAST_UPD_TMST <=                                     
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * C300-OPEN-ADHOC-CURSOR.                                    *            
      * ==> PROCESSES:                                                *         
      *  - OPEN ADHOCCSR                                              *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ***************************************************************           
       C300-OPEN-ADHOC-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
                 OPEN ADHOCCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-ADHOC-CURSOR      '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON ADHOC-CURSOR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-ADHOC-CURSOR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-ADHOC-CURSOR  *         
      *****************************************************************         
       C400-FETCH-ADHOC-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH ADHOCCSR                                                   
                INTO  :PV-STR-SELN-ID          :PV-STR-SELN-ID-NULL             
                    , :PV-ADHC-STR-GP-ID                                        
                    , :PV-ADHC-STR-GP-NM                                        
                    , :PV-RUSBL-IND                                             
                    , :PV-RUSBL-STRT-TMST      :PV-STRT-DTE-NULL                
                    , :PV-RUSBL-END-TMST       :PV-END-DTE-NULL                 
                    , :PV-RUSBL-STRT-DTE       :PV-STRT-DTE-FMT-NULL            
                    , :PV-RUSBL-END-DTE        :PV-END-DTE-FMT-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-ADHOC-CNT                                        
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'ADHOC COUNT FROM FETCH = ' PA-ADHOC-CNT             
                   DISPLAY 'END OF ADHOC CSR'                                   
                   SET PS-END-OF-ADHOC-CSR TO TRUE                              
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STORE GROUP = ' PV-ADHC-STR-GP-ID             
                   MOVE 'C400-FETCH-ADHOC-CURSOR        '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON ADHOC-CURSOR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C500-PROCESS-ADHOC-CURSOR.                                   *          
      * ==> PROCESSES:                                               *          
      *  - IF IT IS REUSABLE CHECK THE START AND END TIME.           *          
      *    - WRITE THE DATA TO THE FILE                              *          
      *  - IF IS IS NOT A REUSABLE THEN GO THROUGH THREE MORE CHECKS *          
      *    TO DECIDE IF IT WILL BE WRITTEN TO THE FILE.              *          
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH           *          
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT.                *          
      * ==> PERFORMED FROM: A100-MAINLINE                            *          
      ****************************************************************          
       C500-PROCESS-ADHOC-CURSOR.                                               
                                                                                
           IF  PM704-RUSBL-IND = 'Y'                                            
           AND (PV-RUSBL-END-DTE >= CC-START-DATE                               
           AND PV-RUSBL-STRT-DTE <=  CC-END-DATE)                               
           AND (PV-RUSBL-STRT-DTE > 0                                           
           AND PV-RUSBL-END-DTE > 0 )                                           
                PERFORM W100-WRITE-DETAIL                                       
           ELSE                                                                 
              IF PV-STR-SELN-ID > 0                                             
                  PERFORM C110-FIND-ITM-ASSN                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-ADHOC-CURSOR.                                     
                                                                                
      ****************************************************************          
      * C600-CLOSE-ADEVNET-CURSOR.                                   *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE ADHOCCSR                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C600-CLOSE-ADHOC-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE ADHOCCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-ADHOC-CURSOR      '                          
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON ADHOC-CSR     '                          
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK                                            *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM: C400-FETCH-ADHOC-CURSOR                   *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM704-ADHOC-STR-GP-RECORD.                                
           MOVE PC-BUSINESS-EVNT-ID TO PM704-BUSINESS-EVNT-ID.                  
           MOVE PC-ACTION-CDE       TO PM704-ACTION-CDE.                        
           MOVE PV-SYS-DATE-XS      TO PM704-BUSINESS-EVNT-CRE-TMST.            
           MOVE PV-ADHC-STR-GP-ID   TO PM704-ADHC-STR-GP-ID.                    
           MOVE PV-ADHC-STR-GP-NM   TO PM704-ADHC-STR-GP-NM.                    
           MOVE PV-RUSBL-IND        TO PM704-RUSBL-IND.                         
           IF PV-RUSBL-STRT-TMST > 0                                            
               STRING PV-RUSBL-STRT-TMST DELIMITED BY SPACE                     
                      '.000000'     DELIMITED BY SIZE                           
                      INTO PM704-RUSBL-STRT-TMST                                
           END-IF.                                                              
           IF PV-RUSBL-END-TMST > 0                                             
               STRING PV-RUSBL-END-TMST DELIMITED BY SPACE                      
                       '.000000'     DELIMITED BY SIZE                          
                       INTO PM704-RUSBL-END-TMST                                
           END-IF.                                                              
           MOVE PV-STAT-CDE         TO PM704-STAT-CDE.                          
           STRING PV-LAST-UPD-TMST DELIMITED BY SPACE                           
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM704-LAST-UPD-TMST.                                     
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE AD HOC OUTPUT FILE            *         
      * ==> PERFORMED FROM:                                           *         
      *    C110-FIND-ITM-ASSN, C120-FIND-CLRNC, C130-FIND-TIERED      *         
      *    C140-FIND-CMPTV-PRICE, C500-PROCESS-ADHOC-CURSOR           *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE ADHOC-STR-GROUP-LINE FROM PM704-ADHOC-STR-GP-RECORD.           
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE ADHOC OUTPUT FILE.             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE ADHOC-STR-GROUP-LINE FROM PM700-TOTAL-RECORD.                  
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
