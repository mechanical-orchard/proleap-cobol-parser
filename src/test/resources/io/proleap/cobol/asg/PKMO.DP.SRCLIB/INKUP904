       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.           INKUP904.                                          
       AUTHOR.               COGNIZANT(KUSHAL)                                  
       INSTALLATION.         KOHLS DEPARTMENT STORES.                           
       DATE-WRITTEN          08/23/2019.                                        
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * NOTE THIS PROGRAM WAS COPIED FROM INKUT904                              
      * AND IS BEING RENAMED BECAUSE THIS PROGRAM WILL UPDATE                   
      * DB2                                                                     
      *****************************************************************         
      *THIS PROGRAM HAVE 2 INPUT FILES                                *         
      *                                                               *         
      *INPUT 1-UNZIP FILE-TO GET LOCATION NUMBER TO USE IN OUTPUT     *         
      *        LOCATION FILE NAME.                                    *         
      *INPUT 2-A CONTROL CARD INDICATING IF THIS IS A TEST OR PROD    *         
      *        RUN.                                                   *         
      *                                                               *         
      *-GET EXSITING GDG COUNT TO DETERMINE NEXT GENERATION NUMBER.   *         
      *-DYNAMICALLY ALLOCATE FILE(GDG) UNDER LOCATION GDG BASE.       *         
      *-COPY UNZIPPED FILE DATA TO THE ALLOCATED FILE.                *         
      *****************************************************************         
      * CHANGE HISTORY                                                          
      *                                                                         
RONNA * 05/25/2023 - BY RONNA MCDONALD    WR:  CHG0288464                       
RONNA *            - RENAMED PROGRAM FROM INKUT904 TO INKUP904                  
RONNA *            - INSERT A 'FI' RECORD TYPE INTO TINVLAP TABLE               
RONNA *              TO INDICATE THAT A TRANSMISSION FILE EXISTS                
RONNA *              AND THE STORES/INVENTORY CONTROL CAN SEE                   
RONNA *              THE STATUS OF THE TRANSMISSION FILE                        
RONNA *            - ADD A CONTROL CARD SO TO INDICATE IF THIS IS               
RONNA *              RUNNING IN PROD OR TEST WITHOUT HAVING TO                  
RONNA *              CHANGE THE PROGRAM CODE                                    
RONNA *                                                                         
RONNA2* 05/25/2023 - BY RONNA MCDONALD    WR:  CHG0288952                       
RONNA2*            - WRITE OUT THE UNZIPPED FILE TO OUTPUT INSTEAD              
RONNA2*              OF THE ZIPPED FILE                                         
RONNA2*            - THE FILE WILL ALREADY BE UNZIPPED FOR THE INK908           
RONNA2*              JOB                                                        
RONNA2*            - ONLY CREATE AN UNZIPPED FILE IF                            
RONNA2*              - THE STORE IS IN AN ACTIVE INVENTORY CYCLE                
RONNA2*              - THE STORE'S TRANSMISSION STATUS IN TINVPAR               
RONNA2*                IS NOT 'Y' FOR BALANCED                                  
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT IN-BIZ-UNZIP-FILE                                             
               ASSIGN TO INP01.                                                 
RONNS                                                                           
RONNA      SELECT IN-ENV-CNTL-FILE                                              
RONNA2         ASSIGN TO INP02.                                                 
                                                                                
           SELECT IN-LOC-FILE-CC                                                
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD  IN-BIZ-UNZIP-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IN-BIZ-UNZIP-FILE-REC           PIC X(58).                           
RONNA                                                                           
RONNA  FD  IN-ENV-CNTL-FILE                                                     
RONNA      LABEL RECORDS ARE STANDARD                                           
RONNA      RECORDING MODE IS F                                                  
RONNA      BLOCK CONTAINS 0 RECORDS.                                            
RONNA  01  IN-ENV-CNTL-REC                 PIC X(80).                           
                                                                                
       FD  IN-LOC-FILE-CC                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
RONNA2 01  IN-LOC-FILE-CC-REC              PIC X(58).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *  PARAMETER LIST FOR THE DYNAMIC ALLOCATION ROUTINE             *        
      ******************************************************************        
      *01  DP023-DYNALC.                                                        
      *01  DP023-DYNALC-PARMS.                                                  
      *01  DP023-DYNALC-PD-PARMS.                                               
      *01  DP023-ABEND-CODE                                                     
      *01  DP023-SUB                                                            
      *01  DP023-MAX                                                            
      *01  DP023-RETURN-CODES.                                                  
      *01  DP023-RETURN-CODE-CONSTANTS.                                         
           COPY DPWS023.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *  PROGRAM'S ACCUMULATORS                                       *         
      *****************************************************************         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-ROWS-FETCHED             PIC 9(09)       VALUE ZEROES.        
RONNA2     05  PA-UNZIP-RECS-READ          PIC 9(09)       VALUE ZEROES.        
RONNA2     05  PA-UNZIP-RECS-WRITTEN       PIC 9(09)       VALUE ZEROES.        
                                                                                
RONNA                                                                   00390000
RONNA *****************************************************************         
RONNA *  PROGRAM'S ERROR MESSAGES                                     *         
RONNA *****************************************************************         
RONNA  01  PE-PROGRAM-ERROR-MESSAGES.                                   00400000
RONNA      05  PE-MSG-01                       PIC X(50) VALUE          00430000
RONNA           'ATTEMPT TO INSERT ROW ON TINVLAP FAILED          '.    00440000
RONNA      05  PE-MSG-02                       PIC X(50) VALUE          00389000
RONNA           'PROGRAM MAXIMUM RETRY COUNT HAS BEEN EXCEEDED'.        INKUT119
RONNA      05  PE-MSG-03                       PIC X(30) VALUE                  
RONNA           'DB2 SELECT ATTEMPTED         '.                                
RONNA                                                                   00550000
      *****************************************************************         
      *  PROGRAM'S CONSTANTS                                          *         
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC X(08)        VALUE                   
RONNA          'INKUP904'.                                                      
RONNA      05  PC-PROD-QUAL            PIC X(04)        VALUE                   
RONNA          'PKMO'.                                                          
RONNA      05  PC-TEST-QUAL            PIC X(04)        VALUE                   
RONNA          'TKMA'.                                                          
RONNA      05  PC-FI                   PIC X(02)        VALUE           00389000
RONNA          'FI'.                                                            
RONNA      05  PC-FR                   PIC X(02)        VALUE           00389000
RONNA          'FR'.                                                            
RONNA      05  PC-MAX-RETRIES          PIC S9(4)   COMP VALUE +3.       INKUT119
                                                                                
      *****************************************************************         
      *  PROGRAM'S SWITCHES                                           *         
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
RONNA      05  PS-TINVPAR-FOUND-SW         PIC X      VALUE 'N'.                
RONNA          88  PS-TINVPAR-FOUND                   VALUE 'Y'.                
RONNA          88  PS-TINVPAR-NOT-FOUND               VALUE 'N'.                
           05  PS-END-OF-FILE-SW           PIC X(01)  VALUE 'N'.                
               88  PS-END-OF-FILE                     VALUE 'Y'.                
               88  PS-NOT-END-OF-FILE                 VALUE 'N'.                
RONNA      05  PS-END-CC-FILE-SW           PIC X(01)  VALUE 'N'.                
RONNA          88  PS-END-CC-FILE                     VALUE 'Y'.                
RONNA          88  PS-NOT-END-CC-FILE                 VALUE 'N'.                
           05  PS-FOUND-DSN-SW-TABLE OCCURS 5 TIMES.                            
               10  PS-FOUND-DSN-SW        PIC X(01)   VALUE 'N'.                
                   88  PS-FOUND-DSN-YES               VALUE 'Y'.                
                   88  PS-FOUND-DSN-NO                VALUE 'N'.                
           05  PS-BAD-RETURN-FROM-23-SW   PIC X(01)   VALUE 'N'.                
               88  PS-BAD-RETURN-DP023-YES            VALUE 'Y'.                
               88  PS-BAD-RETURN-DP023-NO             VALUE 'N'.                
                                                                                
      *****************************************************************         
      *  PROGRAM'S VARIABLES                                          *         
      *****************************************************************         
       01  INPUT-FILE-RECORD.                                                   
           05  LOC-NBR                   PIC X(04).                             
           05  FILE-DATA                 PIC X(54).                             
RONNA                                                                           
RONNA  01  CC-CNTL-REC.                                                         
RONNA      05  CC-ENV-IND                PIC X(01).                             
RONNA          88  CC-TEST               VALUE 'T'.                             
RONNA      05  CC-FILLER                 PIC X(79).                             
                                                                                
       01  PV-GEN-COUNT                  PIC  9(01)  VALUE ZEROS.               
RONNA  01  PV-SQL-SUB                    PIC S9(04) VALUE ZERO  COMP.           
RONNA  01  PV-PARAGRAPH-NAME             PIC  X(30) VALUE SPACES.               
RONNA  01  PV-OPERATION-ATTEMPTED        PIC  X(50) VALUE SPACES.               
RONNA  01  PV-DISPLAY-LOC                PIC  9(04) VALUE ZEROS.                
RONNA  01  PV-DISPLAY-INV-ID             PIC  9(04) VALUE ZEROS.                
       01  PV-INV-DSN.                                                          
           05  PV-INV-DSN-P-OR-T             PIC  X(04)  VALUE 'PKMO'.          
           05  PV-INV-DSN-COMMON             PIC  X(13)                         
                     VALUE '.IN.KOHLS.STR'.                                     
           05  PV-INV-DSN-STR-LOC            PIC  9(04)  VALUE ZEROS.           
           05  PV-INV-DSN-G000               PIC  X(05)  VALUE '.G000'.         
           05  PV-INV-DSN-GEN                PIC  9(01)  VALUE ZERO.            
           05  PV-INV-DSN-V00                PIC  X(03)  VALUE 'V00'.           
RONNA                                                                           
RONNA *****************************************************************         
RONNA *  ABEND ROUTINE WORKING STORAGE                                          
RONNA *****************************************************************         
RONNA                                                                           
RONNA      COPY DPWS004.                                                        
RONNA                                                                           
RONNA  01  ABEND-CODE                          PIC S9(04)  VALUE +0             
RONNA                                                      COMP SYNC.           
RONNA      88  ABEND-4003                                  VALUE +4003.         
RONNA                                                                           
RONNA  EJECT                                                                    
RONNA      EXEC SQL                                                             
RONNA          INCLUDE SQLCA                                                    
RONNA      END-EXEC                                                             
RONNA                                                                           
RONNA      EXEC SQL                                                             
RONNA          INCLUDE TINVLAP                                                  
RONNA      END-EXEC                                                             
RONNA                                                                           
RONNA      EXEC SQL                                                             
RONNA          INCLUDE TINVPAR                                                  
RONNA      END-EXEC                                                             
RONNA                                                                           
RONNA      EXEC SQL                                                             
RONNA          INCLUDE TINCNTL                                                  
RONNA      END-EXEC                                                             
RONNA                                                                           
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      *    0000-MAINLINE LOGIC.                                       *         
      *****************************************************************         
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 8000-GDG-COUNT.                                              
RONNA                                                                           
RONNA      MOVE PV-INV-DSN-STR-LOC TO INVLAP-LOC-NBR                            
RONNA                                 PV-DISPLAY-LOC                            
RONNA                                 INVPAR-LOC-NBR.                           
RONNA      SET PS-TINVPAR-NOT-FOUND TO TRUE.                                    
RONNA      PERFORM 4000-SELECT-TINVPAR.                                         
                                                                                
RONNA      IF PS-TINVPAR-FOUND                                                  
RONNA2        IF INVPAR-FIL-TOT-BAL-CDE = 'Y'                                   
RONNA2           DISPLAY 'LOCATION ' LOC-NBR ' TOTALS BALANCED '                
RONNA2           DISPLAY 'NO NEED TO CREATE ANOTHER UNZIPPED FILE'              
RONNA2        ELSE                                                              
                 PERFORM 2000-CREATE-LOC-FILE                                   
RONNA2           PERFORM 3200-WRITE-OUTPUT-RECORD                               
                 PERFORM 3000-PROCESS-FILE                                      
                  UNTIL PS-END-OF-FILE                                          
RONNA                                                                           
RONNA            MOVE INVPAR-INV-ID    TO INVLAP-INV-ID                         
RONNA                                     PV-DISPLAY-INV-ID                     
RONNA            MOVE PC-FI            TO INVLAP-LPTP-NBR                       
RONNA            MOVE PC-FR            TO INVLAP-INV-STAT-CDE                   
RONNA            MOVE ZERO             TO INVLAP-LPTP-EXT-AMT                   
RONNA                                     INVLAP-LPTP-QTY                       
RONNA            MOVE 'Y'              TO INVLAP-STR-TRSM-IND                   
RONNA            PERFORM 4100-INSERT-TINVLAP                                    
RONNA2        END-IF                                                            
RONNA      ELSE                                                                 
RONNA2        DISPLAY 'LOCATION ' LOC-NBR ' NOT IN AN ACTIVE'                   
RONNA2        DISPLAY 'INVENTORY CYCLE. NO UNZIPPED FILE CREATED.'              
RONNA      END-IF.                                                              
RONNA                                                                           
           PERFORM 9900-EOJ-LOGIC.                                              
                                                                                
           GOBACK.                                                              
                                                                                
      *****************************************************************         
      *    1000-INITIALIZATION.                                       *         
      *****************************************************************         
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  IN-BIZ-UNZIP-FILE                                        
RONNA                  IN-ENV-CNTL-FILE.                                        
                                                                                
RONNA2     MOVE 0 TO PA-UNZIP-RECS-READ                                         
RONNA2               PA-UNZIP-RECS-WRITTEN.                                     
                                                                                
RONNA      SET PS-NOT-END-CC-FILE TO TRUE.                                      
RONNA      PERFORM 1010-READ-IN-ENV-CNTL-FILE.                                  
RONNA                                                                           
RONNA *    SET THE ENVIRONMENT FOR THE DYNAMICALLY ALLOCATED FILE               
RONNA      IF PS-END-CC-FILE                                                    
RONNA         DISPLAY ' * END OF CC FILE - DEFAULT TO PKMO '                    
RONNA *       WHEN END OF FILE DEFAULT TO PROD                                  
RONNA         MOVE PC-PROD-QUAL TO PV-INV-DSN-P-OR-T                            
RONNA      ELSE                                                                 
RONNA *       WHEN CONTROL CARD HAS A 'T' IN THE FIRST POSITION                 
RONNA *       DEFAULT TO THE DYNAMICALLY ALLOCATED FILE TO TEST                 
RONNA         DISPLAY ' CC-ENV-IND VALUE IS: ' CC-ENV-IND                       
RONNA         IF CC-TEST                                                        
RONNA            MOVE PC-TEST-QUAL TO PV-INV-DSN-P-OR-T                         
RONNA         ELSE                                                              
RONNA *       WHEN CONTROL CARD IS NOT 'T' IN THE FIRST POSITION                
RONNA *       DEFAULT TO THE DYNAMICALLY ALLOCATED FILE TO PROD                 
RONNA            MOVE PC-PROD-QUAL TO PV-INV-DSN-P-OR-T                         
RONNA         END-IF                                                            
RONNA      END-IF.                                                              
RONNA                                                                           
           SET PS-NOT-END-OF-FILE TO TRUE.                                      
           PERFORM 1100-READ-IN-BIZ-UNZIP-FILE.                                 
RONNA *****************************************************************         
RONNA *    1010-READ CONTROL CARD FILE TO GET THE ENVIRONMENT         *         
RONNA *****************************************************************         
RONNA  1010-READ-IN-ENV-CNTL-FILE.                                              
RONNA                                                                           
RONNA      READ IN-ENV-CNTL-FILE  INTO CC-CNTL-REC                              
RONNA           AT END SET PS-END-CC-FILE TO TRUE.                              
                                                                                
      *****************************************************************         
      *    1100-READ FIRST RECORD FROM BIZLIK UNZIPED FILE.           *         
      *****************************************************************         
       1100-READ-IN-BIZ-UNZIP-FILE.                                             
                                                                                
RONNA2     READ IN-BIZ-UNZIP-FILE INTO INPUT-FILE-RECORD                        
RONNA2         AT END SET PS-END-OF-FILE TO TRUE                                
RONNA2         NOT AT END ADD +1 TO PA-UNZIP-RECS-READ.                         
RONNA2                                                                          
      *****************************************************************         
      *    2000-ALLOCATE LOCATION FILE.                               *         
      *****************************************************************         
       2000-CREATE-LOC-FILE.                                                    
                                                                                
           INITIALIZE DP023-DYNALC-PARMS.                                       
      *ADD 1 TO GEN-COUNT TO CREATE NEW/NEXT GDG.                               
           ADD  +1 TO PV-GEN-COUNT                                              
           MOVE PV-GEN-COUNT TO PV-INV-DSN-GEN                                  
           MOVE 'OUT01' TO DP023-DDNAME.                                        
           SET DP023-DSNAME-SYSOUT TO TRUE.                                     
           SET DP023-FREE-CLOSE-YES TO TRUE.                                    
           SET DP023-RECFM-FIXED-BLOCK TO TRUE.                                 
           SET DP023-DISP-NEW          TO TRUE.                                 
RONNA2     MOVE 58     TO DP023-RECSZ.                                          
RONNA2     MOVE 600000 TO DP023-RECOUNT.                                        
               MOVE 'B'    TO DP023-PRINT-CLASS                                 
               STRING PV-INV-DSN DELIMITED BY SIZE                              
               INTO DP023-DSNAME                                                
           MOVE 'SMTP' TO DP023-PRINT-WRITER.                                   
           PERFORM DP023-0000-DYNALC.                                           
           OPEN OUTPUT IN-LOC-FILE-CC.                                          
                                                                                
      *****************************************************************         
      *    3000-MOVE ZIP FILE DATA TO THE ALLOCATED FILE.             *         
      *****************************************************************         
       3000-PROCESS-FILE.                                                       
RONNA2     PERFORM 1100-READ-IN-BIZ-UNZIP-FILE                                  
             IF PS-NOT-END-OF-FILE                                              
           PERFORM 3200-WRITE-OUTPUT-RECORD                                     
             END-IF.                                                            
RONNA2                                                                          
      *****************************************************************         
      *    3200-WRITE RECORDS TO OUTPUT FILE                          *         
      *****************************************************************         
       3200-WRITE-OUTPUT-RECORD.                                                
                                                                                
RONNA2     WRITE IN-LOC-FILE-CC-REC FROM INPUT-FILE-RECORD.                     
RONNA2     ADD +1 TO PA-UNZIP-RECS-WRITTEN.                                     
                                                                                
RONNA ***************************************************************** 03300000
RONNA * SELECT TINVPAR RECORD FOR STATUS OF CYCLE AND TRANSMISSION      03310000
RONNA ***************************************************************** 03330000
RONNA  4000-SELECT-TINVPAR.                                             03350000
RONNA                                                                           
RONNA      PERFORM WITH TEST AFTER                                              
RONNA          VARYING PV-SQL-SUB FROM 1 BY 1                                   
RONNA          UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
RONNA             OR   SQLCODE = 0                                              
RONNA             OR   SQLCODE = +100)                                          
RONNA                                                                           
RONNA          EXEC SQL                                                         
RONNA              SELECT A.INV_ID                                              
RONNA2                   ,A.FIL_TOT_BAL_CDE                                     
RONNA              INTO :INVPAR-INV-ID                                          
RONNA2                 ,:INVPAR-FIL-TOT-BAL-CDE                                 
RONNA              FROM TINVPAR A                                               
RONNA                  ,TINCNTL B                                               
RONNA              WHERE A.LOC_NBR = :INVLAP-LOC-NBR                            
RONNA                AND A.LOC_INV_STAT_CDE = 'IN'                              
RONNA                AND A.ACTL_FIN_BK_DTE = '9999-09-09'                       
RONNA                AND B.ACTV_IND = 'Y'                                       
RONNA                AND A.INV_ID   = B.INV_ID                                  
RONNA          END-EXEC                                                         
RONNA                                                                           
RONNA          EVALUATE TRUE                                                    
RONNA              WHEN SQLCODE = +100                                          
RONNA                  SET PS-TINVPAR-NOT-FOUND TO TRUE                         
RONNA                  MOVE INVLAP-LOC-NBR TO PV-DISPLAY-LOC                    
RONNA                  DISPLAY 'NO ACTIVE CYCLE FOR LOC:'                       
RONNA                           PV-DISPLAY-LOC                                  
RONNA              WHEN SQLCODE = 0                                             
RONNA                  SET PS-TINVPAR-FOUND TO TRUE                             
RONNA                  MOVE INVLAP-LOC-NBR TO PV-DISPLAY-LOC                    
RONNA                  DISPLAY 'ACTIVE CYCLE FOUND FOR LOC:'                    
RONNA                           PV-DISPLAY-LOC                                  
RONNA              WHEN SQLCODE = -904                                          
RONNA              WHEN SQLCODE = -911                                          
RONNA              WHEN SQLCODE = -913                                          
RONNA                  CONTINUE                                                 
RONNA              WHEN SQLWARN NOT = SPACES                                    
RONNA              WHEN SQLCODE NOT = ZERO                                      
RONNA                  MOVE '4000-SELECT-TINVPAR'                               
RONNA                      TO PV-PARAGRAPH-NAME                                 
RONNA                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                 
RONNA                  PERFORM 9999-SQL-ABEND                                   
RONNA          END-EVALUATE                                                     
RONNA      END-PERFORM.                                                         
RONNA                                                                           
RONNA      IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
RONNA          MOVE '4000-SELECT-TINVPAR' TO PV-PARAGRAPH-NAME                  
RONNA          MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                 03720000
RONNA          PERFORM 9999-SQL-ABEND                                           
RONNA      END-IF.                                                              
RONNA                                                                           
RONNA                                                                   03360000
RONNA ***************************************************************** 03300000
RONNA * INSERT A 'FI' FILE TYPE INTO THE TINVLAP TABLE.                 03310000
RONNA ***************************************************************** 03330000
RONNA  4100-INSERT-TINVLAP.                                             03350000
RONNA                                                                           
RONNA      PERFORM WITH TEST AFTER                                              
RONNA          VARYING PV-SQL-SUB FROM 1 BY 1                                   
RONNA          UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
RONNA             OR   SQLCODE = 0                                              
RONNA             OR   SQLCODE = +100                                           
RONNA             OR   SQLCODE = -803)                                          
RONNA                                                                   03360000
RONNA          EXEC SQL                                                 03400000
RONNA              INSERT INTO TINVLAP                                          
RONNA                     ( INV_ID                                      03520000
RONNA                      ,LOC_NBR                                     03530000
RONNA                      ,LPTP_NBR                                    03421000
RONNA                      ,LPTP_EXT_AMT                                03422000
RONNA                      ,LPTP_QTY                                    03423000
RONNA                      ,INV_STAT_CDE                                03424000
RONNA                      ,CRE_TMST                                    03425000
RONNA                      ,STR_TRSM_IND )                               3425000
RONNA              VALUES (:INVLAP-INV-ID                                       
RONNA                     ,:INVLAP-LOC-NBR                                      
RONNA                     ,:INVLAP-LPTP-NBR                                     
RONNA                     ,:INVLAP-LPTP-EXT-AMT                                 
RONNA                     ,:INVLAP-LPTP-QTY                                     
RONNA                     ,:INVLAP-INV-STAT-CDE                                 
RONNA                     ,CURRENT TIMESTAMP                                    
RONNA                     ,:INVLAP-STR-TRSM-IND  )                              
RONNA          END-EXEC                                                  3560000
RONNA                                                                    3570000
RONNA          EVALUATE TRUE                                             3580000
RONNA              WHEN SQLCODE = 0                                      3590000
RONNA                  DISPLAY '** INSERTED FIRST FI TYPE INTO TINVLAP.' 3591000
RONNA                  DISPLAY '** INV ID IS :  ' PV-DISPLAY-INV-ID      3591000
RONNA                  DISPLAY '** LOC NBR IS:  ' PV-DISPLAY-LOC         3591000
RONNA              WHEN SQLCODE = -803                                   3590000
RONNA                  DISPLAY '** FI RECORD TYPE ALREADY EXISTS. '             
RONNA                  DISPLAY '** NO INSERT INTO TINVLAP DONE.   '             
RONNA              WHEN SQLCODE = -904                                          
RONNA              WHEN SQLCODE = -911                                          
RONNA              WHEN SQLCODE = -913                                          
RONNA                  CONTINUE                                                 
RONNA              WHEN OTHER                                            3700000
RONNA                  MOVE INVLAP-LOC-NBR TO PV-DISPLAY-LOC                    
RONNA                  DISPLAY '**  TINVLAP INSERT FAILED FOR LOC: '     3621000
RONNA                        PV-DISPLAY-LOC                                     
RONNA                  MOVE '4100-INSERT-TINVLAP      '                  3640000
RONNA                                     TO PV-PARAGRAPH-NAME           3650000
RONNA                  MOVE PE-MSG-01     TO PV-OPERATION-ATTEMPTED      3720000
RONNA                  PERFORM 9999-SQL-ABEND                            3730000
RONNA          END-EVALUATE                                              3740000
RONNA      END-PERFORM.                                                         
RONNA                                                                           
RONNA      IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
RONNA          MOVE '4100-INSERT-TINVLAP' TO PV-PARAGRAPH-NAME                  
RONNA          MOVE PE-MSG-02             TO PV-OPERATION-ATTEMPTED      3720000
RONNA          PERFORM 9999-SQL-ABEND                                           
RONNA      END-IF.                                                              
RONNA                                                                           
                                                                         3750000
      *****************************************************************         
      *    8000-GDG-COUNT.                                            *         
      *****************************************************************         
        8000-GDG-COUNT.                                                         
           PERFORM 8100-DSN-PRESENT-CHK                                         
               VARYING PV-INV-DSN-GEN FROM 1 BY 1                               
                   UNTIL PV-INV-DSN-GEN > 5                                     
                      OR PS-BAD-RETURN-DP023-YES.                               
                                                                                
           IF PS-BAD-RETURN-DP023-NO                                            
               MOVE 0 TO PV-GEN-COUNT                                           
               PERFORM 8200-SET-GEN-COUNT                                       
                   VARYING PV-INV-DSN-GEN FROM 1 BY 1                           
                       UNTIL PV-INV-DSN-GEN > 5                                 
           END-IF.                                                              
      *****************************************************************         
      *    8100-DSN-PRESENT-CHK.                                      *         
      *    THIS ROUTINE CHECKS FOR THE PRESENCE OF THE EXISTING       *         
      *    INVENTORY FILE GENERATIONS.                                *         
      *****************************************************************         
       8100-DSN-PRESENT-CHK.                                                    
                                                                                
           INITIALIZE DP023-DYNALC-PARMS.                                       
           MOVE 'INP99'                TO DP023-DDNAME.                         
           MOVE LOC-NBR                TO PV-INV-DSN-STR-LOC.                   
           MOVE PV-INV-DSN             TO DP023-DSNAME.                         
           SET DP023-DISP-SHARE        TO TRUE.                                 
           SET DP023-FREE-CLOSE-NO     TO TRUE.                                 
           SET DP023-RECFM-FIXED-BLOCK TO TRUE.                                 
RONNA2     MOVE 58                     TO DP023-RECSZ.                          
           SET DP023-RECOUNT-ALLOCATE-TAPE TO TRUE.                             
                                                                                
           SET DP023-DISABLE-DISPLAY-ABEND TO TRUE.                             
                                                                                
           DISPLAY ' ALLOCATING ' DP023-DDNAME                                  
                   ' TO '         DP023-DSNAME.                                 
                                                                                
           PERFORM DP023-0000-DYNALC.                                           
           DISPLAY '** PARMS = ' DP023-DYNALC-PARMS.                            
           IF DP023-LASTRC = ZERO                                               
               DISPLAY ' *** EXISTING  DSN: ' DP023-DSNAME                      
               SET PS-FOUND-DSN-YES (PV-INV-DSN-GEN) TO TRUE                    
           ELSE                                                                 
               IF DP023-LASTRC = 4009                                           
                   SET PS-FOUND-DSN-NO (PV-INV-DSN-GEN) TO TRUE                 
                   DISPLAY 'NO GDG FOUND'                                       
               ELSE                                                             
                   SET PS-BAD-RETURN-DP023-YES TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
      *    8200-SET-GEN-COUNT.                                        *         
      *    THIS ROUTINE WILL SET THE GEN COUNT BASED UPON THE FILES   *         
      *    FOUND.                                                     *         
      *****************************************************************         
       8200-SET-GEN-COUNT.                                                      
                                                                                
           IF PS-FOUND-DSN-YES (PV-INV-DSN-GEN)                                 
               ADD +1 TO PV-GEN-COUNT                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *    9900-EOJ-LOGIC.                                            *         
      *      THIS IS PERFORMED AT END OF INPUT FILE.                  *         
      *      PURPOSE: CLOSE INPUT FILE AND DISPLAY RECORD COUNTS      *         
      *****************************************************************         
       9900-EOJ-LOGIC.                                                          
                                                                                
RONNA2     DISPLAY 'UNZIP RECORDS READ   :  ' PA-UNZIP-RECS-READ.               
RONNA2     DISPLAY 'UNZIP RECORDS WRITTEN:  ' PA-UNZIP-RECS-WRITTEN.            
RONNA2                                                                          
RONNA      IF PS-TINVPAR-FOUND                                                  
RONNA2        IF INVPAR-FIL-TOT-BAL-CDE = 'Y'                                   
RONNA2           CLOSE IN-BIZ-UNZIP-FILE                                        
RONNA2                 IN-ENV-CNTL-FILE                                         
RONNA2        ELSE                                                              
RONNA2           CLOSE IN-BIZ-UNZIP-FILE                                        
RONNA                  IN-ENV-CNTL-FILE                                         
RONNA2                 IN-LOC-FILE-CC                                           
RONNA2        END-IF                                                            
RONNA2     ELSE                                                                 
RONNA2        CLOSE IN-BIZ-UNZIP-FILE                                           
RONNA2              IN-ENV-CNTL-FILE                                            
RONNA2     END-IF.                                                              
                                                                                
      *----------------------------------------------------------               
      *  DYNAMIC ALLOCATION ROUTINE                                             
      *----------------------------------------------------------               
      *DP023-0000-DYNALC.                                                       
           COPY DPPD023.                                                        
                                                                                
RONNA ******************************************************************06980000
RONNA *  STANDARD DB2 ERROR ABEND ROUTINE                               06990000
RONNA *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07000000
RONNA ******************************************************************07010000
RONNA  9999-SQL-ABEND.                                                  07020000
RONNA                                                                   07030000
RONNA      MOVE +4013                 TO ABEND-CODE.                    07040000
RONNA      DISPLAY '**  ABEND     **'.                                  07050000
RONNA      DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07060000
RONNA      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07070000
RONNA      DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07080000
RONNA                                                                   07090000
RONNA *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07100000
RONNA *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07110000
RONNA                                                                   07120000
RONNA      COPY DPPD004.                                                07130000
RONNA      CALL 'ILBOABN0'  USING ABEND-CODE.                           07140000
