000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    DLKUP002.                                                 
000300 AUTHOR.        PAUL OMAN.                                                
000400                                                                          
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  DECEMBER 2003.                                            
000700 DATE-COMPILED.                                                           
000800                                                                          
000900 ENVIRONMENT DIVISION.                                                    
001000 CONFIGURATION SECTION.                                                   
001100 SOURCE-COMPUTER. IBM-3090.                                               
001200 OBJECT-COMPUTER. IBM-3090.                                               
001300                                                                          
001400****************************************************************          
001500*                                                              *          
001501*    NOTE:  THERE IS A BATCH VERSION OF THIS PROGRAM CALLED    *          
001502*           DLKUP025.  ANY CHANGES MADE TO THIS PROGRAM        *          
001503*           SHOULD ALSO BE MADE IN THE BATCH VERSION.          *          
001504*                                                              *          
001600*    USED TO ADD AN ASSOCIATE (DLT00003) TO AN ASSIGNMENT      *          
001700*    (DLT00004) OR END AN ASSOCIATE ON AN ASSIGNMENT. IF THE   *          
001800*    ASSOCIATE IS THE LAST ASSOCIATE ON THE ASSIGNMENT, THE    *          
001900*    ASSIGNMENT WILL ALSO BE CLOSED.                           *          
002000*        THE ASSIGNMENT MUST BE ACTIVE TO ALLOW THESE CHANGES  *          
002100*    (ASGMT_STP_RSN_CDE = 'N').                                *          
002200*                                                              *          
002300*    ONLY USED BY CICS PROGRAMS. SOME EXAMPLES ARE:            *          
002400*         DLKCS410                                             *          
002500*         DLKCS450                                             *          
002600*         DLKCS454                                             *          
002700*         DLKCS455                                             *          
002800*                                                              *          
002900*  COPYBOOKS:                                                  *          
003000*     DLWS005 - USED WHEN CALLING THIS PROGRAM                 *          
003100*     DLWS001 - USED WHEN CALLING DLKUP001                     *          
003200*     DLWS011 - USED TO CONSISTENTLY FORMAT KEY FIELDS         *          
003300*     DLWS017 - USED WHEN CALLING DLKUT006                     *          
003400*     XLWS072 - USED WHEN CALLING XLKUP033 WHICH WRITES        *          
003500*               INFORMATION TO THE EAI EXPORT TABLES.          *          
003600*     XLWS067 - USED WHEN CALLING XLKUT067 WHICH IS USED TO    *          
003700*               ACCESS INFORMATION FOR A REQUESTED USER.       *          
003800*                                                              *          
003900*  DB2 ACCESS:                                                 *          
004000*     DLT_DOOR_GP_TMPL (DLT00000) (DOOR GROUP TEMPLATE)        *          
004100*                SELECTS                                       *          
004200*                                                              *          
004300*     DLT_DOOR_GP (DLT00001) (DOOR GROUP)                      *          
004400*                SELECTS                                       *          
004500*                                                              *          
004600*     DLT_TEM_ASGMT (DLT00003) (ASSIGNMENT USER ASSOCIATION)   *          
004700*                SELECTS                                       *          
004800*                UPDATES                                       *          
004900*                INSERTS                                       *          
005000*                                                              *          
005100*     DLT_ASGMT (DLT00004) (ASSIGNMENT)                        *          
005200*                SELECTS                                       *          
005300*                                                              *          
005400****************************************************************          
005500                                                                          
005600 DATA DIVISION.                                                           
005700 WORKING-STORAGE SECTION.                                                 
005800                                                                          
005900 01  PV-CHARACTER-FIELDS.                                                 
006000     05  PV-CURRENT-TIMESTAMP     PIC X(26)  VALUE SPACES.                
006100     05  PV-KEY-START-DATE        PIC X(10)  VALUE SPACES.                
006200     05  PV-KEY-NBR-TXT           PIC X(10)  VALUE SPACES.                
006300     05  PV-TIMESTAMP             PIC X(26)  VALUE SPACES.                
006400     05  PV-CICS-MSG-AREA2        PIC X(100) VALUE SPACES.                
007000     05  PV-CLOSE-TIMESTAMP.                                              
007100         10  PV-CLOSE-DTE                                                 
007200                                  PIC X(10)  VALUE SPACES.                
007300         10  FILLER               PIC X(16)  VALUE                        
007400             '-01.01.01.000001'.                                          
007401                                                                          
007402 01  PV-COMP-FIELDS.                                                      
007403     05  PV-RETRY-COUNTER         PIC S9(04) VALUE 0 COMP.                
007404     05  PV-KEY-NBR-SEQ-NBR       PIC S9(09) VALUE 0 COMP.                
007405     05  PV-ASSOCIATE-COUNT       PIC S9(09) VALUE 0 COMP.                
007406     05  PV-NULL                  PIC S9(04) VALUE -1 COMP.               
007407     05  PV-SD-WRK-AREA-DOOR-GROUP PIC S9(09) VALUE 0 COMP.               
007408     05  PV-DOOR-COUNT            PIC S9(09) VALUE 0 COMP.                
007409     05  PV-DIVERT-COUNT          PIC S9(09) VALUE 0 COMP.                
007410     05  PV-COMMAREA-LENGTH       PIC S9(04) VALUE 0 COMP.                
007411     05  PV-CICS-RESPONSE         PIC S9(09) VALUE 0 COMP.                
007412                                                                          
007500 01  PS-SWITCHES.                                                         
007600     05  PS-ASSIGNMENT-SW             PIC X(01)  VALUE ' '.               
007700         88  PS-ASSIGNMENT-FOUND                 VALUE 'Y'.               
007800         88  PS-ASSIGNMENT-NOT-FOUND             VALUE 'N'.               
007900                                                                          
008000 01  PC-CONSTANTS.                                                        
008100     05  PC-MAX-RETRIES           PIC S9(04) VALUE +3 COMP.               
008200     05  PC-A                     PIC X      VALUE 'A'.                   
008210     05  PC-N                     PIC X      VALUE 'N'.                   
008300     05  PC-DLKUP001              PIC X(08)  VALUE 'DLKUP001'.            
008400     05  PC-DLKUP002              PIC X(08)  VALUE 'DLKUP002'.            
008500     05  PC-DC-LABOR              PIC X(40)  VALUE                        
008600         'DC LABOR                                '.                      
008700     05  PC-ASSIGNMENT-CLOSE      PIC X(40)  VALUE                        
008800         'ASSIGNMENT CLOSE                        '.                      
008900                                                                          
009000****************************************************************          
009100*    DLT00003 - DLT_TEM_ASGMT                                  *          
009200*    DLT00004 - DLT_ASGMT                                      *          
009300*                                                              *          
009400****************************************************************          
009500                                                                          
009600     EXEC SQL                                                             
009700         INCLUDE DLT00000                                                 
009800     END-EXEC.                                                            
009900                                                                          
010000     EXEC SQL                                                             
010100         INCLUDE DLT00001                                                 
010200     END-EXEC.                                                            
010300                                                                          
010400     EXEC SQL                                                             
010500         INCLUDE DLT00003                                                 
010600     END-EXEC.                                                            
010700                                                                          
010800     EXEC SQL                                                             
010900         INCLUDE DLT00004                                                 
011000     END-EXEC.                                                            
011010                                                                          
011011     EXEC SQL                                                             
011012         INCLUDE DLT00009                                                 
011013     END-EXEC.                                                            
011014                                                                          
011020     EXEC SQL                                                             
011030         INCLUDE TSUCRDV                                                  
011040     END-EXEC.                                                            
011100                                                                          
011200     EXEC SQL                                                             
011300          INCLUDE SQLCA                                                   
011400     END-EXEC.                                                            
011500                                                                          
011600****************************************************************          
011700*   XLWS067 - COPYBOOK FOR XLKUT067 - ACCESS USER INFORMATION  *          
011800*   XLWS072 - COPYBOOK FOR XLKUP033 - INSERTS BUSINESS EVENTS  *          
011900*   DLWS011 - USED TO CONSISTENTLY FORMAT KEY FIELDS.          *          
012000*   DLWS017 - COPYBOOK FOR DLKUT006 - FORMAT DOOR SUMMARY      *          
012100*             BUSINESS EVENTS.                                 *          
012200*                                                              *          
012300****************************************************************          
012400                                                                          
012500     COPY XLWS067.                                                        
012600                                                                          
012700     COPY XLWS072.                                                        
012800                                                                          
012900     COPY DLWS001.                                                        
013000                                                                          
013100     COPY DLWS011.                                                        
013200                                                                          
013300     COPY DLWS017.                                                        
013400                                                                          
013500                                                                          
013600 LINKAGE SECTION.                                                         
013700     COPY DLWS005.                                                        
013800                                                                          
013900 PROCEDURE DIVISION USING DL005-ASGMT-COMMAREA.                           
014000                                                                          
014100     SET DL005-SUCCESSFUL  TO TRUE.                                       
014200                                                                          
014300     EVALUATE TRUE                                                        
014400         WHEN DL005-BY-ASGMT-NBR                                          
014500              PERFORM 1000-FIND-SEQUENCE-NBR                              
014600                                                                          
014700         WHEN OTHER                                                       
014800              SET DL005-INVALID-ACCESS TO TRUE                            
014900                                                                          
015000     END-EVALUATE.                                                        
015100                                                                          
015200     GOBACK.                                                              
015300                                                                          
015400                                                                          
015500****************************************************************          
015600*    VALIDATE THAT ALL INPUT FIELDS ARE PRESENT.               *          
015700*        STOP PROCESSING AND RETURN IF THERE ARE ANY ERRORS.   *          
015800*                                                              *          
015900****************************************************************          
016000                                                                          
016100 1000-FIND-SEQUENCE-NBR.                                                  
016200                                                                          
016300     EVALUATE TRUE                                                        
016400         WHEN DL005-KEY-START-DTE = SPACES                                
016500              SET DL005-MISSING-KEY-FIELDS TO TRUE                        
016600                                                                          
016700         WHEN DL005-NO-KEY-NBR-TXT                                        
016800              SET DL005-MISSING-KEY-FIELDS TO TRUE                        
016900                                                                          
017000         WHEN DL005-NO-KEY-NBR-SEQ-NBR                                    
017100              SET DL005-MISSING-KEY-FIELDS TO TRUE                        
017200                                                                          
017300         WHEN DL005-NO-KEY-USER-ID                                        
017400              SET DL005-MISSING-KEY-FIELDS TO TRUE                        
017500                                                                          
017600         WHEN OTHER                                                       
017700              PERFORM 1200-PROCESS-SPECIFIC-EDITS                         
017800                                                                          
017900              IF DL005-SUCCESSFUL                                         
018000                 PERFORM 2000-PROCESS-ASSIGNMENTS                         
018100              END-IF                                                      
018200                                                                          
018300     END-EVALUATE.                                                        
018400                                                                          
018500     IF DL005-SUCCESSFUL                                                  
018600        IF DL005-COMMIT-UPDATE                                            
018700           PERFORM 8000-SYNCPOINT                                         
018800        END-IF                                                            
018900     END-IF.                                                              
019000                                                                          
019100****************************************************************          
019200*    VALIDATES THAT INPUT FIELDS ARE VALID AND IF REQUIRED     *          
019300*        ARE PRESENT.                                          *          
019400*                                                              *          
019500****************************************************************          
019600                                                                          
019700 1200-PROCESS-SPECIFIC-EDITS.                                             
019800                                                                          
019900     EVALUATE TRUE                                                        
020000         WHEN DL005-ADD-TO-ASGMT                                          
020100              IF DL005-NO-KEY-ADD-BY-USR                                  
020200                 SET DL005-MISSING-KEY-FIELDS TO TRUE                     
020300              END-IF                                                      
020400                                                                          
020500         WHEN DL005-END-FROM-ASGMT                                        
020600              IF DL005-NO-KEY-TEM-SEQ-NBR                                 
020700                 SET DL005-MISSING-KEY-FIELDS TO TRUE                     
020800              END-IF                                                      
020900                                                                          
021000              IF DL005-NO-KEY-LVE-BY-PGM                                  
021100                 SET DL005-MISSING-KEY-FIELDS TO TRUE                     
021200              END-IF                                                      
021300                                                                          
021400              IF DL005-NO-KEY-LVE-RSN                                     
021500                 SET DL005-MISSING-KEY-FIELDS TO TRUE                     
021600              END-IF                                                      
021700                                                                          
021800              IF DL005-VALID-LVE-RSN-CDE                                  
021900                 CONTINUE                                                 
022000              ELSE                                                        
022100                 SET DL005-INVALID-LVE-RSN-CDE TO TRUE                    
022200              END-IF                                                      
022300                                                                          
022400         WHEN OTHER                                                       
022500              SET DL005-PROCESS-CODE-INVALID TO TRUE                      
022600                                                                          
022700     END-EVALUATE.                                                        
022800                                                                          
022900     IF DL005-COMMIT-UPDATE OR                                            
023000        DL005-DO-NOT-COMMIT-UPDATE                                        
023100        CONTINUE                                                          
023200     ELSE                                                                 
023300        SET DL005-COMMIT-CODE-INVALID TO TRUE                             
023400     END-IF.                                                              
023500                                                                          
023600     IF DL005-NO-KEY-USR-TYPE                                             
023700        PERFORM 1400-CALL-XLKUT067                                        
023800     ELSE                                                                 
023900        IF DL005-KEY-USR-IS-KOHLS OR                                      
024000           DL005-KEY-USR-IS-TEMP                                          
024100           CONTINUE                                                       
024200        ELSE                                                              
024300           SET DL005-MISSING-KEY-FIELDS TO TRUE                           
024400        END-IF                                                            
024500     END-IF.                                                              
024600                                                                          
024700****************************************************************          
024800*    FORMAT COMMAREA TO CALL XLKUT067 TO ACCESS INFORMATION    *          
024900*    ABOUT A SPECIFIC USERID.                                  *          
025000*                                                              *          
025100****************************************************************          
025200                                                                          
025300 1400-CALL-XLKUT067.                                                      
025400                                                                          
025500     INITIALIZE XL067-USER-COMMAREA.                                      
025600     SET XL067-BY-USER-ID TO TRUE.                                        
025700     SET XL067-SELECT-BOTH-USERS TO TRUE.                                 
025800                                                                          
025900     MOVE DL005-KEY-USER-ID TO XL067-KEY-USER-ID.                         
026000                                                                          
026100     CALL XL067-USER-PGM USING DFHEIBLK                                   
026200                               XL067-COMMAREA                             
026300                               XL067-USER-COMMAREA.                       
026400                                                                          
026500     IF XL067-SUCCESSFUL                                                  
026600        MOVE XL067-USER-FOUND-CODE TO DL005-KEY-USR-TYPE-CDE              
026700     ELSE                                                                 
026800        SET DL005-CALLED-MODULE-FAILURE TO TRUE                           
026900        MOVE XL067-CALL-MOD-FAIL-NM TO                                    
027000                            DL005-CALL-MOD-FAIL-NM                        
027100        MOVE XL067-CALL-MOD-FAIL-ERR-CDE TO                               
027200                            DL005-CALL-MOD-FAIL-ERR-CDE                   
027300     END-IF.                                                              
027400                                                                          
027500****************************************************************          
027600*    TWO PROCESSES CAN HAPPEN HERE - ADD OR END                *          
027700*                                                              *          
027800****************************************************************          
027900                                                                          
028000 2000-PROCESS-ASSIGNMENTS.                                                
028100                                                                          
028200     MOVE DL005-KEY-START-DTE   TO PV-KEY-START-DATE.                     
028300     MOVE DL005-KEY-NBR-TXT     TO PV-KEY-NBR-TXT.                        
028400     MOVE DL005-KEY-NBR-SEQ-NBR TO PV-KEY-NBR-SEQ-NBR.                    
028500                                                                          
028600     PERFORM 8400-GET-TIMESTAMP.                                          
028700                                                                          
028800     PERFORM 3000-SELECT-ASSIGNMENT.                                      
028900                                                                          
029000     EVALUATE TRUE                                                        
029100        WHEN DL005-SQL-ERROR                                              
029200             MOVE SQLCA TO DL005-SQLCA                                    
029300                                                                          
029400        WHEN PS-ASSIGNMENT-NOT-FOUND                                      
029500             SET DL005-ASGMT-NOT-ACTIVE TO TRUE                           
029600                                                                          
029700        WHEN PS-ASSIGNMENT-FOUND                                          
029800             PERFORM 2400-PROCESS-UPDATES                                 
029900                                                                          
030000        WHEN OTHER                                                        
030100             CONTINUE                                                     
030200                                                                          
030300     END-EVALUATE.                                                        
030400                                                                          
030500****************************************************************          
030600*                                                              *          
030700*                                                              *          
030800****************************************************************          
030900                                                                          
031000 2400-PROCESS-UPDATES.                                                    
031100                                                                          
031200     EVALUATE TRUE                                                        
031300         WHEN DL005-ADD-TO-ASGMT                                          
031400              PERFORM 5400-INSERT-ASSOCIATE                               
031410              PERFORM 5500-INSERT-RDPR-USR                                
031500              IF DL005-SUCCESSFUL                                         
031600                 SET DL005-ASSOC-FOUND TO TRUE                            
031700                 MOVE PV-CURRENT-TIMESTAMP TO                             
031800                                 DL005-ASSOC-TMST                         
031900              END-IF                                                      
032000                                                                          
032100         WHEN DL005-END-FROM-ASGMT                                        
032200              PERFORM 4000-COUNT-ASSOCIATES                               
032300                                                                          
032310****************************************************************          
032400**         ONLY PUT OUT AN ASSIGNMENT CLOSE BUSINESS EVENT     *          
032500**         IF THERE HAS BEEN ACTIVITY AGAINST THIS ASSIGNMENT. *          
032600**           (DLT00004-LAST-USD-SEQ-NBR > 0)                   *          
032601**      OR THE ASSIGNMENT HAS DIVERTED CARTONS                 *          
032602**           (PV-DIVERT-COUNT > 0).                            *          
032603**                                                             *          
032610****************************************************************          
032700                                                                          
032800              IF DLT00004-LAST-USD-SEQ-NBR = 0                            
032900                 IF DLT00004-WK-TYP-DESC = 'SHIP'                         
033000                    PERFORM 4400-COUNT-DIVERTS                            
033100                    IF PV-DIVERT-COUNT    > 0  AND                        
033200                       PV-ASSOCIATE-COUNT = 0                             
033300                       PERFORM 6000-FORMAT-XLWS072-CLOSE                  
033400                    END-IF                                                
033500                 END-IF                                                   
033600              ELSE                                                        
033700                 IF PV-ASSOCIATE-COUNT = 0                                
033800                    PERFORM 6000-FORMAT-XLWS072-CLOSE                     
033900                 END-IF                                                   
034000              END-IF                                                      
034100                                                                          
034200              IF PV-ASSOCIATE-COUNT = 0                                   
034300                 PERFORM 5200-UPDATE-ASSOCIATE                            
034400                 IF DL005-SUCCESSFUL                                      
034500                    SET DL005-ASSOC-FOUND TO TRUE                         
034600                    MOVE PV-CURRENT-TIMESTAMP TO                          
034700                                    DL005-ASSOC-TMST                      
034800                    PERFORM 5000-UPDATE-ASSIGNMENT                        
034900                    IF DL005-SUCCESSFUL                                   
035000                       SET DL005-ASSOC-FOUND TO TRUE                      
035100                       MOVE PV-CURRENT-TIMESTAMP TO                       
035200                            DL005-ASSOC-TMST                              
035300                    END-IF                                                
035400                 END-IF                                                   
035500              ELSE                                                        
035600                 PERFORM 5200-UPDATE-ASSOCIATE                            
035700                 IF DL005-SUCCESSFUL                                      
035800                    SET DL005-ASSOC-FOUND TO TRUE                         
035900                    MOVE PV-CURRENT-TIMESTAMP TO                          
036000                                    DL005-ASSOC-TMST                      
036100                 END-IF                                                   
036200              END-IF                                                      
036300                                                                          
036400     END-EVALUATE.                                                        
036500                                                                          
036600                                                                          
036700****************************************************************          
036800*    CHECK THAT THE ASSIGNMENT IS ACTIVE (ASGMT_STP_RSN_CDE =  *          
036900*    "N".                                                      *          
037000*                                                              *          
037100****************************************************************          
037200                                                                          
037300 3000-SELECT-ASSIGNMENT.                                                  
037400                                                                          
037500     PERFORM                                                              
037600         WITH TEST AFTER                                                  
037700         VARYING PV-RETRY-COUNTER                                         
037800         FROM 1 BY 1                                                      
037900         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
038000            OR SQLCODE = ZERO                                             
038100            OR SQLCODE = +100                                             
038200                                                                          
038300         EXEC SQL                                                         
038400             SELECT                                                       
038500                    ASGMT_STRT_DTE                                        
038600                   ,ASGMT_STRT_TM                                         
038700                   ,WK_TYP_DESC                                           
038800                   ,WK_AREA_DESC                                          
038900                   ,DC_NBR                                                
039000                   ,ASGMT_STP_RSN_CDE                                     
039100                   ,LAST_USD_SEQ_NBR                                      
039200              INTO                                                        
039300                    :DLT00004-ASGMT-STRT-DTE                              
039400                   ,:DLT00004-ASGMT-STRT-TM                               
039500                   ,:DLT00004-WK-TYP-DESC                                 
039600                   ,:DLT00004-WK-AREA-DESC                                
039700                   ,:DLT00004-DC-NBR                                      
039800                   ,:DLT00004-ASGMT-STP-RSN-CDE                           
039900                   ,:DLT00004-LAST-USD-SEQ-NBR                            
040000              FROM  DLT_ASGMT                                             
040100             WHERE  ASGMT_STRT_DTE    = :PV-KEY-START-DATE                
040200               AND  ASGMT_NBR_SEQ_NBR = :PV-KEY-NBR-SEQ-NBR               
040300               AND  ASGMT_NBR_TXT     = :PV-KEY-NBR-TXT                   
040400               AND  ASGMT_STP_RSN_CDE = :PC-N                             
040500         END-EXEC                                                         
040600                                                                          
040700         EVALUATE TRUE                                                    
040800             WHEN SQLCODE = ZERO                                          
040900                  SET PS-ASSIGNMENT-FOUND TO TRUE                         
041000                                                                          
041100             WHEN SQLCODE = +100                                          
041200                  SET PS-ASSIGNMENT-NOT-FOUND TO TRUE                     
041300                                                                          
041400             WHEN SQLCODE = -904                                          
041500             WHEN SQLCODE = -913                                          
041600             WHEN SQLCODE = -911                                          
041700                  CONTINUE                                                
041800                                                                          
041900             WHEN SQLWARN0 NOT = SPACES                                   
042000             WHEN SQLCODE NOT = ZERO                                      
042100                  MOVE +4             TO PV-RETRY-COUNTER                 
042200         END-EVALUATE                                                     
042300     END-PERFORM.                                                         
042400                                                                          
042500     IF SQLCODE = ZERO OR                                                 
042600        SQLCODE = +100                                                    
042700        CONTINUE                                                          
042800     ELSE                                                                 
042900        SET DL005-SQL-ERROR TO TRUE                                       
043000        MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                        
043100        MOVE '3000-'        TO DL005-SQL-ERROR-PARA                       
043200        MOVE SQLCA          TO DL005-SQLCA                                
043300     END-IF.                                                              
043400                                                                          
043500****************************************************************          
043600*    COUNTS THE NUMBER OF ACTIVE ASSOCIATES ATTACHED TO THE    *          
043700*    ASSIGNMENT BESIDES THE USER ID THAT WE ARE GOING TO END.  *          
043800*                                                              *          
043900****************************************************************          
044000                                                                          
044100 4000-COUNT-ASSOCIATES.                                                   
044200                                                                          
044300     MOVE 0 TO PV-ASSOCIATE-COUNT.                                        
044400                                                                          
044500     PERFORM                                                              
044600         WITH TEST AFTER                                                  
044700         VARYING PV-RETRY-COUNTER                                         
044800         FROM 1 BY 1                                                      
044900         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
045000            OR SQLCODE = ZERO                                             
045100            OR SQLCODE = +100                                             
045200                                                                          
045300         IF DL005-KEY-USR-IS-KOHLS                                        
045400            EXEC SQL                                                      
045500                                                                          
045600               SELECT   COUNT (*)                                         
045700                                                                          
045800                 INTO  :PV-ASSOCIATE-COUNT                                
045900                                                                          
046000                 FROM  DLT_TEM_ASGMT                                      
046100                                                                          
046200                WHERE (ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
046300                  AND  ASGMT_NBR_TXT     = :PV-KEY-NBR-TXT                
046400                  AND  ASGMT_NBR_SEQ_NBR = :PV-KEY-NBR-SEQ-NBR            
046500                  AND  NOT KOHLS_USR_ID  = :DL005-KEY-USER-ID             
046600                  AND  LVE_RSN_CDE       = :PC-N)                         
046700                   OR (ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
046800                  AND  ASGMT_NBR_TXT     = :PV-KEY-NBR-TXT                
046900                  AND  ASGMT_NBR_SEQ_NBR = :PV-KEY-NBR-SEQ-NBR            
047000                  AND  KOHLS_USR_ID IS NULL                               
047100                  AND  LVE_RSN_CDE       = :PC-N)                         
047200            END-EXEC                                                      
047300         ELSE                                                             
047400            EXEC SQL                                                      
047500                                                                          
047600               SELECT   COUNT (*)                                         
047700                                                                          
047800                 INTO  :PV-ASSOCIATE-COUNT                                
047900                                                                          
048000                 FROM  DLT_TEM_ASGMT                                      
048100                                                                          
048200                WHERE (ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
048300                  AND  ASGMT_NBR_TXT     = :PV-KEY-NBR-TXT                
048400                  AND  ASGMT_NBR_SEQ_NBR = :PV-KEY-NBR-SEQ-NBR            
048500                  AND  NOT TMP_USR_ID    = :DL005-KEY-USER-ID             
048600                  AND  LVE_RSN_CDE       = :PC-N)                         
048700                   OR (ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
048800                  AND  ASGMT_NBR_TXT     = :PV-KEY-NBR-TXT                
048900                  AND  ASGMT_NBR_SEQ_NBR = :PV-KEY-NBR-SEQ-NBR            
049000                  AND  TMP_USR_ID IS NULL                                 
049100                  AND  LVE_RSN_CDE       = :PC-N)                         
049200            END-EXEC                                                      
049300         END-IF                                                           
049400                                                                          
049500         EVALUATE TRUE                                                    
049600             WHEN SQLCODE = ZERO                                          
049700                  CONTINUE                                                
049800                                                                          
049900             WHEN SQLCODE = +100                                          
050000                  CONTINUE                                                
050100                                                                          
050200             WHEN SQLCODE = -904                                          
050300             WHEN SQLCODE = -913                                          
050400             WHEN SQLCODE = -911                                          
050500                                                                          
050600             WHEN SQLWARN0 NOT = SPACES                                   
050700             WHEN SQLCODE NOT = ZERO                                      
050800                  MOVE +4             TO PV-RETRY-COUNTER                 
050900         END-EVALUATE                                                     
051000     END-PERFORM.                                                         
051100                                                                          
051200     IF SQLCODE = ZERO OR                                                 
051300        SQLCODE = +100                                                    
051400        CONTINUE                                                          
051500     ELSE                                                                 
051600        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
051700           SET DL005-SQL-ERROR TO TRUE                                    
051800           MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                     
051900           MOVE '4000-'        TO DL005-SQL-ERROR-PARA                    
052000           MOVE SQLCA          TO DL005-SQLCA                             
052100        END-IF                                                            
052200     END-IF.                                                              
052300                                                                          
052400****************************************************************          
052500*    COUNTS THE NUMBER OF DIVERTED CARTONS.                    *          
052600*                                                              *          
052700*    SHOULD ONLY BE DONE FOR SHIPPING EVENTS.                  *          
052800****************************************************************          
052900                                                                          
053000 4400-COUNT-DIVERTS.                                                      
053100                                                                          
053200     STRING DLT00004-ASGMT-STRT-DTE                                       
053300            '-'                                                           
053400            DLT00004-ASGMT-STRT-TM                                        
053410            '.000000'                                                     
053500            DELIMITED BY SIZE                                             
053600            INTO PV-TIMESTAMP.                                            
053700     MOVE DLT00004-WK-AREA-DESC  TO DL011-SHIP-DOORGRP-WRK-AREA.          
053710     MOVE DL011-SD-WRK-AREA-DOOR-GROUP TO                                 
053720                                 PV-SD-WRK-AREA-DOOR-GROUP.               
053800                                                                          
053900     MOVE 0 TO PV-DIVERT-COUNT.                                           
054000                                                                          
054100     EXEC SQL                                                             
054200                                                                          
054300        SELECT   COUNT (*)                                                
054400                                                                          
054500          INTO  :PV-DIVERT-COUNT                                          
054600                                                                          
054700          FROM  DLT_DOOR_GP  GP                                           
054800               ,TSUCRDV      DRVT                                         
054900                                                                          
055000         WHERE  GP.DOOR_GP_NBR    = :PV-SD-WRK-AREA-DOOR-GROUP            
055100           AND  DRVT.ACTV_CDE     = :PC-A                                 
055200           AND  DRVT.OPER_UNT_ID  = :DLT00004-DC-NBR                      
055300           AND  DRVT.DIVERT_TMST  > :PV-TIMESTAMP                         
055400                                                                          
055500     END-EXEC.                                                            
055600                                                                          
055700     EVALUATE TRUE                                                        
055800         WHEN SQLCODE = ZERO                                              
055900              CONTINUE                                                    
056000                                                                          
056100         WHEN SQLCODE = +100                                              
056200              CONTINUE                                                    
056300                                                                          
056400         WHEN OTHER                                                       
056500              SET DL005-SQL-ERROR TO TRUE                                 
056600              MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                  
056700              MOVE '4000-'        TO DL005-SQL-ERROR-PARA                 
056800              MOVE SQLCA          TO DL005-SQLCA                          
056900              MOVE +4             TO PV-RETRY-COUNTER                     
057000                                                                          
057100     END-EVALUATE.                                                        
057200                                                                          
057300****************************************************************          
057400*    IF THE ASSOCIATE WE ARE GOING TO END IS THE ONLY ACTIVE   *          
057500*    ASSOCIATE FOR THE ASSIGNMENT, THE ASSIGNMENT ALSO NEEDS   *          
057600*    TO BE ENDED.                                              *          
057700*                                                              *          
057800****************************************************************          
057900                                                                          
058000 5000-UPDATE-ASSIGNMENT.                                                  
058100                                                                          
058200     PERFORM                                                              
058300         WITH TEST AFTER                                                  
058400         VARYING PV-RETRY-COUNTER                                         
058500         FROM 1 BY 1                                                      
058600         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
058700            OR SQLCODE = ZERO                                             
058800                                                                          
058900         EXEC SQL                                                         
059000             UPDATE DLT_ASGMT                                             
059100                SET ASGMT_STP_TMST    = :PV-CURRENT-TIMESTAMP             
059200                   ,ASGMT_STP_RSN_CDE = :DL005-KEY-LVE-RSN-CDE            
059300                   ,STP_BY_PGM_NM     = :DL005-KEY-LVE-PGM-NM             
059400                                                                          
059500             WHERE  ASGMT_STRT_DTE    = :PV-KEY-START-DATE                
059600               AND  ASGMT_NBR_TXT     = :DL005-KEY-NBR-TXT                
059700               AND  ASGMT_NBR_SEQ_NBR = :DL005-KEY-NBR-SEQ-NBR            
059800               AND  ASGMT_STP_RSN_CDE = :PC-N                             
059900         END-EXEC                                                         
060000                                                                          
060100         EVALUATE TRUE                                                    
060200             WHEN SQLCODE = ZERO                                          
060300             WHEN SQLCODE = -904                                          
060400             WHEN SQLCODE = -913                                          
060500             WHEN SQLCODE = -911                                          
060600                  CONTINUE                                                
060700             WHEN SQLWARN0 NOT = SPACES                                   
060800             WHEN SQLCODE NOT = ZERO                                      
060900                  MOVE +4             TO PV-RETRY-COUNTER                 
061000         END-EVALUATE                                                     
061100     END-PERFORM.                                                         
061200                                                                          
061300     IF SQLCODE = 0                                                       
061400        CONTINUE                                                          
061500     ELSE                                                                 
061600        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
061700           SET DL005-SQL-ERROR      TO TRUE                               
061800           MOVE '5000-'        TO DL005-SQL-ERROR-PARA                    
061900           MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                     
062000           MOVE SQLCA          TO DL005-SQLCA                             
062100        END-IF                                                            
062200     END-IF.                                                              
062300                                                                          
062400****************************************************************          
062500*    END THE ASSOCIATE ATTACHED TO THE ASSIGNMENT.             *          
062600*                                                              *          
062700****************************************************************          
062800                                                                          
062900 5200-UPDATE-ASSOCIATE.                                                   
063000                                                                          
063100     PERFORM                                                              
063200         WITH TEST AFTER                                                  
063300         VARYING PV-RETRY-COUNTER                                         
063400         FROM 1 BY 1                                                      
063500         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
063600            OR SQLCODE = ZERO                                             
063700                                                                          
063800         IF DL005-KEY-USR-IS-KOHLS                                        
063900            EXEC SQL                                                      
064000                UPDATE DLT_TEM_ASGMT                                      
064100                   SET LVE_TMST      = :PV-CURRENT-TIMESTAMP              
064200                      ,LVE_RSN_CDE   = :DL005-KEY-LVE-RSN-CDE             
064300                      ,LVE_BY_PGM_NM = :DL005-KEY-LVE-PGM-NM              
064400                                                                          
064500                WHERE  ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
064600                  AND  ASGMT_NBR_TXT     = :DL005-KEY-NBR-TXT             
064700                  AND  ASGMT_NBR_SEQ_NBR = :DL005-KEY-NBR-SEQ-NBR         
064800                  AND  TEM_ASGMT_SEQ_NBR =                                
064900                                    :DL005-KEY-TEM-ASGMT-SEQ-NBR          
065000                  AND  KOHLS_USR_ID      = :DL005-KEY-USER-ID             
065100                  AND  LVE_RSN_CDE       = :PC-N                          
065200            END-EXEC                                                      
065300         ELSE                                                             
065400            EXEC SQL                                                      
065500                UPDATE DLT_TEM_ASGMT                                      
065600                   SET LVE_TMST      = :PV-CURRENT-TIMESTAMP              
065700                      ,LVE_RSN_CDE   = :DL005-KEY-LVE-RSN-CDE             
065800                      ,LVE_BY_PGM_NM = :DL005-KEY-LVE-PGM-NM              
065900                                                                          
066000                WHERE  ASGMT_STRT_DTE    = :PV-KEY-START-DATE             
066100                  AND  ASGMT_NBR_TXT     = :DL005-KEY-NBR-TXT             
066200                  AND  ASGMT_NBR_SEQ_NBR = :DL005-KEY-NBR-SEQ-NBR         
066300                  AND  TEM_ASGMT_SEQ_NBR =                                
066400                                    :DL005-KEY-TEM-ASGMT-SEQ-NBR          
066500                  AND  TMP_USR_ID        = :DL005-KEY-USER-ID             
066600                  AND  LVE_RSN_CDE       = :PC-N                          
066700            END-EXEC                                                      
066800         END-IF                                                           
066900                                                                          
067000         EVALUATE TRUE                                                    
067100             WHEN SQLCODE = ZERO                                          
067200             WHEN SQLCODE = -904                                          
067300             WHEN SQLCODE = -913                                          
067400             WHEN SQLCODE = -911                                          
067500                  CONTINUE                                                
067600             WHEN SQLWARN0 NOT = SPACES                                   
067700             WHEN SQLCODE NOT = ZERO                                      
067800                  MOVE +4             TO PV-RETRY-COUNTER                 
067900         END-EVALUATE                                                     
068000     END-PERFORM.                                                         
068100                                                                          
068200     IF SQLCODE = 0                                                       
068300        CONTINUE                                                          
068400     ELSE                                                                 
068500        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
068600           SET DL005-SQL-ERROR      TO TRUE                               
068700           MOVE '5200-'        TO DL005-SQL-ERROR-PARA                    
068800           MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                     
068900           MOVE SQLCA          TO DL005-SQLCA                             
069000        END-IF                                                            
069100     END-IF.                                                              
069200                                                                          
069300****************************************************************          
069400*                                                              *          
069500*                                                              *          
069600*                                                              *          
069700****************************************************************          
069800                                                                          
069900 5400-INSERT-ASSOCIATE.                                                   
070000                                                                          
070100     MOVE DL005-KEY-START-DTE TO PV-KEY-START-DATE.                       
070200                                                                          
070300     PERFORM                                                              
070400         WITH TEST AFTER                                                  
070500         VARYING PV-RETRY-COUNTER                                         
070600         FROM 1 BY 1                                                      
070700         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
070800            OR SQLCODE = ZERO                                             
070900                                                                          
071000         IF DL005-KEY-USR-IS-KOHLS                                        
071100            EXEC SQL                                                      
071200                INSERT INTO DLT_TEM_ASGMT                                 
071300                      (ASGMT_STRT_DTE                                     
071400                      ,ASGMT_NBR_TXT                                      
071500                      ,ASGMT_NBR_SEQ_NBR                                  
071600                      ,KOHLS_USR_ID                                       
071700                      ,TMP_USR_ID                                         
071800                      ,JN_TMST                                            
071900                      ,CRE_BY_USR_ID                                      
072000                      ,LVE_TMST                                           
072100                      ,LVE_RSN_CDE                                        
072200                      ,LVE_BY_PGM_NM)                                     
072300                                                                          
072400               VALUES (:PV-KEY-START-DATE                                 
072500                      ,:DL005-KEY-NBR-TXT                                 
072600                      ,:DL005-KEY-NBR-SEQ-NBR                             
072700                      ,:DL005-KEY-USER-ID                                 
072800                      ,DEFAULT                                            
072900                      ,:PV-CURRENT-TIMESTAMP                              
073000                      ,:DL005-KEY-ADD-BY-USER-ID                          
073100                      ,:DLT00003-LVE-TMST   :PV-NULL                      
073200                      ,:PC-N                                              
073300                      ,DEFAULT)                                           
073400            END-EXEC                                                      
073500         ELSE                                                             
073600            EXEC SQL                                                      
073700                INSERT INTO DLT_TEM_ASGMT                                 
073800                      (ASGMT_STRT_DTE                                     
073900                      ,ASGMT_NBR_TXT                                      
074000                      ,ASGMT_NBR_SEQ_NBR                                  
074100                      ,KOHLS_USR_ID                                       
074200                      ,TMP_USR_ID                                         
074300                      ,JN_TMST                                            
074400                      ,CRE_BY_USR_ID                                      
074500                      ,LVE_TMST                                           
074600                      ,LVE_RSN_CDE                                        
074700                      ,LVE_BY_PGM_NM)                                     
074800                                                                          
074900               VALUES (:PV-KEY-START-DATE                                 
075000                      ,:DL005-KEY-NBR-TXT                                 
075100                      ,:DL005-KEY-NBR-SEQ-NBR                             
075200                      ,DEFAULT                                            
075300                      ,:DL005-KEY-USER-ID                                 
075400                      ,:PV-CURRENT-TIMESTAMP                              
075500                      ,:DL005-KEY-ADD-BY-USER-ID                          
075600                      ,:DLT00003-LVE-TMST   :PV-NULL                      
075700                      ,:PC-N                                              
075800                      ,DEFAULT)                                           
075900            END-EXEC                                                      
076000         END-IF                                                           
076100                                                                          
076200         EVALUATE TRUE                                                    
076300             WHEN SQLCODE = ZERO                                          
076400             WHEN SQLCODE = -904                                          
076500             WHEN SQLCODE = -913                                          
076600             WHEN SQLCODE = -911                                          
076700                 CONTINUE                                                 
076800             WHEN SQLWARN0 NOT = SPACES                                   
076900             WHEN SQLCODE NOT = ZERO                                      
077000                  MOVE +4             TO PV-RETRY-COUNTER                 
077100         END-EVALUATE                                                     
077200     END-PERFORM.                                                         
077300                                                                          
077400     IF SQLCODE = 0                                                       
077500        CONTINUE                                                          
077600     ELSE                                                                 
077700        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
077800           SET DL005-SQL-ERROR      TO TRUE                               
077900           MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                     
078000           MOVE '5400-'        TO DL005-SQL-ERROR-PARA                    
078100           MOVE SQLCA          TO DL005-SQLCA                             
078200        END-IF                                                            
078300     END-IF.                                                              
078301                                                                          
078302 5500-INSERT-RDPR-USR.                                                    
078303                                                                          
078304     PERFORM                                                              
078305         WITH TEST AFTER                                                  
078306         VARYING PV-RETRY-COUNTER                                         
078307         FROM 1 BY 1                                                      
078308         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
078309            OR SQLCODE = ZERO OR SQLCODE = -803                           
078310                                                                          
078311         IF DL005-KEY-USR-IS-KOHLS                                        
078312            EXEC SQL                                                      
078313               INSERT INTO DLT_RDPR_USR_LSTG                              
078314                  (USR_ID                                                 
078315                  ,LOC_NBR                                                
078316                  ,TMP_USR_ID                                             
078317                  ,CRE_TMST)                                              
078318                                                                          
078319               VALUES (:DL005-KEY-USER-ID                                 
078320                      ,:DLT00004-DC-NBR                                   
078321                      ,NULL                                               
078322                      ,:PV-CURRENT-TIMESTAMP)                             
078323            END-EXEC                                                      
078324         ELSE                                                             
078325            EXEC SQL                                                      
078326               INSERT INTO DLT_RDPR_USR_LSTG                              
078327                  (USR_ID                                                 
078328                  ,LOC_NBR                                                
078329                  ,TMP_USR_ID                                             
078330                  ,CRE_TMST)                                              
078331                                                                          
078332               VALUES (NULL                                               
078333                      ,:DLT00004-DC-NBR                                   
078334                      ,:DL005-KEY-USER-ID                                 
078335                      ,:PV-CURRENT-TIMESTAMP)                             
078336            END-EXEC                                                      
078337         END-IF                                                           
078338                                                                          
078339         EVALUATE TRUE                                                    
078340             WHEN SQLCODE = ZERO                                          
078341             WHEN SQLCODE = -803                                          
078342             WHEN SQLCODE = -904                                          
078343             WHEN SQLCODE = -913                                          
078344             WHEN SQLCODE = -911                                          
078345                 CONTINUE                                                 
078346             WHEN SQLWARN0 NOT = SPACES                                   
078347             WHEN SQLCODE NOT = ZERO                                      
078348                  MOVE +4             TO PV-RETRY-COUNTER                 
078349         END-EVALUATE                                                     
078350     END-PERFORM.                                                         
078351                                                                          
078352     IF SQLCODE = 0 OR -803                                               
078353        CONTINUE                                                          
078354     ELSE                                                                 
078355        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
078356           SET DL005-SQL-ERROR      TO TRUE                               
078357           MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                     
078358           MOVE '5500-'        TO DL005-SQL-ERROR-PARA                    
078359           MOVE SQLCA          TO DL005-SQLCA                             
078360        END-IF                                                            
078370     END-IF.                                                              
078400                                                                          
078500****************************************************************          
078600*    IF CLOSING AN ASSIGNMENT AND THE DL005-LOG-TO-EAI-EXPORT  *          
078700*    SWITCH IS ON, WE CALL XLKUP033 TO WRITE THE APPROPRIATE   *          
078800*    INFORMATION TO EITHER THE PCT00011 OR SUT00037 TABLES.    *          
078900*                                                              *          
079000*    THE DOOR SUMMARY BUSINESS EVENTS NEED TO BE DONE BEFORE   *          
079100*    THE ASSIGNMENT CLOSE BUSINESS EVENT.                      *          
079200****************************************************************          
079300                                                                          
079400 6000-FORMAT-XLWS072-CLOSE.                                               
079500                                                                          
079600     EVALUATE TRUE                                                        
079700         WHEN DLT00004-WK-TYP-DESC = 'SHIP'                               
079800              PERFORM 6800-FORMAT-DLKUT006-INPUT                          
079900                                                                          
080000         WHEN OTHER                                                       
080100              CONTINUE                                                    
080200                                                                          
080300     END-EVALUATE.                                                        
080400                                                                          
080500     INITIALIZE XL072-EAI-EXPORT-COMMAREA.                                
080600                                                                          
080700     EVALUATE TRUE                                                        
080800         WHEN DLT00004-WK-TYP-DESC = 'P2L'                                
080900         WHEN DLT00004-WK-TYP-DESC = 'RLABL'                              
080910         WHEN DLT00004-WK-TYP-DESC = 'MASTCON'                            
081000              SET XL072-PC-APP-ID TO TRUE                                 
081100                                                                          
081200         WHEN OTHER                                                       
081300              SET XL072-SU-APP-ID TO TRUE                                 
081400                                                                          
081500     END-EVALUATE.                                                        
081600                                                                          
081700     MOVE PC-DC-LABOR         TO XL072-EVNT-OBJ-TXT.                      
081800     MOVE PC-ASSIGNMENT-CLOSE TO XL072-EVNT-OBJ-ACTN-TXT.                 
081900     MOVE DL005-ORIG-CALLING-PGM-NM TO XL072-CRE-BY-USR-ID.               
082000     MOVE PC-DLKUP002               TO XL072-CRE-PGM-NM.                  
082100                                                                          
082200     SET XL072-DO-NOT-COMMIT-UPDATE TO TRUE.                              
082300     SET XL072-COPYBOOK-NOT-USED    TO TRUE.                              
082400                                                                          
082500     MOVE DL005-KEY-NBR-SEQ-NBR TO XL072-EVNT-KY-1-NBR.                   
082600     MOVE DL005-KEY-NBR-TXT     TO XL072-EVNT-KY-1-TXT.                   
082601     MOVE DL005-KEY-START-DTE   TO PV-CLOSE-DTE.                          
082602     MOVE PV-CLOSE-TIMESTAMP    TO XL072-EVNT-KY-1-TMST.                  
082700***  MOVE PV-CURRENT-TIMESTAMP  TO XL072-EVNT-KY-1-TMST.                  
082800     MOVE DLT00004-DC-NBR       TO XL072-EVNT-KY-2-NBR.                   
082900                                                                          
083000     PERFORM 6400-CALL-XLKUP033.                                          
083100                                                                          
083200                                                                          
083300****************************************************************          
083400*    IF CLOSING AN ASSIGNMENT AND THE DL005-LOG-TO-EAI-EXPORT  *          
083500*    SWITCH IS ON, WE CALL XLKUP033 TO WRITE THE APPROPRIATE   *          
083600*    INFORMATION TO EITHER THE PCT00011 OR SUT00037 TABLES.    *          
083700*                                                              *          
083800****************************************************************          
083900                                                                          
084000 6400-CALL-XLKUP033.                                                      
084100                                                                          
084200     CALL XL072-USER-PGM USING DFHEIBLK                                   
084300                               XL072-COMMAREA                             
084400                               XL072-EAI-EXPORT-COMMAREA.                 
084500                                                                          
084600     IF XL072-SUCCESSFUL                                                  
084700        CONTINUE                                                          
084800     ELSE                                                                 
084900        SET DL005-CALLED-MODULE-FAILURE TO TRUE                           
085000        MOVE XL072-USER-PGM        TO DL005-CALL-MOD-FAIL-NM              
085100        MOVE XL072-RETURN-CODE     TO DL005-CALL-MOD-FAIL-ERR-CDE         
085200        MOVE XL072-RETURN-SQLCODES TO DL005-RETURN-SQLCODES               
085300     END-IF.                                                              
085400                                                                          
085500******************************************************************        
085600*    THIS WILL ONLY BE USED WHEN CLOSING A SHIPPING ASSIGNMENT.  *        
085700*        IT CALLS DLKUT006 TO GENERATE A "SHIPPING DOOR SUMMARY" *        
085800*        BUSINESS EVENT FOR EVERY DOOR WITHIN THE DOOR GROUP     *        
085900*        TEMPLATE ID.                                            *        
086000*    THIS IS ONLY DONE IF THIS WAS THE LAST ASSOCIATE ON THE     *        
086100*        ASSIGNMENT.                                             *        
086200******************************************************************        
086300                                                                          
086400 6800-FORMAT-DLKUT006-INPUT.                                              
086500                                                                          
086600     INITIALIZE DL017-ASGMT-COMMAREA.                                     
086700     SET  DL017-BY-DOOR-GROUP-ID TO TRUE.                                 
086800                                                                          
086900     MOVE DL005-KEY-START-DTE    TO DL017-ASGMT-START-DTE.                
087000     MOVE DL005-KEY-NBR-TXT      TO DL017-ASGMT-NBR-TXT.                  
087100     MOVE DL005-KEY-NBR-SEQ-NBR  TO DL017-ASGMT-NBR-SEQ-NBR.              
087200                                                                          
087300     MOVE DLT00004-WK-AREA-DESC  TO DL011-SHIP-DOORGRP-WRK-AREA.          
087400     MOVE DL011-SD-WRK-AREA-DOOR-GROUP TO DL017-DOOR-GP-NBR.              
087500     MOVE DLT00004-DC-NBR        TO DL017-DC-NBR.                         
087600                                                                          
087700**   TRIP IS NOW DETERMINED IN DLKUT006                                   
087800                                                                          
087900     STRING DLT00004-ASGMT-STRT-DTE                                       
088000            '-'                                                           
088100            DLT00004-ASGMT-STRT-TM                                        
088200            '.000000'                                                     
088300            DELIMITED BY SIZE                                             
088400            INTO DL017-ASGMT-STRT-TMST.                                   
088500                                                                          
088600     MOVE PV-CURRENT-TIMESTAMP TO DL017-ASGMT-STOP-TMST.                  
088700                                                                          
088800     PERFORM 8000-COUNT-DOORS.                                            
088801                                                                          
088810     IF DL005-SUCCESSFUL                                                  
088900        PERFORM 8400-CALL-DLKUP001                                        
088910     END-IF.                                                              
089000                                                                          
089100     MOVE DL001-SEQ-START-NBR  TO DL017-BEGIN-DETAIL-SEQ-NBR.             
089200     MOVE PV-DOOR-COUNT        TO DL017-NBR-DOORS.                        
089300     MOVE DL005-KEY-ADD-BY-USER-ID TO DL017-USR-ID.                       
089400                                                                          
089500     IF DL005-SUCCESSFUL                                                  
089600        PERFORM 6900-CALL-DLKUT006                                        
089700     END-IF.                                                              
089800                                                                          
089900******************************************************************        
090000*    THIS WILL ONLY BE USED WHEN CLOSING A SHIPPING ASSIGNMENT.  *        
090100*        IT CALLS DLKUT006 TO GENERATE A "SHIPPING DOOR SUMMARY" *        
090200*        BUSINESS EVENT FOR EVERY DOOR WITHIN THE DOOR GROUP     *        
090300*        TEMPLATE ID.                                            *        
090400******************************************************************        
090500                                                                          
090600 6900-CALL-DLKUT006.                                                      
090700                                                                          
090800     CALL DL017-USER-PGM USING DFHEIBLK                                   
090900                               DL017-COMMAREA                             
091000                               DL017-ASGMT-COMMAREA.                      
091100                                                                          
091200     IF DL017-SUCCESSFUL                                                  
091300        CONTINUE                                                          
091400     ELSE                                                                 
091500        SET  DL005-CALLED-MODULE-FAILURE TO TRUE                          
091600        MOVE DL017-USER-PGM        TO DL005-CALL-MOD-FAIL-NM              
091700        MOVE DL017-RETURN-CODE     TO DL005-CALL-MOD-FAIL-ERR-CDE         
091800        MOVE DL017-RETURN-SQLCODES TO DL005-RETURN-SQLCODES               
091900     END-IF.                                                              
092000                                                                          
092100****************************************************************          
092200*                                                              *          
092300*                                                              *          
092400****************************************************************          
092500                                                                          
092600 8000-COUNT-DOORS.                                                        
092700                                                                          
092800     MOVE 0 TO PV-DOOR-COUNT.                                             
092900                                                                          
093000     EXEC SQL                                                             
093100           SELECT COUNT(*)                                                
093200                                                                          
093300             INTO :PV-DOOR-COUNT                                          
093400                                                                          
093500             FROM DLT_DOOR_GP       GP                                    
093600                 ,DLT_DOOR_GP_TMPL  TMPL                                  
093700            WHERE GP.DOOR_GP_NBR     = :DL017-DOOR-GP-NBR                 
093800              AND TMPL.DC_NBR        = :DL017-DC-NBR                      
093900              AND TMPL.ACTV_IND      = 'Y'                                
094000              AND GP.DOOR_GP_TMPL_ID = TMPL.DOOR_GP_TMPL_ID               
094100                                                                          
094200     END-EXEC.                                                            
094300                                                                          
094400     EVALUATE TRUE                                                        
094500         WHEN SQLCODE = ZERO                                              
094600              CONTINUE                                                    
094700                                                                          
094800         WHEN SQLCODE = +100                                              
094900              CONTINUE                                                    
095000                                                                          
095100         WHEN SQLCODE = -904                                              
095200         WHEN SQLCODE = -913                                              
095300         WHEN SQLCODE = -911                                              
095400                                                                          
095500         WHEN SQLWARN0 NOT = SPACES                                       
095600         WHEN SQLCODE NOT = ZERO                                          
095700              SET DL005-SQL-ERROR TO TRUE                                 
095800              MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                  
095900              MOVE '8000-'        TO DL005-SQL-ERROR-PARA                 
096000              MOVE SQLCA          TO DL005-SQLCA                          
096100     END-EVALUATE.                                                        
096200                                                                          
096300******************************************************************        
096400*    RESERVE THE SAME NUMBER OF SEQUENCE NUMBERS THAT THERE ARE  *        
096500*       NUMBER OF DOORS (PV-DOOR-COUNT).                         *        
096600*                                                                *        
096700******************************************************************        
096800                                                                          
096900 8400-CALL-DLKUP001.                                                      
097000                                                                          
097100     INITIALIZE DL001-ASGMT-COMMAREA.                                     
097200                                                                          
097300     SET DL001-BY-ASGMT-KEY         TO TRUE.                              
097400     SET DL001-COMMIT-UPDATE        TO TRUE.                              
097500     SET DL001-FIND-NEXT-ASGMT      TO TRUE.                              
097600                                                                          
097700     MOVE DL017-ASGMT-START-DTE TO DL001-KEY-START-DTE.                   
097800     MOVE DL017-ASGMT-NBR-TXT   TO DL001-KEY-NBR-TXT.                     
097900     MOVE DL017-ASGMT-NBR-SEQ-NBR TO DL001-KEY-NBR-SEQ-NBR.               
098000                                                                          
098100     MOVE PV-DOOR-COUNT TO DL001-NBR-OF-NBRS-NEED-QTY.                    
098200     MOVE PC-DLKUP001   TO DL001-ORIG-CALLING-PGM-NM.                     
098300                                                                          
098400     CALL DL001-USER-PGM USING DFHEIBLK                                   
098500                               DL001-COMMAREA                             
098600                               DL001-ASGMT-COMMAREA.                      
098700                                                                          
098800     EVALUATE TRUE                                                        
098900        WHEN DL001-SUCCESSFUL                                             
099000*            ****************************************************         
099100*            *  ASSIGNMENT SENT IN HAS BEEN CLOSED, DLKUP001    *         
099200*            *  HAS SENT BACK CURRENT OPEN ASSIGNMENT.          *         
099300*            *                                                  *         
099400*            ****************************************************         
099500             IF DL001-ASGMT-NEXT-USED                                     
099600                MOVE DL001-NXT-START-DTE TO DL017-ASGMT-START-DTE         
099700                MOVE DL001-NXT-NBR-TXT   TO DL017-ASGMT-NBR-TXT           
099800                MOVE DL001-NXT-NBR-SEQ-NBR TO                             
099900                                   DL017-ASGMT-NBR-SEQ-NBR                
100000*            ELSE                                                         
100100*               MOVE DL001-SEQ-START-NBR TO                               
100200*                                  DL017-ASGMT-NBR-SEQ-NBR                
100300             END-IF                                                       
100400                                                                          
100500        WHEN DL001-CALLED-MODULE-FAILURE                                  
100600             SET  DL005-CALLED-MODULE-FAILURE TO TRUE                     
100700             MOVE DL001-CALL-MOD-FAIL-NM TO                               
100800                                 DL005-CALL-MOD-FAIL-NM                   
100900             MOVE DL001-CALL-MOD-FAIL-ERR-CDE TO                          
101000                                 DL005-CALL-MOD-FAIL-ERR-CDE              
101100                                                                          
101200        WHEN OTHER                                                        
101300             SET  DL005-CALLED-MODULE-FAILURE TO TRUE                     
101400             MOVE DL001-RETURN-CODE TO                                    
101500                               DL005-CALL-MOD-FAIL-ERR-CDE                
101600             MOVE DL001-USER-PGM    TO                                    
101700                               DL005-CALL-MOD-FAIL-NM                     
101800             MOVE DL001-RETURN-SQLCODES TO                                
101900                               DL005-RETURN-SQLCODES                      
102000             MOVE DL001-SQL-ERROR-PARA TO                                 
102100                               DL005-SQL-ERROR-PARA                       
102200             MOVE DL001-SQLCA TO DL005-SQLCA                              
102300                                                                          
102400     END-EVALUATE.                                                        
102600                                                                          
102700                                                                          
102800******************************************************************        
102900*                                                                *        
103000******************************************************************        
103100                                                                          
103200 8000-SYNCPOINT.                                                          
103300                                                                          
103400     EXEC CICS                                                            
103500          SYNCPOINT                                                       
103600     END-EXEC.                                                            
103700                                                                          
103800                                                                          
103900****************************************************************          
104000*    RETRIEVE THE CURRENT TIMESTAMP TO USE WHEN UPDATING ROWS. *          
104100*                                                              *          
104200****************************************************************          
104300                                                                          
104400 8400-GET-TIMESTAMP.                                                      
104500                                                                          
104600     EXEC SQL                                                             
104700                                                                          
104800         SET :PV-CURRENT-TIMESTAMP  = CURRENT TIMESTAMP                   
104900                                                                          
105000     END-EXEC.                                                            
105100                                                                          
105200     EVALUATE TRUE                                                        
105300         WHEN SQLCODE = ZERO                                              
105400              CONTINUE                                                    
105500                                                                          
105600         WHEN OTHER                                                       
105700              SET DL005-SQL-ERROR      TO TRUE                            
105800              MOVE '8400-'        TO DL005-SQL-ERROR-PARA                 
105900              MOVE SQLCODE        TO DL005-ASGMT-SQLCODE                  
106000                                                                          
106100      END-EVALUATE.                                                       
106200                                                                          
106300                                                                          
