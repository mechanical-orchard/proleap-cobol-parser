       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUP600.                                                 
       AUTHOR.        ARCHANA SELVARAJ.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03-12-2010.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUP600 - UPDATES MLT_BRND_VND_DTE_CNTL TABLE                 *        
      *                                                                *        
      * MLKUP600 - THIS PROGRAM UPDATES THE MLT_BRND_VND_DTE_CNTL      *        
      * TABLE BY FETCHING VALUES FROM THE TABLES TMLCNTL AND TDTATTR.  *        
      ******************************************************************        
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * W37965   03-12-2010  ARCHANA S  INITIAL INSTALL                *        
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT FISCAL-WEEK-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ***************************************************************           
       FILE SECTION.                                                            
                                                                                
       FD  FISCAL-WEEK-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  FISCAL-WEEK-RECORD          PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01 PV-PROGRAM-VARIABLES.                                                 
          05 PROGRAM-NAME              PIC X(08) VALUE 'MLKUP600'.              
          05 PV-CLO-DTE                PIC X(10).                               
          05 PV-ML-CLO-DTE             PIC X(10).                               
          05 PV-KEY-DATE               PIC X(10).                               
          05 PV-FSCL-MN-END-DTE        PIC X(10).                               
          05 PV-FSCL-WK-END-DTE        PIC X(10).                               
          05 PV-NFM-END-DTE            PIC X(10).                               
          05 PV-CURRENT-TMST           PIC X(26)  VALUE SPACES.                 
                                                                                
       01  FILLER                      PIC X(40)                                
            VALUE '***** WORKING STORAGE STARTS HERE ******'.                   
                                                                                
      *** START OF INPUT RECORD                                                 
       01  CC-CONTROL-RECORD.                                                   
           05  CC-OPEN-FSCL-WK-END-DTE PIC X(10).                               
           05  FILLER                  PIC X(70).                               
      *                                                                         
       EJECT                                                                    
      *-----------------------------------------------------------------        
      *  ABEND AREA                                                             
      *-----------------------------------------------------------------        
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: MLKUP600'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT FOR DATE TABLE                                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT FOR OPEN FISCAL PERIOD TABLE                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT FOR MLT_BRND_VND_DTE_CNTL TABLE                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLT00021                                                 
           END-EXEC.                                                            
                                                                                
       01  FILLER                      PIC X(22)                                
           VALUE 'END OF WORKING STORAGE'.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-MLKUP600.                                                  
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-CONTROL.                                        
                                                                                
           PERFORM 8000-EOJ-PROCESS.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN INPUT FISCAL-WEEK-FILE.                                         
                                                                                
           EXEC SQL                                                             
              SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                          
           END-EXEC.                                                            
                                                                                
           DISPLAY 'CURRENT-TMST:' PV-CURRENT-TMST.                             
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-PROCESS-CONTROL.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-READ-FISCAL-WEEK-FILE.                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** INPUT CONTROL CARD PROCESSED ***'.                      
           DISPLAY 'THE CURRENT DATE IS:' CC-OPEN-FSCL-WK-END-DTE.              
                                                                                
           PERFORM 7000-SELECT-TMLCNTL.                                         
      *    DISPLAY '7000-SELECT-TMLCNTL SUCCESSFULLY EXECUTED'.                 
           PERFORM 8100-SELECT-TDTATTR.                                         
      *    DISPLAY '8100-SELECT-TDTATTR SUCCESSFULLY EXECUTED'.                 
                                                                                
      *    IF CC-OPEN-FSCL-WK-END-DTE NOT = DTATTR-FSCL-WK-END-DTE              
      *     MOVE '2000-PROCESS-CONTROL'                                         
      *                           TO AA-PARAGRAPH-NAME                          
      *     MOVE 'INPUT DATE NOT EQUAL TO DTATTR FISCAL WEEKEND DATE'           
      *                          TO AA-MESSAGE                                  
      *     MOVE +4002          TO ABEND-CODE                                   
      *     PERFORM 9900-COMMON-ABEND-ROUTINE                                   
      *    ELSE                                                                 
      *     DISPLAY 'INPUT DATE IS EQUAL TO TDTATTR FISCAL WEEKEND DATE'        
      *    END-IF.                                                              
                                                                                
           IF CC-OPEN-FSCL-WK-END-DTE NOT = DTATTR-FSCL-MN-END-DTE              
              DISPLAY 'INPUT DATE NOT EQUAL TO TDTATTR FISCAL MONTH'            
                      'END DATE'                                                
           END-IF.                                                              
                                                                                
           IF DTATTR-KY-DTE NOT = DTATTR-FSCL-MN-END-DTE                        
              MOVE MLCNTL-OPN-FSCL-MN-DTE TO MLT00021-OPN-FSCL-MN-DTE           
              MOVE MLCNTL-OPN-FSCL-WK-DTE TO MLT00021-OPN-FSCL-WK-DTE           
              MOVE PV-CURRENT-TMST TO MLT00021-LAST-UPD-TMST                    
              MOVE PROGRAM-NAME TO MLT00021-LAST-UPD-USR-ID                     
              PERFORM 8200-UPDATE-DT-CTRL-TABLE                                 
      *       DISPLAY '8200-UPDATE-DT-CTRL-TBL SUCCESSFULLY EXECUTED'           
           END-IF.                                                              
                                                                                
           IF DTATTR-KY-DTE EQUAL TO DTATTR-FSCL-MN-END-DTE                     
             PERFORM 8300-READ-TDTATTR-TABLE                                    
      *      DISPLAY '8300-READ-TDTATTR-TABLE PERFORMED SUCCESSFULLY'           
             PERFORM 8400-READ-TDTATTR-TABLE                                    
      *      DISPLAY '8400-READ-TDTATTR-TABLE PERFORMED SUCCESSFULLY'           
             MOVE DTATTR-FSCL-MN-END-DTE TO                                     
                  MLT00021-PSTG-FSCL-MN-END-DTE                                 
             MOVE DTATTR-FSCL-MN-END-DTE TO                                     
                  MLT00021-PSTG-FSCL-WK-END-DTE                                 
             MOVE MLCNTL-OPN-FSCL-MN-DTE TO                                     
                  MLT00021-OPN-FSCL-MN-DTE                                      
             MOVE MLCNTL-OPN-FSCL-WK-DTE  TO                                    
                  MLT00021-OPN-FSCL-WK-DTE                                      
             MOVE PV-CLO-DTE TO MLT00021-CLO-DTE                                
             MOVE PV-ML-CLO-DTE TO MLT00021-ML-CLO-DTE                          
             MOVE PV-KEY-DATE   TO MLT00021-LAST-PSTG-UPD-DTE                   
             MOVE PV-CURRENT-TMST TO MLT00021-LAST-UPD-TMST                     
             MOVE PROGRAM-NAME TO MLT00021-LAST-UPD-USR-ID                      
             PERFORM 8500-UPDATE-DT-CTRL-TABLE                                  
      *      DISPLAY '8500-UPDATE-DT-CTRL-TABLE SUCCESSFUL'                     
             END-IF.                                                            
       EJECT                                                                    
      ******************************************************************        
       4000-READ-FISCAL-WEEK-FILE.                                              
      ******************************************************************        
                                                                                
           READ FISCAL-WEEK-FILE                                                
               INTO CC-CONTROL-RECORD                                           
               AT END                                                           
                   MOVE '4000-READ-FISCAL-WEEK-FILE'                            
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'CONTROL CARD IS MISSING'                               
                                       TO AA-MESSAGE                            
                   MOVE +4001          TO ABEND-CODE                            
                   PERFORM 9900-COMMON-ABEND-ROUTINE                            
           END-READ.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7000-SELECT-TMLCNTL.                                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT OPN_FSCL_MN_DTE,                                          
                      OPN_FSCL_WK_DTE                                           
               INTO  :MLCNTL-OPN-FSCL-MN-DTE,                                   
                     :MLCNTL-OPN-FSCL-WK-DTE                                    
               FROM TMLCNTL                                                     
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQLCODE IN 7000-SELECT-TMLCNTL:' SQLCODE                    
           DISPLAY 'FSCL-MN-DT IS:' MLCNTL-OPN-FSCL-MN-DTE                      
           DISPLAY 'FSCL-WK-DT IS:' MLCNTL-OPN-FSCL-WK-DTE                      
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 100                                               
                   MOVE '7000-SELECT-TMLCNTL'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE < 0                                                 
                   MOVE '7000-SELECT-TMLCNTL'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '7000-SELECT-TMLCNTL'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL TABLE WARNING'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8100-SELECT-TDTATTR.                                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT KY_DTE,                                                   
                      FSCL_MN_END_DTE,                                          
                      FSCL_WK_END_DTE,                                          
                      NFM_END_DTE                                               
               INTO  :DTATTR-KY-DTE,                                            
                     :DTATTR-FSCL-MN-END-DTE,                                   
                     :DTATTR-FSCL-WK-END-DTE,                                   
                     :DTATTR-NFM-END-DTE                                        
               FROM TDTATTR                                                     
               WHERE KY_DTE = :CC-OPEN-FSCL-WK-END-DTE                          
               AND KY_DTE = FSCL_WK_END_DTE                                     
           END-EXEC.                                                            
                                                                                
           MOVE DTATTR-KY-DTE TO PV-KEY-DATE                                    
           MOVE DTATTR-FSCL-MN-END-DTE TO PV-FSCL-MN-END-DTE                    
           MOVE DTATTR-FSCL-WK-END-DTE TO PV-FSCL-WK-END-DTE                    
           MOVE DTATTR-NFM-END-DTE TO PV-NFM-END-DTE                            
           DISPLAY 'KEY-DATE IS:' PV-KEY-DATE                                   
           DISPLAY 'FSCL-MN-END-DT IS:' PV-FSCL-MN-END-DTE                      
           DISPLAY 'FSCL-WK-END-DT IS:' PV-FSCL-WK-END-DTE                      
           DISPLAY 'NFM-END-DTE IS:' PV-NFM-END-DTE                             
           DISPLAY 'SQLCODE IN 8100-SELECT-TDTATTR IS:' SQLCODE                 
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 100                                               
                   MOVE '8100-SELECT-TDTATTR'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'INPUT CONTROL DATE IS NOT FISCAL WEEK END DATE'        
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE < 0                                                 
                   MOVE '8100-SELECT-TDTATTR'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '8100-SELECT-TDTATTR'                                   
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE WARNING'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8200-UPDATE-DT-CTRL-TABLE.                                               
      ******************************************************************        
           EXEC SQL                                                             
               UPDATE MLT_BRND_VND_DTE_CNTL                                     
                  SET OPN_FSCL_MN_DTE = :MLT00021-OPN-FSCL-MN-DTE,              
                     OPN_FSCL_WK_DTE  = :MLT00021-OPN-FSCL-WK-DTE,              
                      LAST_UPD_TMST   = :MLT00021-LAST-UPD-TMST,                
                      LAST_UPD_USR_ID = :MLT00021-LAST-UPD-USR-ID               
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQLCODE IN  8200-UPDATE-DT-CTRL-TABLE:' SQLCODE             
           DISPLAY 'MLT00021-OPN-FSCL-MN-DT:' MLT00021-OPN-FSCL-MN-DTE          
           DISPLAY 'MLT00021-OPN-FSCL-WK-DTE:' MLT00021-OPN-FSCL-WK-DTE         
           DISPLAY 'MLT00021-LAST-UPD-TMST:' MLT00021-LAST-UPD-TMST             
           DISPLAY 'MLT00021-LAST-UPD-USR-ID:' MLT00021-LAST-UPD-USR-ID         
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE < 0                                                 
                   MOVE '8200-UPDATE-DT-CTRL-TABLE' TO                          
                                          AA-PARAGRAPH-NAME                     
                   MOVE 'MLT-BRND-VND-DTE-CNTL TABLE ERROR' TO                  
                                  AA-MESSAGE                                    
                   MOVE 'UPDATE' TO AA-DB2-OPERATION                            
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '8200-UPDATE-DT-CTRL-TABLE'                             
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'MLT-BRND-VND-DTE-CNTL TABLE WARNING'                   
                                       TO AA-MESSAGE                            
                   MOVE 'UPDATE'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8300-READ-TDTATTR-TABLE.                                                 
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT KY_DTE                                                    
               INTO  :DTATTR-KY-DTE                                             
               FROM TDTATTR                                                     
               WHERE  FSCL_MN_END_DTE = :PV-NFM-END-DTE                         
BVADJ *        AND    DAY_NM  = 'FRIDAY' AND WK_IN_MN_NBR ='2'                  
AUDIT          AND    DAY_NM  = 'FRIDAY' AND WK_IN_MN_NBR ='1'                  
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQLCODE IN 8300-READ-TDTATTR-TABLE:' SQLCODE                
           DISPLAY 'KEY DATE IS:' DTATTR-KY-DTE                                 
           DISPLAY 'PV-NFM-END-DTE  IS:' PV-NFM-END-DTE                         
           MOVE DTATTR-KY-DTE TO PV-CLO-DTE                                     
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 100                                               
                   MOVE '8300-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE < 0                                                 
                   MOVE '8300-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '8300-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE WARNING'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8400-READ-TDTATTR-TABLE.                                                 
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT KY_DTE                                                    
               INTO  :DTATTR-KY-DTE                                             
               FROM TDTATTR                                                     
               WHERE  FSCL_MN_END_DTE = :PV-NFM-END-DTE                         
               AND    DAY_NM  = 'FRIDAY' AND WK_IN_MN_NBR ='1'                  
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQLCODE IN 8400-READ-TDTATTR-TABLE:' SQLCODE                
           DISPLAY 'DTATTR-KY-DTE IS:' DTATTR-KY-DTE                            
           DISPLAY 'PV-NFM-END-DTE IS:' PV-NFM-END-DTE                          
           MOVE DTATTR-KY-DTE TO PV-ML-CLO-DTE                                  
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 100                                               
                   MOVE '8400-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE < 0                                                 
                   MOVE '8400-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '8400-READ-TDTATTR-TABLE'                               
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TDTATTR TABLE WARNING'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8500-UPDATE-DT-CTRL-TABLE.                                               
      ******************************************************************        
           EXEC SQL                                                             
            UPDATE MLT_BRND_VND_DTE_CNTL                                        
             SET PSTG_FSCL_MN_END_DTE = :MLT00021-PSTG-FSCL-MN-END-DTE,         
               PSTG_FSCL_WK_END_DTE = :MLT00021-PSTG-FSCL-WK-END-DTE,           
               OPN_FSCL_MN_DTE      = :MLT00021-OPN-FSCL-MN-DTE,                
               OPN_FSCL_WK_DTE      = :MLT00021-OPN-FSCL-WK-DTE,                
               CLO_DTE              = :MLT00021-CLO-DTE,                        
               ML_CLO_DTE           = :MLT00021-ML-CLO-DTE,                     
               LAST_PSTG_UPD_DTE    = :MLT00021-LAST-PSTG-UPD-DTE,              
               LAST_UPD_TMST        = :MLT00021-LAST-UPD-TMST,                  
               LAST_UPD_USR_ID      = :MLT00021-LAST-UPD-USR-ID                 
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SQLCODE IN 8500-UPDATE-DT-CTRL-TABLE:' SQLCODE              
           DISPLAY 'PSTG-FSCL-MN-END-DTE:' MLT00021-PSTG-FSCL-MN-END-DTE        
           DISPLAY 'PSTG-FSCL-WK-END-DTE:' MLT00021-PSTG-FSCL-WK-END-DTE        
           DISPLAY 'OPN-FSCL-MN-DTE:' MLT00021-OPN-FSCL-MN-DTE                  
           DISPLAY 'OPN-FSCL-WK-DTE:' MLT00021-OPN-FSCL-WK-DTE                  
           DISPLAY 'CLO-DTE:' MLT00021-CLO-DTE                                  
           DISPLAY 'ML-CLO-DTE:' MLT00021-ML-CLO-DTE                            
           DISPLAY 'LAST-PSTG-UPD-DTE:' MLT00021-LAST-PSTG-UPD-DTE              
           DISPLAY 'LAST-UPD-TMST:' MLT00021-LAST-UPD-TMST                      
           DISPLAY 'LAST-UPD-USR-ID:' MLT00021-LAST-UPD-USR-ID                  
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE < 0                                                 
                   MOVE '8500-UPDATE-DT-CTRL-TABLE' TO                          
                                AA-PARAGRAPH-NAME                               
                   MOVE 'MLT-BRND-VND-DTE-CNTL TBL UPDT ERROR' TO               
                                AA-MESSAGE                                      
                   MOVE 'UPDATE' TO AA-DB2-OPERATION                            
                   PERFORM 9998-DB2-ABEND                                       
               WHEN SQLCODE > 0                                                 
               WHEN SQLWARN0 NOT = SPACES                                       
                   MOVE '8500-UPDATE-DT-CTRL-TABLE' TO                          
                                AA-PARAGRAPH-NAME                               
                   MOVE 'MLT-BRND-VND-DTE-CNTL TABLE WARNING'                   
                                       TO AA-MESSAGE                            
                   MOVE 'UPDATE'       TO AA-DB2-OPERATION                      
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8000-EOJ-PROCESS.                                                        
      ******************************************************************        
                                                                                
           CLOSE FISCAL-WEEK-FILE.                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9998-DB2-ABEND.                                                          
      ******************************************************************        
                                                                                
           CLOSE FISCAL-WEEK-FILE.                                              
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           SET AC-DB2-ERROR TO TRUE.                                            
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
