       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKRP331.                                        
       AUTHOR.                 VINOD/INFOSYS.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           09-19-08.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *       TO REPORT FAST TRANSFER ASSUMPTIVE RECEIVE TRANSFERS.    *        
      ******************************************************************        
      * THIS PROGRAM USES THE FILE FROM PROGRAM INKUP305 AS INPUT AND  *        
      * CREATES THE FAST TRANSFER ASSUMPTIVE RECEIVE TRANSFERS REPORT  *        
      *                                                                *        
      * WR/PRO DATE       CHANGED  DESCRIPTION OF CHANGES              *        
      *                   BY                                           *        
      * ------ ---------- -------  -------------------------------     *        
      * W33304 09-19-2008 VINOD/   NEW PROGRAM CREATED AS PART OF      *        
      *                   INFOSYS  HOLDING INVENTORY LEDGER OPEN - HILO*        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT TRANS-REPT-IN-FILE         ASSIGN TO INP01.                   
           SELECT TRANSFER-REPORT            ASSIGN TO RPT01.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TRANS-REPT-IN-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS.                                      
                                                                                
       01  TRANS-REPT-IN-RECORD             PIC  X(132).                        
                                                                                
       FD  TRANSFER-REPORT                                                      
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           DATA RECORD IS REPORT-LINE.                                          
                                                                                
       01  REPORT-LINE                      PIC  X(132).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *    CONSTANTS ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08)    VALUE                 
                                                          'INKRP331'.           
           05  PC-PRINT-LINE-MAX            PIC S9(04)    VALUE +60             
                                                          COMP.                 
                                                                                
      ******************************************************************        
      *    VARIABLES ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-INV-ID                    PIC S9(09)    COMP.                 
           05  PV-LOC-NBR                   PIC S9(04)    COMP                  
                                                          VALUE ZEROES.         
           05  PV-PAGE-COUNT                PIC S9(05)    COMP-3                
                                                          VALUE 0.              
           05  PV-LINE-COUNT                PIC S9(05)    COMP-3                
                                                          VALUE 0.              
           05  PV-LOC-ITEM-QTY              PIC S9(15)    COMP-3                
                                                          VALUE ZEROES.         
           05  PV-LOC-EXTD-RTL-AMT          PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-LOC-TFR-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-LOC-DFF-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-CYC-ITEM-QTY              PIC S9(15)    COMP-3                
                                                          VALUE ZEROES.         
           05  PV-CYC-EXTD-RTL-AMT          PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-CYC-TFR-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-CYC-DFF-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-TOT-ITEM-QTY              PIC S9(15)    COMP-3                
                                                          VALUE ZEROES.         
           05  PV-TOT-EXTD-RTL-AMT          PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-TOT-TFR-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
           05  PV-TOT-DFF-EXTD-RTL-AMT      PIC S9(15)V99 COMP-3                
                                                          VALUE ZEROES.         
                                                                                
      ******************************************************************        
      *    DATE AND TIME VARIABLES ARE DECLARED HERE                   *        
      ******************************************************************        
       01  SD-SYSTEM-DATE.                                                      
           05  SD-YY                        PIC 9(02)     VALUE ZEROES.         
           05  SD-MM                        PIC 9(02)     VALUE ZEROES.         
           05  SD-DD                        PIC 9(02)     VALUE ZEROES.         
                                                                                
       01  ST-SYSTEM-TIME.                                                      
           05  ST-HR                        PIC 9(02)     VALUE ZEROES.         
           05  ST-MM                        PIC 9(02)     VALUE ZEROES.         
           05  ST-SS                        PIC 9(02)     VALUE ZEROES.         
           05  FILLER                       PIC 9(02)     VALUE ZEROES.         
                                                                                
      ******************************************************************        
      *    PROGRAM SWITCHES ARE DECLARED HERE                          *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-TRANS-REPT-IN-FILE-SW     PIC X(01)     VALUE 'N'.            
               88  PS-TRANS-REPT-IN-EOF-YES               VALUE 'Y'.            
               88  PS-TRANS-REPT-IN-EOF-NO                VALUE 'N'.            
           05  PS-CHANGE-CYCLE-SW           PIC X(01)     VALUE 'N'.            
               88  PS-CHANGE-CYCLE-YES                    VALUE 'Y'.            
               88  PS-CHANGE-CYCLE-NO                     VALUE 'N'.            
           05  PS-INPUT-STORE-SW            PIC X(01)     VALUE 'N'.            
               88  PS-INPUT-STORE-YES                     VALUE 'Y'.            
               88  PS-INPUT-STORE-NO                      VALUE 'N'.            
                                                                                
      ******************************************************************        
      *    REPORT HEADERS AND VARIABLES                                *        
      ******************************************************************        
       01  HL1-HEADING-LINE-1.                                                  
           05  FILLER                  PIC X(40) VALUE SPACES.                  
           05  FILLER                  PIC X(53) VALUE                          
               'ADJUSTMENTS TO PHYSICAL INVENTORY FROM FAST TRANSFERS'.         
                                                                                
       01  STOR-HEADING-LINE.                                                   
           05  FILLER                  PIC X(05) VALUE 'CYCLE'.                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CYCLE-NO                PIC 9(04) VALUE ZEROES.                  
           05  FILLER                  PIC X(51) VALUE SPACES.                  
           05  CYCLE-NAME              PIC X(20) VALUE SPACES.                  
           05  FILLER                  PIC X(24) VALUE SPACES.                  
                                                                                
       01  CYCL-HEADING-LINE.                                                   
           05  FILLER                  PIC X(05) VALUE 'CYCLE'.                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CYCLE-NUM               PIC 9(04) VALUE ZEROES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'TOTAL'.                 
           05  FILLER                  PIC X(45) VALUE SPACES.                  
           05  CYCL-NAME               PIC X(20) VALUE SPACES.                  
           05  FILLER                  PIC X(24) VALUE SPACES.                  
                                                                                
       01  GRAND-HEADING-LINE.                                                  
           05  FILLER                  PIC X(11) VALUE                          
                                                'GRAND TOTAL'.                  
           05  FILLER                  PIC X(50) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                           ' FOR ALL CYCLES '.                  
           05  FILLER                  PIC X(24) VALUE SPACES.                  
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'STORE'.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'TRANSFER/'.             
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'SUB-'.                  
           05  FILLER                  PIC X(27) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'UNIT'.                  
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'EXTENDED'.              
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'TRANSFER'.              
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'EXTENDED'.              
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'EXTENDED'.              
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'NBR  '.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'SHEET NBR'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'DEPT'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CLASS'.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'SKU     '.              
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'QTY'.                   
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'RETAIL'.                
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'RETAIL'.                
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'RETAIL'.                
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'XFER RETAIL'.           
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'RETAIL DIFF'.           
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-LOC-NBR              PIC Z9999.                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-SHEET-NBR            PIC ZZZ999999.                           
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-DEPT-NBR             PIC 9(04).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-SUBCLASS-NBR         PIC 9(04).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  DL-SKU-NBR              PIC 99999999.                            
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-UNIT-QTY             PIC Z,ZZZ,ZZ9-.                          
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  DL-UNIT-RTL-AMT         PIC Z,ZZZ,ZZ9.99-.                       
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-EXTD-RTL-AMT         PIC ZZ,ZZZ,ZZ9.99-.                      
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  DL-TFR-RTL-AMT          PIC Z,ZZZ,ZZ9.99-.                       
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  DL-EXTD-TFR-RTL-AMT     PIC ZZ,ZZZ,ZZ9.99-.                      
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DL-DIFF-RTL-AMT         PIC ZZ,ZZZ,ZZ9.99-.                      
                                                                                
       01  TL-TOTAL-LINE.                                                       
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  TL-VERBIAGE             PIC X(22).                               
           05  FILLER                  PIC X(16) VALUE SPACES.                  
           05  TL-UNIT-QTY             PIC ZZ,ZZZ,ZZ9-.                         
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  TL-EXTD-RTL-AMT         PIC ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                  PIC X(16) VALUE SPACES.                  
           05  TL-EXTD-TFR-RTL-AMT     PIC ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  TL-DIFF-RTL-AMT         PIC ZZZ,ZZZ,ZZ9.99-.                     
                                                                                
       01  NL-NO-REPORT-LINE.                                                   
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(26) VALUE                          
                                          'NOTHING TO REPORT'.                  
                                                                                
      ******************************************************************        
      * INPUT FILE COPY MEMBER                                         *        
      ******************************************************************        
           COPY INRD031.                                                        
                                                                                
      ******************************************************************        
      * COPYBOOK -KOHLS STANDARD REPORT HEADINGS, DETAIL FOR 132 BYTES *        
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      ******************************************************************        
      *  PROGRAM PROCESSING STARTS HERE                                *        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATIONS.                                        
           PERFORM 2000-PROCESS-FILE                                            
              UNTIL PS-TRANS-REPT-IN-EOF-YES.                                   
           IF  PS-INPUT-STORE-NO                                                
               PERFORM 3000-PROCESS-STORE-TOTAL                                 
               PERFORM 4000-PROCESS-CYCLE-TOTAL                                 
               PERFORM 5000-PROCESS-GRAND-TOTAL                                 
           END-IF.                                                              
           PERFORM A000-END-OF-JOB.                                             
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   INITIALIZATION PARA                                          *        
      ******************************************************************        
                                                                                
       1000-INITIALIZATIONS.                                                    
                                                                                
           OPEN INPUT  TRANS-REPT-IN-FILE                                       
                OUTPUT TRANSFER-REPORT.                                         
                                                                                
           ACCEPT SD-SYSTEM-DATE            FROM DATE.                          
           ACCEPT ST-SYSTEM-TIME            FROM TIME.                          
                                                                                
           MOVE SD-MM                       TO DP132O-RUN-MONTH.                
           MOVE SD-DD                       TO DP132O-RUN-DAY.                  
           MOVE SD-YY                       TO DP132O-RUN-YEAR.                 
           MOVE ST-HR                       TO DP132O-RUN-HOUR.                 
           MOVE ST-MM                       TO DP132O-RUN-MINUTE.               
           MOVE 'INKRP331'                  TO DP132O-PROGRAM-NAME.             
           MOVE 1                           TO DP132O-REPORT-NUMBER.            
                                                                                
           SET PS-TRANS-REPT-IN-EOF-NO      TO   TRUE.                          
                                                                                
           PERFORM 9000-READ-TRANS-REPT-IN-FILE.                                
                                                                                
           IF  PS-TRANS-REPT-IN-EOF-YES                                         
               SET PS-INPUT-STORE-YES       TO   TRUE                           
               PERFORM 6000-PRINT-GENERAL-HEADINGS                              
               WRITE REPORT-LINE                                                
                 FROM NL-NO-REPORT-LINE     AFTER ADVANCING 2 LINE              
           ELSE                                                                 
               MOVE IN031-INV-ID            TO CYCLE-NO                         
               MOVE IN031-INV-NAME          TO CYCLE-NAME                       
               PERFORM 6000-PRINT-GENERAL-HEADINGS                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   MAIN PROCESSING PARA                                         *        
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           IF PV-LINE-COUNT IS GREATER THAN (PC-PRINT-LINE-MAX - 2)             
               PERFORM 6000-PRINT-GENERAL-HEADINGS                              
           END-IF.                                                              
                                                                                
           MOVE IN031-LOC-NBR               TO DL-LOC-NBR                       
                                               PV-LOC-NBR.                      
           MOVE IN031-SHEET-NBR             TO DL-SHEET-NBR.                    
           MOVE IN031-DEPT-NBR              TO DL-DEPT-NBR.                     
           MOVE IN031-SUBCLASS-NBR          TO DL-SUBCLASS-NBR.                 
           MOVE IN031-SKU-NBR               TO DL-SKU-NBR.                      
           MOVE IN031-UNIT-QTY              TO DL-UNIT-QTY.                     
           MOVE IN031-UNIT-RTL-AMT          TO DL-UNIT-RTL-AMT.                 
           MOVE IN031-EXTD-RTL-AMT          TO DL-EXTD-RTL-AMT.                 
           MOVE IN031-TFR-RTL-AMT           TO DL-TFR-RTL-AMT.                  
           MOVE IN031-EXTD-TFR-RTL-AMT      TO DL-EXTD-TFR-RTL-AMT.             
           MOVE IN031-DIFF-RTL-AMT          TO DL-DIFF-RTL-AMT.                 
           MOVE IN031-INV-ID                TO PV-INV-ID.                       
           ADD IN031-UNIT-QTY               TO PV-LOC-ITEM-QTY                  
                                               PV-CYC-ITEM-QTY                  
                                               PV-TOT-ITEM-QTY.                 
           ADD IN031-EXTD-RTL-AMT           TO PV-LOC-EXTD-RTL-AMT              
                                               PV-CYC-EXTD-RTL-AMT              
                                               PV-TOT-EXTD-RTL-AMT.             
           ADD IN031-EXTD-TFR-RTL-AMT       TO PV-LOC-TFR-EXTD-RTL-AMT          
                                               PV-CYC-TFR-EXTD-RTL-AMT          
                                               PV-TOT-TFR-EXTD-RTL-AMT.         
           ADD IN031-DIFF-RTL-AMT           TO PV-LOC-DFF-EXTD-RTL-AMT          
                                               PV-CYC-DFF-EXTD-RTL-AMT          
                                               PV-TOT-DFF-EXTD-RTL-AMT.         
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DL-DETAIL-LINE            AFTER ADVANCING 1 LINE.             
                                                                                
           PERFORM 9000-READ-TRANS-REPT-IN-FILE.                                
                                                                                
           IF  PS-TRANS-REPT-IN-EOF-NO                                          
               IF  IN031-INV-ID NOT = PV-INV-ID                                 
                   PERFORM 3000-PROCESS-STORE-TOTAL                             
                   PERFORM 4000-PROCESS-CYCLE-TOTAL                             
                   MOVE IN031-INV-ID        TO CYCLE-NO                         
                   MOVE IN031-INV-NAME      TO CYCLE-NAME                       
                   PERFORM 6000-PRINT-GENERAL-HEADINGS                          
                   SET PS-CHANGE-CYCLE-YES  TO TRUE                             
               ELSE                                                             
                   SET PS-CHANGE-CYCLE-NO   TO TRUE                             
               END-IF                                                           
               IF  ((IN031-LOC-NBR NOT = PV-LOC-NBR)                            
                   AND PS-CHANGE-CYCLE-NO)                                      
                   PERFORM 3000-PROCESS-STORE-TOTAL                             
                   PERFORM 6000-PRINT-GENERAL-HEADINGS                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   TO MOVE THE SUM QUANTITY AND SUM AMOUNT OF A SINGLE STORE    *        
      *   TO THE REPORT VARIABLE AND PRINT THEM                        *        
      ******************************************************************        
       3000-PROCESS-STORE-TOTAL.                                                
                                                                                
           IF PV-LINE-COUNT IS GREATER THAN (PC-PRINT-LINE-MAX - 2)             
               PERFORM 6000-PRINT-GENERAL-HEADINGS                              
           END-IF.                                                              
                                                                                
           MOVE '  STORE TOTAL   '          TO TL-VERBIAGE.                     
           MOVE PV-LOC-ITEM-QTY             TO TL-UNIT-QTY.                     
           MOVE PV-LOC-EXTD-RTL-AMT         TO TL-EXTD-RTL-AMT.                 
           MOVE PV-LOC-TFR-EXTD-RTL-AMT     TO TL-EXTD-TFR-RTL-AMT.             
           MOVE PV-LOC-DFF-EXTD-RTL-AMT     TO TL-DIFF-RTL-AMT.                 
                                                                                
           WRITE REPORT-LINE                                                    
             FROM TL-TOTAL-LINE             AFTER ADVANCING 2 LINE.             
                                                                                
           MOVE ZEROS                       TO PV-LOC-ITEM-QTY.                 
           MOVE ZEROS                       TO PV-LOC-EXTD-RTL-AMT.             
           MOVE ZEROS                       TO PV-LOC-TFR-EXTD-RTL-AMT.         
           MOVE ZEROS                       TO PV-LOC-DFF-EXTD-RTL-AMT.         
                                                                                
      ******************************************************************        
      *   TO MOVE THE SUM QUANTITY AND SUM AMOUNT OF ALL STORES IN     *        
      *   A SINGLE CYCLE TO THE REPORT VARIABLE AND PRINT THEM         *        
      ******************************************************************        
       4000-PROCESS-CYCLE-TOTAL.                                                
                                                                                
           MOVE CYCLE-NO                    TO CYCLE-NUM.                       
           MOVE CYCLE-NAME                  TO CYCL-NAME.                       
           PERFORM 7000-PRINT-CYCLE-TOTAL-HDNG.                                 
                                                                                
           MOVE '  TOTAL ADJUSTED  '        TO TL-VERBIAGE.                     
           MOVE PV-CYC-ITEM-QTY             TO TL-UNIT-QTY.                     
           MOVE PV-CYC-EXTD-RTL-AMT         TO TL-EXTD-RTL-AMT.                 
           MOVE PV-CYC-TFR-EXTD-RTL-AMT     TO TL-EXTD-TFR-RTL-AMT.             
           MOVE PV-CYC-DFF-EXTD-RTL-AMT     TO TL-DIFF-RTL-AMT.                 
                                                                                
           WRITE REPORT-LINE                                                    
             FROM TL-TOTAL-LINE             AFTER ADVANCING 2 LINE.             
                                                                                
           MOVE ZEROS                       TO PV-CYC-ITEM-QTY.                 
           MOVE ZEROS                       TO PV-CYC-EXTD-RTL-AMT.             
           MOVE ZEROS                       TO PV-CYC-TFR-EXTD-RTL-AMT.         
           MOVE ZEROS                       TO PV-CYC-DFF-EXTD-RTL-AMT.         
                                                                                
      ******************************************************************        
      *   TO MOVE THE SUM QUANTITY AND SUM AMOUNT OF ALL CYCLES        *        
      *   TO THE REPORT VARIABLE AND PRINT THEM                        *        
      ******************************************************************        
       5000-PROCESS-GRAND-TOTAL.                                                
                                                                                
           PERFORM 8000-PRINT-GRAND-TOTAL-HDNG.                                 
                                                                                
           MOVE ' GRAND TOTAL ADJUSTED'     TO TL-VERBIAGE.                     
           MOVE PV-TOT-ITEM-QTY             TO TL-UNIT-QTY.                     
           MOVE PV-TOT-EXTD-RTL-AMT         TO TL-EXTD-RTL-AMT.                 
           MOVE PV-TOT-TFR-EXTD-RTL-AMT     TO TL-EXTD-TFR-RTL-AMT.             
           MOVE PV-TOT-DFF-EXTD-RTL-AMT     TO TL-DIFF-RTL-AMT.                 
                                                                                
           WRITE REPORT-LINE                                                    
             FROM TL-TOTAL-LINE             AFTER ADVANCING 2 LINE.             
                                                                                
      ******************************************************************        
      * TO PRINT THE GENERAL HEADING IN THE REPORT                     *        
      ******************************************************************        
       6000-PRINT-GENERAL-HEADINGS.                                             
                                                                                
           ADD 1                            TO PV-PAGE-COUNT.                   
           MOVE PV-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DP132O-STANDRD-HEADER-132-COLS                                
                                            AFTER ADVANCING PAGE.               
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL1-HEADING-LINE-1        AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM STOR-HEADING-LINE         AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL2-HEADING-LINE-2        AFTER ADVANCING 2 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL3-HEADING-LINE-3        AFTER ADVANCING 1 LINE.             
                                                                                
           MOVE 5                           TO PV-LINE-COUNT.                   
                                                                                
      ******************************************************************        
      * TO PRINT THE HEADING FOR CYCLE TOTAL                           *        
      ******************************************************************        
       7000-PRINT-CYCLE-TOTAL-HDNG.                                             
                                                                                
           ADD 1                            TO PV-PAGE-COUNT.                   
           MOVE PV-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DP132O-STANDRD-HEADER-132-COLS                                
                                            AFTER ADVANCING PAGE.               
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL1-HEADING-LINE-1        AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM CYCL-HEADING-LINE         AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL2-HEADING-LINE-2        AFTER ADVANCING 2 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL3-HEADING-LINE-3        AFTER ADVANCING 1 LINE.             
                                                                                
           MOVE 5                           TO PV-LINE-COUNT.                   
                                                                                
      ******************************************************************        
      * TO PRINT THE REPORT HEADINGS FOR GRAND TOTAL                   *        
      ******************************************************************        
       8000-PRINT-GRAND-TOTAL-HDNG.                                             
                                                                                
           ADD 1                            TO PV-PAGE-COUNT.                   
           MOVE PV-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DP132O-STANDRD-HEADER-132-COLS                                
                                            AFTER ADVANCING PAGE.               
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL1-HEADING-LINE-1        AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM GRAND-HEADING-LINE        AFTER ADVANCING 1 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL2-HEADING-LINE-2        AFTER ADVANCING 2 LINE.             
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL3-HEADING-LINE-3        AFTER ADVANCING 1 LINE.             
                                                                                
           MOVE 5                           TO PV-LINE-COUNT.                   
                                                                                
      ******************************************************************        
      *   READS INPUT FILE                                             *        
      ******************************************************************        
       9000-READ-TRANS-REPT-IN-FILE.                                            
                                                                                
           READ TRANS-REPT-IN-FILE          INTO IN031-RECORD                   
           AT END                                                               
               SET PS-TRANS-REPT-IN-EOF-YES TO  TRUE                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *   END OF JOB PROCESSING                                        *        
      ******************************************************************        
       A000-END-OF-JOB.                                                         
                                                                                
           CLOSE TRANS-REPT-IN-FILE                                             
                 TRANSFER-REPORT.                                               
                                                                                
      ******************************************************************        
      *   END OF PROGRAM INKRP331                                      *        
      ******************************************************************        
