000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.             SUKRF505.                                        
000600 AUTHOR.                 LARRY SCHLEICHER - STRATAGEM                     
000700 INSTALLATION.           KOHLS.                                           
000800 DATE-WRITTEN            JUNE, 1996.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200* THIS PROGRAM DISPLAYS THE NOT FOUND SCREEN ON THE                       
001300* LRT (LASER RADIO TERMINAL) AND ACCEPTS A FUNCTION KEY AND NUMBER        
001400* TO DETERMINE HOW TO PROCESS THE CARTON DATA.                            
001500******************************************************************        
001600                                                                          
001700******************************************************************        
001800 ENVIRONMENT DIVISION.                                                    
001900******************************************************************        
002000                                                                          
002100******************************************************************        
002200 DATA DIVISION.                                                           
002300******************************************************************        
002400                                                                          
002500******************************************************************        
002600 WORKING-STORAGE SECTION.                                                 
002700******************************************************************        
002800 01  CA-COMMAREA.                                                         
002900     COPY SUWS001.                                                        
003000                                                                          
003011     COPY DPWS026.                                                        
003012                                                                          
003020******************************************************************        
003070*  DB2 KEYS                                                               
003071******************************************************************        
003090 01  DK-DB2-KEYS.                                                         
003091     05  DK-LOC-ID                       PIC  S9(4) COMP.                 
003100                                                                          
003200******************************************************************        
003300 01  PC-PROGRAM-CONSTANTS.                                                
003400*    05  PC-COMMAREA-LENGTH              PIC S9(04)  VALUE +300           
003500     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE +0             
003600                                                     COMP SYNC.           
003700     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
003800         'SUKRF505'.                                                      
003900     05  PC-DC-RF-APPL-PGM               PIC  X(08)  VALUE                
004000         'MPKCS027'.                                                      
004100     05  PC-SU-NOT-FOUND                 PIC  X(04)  VALUE 'S505'.        
004200     05  PC-SU-CHECK-IN-PGM              PIC  X(08)  VALUE                
004300         'SUKRF502'.                                                      
004400     05  PC-SU-ADD-NOT-FOUND             PIC  X(08)  VALUE                
004500         'SUKRF506'.                                                      
004600     05  PC-END-OF-SESSION-MSG           PIC  X(19)  VALUE                
004700         'END OF SESSION     '.                                           
004800     05  PC-INVALID-SELECTION-MSG        PIC  X(19)  VALUE                
004900         'INVALID SELECTION  '.                                           
005000     05  PC-INVALID-VALUE-MSG            PIC  X(19)  VALUE                
005100         'INVALID VALUE      '.                                           
005200     05  PC-UNRECOGNIZED-PF-KEY-MSG      PIC  X(19)  VALUE                
005300         'UNRECOGNIZED PF KEY'.                                           
005400     05  PC-ACCESS-NOT-ALLOWED-MSG       PIC  X(19)  VALUE                
005500         'USER NOT AUTHORIZED'.                                           
005600     05  PC-NON-NUMERIC-SELECTION-MSG    PIC  X(19)  VALUE                
005700         'NUMERIC VALUE REQ. '.                                           
005800     05  PC-TRANS-UNAVAILABLE-MSG        PIC  X(19)  VALUE                
005900         'TRANS NOT AVAILABLE'.                                           
006000     05  PC-CICS-ABEND-MSG               PIC  X(19)  VALUE                
006100         'CICS ABEND -CALL DP'.                                           
006200     05  PC-CICSAUTH-MSG                 PIC  X(19)  VALUE                
006300         'CICSAUTH BAD RETURN'.                                           
006400     05  PC-UNAVAIL-SELECTION-MSG        PIC X(19)   VALUE                
006500         'SELECTION NOT AVAIL'.                                           
006600     05  PC-N                            PIC X(01)   VALUE 'N'.           
006700                                                                          
006800                                                                          
006900 01  PS-PROGRAM-SWITCHES.                                                 
007000     05  PS-ACCESS-ALLOWED-IND           PIC  X(01)  VALUE 'N'.           
007100         88  PS-NO-ACCESS-ALLOWED                    VALUE 'N'.           
007200         88  PS-ACCESS-ALLOWED                       VALUE 'Y'.           
007300                                                                          
007400                                                                          
007500 01  PV-PROGRAM-VARIABLES.                                                
007600     05  PV-MESSAGE-AREA                 PIC  X(19)  VALUE SPACE.         
007700     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS          
007800                                                     COMP SYNC.           
007900     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE.         
008000     05  PV-DUPLICATE-IND                PIC  X(01)  VALUE SPACE.         
008100         88 PV-VALID-DUP-IND             VALUE 'Y', 'N'.                  
008200     05  PV-LABEL-IND                    PIC  X(01)  VALUE SPACE.         
008300         88 PV-VALID-LBL-IND             VALUE 'Y', 'N'.                  
008400                                                                          
008500******************************************************************        
008600*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
008700******************************************************************        
008800     COPY DPWS013.                                                        
008900                                                                          
009000******************************************************************        
009100*    COPY BOOK FOR THE CICSAUTH SUBROUTINE                                
009200******************************************************************        
009300     COPY DPWS008.                                                        
009400                                                                          
009500******************************************************************        
009600*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)         
009700******************************************************************        
009800     COPY DPWS010I.                                                       
009900                                                                          
010000******************************************************************        
010100*  GENERATED DSECT FOR THE MPKM027 MAPSET                                 
010200******************************************************************        
010300     COPY SUKM505.                                                        
010400                                                                          
010500******************************************************************        
010600*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           
010700******************************************************************        
010800     COPY DPWS015.                                                        
010900                                                                          
011000******************************************************************        
011100*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                              
011200******************************************************************        
011300     COPY DFHAID.                                                         
011400                                                                          
011401******************************************************************        
011420*  SQL AND COBOL DECLARATIONS FOR THE KOHLS/RETEK PARMS (TKRPRPM)         
011421******************************************************************        
011440     EXEC SQL                                                             
011450          INCLUDE TKRPRPM                                                 
011460     END-EXEC.                                                            
011470                                                                          
011481******************************************************************        
011491*    DB2 AREA FOR COMMUNICATIONS                                          
011492******************************************************************        
011493     EXEC SQL                                                             
011494          INCLUDE SQLCA                                                   
011495     END-EXEC.                                                            
011496                                                                          
011500******************************************************************        
011600 LINKAGE SECTION.                                                         
011700******************************************************************        
011800*01  DFHCOMMAREA                         PIC  X(300).                     
011900 01  DFHCOMMAREA.                                                         
012000     05  FILLER                       OCCURS 1 TO 4072 TIMES              
012100                                      DEPENDING ON EIBCALEN.              
012200         10  FILLER                   PIC X.                              
012300                                                                          
012400                                                                          
012500******************************************************************        
012600 PROCEDURE DIVISION.                                                      
012700******************************************************************        
012800 0000-PROGRAM-MAINLINE.                                                   
012900*LMM                                                                      
013000     INITIALIZE CA-COMMAREA                                               
013100     MOVE DFHCOMMAREA                TO CA-COMMAREA.                      
013200     MOVE LOW-VALUES                 TO SU505AI.                          
013300                                                                          
013400     EVALUATE TRUE                                                        
013500         WHEN EIBCALEN = ZERO                                             
013600         WHEN EIBAID = DFHCLEAR                                           
013700         WHEN SU001-INITIALIZE-SCREEN                                     
013800              MOVE SU001-CARTON TO ACARTNO                                
013900              SET SU001-PROCESS-SCREEN TO TRUE                            
014000              PERFORM 8000-SEND-MAP                                       
014100              PERFORM 8200-RETURN-TRANID                                  
014200         WHEN EIBAID = DFHPF2                                             
014210              PERFORM 7000-GET-CARTON-BUILD                               
014220              IF KRPRPM-FUNC-TXT = 'N'                                    
014300                  SET SU001-ADD-NOT-FOUND TO TRUE                         
014400                  MOVE PC-SU-ADD-NOT-FOUND  TO PV-TRANSFER-PROGRAM        
014401                  PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                   
014410              ELSE                                                        
014411* DMH 6/25/14 BEGIN                                                       
014412                  MOVE PC-UNRECOGNIZED-PF-KEY-MSG                         
014413                                     TO AMSGLNO                           
014414                  PERFORM 8100-SEND-DATA-ONLY                             
014415                  PERFORM 8200-RETURN-TRANID                              
014416* DMH 6/25/14 END                                                         
014420              END-IF                                                      
014600         WHEN EIBAID = DFHPF3                                             
014610         WHEN EIBAID = DFHENTER                                           
014700              SET SU001-SKIP-NOT-FOUND TO TRUE                            
014800              MOVE PC-SU-CHECK-IN-PGM     TO PV-TRANSFER-PROGRAM          
014900              PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                       
015000         WHEN OTHER                                                       
015100              MOVE PC-UNRECOGNIZED-PF-KEY-MSG                             
015200                                     TO AMSGLNO                           
015300              PERFORM 8100-SEND-DATA-ONLY                                 
015400              PERFORM 8200-RETURN-TRANID                                  
015500     END-EVALUATE.                                                        
015600                                                                          
015700     GOBACK.                                                              
015800*                                                                         
015900*                                                                         
016000 2100-XCTL-TO-SELECTED-PROGRAM.                                           
016100     SET SU001-INITIALIZE-SCREEN     TO TRUE.                             
016200     MOVE PC-CURRENT-PROGRAM-NAME    TO SU001-LAST-PROGRAM.               
016300                                                                          
016400*LMM                                                                      
016500     MOVE LENGTH OF CA-COMMAREA   TO PV-COMMAREA-LENGTH                   
016600     EXEC CICS                                                            
016700         XCTL                                                             
016800             PROGRAM   (PV-TRANSFER-PROGRAM)                              
016900             COMMAREA  (CA-COMMAREA)                                      
017000*LMM         LENGTH    (PC-COMMAREA-LENGTH)                               
017100             LENGTH    (PV-COMMAREA-LENGTH)                               
017200             RESP      (PV-CICS-RESPONSE)                                 
017300     END-EXEC.                                                            
017400                                                                          
017500     EVALUATE TRUE                                                        
017600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
017700              CONTINUE                                                    
017800         WHEN OTHER                                                       
017900              MOVE '2100-XCTL-TO-SELECTED-PROGRAM'                        
018000                                     TO DP013-PARAGRAPH                   
018100              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '               
018200                                     TO DP013-MESSAGE-TEXT (1)            
018300              MOVE PV-TRANSFER-PROGRAM                                    
018400                                     TO DP013-MESSAGE-TEXT (2)            
018500              SET DP013-CICS-ABEND   TO TRUE                              
018600              SET DP013-NO-ROLLBACK  TO TRUE                              
018700              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
018800              MOVE PC-TRANS-UNAVAILABLE-MSG                               
018900                                     TO AMSGLNO                           
019000              SET SU001-PROCESS-SCREEN TO TRUE                            
019100              PERFORM 8100-SEND-DATA-ONLY                                 
019200              PERFORM 8200-RETURN-TRANID                                  
019300     END-EVALUATE.                                                        
019302                                                                          
019357                                                                          
019358     COPY DPPD026.                                                        
019359                                                                          
019360*****************************************************************         
019361 7000-GET-CARTON-BUILD.                                                   
019362*     SELECT THE CARTON BUILD INDICATOR                                   
019363*****************************************************************         
019364                                                                          
019365     MOVE SU001-CURR-OPER-UNT-ID   TO DK-LOC-ID.                          
019366                                                                          
019370     EXEC SQL                                                             
019380         SELECT FUNC_TXT                                                  
019390           INTO :KRPRPM-FUNC-TXT                                          
019391           FROM TKRPRPM                                                   
019392          WHERE SYS_PRC_FUNC_CDE  = 'UCCCAB'                              
019393            AND LOC_ID            = :DK-LOC-ID                            
019394            AND INTFC_SYS_CDE     = 'KHL'                                 
019395            AND FUNC_PRC_STAT_CDE = 'AC'                                  
019396            AND (CURRENT DATE                                             
019397        BETWEEN DATE(FUNC_BEG_TMST) AND                                   
019398                DATE(FUNC_END_TMST) )                                     
019399     END-EXEC.                                                            
019400                                                                          
019401     EVALUATE SQLCODE                                                     
019402         WHEN ZERO                                                        
019403             CONTINUE                                                     
019404         WHEN OTHER                                                       
019405              MOVE '7000-GET-CARTON-BUILD'                                
019406                               TO DP013-PARAGRAPH                         
019407              MOVE 'SELECT CARTON BUILD IND'                              
019408                               TO DP013-MESSAGE-TEXT (1)                  
019409              MOVE SQLCA       TO DP013-SQLCA                             
019410              SET DP013-DB2-ABEND                                         
019411                               TO TRUE                                    
019412              PERFORM DP014-0000-PROCESS-ABEND                            
019413     END-EVALUATE.                                                        
019414                                                                          
019420*                                                                         
019500 8000-SEND-MAP.                                                           
019600     EXEC CICS                                                            
019700         SEND MAP       ('SU505A')                                        
019800              MAPSET    ('SUKM505')                                       
019900              ERASE                                                       
020000              RESP      (PV-CICS-RESPONSE)                                
020100     END-EXEC.                                                            
020200                                                                          
020300     EVALUATE TRUE                                                        
020400         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
020500              CONTINUE                                                    
020600         WHEN OTHER                                                       
020700              MOVE '8000-SEND-MAP'   TO DP013-PARAGRAPH                   
020800              MOVE 'UNABLE TO SEND MAP'                                   
020900                                     TO DP013-MESSAGE-TEXT (1)            
021000              SET DP013-CICS-ABEND   TO TRUE                              
021100              SET DP013-NO-ROLLBACK  TO TRUE                              
021200              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
021300     END-EVALUATE.                                                        
021400*                                                                         
021500 8100-SEND-DATA-ONLY.                                                     
021600                                                                          
021700     EXEC CICS                                                            
021800         SEND MAP       ('SU505A')                                        
021900              MAPSET    ('SUKM505')                                       
022000              CURSOR                                                      
022100              DATAONLY                                                    
022200              RESP      (PV-CICS-RESPONSE)                                
022300     END-EXEC.                                                            
022400                                                                          
022500     EVALUATE TRUE                                                        
022600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
022700              CONTINUE                                                    
022800         WHEN OTHER                                                       
022900              MOVE '8100-SEND-DATA-ONLY'                                  
023000                                     TO DP013-PARAGRAPH                   
023100              MOVE 'UNABLE TO SEND DATA'                                  
023200                                     TO DP013-MESSAGE-TEXT (1)            
023300              SET DP013-CICS-ABEND   TO TRUE                              
023400              SET DP013-NO-ROLLBACK  TO TRUE                              
023500              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
023600     END-EVALUATE.                                                        
023700*                                                                         
023800*                                                                         
023900 8200-RETURN-TRANID.                                                      
024000*LMM                                                                      
024100     MOVE LENGTH OF CA-COMMAREA    TO PV-COMMAREA-LENGTH                  
024200     EXEC CICS                                                            
024300         RETURN TRANSID   (PC-SU-NOT-FOUND)                               
024400                COMMAREA  (CA-COMMAREA)                                   
024500*LMM            LENGTH    (PC-COMMAREA-LENGTH)                            
024600                LENGTH    (PV-COMMAREA-LENGTH)                            
024700                RESP      (PV-CICS-RESPONSE)                              
024800     END-EXEC.                                                            
024900                                                                          
025000     EVALUATE TRUE                                                        
025100         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
025200              CONTINUE                                                    
025300         WHEN OTHER                                                       
025400              MOVE '8200-RETURN-TRANID'                                   
025500                                     TO DP013-PARAGRAPH                   
025600              MOVE 'UNABLE TO RETURN TO TRANID'                           
025700                                     TO DP013-MESSAGE-TEXT (1)            
025800              SET DP013-CICS-ABEND   TO TRUE                              
025900              SET DP013-NO-ROLLBACK  TO TRUE                              
026000              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
026100     END-EVALUATE.                                                        
026200*                                                                         
026300 9999-WRITE-MESSAGE-TO-CICS-LOG.                                          
026400     MOVE PC-CURRENT-PROGRAM-NAME    TO DP013-PROGRAM.                    
026500     MOVE DFHEIBLK                   TO DP013-DFHEIBLK.                   
026600     INITIALIZE DP013-SRC-TASK-NUMBER                                     
026700                DP013-STACK.                                              
026800     SET DP013-LINK-RETURN           TO TRUE.                             
026900                                                                          
027000     EXEC CICS                                                            
027100         LINK                                                             
027200             PROGRAM   (DP013-NAME)                                       
027300             COMMAREA  (DP013-PARAMETERS)                                 
027400             LENGTH    (DP013-LENGTH)                                     
027500     END-EXEC.                                                            
027600*----------------------------------------------------------------*        
027700*    ABEND PROCESSOR MODULE                                               
027800*----------------------------------------------------------------*        
027900                                                                          
028000     COPY DPPD014.                                                        
