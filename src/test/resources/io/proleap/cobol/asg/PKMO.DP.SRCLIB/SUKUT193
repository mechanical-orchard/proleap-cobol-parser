       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.    SUKUT193.                                         00020002
       AUTHOR.        JIM ARENDT.                                       00030002
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040002
       DATE-WRITTEN.  SEPTEMBER, 2017.                                  00050002
       DATE-COMPILED.                                                   00060002
      ******************************************************************00070002
      *  DB2                                                            00080002
      * EXTRACT OF THE SML'S THAT WERE ASSUMPTIVELY RECEIVED            00090026
      * NOTE: INPUT FILE IS FROM SUK041                                 00100026
      ******************************************************************00110002
      * DATE: 09/2017 MADE BY:JIM ARENDT  INITIAL IMPLEMENTATION        00120002
      ******************************************************************00130002
       ENVIRONMENT DIVISION.                                            00140002
       CONFIGURATION SECTION.                                           00150002
       SOURCE-COMPUTER.    IBM-3090.                                    00160002
       OBJECT-COMPUTER.    IBM-3090.                                    00170002
       INPUT-OUTPUT SECTION.                                            00180002
       FILE-CONTROL.                                                    00190002
                                                                        00200002
           SELECT INPUT-ASSUMPT-CRTN-FILE                               00210002
                  ASSIGN TO INP01.                                      00220002
                                                                        00230002
           SELECT OUTPUT-SML-FILE                                       00240002
                  ASSIGN TO OUT01.                                      00250002
                                                                        00260002
       DATA DIVISION.                                                   00270002
       FILE SECTION.                                                    00280002
                                                                        00290002
       FD  INPUT-ASSUMPT-CRTN-FILE                                      00300002
           RECORDING MODE IS F                                          00310002
           LABEL RECORDS OMITTED                                        00320002
           BLOCK CONTAINS 0 RECORDS                                     00330002
           DATA RECORD IS INPUT-ASSUMPT-CRTN-REC.                       00340002
       01  INPUT-ASSUMPT-CRTN-REC              PIC  X(100).             00350002
                                                                        00360002
       FD  OUTPUT-SML-FILE                                              00370002
           RECORDING MODE IS F                                          00380002
           LABEL RECORDS OMITTED                                        00390002
           BLOCK CONTAINS 0 RECORDS                                     00400002
           DATA RECORD IS OUTPUT-SML-RECORD.                            00410002
       01  OUTPUT-SML-RECORD                   PIC  X(250).             00420004
                                                                        00430002
       WORKING-STORAGE SECTION.                                         00440002
                                                                        00450002
      ******************************************************************00460002
      * PROGRAM CONSTANTS                                               00470002
      ******************************************************************00480002
       01  PC-CONSTANTS-FIELDS.                                         00490002
           05  PC-PGM-NAME                     PIC  X(08) VALUE         00500002
                                                          'SUKUT193'.   00510002
           05  PC-MAX-RETRIES                  PIC S9(03) VALUE +3      00520002
                                                          COMP-3.       00530002
                                                                        00540002
      ******************************************************************00550002
      * PROGRAM VARIABLES                                               00560002
      ******************************************************************00570002
       01  PV-VARIABLE-FIELDS.                                          00580002
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACE.  00590002
           05  PV-DB2-OPERATION                PIC  X(30) VALUE SPACE.  00600002
           05  PV-EDIT-FIELD                   PIC  ZZZ,ZZZ,ZZ9.        00610012
           05  PV-TRIP-ID                      PIC S9(09) COMP.         00620002
           05  PV-PREVIOUS-TRIP-ID             PIC S9(09) COMP.         00630014
           05  PV-TRAILER-TMST                 PIC  X(26) VALUE SPACES. 00640014
           05  PV-SHIPT-UNT-ID                 PIC S9(18)V COMP-3.      00650003
           05  PV-TIMESTAMP.                                            00660010
               10  PV-DATE-FIELD               PIC  X(10) VALUE SPACES. 00670010
               10  FILLER                      PIC  X(01) VALUE SPACES. 00680010
               10  PV-TIME-FIELD.                                       00690010
                   15  PV-TIME-HH              PIC  X(02) VALUE SPACES. 00700010
                   15  PV-TIME-COLON-1         PIC  X(01) VALUE SPACES. 00710010
                   15  PV-TIME-MM              PIC  X(02) VALUE SPACES. 00720010
                   15  PV-TIME-COLON-2         PIC  X(01) VALUE SPACES. 00730010
                   15  PV-TIME-SS              PIC  X(02) VALUE SPACES. 00740010
               10  FILLER                      PIC  X(07) VALUE SPACES. 00750010
                                                                        00760002
      ******************************************************************00770002
      * PROGRAM ACCUMULATORS                                            00780002
      ******************************************************************00790002
       01  PA-PROGRAM-ACCUMULATORS.                                     00800002
           05  PA-INPUT-RECORDS-READ           PIC  9(09) VALUE ZEROS.  00810002
           05  PA-NO-DETAIL-FOUND              PIC  9(09) VALUE ZEROS.  00820018
           05  PA-OUTPUT-RECS-WROTE            PIC  9(09) VALUE ZEROS.  00830018
                                                                        00840002
      ******************************************************************00850002
      * PROGRAM SWITCHES                                                00860002
      ******************************************************************00870002
       01  PS-SWITCH-AREAS.                                             00880002
           05  PS-FIRST-TIME-SW                PIC  X(01) VALUE 'Y'.    00890019
               88  PS-FIRST-TIME-Y                        VALUE 'Y'.    00900002
               88  PS-FIRST-TIME-N                        VALUE 'N'.    00910002
           05  PS-END-OF-INPUT-FILE-SW         PIC  X(01) VALUE 'N'.    00920002
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00930002
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    00940002
           05  PS-SML-FOUND-SW                 PIC  X(01) VALUE 'N'.    00950015
               88  PS-SML-FOUND-Y                         VALUE 'Y'.    00960015
               88  PS-SML-FOUND-N                         VALUE 'N'.    00970015
                                                                        00980002
      ******************************************************************00990002
      * ABEND INFORMATION                                               01000002
      ******************************************************************01010002
       01  ABEND-CODE                          PIC S9(04) VALUE ZEROS   01020002
                                                          COMP SYNC.    01030002
           88  ABEND-4001-WRITE-ERROR                     VALUE +4001.  01040002
           88  ABEND-4002-READ-ERROR                      VALUE +4002.  01050002
           88  ABEND-4003-CTRL-CARD-ERROR                 VALUE +4003.  01060002
           88  ABEND-4004-TABLE-ERROR                     VALUE +4004.  01070002
           88  ABEND-4005-OPEN-ERROR                      VALUE +4005.  01080002
           88  ABEND-4006-CLOSE-ERROR                     VALUE +4006.  01090002
           88  ABEND-4007-SEQUENCE-ERROR                  VALUE +4007.  01100002
           88  ABEND-4008-DATA-ERROR                      VALUE +4008.  01110002
           88  ABEND-4009-KEY-DATA-ERROR                  VALUE +4009.  01120002
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.  01130002
           88  ABEND-4019-CAL-SUB-ERROR                   VALUE +4019.  01140002
           88  ABEND-4020-MQ-ERROR                        VALUE +4020.  01150002
           88  ABEND-4444-FORCED-ABEND                    VALUE +4444.  01160002
                                                                        01170002
      ******************************************************************01180002
      ****ABEND ROUTINE INFORMATION                                     01190002
           COPY SQLCA2.                                                 01200002
                                                                        01210002
      ****ABEND ROUTINE INFORMATION                                     01220002
           COPY DPWS004.                                                01230002
                                                                        01240002
      ****INPUT COPYBOOK                                                01250002
           COPY SURD153.                                                01260002
                                                                        01270002
      ****OUTPUT COPYBOOK                                               01280002
           COPY SURD163.                                                01290002
                                                                        01300002
      ******************************************************************01310002
      * TABLE INCLUSIONS                                                01320002
      ******************************************************************01330002
      ** SUT_OTBND_CART TABLE                                           01340002
           EXEC SQL INCLUDE SUT00024 END-EXEC.                          01350002
                                                                        01360002
      ** TSUOBTR TABLE                                                  01370014
           EXEC SQL INCLUDE TSUOBTR  END-EXEC.                          01380014
                                                                        01390014
      ** TSUOBTS TABLE                                                  01400014
           EXEC SQL INCLUDE TSUOBTS  END-EXEC.                          01410014
                                                                        01420014
      ** TCNTRID TABLE                                                  01430014
           EXEC SQL INCLUDE TCNTRID  END-EXEC.                          01440014
                                                                        01450014
      ** XLT_LOC TABLE                                                  01460014
           EXEC SQL INCLUDE XLT00010 END-EXEC.                          01470014
                                                                        01480014
      ** DB2 TABLE INCLUSION                                            01490002
           EXEC SQL INCLUDE SQLCA    END-EXEC.                          01500002
                                                                        01510002
                                                                        01520002
       PROCEDURE DIVISION.                                              01530002
      ******************************************************************01540002
       0000-MAINLINE-PROCESSING.                                        01550002
                                                                        01560002
           PERFORM 1000-INITIALIZATION.                                 01570002
                                                                        01580002
           PERFORM 2000-PROCESS-INPUT-FILE                              01590002
             UNTIL PS-END-OF-INPUT-FILE.                                01600002
                                                                        01610002
           PERFORM 9000-WRAPUP.                                         01620002
                                                                        01630002
           GOBACK.                                                      01640002
                                                                        01650002
      ******************************************************************01660002
      * INITIALIZATION ROUTINE                                          01670002
      ******************************************************************01680002
       1000-INITIALIZATION.                                             01690002
                                                                        01700002
           DISPLAY ' '.                                                 01710002
           DISPLAY '*******************************************'.       01720002
           DISPLAY '***  PROGRAM NAME:  '  PC-PGM-NAME.                 01730002
           DISPLAY ' '.                                                 01740002
                                                                        01750002
           OPEN INPUT  INPUT-ASSUMPT-CRTN-FILE                          01760002
                OUTPUT OUTPUT-SML-FILE.                                 01770002
                                                                        01780002
           PERFORM 1100-READ-INPUT-FILE.                                01790002
                                                                        01800002
                                                                        01810002
      ******************************************************************01820002
      * READ THE INPUT ASSUMPTIVE CARTON FILE                           01830002
      ******************************************************************01840002
       1100-READ-INPUT-FILE.                                            01850002
                                                                        01860002
           READ INPUT-ASSUMPT-CRTN-FILE INTO SU153-EXTRACT-RECORD       01870002
              AT END                                                    01880002
                 SET PS-END-OF-INPUT-FILE TO TRUE.                      01890002
                                                                        01900002
           IF PS-END-OF-INPUT-FILE                                      01910002
               IF PS-FIRST-TIME-Y                                       01920002
                  DISPLAY ' '                                           01930002
                  DISPLAY '-- INPUT FILE MISSING --'                    01940002
                  DISPLAY ' '                                           01950002
               END-IF                                                   01960002
           ELSE                                                         01970002
               ADD 1                     TO PA-INPUT-RECORDS-READ       01980002
               MOVE SU153-OTBND-TRIP-ID  TO PV-PREVIOUS-TRIP-ID         01990015
               MOVE SU153-SHIPT-UNT-ID   TO PV-SHIPT-UNT-ID             02000015
               SET PS-FIRST-TIME-N       TO TRUE                        02010002
           END-IF.                                                      02020002
                                                                        02030002
      ******************************************************************02040002
      * PROCESS ALL THE RECORDS IN THE INPUT FILE                       02050002
      ******************************************************************02060002
       2000-PROCESS-INPUT-FILE.                                         02070002
                                                                        02080002
           PERFORM 2100-SELECT-SML-DATA.                                02090015
           IF PS-SML-FOUND-N                                            02100015
              ADD 1             TO PA-NO-DETAIL-FOUND                   02110019
              PERFORM 2400-PROCESS-RECORDS                              02120019
                                                                        02130017
              PERFORM 2500-WRITE-OUTPUT-RECORD                          02140019
                                                                        02150002
           ELSE                                                         02160002
              PERFORM 2300-PROCESS-DATA                                 02170017
           END-IF.                                                      02180002
                                                                        02190002
           PERFORM 1100-READ-INPUT-FILE.                                02200002
                                                                        02210002
      ******************************************************************02220015
      * RETRIEVE THE SML TRAILER DATA THAT WERE ASSUMPTIVELY RECEIVED   02230025
      ******************************************************************02240015
       2100-SELECT-SML-DATA.                                            02250015
                                                                        02260015
           EXEC SQL                                                     02270015
               SELECT  SHIPT_UNT_ID                                     02280015
                      ,ORGN_LOC_NBR                                     02290015
                      ,DEST_LOC_NBR                                     02300015
                      ,CART_SRC_CDE                                     02310015
                      ,CRE_DTE                                          02320015
                      ,CRE_TM                                           02330015
                      ,ORIG_CART_ID                                     02340015
                      ,ACTV_IND                                         02350015
                 INTO :SUT00024-SHIPT-UNT-ID                            02360015
                     ,:SUT00024-ORGN-LOC-NBR                            02370015
                     ,:SUT00024-DEST-LOC-NBR                            02380015
                     ,:SUT00024-CART-SRC-CDE                            02390015
                     ,:SUT00024-CRE-DTE                                 02400015
                     ,:SUT00024-CRE-TM                                  02410015
                     ,:SUT00024-ORIG-CART-ID                            02420015
                     ,:SUT00024-ACTV-IND                                02430015
                 FROM SUT_OTBND_CART                                    02440015
                WHERE SHIPT_UNT_ID = :PV-SHIPT-UNT-ID                   02450015
               WITH UR                                                  02460015
           END-EXEC.                                                    02470015
                                                                        02480015
           EVALUATE TRUE                                                02490015
               WHEN SQLCODE = ZERO                                      02500015
                   SET PS-SML-FOUND-Y        TO TRUE                    02510020
                                                                        02520015
               WHEN SQLCODE = +100                                      02530015
                   SET PS-SML-FOUND-N        TO TRUE                    02540015
                                                                        02550015
               WHEN SQLWARN0 NOT EQUAL SPACE                            02560015
               WHEN SQLCODE NOT EQUAL ZERO                              02570015
                   DISPLAY ' ERROR SHIPT-UNT-ID = '                     02580015
                                                PV-SHIPT-UNT-ID         02590015
                   MOVE '2100-SELECT-SML-DATA'                          02600015
                                             TO PV-PARAGRAPH-NAME       02610015
                   MOVE 'ERROR SELECTING SML DATA'                      02620015
                                             TO PV-DB2-OPERATION        02630015
                   SET ABEND-4002-READ-ERROR TO TRUE                    02640015
                   PERFORM 9200-SQL-ABEND                               02650015
                                                                        02660015
           END-EVALUATE.                                                02670015
                                                                        02680015
      ******************************************************************02690002
      * POPULATE THE OUTPUT RECORD WITH DATA FOUND ON THE INPUT RECORD  02700017
      * AND DATA RETURNED IN THE SELECT STATEMENTS                      02710017
      ******************************************************************02720002
       2300-PROCESS-DATA.                                               02730017
                                                                        02740002
           PERFORM 2400-PROCESS-RECORDS.                                02750017
                                                                        02760010
           MOVE SUT00024-ORGN-LOC-NBR     TO SU163-SML-ORGN-LOC-NBR.    02770002
           MOVE SUT00024-DEST-LOC-NBR     TO SU163-SML-DEST-LOC-NBR.    02780002
           MOVE SUT00024-CART-SRC-CDE     TO SU163-CART-SRC-CDE.        02790008
           MOVE SUT00024-CRE-DTE          TO SU163-SML-CRE-DTE.         02800002
           IF SUT00024-CRE-TM > SPACES                                  02810011
              MOVE SUT00024-CRE-TM        TO PV-TIME-FIELD              02820011
              MOVE ':'                    TO PV-TIME-COLON-1            02830011
                                             PV-TIME-COLON-2            02840011
              MOVE PV-TIME-FIELD          TO SU163-SML-CRE-TM           02850011
           END-IF.                                                      02860011
           MOVE SUT00024-ORIG-CART-ID     TO SU163-ORIG-CART-ID.        02870002
           MOVE SUT00024-ACTV-IND         TO SU163-ACTV-IND.            02880002
                                                                        02890014
           IF PV-PREVIOUS-TRIP-ID = PV-TRIP-ID                          02900017
              CONTINUE                                                  02910014
           ELSE                                                         02920014
              MOVE PV-PREVIOUS-TRIP-ID    TO PV-TRIP-ID                 02930015
              PERFORM 4000-SELECT-TRAILER-INFO                          02940014
           END-IF.                                                      02950014
                                                                        02960010
           MOVE SUOBTR-ORGN-OPER-UNT-ID   TO SU163-ORGN-OPER-UNT-ID.    02970014
           MOVE SUOBTR-DEST-OPER-UNT-ID   TO SU163-DEST-OPER-UNT-ID.    02980014
           MOVE SUOBTS-TRIP-STAT-CDE      TO SU163-TRIP-STAT-CDE.       02990014
           MOVE XLT00010-E-BUS-IND        TO SU163-E-BUS.               03000014
           MOVE CNTRID-SCAC-CDE           TO SU163-SCAC-CDE.            03010014
           MOVE CNTRID-TRLR-ID            TO SU163-TRLR-ID.             03020014
           MOVE CNTRID-TRLR-SRL-NBR       TO SU163-TRLR-SRL-NBR.        03030014
           IF PV-TRAILER-TMST  > SPACES                                 03040014
              MOVE PV-TRAILER-TMST        TO PV-TIMESTAMP               03050014
              MOVE PV-DATE-FIELD          TO SU163-TRIP-STAT-DATE       03060014
              MOVE ':'                    TO PV-TIME-COLON-1            03070014
                                             PV-TIME-COLON-2            03080014
              MOVE PV-TIME-FIELD          TO SU163-TRIP-STAT-TIME       03090014
           END-IF.                                                      03100014
                                                                        03110004
           PERFORM 2500-WRITE-OUTPUT-RECORD.                            03120015
                                                                        03130002
      ******************************************************************03140015
      * POPULATE THE OUTPUT RECORD WITH ONLY THE DATA FOUND ON THE      03150017
      * INPUT RECORD                                                    03160017
      ******************************************************************03170015
       2400-PROCESS-RECORDS.                                            03180017
                                                                        03190015
           INITIALIZE SU163-EXTRACT-RECORD.                             03200015
           MOVE SU153-OTBND-TRIP-ID       TO SU163-OTBND-TRIP-ID.       03210015
           MOVE SU153-SHIPT-UNT-ID        TO SU163-SHIPT-UNIT-ID.       03220015
           MOVE SU153-SCN-LOC-NBR         TO SU163-STR-SCAN-LOC.        03230015
           MOVE SU153-SCN-TYPE-CDE        TO SU163-STR-SCAN-TYPE-CDE.   03240015
           IF SU153-CRTN-SCAN-TMST > SPACES                             03250015
              MOVE SU153-CRTN-SCAN-TMST   TO PV-TIMESTAMP               03260015
              MOVE PV-DATE-FIELD          TO SU163-STR-SCAN-DATE        03270015
              MOVE ':'                    TO PV-TIME-COLON-1            03280015
                                             PV-TIME-COLON-2            03290015
              MOVE PV-TIME-FIELD          TO SU163-STR-SCAN-TIME        03300015
           END-IF.                                                      03310015
                                                                        03320015
      ******************************************************************03330002
      * WRITE THE OUTPUT RECORD                                         03340002
      ******************************************************************03350002
       2500-WRITE-OUTPUT-RECORD.                                        03360015
                                                                        03370002
           WRITE OUTPUT-SML-RECORD FROM SU163-EXTRACT-RECORD.           03380002
                                                                        03390002
           ADD +1              TO PA-OUTPUT-RECS-WROTE.                 03400002
                                                                        03410002
      ******************************************************************03420014
      * RETRIEVE TRAILER DETAIL INFO                                    03430014
      ******************************************************************03440014
       4000-SELECT-TRAILER-INFO.                                        03450014
                                                                        03460014
           EXEC SQL                                                     03470014
              SELECT A.ORGN_OPER_UNT_ID                                 03480014
                   , A.DEST_OPER_UNT_ID                                 03490014
                   , B.TRIP_STAT_CDE                                    03500014
                   , E.E_BUS_IND                                        03510014
                   , C.SCAC_CDE                                         03520014
                   , C.TRLR_ID                                          03530014
                   , C.TRLR_SRL_NBR                                     03540014
                   , B.OTBND_STAT_TMST                                  03550014
               INTO :SUOBTR-ORGN-OPER-UNT-ID                            03560014
                   ,:SUOBTR-DEST-OPER-UNT-ID                            03570014
                   ,:SUOBTS-TRIP-STAT-CDE                               03580014
                   ,:XLT00010-E-BUS-IND                                 03590014
                   ,:CNTRID-SCAC-CDE                                    03600014
                   ,:CNTRID-TRLR-ID                                     03610014
                   ,:CNTRID-TRLR-SRL-NBR                                03620014
                   ,:PV-TRAILER-TMST                                    03630014
                FROM TSUOBTR A                                          03640014
                   , TSUOBTS B                                          03650014
                   , TCNTRID C                                          03660014
                   , XLT_LOC E                                          03670014
              WHERE A.OTBND_TRIP_ID    = :PV-TRIP-ID                    03680014
                AND A.OTBND_TRIP_ID    = B.OTBND_TRIP_ID                03690014
                AND B.OTBND_STAT_TMST IN                                03700014
                    (SELECT MAX(D.OTBND_STAT_TMST)                      03710014
                       FROM TSUOBTS D                                   03720014
                      WHERE D.OTBND_TRIP_ID  =  B.OTBND_TRIP_ID)        03730014
                AND A.TRLR_SRL_NBR     = C.TRLR_SRL_NBR                 03740014
                AND A.DEST_OPER_UNT_ID = E.LOC_NBR                      03750014
           END-EXEC.                                                    03760014
                                                                        03770014
           EVALUATE TRUE                                                03780014
               WHEN SQLCODE = ZERO                                      03790014
               WHEN SQLCODE = +100                                      03800014
                   CONTINUE                                             03810014
                                                                        03820014
               WHEN SQLWARN0 NOT EQUAL SPACE                            03830014
               WHEN SQLCODE NOT EQUAL ZERO                              03840014
                   DISPLAY ' ERROR SHIPT-UNT-ID = '                     03850014
                                                PV-SHIPT-UNT-ID         03860014
                   DISPLAY ' ERROR TRIP-ID      = '                     03870014
                                                PV-TRIP-ID              03880014
                   MOVE '4000-SELECT-TRAILER-INFO'                      03890014
                                             TO PV-PARAGRAPH-NAME       03900014
                   MOVE 'ERROR ON SELECTING TRAILERS'                   03910014
                                             TO PV-DB2-OPERATION        03920014
                   SET ABEND-4002-READ-ERROR TO TRUE                    03930014
                   PERFORM 9200-SQL-ABEND                               03940014
                                                                        03950014
           END-EVALUATE.                                                03960014
                                                                        03970014
      ******************************************************************03980014
      * CLOSE INPUT/OUTPUT FILES AND DISPLAY PROGRAM COUNTS             03990002
      ******************************************************************04000002
       9000-WRAPUP.                                                     04010002
                                                                        04020002
           CLOSE INPUT-ASSUMPT-CRTN-FILE                                04030002
                 OUTPUT-SML-FILE.                                       04040002
                                                                        04050002
           PERFORM 9100-DISPLAY-COUNTS.                                 04060002
                                                                        04070002
      ******************************************************************04080002
      * DISPLAY PROGRAM COUNTS                                          04090002
      ******************************************************************04100002
       9100-DISPLAY-COUNTS.                                             04110002
                                                                        04120002
           MOVE PA-INPUT-RECORDS-READ             TO PV-EDIT-FIELD.     04130002
           DISPLAY ' INPUT RECORDS READ.......: '    PV-EDIT-FIELD.     04140002
                                                                        04150018
           MOVE PA-NO-DETAIL-FOUND                TO PV-EDIT-FIELD.     04160018
           DISPLAY ' RECORDS NO DETAIL FOUND..: '    PV-EDIT-FIELD.     04170018
                                                                        04180002
           MOVE PA-OUTPUT-RECS-WROTE              TO PV-EDIT-FIELD.     04190002
           DISPLAY ' OUTPUT RECORDS WROTE.....: '    PV-EDIT-FIELD.     04200002
                                                                        04210002
           DISPLAY ' '.                                                 04220002
           DISPLAY '*******************************************'.       04230002
           DISPLAY ' '.                                                 04240002
                                                                        04250002
      ******************************************************************04260002
      * ABEND ROUTINE FOR BAD SQL CALLS                                 04270002
      ******************************************************************04280002
       9200-SQL-ABEND.                                                  04290002
                                                                        04300002
           DISPLAY '** ABEND     **'.                                   04310002
           DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME.                    04320002
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04330002
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04340002
           DISPLAY '** SQLCODE   ** = ' SQLCODE.                        04350002
                                                                        04360002
      ***************************************************************** 04370002
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  04380002
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   04390002
      ***************************************************************** 04400002
           COPY DPPD004.                                                04410002
                                                                        04420002
           CALL 'ILBOABN0' USING ABEND-CODE.                            04430002
