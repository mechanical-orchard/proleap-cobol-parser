      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PCKUT800.                                       00050000
       AUTHOR.          SURENDRA BABU PAKALA.                           00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.    JANUARY, 2018.                                  00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM EXTRACTS RECORDS THAT ARE MORE THAN 5 YEARS OLD   *00120000
      * FROM THE  PCT_SUM_WRKD AND PCT_SUM_VRFD TABLES TO BE PURGED.   *00130000
      *                                                                *00140000
      * THIS PROGRAM WILL BE RUN YEARLY.                              * 00150000
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
      ******************************************************************00180000
                                                                        00190000
       CONFIGURATION SECTION.                                           00200000
                                                                        00210000
       SOURCE-COMPUTER. IBM-3090.                                       00220000
       OBJECT-COMPUTER. IBM-3090.                                       00230000
                                                                        00240000
       INPUT-OUTPUT SECTION.                                            00250000
                                                                        00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
           SELECT OUTPUT-PURGE-DATE-FILE ASSIGN TO OUT01.               00290000
           SELECT OUTPUT-WRKD-BKUP-FILE ASSIGN  TO OUT02.               00300000
           SELECT OUTPUT-VRFD-BKUP-FILE ASSIGN  TO OUT03.               00310000
                                                                        00320000
                                                                        00330000
                                                                        00340000
      ******************************************************************00350000
       DATA DIVISION.                                                   00360000
      ******************************************************************00370000
                                                                        00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  OUTPUT-PURGE-DATE-FILE                                       00410000
           LABEL RECORDS ARE STANDARD                                   00420000
           RECORDING MODE IS F                                          00430000
           BLOCK CONTAINS 0 RECORDS                                     00440000
           RECORD CONTAINS 80 CHARACTERS                                00450000
           DATA RECORD IS OUTPUT-PURGE-DATE-RECORD.                     00460000
                                                                        00470000
       01  OUTPUT-PURGE-DATE-RECORD      PIC X(80).                     00480000
                                                                        00490000
       FD  OUTPUT-WRKD-BKUP-FILE                                        00500000
           LABEL RECORDS ARE STANDARD                                   00510000
           RECORDING MODE IS F                                          00511000
           BLOCK CONTAINS 0 RECORDS                                     00512000
           RECORD CONTAINS 80 CHARACTERS                                00513000
           DATA RECORD IS OUTPUT-WRKD-BKUP-RECORD.                      00514000
                                                                        00515000
       01  OUTPUT-WRKD-BKUP-RECORD      PIC X(80).                      00516000
                                                                        00517000
                                                                        00518000
       FD  OUTPUT-VRFD-BKUP-FILE                                        00519000
           LABEL RECORDS ARE STANDARD                                   00520000
           RECORDING MODE IS F                                          00521000
           BLOCK CONTAINS 0 RECORDS                                     00522000
           RECORD CONTAINS 80 CHARACTERS                                00523000
           DATA RECORD IS OUTPUT-VRFD-BKUP-RECORD.                      00524000
                                                                        00525000
       01  OUTPUT-VRFD-BKUP-RECORD      PIC X(80).                      00526000
                                                                        00527000
                                                                        00528000
       WORKING-STORAGE SECTION.                                         00529000
                                                                        00530000
      ******************************************************************00540000
      * PC PROGRAM CONSTANTS                                           *00550000
      ******************************************************************00560000
       01  PC-PROGRAM-CONSTANTS.                                        00570000
           05  PC-PROGRAM-NAME          PIC X(08)    VALUE 'PCKUT800'.  00580000
                                                                        00590000
      ******************************************************************00600000
      * PV PROGRAM VARIABLES                                           *00610000
      ******************************************************************00620000
       01  PV-PROGRAM-VARIABLES.                                        00630000
           05  PV-PARAGRAPH-NAME        PIC X(30)      VALUE SPACES.    00640000
           05  PV-DB2-OPERATION         PIC X(30)      VALUE SPACES.    00650000
           05  PV-CCYYMMDD-DATE         PIC X(10)      VALUE SPACES.    00660000
           05  FILLER REDEFINES PV-CCYYMMDD-DATE.                       00670000
               10  PV-CCYY              PIC X(4).                       00680000
               10  PV-DASH1             PIC X.                          00690000
               10  PV-MM                PIC X(2).                       00700000
               10  PV-DASH2             PIC X.                          00710000
               10  PV-DD                PIC X(2).                       00720000
           05  PV-FUNC-QTY              PIC S9(4) COMP VALUE ZEROES.    00730000
           05  PV-DISPLAY-COUNT         PIC Z(8)9      VALUE ZEROES.    00740000
           05  PV-PURGE-DATE            PIC X(10)      VALUE SPACES.    00750000
           05  PV-SUB                   PIC S9(05)     VALUE ZEROES.    00760000
                                                                        00770000
       01  PV-CURRENT-DATE-DATA.                                        00780000
           05 PV-CURRENT-YEAR              PIC 9(04).                   00790000
           05 PV-CURRENT-MONTH             PIC 9(02).                   00791000
           05 PV-CURRENT-DAY               PIC 9(02).                   00792000
                                                                        00793000
      ******************************************************************00794000
      * PS PROGRAM SWITCHES                                            *00795000
      ******************************************************************00796000
       01  PS-PROGRAM-SWITCHES.                                         00797000
           05  PS-END-OF-PURGE-DATE-CSR-SW       PIC X(01) VALUE 'N'.   00798000
               88  PS-END-OF-PURGE-DATE-CSR                VALUE 'Y'.   00799000
               88  PS-NOT-END-OF-PURGE-DATE-CSR            VALUE 'N'.   00800000
                                                                        00810000
                                                                        00820000
      ******************************************************************00830000
      * PA PROGRAM ACCUMULATORS                                        *00840000
      ******************************************************************00850000
       01  PA-PROGRAM-ACCUMULATORS.                                     00860000
           05  PA-OUTPUT-TOTAL-RECS-WRKD PIC S9(7) COMP-3 VALUE ZEROES. 00870000
           05  PA-OUTPUT-TOTAL-RECS-VRFD PIC S9(7) COMP-3 VALUE ZEROES. 00880000
                                                                        00890000
      ******************************************************************00900000
      * COPY BOOK FOR DRIVER DATE FILE                                  00910000
      ******************************************************************00920000
                                                                        00930000
           COPY PCRD268.                                                00931000
                                                                        00931100
      ******************************************************************00931200
      * COPY BOOK FOR PCT_SUM_WRKD TABLE BACKUP                         00931300
      ******************************************************************00931400
                                                                        00931500
           COPY PCRD269.                                                00931600
                                                                        00931700
      ******************************************************************00931800
      * COPY BOOK FOR PCT_SUM_VRFD TABLE BACKUP                         00931900
      ******************************************************************00932000
                                                                        00932100
           COPY PCRD270.                                                00932200
                                                                        00932300
      ******************************************************************00932400
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *00932500
      ******************************************************************00932600
           COPY DPWS004.                                                00932700
                                                                        00932800
      ******************************************************************00932900
      *    SQL COMMUNICATIONS AREA DCLGEN                              *00933000
      ******************************************************************00934000
           EXEC SQL                                                     00935000
               INCLUDE SQLCA                                            00936000
           END-EXEC.                                                    00937000
                                                                        00938000
           COPY SQLCA2.                                                 00939000
                                                                        00940000
      *---------------------------------------------------------------* 00950000
      *    DB2 AREA FOR TKRPRPM.                                      * 00960000
      *---------------------------------------------------------------* 00970000
           EXEC SQL                                                     00980000
                INCLUDE TKRPRPM                                         00990000
           END-EXEC.                                                    01000000
                                                                        01010000
      *---------------------------------------------------------------* 01020000
      *    DB2 AREA FOR TDTATTR.                                      * 01030000
      *---------------------------------------------------------------* 01040000
           EXEC SQL                                                     01050000
                INCLUDE TDTATTR                                         01060000
           END-EXEC.                                                    01070000
                                                                        01071000
      ******************************************************************01072000
      *    PCT_SUM_WRKD  TABLE                                          01073000
      ******************************************************************01074000
           EXEC SQL                                                     01075000
               INCLUDE PCT00005                                         01076000
           END-EXEC.                                                    01077000
                                                                        01078000
                                                                        01079000
      ******************************************************************01080000
      *    PCT_SUM_VRFD TABLE                                           01090000
      ******************************************************************01100000
           EXEC SQL                                                     01110000
               INCLUDE PCT00006                                         01120000
           END-EXEC.                                                    01130000
                                                                        01140000
                                                                        01150000
      ******************************************************************01160000
      *    CURSOR FOR RETRIEVING ROWS FROM PCT_SUM_WRKD THAT HAVE       01170000
      *    BUS_DTE LESSTHAN THE PURGE DATE                              01180000
      ******************************************************************01190000
                                                                        01200000
           EXEC SQL                                                     01210000
              DECLARE WRKD-PURGE-DATE-CSR CURSOR WITH HOLD FOR          01220000
                SELECT BUS_DTE                                          01230000
                     , OPER_UNT_ID                                      01240000
                     , SHFT_NBR                                         01250000
                     , PRCG_AREA_CDE                                    01260000
                     , UNT_WRKD_QTY                                     01270000
                     , CART_WRKD_QTY                                    01280000
                     , PO_RCVR_WRKD_QTY                                 01290000
                     , EXTD_RTL_WRKD_AMT                                01300000
                  FROM PCT_SUM_WRKD                                     01310000
                 WHERE BUS_DTE < :PV-PURGE-DATE                         01320000
              ORDER BY BUS_DTE                                          01330000
                     , OPER_UNT_ID                                      01340000
                     , SHFT_NBR                                         01350000
                     , PRCG_AREA_CDE                                    01360000
           END-EXEC.                                                    01370000
                                                                        01380000
      ******************************************************************01390000
      *    CURSOR FOR RETRIEVING ROWS FROM PCT_SUM_WRKD THAT HAVE       01400000
      *    BUS_DTE LESSTHAN THE PURGE DATE                              01410000
      ******************************************************************01420000
                                                                        01430000
           EXEC SQL                                                     01440000
              DECLARE VRFD-PURGE-DATE-CSR CURSOR WITH HOLD FOR          01450000
                SELECT BUS_DTE                                          01460000
                     , OPER_UNT_ID                                      01470000
                     , SHFT_NBR                                         01471000
                     , UNT_VRFD_QTY                                     01472000
                     , CART_VRFD_QTY                                    01473000
                     , PO_RCVR_VRFD_QTY                                 01474000
                     , EXTD_RTL_VRFD_AMT                                01475000
                  FROM PCT_SUM_VRFD                                     01476000
                 WHERE BUS_DTE < :PV-PURGE-DATE                         01477000
              ORDER BY BUS_DTE                                          01478000
                     , OPER_UNT_ID                                      01479000
                     , SHFT_NBR                                         01480000
           END-EXEC.                                                    01480100
                                                                        01480200
                                                                        01480300
      ******************************************************************01480400
      * AC ABEND CODES                                                 *01480500
      ******************************************************************01480600
       01  ABEND-CODE                      PIC S9(04) COMP VALUE +0.    01480700
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 01480800
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 01480900
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 01481000
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 01482000
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 01483000
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 01484000
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 01485000
           88  ABEND-4008-DATA-ERROR                       VALUE +4008. 01486000
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009. 01487000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 01488000
           88  ABEND-4019-DATE-ROUTINE-ERROR               VALUE +4019. 01489000
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444. 01490000
                                                                        01500000
                                                                        01510000
                                                                        01520000
      ******************************************************************01530000
       PROCEDURE DIVISION.                                              01540000
      ******************************************************************01550000
                                                                        01560000
       0000-MAINLINE.                                                   01570000
                                                                        01580000
                                                                        01590000
           PERFORM 1000-INITIALIZATION.                                 01600000
                                                                        01610000
           PERFORM 2000-CALCULATE-PURGE-DATE.                           01620000
                                                                        01630000
           MOVE PC268-PURGE-YR-BEG-DATE  TO PV-PURGE-DATE.              01640000
                                                                        01650000
           PERFORM 3000-EXTRACT-WRKD-ROWS.                              01660000
                                                                        01670000
           PERFORM 4000-EXTRACT-VRFD-ROWS.                              01680000
                                                                        01690000
           PERFORM 8000-EOJ-ROUTINE.                                    01700000
                                                                        01710000
           GOBACK.                                                      01720000
                                                                        01730000
                                                                        01740000
      ******************************************************************01750000
      *  - OPEN THE OUTPUT FILE AND GET PURGE OFFSET DATE              *01760000
      ******************************************************************01770000
       1000-INITIALIZATION.                                             01780000
                                                                        01790000
           OPEN OUTPUT OUTPUT-PURGE-DATE-FILE.                          01800000
           OPEN OUTPUT OUTPUT-WRKD-BKUP-FILE.                           01810000
           OPEN OUTPUT OUTPUT-VRFD-BKUP-FILE.                           01820000
                                                                        01830000
                                                                        01840000
                                                                        01850000
      ******************************************************************01860000
      * - CALCULATE THE PURGE DATE                                      01870000
      ******************************************************************01880000
       2000-CALCULATE-PURGE-DATE.                                       01890000
                                                                        01900000
                                                                        01910000
           PERFORM 2100-CALCULATE-PURGE-YEAR.                           01920000
                                                                        01930000
           PERFORM 2200-GET-PUREGE-YR-BEG-DATE.                         01940000
                                                                        01950000
      ******************************************************************01960000
      * CALURE THE PUREG YEAR                                           01970000
      ******************************************************************01980000
       2100-CALCULATE-PURGE-YEAR.                                       01990000
                                                                        02000000
            EXEC SQL                                                    02010000
                 SELECT FUNC_QTY                                        02020000
                   INTO :KRPRPM-FUNC-QTY                                02030000
                 FROM TKRPRPM                                           02040000
                 WHERE LOC_ID            = 90                           02050000
                   AND SYS_PRC_FUNC_CDE  = 'PCSMPG'                     02060000
                   AND INTFC_SYS_CDE     = 'KHL'                        02070000
                   AND FUNC_PRC_STAT_CDE = 'AC'                         02080000
                   AND (CURRENT_DATE BETWEEN DATE(FUNC_BEG_TMST)        02090000
                                         AND DATE(FUNC_END_TMST))       02100000
                 WITH UR                                                02110000
            END-EXEC.                                                   02120000
                                                                        02130000
            EVALUATE TRUE                                               02140000
               WHEN SQLCODE = ZERO                                      02150000
                   CONTINUE                                             02160000
               WHEN OTHER                                               02170000
                   MOVE '2100-CALCULATE-PURGE-YEAR'                     02180000
                                               TO PV-PARAGRAPH-NAME     02190000
                   MOVE 'TKRPRPM'                                       02200000
                                               TO PV-DB2-OPERATION      02210000
                   PERFORM Z900-SQL-ABEND                               02220000
            END-EVALUATE.                                               02230000
                                                                        02240000
                                                                        02250000
                                                                        02260000
            MOVE FUNCTION CURRENT-DATE TO PV-CURRENT-DATE-DATA          02270000
            COMPUTE PC268-PURGE-YEAR = (PV-CURRENT-YEAR -               02280000
                                          KRPRPM-FUNC-QTY).             02290000
      ******************************************************************02300000
      *  PURGE YEARE BEG DATE.                                          02310000
      ******************************************************************02320000
       2200-GET-PUREGE-YR-BEG-DATE.                                     02330000
                                                                        02340000
             MOVE PC268-PURGE-YEAR TO DTATTR-FSCL-YR-NBR                02350000
                                                                        02360000
            EXEC SQL                                                    02370000
                 SELECT FSCL_YR_BEG_DTE                                 02380000
                   INTO :DTATTR-FSCL-YR-BEG-DTE                         02390000
                 FROM TDTATTR                                           02400000
                 WHERE FSCL_YR_NBR   = :DTATTR-FSCL-YR-NBR              02410000
                   AND FSCL_WK_NBR   = '01'                             02420000
                   AND DAY_OF_WK_NBR = '1'                              02430000
                 WITH UR                                                02440000
            END-EXEC.                                                   02450000
                                                                        02460000
            EVALUATE TRUE                                               02470000
               WHEN SQLCODE = ZERO                                      02480000
                   CONTINUE                                             02490000
               WHEN OTHER                                               02500000
                   MOVE '2200-GET-PUREGE-YR-BEG-DATE'                   02510000
                                               TO PV-PARAGRAPH-NAME     02520000
                   MOVE 'TDTATTR'                                       02530000
                                               TO PV-DB2-OPERATION      02540000
                   PERFORM Z900-SQL-ABEND                               02550000
           END-EVALUATE.                                                02551000
                                                                        02552000
                                                                        02553000
                                                                        02554000
              MOVE DTATTR-FSCL-YR-BEG-DTE TO PC268-PURGE-YR-BEG-DATE    02554100
                                                                        02554200
           WRITE OUTPUT-PURGE-DATE-RECORD FROM PC268-RECORD.            02554300
                                                                        02554400
      ******************************************************************02554500
      * - EXTRACT ROWS THAT ARE OLDER THAN THE PURGE DATE              *02554600
      ******************************************************************02554700
       3000-EXTRACT-WRKD-ROWS.                                          02554800
                                                                        02554900
           MOVE ZEROES  TO PA-OUTPUT-TOTAL-RECS-WRKD.                   02555000
                                                                        02556000
           SET PS-NOT-END-OF-PURGE-DATE-CSR TO TRUE.                    02557000
                                                                        02558000
           PERFORM 3100-OPEN-WRKD-PURGE-DATE-CSR.                       02559000
                                                                        02559100
           PERFORM 3200-PROCESS-WRKD-PURGE-DATE                         02559200
               UNTIL PS-END-OF-PURGE-DATE-CSR.                          02559300
                                                                        02559400
           PERFORM 3300-CLOSE-WRKD-PURGE-DATE-CSR.                      02559500
                                                                        02559600
                                                                        02559700
      ******************************************************************02559800
      *     3100-OPEN-WRKD-PURGE-DATE-CSR                              *02559900
      ******************************************************************02560000
       3100-OPEN-WRKD-PURGE-DATE-CSR.                                   02570000
           EXEC SQL                                                     02580000
                OPEN WRKD-PURGE-DATE-CSR                                02590000
           END-EXEC.                                                    02600000
                                                                        02610000
           EVALUATE TRUE                                                02620000
               WHEN SQLCODE = ZERO                                      02630000
                    CONTINUE                                            02640000
               WHEN OTHER                                               02650000
                   MOVE '3100-OPEN-WRKD-PURGE-DATE-CSR'                 02660000
                                               TO PV-PARAGRAPH-NAME     02670000
                   MOVE 'PCT_SUM_WRKD'                                  02680000
                                               TO PV-DB2-OPERATION      02690000
                   PERFORM Z900-SQL-ABEND                               02700000
           END-EVALUATE.                                                02710000
                                                                        02720000
                                                                        02730000
      ******************************************************************02740000
      *     3200-PROCESS-WRKD-PURGE-DATE                               *02750000
      ******************************************************************02760000
       3200-PROCESS-WRKD-PURGE-DATE.                                    02770000
                                                                        02780000
           EXEC SQL                                                     02790000
                FETCH WRKD-PURGE-DATE-CSR                               02800000
                 INTO :PCT00005-BUS-DTE                                 02810000
                    , :PCT00005-OPER-UNT-ID                             02820000
                    , :PCT00005-SHFT-NBR                                02830000
                    , :PCT00005-PRCG-AREA-CDE                           02840000
                    , :PCT00005-UNT-WRKD-QTY                            02850000
                    , :PCT00005-CART-WRKD-QTY                           02860000
                    , :PCT00005-PO-RCVR-WRKD-QTY                        02870000
                    , :PCT00005-EXTD-RTL-WRKD-AMT                       02880000
           END-EXEC.                                                    02890000
                                                                        02900000
           EVALUATE TRUE                                                02910000
               WHEN SQLCODE = ZERO                                      02920000
                   PERFORM 5000-WRITE-WRKD-BKUP-REC                     02930000
               WHEN SQLCODE = +100                                      02940000
                   SET PS-END-OF-PURGE-DATE-CSR TO TRUE                 02950000
               WHEN OTHER                                               02960000
                   MOVE '3200-PROCESS-WRKD-PURGE-DATE'                  02970000
                                               TO PV-PARAGRAPH-NAME     02980000
                   MOVE 'PCT_SUM_WRKD'                                  02990000
                                               TO PV-DB2-OPERATION      03000000
                   PERFORM Z900-SQL-ABEND                               03010000
           END-EVALUATE.                                                03020000
                                                                        03030000
                                                                        03040000
      ******************************************************************03050000
      *     3300-CLOSE-WRKD-PURGE-DATE-CSR                             *03060000
      ******************************************************************03070000
       3300-CLOSE-WRKD-PURGE-DATE-CSR.                                  03080000
                                                                        03090000
           EXEC SQL                                                     03100000
                CLOSE WRKD-PURGE-DATE-CSR                               03110000
           END-EXEC.                                                    03120000
                                                                        03130000
           EVALUATE TRUE                                                03140000
               WHEN SQLCODE = ZERO                                      03150000
                    CONTINUE                                            03160000
               WHEN OTHER                                               03170000
                   MOVE '3300-CLOSE-WRKD-PURGE-DATE-CSR'                03180000
                                               TO PV-PARAGRAPH-NAME     03190000
                   MOVE 'PCT_SUM_WRKD'                                  03200000
                                               TO PV-DB2-OPERATION      03210000
                   PERFORM Z900-SQL-ABEND                               03220000
           END-EVALUATE.                                                03230000
                                                                        03240000
                                                                        03250000
      ******************************************************************03260000
      *     WRITE THE ROWS FETCHED FROM THE DATE AND DEPT CURSORS TO   *03270000
      *     THE OUTPUT EXTRACT.                                        *03280000
      ******************************************************************03290000
       5000-WRITE-WRKD-BKUP-REC.                                        03300000
                                                                        03310000
           MOVE 'DATE'                      TO PC269-PURGE-TYPE.        03320000
           MOVE PV-PURGE-DATE               TO PC269-PURGE-KEY.         03330000
           MOVE PCT00005-BUS-DTE            TO PC269-BUS-DTE.           03340000
           MOVE PCT00005-OPER-UNT-ID        TO PC269-OPER-UNT-ID.       03350000
           MOVE PCT00005-SHFT-NBR           TO PC269-SHFT-NBR.          03360000
           MOVE PCT00005-PRCG-AREA-CDE      TO PC269-PRCG-AREA-CDE.     03370000
           MOVE PCT00005-UNT-WRKD-QTY       TO PC269-UNT-WRKD-QTY.      03380000
           MOVE PCT00005-CART-WRKD-QTY      TO PC269-CART-WRKD-QTY.     03390000
           MOVE PCT00005-PO-RCVR-WRKD-QTY   TO PC269-PO-RCVR-WRKD-QTY.  03400000
           MOVE PCT00005-EXTD-RTL-WRKD-AMT  TO PC269-EXTD-RTL-WRKD-AMT. 03410000
                                                                        03420000
           WRITE OUTPUT-WRKD-BKUP-RECORD                                03430000
               FROM PC269-WRKD-BKUP-RECORD.                             03440000
                                                                        03450000
           ADD +1 TO  PA-OUTPUT-TOTAL-RECS-WRKD.                        03460000
                                                                        03470000
                                                                        03480000
      ******************************************************************03490000
      * - EXTRACT ROWS THAT ARE OLDER THAN THE PURGE DATE              *03500000
      ******************************************************************03510000
       4000-EXTRACT-VRFD-ROWS.                                          03520000
                                                                        03530000
           MOVE ZEROES  TO PA-OUTPUT-TOTAL-RECS-VRFD.                   03540000
                                                                        03550000
           SET PS-NOT-END-OF-PURGE-DATE-CSR TO TRUE.                    03560000
                                                                        03570000
           PERFORM 4100-OPEN-VRFD-PURGE-DATE-CSR.                       03580000
                                                                        03590000
           PERFORM 4200-PROCESS-VRFD-PURGE-DATE                         03600000
               UNTIL PS-END-OF-PURGE-DATE-CSR.                          03610000
                                                                        03620000
           PERFORM 4300-CLOSE-VRFD-PURGE-DATE-CSR.                      03621000
                                                                        03622000
                                                                        03623000
      ******************************************************************03624000
      *     4100-OPEN-WRKD-PURGE-DATE-CSR                              *03625000
      ******************************************************************03626000
       4100-OPEN-VRFD-PURGE-DATE-CSR.                                   03627000
           EXEC SQL                                                     03628000
                OPEN VRFD-PURGE-DATE-CSR                                03629000
           END-EXEC.                                                    03630000
                                                                        03630100
           EVALUATE TRUE                                                03630200
               WHEN SQLCODE = ZERO                                      03630300
                    CONTINUE                                            03630400
               WHEN OTHER                                               03630500
                   MOVE '4100-OPEN-VRFD-PURGE-DATE-CSR'                 03630600
                                               TO PV-PARAGRAPH-NAME     03630700
                   MOVE 'PCT_SUM_VRFD'                                  03630800
                                               TO PV-DB2-OPERATION      03630900
                   PERFORM Z900-SQL-ABEND                               03631000
           END-EVALUATE.                                                03631100
                                                                        03631200
                                                                        03631300
      ******************************************************************03631400
      *     4200-PROCESS-WRKD-PURGE-DATE                               *03631500
      ******************************************************************03631600
       4200-PROCESS-VRFD-PURGE-DATE.                                    03631700
                                                                        03631800
           EXEC SQL                                                     03631900
                FETCH VRFD-PURGE-DATE-CSR                               03632000
                 INTO :PCT00006-BUS-DTE                                 03632100
                    , :PCT00006-OPER-UNT-ID                             03632200
                    , :PCT00006-SHFT-NBR                                03632300
                    , :PCT00006-UNT-VRFD-QTY                            03632400
                    , :PCT00006-CART-VRFD-QTY                           03632500
                    , :PCT00006-PO-RCVR-VRFD-QTY                        03632600
                    , :PCT00006-EXTD-RTL-VRFD-AMT                       03632700
           END-EXEC.                                                    03632800
                                                                        03632900
           EVALUATE TRUE                                                03633000
               WHEN SQLCODE = ZERO                                      03633100
                   PERFORM 6000-WRITE-VRFD-BKUP-REC                     03633200
               WHEN SQLCODE = +100                                      03633300
                   SET PS-END-OF-PURGE-DATE-CSR TO TRUE                 03633400
               WHEN OTHER                                               03633500
                   MOVE '4200-PROCESS-VRFD-PURGE-DATE'                  03633600
                                               TO PV-PARAGRAPH-NAME     03633700
                   MOVE 'PCT_SUM_VRFD'                                  03633800
                                               TO PV-DB2-OPERATION      03633900
                   PERFORM Z900-SQL-ABEND                               03634000
           END-EVALUATE.                                                03634100
                                                                        03634200
                                                                        03634300
      ******************************************************************03634400
      *     4300-CLOSE-WRKD-PURGE-DATE-CSR                             *03634500
      ******************************************************************03634600
       4300-CLOSE-VRFD-PURGE-DATE-CSR.                                  03634700
                                                                        03634800
           EXEC SQL                                                     03634900
                CLOSE VRFD-PURGE-DATE-CSR                               03635000
           END-EXEC.                                                    03635100
                                                                        03635200
           EVALUATE TRUE                                                03635300
               WHEN SQLCODE = ZERO                                      03635400
                    CONTINUE                                            03635500
               WHEN OTHER                                               03635600
                   MOVE '4300-CLOSE-VRFD-PURGE-DATE-CSR'                03635700
                                               TO PV-PARAGRAPH-NAME     03635800
                   MOVE 'PCT_SUM_VRFD'                                  03635900
                                               TO PV-DB2-OPERATION      03636000
                   PERFORM Z900-SQL-ABEND                               03636100
           END-EVALUATE.                                                03636200
                                                                        03636300
                                                                        03636400
      ******************************************************************03636500
      *     WRITE THE ROWS FETCHED FROM THE DATE AND DEPT CURSORS TO   *03636600
      *     THE OUTPUT EXTRACT.                                        *03636700
      ******************************************************************03636800
       6000-WRITE-VRFD-BKUP-REC.                                        03636900
                                                                        03637000
           MOVE 'DATE'                      TO PC270-PURGE-TYPE.        03637100
           MOVE PV-PURGE-DATE               TO PC270-PURGE-KEY.         03637200
           MOVE PCT00006-BUS-DTE            TO PC270-BUS-DTE.           03637300
           MOVE PCT00006-OPER-UNT-ID        TO PC270-OPER-UNT-ID.       03637400
           MOVE PCT00006-SHFT-NBR           TO PC270-SHFT-NBR.          03637500
           MOVE PCT00006-UNT-VRFD-QTY       TO PC270-UNT-VRFD-QTY.      03637600
           MOVE PCT00006-CART-VRFD-QTY      TO PC270-CART-VRFD-QTY.     03637700
           MOVE PCT00006-PO-RCVR-VRFD-QTY   TO PC270-PO-RCVR-VRFD-QTY.  03637800
           MOVE PCT00006-EXTD-RTL-VRFD-AMT  TO PC270-EXTD-RTL-VRFD-AMT. 03637900
                                                                        03638000
           WRITE OUTPUT-VRFD-BKUP-RECORD                                03638100
               FROM PC270-VRFD-BKUP-RECORD.                             03638200
                                                                        03638300
           ADD +1 TO  PA-OUTPUT-TOTAL-RECS-VRFD.                        03638400
                                                                        03638500
                                                                        03638600
      ******************************************************************03638700
      * - CLOSE THE INPUT FILE.                                        *03638800
      * - DISPLAY THE NUMBER OF INPUT RECORDS READ AND ROWS DELETED.   *03638900
      ******************************************************************03639000
       8000-EOJ-ROUTINE.                                                03640000
                                                                        03650000
           CLOSE OUTPUT-WRKD-BKUP-FILE.                                 03660000
           CLOSE OUTPUT-VRFD-BKUP-FILE.                                 03670000
           CLOSE OUTPUT-PURGE-DATE-FILE.                                03680000
                                                                        03690000
           DISPLAY '** ' PC-PROGRAM-NAME '**'.                          03700000
           DISPLAY 'PURGE DATE  : ' PV-PURGE-DATE.                      03710000
           MOVE PA-OUTPUT-TOTAL-RECS-WRKD TO PV-DISPLAY-COUNT           03720000
           DISPLAY 'PCT_SUM_WRKD - TOTAL NO OF BACKUP RECORDS : '       03730000
                                          PV-DISPLAY-COUNT.             03730100
           MOVE PA-OUTPUT-TOTAL-RECS-VRFD TO PV-DISPLAY-COUNT           03730200
           DISPLAY 'PCT_SUM_VRFD - TOTAL NO OF BACKUP RECORDS : '       03730300
                                          PV-DISPLAY-COUNT.             03730400
                                                                        03730500
                                                                        03730600
      ******************************************************************03730700
      * 9200-NON-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE  03730800
      * EVENT THAT A NON-SQL ERROR OCCURS.                              03730900
      ******************************************************************03731000
       9200-NON-SQL-ABEND.                                              03732000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03733000
                                                                        03734000
                                                                        03735000
      ******************************************************************03736000
      *  PERFORMS ABEND PROCESSING FOR SQL ERRORS.                     *03737000
      *  INITIALLY, THIS PARAGRAPH WILL NOT BE PERFORMED               *03738000
      ******************************************************************03739000
       Z900-SQL-ABEND.                                                  03740000
                                                                        03750000
           DISPLAY 'Z900-SQL-ABEND'.                                    03760000
           DISPLAY '**  ABEND     **'.                                  03770000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              03780000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            03790000
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION.             03800000
           DISPLAY '**  SQLCODE   **  = ' SQLCODE.                      03810000
                                                                        03820000
      ******************************************************************03830000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *03840000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *03850000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *03860000
      * FORMAT.                                                        *03870000
      ******************************************************************03880000
           COPY DPPD004.                                                03890000
                                                                        03900000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03910000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03920000
