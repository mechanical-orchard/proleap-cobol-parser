       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT135.                                       
       AUTHOR.                  TIA JOHNSTON.                                   
       DATE-WRITTEN.            07/02/01.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WILL COMPARE MULTIPLE TRADING PARTNERS USING THE  *        
      * SAME ID ON TTRDPRT VERSUS TTRDMLT, WRITE OUT A MESSAGE TO      *        
      * SYSOUT AND SET A RETURN CODE FOR ANY DISCREPANCIES.            *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-CSR-SW                PIC X(01)   VALUE 'N'.              
               88  NOT-END-OF-CSR                       VALUE 'N'.              
               88  END-OF-CSR                           VALUE 'Y'.              
           05  ERRORS-FOUND-SW              PIC X(01)   VALUE 'N'.              
               88  ERRORS-FOUND                         VALUE 'Y'.              
                                                                                
       01  PV-PARAGRAPH-NAME                PIC X(30).                          
       01  PV-DB2-OPER-ATTEMPTED            PIC X(30).                          
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TTRDPRT CURSOR                                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE TTRDPRT_CSR CURSOR FOR                                     
               SELECT DISTINCT A.VEND_INCHG_ID_DESC,                            
                               A.VEND_INCHG_ID                                  
               FROM   TTRDPRT A,                                                
                      TTRDPRT B                                                 
               WHERE  A.TP_DKEY           <> B.TP_DKEY                          
                 AND  A.VEND_INCHG_ID      = B.VEND_INCHG_ID                    
                 AND  A.KOHL_INCHG_ID      = B.KOHL_INCHG_ID                    
                 AND  A.VEND_INCHG_ID_DESC = B.VEND_INCHG_ID_DESC               
                 AND  A.KOHL_INCHG_ID_DESC = B.KOHL_INCHG_ID_DESC               
                 AND  A.INACTV_DTE         = '9999-09-09'                       
                 AND  B.INACTV_DTE         = '9999-09-09'                       
            ORDER BY  A.VEND_INCHG_ID                                           
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *    TTRDMLT CURSOR                                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE TTRDMLT_CSR CURSOR FOR                                     
               SELECT DISTINCT VND_INCHG_ID_DESC,                               
                               VND_INCHG_ID                                     
               FROM   TTRDMLT                                                   
            ORDER BY  VND_INCHG_ID                                              
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           SET NOT-END-OF-CSR TO TRUE.                                          
                                                                                
           PERFORM 3000-OPEN-TTRDPRT-CSR                                        
              THRU 3000-OPEN-TTRDPRT-CSR-EXIT.                                  
                                                                                
           PERFORM 3100-FETCH-TTRDPRT-CSR                                       
              THRU 3100-FETCH-TTRDPRT-CSR-EXIT.                                 
                                                                                
           IF SQLCODE = +0                                                      
               PERFORM 1000-PROCESS-TTRDPRT-CSR                                 
                  THRU 1000-PROCESS-TTRDPRT-CSR-EXIT                            
                 UNTIL END-OF-CSR                                               
           ELSE                                                                 
               DISPLAY 'TTRDPRT CURSOR IS EMPTY'                                
           END-IF.                                                              
                                                                                
           PERFORM 3200-CLOSE-TTRDPRT-CSR                                       
              THRU 3200-CLOSE-TTRDPRT-CSR-EXIT.                                 
                                                                                
           SET NOT-END-OF-CSR TO TRUE.                                          
                                                                                
           PERFORM 4000-OPEN-TTRDMLT-CSR                                        
              THRU 4000-OPEN-TTRDMLT-CSR-EXIT.                                  
                                                                                
           PERFORM 4100-FETCH-TTRDMLT-CSR                                       
              THRU 4100-FETCH-TTRDMLT-CSR-EXIT.                                 
                                                                                
           IF SQLCODE = +0                                                      
               PERFORM 2000-PROCESS-TTRDMLT-CSR                                 
                  THRU 2000-PROCESS-TTRDMLT-CSR-EXIT                            
                 UNTIL END-OF-CSR                                               
           ELSE                                                                 
               DISPLAY 'TTRDMLT CURSOR IS EMPTY'                                
           END-IF.                                                              
                                                                                
           PERFORM 4200-CLOSE-TTRDMLT-CSR                                       
              THRU 4200-CLOSE-TTRDMLT-CSR-EXIT.                                 
                                                                                
           IF ERRORS-FOUND                                                      
               MOVE 4095 TO RETURN-CODE                                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-TTRDPRT-CSR.                                                
                                                                                
           PERFORM 3300-SELECT-TTRDMLT                                          
              THRU 3300-SELECT-TTRDMLT-EXIT.                                    
                                                                                
           PERFORM 3100-FETCH-TTRDPRT-CSR                                       
              THRU 3100-FETCH-TTRDPRT-CSR-EXIT.                                 
                                                                                
       1000-PROCESS-TTRDPRT-CSR-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-TTRDMLT-CSR.                                                
                                                                                
           PERFORM 4300-SELECT-TTRDPRT                                          
              THRU 4300-SELECT-TTRDPRT-EXIT.                                    
                                                                                
           PERFORM 4100-FETCH-TTRDMLT-CSR                                       
              THRU 4100-FETCH-TTRDMLT-CSR-EXIT.                                 
                                                                                
       2000-PROCESS-TTRDMLT-CSR-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    OPEN TTRDPRT CURSOR                                                  
      *****************************************************************         
       3000-OPEN-TTRDPRT-CSR.                                                   
                                                                                
           MOVE '3000-'              TO PV-PARAGRAPH-NAME.                      
           MOVE 'OPEN TTRDPRT CSR'   TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN TTRDPRT_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       3000-OPEN-TTRDPRT-CSR-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    FETCH TTRDPRT CURSOR                                                 
      *****************************************************************         
       3100-FETCH-TTRDPRT-CSR.                                                  
                                                                                
           MOVE '3100-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'FETCH TTRDPRT CSR' TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
             FETCH TTRDPRT_CSR                                                  
             INTO  :TRDPRT-VEND-INCHG-ID-DESC,                                  
                   :TRDPRT-VEND-INCHG-ID                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET END-OF-CSR TO TRUE                                       
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       3100-FETCH-TTRDPRT-CSR-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    CLOSE TTRDPRT CURSOR                                                 
      *****************************************************************         
       3200-CLOSE-TTRDPRT-CSR.                                                  
                                                                                
           MOVE '3200-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'CLOSE TTRDPRT CSR' TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
              CLOSE TTRDPRT_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       3200-CLOSE-TTRDPRT-CSR-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SELECT TTRDMLT                                                       
      *****************************************************************         
       3300-SELECT-TTRDMLT.                                                     
                                                                                
           MOVE '3300-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'SELECT TTRDMLT'    TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
             SELECT VND_INCHG_ID                                                
             INTO   :TRDMLT-VND-INCHG-ID                                        
             FROM   TTRDMLT                                                     
             WHERE  VND_INCHG_ID      = :TRDPRT-VEND-INCHG-ID                   
             AND    VND_INCHG_ID_DESC = :TRDPRT-VEND-INCHG-ID-DESC              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   DISPLAY 'ID NOT FOUND ON TRDMLT: '                           
                            TRDPRT-VEND-INCHG-ID                                
                   SET ERRORS-FOUND TO TRUE                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       3300-SELECT-TTRDMLT-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    OPEN TTRDMLT CURSOR                                                  
      *****************************************************************         
       4000-OPEN-TTRDMLT-CSR.                                                   
                                                                                
           MOVE '4000-'              TO PV-PARAGRAPH-NAME.                      
           MOVE 'OPEN TTRDMLT CSR'   TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN TTRDMLT_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       4000-OPEN-TTRDMLT-CSR-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    FETCH TTRDMLT CURSOR                                                 
      *****************************************************************         
       4100-FETCH-TTRDMLT-CSR.                                                  
                                                                                
           MOVE '4100-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'FETCH TTRDMLT CSR' TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
             FETCH TTRDMLT_CSR                                                  
             INTO  :TRDMLT-VND-INCHG-ID-DESC,                                   
                   :TRDMLT-VND-INCHG-ID                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET END-OF-CSR TO TRUE                                       
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       4100-FETCH-TTRDMLT-CSR-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    CLOSE TTRDMLT CURSOR                                                 
      *****************************************************************         
       4200-CLOSE-TTRDMLT-CSR.                                                  
                                                                                
           MOVE '4200-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'CLOSE TTRDMLT CSR' TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
              CLOSE TTRDMLT_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       4200-CLOSE-TTRDMLT-CSR-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SELECT TTRDPRT                                                       
      *****************************************************************         
       4300-SELECT-TTRDPRT.                                                     
                                                                                
           MOVE '4300-'             TO PV-PARAGRAPH-NAME.                       
           MOVE 'SELECT TTRDPRT'    TO PV-DB2-OPER-ATTEMPTED.                   
                                                                                
           EXEC SQL                                                             
             SELECT DISTINCT A.VEND_INCHG_ID                                    
             INTO   :TRDPRT-VEND-INCHG-ID                                       
             FROM   TTRDPRT A,                                                  
                    TTRDPRT B                                                   
             WHERE  A.VEND_INCHG_ID      = :TRDMLT-VND-INCHG-ID                 
             AND    A.VEND_INCHG_ID_DESC = :TRDMLT-VND-INCHG-ID-DESC            
             AND    B.VEND_INCHG_ID      = :TRDMLT-VND-INCHG-ID                 
             AND    B.VEND_INCHG_ID_DESC = :TRDMLT-VND-INCHG-ID-DESC            
             AND    A.TP_DKEY           <> B.TP_DKEY                            
             AND    A.INACTV_DTE         = '9999-09-09'                         
             AND    B.INACTV_DTE         = '9999-09-09'                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = -811                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   DISPLAY 'ID NOT FOUND ON TRDPRT: '                           
                            TRDMLT-VND-INCHG-ID                                 
                   SET ERRORS-FOUND TO TRUE                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       4300-SELECT-TTRDPRT-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       9000-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
