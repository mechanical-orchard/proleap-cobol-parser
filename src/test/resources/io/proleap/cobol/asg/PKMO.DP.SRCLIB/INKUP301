       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKUP301.                                                 
       AUTHOR.        JEFF HARTIG/COMPUWARE.                                    
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  1998-06-10.                                               
       DATE-COMPILED.                                                           
                                                                                
W34517******************************************************************        
W34517*                                                                         
W34517*               !!!  I M P O R T A N T  !!!                               
W34517*                                                                         
W34517*                                                                         
W34517*    !!!  THIS PROGRAM MUST BE KEPT IN SYNC WITH VAKUP301  !!!            
W34517*                                                                         
W34517*                                                                         
W34517*                                                                         
W34517* INKUP301 - BV INVENTORY LEDGER UPDATE PROGRAM UPDATES                   
W34517* THE NEWLY CREATED TABLE (INT_SUB_CL_INV_LDGR CLONE OF TINSCLS).         
W34517* THIS PROGRAM PERFORMS UPDATES FOR THE INVENTORY LEDGER                  
W34517* FINANCIALS. ANY CHANGES MADE TO THIS PROGRAM MUST ALSO                  
W34517* BE MADE TO THE VA VERSION OF THIS PROGRAM                               
W34517* (VAKUP301 - TINSCLS).                                                   
W34517*                                                                         
W34517*                                                                         
W34517* CHANGE MADE: REPLACING REFERENCES TO (TINSCLS) WITH THE NEW             
W34517*              TABLE (INT_SUB_CL_INV_LDGR).                               
W34517*     WR34517  THERESE RAMSEYER   INSTALL DATE: 10/14/2008                
W34517*                                                                         
      ******************************************************************        
      *           INKUP301 - MONTHLY SUB-CLASS STORE INVENTORY         *00100000
      *                      INVENTORY - LEDGER TABLE UPDATE (TINSCLS) *00110027
W34517*                      WAS UPDATING (TINSCLS), NOW UPDATING      *00110127
W34517*                      (INT_SUB_CL_INV_LDGR).                    *00110127
      ******************** HISTORY OF CHANGES **************************00111027
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00113027
      * -------  ----------  ----------------------------------------   00114027
      * 21591    03-01-2002  MANIFEST PROJECT CHANGE FOR IN-TRANSIT     00119027
      *                      REVERSAL FROM INVENTORY LEDGER             00119127
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W27808* W27808  05-24-2005 S. JACKSON  INCORPORATE INSCLS-STR-EXPTED            
W28582* W28582  12-21-2005 CLARK SCHWENN INCORPORATE VENDOR BRAND               
W34517* W34517  10-14-2008 THERESE RAMSEYER - SUBSTITUTE THE NEW TABLE          
W34517*                    (INT_SUB_CL_INV_LDGR) FOR (TINSCLS).                 
W33304* W33304  10-14-2008 INFOSYS HILO CHANGES.INTRODUCE NEW TABLES            
W33304*                    /JIMMY  TINVPAR & TINCNTL AND JOIN THEM TO           
W33304*                            BRING IN THE CORRECT INV POSTING             
W33304*                            MONTH END DATE.                              
      ******************************************************************00120000
                                                                        00130000
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
                                                                        00211000
           SELECT CONDENSED-FILE                                        00220000
               ASSIGN TO INP01.                                         00230000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CONDENSED-FILE                                               00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
W20445 01  CONDENSED-RECORD            PIC X(120).                      00330026
                                                                        00340000
       WORKING-STORAGE SECTION.                                         00370000
                                                                        00380000
       01  FILLER                      PIC X(28)   VALUE                00381000
                'BEGINNING OF WORKING STORAGE'.                         00382000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00383000
                                                                        00384000
       01  PC-PROGRAM-CONSTANTS.                                        00385000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'INK215'.    00386000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUP301'.  00387000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00389000
                                                                        00390000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00400000
           05  PE-MSG-01                       PIC X(50) VALUE          00410000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00420000
           05  PE-MSG-02                       PIC X(50) VALUE          00430000
W34517          'ATTEMPT TO UPDATE INT_SUB_CL_INV_LDGR FAILED     '.    00440000
           05  PE-MSG-03                       PIC X(50) VALUE          00450000
W34517          'ATTEMPT TO INSERT INT_SUB_CL_INV_LDGR FAILED     '.    00460000
           05  PE-MSG-04                       PIC X(50) VALUE          00470000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00480000
           05  PE-MSG-05                       PIC X(50) VALUE          00490000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00500000
           05  PE-MSG-06                       PIC X(50) VALUE          00510000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00520000
           05  PE-MSG-07                       PIC X(50) VALUE          00530000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00540000
W33304     05  PE-MSG-08                       PIC X(50) VALUE          00530000
W33304          'SELECT ON JOIN OF TINVPAR AND TINCNTL FAILED'.         00540000
                                                                        00550000
       01  PS-PROGRAM-SWITCHES.                                         00560000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00570000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00580000
           05  PS-INT00007-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00590000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00600000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00610000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00620000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00630000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00640000
W33304     05  PS-SKIP-RECORD-SW            PIC X(01)     VALUE 'N'.    00630000
W33304         88  PS-SKIP-RECORD-YES                     VALUE 'Y'.    00640000
W33304         88  PS-SKIP-RECORD-NO                      VALUE 'N'.    00640000
                                                                        00650000
       01  PI-PROGRAM-INDICATORS.                                       00660000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00670000
               88  PI-UPDATE-INT00007-ROW                 VALUE 'U'.    00680000
               88  PI-INSERT-INT00007-ROW                 VALUE 'I'.    00690000
                                                                        00700000
       01  PROGRAM-VARIABLES.                                           00710000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00720000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00730000
                                                                        00740000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00750000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00760000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00770000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00780000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 00790000
W20445     05  PV-EDIT-SUB-CLASS               PIC 9(03) VALUE ZEROS.           
W20445     05  PV-EDIT-SUB-CLASS-RED REDEFINES                                  
W20445         PV-EDIT-SUB-CLASS.                                               
W20445         10  PV-EDIT-CLASS-P1            PIC X(01).                       
W20445         10  PV-EDIT-CLASS-P2            PIC X(02).                       
W20445     05  PV-EDIT-DEPT-NBR                PIC 9(03) VALUE ZEROS.           
W20445     05  PV-EDIT-DEPT-NBR-RED REDEFINES                                   
W20445         PV-EDIT-DEPT-NBR                PIC X(03).                       
                                                                        00800000
       01  PA-PROGRAM-ACCUMULATORS.                                     00810000
           05  PA-RECORD-COUNTS.                                        00820000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00830000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00840000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00850000
W33304         10  PA-SKIP-COUNT         PIC  9(09)  VALUE ZEROES.      00850000
           05  PA-ACCUMULATORS.                                         00850100
               10 PA-SLS-RTL-AMT-IN      PIC S9(13)V99 VALUE 0.         00850200
               10 PA-SLS-RTL-AMT-TOT     PIC S9(13)V99 VALUE 0.         00850300
               10 PA-MKDN-RTL-AMT-IN     PIC S9(13)V99 VALUE 0.         00850400
               10 PA-MKDN-RTL-AMT-TOT    PIC S9(13)V99 VALUE 0.         00850500
               10 PA-MKUP-RTL-AMT-IN     PIC S9(13)V99 VALUE 0.         00850600
               10 PA-MKUP-RTL-AMT-TOT    PIC S9(13)V99 VALUE 0.         00850700
               10 PA-XFER-IN-AMT-IN      PIC S9(13)V99 VALUE 0.         00850800
               10 PA-XFER-IN-AMT-TOT     PIC S9(13)V99 VALUE 0.         00850900
               10 PA-XFER-OUT-AMT-IN     PIC S9(13)V99 VALUE 0.         00851000
               10 PA-XFER-OUT-AMT-TOT    PIC S9(13)V99 VALUE 0.         00851100
               10 PA-RCPT-RTL-AMT-IN     PIC S9(13)V99 VALUE 0.         00851200
               10 PA-RCPT-RTL-AMT-TOT    PIC S9(13)V99 VALUE 0.         00851300
               10 PA-PADJ-RTL-AMT-IN     PIC S9(13)V99 VALUE 0.         00851400
               10 PA-PADJ-RTL-AMT-TOT    PIC S9(13)V99 VALUE 0.         00851500
OCFSFS         10 PA-STR-PARL-SHIP-RTL-AMT-IN PIC S9(13)V99 VALUE 0.    00851400
OCFSFS         10 PA-STR-PARL-SHIP-RTL-AMT-TOT PIC S9(13)V99 VALUE 0.   00851500
W27808         10 PA-STR-EXPTED-RTL-IN     PIC S9(13)V99 VALUE 0.       00851400
W27808         10 PA-STR-EXPTED-RTL-TOT    PIC S9(13)V99 VALUE 0.       00851500
                                                                        00880000
       01  PD-PROGRAM-DISPLAYS.                                         00890000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00900000
           05  PD-DISPLAY-TOTAL          PIC  Z,ZZZ,ZZZ,ZZZ,ZZ9.99-.    00901000
       01  SAVE-AREA.                                                   00910000
           05  SV-PRIMARY-KEY.                                          00920000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACE.        00930000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACE.        00940000
               10  SV-DEPT-NBR           PIC S9(04)V COMP-3 VALUE ZEROS.00950000
               10  SV-SUB-CL-NBR         PIC S9(04)V COMP-3 VALUE ZEROS.00960000
               10  SV-STR-NBR            PIC S9(04) COMP VALUE 0.       00961000
W28582         10  SV-ML-LWST-ID         PIC  S9(9) COMP VALUE 0.       00961000
           05  SV-UPDATE-FIELDS.                                        00970000
               10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.00980000
               10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.00981000
               10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.00982000
               10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.00983000
               10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.00984000
               10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.00990000
               10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01000000
OCFSFS         10  SV-STR-PARL-SHIP-RTL-AMT                             01000000
OCFSFS                                   PIC S9(11)V99  COMP-3 VALUE +0.        
W27808         10  SV-STR-EXPTED-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01000000
                                                                        01080000
      ******************************************************************01090000
      * INRD012 - INVENTORY MONTHLY DATABASE UPDATE TRANSACTION LAYOUT  01100000
      ******************************************************************01110000
W27808     COPY INRD012.                                                01120000
                                                                        01130000
      ******************************************************************01140000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01150000
      ******************************************************************01160000
           COPY DPWS004.                                                01170000
                                                                        01180000
      ******************************************************************01190000
      *  USED FOR DB2 ERRORS                                            01200000
      ******************************************************************01210000
           EXEC SQL                                                     01220000
               INCLUDE SQLCA                                            01230000
           END-EXEC.                                                    01240000
                                                                        01250000
      ******************************************************************01260000
      *  INVENTORY MONTH-TO-DATE SUB-CLASS STORE TABLE                  01270000
      ******************************************************************01280000
W34517*    EXEC SQL                                                     01290000
W20445*W34517  INCLUDE TINSCLS                                          01300000
W34517*    END-EXEC.                                                    01310000
                                                                        01320000
W34517******************************************************************01260000
W34517*  INVENTORY MONTH-TO-DATE SUB-CLASS STORE TABLE                  01270000
W34517*  (REPLICATED FOR BV / VA SPLIT  INT_SUB_CL_INV_LDGR)            01270000
W34517******************************************************************01280000
W34517     EXEC SQL                                                     01290000
W34517         INCLUDE INT00007                                         01300000
W34517     END-EXEC.                                                    01310000
W34517                                                                  01320000
      ***************************************************************** 01330000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01340000
      ***************************************************************** 01350000
           EXEC SQL                                                     01360000
               INCLUDE TCKRSTCN                                         01370000
           END-EXEC.                                                    01380000
                                                                        01390000
           EXEC SQL                                                     01400000
               INCLUDE TCKRSTIN                                         01410000
           END-EXEC.                                                    01420000
                                                                                
W33304*    INCLUDE CONTROL TABLES SPECIFIC TO INVENTORY LEDGER          01390000
W33304*                                                                 01390000
W33304     EXEC SQL                                                     01400000
W33304         INCLUDE TINVPAR                                          01410000
W33304     END-EXEC.                                                    01420000
W33304                                                                  01420000
W33304     EXEC SQL                                                     01400000
W33304         INCLUDE TINCNTL                                          01410000
W33304     END-EXEC.                                                    01420000
                                                                        01430000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01470000
W20445     05  CR-AREA-LEN                  PIC S9(04) VALUE +36  COMP. 01480000
           05  CR-CHECKPOINT-RESTART-VAR.                               01490000
W20445*        10  CR-PREV-DTL-KEY          PIC  X(25) VALUE SPACES.    01500000
W27808         10  CR-PREV-DTL-KEY          PIC  X(24) VALUE SPACES.    01500000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01510000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01520000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01530000
W20445             15  CR-DEPT-NBR          PIC  S9(04)V COMP-3.        01540000
W20445             15  CR-SUB-CL-NBR        PIC  S9(04)V COMP-3.        01550000
                   15  CR-STR-NBR           PIC  S9(04) COMP.           01551000
W28582             15  CR-ML-LWST-ID        PIC  S9(9) COMP.            01551000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01560000
                                                                        01570000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01580000
W20445     05  CK-SAVE-PREV-KEY         PIC  X(25) VALUE SPACES.        01590000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01600000
               10  CK-FISCAL-NBR        PIC  X(02).                     01610000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01620000
W20445         10  CK-DEPT-NBR          PIC  S9(04)V COMP-3.            01630000
W20445         10  CK-SUB-CL-NBR        PIC  S9(04)V COMP-3.            01641000
               10  CK-STR-NBR           PIC  S9(04) COMP.               01642000
W28582         10  CK-ML-LWST-ID        PIC  S9(09) COMP.               01642000
                                                                        01650000
      *-------------------*                                             01660000
       PROCEDURE DIVISION.                                              01670000
      *-------------------*                                             01680000
                                                                        01690000
       0000-INKUP301.                                                   01700000
                                                                        01710000
           PERFORM 1000-INITIALIZATION.                                 01720000
                                                                        01730000
           PERFORM 2000-PROCESS-INPUT                                   01740000
                UNTIL PS-EOF-CONDENSED-FILE.                            01750000
                                                                        01760000
           PERFORM 8000-EOJ-ROUTINE.                                    01770000
                                                                        01780000
           STOP RUN.                                                    01790000
                                                                        01800000
      ******************************************************************01810000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01820000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      01830000
      ******************************************************************01840000
                                                                        01850000
       1000-INITIALIZATION.                                             01860000
                                                                        01870000
           OPEN INPUT CONDENSED-FILE.                                   01880000
                                                                        01890000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         01900000
                                                                        01910000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              01920000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01930000
                PERFORM 7500-SELECT-RESTART-INFO                        01940000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   01950000
                                             CR-CHECKPOINT-RESTART-VAR  01960000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       01970000
           ELSE                                                         01980000
               SET PS-FIRST-COMMIT TO TRUE                              01990000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02000000
           END-IF.                                                      02010000
                                                                        02020000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02030000
              (PA-INPUT-COUNT > CR-INPUT-READ-COUNT)                    02040000
               OR                                                       02050000
               PS-EOF-CONDENSED-FILE.                                   02051000
                                                                        02060000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02061000
               DISPLAY 'FISCAL MONTH NBR      ' IN012-FSCL-MN-NBR       02062000
               DISPLAY 'FISCAL MONTH END DATE ' IN012-FSCL-MN-END-DTE   02063000
               DISPLAY 'DEPARTMENT NBR        ' IN012-DEPT-NBR          02064000
               DISPLAY 'SUB CLASS NBR         ' IN012-SUB-CL-NBR        02065000
               DISPLAY 'STORE NBR             ' IN012-STR-NBR           02066000
               DISPLAY 'BRAND VENDOR KEY      ' IN012-ML-LWST-ID        02066000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02067000
                        PA-INPUT-COUNT                                  02068000
           END-IF.                                                      02069000
                                                                        02069100
           IF PS-EOF-CONDENSED-FILE                                     02070000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02071000
                   CONTINUE                                             02072000
               ELSE                                                     02073000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02080000
           ELSE                                                         02090000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02100000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02110000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02120000
           END-IF.                                                      02130000
                                                                        02140000
W33304******************************************************************02150000
W33304* SELECT INV DATE FROM INV CONTROL TABLES                         02160000
W33304******************************************************************02170000
W33304 1100-SELECT-INV-DATE.                                            02180000
W33304                                                                  02190000
W33304     EXEC SQL                                                     99000   
W33304       SELECT B.OPN_FSCL_MN_DTE                                   99100   
W33304             ,B.OPN_FSCL_MN_NBR                                           
W33304         INTO :INCNTL-OPN-FSCL-MN-DTE                                     
W33304             ,:INCNTL-OPN-FSCL-MN-NBR                                     
W33304         FROM TINVPAR A                                           99100   
W33304             ,TINCNTL B                                           99100   
W33304        WHERE B.ACTV_IND = 'Y'                                    99100   
W33304          AND A.INV_ID = B.INV_ID                                         
W33304          AND A.LOC_INV_STAT_CDE = 'IN'                                   
W33304          AND A.LOC_NBR = :IN012-STR-NBR                                  
W33304          AND A.ACTL_FIN_BK_DTE = '9999-09-09'                            
W33304     END-EXEC                                                     99500   
W33304                                                                  99500   
W33304     EVALUATE SQLCODE                                             99700   
W33304         WHEN 0                                                   99700   
W33304             CONTINUE                                             99800   
W33304         WHEN +100                                                99700   
W33304             SET PS-SKIP-RECORD-YES TO TRUE                       99800   
W33304         WHEN OTHER                                               99900   
W33304             MOVE PE-MSG-08         TO PV-OPERATION-ATTEMPTED     00000   
W33304             MOVE '1100-SELECT-INV-DATE'                                  
W33304                                    TO PV-PARAGRAPH-NAME                  
W33304             PERFORM 9999-SQL-ABEND                               00300   
W33304     END-EVALUATE.                                                00400   
W33304                                                                          
      ******************************************************************02150000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02160000
      ******************************************************************02170000
       2000-PROCESS-INPUT.                                              02180000
                                                                        02190000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02200000
                                                                        02210000
W33304     PERFORM 1100-SELECT-INV-DATE.                                        
W33304                                                                          
W33304     IF PS-SKIP-RECORD-YES                                                
W33304        ADD 1 TO PA-SKIP-COUNT                                            
W33304     ELSE                                                                 
W33304        PERFORM 7000-SELECT-DB-ROW                                   02220
                                                                           02230
W33304        IF PS-PROGRAM-DEADLOCKED                                     02231
W33304            CONTINUE                                                 02232
W33304        ELSE                                                         02233
W33304            EVALUATE TRUE                                            02240
W33304                WHEN PI-UPDATE-INT00007-ROW                          02250
W33304                    PERFORM 7100-UPDATE-INT00007-ROW                 02260
W33304                WHEN PI-INSERT-INT00007-ROW                          02270
W33304                    PERFORM 7200-INSERT-INT00007-ROW                 02280
W33304                WHEN OTHER                                           02290
W33304                    MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02300
W33304                    MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02310
W33304                    PERFORM 9999-SQL-ABEND                           02320
W33304            END-EVALUATE                                             02330
              END-IF                                                       02331
W33304     END-IF.                                                         02331
                                                                        02340000
           IF PS-PROGRAM-DEADLOCKED                                     02350000
               CONTINUE                                                 02360000
           ELSE                                                         02370000
               PERFORM 7700-COMMIT-PROCESSING                           02380000
               PERFORM 4000-READ-CONDENSED-FILE                         02390000
           END-IF.                                                      02401000
                                                                        02410000
      ******************************************************************02430000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02440000
      ******************************************************************02450000
       2100-CALCULATE-UPDATES.                                          02460000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02470000
                                                                        02480000
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
W34517* IMPORTANT NOTE: IN THIS ENTIRE PARAGRAPH                                
W34517* ALL REFERENCES TO INSCLS- WERE CHANGED TO INT00007-                     
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
           COMPUTE SV-SLS-RTL-AMT       =                               02490000
W34517             INT00007-SLS-RTL-AMT      + IN012-SLS-RTL-AMT.       02500000
           COMPUTE SV-MKDN-RTL-AMT      =                               02510000
W34517             INT00007-MKDN-RTL-AMT     + IN012-MKDN-RTL-AMT.      02520000
           COMPUTE SV-MKUP-RTL-AMT      =                               02530000
W34517             INT00007-MKUP-RTL-AMT     + IN012-MKUP-RTL-AMT.      02540000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               02550000
W34517             INT00007-XFER-IN-RTL-AMT  + IN012-XFER-IN-RTL-AMT.   02560000
           COMPUTE SV-XFER-OUT-RTL      =                               02570000
W34517             INT00007-XFER-OUT-RTL-AMT + IN012-XFER-OUT-RTL-AMT.  02580000
           COMPUTE SV-RCPT-RTL-AMT      =                               02590000
W34517             INT00007-RCPT-RTL-AMT + IN012-RCPT-RTL-AMT.          02600000
           COMPUTE SV-PADJ-RTL-AMT      =                               02630000
W34517             INT00007-PADJ-RTL-AMT     + IN012-PADJ-RTL-AMT.      02640000
OCFSFS     COMPUTE SV-STR-PARL-SHIP-RTL-AMT =                           02630000
OCFSFS             INT00007-STR-PARL-SHIP-RTL-AMT                       02640000
OCFSFS                                    + IN012-STR-PARL-SHIP-RTL-AMT.02640000
21591 *W34517      INSCLS-IN-TRNST-RTL-AMT + IN012-IN-TRNST-RTL-AMT.    02640000
W27808     COMPUTE SV-STR-EXPTED-RTL-AMT =                              02630000
W34517           INT00007-STR-EXPTED-RTL-AMT + IN012-STR-EXPTED-RTL-AMT.02640000
W27808*W34517      INSCLS-STR-EXPTED-RTL-AMT + IN012-STR-EXPTED-RTL-AMT.02640000
                                                                        02690000
           ADD IN012-SLS-RTL-AMT      TO PA-SLS-RTL-AMT-IN.             02691000
           ADD SV-SLS-RTL-AMT         TO PA-SLS-RTL-AMT-TOT.            02692000
           ADD IN012-MKDN-RTL-AMT     TO PA-MKDN-RTL-AMT-IN.            02693000
           ADD SV-MKDN-RTL-AMT        TO PA-MKDN-RTL-AMT-TOT.           02694000
           ADD IN012-MKUP-RTL-AMT     TO PA-MKUP-RTL-AMT-IN.            02695000
           ADD SV-MKUP-RTL-AMT        TO PA-MKUP-RTL-AMT-TOT.           02696000
           ADD IN012-XFER-IN-RTL-AMT  TO PA-XFER-IN-AMT-IN.             02697000
           ADD SV-XFER-IN-RTL-AMT     TO PA-XFER-IN-AMT-TOT.            02698000
           ADD IN012-XFER-OUT-RTL-AMT TO PA-XFER-OUT-AMT-IN.            02699000
           ADD SV-XFER-OUT-RTL        TO PA-XFER-OUT-AMT-TOT.           02699100
           ADD IN012-RCPT-RTL-AMT     TO PA-RCPT-RTL-AMT-IN.            02699200
           ADD SV-RCPT-RTL-AMT        TO PA-RCPT-RTL-AMT-TOT.           02699300
           ADD IN012-PADJ-RTL-AMT     TO PA-PADJ-RTL-AMT-IN.            02699400
           ADD SV-PADJ-RTL-AMT        TO PA-PADJ-RTL-AMT-TOT.           02699500
OCFSFS     ADD IN012-STR-PARL-SHIP-RTL-AMT                              02699400
OCFSFS                                TO PA-STR-PARL-SHIP-RTL-AMT-IN.   02699400
OCFSFS     ADD SV-STR-PARL-SHIP-RTL-AMT                                 02699500
OCFSFS                                TO PA-STR-PARL-SHIP-RTL-AMT-TOT.  02699500
W27808     ADD IN012-STR-EXPTED-RTL-AMT TO PA-STR-EXPTED-RTL-IN.        02699400
W27808     ADD SV-STR-EXPTED-RTL-AMT    TO PA-STR-EXPTED-RTL-TOT.       02699500
                                                                        02699600
      ******************************************************************02700000
W34517* READS THE CONDENSED FILE INTO THE INT_SUB_CL_INV_LDGR LAYOUT    02710000
      ******************************************************************02720000
       4000-READ-CONDENSED-FILE.                                        02730000
           READ CONDENSED-FILE INTO IN012-UPDATE-REC                    02740000
              AT END                                                    02750000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   02760000
                                                                        02770000
W33304     SET PS-SKIP-RECORD-NO TO TRUE.                                       
W33304                                                                          
           IF PS-EOF-CONDENSED-FILE                                     02780000
               MOVE IN012-FSCL-MN-NBR         TO CR-FISCAL-NBR          02790000
               MOVE IN012-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   02791000
               MOVE IN012-DEPT-NBR            TO CR-DEPT-NBR            02792000
               MOVE IN012-SUB-CL-NBR          TO CR-SUB-CL-NBR          02793000
               MOVE IN012-STR-NBR             TO CR-STR-NBR             02794000
               MOVE IN012-ML-LWST-ID          TO CR-ML-LWST-ID          02794000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    02795000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  02796000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       02797000
                                             TCKRSTINF-WORK-STRG-DESC   02798000
               EXEC SQL                                                 02799000
                 UPDATE TCKRSTINF                                       02799100
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      02799200
                  WHERE JOB_NAME   = :PC-JOB-NAME                       02799300
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   02799400
               END-EXEC                                                 02799500
               IF SQLCODE = 0                                           02799700
                   CONTINUE                                             02799800
               ELSE                                                     02799900
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED  02800000
                   MOVE '* 4000-READ-CONDENSED-FILE *' TO               02800100
                        PV-PARAGRAPH-NAME                               02800200
                   PERFORM 9999-SQL-ABEND                               02800300
               END-IF                                                   02800400
           ELSE                                                         02801000
               ADD 1                         TO PA-INPUT-COUNT          02810000
           END-IF.                                                      02820000
                                                                        02830000
      ******************************************************************02840000
      * SELECT A ROW FROM THE MONTHLY INV  TABLE THAT MATCHES INPUT KEY 02850000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      02860000
      ******************************************************************02870000
       7000-SELECT-DB-ROW.                                              02880000
                                                                        02890000
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
W34517* IMPORTANT NOTE: IN THIS ENTIRE PARAGRAPH                                
W34517* ALL REFERENCES TO INSCLS- WERE CHANGED TO INT00007-                     
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
                                                                                
W26600*W34517VE IN012-FSCL-MN-NBR     TO INSCLS-FSCL-MN-NBR.            03120000
W33304*    MOVE IN012-FSCL-MN-NBR     TO INT00007-FSCL-MN-NBR.          03120000
W33304     MOVE INCNTL-OPN-FSCL-MN-NBR TO INT00007-FSCL-MN-NBR.         03120000
                                                                                
W26600*W34517VE IN012-FSCL-MN-END-DTE TO INSCLS-FSCL-MN-END-DTE.        03130000
W33304* INVENTORY DATE IS POPULATED TO FISC MNTH END DATE                       
W33304     MOVE INCNTL-OPN-FSCL-MN-DTE TO INT00007-FSCL-MN-END-DTE.     03130000
W33304     MOVE IN012-FSCL-MN-END-DTE TO INT00007-ML-PSTG-MN-END-DTE.   03130000
W20445                                                                          
W34517     MOVE IN012-DEPT-NBR        TO PV-EDIT-DEPT-NBR.              03140000
W34517     MOVE PV-EDIT-DEPT-NBR-RED  TO INT00007-DEPT-NBR.             03140000
W20445*W34517VE PV-EDIT-DEPT-NBR-RED  TO INSCLS-DEPT-NBR.               03140000
W20445                                                                          
W34517     MOVE IN012-SUB-CL-NBR      TO PV-EDIT-SUB-CLASS.             03150000
W34517     MOVE PV-EDIT-CLASS-P2      TO INT00007-SUB-CL-NBR.           03150000
W20445*W34517VE PV-EDIT-CLASS-P2      TO INSCLS-SUB-CL-NBR.             03150000
W20445                                                                          
W26600*W34517VE IN012-STR-NBR         TO INSCLS-STR-NBR.                03151000
W34517     MOVE IN012-STR-NBR         TO INT00007-STR-NBR.              03151000
W34517*                                                                         
W28582*W34517VE IN012-ML-LWST-ID      TO INSCLS-IN-LO-MDSE-LVL-ID.      03151000
W34517     MOVE IN012-ML-LWST-ID      TO INT00007-IN-LO-MDSE-LVL-ID.    03151000
W26600                                                                  02890000
W34517***** COMMENTED ENTIRE EXEC STATEMENT TO RETAIN CHANGE HISTORY            
W34517***** AND REPLACED THE REFERENCES TO INSCLS- WITH INT00007-               
      *    EXEC SQL                                                     02900000
      *        SELECT SLS_RTL_AMT                                       02910000
      *              ,MKDN_RTL_AMT                                      02920000
      *              ,MKUP_RTL_AMT                                      02930000
      *              ,XFER_IN_RTL_AMT                                   02940000
      *              ,XFER_OUT_RTL_AMT                                  02950000
      *              ,RCPT_RTL_AMT                                      02960000
      *              ,PADJ_RTL_AMT                                      02980000
21591 *              ,IN_TRNST_RTL_AMT                                  02980000
W27808*              ,STR_EXPTED_RTL_AMT                                02980000
      *          INTO :INSCLS-SLS-RTL-AMT                               03010000
      *              ,:INSCLS-MKDN-RTL-AMT                              03020000
      *              ,:INSCLS-MKUP-RTL-AMT                              03030000
      *              ,:INSCLS-XFER-IN-RTL-AMT                           03040000
      *              ,:INSCLS-XFER-OUT-RTL-AMT                          03050000
      *              ,:INSCLS-RCPT-RTL-AMT                              03060000
      *              ,:INSCLS-PADJ-RTL-AMT                              03080000
21591 *              ,:INSCLS-IN-TRNST-RTL-AMT                          03080000
W27808*              ,:INSCLS-STR-EXPTED-RTL-AMT                        03080000
      *       FROM TINSCLS                                              03110000
W26600*       WHERE   FSCL_MN_NBR         = :INSCLS-FSCL-MN-NBR         03120000
W26600*         AND   FSCL_MN_END_DTE     = :INSCLS-FSCL-MN-END-DTE     03130000
W26600*         AND   DEPT_NBR            = :INSCLS-DEPT-NBR            03140000
W26600*         AND   SUB_CL_NBR          = :INSCLS-SUB-CL-NBR          03150000
W26600*         AND   STR_NBR             = :INSCLS-STR-NBR             03151000
W28582*         AND   IN_LO_MDSE_LVL_ID   = :INSCLS-IN-LO-MDSE-LVL-ID   03151000
      *    END-EXEC.                                                    03160000
                                                                        03172000
W34517     EXEC SQL                                                     02900000
W34517         SELECT SLS_RTL_AMT                                       02910000
W34517               ,MKDN_RTL_AMT                                      02920000
W34517               ,MKUP_RTL_AMT                                      02930000
W34517               ,XFER_IN_RTL_AMT                                   02940000
W34517               ,XFER_OUT_RTL_AMT                                  02950000
W34517               ,RCPT_RTL_AMT                                      02960000
W34517               ,PADJ_RTL_AMT                                      02980000
OCFSFS               ,STR_PARL_SHIP_RTL_AMT                             02980000
W34517               ,STR_EXPTED_RTL_AMT                                02980000
W34517           INTO :INT00007-SLS-RTL-AMT                             03010000
W34517               ,:INT00007-MKDN-RTL-AMT                            03020000
W34517               ,:INT00007-MKUP-RTL-AMT                            03030000
W34517               ,:INT00007-XFER-IN-RTL-AMT                         03040000
W34517               ,:INT00007-XFER-OUT-RTL-AMT                        03050000
W34517               ,:INT00007-RCPT-RTL-AMT                            03060000
W34517               ,:INT00007-PADJ-RTL-AMT                            03080000
OCFSFS               ,:INT00007-STR-PARL-SHIP-RTL-AMT                   03080000
W34517               ,:INT00007-STR-EXPTED-RTL-AMT                      03080000
W34517        FROM INT_SUB_CL_INV_LDGR                                  03110000
W34517        WHERE   FSCL_MN_NBR         = :INT00007-FSCL-MN-NBR       03120000
W34517          AND   FSCL_MN_END_DTE     = :INT00007-FSCL-MN-END-DTE   03130000
W34517          AND   DEPT_NBR            = :INT00007-DEPT-NBR          03140000
W34517          AND   SUB_CL_NBR          = :INT00007-SUB-CL-NBR        03150000
W34517          AND   STR_NBR             = :INT00007-STR-NBR           03151000
W34517          AND   IN_LO_MDSE_LVL_ID   = :INT00007-IN-LO-MDSE-LVL-ID 03151000
W33304          AND   ML_PSTG_MN_END_DTE  = :INT00007-ML-PSTG-MN-END-DTE03151000
W34517     END-EXEC.                                                    03160000
                                                                        03172000
           EVALUATE SQLCODE                                             03180000
               WHEN 0                                                   03190000
W34517             SET PI-UPDATE-INT00007-ROW       TO TRUE             03200000
               WHEN +100                                                03210000
W34517             SET PI-INSERT-INT00007-ROW       TO TRUE             03220000
               WHEN -911                                                03220100
                   ADD +1             TO PV-DEADLOCK-COUNT              03220200
                   DISPLAY 'DEADLOCK -911 IN 7000-SELECT-DB-ROW'        03220300
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03220400
                       MOVE '7000-SELECT-DB-ROW' TO                     03220500
                                                    PV-PARAGRAPH-NAME   03220600
W34517                 MOVE 'SELECT AGAINST INT_SUB_CL_INV_LDGR'        03220700
                                      TO PV-OPERATION-ATTEMPTED         03220800
                       PERFORM 9000-DEADLOCK-ERROR                      03220900
                   ELSE                                                 03221000
                       PERFORM 7999-RESTART-PROCESS                     03221100
                   END-IF                                               03222000
               WHEN OTHER                                               03230000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03240000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03250000
                   PERFORM 9999-SQL-ABEND                               03260000
           END-EVALUATE.                                                03270000
                                                                        03280000
                                                                        03290000
      ***************************************************************** 03300000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  03310000
      * SUB CLASS AND STORE. CURRENT TABLE VALUES UPDATED TO INCLUDE    03320000
      * VALUES FROM INPUT REC MLRD012                                   03321026
      ***************************************************************** 03330000
W34517 7100-UPDATE-INT00007-ROW.                                        03350000
                                                                        03360000
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
W34517* IMPORTANT NOTE: IN THIS ENTIRE PARAGRAPH                                
W34517* ALL REFERENCES TO INSCLS- WERE CHANGED TO INT00007-                     
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
           PERFORM 2100-CALCULATE-UPDATES.                              03380000
                                                                        03390000
W33304*    MOVE IN012-FSCL-MN-NBR     TO INT00007-FSCL-MN-NBR.          03520000
W33304     MOVE INCNTL-OPN-FSCL-MN-NBR TO INT00007-FSCL-MN-NBR.         03520000
W26600*W34517VE IN012-FSCL-MN-NBR     TO INSCLS-FSCL-MN-NBR.            03520000
W26600*W34517VE IN012-FSCL-MN-END-DTE TO INSCLS-FSCL-MN-END-DTE.        03530000
W20445                                                                          
           MOVE IN012-DEPT-NBR        TO PV-EDIT-DEPT-NBR.              03540000
W34517     MOVE PV-EDIT-DEPT-NBR-RED  TO INT00007-DEPT-NBR.             03540000
W20445*W34517VE PV-EDIT-DEPT-NBR-RED  TO INSCLS-DEPT-NBR.               03540000
W20445                                                                          
           MOVE IN012-SUB-CL-NBR      TO PV-EDIT-SUB-CLASS.             03550000
W34517     MOVE PV-EDIT-CLASS-P2      TO INT00007-SUB-CL-NBR.           03550000
W20445*W34517VE PV-EDIT-CLASS-P2      TO INSCLS-SUB-CL-NBR.             03550000
W20445                                                                          
W26600*W34517VE IN012-STR-NBR         TO INSCLS-STR-NBR.                03551000
W34517     MOVE IN012-STR-NBR         TO INT00007-STR-NBR.              03551000
W28582*W34517VE IN012-ML-LWST-ID      TO INSCLS-IN-LO-MDSE-LVL-ID.      03550000
W34517     MOVE IN012-ML-LWST-ID      TO INT00007-IN-LO-MDSE-LVL-ID.    03550000
                                                                        03390000
W26600*W34517VE SV-SLS-RTL-AMT        TO INSCLS-SLS-RTL-AMT.            03520000
W34517     MOVE SV-SLS-RTL-AMT        TO INT00007-SLS-RTL-AMT.          03520000
W26600*W34517VE SV-MKDN-RTL-AMT       TO INSCLS-MKDN-RTL-AMT.           03530000
W34517     MOVE SV-MKDN-RTL-AMT       TO INT00007-MKDN-RTL-AMT.         03530000
W26600*W34517VE SV-MKUP-RTL-AMT       TO INSCLS-MKUP-RTL-AMT.           03540000
W34517     MOVE SV-MKUP-RTL-AMT       TO INT00007-MKUP-RTL-AMT.         03540000
W26600*W34517VE SV-XFER-IN-RTL-AMT    TO INSCLS-XFER-IN-RTL-AMT.        03550000
W34517     MOVE SV-XFER-IN-RTL-AMT    TO INT00007-XFER-IN-RTL-AMT.      03550000
W26600*W34517VE SV-XFER-OUT-RTL       TO INSCLS-XFER-OUT-RTL-AMT.       03551000
W34517     MOVE SV-XFER-OUT-RTL       TO INT00007-XFER-OUT-RTL-AMT.     03551000
W26600*W34517VE SV-XFER-OUT-RTL       TO INSCLS-XFER-OUT-RTL-AMT.       03551000
W34517     MOVE SV-XFER-OUT-RTL       TO INT00007-XFER-OUT-RTL-AMT.     03551000
W26600*W34517VE SV-RCPT-RTL-AMT       TO INSCLS-RCPT-RTL-AMT.           03520000
W34517     MOVE SV-RCPT-RTL-AMT       TO INT00007-RCPT-RTL-AMT.         03520000
W26600*W34517VE SV-PADJ-RTL-AMT       TO INSCLS-PADJ-RTL-AMT.           03530000
W34517     MOVE SV-PADJ-RTL-AMT       TO INT00007-PADJ-RTL-AMT.         03530000
W26600*W34517VE SV-IN-TRNST-RTL-AMT   TO INSCLS-IN-TRNST-RTL-AMT.       03540000
OCFSFS     MOVE SV-STR-PARL-SHIP-RTL-AMT                                03540000
OCFSFS                                TO INT00007-STR-PARL-SHIP-RTL-AMT.03540000
W27808*W34517VE SV-STR-EXPTED-RTL-AMT TO INSCLS-STR-EXPTED-RTL-AMT.     03540000
W34517     MOVE SV-STR-EXPTED-RTL-AMT TO INT00007-STR-EXPTED-RTL-AMT.   03540000
W33304     MOVE INCNTL-OPN-FSCL-MN-DTE TO INT00007-FSCL-MN-END-DTE.             
W33304     MOVE IN012-FSCL-MN-END-DTE  TO INT00007-ML-PSTG-MN-END-DTE.          
                                                                        03390000
W34517***** COMMENTED ENTIRE EXEC STATEMENT TO RETAIN CHANGE HISTORY            
W34517***** AND REPLACED THE REFERENCES TO INSCLS- WITH INT00007-               
      *    EXEC SQL                                                     03400000
      *        UPDATE TINSCLS                                           03410000
W26600*            SET SLS_RTL_AMT         = :INSCLS-SLS-RTL-AMT        03421000
W26600*              , MKDN_RTL_AMT        = :INSCLS-MKDN-RTL-AMT       03422000
W26600*              , MKUP_RTL_AMT        = :INSCLS-MKUP-RTL-AMT       03423000
W26600*              , XFER_IN_RTL_AMT     = :INSCLS-XFER-IN-RTL-AMT    03424000
W26600*              , XFER_OUT_RTL_AMT    = :INSCLS-XFER-OUT-RTL-AMT   03425000
W26600*              , RCPT_RTL_AMT        = :INSCLS-RCPT-RTL-AMT       03430000
W26600*              , PADJ_RTL_AMT        = :INSCLS-PADJ-RTL-AMT       03440000
W26600*              , IN_TRNST_RTL_AMT    = :INSCLS-IN-TRNST-RTL-AMT   03440000
W27808*              , STR_EXPTED_RTL_AMT  = :INSCLS-STR-EXPTED-RTL-AMT 03440000
W26600*       WHERE   FSCL_MN_NBR          = :INSCLS-FSCL-MN-NBR        03520000
W26600*         AND   FSCL_MN_END_DTE      = :INSCLS-FSCL-MN-END-DTE    03530000
W26600*         AND   DEPT_NBR             = :INSCLS-DEPT-NBR           03540000
W26600*         AND   SUB_CL_NBR           = :INSCLS-SUB-CL-NBR         03550000
W26600*         AND   STR_NBR              = :INSCLS-STR-NBR            03551000
W28582*         AND   IN_LO_MDSE_LVL_ID    = :INSCLS-IN-LO-MDSE-LVL-ID  03551000
      *    END-EXEC                                                     03560000
                                                                                
W34517     EXEC SQL                                                     03400000
W34517         UPDATE INT_SUB_CL_INV_LDGR                               03410000
W34517             SET SLS_RTL_AMT         = :INT00007-SLS-RTL-AMT      03421000
W34517               , MKDN_RTL_AMT        = :INT00007-MKDN-RTL-AMT     03422000
W34517               , MKUP_RTL_AMT        = :INT00007-MKUP-RTL-AMT     03423000
W34517               , XFER_IN_RTL_AMT     = :INT00007-XFER-IN-RTL-AMT  03424000
W34517               , XFER_OUT_RTL_AMT    = :INT00007-XFER-OUT-RTL-AMT 03425000
W34517               , RCPT_RTL_AMT        = :INT00007-RCPT-RTL-AMT     03430000
W34517               , PADJ_RTL_AMT        = :INT00007-PADJ-RTL-AMT     03440000
OCFSFS               , STR_PARL_SHIP_RTL_AMT                            03440000
OCFSFS                                 = :INT00007-STR-PARL-SHIP-RTL-AMT03440000
W34517               , STR_EXPTED_RTL_AMT = :INT00007-STR-EXPTED-RTL-AMT03440000
W34517        WHERE   FSCL_MN_NBR          = :INT00007-FSCL-MN-NBR      03520000
W34517          AND   FSCL_MN_END_DTE      = :INT00007-FSCL-MN-END-DTE  03530000
W33304          AND   ML_PSTG_MN_END_DTE   =                                    
W33304                                    :INT00007-ML-PSTG-MN-END-DTE          
W34517          AND   DEPT_NBR             = :INT00007-DEPT-NBR         03540000
W34517          AND   SUB_CL_NBR           = :INT00007-SUB-CL-NBR       03550000
W34517          AND   STR_NBR              = :INT00007-STR-NBR          03551000
W34517          AND   IN_LO_MDSE_LVL_ID   = :INT00007-IN-LO-MDSE-LVL-ID 03551000
W34517     END-EXEC                                                     03560000
                                                                        03570000
           EVALUATE SQLCODE                                             03580000
               WHEN 0                                                   03590000
                   ADD 1 TO PA-UPDATE-COUNT                             03591000
               WHEN -911                                                03610000
                   ADD +1             TO PV-DEADLOCK-COUNT              03620000
W34517             DISPLAY 'DEADLOCK -911 IN 7100-UPDATE-INT00007-ROW'  03621000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03630000
W34517                 MOVE '7100-UPDATE-INT00007-ROW' TO               03640000
                                                    PV-PARAGRAPH-NAME   03650000
W34517                 MOVE 'UPDATE AGAINST INT_SUB_CL_INV_LDGR'        03651000
                                      TO PV-OPERATION-ATTEMPTED         03652000
                       PERFORM 9000-DEADLOCK-ERROR                      03660000
                   ELSE                                                 03670000
                       PERFORM 7999-RESTART-PROCESS                     03680000
                   END-IF                                               03690000
               WHEN OTHER                                               03700000
W34517             MOVE '7100-UPDATE-INT00007-ROW' TO PV-PARAGRAPH-NAME 03710000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
                                                                        03750000
      ***************************************************************** 03840000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY INCL TABLE. INITIAL  * 03850000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 03860000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC03870000
      ***************************************************************** 03880000
W34517 7200-INSERT-INT00007-ROW.                                        03890000
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
W34517* IMPORTANT NOTE: IN THIS ENTIRE PARAGRAPH                                
W34517* ALL REFERENCES TO INSCLS- WERE CHANGED TO INT00007-                     
W34517*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*                             
                                                                        03900000
W26600*W34517VE IN012-FSCL-MN-NBR      TO INSCLS-FSCL-MN-NBR.           04150000
W33304*    MOVE IN012-FSCL-MN-NBR      TO INT00007-FSCL-MN-NBR.         04150000
W33304     MOVE INCNTL-OPN-FSCL-MN-NBR TO INT00007-FSCL-MN-NBR.         04150000
W20445                                                                          
           MOVE IN012-DEPT-NBR         TO PV-EDIT-DEPT-NBR.             04170000
W34517     MOVE PV-EDIT-DEPT-NBR-RED   TO INT00007-DEPT-NBR.            04170000
W20445*W34517VE PV-EDIT-DEPT-NBR-RED   TO INSCLS-DEPT-NBR.              04170000
W20445                                                                          
           MOVE IN012-SUB-CL-NBR       TO PV-EDIT-SUB-CLASS.            04170100
W34517     MOVE PV-EDIT-CLASS-P2       TO INT00007-SUB-CL-NBR.          04170100
W20445*W34517VE PV-EDIT-CLASS-P2       TO INSCLS-SUB-CL-NBR.            04170100
W20445                                                                          
W26600*W34517VE IN012-STR-NBR          TO INSCLS-STR-NBR.               04170200
W34517     MOVE IN012-STR-NBR          TO INT00007-STR-NBR.             04170200
W28582*W34517VE IN012-ML-LWST-ID       TO INSCLS-IN-LO-MDSE-LVL-ID.     04170200
W34517     MOVE IN012-ML-LWST-ID       TO INT00007-IN-LO-MDSE-LVL-ID.   04170200
W26600*W34517VE IN012-SLS-RTL-AMT      TO INSCLS-SLS-RTL-AMT.           04171000
W34517     MOVE IN012-SLS-RTL-AMT      TO INT00007-SLS-RTL-AMT.         04171000
W26600*34517OVE IN012-MKDN-RTL-AMT     TO INSCLS-MKDN-RTL-AMT.          04172000
W34517     MOVE IN012-MKDN-RTL-AMT     TO INT00007-MKDN-RTL-AMT.        04172000
W26600*W34517VE IN012-MKUP-RTL-AMT     TO INSCLS-MKUP-RTL-AMT.          04173000
W34517     MOVE IN012-MKUP-RTL-AMT     TO INT00007-MKUP-RTL-AMT.        04173000
W26600*W34517VE IN012-XFER-IN-RTL-AMT  TO INSCLS-XFER-IN-RTL-AMT.       04174000
W34517     MOVE IN012-XFER-IN-RTL-AMT  TO INT00007-XFER-IN-RTL-AMT.     04174000
W26600*W34517VE IN012-XFER-OUT-RTL-AMT TO INSCLS-XFER-OUT-RTL-AMT.      04175000
W34517     MOVE IN012-XFER-OUT-RTL-AMT TO INT00007-XFER-OUT-RTL-AMT.    04175000
W26600*W34517VE IN012-RCPT-RTL-AMT     TO INSCLS-RCPT-RTL-AMT.          04176000
W34517     MOVE IN012-RCPT-RTL-AMT     TO INT00007-RCPT-RTL-AMT.        04176000
W26600*W34517VE IN012-PADJ-RTL-AMT     TO INSCLS-PADJ-RTL-AMT.          04177000
W34517     MOVE IN012-PADJ-RTL-AMT     TO INT00007-PADJ-RTL-AMT.        04177000
W26600*34517OVE ZEROS                  TO INSCLS-SHNK-RTL-AMT.          04290000
W34517     MOVE ZEROS                  TO INT00007-SHNK-RTL-AMT.        04290000
W26600*W34517VE ZEROS                  TO INSCLS-END-INV-RTL-AMT.       04300000
W34517     MOVE ZEROS                  TO INT00007-END-INV-RTL-AMT.     04300000
W26600*W34517VE ZEROS                  TO INSCLS-CTOFF-SLS-AMT.         04310000
W34517     MOVE ZEROS                  TO INT00007-CTOFF-SLS-AMT.       04310000
W26600*W34517VE ZEROS                  TO INSCLS-CTOFF-SHNK-AMT.        04320000
W34517     MOVE ZEROS                  TO INT00007-CTOFF-SHNK-AMT.      04320000
W26600*W34517VE ZEROS                  TO INSCLS-CTOFF-END-INV-AMT.     04330000
W34517     MOVE ZEROS                  TO INT00007-CTOFF-END-INV-AMT.   04330000
W26600*34517OVE IN012-IN-TRNST-RTL-AMT TO INSCLS-IN-TRNST-RTL-AMT.      04177000
OCFSFS     MOVE IN012-STR-PARL-SHIP-RTL-AMT                             04177000
OCFSFS                                TO INT00007-STR-PARL-SHIP-RTL-AMT.04177000
W27808*W34517VE IN012-STR-EXPTED-RTL-AMT TO INSCLS-STR-EXPTED-RTL-AMT.  04177000
W27808     MOVE IN012-STR-EXPTED-RTL-AMT TO INT00007-STR-EXPTED-RTL-AMT.04177000
W33304     MOVE INCNTL-OPN-FSCL-MN-DTE TO INT00007-FSCL-MN-END-DTE.             
W33304     MOVE IN012-FSCL-MN-END-DTE  TO INT00007-ML-PSTG-MN-END-DTE.          
                                                                                
W34517***** COMMENTED ENTIRE EXEC STATEMENT TO RETAIN CHANGE HISTORY            
W34517***** AND REPLACED THE REFERENCES TO INSCLS- WITH INT00007-               
      *    EXEC SQL                                                     03920000
      *        INSERT INTO TINSCLS                                      03930000
      *            (   FSCL_MN_NBR                                      03940000
      *              , FSCL_MN_END_DTE                                  03950000
      *              , DEPT_NBR                                         03960000
      *              , SUB_CL_NBR                                       03970000
      *              , STR_NBR                                          03971000
W28582*              , IN_LO_MDSE_LVL_ID                                        
      *              , SLS_RTL_AMT                                      03972000
      *              , MKDN_RTL_AMT                                     03973000
      *              , MKUP_RTL_AMT                                     03974000
      *              , XFER_IN_RTL_AMT                                  03975000
      *              , XFER_OUT_RTL_AMT                                 03976000
      *              , RCPT_RTL_AMT                                     03980000
      *              , PADJ_RTL_AMT                                     04000000
      *              , SHNK_RTL_AMT                                     04080000
      *              , END_INV_RTL_AMT                                  04090000
      *              , CTOFF_SLS_AMT                                    04100000
      *              , CTOFF_SHNK_AMT                                   04110000
21591 *              , CTOFF_END_INV_AMT                                04120000
21591 *              , IN_TRNST_RTL_AMT                                 04120000
W27808*              , STR_EXPTED_RTL_AMT )                             04120000
      *        VALUES                                                   04140000
W26600*            (   :INSCLS-FSCL-MN-NBR                              04150000
W26600*              , :INSCLS-FSCL-MN-END-DTE                          04160000
W26600*              , :INSCLS-DEPT-NBR                                 04170000
W26600*              , :INSCLS-SUB-CL-NBR                               04170100
W26600*              , :INSCLS-STR-NBR                                  04170200
W28582*              , :INSCLS-IN-LO-MDSE-LVL-ID                        04170200
W26600*              , :INSCLS-SLS-RTL-AMT                              04171000
W26600*              , :INSCLS-MKDN-RTL-AMT                             04172000
W26600*              , :INSCLS-MKUP-RTL-AMT                             04173000
W26600*              , :INSCLS-XFER-IN-RTL-AMT                          04174000
W26600*              , :INSCLS-XFER-OUT-RTL-AMT                         04175000
W26600*              , :INSCLS-RCPT-RTL-AMT                             04176000
W26600*              , :INSCLS-PADJ-RTL-AMT                             04177000
W26600*              , :INSCLS-SHNK-RTL-AMT                             04290000
W26600*              , :INSCLS-END-INV-RTL-AMT                          04300000
W26600*              , :INSCLS-CTOFF-SLS-AMT                            04310000
W26600*              , :INSCLS-CTOFF-SHNK-AMT                           04320000
W26600*              , :INSCLS-CTOFF-END-INV-AMT                        04330000
W26600*              , :INSCLS-IN-TRNST-RTL-AMT                         04177000
W27808*              , :INSCLS-STR-EXPTED-RTL-AMT)                      04177000
      *    END-EXEC.                                                    04350000
                                                                        04370200
W34517     EXEC SQL                                                     03920000
W34517         INSERT INTO INT_SUB_CL_INV_LDGR                          03930000
W34517             (   FSCL_MN_NBR                                      03940000
W34517               , FSCL_MN_END_DTE                                  03950000
W34517               , DEPT_NBR                                         03960000
W34517               , SUB_CL_NBR                                       03970000
W34517               , STR_NBR                                          03971000
W34517               , IN_LO_MDSE_LVL_ID                                        
W34517               , SLS_RTL_AMT                                      03972000
W34517               , MKDN_RTL_AMT                                     03973000
W34517               , MKUP_RTL_AMT                                     03974000
W34517               , XFER_IN_RTL_AMT                                  03975000
W34517               , XFER_OUT_RTL_AMT                                 03976000
W34517               , RCPT_RTL_AMT                                     03980000
W34517               , PADJ_RTL_AMT                                     04000000
W34517               , SHNK_RTL_AMT                                     04080000
W34517               , END_INV_RTL_AMT                                  04090000
W34517               , CTOFF_SLS_AMT                                    04100000
W34517               , CTOFF_SHNK_AMT                                   04110000
W34517               , CTOFF_END_INV_AMT                                04120000
OCFSFS               , STR_PARL_SHIP_RTL_AMT                            04120000
W34517               , STR_EXPTED_RTL_AMT                               04120000
W33304               , ML_PSTG_MN_END_DTE )                             04120000
W34517         VALUES                                                   04140000
W34517             (   :INT00007-FSCL-MN-NBR                            04150000
W34517               , :INT00007-FSCL-MN-END-DTE                        04160000
W34517               , :INT00007-DEPT-NBR                               04170000
W34517               , :INT00007-SUB-CL-NBR                             04170100
W34517               , :INT00007-STR-NBR                                04170200
W34517               , :INT00007-IN-LO-MDSE-LVL-ID                      04170200
W34517               , :INT00007-SLS-RTL-AMT                            04171000
W34517               , :INT00007-MKDN-RTL-AMT                           04172000
W34517               , :INT00007-MKUP-RTL-AMT                           04173000
W34517               , :INT00007-XFER-IN-RTL-AMT                        04174000
W34517               , :INT00007-XFER-OUT-RTL-AMT                       04175000
W34517               , :INT00007-RCPT-RTL-AMT                           04176000
W34517               , :INT00007-PADJ-RTL-AMT                           04177000
W34517               , :INT00007-SHNK-RTL-AMT                           04290000
W34517               , :INT00007-END-INV-RTL-AMT                        04300000
W34517               , :INT00007-CTOFF-SLS-AMT                          04310000
W34517               , :INT00007-CTOFF-SHNK-AMT                         04320000
W34517               , :INT00007-CTOFF-END-INV-AMT                      04330000
OCFSFS               , :INT00007-STR-PARL-SHIP-RTL-AMT                  04177000
W34517               , :INT00007-STR-EXPTED-RTL-AMT                     04177000
W33304               , :INT00007-ML-PSTG-MN-END-DTE)                    04177000
W34517     END-EXEC.                                                    04350000
                                                                        04370200
           EVALUATE SQLCODE                                             04371000
               WHEN 0                                                   04380000
                   ADD 1 TO PA-INSERT-COUNT                             04381000
                   ADD IN012-SLS-RTL-AMT      TO PA-SLS-RTL-AMT-IN      04382000
                   ADD IN012-MKDN-RTL-AMT     TO PA-MKDN-RTL-AMT-IN     04384000
                   ADD IN012-MKUP-RTL-AMT     TO PA-MKUP-RTL-AMT-IN     04386000
                   ADD IN012-XFER-IN-RTL-AMT  TO PA-XFER-IN-AMT-IN      04388000
                   ADD IN012-XFER-OUT-RTL-AMT TO PA-XFER-OUT-AMT-IN     04390000
                   ADD IN012-RCPT-RTL-AMT     TO PA-RCPT-RTL-AMT-IN     04392000
                   ADD IN012-PADJ-RTL-AMT     TO PA-PADJ-RTL-AMT-IN     04394000
OCFSFS             ADD IN012-STR-PARL-SHIP-RTL-AMT                      04394000
OCFSFS                                    TO PA-STR-PARL-SHIP-RTL-AMT-IN04394000
                   ADD IN012-STR-EXPTED-RTL-AMT TO                              
                                                 PA-STR-EXPTED-RTL-IN           
               WHEN -911                                                04400000
                   ADD +1             TO PV-DEADLOCK-COUNT              04410000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04420000
W34517                 MOVE '7200-INSERT-INT00007-ROW' TO               04430000
                                                    PV-PARAGRAPH-NAME   04440000
W34517                 MOVE 'INSERT AGAINST INT_SUB_CL_INV_LDGR'        04441000
                                      TO PV-OPERATION-ATTEMPTED         04442000
                       PERFORM 9000-DEADLOCK-ERROR                      04450000
                   ELSE                                                 04460000
                       PERFORM 7999-RESTART-PROCESS                     04470000
                   END-IF                                               04480000
               WHEN OTHER                                               04490000
W34517             MOVE '7200-INSERT-INT00007-ROW' TO PV-PARAGRAPH-NAME 04500000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED04510000
                   PERFORM 9999-SQL-ABEND                               04520000
           END-EVALUATE.                                                04530000
                                                                        04540000
      ******************************************************************04630000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *04640000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *04650000
      *            CONTROL TABLE                                       *04660000
      ******************************************************************04670000
       7300-GET-COMMIT-TIMESTAMP.                                       04680000
                                                                        04690000
      * CHECKPOINT TAKEN BASED ON DB2 CHECKPOINT RESTART TABLE          04691000
           EXEC SQL                                                     04700000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       04720000
               INTO :PV-COMMIT-TIMESTAMP                                04730000
               FROM TCKRSTCNTL                                          04740000
              WHERE JOB_NAME  = :PC-JOB-NAME                            04750000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        04760000
           END-EXEC.                                                    04770000
                                                                        04780000
           EVALUATE TRUE                                                04790000
               WHEN SQLCODE = 0                                         04800000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  04810000
                   CONTINUE                                             04820000
               WHEN SQLCODE NOT EQUAL ZERO                              04830000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME04840000
                   MOVE 'SELECT TMSTMP FROM TCKRSTCNTL'                 04841000
                                      TO PV-OPERATION-ATTEMPTED         04842000
                   PERFORM 9999-SQL-ABEND                               04850000
           END-EVALUATE.                                                04860000
                                                                        04870000
      ******************************************************************04890000
      * 7400-INIT-CHECKPOINT-SELECT                                    *04900000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *04910000
      *            IF WE ARE IN A RESTART CONDITION.                   *04920000
      ******************************************************************04930000
       7400-INIT-CHECKPOINT-SELECT.                                     04940000
                                                                        04950000
           EXEC SQL                                                     04960000
             SELECT  CKPT_STAT_CODE                                     04970000
                    ,CKPT_FRQNCY_QTY                                    04980000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         04990000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05000000
               FROM  TCKRSTCNTL                                         05010000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05020000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05030000
           END-EXEC.                                                    05040000
                                                                        05050000
           IF SQLCODE = 0                                               05060000
               CONTINUE                                                 05070000
           ELSE                                                         05080000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05090000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05100000
               PERFORM 9999-SQL-ABEND                                   05110000
           END-IF.                                                      05120000
                                                                        05140000
      ******************************************************************05150000
      * 7500-SELECT-RESTART-INFO                                       *05160000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05170000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05180000
      ******************************************************************05190000
       7500-SELECT-RESTART-INFO.                                        05200000
                                                                        05210000
           EXEC SQL                                                     05220000
             SELECT  WORK_STRG_DESC                                     05230000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05240000
               FROM  TCKRSTINF                                          05250000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05260000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05270000
           END-EXEC.                                                    05280000
                                                                        05290000
           IF SQLCODE = 0                                               05300000
               CONTINUE                                                 05310000
           ELSE                                                         05320000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05330000
               MOVE 'SELECT  FROM TCKRSTINF'                            05331000
                                      TO PV-OPERATION-ATTEMPTED         05332000
               PERFORM 9999-SQL-ABEND                                   05340000
           END-IF.                                                      05350000
                                                                        05360000
      ******************************************************************05370000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05380000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05390000
      ******************************************************************05400000
       7600-SET-CURRENT-TIMESTAMP.                                      05410000
                                                                        05420000
           EXEC SQL                                                     05430000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05440000
           END-EXEC.                                                    05450000
           IF SQLCODE = 0                                               05490000
               CONTINUE                                                 05500000
           ELSE                                                         05510000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05520000
               MOVE 'SET TIMESTAMP'                                     05521000
                                      TO PV-OPERATION-ATTEMPTED         05522000
               PERFORM 9999-SQL-ABEND                                   05530000
           END-IF.                                                      05540000
                                                                        05560000
      ******************************************************************05570000
      * 7700-COMMIT-PROCESSING.                                        *05580000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *05590000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*05600000
      ******************************************************************05610000
       7700-COMMIT-PROCESSING.                                          05620000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     05630000
                                                                        05640000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          05650000
                                                                        05660000
      * PREPARE COMMIT RECORD FOR UPDATE                                05670000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                05680000
               MOVE IN012-FSCL-MN-NBR         TO CR-FISCAL-NBR          05690000
               MOVE IN012-FSCL-MN-NBR         TO CR-FISCAL-NBR          05690000
               MOVE IN012-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   05700000
               MOVE IN012-DEPT-NBR            TO CR-DEPT-NBR            05710000
               MOVE IN012-SUB-CL-NBR          TO CR-SUB-CL-NBR          05720000
               MOVE IN012-STR-NBR             TO CR-STR-NBR             05721000
               MOVE IN012-ML-LWST-ID          TO CR-ML-LWST-ID          05721000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    05730000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  05740000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       05750000
                                             TCKRSTINF-WORK-STRG-DESC   05760000
               IF PS-FIRST-COMMIT                                       05770000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                05780000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                05790000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       05800000
               END-IF                                                   05810000
               PERFORM 7800-COMMIT-CHANGES                              05820000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        05830000
           END-IF.                                                      05840000
                                                                        05850000
      ******************************************************************05870000
      * 7800-COMMIT-CHANGES.                                            05880000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      05890000
      *            TAKE A COMMIT.                                       05900000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    05910000
      ******************************************************************05920000
       7800-COMMIT-CHANGES.                                             05930000
                                                                        05940000
           EXEC SQL                                                     05950000
             UPDATE TCKRSTINF                                           05960000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          05970000
              WHERE JOB_NAME       = :PC-JOB-NAME                       05980000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   05990000
           END-EXEC.                                                    06000000
                                                                        06010000
           IF SQLCODE = 0                                               06020000
               CONTINUE                                                 06030000
      ****     DISPLAY 'COMMIT TAKEN ==>> ' CR-CHECKPOINT-RESTART-AREA  06031000
      ****             ' <<=='                                          06032000
           ELSE                                                         06040000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06050000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06060000
               PERFORM 9999-SQL-ABEND                                   06070000
           END-IF.                                                      06080000
                                                                        06090000
           EXEC SQL                                                     06100000
               COMMIT                                                   06110000
           END-EXEC.                                                    06120000
                                                                        06130000
           IF SQLCODE = 0                                               06140000
               CONTINUE                                                 06150000
           ELSE                                                         06160000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06170000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06180000
               PERFORM 9999-SQL-ABEND                                   06190000
           END-IF.                                                      06200000
                                                                        06220000
      ******************************************************************06230000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06240000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06250000
      *                                                                 06260000
      ******************************************************************06270000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06280000
                                                                        06290000
           EXEC SQL                                                     06300000
             UPDATE  TCKRSTCNTL                                         06310000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06320000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06330000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06340000
           END-EXEC.                                                    06350000
                                                                        06360000
           IF SQLCODE = 0                                               06370000
               CONTINUE                                                 06380000
           ELSE                                                         06390000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06400000
               PERFORM 9999-SQL-ABEND                                   06410000
           END-IF.                                                      06420000
                                                                        06430000
           EJECT                                                        06440000
                                                                        06460000
      ******************************************************************06470000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TINSCL ROW FOR UPDATE 06480000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  06490000
      ******************************************************************06500000
       7999-RESTART-PROCESS.                                            06510000
                                                                        06520000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           06530000
                                                                        06540000
           CLOSE CONDENSED-FILE.                                        06550000
                                                                        06560000
           PERFORM 7500-SELECT-RESTART-INFO.                            06570000
                                                                        06580000
           DISPLAY '**** RESTART **** '.                                06590000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 06600000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   06610000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      06610100
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        06610200
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                       06610300
           IF PS-FIRST-COMMIT                                           06611000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               06612000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     06613000
                      ' BEGINNING'                                      06614000
           ELSE                                                         06615000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           06620000
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 06621000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 06650000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         06651000
                    CR-CHECKPOINT-RESTART-AREA                          06651100
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                06651200
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 06651300
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          06651400
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   06651500
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 06651600
               DISPLAY 'STORE NBR       ' CR-STR-NBR                    06651700
W28582         DISPLAY 'BRAND VENDOR KEY' CR-ML-LWST-ID                 06651700
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           06652000
           END-IF.                                                      06661000
                                                                        06662000
           OPEN INPUT CONDENSED-FILE.                                   06670000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      06680000
                                                                        06690000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          06700000
           PERFORM 4000-READ-CONDENSED-FILE                             06710000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               06720000
               OR PS-EOF-CONDENSED-FILE.                                06720100
                                                                        06720200
           IF PS-EOF-CONDENSED-FILE                                     06720300
               DISPLAY 'END OF INPUT FILE ON RESTART'                   06720400
           ELSE                                                         06720500
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              06720600
                        PA-INPUT-COUNT                                  06720700
               DISPLAY 'FISCAL MONTH NBR      ' IN012-FSCL-MN-NBR       06720800
               DISPLAY 'FISCAL MONTH END DATE ' IN012-FSCL-MN-END-DTE   06720900
               DISPLAY 'DEPARTMENT NBR        ' IN012-DEPT-NBR          06721000
               DISPLAY 'SUB CLASS NBR         ' IN012-SUB-CL-NBR        06721100
               DISPLAY 'STORE NBR             ' IN012-STR-NBR           06722000
               DISPLAY 'BRAND VENDOR KEY      ' IN012-ML-LWST-ID        06722000
           END-IF.                                                      06723000
                                                                        06730000
      ******************************************************************06740000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *06750000
      ******************************************************************06760000
       8000-EOJ-ROUTINE.                                                06770000
                                                                        06780000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       06790000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       06800000
                                                                        06810000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         06830000
W33304     DISPLAY 'INPUT RECORDS SKIPPED    ', PA-SKIP-COUNT.          06830000
           DISPLAY 'UPDATES PROCESSED        ', PA-UPDATE-COUNT.        06840000
           DISPLAY 'INSERTS PROCESSED        ', PA-INSERT-COUNT.        06841000
                                                                        06850000
           MOVE PA-SLS-RTL-AMT-IN   TO PD-DISPLAY-TOTAL.                06851000
           DISPLAY 'SLS-RTL-AMT INPUT : ', PD-DISPLAY-TOTAL.            06851100
           MOVE PA-SLS-RTL-AMT-TOT  TO PD-DISPLAY-TOTAL.                06852000
           DISPLAY 'SLS-RTL-AMT TOTAL : ', PD-DISPLAY-TOTAL.            06852100
           MOVE PA-MKDN-RTL-AMT-IN  TO PD-DISPLAY-TOTAL.                06853000
           DISPLAY 'MKDN-RTL-AMT INPUT: ', PD-DISPLAY-TOTAL.            06853100
           MOVE PA-MKDN-RTL-AMT-TOT TO PD-DISPLAY-TOTAL.                06854000
           DISPLAY 'MKDN-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.            06854100
           MOVE PA-MKUP-RTL-AMT-IN  TO PD-DISPLAY-TOTAL.                06855000
           DISPLAY 'MKUP-RTL-AMT INPUT: ', PD-DISPLAY-TOTAL.            06855100
           MOVE PA-MKUP-RTL-AMT-TOT TO PD-DISPLAY-TOTAL.                06856000
           DISPLAY 'MKUP-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.            06856100
           MOVE PA-XFER-IN-AMT-IN   TO PD-DISPLAY-TOTAL.                06857000
           DISPLAY 'XFER-IN-AMT INPUT : ', PD-DISPLAY-TOTAL.            06857100
           MOVE PA-XFER-IN-AMT-TOT  TO PD-DISPLAY-TOTAL.                06858000
           DISPLAY 'XFER-IN-AMT TOTAL : ', PD-DISPLAY-TOTAL.            06858100
           MOVE PA-XFER-OUT-AMT-IN  TO PD-DISPLAY-TOTAL.                06859000
           DISPLAY 'XFER-OUT-AMT INPUT: ', PD-DISPLAY-TOTAL.            06859100
           MOVE PA-XFER-OUT-AMT-TOT TO PD-DISPLAY-TOTAL.                06859200
           DISPLAY 'XFER-OUT-AMT TOTAL: ', PD-DISPLAY-TOTAL.            06859300
           MOVE PA-RCPT-RTL-AMT-IN  TO PD-DISPLAY-TOTAL.                06859400
           DISPLAY 'RCPT-RTL-AMT INPUT: ', PD-DISPLAY-TOTAL.            06859500
           MOVE PA-RCPT-RTL-AMT-TOT TO PD-DISPLAY-TOTAL.                06859600
           DISPLAY 'RCPT-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.            06859700
           MOVE PA-PADJ-RTL-AMT-IN  TO PD-DISPLAY-TOTAL.                06859800
           DISPLAY 'PADJ-RTL-AMT INPUT: ', PD-DISPLAY-TOTAL.            06859900
           MOVE PA-PADJ-RTL-AMT-TOT TO PD-DISPLAY-TOTAL.                06860000
           DISPLAY 'PADJ-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.            06860100
OCFSFS     MOVE PA-STR-PARL-SHIP-RTL-AMT-IN TO PD-DISPLAY-TOTAL.        06859800
OCFSFS     DISPLAY 'STR-PARL-SHIP-RTL-AMT: ', PD-DISPLAY-TOTAL.         06859900
OCFSFS     MOVE PA-STR-PARL-SHIP-RTL-AMT-TOT TO PD-DISPLAY-TOTAL.       06860000
OCFSFS     DISPLAY 'STR-PARL-SHIP-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.   06860100
21591      MOVE PA-STR-EXPTED-RTL-IN   TO PD-DISPLAY-TOTAL.             06859800
21591      DISPLAY 'STR-EXPTED-RTL-AMT INPUT: ', PD-DISPLAY-TOTAL.      06859900
21591      MOVE PA-STR-EXPTED-RTL-TOT  TO PD-DISPLAY-TOTAL.             06860000
21591      DISPLAY 'STR-EXPTED-RTL-AMT TOTAL: ', PD-DISPLAY-TOTAL.      06860100
                                                                        06860200
           CLOSE CONDENSED-FILE.                                        06861000
                                                                        06870000
      ******************************************************************06890000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       06900000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   06910000
      ******************************************************************06920000
       9000-DEADLOCK-ERROR.                                             06930000
                                                                        06940000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     06950000
               PERFORM 9999-SQL-ABEND.                                  06960000
                                                                        06970000
      ******************************************************************06980000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               06990000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07000000
      ******************************************************************07010000
       9999-SQL-ABEND.                                                  07020000
                                                                        07030000
           MOVE +4013                 TO ABEND-CODE.                    07040000
           DISPLAY '**  ABEND     **'.                                  07050000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07060000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07070000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07080000
                                                                        07090000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07100000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07110000
                                                                        07120000
           COPY DPPD004.                                                07130000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07140000
