       IDENTIFICATION DIVISION.                                         04/26/94
       PROGRAM-ID.    INKRP144.                                                 
       AUTHOR.        JEFF HARTIG/COMPUWARE.                                    
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-19-98                                                  
      *****************************************************************         
      *           DAILY SALES HISTORY UPDATE TOTALS                             
      *                                                                         
      *   THIS PROGRAM WILL READ A SUMMERIZED VERSION OF THE          *         
      *   CONSOLIDATED SALES INVENTORY UPDATE FILE AND CREATE THE     *         
      *   DAILY SALES HISTORY UPDATE TOTALS REPORT.                   *         
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W33304* W33304  09-08-2008 NIJESH/     HOLDING INVENTORY LEDGER OPEN            
W33304*                    INFOSYS     HILO                                     
      *                                                                         
RONNA1* CHG0305858                                                              
RONNA1*         02-05-2024 RONNA MCDONALD                                       
RONNA1*                    REMOVE LOGIC FOR ESTIMATED INVENTORY.                
RONNA1*                    THIS IS OBSOLETE.                                    
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-370.                                         
       OBJECT-COMPUTER.        IBM-370.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-FILE           ASSIGN INP01.                            
           SELECT SALES-REPORT         ASSIGN RPT01.                            
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W33304     RECORD CONTAINS 130 CHARACTERS                                       
           BLOCK CONTAINS 00 RECORDS                                            
           DATA RECORD IS SALES-REC.                                            
                                                                                
W33304 01  SALES-REC               PIC X(130).                                  
                                                                                
       FD  SALES-REPORT                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS PRT-LINE.                                             
                                                                                
       01  PRT-LINE.                                                            
           05  PRT-DATA            PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PROGRAM-CONSTANTS.                                                   
           05  LINE-COUNT      PIC S999        COMP-3  VALUE +71.               
           05  PAGE-COUNT      PIC S9(5)       COMP-3  VALUE +0.                
           05  LINE-MAX        PIC S999        COMP-3  VALUE +54.               
W26600     05  STORE-TABLE-MAX PIC S9(4)   COMP  SYNC VALUE +9999.              
                                                                                
       01  ABEND-CODE              PIC S9(04)      VALUE ZEROES.                
           88 ABEND-4003-CTRL-CARD-ERROR           VALUE +4003.                 
           88 ABEND-4004-TABLE-ERROR               VALUE +4004.                 
           88 ABEND-4007-SEQUENCE-ERROR            VALUE +4007.                 
           88 ABEND-4013-DB2-ERROR                 VALUE +4013.                 
           88 ABEND-4015-DATE-ERROR                VALUE +4015.                 
                                                                                
       01  LC-LEDGER-CONTROL-RECORD.                                            
           05  LC-LEDGER-IND       PIC X.                                       
               88  LC-MERCH-LEDGER                 VALUE 'M'.                   
               88  LC-INV-LEDGER                   VALUE 'I'.                   
           05  FILLER              PIC X(79).                                   
                                                                                
       01  SD-SYSTEM-DATE.                                                      
           05  SD-YY                        PIC 9(02)     VALUE ZEROES.         
           05  SD-MM                        PIC 9(02)     VALUE ZEROES.         
           05  SD-DD                        PIC 9(02)     VALUE ZEROES.         
                                                                                
       01  ST-SYSTEM-TIME.                                                      
           05  ST-HR                        PIC 9(02)     VALUE ZEROES.         
           05  ST-MM                        PIC 9(02)     VALUE ZEROES.         
           05  ST-SS                        PIC 9(02)     VALUE ZEROES.         
           05  FILLER                       PIC 9(02)     VALUE ZEROES.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STORE-TABLE-SW                                         
                                   PIC X(01)       VALUE 'N'.                   
               88 PS-END-OF-STORE-TABLE                                         
                                                   VALUE 'Y'.                   
           05  PS-END-OF-SALES-FILE-SW                                          
                                   PIC X(01)       VALUE 'N'.                   
               88 PS-END-OF-SALES-FILE                                          
                                                   VALUE 'Y'.                   
W33304     05  PS-END-OF-ACTIV-INV-SW                                           
W33304                             PIC X(01)       VALUE 'N'.                   
W33304         88 PS-END-OF-ACTIV-INV                                           
W33304                                             VALUE 'Y'.                   
W33304     05  PS-STORE-EXISTS-SW                                               
W33304                             PIC X(01)       VALUE 'N'.                   
W33304         88 PS-STORE-EXISTS-SW-YES                                        
W33304                                             VALUE 'Y'.                   
W33304         88 PS-STORE-EXISTS-SW-NO                                         
W33304                                             VALUE 'N'.                   
W33304     05  PS-INV-ID-BREAK-SW                                               
W33304                             PIC X(01)       VALUE 'N'.                   
W33304         88 PS-INV-ID-BREAK-YES                                           
W33304                                             VALUE 'Y'.                   
W33304         88 PS-INV-ID-BREAK-NO                                            
W33304                                             VALUE 'N'.                   
           05  PS-OPEN-FSCL-MN-EOF-SW                                           
                                   PIC X(01)       VALUE 'N'.                   
               88 PS-OPEN-FSCL-MN-EOF                                           
                                                   VALUE 'Y'.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  TOTAL-FOR-WEEK      PIC S9(09)      VALUE ZEROS.                 
           05  PRT-CC              PIC X.                                       
           05  PV-PARAGRAPH-NAME   PIC X(30)       VALUE SPACES.                
           05  PV-DB2-OPER-ATTEMPTED PIC X(30) VALUE SPACES.                    
           05  PV-DB2-FUNCTION     PIC X(30)       VALUE SPACES.                
           05  PV-RETRY-COUNT      PIC S9(04) COMP SYNC                         
                                                       VALUE ZERO.              
           05  PV-YEAR             PIC 99.                                      
           05  PV-DAY-OF-WEEK      PIC X.                                       
           05  PV-DAY-OF-WEEK-N REDEFINES PV-DAY-OF-WEEK PIC 9.                 
           05  PV-BEG-OF-WEEK-DATE PIC X(10)       VALUE SPACES.                
           05  PV-WEEKS-TOTALS     PIC S9(13)V99   VALUE ZEROS.                 
           05  PV-PROCESSING-WK-NBR PIC X(02)      VALUE ZEROS.                 
           05  PV-IN-DATE.                                                      
               10 PV-IN-DATE-CC    PIC XX.                                      
               10 PV-IN-DATE-YY    PIC XX.                                      
               10 FILLER           PIC  X.                                      
               10 PV-IN-DATE-MM    PIC XX.                                      
               10 FILLER           PIC  X.                                      
               10 PV-IN-DATE-DD    PIC XX.                                      
           05  PV-OUT-DATE.                                                     
               10 PV-OUT-DATE-MM   PIC XX.                                      
               10 FILLER           PIC  X VALUE '-'.                            
               10 PV-OUT-DATE-DD   PIC XX.                                      
               10 FILLER           PIC  X VALUE '-'.                            
               10 PV-OUT-DATE-YY   PIC XX.                                      
W20445     05  PV-WORK-AMOUNT      PIC S9(11)V9(2) COMP-3 VALUE ZEROS.          
                                                                                
       01  PROGRAM-CONTROLS.                                                    
           05  PC-MAX-RETRIES      PIC S9(04) COMP SYNC                         
                                                       VALUE +3.                
       01  TABLE-SUB-SCRIPTS.                                                   
W26600     05  STORE-SUB           PIC S9(04) COMP VALUE ZEROS.                 
W26600     05  DAY-INDX            PIC S9(04) COMP VALUE ZEROS.                 
W26600     05  A-STORES-INDX       PIC S9(04) COMP VALUE ZEROS.                 
W33304     05  DATE-INDX           PIC S9(04) COMP VALUE ZEROS.                 
                                                                                
       01  SAVE-DATA.                                                           
           05  SD-HOLD-DATE        PIC X(10)    VALUE SPACES.                   
                                                                                
       01  FORMAT-ADDED-RECORDS.                                                
           05  FILLER              PIC X(39)   VALUE                            
               'RECORDS ADDED TO SALES HISTORY FILE = '.                        
           05  ADDED-RECORDS       PIC ZZZ,ZZZ.                                 
                                                                                
      *                                                                         
      * INPUT FILE LAYOUT                                                       
      *                                                                         
W33304     COPY INRD154.                                                        
                                                                                
      *****************************************************************         
      *                    SALES TOTAL TABLE                          *         
      *****************************************************************         
       01  SALES-TOTAL-TABLE.                                                   
W26600     05  STORE-ENTRY OCCURS 9999 TIMES.                                   
W26600         10  STORE-NUMBER    PIC 9(05).                                   
               10  STORE-NAME      PIC X(10).                                   
               10  DAY-ENTRY OCCURS 7 TIMES.                                    
W26600             15  DAILY-SALES PIC S9(11)V99    COMP-3.                     
                                                                                
       01  ALL-STORES-TOTAL.                                                    
           05  ALL-STORES-ENTRY OCCURS 7 TIMES.                                 
W26600         10  ALL-STORES-SALES    PIC S9(11)V99    COMP-3.                 
                                                                                
      **********************************************************                
      *                    HEADINGS                            *                
      **********************************************************                
                                                                                
      * COPY FOR STANDARD HEADING                                               
           COPY DPWS132O.                                                       
                                                                                
                                                                                
       01  HEAD-LN1.                                                            
           05  HD-DATE         PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X       VALUE SPACES.                        
           05  FILLER          PIC X(05)   VALUE SPACES.                        
W26600     05  FILLER          PIC X(34)   VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE 'DAILY '.                      
           05  FILLER          PIC X(6)    VALUE 'SALES '.                      
           05  FILLER          PIC X(8)    VALUE 'HISTORY '.                    
           05  FILLER          PIC X(7)    VALUE 'UPDATE '.                     
           05  FILLER          PIC X(6)    VALUE 'TOTALS'.                      
           05  FILLER          PIC X(2)    VALUE SPACES.                        
           05  HD-LEDGER-LIT   PIC X(20)   VALUE SPACES.                        
                                                                                
W33304 01  CYCL-HDG.                                                            
W33304     05  FILLER          PIC X(5)    VALUE 'CYCLE'.                       
W33304     05  FILLER          PIC X       VALUE SPACES.                        
W33304     05  CYCLE-NO        PIC 9(05)   VALUE ZEROES.                        
W33304     05  FILLER          PIC X(44)   VALUE SPACES.                        
W33304     05  CYCLE-NAME      PIC X(20)   VALUE SPACES.                        
W33304     05  FILLER          PIC X(30)   VALUE SPACES.                        
W33304                                                                          
       01  HEAD-LN2.                                                            
W26600     05  FILLER          PIC X(24)   VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE 'SUNDAY'.                      
           05  FILLER          PIC X(4)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE 'MONDAY'.                      
           05  FILLER          PIC X(7)    VALUE SPACES.                        
           05  FILLER          PIC X(7)    VALUE 'TUESDAY'.                     
           05  FILLER          PIC X(7)    VALUE SPACES.                        
           05  FILLER          PIC X(9)    VALUE 'WEDNESDAY'.                   
           05  FILLER          PIC X(5)    VALUE SPACES.                        
           05  FILLER          PIC X(8)    VALUE 'THURSDAY'.                    
           05  FILLER          PIC X(7)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE 'FRIDAY'.                      
           05  FILLER          PIC X(7)    VALUE SPACES.                        
           05  FILLER          PIC X(8)    VALUE 'SATURDAY'.                    
           05  FILLER          PIC X(10)   VALUE SPACES.                        
           05  FILLER          PIC X(5)    VALUE 'TOTAL'.                       
                                                                                
       01  HEAD-LN3.                                                            
           05  FILLER          PIC X(10)   VALUE '   STORE  '.                  
W26600     05  FILLER          PIC X(13)   VALUE SPACES.                        
           05  HD-SUNDAY       PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC XXX     VALUE SPACES.                        
           05  HD-MONDAY       PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE SPACES.                        
           05  HD-TUESDAY      PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE SPACES.                        
           05  HD-WEDNESDAY    PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE SPACES.                        
           05  HD-THURSDAY     PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE SPACES.                        
           05  HD-FRIDAY       PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(6)    VALUE SPACES.                        
           05  HD-SATURDAY     PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(8)    VALUE SPACES.                        
           05  FILLER          PIC X(8)    VALUE 'FOR WEEK'.                    
                                                                                
       01  DETAIL-LN.                                                           
           05  DET-STORE.                                                       
W26600         10  DET-STORE-NO        PIC X(05).                               
               10  FILLER              PIC X.                                   
               10  DET-STORE-NAME      PIC X(10).                               
               10  FILLER              PIC XX.                                  
           05  DET-STOREX  REDEFINES DET-STORE.                                 
W26600         10  DET-ALL-STORES      PIC X(18).                               
           05  DET-SUNDAY              PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-MONDAY              PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-TUESDAY             PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-WEDNESDAY           PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-THURSDAY            PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-FRIDAY              PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC XX.                                  
           05  DET-SATURDAY            PIC ZZZ,ZZZ,ZZZ-.                        
           05  FILLER                  PIC X(5).                                
           05  DET-TOTAL-FOR-WEEK      PIC ZZZ,ZZZ,ZZZ-.                        
                                                                                
W33304 01  TOT-DETAIL-LN.                                                       
W33304     05  TOT-VERBIAGE            PIC X(18).                               
W33304     05  TOT-DET-SUNDAY          PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-MONDAY          PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-TUESDAY         PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-WEDNESDAY       PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-THURSDAY        PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-FRIDAY          PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC XX.                                  
W33304     05  TOT-DET-SATURDAY        PIC ZZZ,ZZZ,ZZ9-.                        
W33304     05  FILLER                  PIC X(5).                                
W33304     05  TOT-DET-TOTAL-FOR-WEEK  PIC ZZZ,ZZZ,ZZ9-.                        
                                                                                
       01  DASH-LN.                                                             
W26600     05  FILLER                  PIC X(18)   VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC XX      VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
           05  FILLER                  PIC X(4)    VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE ALL '-'.               
                                                                                
      * ****************************************************************        
      *                TOTAL CONSTANTS AND WORK AREAS                  *        
      * ****************************************************************        
       01  TOTAL-CONSTANTS.                                                     
           05  BEGN            PIC X(4)    VALUE 'BEGN'.                        
                                                                                
           05  MDPTLKDH        PIC X(8)    VALUE 'MDPTLKDH'.                    
           05  MULTI-STAT.                                                      
               10  STAT1       PIC X(4)    VALUE '****'.                        
               10  STAT2       PIC X(4)    VALUE '****'.                        
               10  STAT3       PIC X(4)    VALUE '****'.                        
           05  DB-MODULE       PIC X(8)    VALUE 'DDM02DBM'.                    
           05  DB-TASK         PIC X(8)    VALUE 'DSAIC37U'.                    
           05  DB-OPTIONS      PIC X(14)   VALUE 'LOGOPTS=N,END.'.              
           05  DB-FILES.                                                        
               10  DB-MDPT     PIC X(4)    VALUE 'MDPT'.                        
               10  DB-DHST     PIC X(4)    VALUE 'DHST'.                        
               10  FILLER      PIC X(4)    VALUE 'END.'.                        
           05  DB-STORE.                                                        
               10  DB-STOR     PIC X(4)    VALUE 'STOR'.                        
               10  FILLER      PIC X(4)    VALUE 'END.'.                        
           05  DB-RESERVE.                                                      
               10  FILLER      PIC X(8)    VALUE 'RESERVE='.                    
               10  FILLER      PIC X(4)    VALUE 'MDPT'.                        
               10  FILLER      PIC X(4)    VALUE 'DHST'.                        
               10  FILLER      PIC X(4)    VALUE 'END.'.                        
      *                                                                         
      * DB2 FORMATTED MESSAGES                                                  
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      * DCLGEN LAYOUT FOR DB2 OPEN FISCAL MONTH CONTROL TABLE                   
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
W33304                                                                          
W33304* DCLGEN LAYOUT FOR DB2 INVENTORY CONTROL TABLE                           
W33304*                                                                         
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
W33304* DCLGEN LAYOUT FOR DB2 INVENTORY PARAMETERS TABLE                        
W33304*                                                                         
W33304     EXEC SQL                                                             
W33304         INCLUDE TINVPAR                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      * DCLGEN LAYOUT FOR DB2 DATE ATTRIBUTE TABLE                              
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      * DCLGEN LAYOUT FOR DB2 STORE TABLE                                       
      *                                                                         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      * COMMUNICATIONS AREA FOR DB2                                             
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    CURSOR DECLARATION USED FOR ACCESSING DB2 STORE TABLE                
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               DECLARE STORE_CURSOR CURSOR FOR                                  
W26600             SELECT LOC_NBR,                                              
W26600                    LOC_10_ABBR_NM                                        
W26600             FROM XMT_LOC                                                 
W26600             WHERE LOC_TYP_CDE = 'DS'                                     
W26600               AND (FINCL_CLO_DTE > :MLCNTL-OPN-FSCL-MN-DTE               
W26600                OR  FINCL_CLO_DTE IS NULL)                                
W33304               AND LOC_NBR IN (SELECT LOC_NBR                             
W33304                                 FROM TINVPAR                             
W33304                                WHERE LOC_INV_STAT_CDE = 'IN'             
W33304                                AND ACTL_FIN_BK_DTE = '9999-09-09'        
RONNA1****                            AND SCHD_PHY_CNT_CDE <> 'EI'              
W33304                                AND INV_ID = :INCNTL-INV-ID)              
W26600             ORDER BY LOC_NBR                                             
W26600             WITH UR                                                      
           END-EXEC.                                                            
                                                                                
W33304***************************************************************           
W33304*    CURSOR DECLARATION USED FOR ACCESSING ACTIVE INV CYCLES              
W33304***************************************************************           
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         DECLARE ACTIV_INV CURSOR FOR                                     
W33304             SELECT INV_ID,                                               
W33304                    INV_NM                                                
W33304             FROM TINCNTL                                                 
W33304             WHERE ACTV_IND = 'Y'                                         
W33304             ORDER BY OPN_FSCL_MN_DTE, INV_ID                             
W33304             WITH UR                                                      
W33304     END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-LINE.                                                          
           OPEN INPUT  SALES-FILE                                               
                OUTPUT SALES-REPORT.                                            
W33304     INITIALIZE SALES-TOTAL-TABLE                                         
W33304                ALL-STORES-TOTAL.                                         
W33304     PERFORM 0050-PROCESS-DATES.                                          
W33304     PERFORM 1200-READ-WEEKLY-SALES.                                      
W33304     PERFORM 0500-OPEN-ACTIV-INV.                                         
W33304     PERFORM 0600-READ-ACTIV-INV                                          
W33304       UNTIL PS-END-OF-ACTIV-INV.                                         
W33304     PERFORM 0700-CLOSE-ACTIV-INV.                                        
                                                                                
           CLOSE SALES-FILE                                                     
                 SALES-REPORT.                                                  
                                                                                
           GOBACK.                                                              
                                                                                
W33304 0050-PROCESS-DATES.                                                      
                                                                                
           ACCEPT SD-SYSTEM-DATE                                                
               FROM DATE.                                                       
           ACCEPT ST-SYSTEM-TIME                                                
               FROM TIME.                                                       
                                                                                
           MOVE SD-MM      TO DP132O-RUN-MONTH.                                 
           MOVE SD-DD      TO DP132O-RUN-DAY.                                   
           MOVE SD-YY      TO DP132O-RUN-YEAR.                                  
           MOVE ST-HR      TO DP132O-RUN-HOUR.                                  
           MOVE ST-MM      TO DP132O-RUN-MINUTE.                                
           MOVE 'INKRP144' TO DP132O-PROGRAM-NAME.                              
           MOVE 1          TO DP132O-REPORT-NUMBER.                             
                                                                                
           PERFORM 3000-PROCESS-OPN-FSCL-TABLE.                                 
                                                                                
           PERFORM 3100-GET-WEEK-BEGIN-DATE.                                    
           MOVE DTATTR-FSCL-WK-BEG-DTE TO PV-BEG-OF-WEEK-DATE.                  
           MOVE DTATTR-FSCL-WK-NBR TO PV-PROCESSING-WK-NBR.                     
                                                                                
           MOVE +1 TO PV-DAY-OF-WEEK-N.                                         
           PERFORM 3200-GET-WEEK-DATES                                          
              UNTIL PV-DAY-OF-WEEK-N > 7.                                       
W33304                                                                          
W33304 0100-MAIN-LOOP.                                                          
W33304                                                                          
W33304     PERFORM 1000-STORE-TABLE-PARA.                                       
W33304                                                                          
W33304     PERFORM 1100-PROCESS-FILE                                            
W33304        UNTIL PS-END-OF-SALES-FILE OR                                     
W33304              PS-INV-ID-BREAK-YES.                                        
W33304                                                                          
W33304     MOVE ZEROS TO STORE-SUB.                                             
W33304     MOVE +60 TO LINE-COUNT.                                              
W33304     PERFORM 1250-PRINT-STORES                                            
W33304        UNTIL STORE-SUB > STORE-TABLE-MAX.                                
W33304                                                                          
W33304     IF PS-STORE-EXISTS-SW-YES                                            
W33304        PERFORM 1120-PRINT-ALL-STORES                                     
W33304        SET PS-STORE-EXISTS-SW-NO TO TRUE                                 
W33304     END-IF                                                               
W33304                                                                          
W33304     SET PS-INV-ID-BREAK-NO TO TRUE.                                      
W33304                                                                          
W33304     INITIALIZE SALES-TOTAL-TABLE                                         
W33304                ALL-STORES-TOTAL                                          
W33304                TOTAL-FOR-WEEK.                                           
W33304                                                                          
W33304 1000-STORE-TABLE-PARA.                                                   
                                                                                
           PERFORM 1740-OPEN-STORE-TABLE.                                       
           PERFORM 1730-READ-STORE-TABLE.                                       
                                                                                
           PERFORM 1700-LOAD-STORE-TABLE                                        
               UNTIL PS-END-OF-STORE-TABLE.                                     
                                                                                
W33304     MOVE 'N' TO PS-END-OF-STORE-TABLE-SW.                                
                                                                                
           PERFORM 1750-CLOSE-STORE-TABLE.                                      
                                                                                
      **********************************************************                
      *  MAIN PROCESSING ROUTINE                                                
      **********************************************************                
       1100-PROCESS-FILE.                                                       
                                                                                
W33304     IF IN154-INV-ID NOT EQUAL INCNTL-INV-ID                              
W33304        SET PS-INV-ID-BREAK-YES TO TRUE                                   
W33304        CONTINUE                                                          
W33304     ELSE                                                                 
W33304       IF SD-HOLD-DATE = IN154-FIN-LIAB-DTE                               
W33304          CONTINUE                                                        
W33304       ELSE                                                               
W33304          MOVE IN154-FIN-LIAB-DTE TO SD-HOLD-DATE                         
W33304          PERFORM 3300-GET-DAY-NUMBER                                     
W33304       END-IF                                                             
                                                                                
      *---------------------------------------------------------                
W33304*  IN THE EVENT THAT THE IN154-FIN-LIAB-DTE FALL OUTSIDE                  
      *  OF THE CURRENT OPEN FISCAL WEEK.  THE SALES WILL BE                    
      *  INCLUDED IN THE REPORT UNDER THE CORRESPONDING DAY OF WK               
      *  COLUMN                                                                 
      *---------------------------------------------------------                
W33304       MOVE IN154-LOC-NBR TO STORE-SUB                                    
                                                                                
W33304       IF STORE-NUMBER(STORE-SUB) > 0                                     
W33304           MOVE DATE-INDX         TO DAY-INDX                             
W33304                                     A-STORES-INDX                        
W33304           COMPUTE PV-WORK-AMOUNT = IN154-SLS-REG-RTL-AMT                 
W33304                                  + IN154-SLS-PROMO-RTL-AMT               
W33304                                  + IN154-SLS-PROCLR-RTL-AMT              
W33304                                  + IN154-SLS-CLR-RTL-AMT                 
W33304                                  + IN154-SLS-OTH-RTL-AMT                 
W33304           ADD PV-WORK-AMOUNT                                             
W33304               TO DAILY-SALES (STORE-SUB, DAY-INDX)                       
W33304           ADD PV-WORK-AMOUNT                                             
W33304               TO ALL-STORES-SALES (A-STORES-INDX)                        
W33304       END-IF                                                             
W33304       PERFORM 1200-READ-WEEKLY-SALES                                     
W33304     END-IF.                                                              
      *****************************************************************         
      *                READ WEEKLY SALE AND INVENTORY FILE            *         
      *****************************************************************         
                                                                                
       1200-READ-WEEKLY-SALES.                                                  
                                                                                
W33304     READ SALES-FILE INTO IN154-UPDATE-REC                                
               AT END                                                           
               SET PS-END-OF-SALES-FILE TO TRUE.                                
                                                                                
      *****************************************************************         
      *                PRINT SALES TOTAL TABLE                        *         
      *****************************************************************         
       1250-PRINT-STORES.                                                       
           ADD 1 TO STORE-SUB.                                                  
           MOVE 1 TO DAY-INDX.                                                  
           MOVE SPACES TO DETAIL-LN                                             
W33304                    TOT-DETAIL-LN.                                        
           IF STORE-NUMBER (STORE-SUB) > 0                                      
W33304        SET PS-STORE-EXISTS-SW-YES TO TRUE                                
              IF LINE-COUNT > LINE-MAX                                          
                  PERFORM 1260-PRINT-HEADINGS                                   
              END-IF                                                            
              MOVE STORE-NUMBER (STORE-SUB) TO DET-STORE-NO                     
              MOVE STORE-NAME (STORE-SUB) TO DET-STORE-NAME                     
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-SUNDAY              
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-MONDAY              
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-TUESDAY             
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-WEDNESDAY           
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-THURSDAY            
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-FRIDAY              
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              ADD 1 TO DAY-INDX                                                 
              MOVE DAILY-SALES (STORE-SUB, DAY-INDX) TO DET-SATURDAY            
              ADD DAILY-SALES (STORE-SUB, DAY-INDX) TO TOTAL-FOR-WEEK           
              MOVE TOTAL-FOR-WEEK TO DET-TOTAL-FOR-WEEK                         
              MOVE ZEROS TO TOTAL-FOR-WEEK                                      
              MOVE DETAIL-LN TO PRT-DATA                                        
              WRITE PRT-LINE AFTER ADVANCING 1                                  
              ADD +1 TO LINE-COUNT                                              
           END-IF.                                                              
                                                                                
       1120-PRINT-ALL-STORES.                                                   
           MOVE ZEROES TO TOTAL-FOR-WEEK.                                       
           MOVE 1 TO A-STORES-INDX.                                             
W33304     MOVE 'CYCLE TOTALS' TO TOT-VERBIAGE.                                 
           IF LINE-COUNT > LINE-MAX                                             
               PERFORM 1260-PRINT-HEADINGS                                      
           END-IF.                                                              
           MOVE DASH-LN TO PRT-DATA.                                            
           WRITE PRT-LINE AFTER ADVANCING 1                                     
           ADD +1 TO LINE-COUNT                                                 
                                                                                
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-SUNDAY.             
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-MONDAY.             
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-TUESDAY.            
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-WEDNESDAY.          
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-THURSDAY.           
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-FRIDAY.             
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
           ADD 1 TO A-STORES-INDX.                                              
W33304     MOVE ALL-STORES-SALES (A-STORES-INDX) TO TOT-DET-SATURDAY.           
           ADD ALL-STORES-SALES (A-STORES-INDX) TO TOTAL-FOR-WEEK.              
W33304     MOVE TOTAL-FOR-WEEK TO TOT-DET-TOTAL-FOR-WEEK.                       
W33304     MOVE TOT-DETAIL-LN TO PRT-DATA.                                      
                                                                                
           WRITE PRT-LINE AFTER ADVANCING 2                                     
           ADD +2 TO LINE-COUNT.                                                
                                                                                
      *****************************************************************         
      *                PRINT HEADINGS                                 *         
      *****************************************************************         
       1260-PRINT-HEADINGS.                                                     
           ADD +1 TO PAGE-COUNT.                                                
                                                                                
           MOVE PAGE-COUNT    TO DP132O-PAGE-NUMBER.                            
           MOVE 1             TO DP132O-REPORT-NUMBER.                          
           MOVE DP132O-STANDRD-HEADER-132-COLS TO PRT-DATA.                     
           WRITE PRT-LINE AFTER ADVANCING PAGE.                                 
           MOVE +1 TO LINE-COUNT.                                               
                                                                                
           MOVE HEAD-LN1 TO PRT-DATA.                                           
           WRITE PRT-LINE AFTER ADVANCING 1.                                    
           ADD +1 TO LINE-COUNT.                                                
                                                                                
W33304     MOVE INCNTL-INV-ID      TO CYCLE-NO.                                 
W33304     MOVE INCNTL-INV-NM-TEXT TO CYCLE-NAME.                               
W33304     MOVE CYCL-HDG TO PRT-DATA.                                           
W33304     WRITE PRT-LINE AFTER ADVANCING 1.                                    
W33304     ADD +1 TO LINE-COUNT.                                                
W33304                                                                          
           MOVE HEAD-LN2 TO PRT-DATA.                                           
           WRITE PRT-LINE AFTER ADVANCING 1.                                    
           ADD +1 TO LINE-COUNT.                                                
                                                                                
           MOVE HEAD-LN3 TO PRT-DATA.                                           
           WRITE PRT-LINE AFTER ADVANCING 2.                                    
           ADD +2 TO LINE-COUNT.                                                
                                                                                
           MOVE ZEROES TO LINE-COUNT.                                           
                                                                                
      *****************************************************************         
      * 1700-LOAD-STORE-TABLE - LOADS THE STORE INFORMATION FROM THE  *         
      * DB2 STORE TABLE INTO A STORE TABLE.                           *         
      *****************************************************************         
       1700-LOAD-STORE-TABLE.                                                   
                                                                                
           IF STORE-SUB > STORE-TABLE-MAX                                       
               DISPLAY '** ABEND **'                                            
               DISPLAY 'INKRP144 '                                              
               DISPLAY 'TABLE FOR STORE TOTALS EXCEEDED'                        
               DISPLAY 'TABLE MUST BE EXPANDED'                                 
               DISPLAY 'CONTACT SYSTEMS AND PROGRAMMING'                        
               SET ABEND-4004-TABLE-ERROR TO TRUE                               
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
W26600     MOVE XMT00001-LOC-NBR  TO STORE-NUMBER (STORE-SUB).                  
W26600     MOVE XMT00001-LOC-10-ABBR-NM TO STORE-NAME (STORE-SUB).              
           MOVE 1 TO DAY-INDX.                                                  
           PERFORM                                                              
                   UNTIL DAY-INDX > +7                                          
               MOVE ZEROES TO DAILY-SALES (STORE-SUB, DAY-INDX)                 
               MOVE ZEROES TO ALL-STORES-SALES (DAY-INDX)                       
               ADD 1 TO DAY-INDX                                                
           END-PERFORM.                                                         
           PERFORM 1730-READ-STORE-TABLE.                                       
                                                                                
      *****************************************************************         
      * 1730-READ-STORE-TABLE - READS THE DB2 STORE TABLE             *         
      *****************************************************************         
       1730-READ-STORE-TABLE.                                                   
                                                                                
           EXEC SQL                                                             
               FETCH STORE_CURSOR                                               
W26600          INTO :XMT00001-LOC-NBR,                                         
W26600               :XMT00001-LOC-10-ABBR-NM                                   
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
W26600             MOVE XMT00001-LOC-NBR TO STORE-SUB                           
               WHEN SQLCODE =  +100                                             
               SET PS-END-OF-STORE-TABLE    TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '1730-READ-STORE-TABLE'                                 
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'READ DB2 STORE TABLE'                                  
                                            TO PV-DB2-FUNCTION                  
                   PERFORM 9999-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       1730-READ-STORE-TABLE-EXIT.                                              
           EXIT.                                                                
W33304                                                                          
W33304*****************************************************************         
W33304* 0500-OPEN-ACTIV-INV CURSOR                                              
W33304*****************************************************************         
W33304 0500-OPEN-ACTIV-INV.                                                     
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         OPEN ACTIV_INV                                                   
W33304     END-EXEC.                                                            
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE = +000                                              
W33304            CONTINUE                                                      
W33304         WHEN OTHER                                                       
W33304             MOVE '0500-OPEN-ACTIV-INV'                                   
W33304                                      TO PV-PARAGRAPH-NAME                
W33304             MOVE 'OPEN TINCNTL CURSOR'                                   
W33304                                      TO PV-DB2-FUNCTION                  
W33304             PERFORM 9999-DB2-ABEND                                       
W33304     END-EVALUATE.                                                        
W33304                                                                          
W33304 0500-OPEN-ACTIV-INV-EXIT.                                                
W33304     EXIT.                                                                
W33304                                                                          
W33304*****************************************************************         
W33304* 0600-READ-ACTIV-INV                                                     
W33304*****************************************************************         
W33304 0600-READ-ACTIV-INV.                                                     
W33304                                                                          
W33304     INITIALIZE INCNTL-INV-NM-TEXT.                                       
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         FETCH ACTIV_INV                                                  
W33304          INTO :INCNTL-INV-ID,                                            
W33304               :INCNTL-INV-NM                                             
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE = +000                                              
W33304             PERFORM 0100-MAIN-LOOP                                       
W33304         WHEN SQLCODE =  +100                                             
W33304             SET PS-END-OF-ACTIV-INV  TO TRUE                             
W33304         WHEN OTHER                                                       
W33304             MOVE '0600-READ-ACTIV-INV'                                   
W33304                                      TO PV-PARAGRAPH-NAME                
W33304             MOVE 'FETCH ACTIV_INV CURSOR'                                
W33304                                      TO PV-DB2-FUNCTION                  
W33304             PERFORM 9999-DB2-ABEND                                       
W33304     END-EVALUATE.                                                        
W33304                                                                          
W33304 0600-READ-ACTIV-INV-EXIT.                                                
W33304     EXIT.                                                                
W33304*****************************************************************         
W33304* 0700-CLOSE-ACTIV-INV                                                    
W33304*****************************************************************         
W33304 0700-CLOSE-ACTIV-INV.                                                    
W33304     EXEC SQL                                                             
W33304         CLOSE ACTIV_INV                                                  
W33304     END-EXEC.                                                            
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE = +000                                              
W33304             CONTINUE                                                     
W33304         WHEN OTHER                                                       
W33304             MOVE '0700-CLOSE-ACTIV-INV'                                  
W33304                                      TO PV-PARAGRAPH-NAME                
W33304             MOVE 'CLOSE ACTIV_INV CURSOR'                                
W33304                                      TO PV-DB2-FUNCTION                  
W33304             PERFORM 9999-DB2-ABEND                                       
W33304     END-EVALUATE.                                                        
W33304 0700-CLOSE-ACTIV-INV-EXIT.                                               
W33304     EXIT.                                                                
      *****************************************************************         
      * 1740-OPEN-STORE-TABLE - OPENS THE DB2 STORE TABLE             *         
      *****************************************************************         
       1740-OPEN-STORE-TABLE.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN STORE_CURSOR                                                
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                  CONTINUE                                                      
               WHEN OTHER                                                       
                   MOVE '1740-OPEN-STORE-TABLE'                                 
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN DB2 STORE TABLE'                                  
                                            TO PV-DB2-FUNCTION                  
                   PERFORM 9999-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       1740-OPEN-STORE-TABLE-EXIT.                                              
           EXIT.                                                                
                                                                                
      *****************************************************************         
      * 1750-CLOSE-STORE-TABLE - CLOSES THE DB2 STORE TABLE           *         
      *****************************************************************         
       1750-CLOSE-STORE-TABLE.                                                  
           EXEC SQL                                                             
               CLOSE STORE_CURSOR                                               
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1750-CLOSE-STORE-TABLE'                                
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'CLOSE DB2 STORE TABLE'                                 
                                            TO PV-DB2-FUNCTION                  
                   PERFORM 9999-DB2-ABEND                                       
           END-EVALUATE.                                                        
       1750-CLOSE-STORE-TABLE-EXIT.                                             
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *    READ THE OPEN FISCAL MONTH FILE                                      
      ***************************************************************           
       3000-PROCESS-OPN-FSCL-TABLE.                                             
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                        OPN_FSCL_MN_DTE                                         
                       ,OPN_FSCL_MN_NBR                                         
                       ,OPN_FSCL_WK_DTE                                         
                    INTO                                                        
                       :MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                      ,:MLCNTL-OPN-FSCL-WK-DTE                                  
                    FROM                                                        
                        TMLCNTL                                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '3000-PROCESS-OPN-FSCL-TABLE'                    
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9999-DB2-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE    '3000-PROCESS-OPN-FSCL-TABLE'                            
                                                TO PV-PARAGRAPH-NAME            
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
      ***************************************************************           
      *    GET OPEN WEEK BEGIN DATES AND DAY-OF-WEEK DATES                      
      ***************************************************************           
       3100-GET-WEEK-BEGIN-DATE.                                                
                                                                                
               EXEC SQL                                                         
                    SELECT  FSCL_WK_NBR                                         
                           ,FSCL_WK_BEG_DTE                                     
                    INTO   :DTATTR-FSCL-WK-NBR                                  
                          ,:DTATTR-FSCL-WK-BEG-DTE                              
                    FROM    TDTATTR                                             
                    WHERE KY_DTE = :MLCNTL-OPN-FSCL-WK-DTE                      
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY 'MLCNTL-OPN-FSCL-WK-DTE: '                       
                                MLCNTL-OPN-FSCL-WK-DTE                          
                       MOVE    '3100-GET-WEEK-BEGIN-DATE'                       
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TDTATTR'                        
                                           TO PV-DB2-OPER-ATTEMPTED             
                       PERFORM 9999-DB2-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
       3200-GET-WEEK-DATES.                                                     
            EXEC SQL                                                            
                 SELECT  KY_DTE                                                 
                 INTO   :DTATTR-KY-DTE                                          
                 FROM    TDTATTR                                                
                 WHERE FSCL_WK_END_DTE = :MLCNTL-OPN-FSCL-WK-DTE                
                 AND   FSCL_WK_BEG_DTE = :PV-BEG-OF-WEEK-DATE                   
                 AND   DAY_OF_WK_NBR   = :PV-DAY-OF-WEEK                        
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
                    CONTINUE                                                    
                WHEN OTHER                                                      
                    DISPLAY 'MLCNTL-OPN-FSCL-WK-DTE = '                         
                             MLCNTL-OPN-FSCL-WK-DTE                             
                    DISPLAY 'PV-BEG-OF-WEEK-DATE = '                            
                             PV-BEG-OF-WEEK-DATE                                
                    DISPLAY 'PV-DAY-OF-WEEK = '                                 
                             PV-DAY-OF-WEEK                                     
                    MOVE    '3200-GET-WEEK-DATES'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE    'SELECT ROW FROM TDTATTR'                           
                                        TO PV-DB2-OPER-ATTEMPTED                
                    PERFORM 9999-DB2-ABEND                                      
            END-EVALUATE.                                                       
                                                                                
            MOVE DTATTR-KY-DTE TO PV-IN-DATE.                                   
            MOVE PV-IN-DATE-YY TO PV-OUT-DATE-YY.                               
            MOVE PV-IN-DATE-MM TO PV-OUT-DATE-MM.                               
            MOVE PV-IN-DATE-DD TO PV-OUT-DATE-DD.                               
                                                                                
            EVALUATE PV-DAY-OF-WEEK                                             
               WHEN '1'                                                         
                  MOVE PV-OUT-DATE               TO HD-SUNDAY                   
               WHEN '2'                                                         
                  MOVE PV-OUT-DATE               TO HD-MONDAY                   
               WHEN '3'                                                         
                  MOVE PV-OUT-DATE               TO HD-TUESDAY                  
               WHEN '4'                                                         
                  MOVE PV-OUT-DATE               TO HD-WEDNESDAY                
               WHEN '5'                                                         
                  MOVE PV-OUT-DATE               TO HD-THURSDAY                 
               WHEN '6'                                                         
                  MOVE PV-OUT-DATE               TO HD-FRIDAY                   
               WHEN '7'                                                         
                  MOVE PV-OUT-DATE               TO HD-SATURDAY                 
            END-EVALUATE.                                                       
                                                                                
            ADD 1 TO PV-DAY-OF-WEEK-N.                                          
                                                                                
       3300-GET-DAY-NUMBER.                                                     
                                                                                
W33304     MOVE IN154-FIN-LIAB-DTE  TO DTATTR-KY-DTE.                           
W20445                                                                          
            EXEC SQL                                                            
                SELECT FSCL_WK_NBR                                              
                      ,DAY_OF_WK_NBR                                            
                INTO :DTATTR-FSCL-WK-NBR                                        
                    ,:DTATTR-DAY-OF-WK-NBR                                      
                FROM TDTATTR                                                    
W20445          WHERE KY_DTE = :DTATTR-KY-DTE                                   
W20445          WITH UR                                                         
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
W33304              MOVE DTATTR-DAY-OF-WK-NBR TO DATE-INDX                      
                WHEN OTHER                                                      
                    MOVE    '3300-GET-DAY-NUMBER'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE    'SELECT ROW FROM TDTATTR'                           
                                        TO PV-DB2-OPER-ATTEMPTED                
                    PERFORM 9999-DB2-ABEND                                      
            END-EVALUATE.                                                       
      *********************************************************                 
      * 9999-DB2-ABEND - DB2 ABEND-ROUTINE                    *                 
      *********************************************************                 
       9999-DB2-ABEND.                                                          
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PROGRAM ** = INKRP144'.                                  
           DISPLAY '** PARAGRAPH ** ' PV-PARAGRAPH-NAME.                        
           DISPLAY '** OPERATION ** ' PV-DB2-FUNCTION.                          
      *                                                                         
      * DISPLAY SQLCA ABEND MESSAGES IN A READABLE FORMAT                       
      *                                                                         
           COPY DPPD004.                                                        
           SET ABEND-4013-DB2-ERROR         TO TRUE.                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
