       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT088.                                                 
       AUTHOR.        WADE GROVE.                                               
       DATE-WRITTEN.  06/30/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ** THIS PROGRAM PRINTS A REPORT SHOWING EDI PROD VENDORS WITH NO          
      ** PROD ACTIVITY WITHIN THE LAST 12 MONTHS AND NO PO'S HAVE BEEN          
      ** WRITTEN. THE VENDOR RANK IS ALSO SHOWN ON THE REPORT.                  
      **                                                                        
      **  IF A NEW NETWORK ID IS ADDED, THE TABLE WS-NETWORK-TABLE              
      **  IN THIS PROGRAM MUST BE UPDATED.                                      
      **                                                                        
      **  THIS PROGRAM WAS A RE-VAMP OF THE ORIGINAL PROGRAM THAT WAS           
      **  WRITTEN BY TIA JOHNSTON IN 1999.                                      
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT REPORT-FILE                                                   
               ASSIGN TO RPT01.                                                 
                                                                                
           SELECT EXTRACT-FILE                                                  
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-RECORD.                                        
                                                                                
       01  REPORT-RECORD               PIC X(132).                              
                                                                                
       FD  EXTRACT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS EXTRACT-RECORD.                                       
                                                                                
       01  EXTRACT-RECORD.                                                      
           05  EXT-TP-DKEY             PIC 9(09).                               
           05  FILLER                  PIC X(71).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DATES-AND-TIME.                                                   
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURRENT-YY       PIC 9(2)  VALUE ZERO.                    
               10  WS-CURRENT-MM       PIC 9(2)  VALUE ZERO.                    
               10  WS-CURRENT-DD       PIC 9(2)  VALUE ZERO.                    
           05  WS-CURRENT-TIME.                                                 
               10  WS-CURRENT-HR       PIC 9(2)  VALUE ZERO.                    
               10  WS-CURRENT-MIN      PIC 9(2)  VALUE ZERO.                    
               10  FILLER              PIC 9(4)  VALUE ZERO.                    
                                                                                
       01  WS-MISC.                                                             
           05  WS-LINE-CNT   COMP-3    PIC S9(5) VALUE +57.                     
           05  WS-PAGE-CNT   COMP-3    PIC S9(5) VALUE ZERO.                    
           05  WS-LINE-MAX             PIC S9(5) VALUE +55.                     
           05  WS-NTWK-MAX             PIC S9(5) VALUE +3.                      
           05  ABEND-CODE    COMP SYNC PIC S9(4) VALUE +0.                      
           05  WS-810-CNT    COMP-3    PIC S9(7) VALUE +0.                      
           05  WS-820-CNT    COMP-3    PIC S9(7) VALUE +0.                      
           05  WS-850-CNT    COMP-3    PIC S9(7) VALUE +0.                      
           05  WS-852-CNT    COMP-3    PIC S9(7) VALUE +0.                      
           05  WS-855-CNT    COMP-3    PIC S9(5) VALUE +0.                      
           05  WS-856-CNT    COMP-3    PIC S9(5) VALUE +0.                      
           05  WS-860-CNT    COMP-3    PIC S9(5) VALUE +0.                      
           05  WS-870-CNT    COMP-3    PIC S9(5) VALUE +0.                      
           05  WS-TRAN-SET-CDE         PIC  X(3) VALUE SPACES.                  
           05  WS-LAST-PO-DATE-IND     PIC S9(04)  COMP.                        
           05  WS-LAST-PO-DATE         PIC  X(10).                              
           05  WS-CUTOFF-DATE          PIC  X(10).                              
           05  WS-NULL-IND             PIC S9(04)  COMP.                        
           05  WS-DOC-COUNT            PIC S9(5) VALUE +0.                      
           05  WS-SHARED-ID-TP-DKEY    PIC S9(9) VALUE +0.                      
                                                                                
       01  WS-TMST.                                                             
           05  WS-TMST-CC              PIC X(02).                               
           05  WS-TMST-YY              PIC X(02).                               
           05  FILLER                  PIC X(01).                               
           05  WS-TMST-MM              PIC X(02).                               
           05  FILLER                  PIC X(01).                               
           05  WS-TMST-DD              PIC X(02).                               
           05  FILLER                  PIC X(16).                               
                                                                                
       01  WS-SENT-DATE.                                                        
           05  WS-SENT-MM              PIC 9(2).                                
           05  FILLER                  PIC X(1)  VALUE '/'.                     
           05  WS-SENT-DD              PIC 9(2).                                
           05  FILLER                  PIC X(1)  VALUE '/'.                     
           05  WS-SENT-YY              PIC 9(2).                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA                 PIC X(30).                               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30).                               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-VEND-CSR-SW      PIC X     VALUE 'N'.                     
               88  END-VEND-CSR                  VALUE 'Y'.                     
           05  WS-END-SHARED-ID-CSR-SW PIC X     VALUE 'N'.                     
               88  END-SHARED-ID-CSR             VALUE 'Y'.                     
           05  WS-NETWORK-FND-SW       PIC X     VALUE 'N'.                     
               88  NETWK-NOT-FND                 VALUE 'N'.                     
               88  NETWK-FND                     VALUE 'Y'.                     
           05  WS-PROD-ACT-SW          PIC X     VALUE 'N'.                     
               88  NO-PROD-ACT-EXISTS            VALUE 'N'.                     
               88  PROD-ACT-EXISTS               VALUE 'Y'.                     
           05  WS-PROD-SET-SW          PIC X     VALUE 'N'.                     
               88  NO-PROD-SET-EXISTS            VALUE 'N'.                     
               88  PROD-SET-EXISTS               VALUE 'Y'.                     
           05  WS-PRINT-SW             PIC X     VALUE 'N'.                     
               88  NO-PRINT                      VALUE 'N'.                     
               88  PRINT                         VALUE 'Y'.                     
                                                                                
       01  WS-SUBSCRIPTS.                                                       
           05  NTWK-SUB                PIC S9(3) VALUE +0                       
                                                 COMP-3.                        
                                                                                
      *************************************************************             
      * ADD TO THIS TABLE IF A NEW NETWORK ID IS ADDED                          
      * THIS TABLE CONTAINS NETWORK ID AND NETWORK NAME                         
      *************************************************************             
       01  WS-NETWORK-TABLE.                                                    
           05  FILLER                  PIC X(4)  VALUE '1GXS'.                  
           05  FILLER                  PIC X(4)  VALUE '2SRS'.                  
           05  FILLER                  PIC X(4)  VALUE 'NIPN'.                  
       01  WS-NETWORK-ID-TABLE REDEFINES WS-NETWORK-TABLE.                      
           05 WS-NETWORK-ID-ENTRY OCCURS 3 TIMES.                               
              10  WS-NETWORK-ID        PIC X.                                   
              10  WS-NETWORK-NAME      PIC X(3).                                
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  REPORT-HEADING1.                                                     
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(43) VALUE                          
               'EDI PRODUCTION VENDORS INACTIVATED WITH NO '.                   
           05  FILLER                  PIC X(19) VALUE                          
               'EDI ACTIVITY AND NO'.                                           
           05  FILLER                  PIC X(49) VALUE                          
               ' PO''S HAVE BEEN WRITTEN IN THE LAST TWELVE MONTHS'.            
           05  FILLER                  PIC X(08) VALUE                          
               ' BY RANK'.                                                      
           05  FILLER                  PIC X(07) VALUE SPACES.                  
                                                                                
       01  REPORT-HEADING2.                                                     
           05  FILLER                  PIC X(58) VALUE SPACES.                  
           05  FILLER                  PIC X(31) VALUE                          
               'INV RA  PO  PA  POA ASN POC OSR'.                               
           05  FILLER                  PIC X(43) VALUE SPACES.                  
                                                                                
       01  REPORT-HEADING3.                                                     
           05  FILLER                  PIC X(04) VALUE 'RANK'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'VENDOR'.                
           05  FILLER                  PIC X(21) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'PARTNER ID'.            
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  FILLER                  PIC X(40) VALUE                          
               'QUAL NET 810 820 850 852 855 856 860 870'.                      
           05  FILLER                  PIC X(43) VALUE SPACES.                  
                                                                                
       01  REPORT-HEADING4.                                                     
           05  FILLER                  PIC X(58) VALUE SPACES.                  
           05  FILLER                  PIC X(31) VALUE                          
               '--- --- --- --- --- --- --- ---'.                               
           05  FILLER                  PIC X(43) VALUE SPACES.                  
                                                                                
       01  REPORT-DETAIL-LINE.                                                  
           05  RPT-RANK                PIC ZZZZ.                                
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  RPT-VENDOR-NAME         PIC X(26).                               
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-PARTNER-ID          PIC X(15).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  RPT-QUALIFIER           PIC X(02).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  RPT-NETWORK             PIC X(03).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  RPT-810                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-820                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-850                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-852                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-855                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-856                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-860                 PIC X(01).                               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  RPT-870                 PIC X(01).                               
           05  FILLER                  PIC X(45) VALUE SPACES.                  
                                                                                
       01  REPORT-TOTAL-LINE1.                                                  
           05  FILLER                  PIC X(58) VALUE SPACES.                  
           05  FILLER                  PIC X(31) VALUE                          
               '--- --- --- --- --- --- --- ---'.                               
           05  FILLER                  PIC X(43) VALUE SPACES.                  
                                                                                
                                                                                
       01  REPORT-TOTAL-LINE2.                                                  
           05  FILLER                  PIC X(58) VALUE SPACES.                  
           05  RPT-TOT-810             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-820             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-850             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-852             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACE.                   
           05  RPT-TOT-855             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-856             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-860             PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  RPT-TOT-870             PIC ZZZ.                                 
           05  FILLER                  PIC X(43) VALUE SPACES.                  
                                                                                
       01  REPORT-FOOTNOTE.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(53) VALUE                          
               '* NOTE: REPORT ONLY DISPLAYS PRODUCTION RELATIONSHIPS'.         
           05  FILLER                  PIC X(51) VALUE                          
               ' IF THERE IS NO PRODUCTION EDI ACTIVITY IN LAST 12 '.           
           05  FILLER                  PIC X(07) VALUE 'MONTHS.'.               
           05  FILLER                  PIC X(16) VALUE SPACES.                  
                                                                                
           COPY DPWS132O.                                                       
                                                                                
           COPY SQLCA2.                                                         
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TEXTENT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TINTTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TFGTRST                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TPURCHS                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    VENDOR CURSOR                                                        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE VENDOR_CSR CURSOR FOR                                      
               SELECT A.TP_DKEY,                                                
                      A.VEND_INCHG_ID,                                          
                      A.VEND_INCHG_ID_DESC,                                     
                      A.NETWK_ID,                                               
                      A.ENT_ID,                                                 
                      B.ENT_NAME_DESC,                                          
                      C.PFY_SLSRNK_NBR,                                         
                      C.VLD_CART_LBL_IND                                        
               FROM TTRDPRT A,                                                  
                    TENTNME B,                                                  
                    TEXTENT C                                                   
               WHERE A.INACTV_DTE = '9999-09-09'                                
                 AND A.ENT_ID = B.ENT_ID                                        
                 AND B.ENT_ID = C.ENT_ID                                        
                 AND B.TYPE_CDE = '01'                                          
                 AND C.MDSE_VENDOR_IND = 'Y'                                    
               ORDER BY B.ENT_NAME_DESC                                         
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    SHARED_ID CURSOR                                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE SHARED_ID_CSR CURSOR FOR                                   
                     SELECT   B.TP_DKEY                                         
                     FROM     TTRDMLT A,                                        
                              TTRDPRT B,                                        
                              TENTNME C                                         
                     WHERE    B.VEND_INCHG_ID_DESC = A.VND_INCHG_ID_DESC        
                       AND    B.VEND_INCHG_ID = A.VND_INCHG_ID                  
                       AND    B.ENT_ID = C.ENT_ID                               
                       AND    C.TYPE_CDE = '01'                                 
                       AND    INACTV_DTE = '9999-09-09'                         
                       AND    B.VEND_INCHG_ID_DESC =                            
                              :TRDPRT-VEND-INCHG-ID-DESC                        
                       AND    B.VEND_INCHG_ID =                                 
                              :TRDPRT-VEND-INCHG-ID                             
                       AND    A.PRNT_ENT_ID <> C.ENT_ID                         
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 8000-OPEN-VEND-CURSOR THRU 8000-EXIT.                        
                                                                                
           PERFORM 2000-PROCESS-VEND THRU 2000-EXIT                             
              UNTIL END-VEND-CSR.                                               
                                                                                
           PERFORM 3500-PRINT-TOTAL-LINE THRU 3500-EXIT.                        
                                                                                
           PERFORM 8010-CLOSE-VEND-CURSOR THRU 8010-EXIT.                       
                                                                                
           CLOSE REPORT-FILE                                                    
                 EXTRACT-FILE.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN OUTPUT REPORT-FILE                                              
                       EXTRACT-FILE.                                            
           MOVE SPACES TO REPORT-DETAIL-LINE                                    
                          REPORT-TOTAL-LINE2.                                   
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE.                                    
           MOVE WS-CURRENT-YY     TO DP132O-RUN-YEAR.                           
           MOVE WS-CURRENT-MM     TO DP132O-RUN-MONTH.                          
           MOVE WS-CURRENT-DD     TO DP132O-RUN-DAY.                            
           ACCEPT WS-CURRENT-TIME FROM TIME.                                    
           MOVE WS-CURRENT-HR     TO DP132O-RUN-HOUR.                           
           MOVE WS-CURRENT-MIN    TO DP132O-RUN-MINUTE.                         
           MOVE 'ETKUT088'        TO DP132O-PROGRAM-NAME.                       
           MOVE 1                 TO DP132O-REPORT-NUMBER.                      
                                                                                
           EXEC SQL                                                             
                SELECT CURRENT DATE - 12 MONTHS                                 
                INTO   :WS-CUTOFF-DATE                                          
                FROM   TCOCNTL                                                  
           END-EXEC.                                                            
                                                                                
           DISPLAY 'CUTOFF-DATE IS ' WS-CUTOFF-DATE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-VEND.                                                       
                                                                                
           PERFORM 8005-FETCH-VEND THRU 8005-EXIT.                              
                                                                                
           IF END-VEND-CSR                                                      
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 2300-GET-LAST-PO-DATE THRU 2300-EXIT.                        
           IF WS-LAST-PO-DATE-IND = -1                                          
              GO TO 2000-EXIT                                                   
           ELSE                                                                 
              IF WS-LAST-PO-DATE >= WS-CUTOFF-DATE                              
                 GO TO 2000-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-NEW-VENDOR THRU 2100-EXIT.                              
                                                                                
           MOVE '810' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-810                                            
                 ADD +1 TO WS-810-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '820' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-820                                            
                 ADD +1 TO WS-810-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '850' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-850                                            
                 ADD +1 TO WS-850-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '852' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-852                                            
                 ADD +1 TO WS-852-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '855' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-855                                            
                 ADD +1 TO WS-855-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '856' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-856                                            
                 ADD +1 TO WS-856-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '860' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-860                                            
                 ADD +1 TO WS-860-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE '870' TO WS-TRAN-SET-CDE.                                       
           PERFORM 2200-GET-PROD-SET THRU 2200-EXIT.                            
           IF PROD-SET-EXISTS                                                   
              PERFORM 2400-GET-PROD-ACT THRU 2400-EXIT                          
              IF NO-PROD-ACT-EXISTS                                             
                 MOVE 'Y' TO RPT-870                                            
                 ADD +1 TO WS-870-CNT                                           
                 SET PRINT TO TRUE                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-DOC-COUNT = 0                                                  
              PERFORM 2500-CHECK-FOR-SHARED-ID.                                 
                                                                                
           IF WS-DOC-COUNT = 0                                                  
              PERFORM 3000-PRINT-VENDOR THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-NEW-VENDOR.                                                         
                                                                                
           MOVE EXTENT-PFY-SLSRNK-NBR     TO RPT-RANK.                          
           MOVE ENTNME-ENT-NAME-DESC      TO RPT-VENDOR-NAME.                   
           MOVE TRDPRT-VEND-INCHG-ID      TO RPT-PARTNER-ID.                    
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO RPT-QUALIFIER.                     
           SET NO-PRINT                   TO TRUE.                              
           SET NETWK-NOT-FND              TO TRUE.                              
           MOVE 0 TO WS-DOC-COUNT.                                              
                                                                                
           PERFORM 2110-FIND-NETWORK-NAME THRU 2110-EXIT                        
              VARYING NTWK-SUB FROM 1 BY 1                                      
              UNTIL NETWK-FND OR NTWK-SUB > WS-NTWK-MAX.                        
                                                                                
           MOVE 'N' TO RPT-810                                                  
                       RPT-820                                                  
                       RPT-850                                                  
                       RPT-852                                                  
                       RPT-855                                                  
                       RPT-856                                                  
                       RPT-860                                                  
                       RPT-870.                                                 
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-FIND-NETWORK-NAME.                                                  
                                                                                
           IF TRDPRT-NETWK-ID = WS-NETWORK-ID (NTWK-SUB)                        
              MOVE WS-NETWORK-NAME (NTWK-SUB) TO RPT-NETWORK                    
              SET NETWK-FND TO TRUE                                             
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2200-GET-PROD-SET.                                                       
                                                                                
           SET NO-PROD-SET-EXISTS TO TRUE.                                      
                                                                                
           MOVE '2200-'              TO PV-PARA.                                
           MOVE 'GET PROD SET'       TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
               SELECT DISTINCT TRAN_SET_CDE,                                    
                      VER_NBR                                                   
               INTO :TRDTRN-TRAN-SET-CDE,                                       
                    :TRDTRN-VER-NBR                                             
               FROM TTRDTRN                                                     
               WHERE TP_DKEY = :TRDPRT-TP-DKEY                                  
                 AND INACTV_DTE = '9999-09-09'                                  
                 AND TRAN_SET_CDE = :WS-TRAN-SET-CDE                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = -811                                              
                   SET PROD-SET-EXISTS TO TRUE                                  
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2300-GET-LAST-PO-DATE.                                                   
                                                                                
           MOVE '2300-'              TO PV-PARA.                                
           MOVE 'GET LAST PO DATE'   TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
                SELECT MAX (ORD_DTE)                                            
                  INTO :WS-LAST-PO-DATE:WS-LAST-PO-DATE-IND                     
                  FROM TPURCHS                                                  
                 WHERE ENT_ID = :TRDPRT-ENT-ID AND                              
                       VER_STATUS_CDE = 'A' AND                                 
                       PO_CATEG_CDE = 'MR' AND                                  
                       STATUS_CDE IN ('AC', 'CM', 'AP', 'CA')                   
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                 WHEN SQLCODE = ZERO                                            
                 WHEN SQLCODE = +100                                            
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -913                                            
                      CONTINUE                                                  
                 WHEN OTHER                                                     
                      PERFORM 9000-SQL-ABEND THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2400-GET-PROD-ACT.                                                       
                                                                                
           SET PROD-ACT-EXISTS     TO TRUE.                                     
           MOVE '2400-'            TO PV-PARA.                                  
           MOVE 'GET PROD ACTV'    TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
               SELECT MAX(A.CRE_DTE)                                            
               INTO :INTTRN-CRE-DTE:WS-NULL-IND                                 
               FROM TINTTRN A,                                                  
                    TFGTRST B                                                   
               WHERE A.TP_DKEY = :TRDPRT-TP-DKEY                                
                 AND A.CRE_DTE > :WS-CUTOFF-DATE                                
                 AND A.TP_DKEY = B.TP_DKEY                                      
                 AND A.TP_DKEY_TMST = B.TP_DKEY_TMST                            
                 AND B.TRN_SET_CDE = :WS-TRAN-SET-CDE                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0 AND WS-NULL-IND NOT = -1                       
                   SET PROD-ACT-EXISTS TO TRUE                                  
                   ADD 1 TO WS-DOC-COUNT                                        
               WHEN SQLCODE = +0 AND WS-NULL-IND = -1                           
               WHEN SQLCODE = +0                                                
                   SET NO-PROD-ACT-EXISTS TO TRUE                               
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2500-CHECK-FOR-SHARED-ID.                                                
                                                                                
           DISPLAY TRDPRT-VEND-INCHG-ID-DESC '-'                                
                   TRDPRT-VEND-INCHG-ID.                                        
                                                                                
           PERFORM 8100-OPEN-SHARED-ID-CURSOR.                                  
           MOVE 'N' TO WS-END-SHARED-ID-CSR-SW.                                 
           PERFORM 2600-PROCESS-SHARED-ID THRU 2600-EXIT                        
                   UNTIL END-SHARED-ID-CSR.                                     
           PERFORM 8110-CLOSE-SHARED-ID-CURSOR.                                 
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2600-PROCESS-SHARED-ID.                                                  
                                                                                
           PERFORM 8105-FETCH-SHARED-ID THRU 8105-EXIT.                         
                                                                                
           IF END-SHARED-ID-CSR                                                 
              GO TO 2600-EXIT.                                                  
                                                                                
           DISPLAY '     SHARED ID ' TRDPRT-TP-DKEY.                            
                                                                                
           MOVE '2600-'            TO PV-PARA.                                  
           MOVE 'GET SHARED-ID ACTV'    TO PV-DB2-OPER-ATTEMPTED.               
                                                                                
           EXEC SQL                                                             
                SELECT MAX(A.CRE_DTE)                                           
                INTO :INTTRN-CRE-DTE:WS-NULL-IND                                
                FROM TINTTRN A,                                                 
                     TFGTRST B,                                                 
                     TTRDPRT C,                                                 
                     TENTNME D                                                  
                WHERE A.TP_DKEY =:TRDPRT-TP-DKEY                                
                  AND A.TP_DKEY = B.TP_DKEY                                     
                  AND A.TP_DKEY = B.TP_DKEY                                     
                  AND A.TP_DKEY_TMST = B.TP_DKEY_TMST                           
                  AND A.TP_DKEY = C.TP_DKEY                                     
                  AND C.ENT_ID = D.ENT_ID                                       
                  AND D.TYPE_CDE = '01'                                         
                  AND A.CRE_DTE > :WS-CUTOFF-DATE                               
                  AND B.TRN_SET_CDE IN                                          
                      ('810','820','850','852','855','856','860','870')         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0 AND WS-NULL-IND NOT = -1                       
                   ADD 1 TO WS-DOC-COUNT                                        
                   DISPLAY 'DOCS FOUND FOR ' TRDPRT-TP-DKEY                     
               WHEN SQLCODE = +0 AND WS-NULL-IND = -1                           
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PRINT-VENDOR.                                                       
                                                                                
           IF WS-LINE-CNT > WS-LINE-MAX                                         
              PERFORM 3010-PRINT-HEADINGS THRU 3010-EXIT.                       
                                                                                
           WRITE REPORT-RECORD FROM REPORT-DETAIL-LINE                          
              AFTER ADVANCING 1 LINE.                                           
           ADD +1 TO WS-LINE-CNT.                                               
           MOVE SPACES TO REPORT-DETAIL-LINE.                                   
                                                                                
           MOVE TRDPRT-TP-DKEY TO EXT-TP-DKEY.                                  
           WRITE EXTRACT-RECORD.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3010-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO WS-PAGE-CNT.                                                
           MOVE WS-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
                                                                                
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS              
              AFTER ADVANCING PAGE.                                             
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING1                             
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING2                             
              AFTER ADVANCING 2 LINES.                                          
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING3                             
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING4                             
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           MOVE +6 TO WS-LINE-CNT.                                              
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-PRINT-TOTAL-LINE.                                                   
                                                                                
           MOVE WS-810-CNT TO RPT-TOT-810.                                      
           MOVE WS-820-CNT TO RPT-TOT-820.                                      
           MOVE WS-850-CNT TO RPT-TOT-850.                                      
           MOVE WS-852-CNT TO RPT-TOT-852.                                      
           MOVE WS-855-CNT TO RPT-TOT-855.                                      
           MOVE WS-856-CNT TO RPT-TOT-856.                                      
           MOVE WS-860-CNT TO RPT-TOT-860.                                      
           MOVE WS-870-CNT TO RPT-TOT-870.                                      
           WRITE REPORT-RECORD FROM REPORT-TOTAL-LINE1                          
              AFTER ADVANCING 2 LINES.                                          
           WRITE REPORT-RECORD FROM REPORT-TOTAL-LINE2                          
              AFTER ADVANCING 1 LINE.                                           
           WRITE REPORT-RECORD FROM REPORT-FOOTNOTE                             
              AFTER ADVANCING 2 LINES.                                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    OPEN VENDOR CURSOR                                                   
      *****************************************************************         
                                                                                
       8000-OPEN-VEND-CURSOR.                                                   
                                                                                
           MOVE '8000-'              TO PV-PARA                                 
           MOVE 'OPEN VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN VENDOR_CSR                                                   
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    FETCH VENDOR CURSOR                                                  
      *****************************************************************         
                                                                                
       8005-FETCH-VEND.                                                         
                                                                                
           MOVE '8005-'            TO PV-PARA                                   
           MOVE 'FETCH VENDOR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
             FETCH VENDOR_CSR                                                   
             INTO :TRDPRT-TP-DKEY,                                              
                  :TRDPRT-VEND-INCHG-ID,                                        
                  :TRDPRT-VEND-INCHG-ID-DESC,                                   
                  :TRDPRT-NETWK-ID,                                             
                  :TRDPRT-ENT-ID,                                               
                  :ENTNME-ENT-NAME-DESC,                                        
                  :EXTENT-PFY-SLSRNK-NBR,                                       
                  :EXTENT-VLD-CART-LBL-IND                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET END-VEND-CSR TO TRUE                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    CLOSE VENDOR CURSOR                                                  
      *****************************************************************         
                                                                                
       8010-CLOSE-VEND-CURSOR.                                                  
                                                                                
           MOVE '8010-'            TO PV-PARA                                   
           MOVE 'CLOSE VENDOR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
              CLOSE VENDOR_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    OPEN SHARED-ID CURSOR                                                
      *****************************************************************         
                                                                                
       8100-OPEN-SHARED-ID-CURSOR.                                              
                                                                                
           MOVE '8100-'              TO PV-PARA                                 
           MOVE 'OPEN SHARED ID CURSOR' TO PV-DB2-OPER-ATTEMPTED.               
                                                                                
           EXEC SQL                                                             
              OPEN SHARED_ID_CSR                                                
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    FETCH SHARED-ID CURSOR                                               
      *****************************************************************         
                                                                                
       8105-FETCH-SHARED-ID.                                                    
                                                                                
           MOVE '8105-'            TO PV-PARA                                   
           MOVE 'FETCH SHARED-ID CSR' TO PV-DB2-OPER-ATTEMPTED.                 
                                                                                
           EXEC SQL                                                             
             FETCH SHARED_ID_CSR                                                
             INTO :TRDPRT-TP-DKEY                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET END-SHARED-ID-CSR TO TRUE                                
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8105-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    CLOSE SHARED-ID CURSOR                                               
      *****************************************************************         
                                                                                
       8110-CLOSE-SHARED-ID-CURSOR.                                             
                                                                                
           MOVE '8110-'            TO PV-PARA                                   
           MOVE 'CLOSE SHARED-ID CURSOR' TO PV-DB2-OPER-ATTEMPTED.              
                                                                                
           EXEC SQL                                                             
              CLOSE SHARED_ID_CSR                                               
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    SQL ABEND ROUTINE                                                    
      *****************************************************************         
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ETKUT088'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND THRU 9999-EXIT.                                   
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'             USING ABEND-CODE.                        
       9999-EXIT.                                                               
           EXIT.                                                                
