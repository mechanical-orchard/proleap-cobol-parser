000001 IDENTIFICATION DIVISION.                                                 
000002                                                                          
000003 PROGRAM-ID.             RVKRP019.                                        
000004 AUTHOR.                 VIRGENE LENNEMAN.                                
000005 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000006 DATE-WRITTEN.           DECEMBER 2004.                                   
000007 DATE-COMPILED.                                                           
000008                                                                          
000009*                                                                         
000010*  PROGRAM NARRATIVE:                                                     
000011*  THIS PROGRAM WILL CREATE A REPORT OF FREIGHT CHARGES, CARRIERS,        
000012*  DEPTS., CLASS AND CARTON QUANTITIES.                                   
000013*                                                                         
000014*                                                                         
000015 ENVIRONMENT DIVISION.                                                    
000016                                                                          
000017 CONFIGURATION SECTION.                                                   
000018                                                                          
000019 SOURCE-COMPUTER.                        IBM-3090.                        
000020 OBJECT-COMPUTER.                        IBM-3090.                        
000021                                                                          
000022 INPUT-OUTPUT SECTION.                                                    
000023                                                                          
000024 FILE-CONTROL.                                                            
000025                                                                          
000026     SELECT  RV-FREIGHT-FILE                                              
000027         ASSIGN TO IN01.                                                  
000028                                                                          
000029     SELECT  RV-FREIGHT-RPT                                               
000030         ASSIGN TO RPT01.                                                 
000031                                                                          
000032******************************************************************        
000033 DATA DIVISION.                                                           
000034******************************************************************        
000035                                                                          
000036 FILE SECTION.                                                            
000037*                                                                         
000038 FD  RV-FREIGHT-FILE                                                      
000039     RECORDING MODE IS F                                                  
000040     LABEL RECORDS ARE STANDARD                                           
000041     BLOCK CONTAINS 0 RECORDS.                                            
000042 01  RV-FRGT-RECORD                   PIC  X(200).                        
000043*                                                                         
000044 FD  RV-FREIGHT-RPT                                                       
000045     RECORDING MODE IS F                                                  
000046     LABEL RECORDS ARE STANDARD                                           
000047     BLOCK CONTAINS 0 RECORDS.                                            
P49473*01  RV-FRGT-RPT-REC                  PIC  X(132).                        
P49473 01  RV-FRGT-RPT-REC                  PIC  X(150).                        
000049*                                                                         
000050*                                                                         
000051 WORKING-STORAGE SECTION.                                                 
000052*****************************************************************         
000053*    PROGRAM CONSTANTS                                                    
000054*****************************************************************         
000055 01  PC-CONSTANTS.                                                        
000056     05  PC-CURRENT-PGM               PIC  X(08) VALUE 'RVKRP019'.        
000057     05  PC-LINE-LIMIT                PIC  9(02) VALUE 60.                
000058     05  PC-TOTL-LIMIT                PIC  9(02) VALUE 55.                
000059     05  PC-ONE                       PIC  9(01) VALUE 1.                 
000060     05  PC-TOTAL-BY-CARRIER          PIC  X(30) VALUE                    
000061         ' TOTAL CARRIER FREIGHT CHARGES'.                                
000062     05  PC-FINAL-TOTAL               PIC  X(30) VALUE                    
000063         '   FINAL TOTAL FREIGHT CHARGES'.                                
000064                                                                          
000065*****************************************************************         
000066*    PROGRAM SWITCHES                                                     
000067*****************************************************************         
000068 01  PS-SWITCHES.                                                         
000069     05  PS-END-OF-FILE-SWITCH        PIC  X(01) VALUE 'N'.               
000070         88  PS-EOF                              VALUE 'Y'.               
000071         88  PS-NOT-EOF                          VALUE 'N'.               
000072     05  PS-1ST-TIME-SWITCH           PIC  X(01) VALUE 'Y'.               
000073         88  PS-1ST-TIME                         VALUE 'Y'.               
000074         88  PS-NOT-1ST-TIME                     VALUE 'N'.               
000072     05  PS-CARRIER-SWITCH            PIC  X(01) VALUE 'Y'.               
000073         88  PS-NEW-CARRIER                      VALUE 'Y'.               
000074         88  PS-NOT-NEW-CARRIER                  VALUE 'N'.               
000075                                                                          
000076*****************************************************************         
000077*    PROGRAM VARIABLES                                                    
000078*****************************************************************         
000079 01  PV-VARIABLES.                                                        
000080     05  PV-RPT-LINE-CNT              PIC  9(03) VALUE  0.                
000081     05  PV-RPT-PAGE-CNT              PIC  9(03) VALUE  0.                
000082     05  PV-RECORDS-READ              PIC S9(7) COMP-3 VALUE ZERO.        
000083     05  PV-RECORDS-WRITTEN           PIC S9(7) COMP-3 VALUE ZERO.        
000084                                                                          
000085     05  PV-PREV-FIELDS.                                                  
000086         10 PV-PREV-CARRIER           PIC  X(04) VALUE SPACES.            
000087         10 PV-PREV-TRACKING-NBR      PIC  X(25) VALUE SPACES.            
000088         10 PV-PREV-TRAILER           PIC  X(12) VALUE SPACES.            
000089     05  PV-TOTAL-CARRIER.                                                
000090         10 PV-CARRIER-INBND        PIC S9(7)V99 VALUE +0.                
000091         10 PV-CARRIER-OUTBND       PIC S9(7)V99 VALUE +0.                
000092     05  PV-FINAL-TOTAL.                                                  
000093         10 PV-FT-INBND-FRGT        PIC S9(8)V99 VALUE +0.                
000094         10 PV-FT-OUTBND-FRGT       PIC S9(8)V99 VALUE +0.                
000095                                                                          
000096 01  SYSTEM-DATE-TIME.                                                    
000097     05  SD-SYSTEM-DATE.                                                  
000098         10  SD-CD-CCYY              PIC 9(04) VALUE ZEROS.               
000099         10  SD-CD-MM                PIC 9(02) VALUE ZEROS.               
000100         10  SD-CD-DD                PIC 9(02) VALUE ZEROS.               
000101     05  ST-SYSTEM-TIME.                                                  
000102         10  ST-CT-HH                PIC 9(02) VALUE ZEROS.               
000103         10  ST-CT-MM                PIC 9(02) VALUE ZEROS.               
000104         10  FILLER                  PIC 9(04) VALUE ZEROS.               
000105                                                                          
000106 01  PH1-PAGE-HEADING.                                                    
000107     05  FILLER                          PIC  X(40)  VALUE SPACE.         
000108     05  FILLER                          PIC  X(52)  VALUE                
000109         'RETURN-TO-VENDOR  --  CARRIER FREIGHT PAYMENT REPORT'.          
000110     05  FILLER                          PIC  X(40)  VALUE SPACE.         
000111                                                                          
000112 01  PH2-PAGE-HEADING-2.                                                  
000113     05  FILLER                          PIC  X(54)  VALUE SPACE.         
000114     05  FILLER                          PIC  X(09)  VALUE                
000115         'FOR DATE '.                                                     
000116     05  PH2-RUN-DT                      PIC  X(10)  VALUE SPACE.         
000117     05  FILLER                          PIC  X(49)  VALUE SPACE.         
000118                                                                          
000119 01  DH1-DETAIL-HEADING-1.                                                
P49473*    05  FILLER                          PIC  X(89)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(99) VALUE SPACES.         
000121     05  FILLER                          PIC  X(07)  VALUE                
000122               'FREIGHT'.                                                 
P49473*    05  FILLER                          PIC  X(39)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(44)  VALUE SPACES.        
000124                                                                          
000125 01  DH2-DETAIL-HEADING-2.                                                
000126     05  FILLER                          PIC  X(07)  VALUE                
000127               'CARRIER'.                                                 
000128     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000141     05  FILLER                          PIC  X(22)  VALUE                
000142               'TRACKING NUMBER (PRO#)'.                                  
000143     05  FILLER                          PIC  X(04)  VALUE SPACES.        
000129     05  FILLER                          PIC  X(07)  VALUE                
000130               'TRAILER'.                                                 
000131     05  FILLER                          PIC  X(07)  VALUE SPACES.        
000132     05  FILLER                          PIC  X(05)  VALUE                
000133               'DEBIT'.                                                   
P49473*    05  FILLER                          PIC  X(01)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(11)  VALUE SPACES.        
000135     05  FILLER                          PIC  X(04)  VALUE                
000136               'DEPT'.                                                    
000137     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000138     05  FILLER                          PIC  X(05)  VALUE                
000139               'CLASS'.                                                   
000140     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000144     05  FILLER                          PIC  X(07)  VALUE                
000145               'CARTONS'.                                                 
000146     05  FILLER                          PIC  X(07)  VALUE SPACES.        
000147     05  FILLER                          PIC  X(07)  VALUE                
000148               'INBOUND'.                                                 
000149     05  FILLER                          PIC  X(10)  VALUE SPACES.        
000150     05  FILLER                          PIC  X(08)  VALUE                
000151               'OUTBOUND'.                                                
P49473*    05  FILLER                          PIC  X(27)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(35)  VALUE SPACES.        
000153                                                                          
000154 01  DH3-DETAIL-HEADING-3.                                                
000155     05  FILLER                          PIC  X(07)  VALUE                
000156               '-------'.                                                 
000157     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000170     05  FILLER                          PIC  X(25)  VALUE                
000171               '-------------------------'.                               
000172     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000158     05  FILLER                          PIC  X(12)  VALUE                
000159               '------------'.                                            
000160     05  FILLER                          PIC  X(01)  VALUE SPACES.        
P49473*    05  FILLER                          PIC  X(06)  VALUE                
P49473*              '------'.                                                  
P49473     05  FILLER                          PIC  X(16)  VALUE                
P49473               '----------------'.                                        
000163     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000164     05  FILLER                          PIC  X(04)  VALUE                
000165               '----'.                                                    
000166     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000167     05  FILLER                          PIC  X(05)  VALUE                
000168               '-----'.                                                   
000169     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000173     05  FILLER                          PIC  X(07)  VALUE                
000174               '-------'.                                                 
000175     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000176     05  FILLER                          PIC  X(12)  VALUE                
000177               '------------'.                                            
000178     05  FILLER                          PIC  X(06)  VALUE                
000179               '------'.                                                  
000180     05  FILLER                          PIC  X(12)  VALUE                
000181               '------------'.                                            
P49473*    05  FILLER                          PIC  X(27)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(35)  VALUE SPACES.        
000183                                                                          
000184                                                                          
000185 01  DL-DETAIL-LINE.                                                      
000186     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000187     05  DL-CARRIER                      PIC  X(04).                      
000188     05  FILLER                          PIC  X(03)  VALUE SPACES.        
000197     05  DL-TRACKING-NBR                 PIC  X(25).                      
000198     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000189     05  DL-TRAILER                      PIC  X(12).                      
000190     05  FILLER                          PIC  X(01)  VALUE SPACES.        
P49473*    05  DL-DEBIT                        PIC  9(06).                      
P49473     05  DL-DEBIT                        PIC  X(16).                      
000192     05  FILLER                          PIC  X(01)  VALUE SPACES.        
000193     05  DL-DEPT                         PIC  ZZ99.                       
000194     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000195     05  DL-CLASS                        PIC  ZZ99.                       
000196     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000199     05  DL-CARTONS                      PIC  ZZZ,ZZ9.                    
000200     05  FILLER                          PIC  X(02)  VALUE SPACES.        
000201     05  DL-INBND-FRGT                   PIC  Z,ZZZ,ZZ9.99.               
000202     05  FILLER                          PIC  X(06)  VALUE SPACES.        
000203     05  DL-OUTBND-FRGT                  PIC  Z,ZZZ,ZZ9.99.               
P49473*    05  FILLER                          PIC  X(27)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(35)  VALUE SPACES.        
000205                                                                          
000206 01  TL-TOTAL-LINE.                                                       
000207     05  FILLER                          PIC  X(10)  VALUE SPACES.        
000208     05  TL-TOT-LEGEND                   PIC  X(30)  VALUE SPACES.        
P49473*    05  FILLER                          PIC  X(34)  VALUE SPACES.        
P49473     05  FILLER                          PIC  X(44)  VALUE SPACES.        
000210     05  TL-INBND-FRGT                   PIC  ZZ,ZZZ,ZZ9.99.              
000211     05  FILLER                          PIC  X(05)  VALUE SPACES.        
000212     05  TL-OUTBND-FRGT                  PIC  ZZ,ZZZ,ZZ9.99.              
000213                                                                          
000214 01  BLANK-LINE.                                                          
000215     05  FILLER                          PIC  X(132) VALUE SPACE.         
000216                                                                          
000217 01  END-OF-REPORT-LINE.                                                  
000218     05  FILLER                          PIC  X(54)  VALUE SPACE.         
000219     05  FILLER                          PIC  X(25)  VALUE                
000220         '***** END OF REPORT *****'.                                     
000221     05  FILLER                          PIC  X(53)  VALUE SPACE.         
000222                                                                          
000223 01  ER-EMPTY-REPORT-LINE.                                                
000224     05  FILLER                          PIC  X(42)  VALUE SPACE.         
000225     05  FILLER                          PIC  X(48)  VALUE                
000226         '*************  NO DATA TO REPORT  **************'.              
000227     05  FILLER                          PIC  X(42)  VALUE SPACE.         
000228                                                                          
000229***  STANDARD HEADING LAYOUT - 132 COLUMNS                                
P49473*    COPY DPWS132.                                                        
P49473***  STANDARD HEADING LAYOUT - 150 COLUMNS                                
P49473     COPY DPWS150.                                                        
000231                                                                          
000232***  INPUT FILE LAYOUT                                                    
000233     COPY RVRD006.                                                        
000234                                                                          
000235                                                                          
000236 LINKAGE SECTION.                                                         
000237                                                                          
000238 PROCEDURE DIVISION.                                                      
000239*****************************************************************         
000240*  MAINLINE LOGIC FOR PROGRAM                                             
000241*****************************************************************         
000242 0000-MAINLINE.                                                           
000243     PERFORM 1000-INITIALIZE                                              
000244                                                                          
000245     PERFORM 2000-PROCESS                                                 
000246         UNTIL PS-EOF.                                                    
000247                                                                          
000248     PERFORM 9000-EOJ.                                                    
000249                                                                          
000250     STOP RUN.                                                            
000251                                                                          
000252*****************************************************************         
000253*  INITIALIZATION PARAGRAPH                                               
000254*****************************************************************         
000255 1000-INITIALIZE.                                                         
000256     OPEN  INPUT RV-FREIGHT-FILE                                          
000257          OUTPUT RV-FREIGHT-RPT.                                          
000258                                                                          
000259     MOVE FUNCTION CURRENT-DATE (1:8) TO  SD-SYSTEM-DATE.                 
000260     MOVE FUNCTION CURRENT-DATE (9:6) TO  ST-SYSTEM-TIME.                 
000261                                                                          
P49473*    MOVE SD-CD-CCYY       TO DP132-RUN-YEAR.                             
P49473*    MOVE SD-CD-MM         TO DP132-RUN-MONTH.                            
P49473*    MOVE SD-CD-DD         TO DP132-RUN-DAY.                              
P49473*    MOVE ST-CT-HH         TO DP132-RUN-HOUR.                             
P49473*    MOVE ST-CT-MM         TO DP132-RUN-MINUTE.                           
P49473*                                                                         
P49473*    MOVE PC-CURRENT-PGM   TO DP132-PROGRAM-NAME.                         
P49473*    MOVE PC-ONE           TO DP132-REPORT-NUMBER.                        
P49473*                                                                         
P49473*    MOVE DP132-RUN-DATE   TO PH2-RUN-DT.                                 
P49473     MOVE SD-CD-CCYY       TO DP150-RUN-YEAR.                             
P49473     MOVE SD-CD-MM         TO DP150-RUN-MONTH.                            
P49473     MOVE SD-CD-DD         TO DP150-RUN-DAY.                              
P49473     MOVE ST-CT-HH         TO DP150-RUN-HOUR.                             
P49473     MOVE ST-CT-MM         TO DP150-RUN-MINUTE.                           
P49473                                                                          
P49473     MOVE PC-CURRENT-PGM   TO DP150-PROGRAM-NAME.                         
P49473     MOVE PC-ONE           TO DP150-REPORT-NUMBER.                        
P49473                                                                          
P49473     MOVE DP150-RUN-DATE   TO PH2-RUN-DT.                                 
000272                                                                          
000273     PERFORM 8000-WRITE-HEADINGS.                                         
000274     PERFORM 7000-READ-FILE.                                              
000275                                                                          
000276     IF PS-EOF                                                            
000277         WRITE RV-FRGT-RPT-REC FROM ER-EMPTY-REPORT-LINE AFTER 3          
000278     ELSE                                                                 
000279         MOVE RV006-CARRIER        TO PV-PREV-CARRIER                     
000280         MOVE RV006-TRACKING-NBR   TO PV-PREV-TRACKING-NBR                
000281         MOVE RV006-TRAILER-NBR    TO PV-PREV-TRAILER                     
000282     END-IF.                                                              
000283                                                                          
000284*****************************************************************         
000285* MOVE INFO TO DETAIL LINE.  IF TRACKING NBR OR TRAILER ARE THE           
000286*   SAME ON MULTIPLE LINES MOVE SPACES TO THOSE FIELD                     
000287*****************************************************************         
000288 2000-PROCESS.                                                            
000289     IF RV006-CARRIER =  PV-PREV-CARRIER                                  
000290         CONTINUE                                                         
000291     ELSE                                                                 
000292         PERFORM 3000-NEW-CARRIER                                         
000293     END-IF.                                                              
000294                                                                          
000295     IF PS-1ST-TIME                                                       
000296         SET PS-NOT-1ST-TIME TO TRUE                                      
000297         MOVE RV006-TRACKING-NBR   TO DL-TRACKING-NBR                     
000298         MOVE RV006-TRAILER-NBR    TO DL-TRAILER                          
000298         MOVE RV006-CARRIER        TO DL-CARRIER                          
000299     ELSE                                                                 
000300         IF RV006-TRACKING-NBR = PV-PREV-TRACKING-NBR                     
000301             MOVE SPACES               TO DL-TRACKING-NBR                 
000302         ELSE                                                             
000303             MOVE RV006-TRACKING-NBR   TO DL-TRACKING-NBR                 
000304         END-IF                                                           
000305         IF RV006-TRAILER-NBR = PV-PREV-TRAILER                           
000306             MOVE SPACES               TO DL-TRAILER                      
000307         ELSE                                                             
000308             MOVE RV006-TRAILER-NBR    TO DL-TRAILER                      
000309         END-IF                                                           
000310     END-IF.                                                              
000311                                                                          
000300     IF PS-NEW-CARRIER                                                    
000301         MOVE RV006-CARRIER        TO DL-CARRIER                          
000301         SET PS-NOT-NEW-CARRIER    TO TRUE                                
000302     ELSE                                                                 
000303         MOVE SPACES               TO DL-CARRIER                          
000304     END-IF                                                               
000311                                                                          
000313     MOVE RV006-DEBIT-NBR      TO DL-DEBIT.                               
000314     MOVE RV006-DEPT-NBR       TO DL-DEPT.                                
000315     MOVE RV006-MAJ-CLASS      TO DL-CLASS.                               
000316     MOVE RV006-CARTONS        TO DL-CARTONS.                             
000317                                                                          
000318     ADD RV006-INB-FRGT-AMT    TO PV-CARRIER-INBND                        
000319                                  PV-FT-INBND-FRGT.                       
000320     ADD RV006-OUTB-FRGT-AMT   TO PV-CARRIER-OUTBND                       
000321                                  PV-FT-OUTBND-FRGT.                      
000322     MOVE RV006-INB-FRGT-AMT   TO DL-INBND-FRGT.                          
000323     MOVE RV006-OUTB-FRGT-AMT  TO DL-OUTBND-FRGT.                         
000324                                                                          
000325     PERFORM 8300-WRITE-REPORT.                                           
000326                                                                          
000327     MOVE RV006-TRACKING-NBR   TO PV-PREV-TRACKING-NBR.                   
000328     MOVE RV006-TRAILER-NBR    TO PV-PREV-TRAILER.                        
000329                                                                          
000330     PERFORM 7000-READ-FILE.                                              
000331                                                                          
000332****************************************************************          
000333*  NEW CARRIER                                                            
000334****************************************************************          
000335 3000-NEW-CARRIER.                                                        
000336     MOVE PC-TOTAL-BY-CARRIER TO TL-TOT-LEGEND.                           
000337     MOVE PV-CARRIER-INBND    TO TL-INBND-FRGT.                           
000338     MOVE PV-CARRIER-OUTBND   TO TL-OUTBND-FRGT.                          
000339     PERFORM 8400-WRITE-TOTAL.                                            
000340     MOVE RV006-CARRIER       TO PV-PREV-CARRIER.                         
000340     SET PS-NEW-CARRIER       TO TRUE.                                    
000341     MOVE +0                  TO PV-CARRIER-INBND.                        
000342     MOVE +0                  TO PV-CARRIER-OUTBND.                       
000343                                                                          
000344******************************************************************        
000345**  READ FREIGHT EXTRACT FILE                                             
000346******************************************************************        
000347 7000-READ-FILE.                                                          
000348     READ RV-FREIGHT-FILE   INTO RV006-RECORD                             
000349         AT END SET PS-EOF TO TRUE.                                       
000350                                                                          
000351     ADD +1                TO PV-RECORDS-READ.                            
000352                                                                          
000353******************************************************************        
000354**  WRITE THE HEADING LINES                                               
000355******************************************************************        
000356 8000-WRITE-HEADINGS.                                                     
000357     ADD PC-ONE               TO PV-RPT-PAGE-CNT.                         
P49473*    MOVE PV-RPT-PAGE-CNT     TO DP132-PAGE-NUMBER.                       
P49473     MOVE PV-RPT-PAGE-CNT     TO DP150-PAGE-NUMBER.                       
000359                                                                          
P49473*    WRITE RV-FRGT-RPT-REC  FROM DP132-STANDRD-HEADER-132-COLS            
P49473     WRITE RV-FRGT-RPT-REC  FROM DP150-STANDRD-HEADER-150-COLS            
000361          AFTER ADVANCING PAGE.                                           
000362                                                                          
000363     WRITE RV-FRGT-RPT-REC  FROM PH1-PAGE-HEADING                         
000364          AFTER ADVANCING 1 LINES.                                        
000365                                                                          
000366     WRITE RV-FRGT-RPT-REC  FROM PH2-PAGE-HEADING-2                       
000367          AFTER ADVANCING 1 LINES.                                        
000368                                                                          
000369     WRITE RV-FRGT-RPT-REC  FROM DH1-DETAIL-HEADING-1                     
000370          AFTER ADVANCING 2 LINES.                                        
000371                                                                          
000372     WRITE RV-FRGT-RPT-REC  FROM DH2-DETAIL-HEADING-2                     
000373          AFTER ADVANCING 1 LINES.                                        
000374                                                                          
000375     WRITE RV-FRGT-RPT-REC  FROM DH3-DETAIL-HEADING-3                     
000376          AFTER ADVANCING 1 LINES.                                        
000377                                                                          
000378     WRITE RV-FRGT-RPT-REC  FROM BLANK-LINE                               
000379          AFTER ADVANCING 1 LINES.                                        
000380                                                                          
000381     MOVE 8               TO PV-RPT-LINE-CNT.                             
000382                                                                          
000383*****************************************************************         
000384*  WRITE THE DETAIL LINE                                                  
000385*****************************************************************         
000386 8300-WRITE-REPORT.                                                       
000387     IF PV-RPT-LINE-CNT       > PC-LINE-LIMIT                             
000388          PERFORM 8000-WRITE-HEADINGS.                                    
000389     WRITE RV-FRGT-RPT-REC FROM DL-DETAIL-LINE.                           
000390                                                                          
000391     ADD 1                TO PV-RPT-LINE-CNT                              
000392                             PV-RECORDS-WRITTEN.                          
000393                                                                          
000394*****************************************************************         
000395*  WRITE THE TOTAL LINE                                                   
000396*****************************************************************         
000397 8400-WRITE-TOTAL.                                                        
000398     IF PV-RPT-LINE-CNT       > PC-TOTL-LIMIT                             
000399          PERFORM 8000-WRITE-HEADINGS.                                    
000400                                                                          
000401     WRITE RV-FRGT-RPT-REC FROM TL-TOTAL-LINE                             
000402                AFTER ADVANCING 2 LINES.                                  
000403     WRITE RV-FRGT-RPT-REC FROM BLANK-LINE                                
000404                AFTER ADVANCING 2 LINES.                                  
000405     ADD 4 TO PV-RPT-LINE-CNT.                                            
000406                                                                          
000407*****************************************************************         
000408*  END OF JOB PROCESSING                                                  
000409*****************************************************************         
000410 9000-EOJ.                                                                
000411**** WRITE OUT LAST CARRIER TOTAL                                         
000412     MOVE PC-TOTAL-BY-CARRIER TO TL-TOT-LEGEND.                           
000413     MOVE PV-CARRIER-INBND    TO TL-INBND-FRGT.                           
000414     MOVE PV-CARRIER-OUTBND   TO TL-OUTBND-FRGT.                          
000415     PERFORM 8400-WRITE-TOTAL.                                            
000416                                                                          
000417**** WRITE OUT FINAL TOTAL LINE                                           
000418     MOVE PC-FINAL-TOTAL      TO TL-TOT-LEGEND.                           
000419     MOVE PV-FT-INBND-FRGT    TO TL-INBND-FRGT.                           
000420     MOVE PV-FT-OUTBND-FRGT   TO TL-OUTBND-FRGT.                          
000421     PERFORM 8400-WRITE-TOTAL.                                            
000422                                                                          
000423     WRITE RV-FRGT-RPT-REC FROM END-OF-REPORT-LINE                        
000424          AFTER ADVANCING 3 LINES.                                        
000425                                                                          
000426     DISPLAY SPACES.                                                      
000427     DISPLAY '*************************************************'.         
000428     DISPLAY '*****        RECORD COUNTS FOR RVKRP019    ******'.         
000429     DISPLAY 'FREIGHT FILE RECS READ =====> ' PV-RECORDS-READ.            
000430     DISPLAY SPACES.                                                      
000431     DISPLAY 'FREIGHT REPT RECS WRITTEN ==> ' PV-RECORDS-WRITTEN.         
000432     DISPLAY SPACES.                                                      
000433     DISPLAY '*************************************************'.         
000434                                                                          
000435     CLOSE RV-FREIGHT-FILE                                                
000436           RV-FREIGHT-RPT.                                                
000437                                                                          
000438*************  END OF RVKRP019  ********************************          
