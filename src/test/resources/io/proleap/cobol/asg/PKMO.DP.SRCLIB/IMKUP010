       IDENTIFICATION DIVISION.                                         00010023
                                                                        00020024
       PROGRAM-ID.    IMKUP010                                          00030024
       AUTHOR.        PAUL KEBER.                                       00040024
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050024
       DATE-WRITTEN.  09-21-99.                                         00060024
       DATE-COMPILED.                                                   00070024
                                                                        00080024
      ******************************************************************00090024
      *     IMKUP010 - VSM INACTIVE TUPCPLS PURGE                      *00100024
      ******************************************************************00110024
      *     THIS PROGRAM WILL PURGE INACTIVE ROWS FROM THE TUPCPLS DB2 *00120024
      * TABLE USING THE DB2 KEY INFO FROM EACH INPUT FILE RECORD.      *00130024
      * COMMITS ARE TAKEN EVERY TEN SECONDS, AND CHECKPOINT RESTART    *00180024
      * LOGIC IS INCORPORATED.                                         *00180024
      *                                                                *00200031
      *    WR/PITS#     DATE        DESCRIPTION                        *00200031
      *    --------   --------     ---------------------------------   *00210031
      *    WR#20411   10/18/99     ONCE ALL TUPCPLS ROWS ARE PURGED    *00220031
      *                            FOR A UPC ID, CHECK IF ANY TUPCPLS  *00200031
      *                            ROWS EXIST FOR THAT UPC ID.  IF NOT,*00200031
      *                            PURGE THE TUPC ROW FOR THAT UPC ID  *00200031
      *                            AND CREATE A BACKUP TUPC PURGE FILE.*00200031
      *                                                                *00270024
      *    WR#20411   11/23/99     RCP PHASE 2 CHANGES TO ADD NEW      *00220031
      *                            TUPC FIELD REGN_PRIC_IND TO THE     *00200031
      *                            TUPC SELECT.                        *00200031
      *                                                                *        
      *    WR#20134   12/21/99     ELIMINATE MDSE-TYP-SEQ-NBR FROM TUPC*        
      *                            THIS WAS IN 6200-SELECT-TUPC-ROW.   *        
      *                                                                *        
      *    ECOMM       8/08/00     ELIMINATED TUPC PATRN_SEQ_NBR,      *        
      *                            TRND_SEQ_NBR, FBRCN_SEQ_NBR, AND    *        
      *                            FNSH_SEQ_NBR.  ADDED TUPC WEB_ITM_  *        
      *                            CDE.  MODIFIED TUPC FILE LRECL TO   *        
      *                            REFLECT ABOVE.                      *        
      *    ECOMM      11/15/00     MODIFIED TUPC LRECL FOR REMOVAL OF  *        
      *                            TUPC WEB ITM CDE.                   *00270024
      *    ECOMM AGAIN  3/5/01     MODIFIED TUPC LRECL FOR ADDITION OF *        
      *                            NEW TX_PRD_CDE_SEQ_NBR AND ADDED    *00270024
      *                            NEW FIELD TO 6200-SELECT-TUPC-ROW.  *00270024
      ******************************************************************00270024
      *    ECOMM AGAIN  4/6/01     MODIFIED TUPC LRECL FOR ADDITION OF *        
      *                            NEW CUSTFACG_CLOR_DESC AND ADDED    *00270024
      *                            NEW FIELD TO 6200-SELECT-TUPC-ROW.  *00270024
      ******************************************************************00270024
      *    wr22746     12/12/01    MODIFIED TUPC LRECL FOR ADDITION OF *        
      *                            NEW INTR_UPC_DESC AND ADDED         *00270024
      *                            NEW FIELD TO 6200-SELECT-TUPC-ROW.  *00270024
      ******************************************************************00270024
      *    wr22746      2/18/02    changed 6200 and 6300 tupc where    *        
      *                            clause from 180 to 120 days         *00270024
      ******************************************************************00270024
      *    wr23443      3/18/02    changed 6200 and 6300 tupc where    *        
      *                            clause from 120 to 60 days          *00270024
      ******************************************************************00270024
      *    wr27400      8/11/04    changed 6200 and 6300 tupc where    *        
      *                            clause from 60 to 45 days           *00270024
      ******************************************************************00270024
      *    wr27400      8/26/04    changed 6200 and 6300 tupc where    *        
      *                            clause from 45 to 30 days           *00270024
      ******************************************************************00270024
      *    wr29925      4/13/06    changed 6200 and 6300 tupc where    *        
      *                            clause from 30 to 20 days           *00270024
      ******************************************************************00270024
      *    wr29858      9/12/06    changed 6200 and 6300 tupc where    *        
      *                            clause from 20 to 40 days.          *00270024
      *                            this is temporary for markdown      *        
      *                            optimization.                       *        
      ******************************************************************00270024
      *    wr29858     10/02/06    changed 6200 and 6300 tupc where    *        
      *                            clause from 40 to 20 days.          *00270024
      *                            this was a temporary change for     *        
      *                            markdown optimization.              *        
      ******************************************************************00270024
204416*    CHG0204416  07/31/18    Change the definitions of           *        
204416*                            PC-1000000000 and PC-2000000000     *00270024
204416*                            to eliminate the hex values which   *        
204416*                            fail in the mainframe refactoring   *        
204416*                            process.                            *        
204416******************************************************************00270024
                                                                        00280024
                                                                        00290024
       ENVIRONMENT DIVISION.                                            00300024
                                                                        00310024
       CONFIGURATION SECTION.                                           00320024
       SOURCE-COMPUTER.        IBM-3090.                                00330024
       OBJECT-COMPUTER.        IBM-3090.                                00340024
       INPUT-OUTPUT SECTION.                                            00350024
       FILE-CONTROL.                                                    00360024
           SELECT TUPCPLS-PURGE-FILE                                    00370024
               ASSIGN TO INP01.                                         00380024
                                                                        00390024
           SELECT TUPC-PURGE-FILE                                       00370024
               ASSIGN TO OUT01.                                         00380024
                                                                        00400024
       DATA DIVISION.                                                   00410024
       FILE SECTION.                                                    00420024
                                                                        00430024
       FD  TUPCPLS-PURGE-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TUPCPLS-PURGE-RECORD        PIC X(144).                      00480024
                                                                        00430024
       FD  TUPC-PURGE-FILE                                              00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TUPC-PURGE-RECORD           PIC X(214).                      00480024
                                                                        00500024
                                                                        00510024
       WORKING-STORAGE SECTION.                                         00520024
                                                                        00530024
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00540024
                                                                        00550024
       01  PC-PROGRAM-CONSTANTS.                                        00560024
204416     05  PC-1000000000    PIC S9(18)  COMP SYNC VALUE 1000000000. 00540024
204416     05  FILLER REDEFINES PC-1000000000.                          00540024
204416         10  FILLER              PIC X(4).                        00540024
204416         10  PC-1000000000-NUM   PIC S9(9) COMP.                  00540024
204416     05  PC-2000000000    PIC S9(18)  COMP SYNC VALUE 2000000000. 00540024
204416     05  FILLER REDEFINES PC-2000000000.                          00540024
204416         10  FILLER              PIC X(4).                        00540024
204416         10  PC-2000000000-NUM   PIC S9(9) COMP.                  00540024
           05  PC-JOB-NAME             PIC  X(06)  VALUE 'IMK505'.      00570024
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUP010'.    00580024
           05  PC-MAX-RETRIES          PIC S9(03)  VALUE +3 COMP-3.     00590024
           05  PC-MAX-DEADLOCKS        PIC S9(03)  VALUE +3 COMP-3.     00600024
                                                                        00610024
       01  PC-PROGRAM-COUNTER.                                          00620024
           05  PC-DISPLAY-CNT          PIC  9(09)  VALUE ZERO.          00640028
           05  PC-UPDATE-CNT           PIC  9(09)  VALUE ZERO.          00640028
                                                                        00650024
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00660024
           05  PE-MSG-01                       PIC X(50) VALUE          00670024
                'ATTEMPT TO DELETE ROW ON TABLE TUPCPLS FAILED    '.    00700024
           05  PE-MSG-02                       PIC X(50) VALUE          00730024
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00740024
           05  PE-MSG-03                       PIC X(50) VALUE          00750024
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00760024
           05  PE-MSG-04                       PIC X(50) VALUE          00770024
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00780024
           05  PE-MSG-05                       PIC X(50) VALUE          00790024
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00800024
           05  PE-MSG-06                       PIC X(50) VALUE          00850024
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00860024
           05  PE-MSG-07                       PIC X(50) VALUE          00870024
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00880024
           05  PE-MSG-08                       PIC X(50) VALUE          00670024
                'ATTEMPT TO SELECT COUNT ON TABLE TUPCPLS FAILED  '.    00700024
           05  PE-MSG-09                       PIC X(50) VALUE          00670024
                'ATTEMPT TO DELETE ROW ON TABLE TUPC FAILED       '.    00700024
           05  PE-MSG-10.                                               00670024
               10  FILLER                      PIC X(50) VALUE          00670024
                   'TUPC ROW IS MISSING FOR UPC ID: '.                  00700024
204416         10  PE-MSG-10-UPC-ID-1          PIC 9     VALUE ZERO.    00670024
               10  PE-MSG-10-UPC-ID            PIC 9(9)  VALUE ZERO.    00670024
           05  PE-MSG-11                       PIC X(50) VALUE          00670024
                'ATTEMPT TO SELECT ROW ON TABLE TUPC FAILED       '.    00700024
      *                                                                 00890024
       01  PS-PROGRAM-SWITCHES.                                         00900024
           05  PS-INPUT-FILE-EOF-SW         PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00920024
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00930024
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00960024
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00970024
           05  PS-TUPC-ROW-FOUND-SW         PIC X         VALUE 'N'.    00960024
               88  PS-TUPC-ROW-FOUND                      VALUE 'Y'.    00970024
               88  PS-TUPC-ROW-NOT-FOUND                  VALUE 'N'.    00970024
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00980024
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00990024
               88  PS-PGM-NOT-DEADLOCKED                  VALUE 'N'.    00990024
      *                                                                 01030024
      *                                                                 01040024
       01  PROGRAM-VARIABLES.                                           01050024
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01060024
           05  PV-TUPCPLS-COUNT                PIC S9(4)  VALUE 0 COMP.         
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070024
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01080024
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01090024
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01100024
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01110024
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.   01120024
           05  PV-UPC-ID-1.                                             01110024
               10  PV-UPC-ID-1HIGH             PIC  9(1)  VALUE 1.      01170024
               10  PV-UPC-ID-1LOW              PIC  9(9)  VALUE ZEROES. 01170024
           05  PV-UPC-ID-2.                                             01110024
               10  PV-UPC-ID-2HIGH             PIC  9(1)  VALUE 2.      01170024
               10  PV-UPC-ID-2LOW              PIC  9(9)  VALUE ZEROES. 01170024
      *                                                                 01130024
      *                                                                 01140024
       01  PA-PROGRAM-ACCUMULATORS.                                     01150024
           05  PA-RECORD-COUNTS.                                        01160024
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01170024
               10  PA-DEL-TUPCPLS-COUNT        PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-DEL-TUPC-COUNT           PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-MISSING-TUPC-COUNT       PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01210024
      *                                                                 01220024
       01  PD-PROGRAM-DISPLAYS.                                         01230024
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01240024
      *                                                                 01250024
       01  SAVE-AREA.                                                   01260024
           05  SV-TUPCPLS-UPC-ID         PIC S9(09) VALUE +0 COMP.      01270034
           05  SV-TUPCPLS-OPERCO-NBR     PIC  X(02) VALUE SPACES.               
           05  SV-TUPCPLS-STR-NBR        PIC S9(04) VALUE +0 COMP.      01910024
           05  SV-TUPCPLS-EFF-BEG-TMST   PIC  X(26) VALUE SPACES.               
      *                                                                 01250024
      *                                                                 01450024
      ******************************************************************01460024
      * IMRD007 - TUPCPLS PURGE FILE COPYBOOK                           01470024
      ******************************************************************01480024
           COPY IMRD007.                                                01490024
      *                                                                 01500024
      ******************************************************************01510024
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01520024
      ******************************************************************01530024
           COPY DPWS004.                                                01540024
      *                                                                 01550024
      ******************************************************************01560024
      *  USED FOR DB2 ERRORS                                            01570024
      ******************************************************************01580024
           EXEC SQL                                                     01590024
               INCLUDE SQLCA                                            01600024
           END-EXEC.                                                    01610024
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE UPC TABLE                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE UPC PHYSICAL LOCATION STORE TABLE                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************** 01700024
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01710024
      ***************************************************************** 01720024
           EXEC SQL                                                     01730024
               INCLUDE TCKRSTCN                                         01740024
           END-EXEC.                                                    01750024
      *                                                                 01760024
           EXEC SQL                                                     01770024
               INCLUDE TCKRSTIN                                         01780024
           END-EXEC.                                                    01790024
      *                                                                 01800024
      ******************************************************************01810024
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01820024
      ******************************************************************01830024
       01  CR-CHECKPOINT-RESTART-AREA.                                  01840024
           05  CR-AREA-LEN                  PIC S9(04) VALUE +83 COMP.  01850037
           05  CR-CHECKPOINT-RESTART-VAR.                               01860024
               10  CR-PREV-DTL-KEY          PIC  X(36) VALUE SPACES.    01870024
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01880024
                   15  CR-TUPCPLS-UPC-ID    PIC S9(09) COMP.            01890024
                   15  CR-TUPCPLS-OPERCO-NBR                                    
                                            PIC  X(02).                         
                   15  CR-TUPCPLS-STR-NBR   PIC  9(04).                 01910024
                   15  CR-TUPCPLS-EFF-BEG-TMST                          01920024
                                            PIC  X(26).                         
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01940024
               10  CR-COMMIT-COUNT          PIC  9(09) VALUE ZEROES.    01950024
               10  CR-DEL-TUPCPLS-COUNT     PIC  9(09) VALUE ZEROES.    01962037
               10  CR-DEL-TUPC-COUNT        PIC  9(09) VALUE ZEROES.    01950024
               10  CR-MISSING-TUPC-COUNT    PIC  9(09) VALUE ZEROES.    01950024
      *                                                                 02080024
      *                                                                 02110024
      *-------------------*                                             02120024
       PROCEDURE DIVISION.                                              02130024
      *-------------------*                                             02140024
                                                                        02150024
       0000-MAIN-MODULE.                                                02160024
                                                                        02170024
           PERFORM 1000-INITIALIZATION.                                 02180024
      *                                                                 02190024
           PERFORM 2000-PROCESS-INPUT                                   02200024
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02210024
      *                                                                 02220024
           PERFORM 8000-EOJ-ROUTINE.                                    02230024
                                                                        02240024
           STOP RUN.                                                    02250024
      *                                                                 02260024
      *                                                                 02270024
      *                                                                 02280024
      ******************************************************************02290024
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02300024
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02310024
      ******************************************************************02320024
      *                                                                 02330024
       1000-INITIALIZATION.                                             02340024
      *                                                                 02350024
           OPEN INPUT TUPCPLS-PURGE-FILE.                               02360024
           OPEN OUTPUT TUPC-PURGE-FILE.                                 02360024
      *                                                                 02370024
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02380024
      *                                                                 02390024
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02400024
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02410024
               PERFORM 7500-SELECT-RESTART-INFO                         02420024
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02430024
                                             CR-CHECKPOINT-RESTART-VAR  02440024
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02460024
               MOVE CR-DEL-TUPCPLS-COUNT    TO PA-DEL-TUPCPLS-COUNT     02490137
               MOVE CR-DEL-TUPC-COUNT       TO PA-DEL-TUPC-COUNT        02490137
               MOVE CR-MISSING-TUPC-COUNT   TO PA-MISSING-TUPC-COUNT    02490137
               MOVE CR-COMMIT-COUNT TO PA-COMMIT-COUNT                  02500024
           ELSE                                                         02510024
               SET PS-FIRST-COMMIT TO TRUE                              02520024
           END-IF.                                                      02530024
      *                                                                 02540024
           DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                      02550024
                   TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'.             02560024
      *                                                                 02570024
           PERFORM 4000-READ-TUPCPLS-PURGE-FILE UNTIL                   02580024
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02590024
            OR PS-NO-MORE-INPUT-RECORDS.                                02600024
      *                                                                 02610024
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02620024
               IF IM007-TUPCPLS-UPC-ID < PC-1000000000-NUM              02630024
                   DISPLAY 'UPC ID            ' IM007-TUPCPLS-UPC-ID    02630024
               ELSE                                                     02770024
                   IF IM007-TUPCPLS-UPC-ID < PC-2000000000-NUM          02630024
                       MOVE IM007-TUPCPLS-UPC-ID TO PV-UPC-ID-1LOW      02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-1                                02630024
                   ELSE                                                 02770024
                       MOVE IM007-TUPCPLS-UPC-ID TO PV-UPC-ID-2LOW      02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-2                                02630024
                   END-IF                                               02770024
               END-IF                                                   02770024
               DISPLAY 'OPERCO NBR        ' IM007-TUPCPLS-OPERCO-NBR    02640024
               DISPLAY 'STORE NBR         ' IM007-TUPCPLS-STR-NBR       02650024
               DISPLAY 'BEGIN TIME STAMP  ' IM007-TUPCPLS-EFF-BEG-TMST  02660024
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02680024
                        PA-INPUT-COUNT                                  02690024
           END-IF.                                                      02700024
                                                                        02710024
           IF PS-NO-MORE-INPUT-RECORDS                                  02720024
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02730024
                   CONTINUE                                             02740024
               ELSE                                                     02750024
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02760024
           ELSE                                                         02770024
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02780024
           END-IF.                                                      02790024
      *                                                                 02800024
           IF PA-INPUT-COUNT > ZERO                                     02720024
               MOVE IM007-TUPCPLS-UPC-ID TO SV-TUPCPLS-UPC-ID           03059137
           END-IF.                                                      02790024
      *                                                                 02800024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS THE TUPCPLS PURGE FILE - DELETE EACH ROW                02830024
      ******************************************************************02840024
       2000-PROCESS-INPUT.                                              02850024
      *                                                                 02860024
           IF IM007-TUPCPLS-UPC-ID NOT = SV-TUPCPLS-UPC-ID              03059137
               PERFORM 3000-TUPC-CLEANUP                                        
               PERFORM 7700-COMMIT-PROCESSING                                   
               MOVE IM007-TUPCPLS-UPC-ID TO SV-TUPCPLS-UPC-ID           03059137
           END-IF.                                                      03101037
           MOVE IM007-TUPCPLS-OPERCO-NBR TO SV-TUPCPLS-OPERCO-NBR.      03590024
           MOVE IM007-TUPCPLS-STR-NBR    TO SV-TUPCPLS-STR-NBR.         03600024
           MOVE IM007-TUPCPLS-EFF-BEG-TMST                              03610024
                                         TO SV-TUPCPLS-EFF-BEG-TMST.            
      *                                                                 02860024
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02870024
      *                                                                 02880024
           PERFORM 6000-DELETE-TUPCPLS-ROW.                             02890037
      *                                                                 02900024
           IF PS-PGM-NOT-DEADLOCKED                                     03060024
               PERFORM 4000-READ-TUPCPLS-PURGE-FILE                     03100037
           END-IF.                                                      03101037
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************03500024
      * CHECKS IF ALL TUPCPLS ROWS WITH THE SAME UPC ID HAVE BEEN       03510024
      * PURGED.  IF SO, DELETE THE TUPC ROW WITH THAT UPC ID.           03490024
      ******************************************************************03520024
       3000-TUPC-CLEANUP.                                               03530024
      *                                                                 02900024
           PERFORM 6100-SELECT-FROM-TUPCPLS.                            02890037
      *                                                                 02900024
           IF PV-TUPCPLS-COUNT = ZERO                                   03060024
               SET PS-TUPC-ROW-NOT-FOUND TO TRUE                                
               PERFORM 6200-SELECT-TUPC-ROW                             03100037
               IF PS-TUPC-ROW-FOUND                                     03060024
                 AND PS-PGM-NOT-DEADLOCKED                              03060024
                   PERFORM 6300-DELETE-TUPC-ROW                         03100037
               END-IF                                                   03101037
           END-IF.                                                      03101037
      *                                                                 03480024
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************03500024
      * READS THE TUPCPLS PURGE FILE INTO THE IMRD007 LAYOUT            03510024
      ******************************************************************03520024
       4000-READ-TUPCPLS-PURGE-FILE.                                    03530024
           READ TUPCPLS-PURGE-FILE INTO IM007-TUPCPLS-PURGE-RECORD      03540024
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW                  03550024
               NOT AT END                                                       
                   ADD 1                     TO PA-INPUT-COUNT                  
                                                PC-UPDATE-CNT                   
                   IF PC-UPDATE-CNT > 100000                                    
                       ADD 1                 TO PC-DISPLAY-CNT                  
                       DISPLAY 'UPDATE COUNT: ' PC-DISPLAY-CNT                  
                       MOVE +0               TO PC-UPDATE-CNT                   
                   END-IF.                                                      
      *                                                                 03480024
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************03500024
      * WRITES A TUPC PURGE BACKUP FILE RECORD                          03510024
      ******************************************************************03520024
       5000-WRITE-TUPC-RECORD.                                                  
                                                                                
           WRITE TUPC-PURGE-RECORD FROM DCLTUPC.                                
      *                                                                 03560024
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************03910024
      * DELETE A ROW FROM THE TUPCPLS TABLE THAT MATCHES THE INPUT KEY  03920024
      ******************************************************************03940024
       6000-DELETE-TUPCPLS-ROW.                                         03950037
      *                                                                 03960024
           MOVE IM007-TUPCPLS-UPC-ID       TO UPCPLS-UPC-ID.            03580024
           MOVE IM007-TUPCPLS-OPERCO-NBR   TO UPCPLS-OPERCO-NBR.        03590024
           MOVE IM007-TUPCPLS-STR-NBR      TO UPCPLS-STR-NBR.           03600024
           MOVE IM007-TUPCPLS-EFF-BEG-TMST TO UPCPLS-EFF-BEG-TMST.      03610024
                                                                        03960024
           EXEC SQL                                                     05110024
               DELETE                                                           
                  FROM TUPCPLS                                                  
               WHERE UPC_ID       = :UPCPLS-UPC-ID                              
                 AND OPERCO_NBR   = :UPCPLS-OPERCO-NBR                          
                 AND STR_NBR      = :UPCPLS-STR-NBR                             
                 AND EFF_BEG_TMST = :UPCPLS-EFF-BEG-TMST                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   ADD 1              TO PA-DEL-TUPCPLS-COUNT           04792037
               WHEN +100                                                04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6000-DELETE-TUPCPLS-ROW'   04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6000-DELETE-TUPCPLS-ROW'                   04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6000-DELETE-TUPCPLS-ROW'                       04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-01     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 05709034
      *                                                                 05710024
      ******************************************************************        
      *  CHECK TO SEE IF THE UPC ID EXISTS ON TUPCPLS                  *        
      ******************************************************************        
       6100-SELECT-FROM-TUPCPLS.                                                
                                                                                
           EXEC SQL                                                             
               SELECT COUNT(*)                                                  
               INTO   :PV-TUPCPLS-COUNT                                         
               FROM    TUPCPLS                                                  
               WHERE   UPC_ID = :SV-TUPCPLS-UPC-ID                              
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   CONTINUE                                             04791037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6100-SELECT-FROM-TUPCPLS'  04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6100-SELECT-FROM-TUPCPLS'                  04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6100-SELECT-FROM-TUPCPLS'                      04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-08     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************03910024
      * SELECT A ROW FROM THE TUPC TABLE THAT MATCHES THE UPC ID        03920024
      ******************************************************************03940024
       6200-SELECT-TUPC-ROW.                                            03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
               SELECT  UPC_ID                                                   
                      ,UPC_NBR                                                  
                      ,SUB_CL_MDSEAR_ID                                         
                      ,VS_ID                                                    
                      ,INTR_UPC_ID                                              
                      ,EFF_BEG_DTE                                              
                      ,EFF_END_DTE                                              
                      ,UPC_TYP_CDE                                              
                      ,KOHLS_SIZE_CDE                                           
                      ,NRF_SIZE_IND                                             
                      ,NRF_CLOR_CDE                                             
                      ,NRF_CLOR_IND                                             
                      ,CSTM_CLOR_DESC                                           
                      ,EDI_ORD_IND                                              
                      ,PREPK_IND                                                
                      ,GP_CDE                                                   
                      ,CERTD_IND                                                
                      ,CERTD_DTE                                                
                      ,RPLNMT_IND                                               
                      ,KY_ITM_IND                                               
                      ,UNT_COST_1ST_AMT                                         
                      ,UPC_SRC_CDE                                              
                      ,INNR_PK_QTY                                              
                      ,OUTR_PK_QTY                                              
                      ,SEA_SEQ_NBR                                              
                      ,CRE_BY_USR_ID                                            
                      ,LAST_UPD_BY_USR_ID                                       
                      ,LAST_UPD_TMST                                            
                      ,WD_UM_QTY                                                
                      ,DPTH_UM_QTY                                              
                      ,HT_UM_QTY                                                
                      ,SIZE_UM_CDE                                              
                      ,WT_UM_QTY                                                
                      ,WT_UM_CDE                                                
                      ,POG_IND                                                  
                      ,REGN_PRIC_IND                                            
                      ,TX_PRD_CDE_SEQ_NBR                                       
                      ,CUSTFACG_CLOR_DESC                                       
                      ,INTR_UPC_DESC                                            
                 INTO  :UPC-UPC-ID                                              
                      ,:UPC-UPC-NBR                                             
                      ,:UPC-SUB-CL-MDSEAR-ID                                    
                      ,:UPC-VS-ID                                               
                      ,:UPC-INTR-UPC-ID                                         
                      ,:UPC-EFF-BEG-DTE                                         
                      ,:UPC-EFF-END-DTE                                         
                      ,:UPC-UPC-TYP-CDE                                         
                      ,:UPC-KOHLS-SIZE-CDE                                      
                      ,:UPC-NRF-SIZE-IND                                        
                      ,:UPC-NRF-CLOR-CDE                                        
                      ,:UPC-NRF-CLOR-IND                                        
                      ,:UPC-CSTM-CLOR-DESC                                      
                      ,:UPC-EDI-ORD-IND                                         
                      ,:UPC-PREPK-IND                                           
                      ,:UPC-GP-CDE                                              
                      ,:UPC-CERTD-IND                                           
                      ,:UPC-CERTD-DTE                                           
                      ,:UPC-RPLNMT-IND                                          
                      ,:UPC-KY-ITM-IND                                          
                      ,:UPC-UNT-COST-1ST-AMT                                    
                      ,:UPC-UPC-SRC-CDE                                         
                      ,:UPC-INNR-PK-QTY                                         
                      ,:UPC-OUTR-PK-QTY                                         
                      ,:UPC-SEA-SEQ-NBR                                         
                      ,:UPC-CRE-BY-USR-ID                                       
                      ,:UPC-LAST-UPD-BY-USR-ID                                  
                      ,:UPC-LAST-UPD-TMST                                       
                      ,:UPC-WD-UM-QTY                                           
                      ,:UPC-DPTH-UM-QTY                                         
                      ,:UPC-HT-UM-QTY                                           
                      ,:UPC-SIZE-UM-CDE                                         
                      ,:UPC-WT-UM-QTY                                           
                      ,:UPC-WT-UM-CDE                                           
                      ,:UPC-POG-IND                                             
                      ,:UPC-REGN-PRIC-IND                                       
                      ,:UPC-TX-PRD-CDE-SEQ-NBR                                  
                      ,:UPC-CUSTFACG-CLOR-DESC                                  
                      ,:UPC-INTR-UPC-DESC                                       
                  FROM TUPC                                                     
               WHERE UPC_ID       = :SV-TUPCPLS-UPC-ID                          
                 AND EFF_END_DTE < (CURRENT DATE - 20 DAYS)                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   PERFORM 5000-WRITE-TUPC-RECORD                       04792037
                   SET PS-TUPC-ROW-FOUND TO TRUE                                
               WHEN +100                                                04790024
                   ADD 1              TO PA-MISSING-TUPC-COUNT          04792037
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   MOVE SV-TUPCPLS-UPC-ID                               04930037
                                      TO PE-MSG-10-UPC-ID               04930037
                                                                        00670024
204416             IF SV-TUPCPLS-UPC-ID  < PC-1000000000-NUM            02630024
204416                 MOVE 0           TO PE-MSG-10-UPC-ID-1           0024    
204416             ELSE                                                 0024    
204416                 IF SV-TUPCPLS-UPC-ID  < PC-2000000000-NUM        0024    
204416                     MOVE 1       TO PE-MSG-10-UPC-ID-1           0024    
204416                 ELSE                                             0024    
204416                     MOVE 2       TO PE-MSG-10-UPC-ID-1           0024    
204416                 END-IF                                           02770024
204416             END-IF                                               02770024
                   DISPLAY PE-MSG-10                                    04830037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6200-SELECT-TUPC-ROW'      04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6200-SELECT-TUPC-ROW'                      04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6200-SELECT-TUPC-ROW'                          04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-11     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************03910024
      * DELETE A ROW FROM THE TUPC TABLE THAT MATCHES THE UPC ID        03920024
      ******************************************************************03940024
       6300-DELETE-TUPC-ROW.                                            03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
               DELETE                                                           
                  FROM TUPC                                                     
               WHERE UPC_ID       = :SV-TUPCPLS-UPC-ID                          
                 AND EFF_END_DTE < (CURRENT DATE - 20 DAYS)                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   ADD 1              TO PA-DEL-TUPC-COUNT              04792037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6300-DELETE-TUPC-ROW'      04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6300-DELETE-TUPC-ROW'                      04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6300-DELETE-TUPC-ROW'                          04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-09     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 05709034
      *                                                                 05710024
      ******************************************************************05720024
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05730024
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05740024
      *            CONTROL TABLE                                       *05750024
      ******************************************************************05760024
       7300-GET-COMMIT-TIMESTAMP.                                       05770024
                                                                        05780024
           EXEC SQL                                                     05790024
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05800024
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05810024
               INTO :PV-COMMIT-TIMESTAMP                                05820024
               FROM TCKRSTCNTL                                          05830024
              WHERE JOB_NAME  = :PC-JOB-NAME                            05840024
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05850024
           END-EXEC.                                                    05860024
                                                                        05870024
           IF SQLCODE = 0                                               05880030
               CONTINUE                                                 05890030
           ELSE                                                         05900030
               MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME    05910030
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     05920030
               PERFORM 9999-SQL-ABEND                                   05930030
           END-IF.                                                      05940030
      *                                                                 05950024
      *                                                                 05960024
      ******************************************************************05970024
      * 7400-INIT-CHECKPOINT-SELECT                                    *05980024
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05990024
      *            IF WE ARE IN A RESTART CONDITION.                   *06000024
      ******************************************************************06010024
       7400-INIT-CHECKPOINT-SELECT.                                     06020024
                                                                        06030024
           EXEC SQL                                                     06040024
             SELECT  CKPT_STAT_CODE                                     06050024
                    ,CKPT_FRQNCY_QTY                                    06060024
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06070024
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06080024
               FROM  TCKRSTCNTL                                         06090024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06100024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06110024
           END-EXEC.                                                    06120024
                                                                        06130024
           IF SQLCODE = 0                                               06140024
               CONTINUE                                                 06150024
           ELSE                                                         06160024
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06170024
               MOVE PE-MSG-02             TO PV-OPERATION-ATTEMPTED     06180024
               PERFORM 9999-SQL-ABEND                                   06190024
           END-IF.                                                      06200024
      *                                                                 06210024
      *                                                                 06220024
      ******************************************************************06230024
      * 7500-SELECT-RESTART-INFO                                       *06240024
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06250024
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06260024
      ******************************************************************06270024
       7500-SELECT-RESTART-INFO.                                        06280024
                                                                        06290024
           EXEC SQL                                                     06300024
             SELECT  WORK_STRG_DESC                                     06310024
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06320024
               FROM  TCKRSTINF                                          06330024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06340024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06350024
           END-EXEC.                                                    06360024
                                                                        06370024
           IF SQLCODE = 0                                               06380024
               CONTINUE                                                 06390024
           ELSE                                                         06400024
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06410024
               PERFORM 9999-SQL-ABEND                                   06420024
           END-IF.                                                      06430024
      *                                                                 06440024
      *                                                                 06450024
      ******************************************************************06460024
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06470024
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06480024
      ******************************************************************06490024
       7600-SET-CURRENT-TIMESTAMP.                                      06500024
                                                                        06510024
           EXEC SQL                                                     06520024
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06530024
           END-EXEC.                                                    06540024
      *                                                                 06550024
           IF SQLCODE = 0                                               06560024
               CONTINUE                                                 06570024
           ELSE                                                         06580024
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06590024
               PERFORM 9999-SQL-ABEND                                   06600024
           END-IF.                                                      06610024
      *                                                                 06620024
      *                                                                 06630024
      ******************************************************************06640024
      * 7700-COMMIT-PROCESSING.                                        *06650024
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06660024
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06670024
      ******************************************************************06680024
       7700-COMMIT-PROCESSING.                                          06690024
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06700024
                                                                        06710024
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06720024
      *                                                                 06730024
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06740024
      *        PREPARE COMMIT RECORD FOR UPDATE                         06750024
               ADD 1                          TO PA-COMMIT-COUNT        06760024
               MOVE SV-TUPCPLS-UPC-ID         TO CR-TUPCPLS-UPC-ID      03580024
               MOVE SV-TUPCPLS-OPERCO-NBR     TO CR-TUPCPLS-OPERCO-NBR  03590024
               MOVE SV-TUPCPLS-STR-NBR        TO CR-TUPCPLS-STR-NBR     03600024
               MOVE SV-TUPCPLS-EFF-BEG-TMST   TO CR-TUPCPLS-EFF-BEG-TMST03610024
               MOVE PA-COMMIT-COUNT           TO CR-COMMIT-COUNT                
               SUBTRACT +1 FROM PA-INPUT-COUNT                                  
                     GIVING CR-INPUT-READ-COUNT                                 
               MOVE PA-DEL-TUPCPLS-COUNT      TO CR-DEL-TUPCPLS-COUNT   06842037
               MOVE PA-DEL-TUPC-COUNT         TO CR-DEL-TUPC-COUNT      06842037
               MOVE PA-MISSING-TUPC-COUNT     TO CR-MISSING-TUPC-COUNT  06842037
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06870024
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06880024
                                             TCKRSTINF-WORK-STRG-DESC   06890024
               IF PS-FIRST-COMMIT                                       06900024
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06910024
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06920024
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06930024
               END-IF                                                   06940024
               PERFORM 7800-COMMIT-CHANGES                              06950024
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06960024
           END-IF.                                                      06970024
      *                                                                 06980024
      *                                                                 06990024
      ******************************************************************07000024
      * 7800-COMMIT-CHANGES.                                            07010024
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07020024
      *            TAKE A COMMIT.                                       07030024
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07040024
      ******************************************************************07050024
       7800-COMMIT-CHANGES.                                             07060024
                                                                        07070024
           EXEC SQL                                                     07080024
             UPDATE TCKRSTINF                                           07090024
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07100024
              WHERE JOB_NAME       = :PC-JOB-NAME                       07110024
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07120024
           END-EXEC.                                                    07130024
                                                                        07140024
           IF SQLCODE = 0                                               07150024
               CONTINUE                                                 07160024
           ELSE                                                         07170024
               MOVE PE-MSG-03                  TO PV-OPERATION-ATTEMPTED07180024
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07190024
               PERFORM 9999-SQL-ABEND                                   07200024
           END-IF.                                                      07210024
                                                                        07220024
           EXEC SQL                                                     07230024
               COMMIT                                                   07240024
           END-EXEC.                                                    07250024
                                                                        07260024
           IF SQLCODE = 0                                               07270024
               CONTINUE                                                 07280024
           ELSE                                                         07290024
               MOVE PE-MSG-04                  TO PV-OPERATION-ATTEMPTED07300024
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07310024
               PERFORM 9999-SQL-ABEND                                   07320024
           END-IF.                                                      07330024
      *                                                                 07340024
      *                                                                 07350024
      ******************************************************************07360024
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07370024
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07380024
      *                                                                 07390024
      ******************************************************************07400024
       7900-UPDATE-CHECKPOINT-STATUS.                                   07410024
                                                                        07420024
           EXEC SQL                                                     07430024
             UPDATE  TCKRSTCNTL                                         07440024
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07450024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07460024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07470024
           END-EXEC.                                                    07480024
                                                                        07490024
           IF SQLCODE = 0                                               07500024
               CONTINUE                                                 07510024
           ELSE                                                         07520024
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07530024
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07540024
               PERFORM 9999-SQL-ABEND                                   07550024
           END-IF.                                                      07560024
                                                                        07570024
           EJECT                                                        07580024
      *                                                                 07590024
      *                                                                 07600024
      ******************************************************************07610024
      *  PROGRAM RESTART DUE TO A DEADLOCK.                             07620024
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07630024
      ******************************************************************07640024
       7999-RESTART-PROCESS.                                            07650024
      *                                                                 07660024
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07670024
      *                                                                 07680024
           CLOSE TUPCPLS-PURGE-FILE.                                    07690024
      *                                                                 07700024
           PERFORM 7500-SELECT-RESTART-INFO.                            07710024
      *                                                                 07720024
           DISPLAY '**** RESTART **** '.                                07730024
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07740024
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07750024
      *                                                                 07760024
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07770024
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07780024
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07790037
           IF PS-FIRST-COMMIT                                           07800024
               INITIALIZE TCKRSTINF-WORK-STRG-DESC                      07810024
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07820024
                      ' BEGINNING'                                      07830024
           ELSE                                                         07840024
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07850024
                        TCKRSTINF-WORK-STRG-DESC (1:83)                 07860040
      ***    INFO ON LAST CHECKPOINT TAKEN IS IN                        07870024
      ***    CR-CHECKPOINT-RESTART-AREA.                                07880024
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07890024
                     CR-CHECKPOINT-RESTART-AREA                         07900024
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07910024
               IF CR-TUPCPLS-UPC-ID < PC-1000000000-NUM                 02630024
                   DISPLAY 'UPC ID            ' CR-TUPCPLS-UPC-ID       02630024
               ELSE                                                     02770024
                   IF CR-TUPCPLS-UPC-ID < PC-2000000000-NUM             02630024
                       MOVE CR-TUPCPLS-UPC-ID TO PV-UPC-ID-1LOW         02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-1                                02630024
                   ELSE                                                 02770024
                       MOVE CR-TUPCPLS-UPC-ID TO PV-UPC-ID-2LOW         02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-2                                02630024
                   END-IF                                               02770024
               END-IF                                                   02770024
               DISPLAY 'OPERCO NBR        ' CR-TUPCPLS-OPERCO-NBR       02640024
               DISPLAY 'STORE NBR         ' CR-TUPCPLS-STR-NBR          02650024
               DISPLAY 'BEGIN TIME STAMP  ' CR-TUPCPLS-EFF-BEG-TMST     02660024
               DISPLAY 'INPUT RECS READ   ' CR-INPUT-READ-COUNT         07970024
           END-IF.                                                      07980024
      *                                                                 07990024
           OPEN INPUT TUPCPLS-PURGE-FILE.                               08000024
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08010024
      *                                                                 08020024
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08030024
           PERFORM 4000-READ-TUPCPLS-PURGE-FILE                         08040024
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               08050024
                 OR PS-NO-MORE-INPUT-RECORDS.                           08060024
           IF PS-NO-MORE-INPUT-RECORDS                                  08070024
               DISPLAY 'END OF INPUT FILE ON RESTART'                   08080024
           ELSE                                                         08090024
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              08100024
                        PA-INPUT-COUNT                                  08110024
               IF IM007-TUPCPLS-UPC-ID < PC-1000000000-NUM              02630024
                   DISPLAY 'UPC ID            ' IM007-TUPCPLS-UPC-ID    02630024
               ELSE                                                     02770024
                   IF IM007-TUPCPLS-UPC-ID < PC-2000000000-NUM          02630024
                       MOVE IM007-TUPCPLS-UPC-ID TO PV-UPC-ID-1LOW      02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-1                                02630024
                   ELSE                                                 02770024
                       MOVE IM007-TUPCPLS-UPC-ID TO PV-UPC-ID-2LOW      02630024
                       DISPLAY 'UPC ID            '                     02630024
                             PV-UPC-ID-2                                02630024
                   END-IF                                               02770024
               END-IF                                                   02770024
               DISPLAY 'OPERCO NBR         ' IM007-TUPCPLS-OPERCO-NBR   08130024
               DISPLAY 'STORE NBR          ' IM007-TUPCPLS-STR-NBR      08140024
               DISPLAY 'BEGIN TIME STAMP   ' IM007-TUPCPLS-EFF-BEG-TMST 08150024
           END-IF.                                                      08170024
      *                                                                 08180024
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 08190024
           MOVE CR-DEL-TUPCPLS-COUNT    TO PA-DEL-TUPCPLS-COUNT.        08202037
           MOVE CR-DEL-TUPC-COUNT       TO PA-DEL-TUPC-COUNT.           08202037
           MOVE CR-MISSING-TUPC-COUNT   TO PA-MISSING-TUPC-COUNT.       08202037
           MOVE CR-COMMIT-COUNT         TO PA-COMMIT-COUNT.             08230024
      *                                                                 08240024
      *                                                                 08250024
      ******************************************************************08260024
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08270024
      ******************************************************************08280024
       8000-EOJ-ROUTINE.                                                08290024
      *                                                                 08300024
           IF SV-TUPCPLS-UPC-ID > ZERO                                          
               ADD 1                          TO PA-COMMIT-COUNT        06760024
               MOVE SV-TUPCPLS-UPC-ID         TO CR-TUPCPLS-UPC-ID      03580024
               MOVE SV-TUPCPLS-OPERCO-NBR     TO CR-TUPCPLS-OPERCO-NBR  03590024
               MOVE SV-TUPCPLS-STR-NBR        TO CR-TUPCPLS-STR-NBR     03600024
               MOVE SV-TUPCPLS-EFF-BEG-TMST   TO CR-TUPCPLS-EFF-BEG-TMST03610024
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03630024
               MOVE PA-COMMIT-COUNT           TO CR-COMMIT-COUNT                
               MOVE PA-DEL-TUPCPLS-COUNT      TO CR-DEL-TUPCPLS-COUNT   06842037
               MOVE PA-DEL-TUPC-COUNT         TO CR-DEL-TUPC-COUNT      06842037
               MOVE PA-MISSING-TUPC-COUNT     TO CR-MISSING-TUPC-COUNT  06842037
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03640024
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03650024
                                             TCKRSTINF-WORK-STRG-DESC   03660024
               PERFORM 7800-COMMIT-CHANGES                              06950024
           END-IF.                                                      03890024
      *                                                                 08330024
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08310024
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08320024
      *                                                                 08330024
           DISPLAY '                                             '.     08340024
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  08350024
           DISPLAY 'DELETED TUPCPLS ', PA-DEL-TUPCPLS-COUNT.            08360238
           DISPLAY 'DELETED TUPC    ', PA-DEL-TUPC-COUNT.               08360238
           DISPLAY 'MISSING TUPC    ', PA-MISSING-TUPC-COUNT.           08360238
           DISPLAY '                                             '.     08390024
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 08400024
      *                                                                 08410024
           CLOSE TUPCPLS-PURGE-FILE                                     08420024
                 TUPC-PURGE-FILE.                                       08420024
      *                                                                 08430024
      *                                                                 08440024
      ******************************************************************08450024
      *  PERFORMED BY 6100-UPDATE AND 7200-INSERT                       08460037
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08470024
      ******************************************************************08480024
       9000-DEADLOCK-ERROR.                                             08490024
      *                                                                 08500024
               MOVE PE-MSG-05             TO PV-OPERATION-ATTEMPTED     08510024
               PERFORM 9999-SQL-ABEND.                                  08520024
      *                                                                 08530024
      ******************************************************************08540024
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08550024
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08560024
      ******************************************************************08570024
       9999-SQL-ABEND.                                                  08580024
                                                                        08590024
           MOVE +4013                 TO ABEND-CODE.                    08600024
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            08610024
           DISPLAY '**  ABEND     **'.                                  08620024
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08630024
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08640024
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08650024
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           08660024
                                                                        08670024
           EXEC SQL                                                     08680024
                ROLLBACK                                                08690024
           END-EXEC.                                                    08700024
                                                                        08710024
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08720024
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08730024
                                                                        08740024
           COPY DPPD004.                                                08750024
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08760024
