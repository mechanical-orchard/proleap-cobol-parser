000100*************************                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*************************                                                 
000400                                                                          
000500 PROGRAM-ID.      TRKUT330.                                               
000600 AUTHOR.          ABRAHAM IVERSON.                                        
000700 DATE-WRITTEN.    03/13/02.                                               
000800                                                                          
000900******************************************************************        
000901* LOAD ENT ID                                                             
000902******************************************************************        
002500                                                                          
002600 ENVIRONMENT DIVISION.                                                    
002700 CONFIGURATION SECTION.                                                   
002800                                                                          
002900 SOURCE-COMPUTER. IBM-3090.                                               
003000 OBJECT-COMPUTER. IBM-3090.                                               
003100                                                                          
003200 INPUT-OUTPUT SECTION.                                                    
003300 FILE-CONTROL.                                                            
003400                                                                          
003500     SELECT TMS-PO-NBR-FILE                                               
003600         ASSIGN                  TO INP01.                                
003700     SELECT TMS-BUSLOC-INFO-FILE                                          
003800         ASSIGN                  TO OUT01.                                
003801     SELECT TMS-ENT-ID-FILE                                               
003802         ASSIGN                  TO OUT02.                                
P48867     SELECT TMS-PO-ENT-ID-ERROR-FILE                                      
P48867         ASSIGN                  TO OUT03.                                
004300                                                                          
004800***************                                                           
004900 DATA DIVISION.                                                           
005000***************                                                           
005100 FILE SECTION.                                                            
005200                                                                          
006900 FD  TMS-PO-NBR-FILE                                                      
007000     RECORDING MODE F                                                     
007100     BLOCK CONTAINS 0 RECORDS                                             
007200     RECORD CONTAINS 80 CHARACTERS                                        
007300     LABEL RECORDS ARE STANDARD                                           
007400     DATA RECORD IS TMS-PO-NBR-RECORD.                                    
007500 01  TMS-PO-NBR-RECORD                     PIC  X(80).                    
007501                                                                          
007502 FD  TMS-BUSLOC-INFO-FILE                                                 
007503     RECORDING MODE F                                                     
007504     BLOCK CONTAINS 0 RECORDS                                             
007505     RECORD CONTAINS 300 CHARACTERS                                       
007506     LABEL RECORDS ARE STANDARD                                           
007507     DATA RECORD IS TMS-BUSLOC-INFO-RECORD.                               
007508 01  TMS-BUSLOC-INFO-RECORD                PIC  X(300).                   
007509                                                                          
007510 FD  TMS-ENT-ID-FILE                                                      
007511     RECORDING MODE F                                                     
007512     BLOCK CONTAINS 0 RECORDS                                             
007513     RECORD CONTAINS 80 CHARACTERS                                        
007514     LABEL RECORDS ARE STANDARD                                           
007515     DATA RECORD IS TMS-ENT-ID-RECORD.                                    
007516 01  TMS-ENT-ID-RECORD                     PIC  X(80).                    
007517                                                                          
P48867 FD  TMS-PO-ENT-ID-ERROR-FILE                                             
P48867     RECORDING MODE F                                                     
P48867     BLOCK CONTAINS 0 RECORDS                                             
P48867     RECORD CONTAINS 80 CHARACTERS                                        
P48867     LABEL RECORDS ARE STANDARD                                           
P48867     DATA RECORD IS TMS-PO-ENT-ID-ERROR-RECORD.                           
P48867 01  TMS-PO-ENT-ID-ERROR-RECORD            PIC  X(80).                    
P48867                                                                          
007518************************                                                  
007520 WORKING-STORAGE SECTION.                                                 
007530************************                                                  
007540                                                                          
013000 01  PS-PROGRAM-SWITCHES.                                                 
033700     05  PS-END-OF-INPUT-SW            PIC  X(01)    VALUE 'N'.           
033800         88  PS-END-OF-INPUT                         VALUE 'Y'.           
P48867*    05  PS-ENT-ID-FOUND-SW            PIC  X(01)    VALUE 'N'.           
P48867*        88  PS-ENT-ID-FOUND                         VALUE 'Y'.           
P48867*        88  PS-ENT-ID-FOUND-NO                      VALUE 'N'.           
P48867*    05  PS-CORP-ADDR-FOUND-SW         PIC  X(01)    VALUE 'N'.           
P48867*        88  PS-CORP-ADDR-FOUND                      VALUE 'Y'.           
P48867*        88  PS-CORP-ADDR-FOUND-NO                   VALUE 'N'.           
033835                                                                          
033836 01  PC-PROGRAM-CONSTANTS.                                                
033837     05  PC-PGM-NAME                   PIC X(08)     VALUE                
033838                                                     'TRKUT330'.          
034200 01  PV-PROGRAM-VARIABLES.                                                
034300     05  PV-CURRENT-TMST               PIC X(26)  VALUE SPACES.           
035500     05  PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.           
035700     05  PV-DB2-OPERATION-MESSAGE      PIC X(79)  VALUE SPACES.           
035800     05  PV-DB2-TABLE-NAME-1           PIC X(18)  VALUE SPACES.           
035900     05  PV-DB2-TABLE-NAME-2           PIC X(18)  VALUE SPACES.           
036000     05  PV-DB2-TABLE-NAME-3           PIC X(18)  VALUE SPACES.           
036100                                                                          
037100 01  PV-INPUT-VARIABLES.                                                  
037200     05  PV-PO-NBR                     PIC X(9)   VALUE SPACES.           
037201                                                                          
037202 01  PV-OUTPUT-VARIABLES.                                                 
037203     05 FILLER                   PIC X(02)  VALUE 'C|'.                   
037204     05 PV-ENT-NAME-DESC-1       PIC X(10)  VALUE SPACES.                 
037205     05 FILLER                   PIC X(13)  VALUE '00-CORPORATE|'.        
037206     05 PV-ADDRESS-LINE1-DESC    PIC X(40)  VALUE SPACES.                 
037207     05 FILLER                   PIC X(02)  VALUE '||'.                   
037208     05 PV-CNTRY-CDE             PIC X(03)  VALUE SPACES.                 
037209     05 FILLER                   PIC X(01)  VALUE '|'.                    
037210     05 PV-CITY-NAME             PIC X(30)  VALUE SPACES.                 
037220     05 FILLER                   PIC X(01)  VALUE '|'.                    
037230     05 PV-STATE-CDE             PIC X(02)  VALUE SPACES.                 
037240     05 FILLER                   PIC X(01)  VALUE '|'.                    
037250     05 PV-ZIP-CDE               PIC X(12)  VALUE SPACES.                 
037260     05 FILLER                   PIC X(10)  VALUE '||||||||||'.           
037270     05 PV-ENT-NAME-DESC-2       PIC X(30)  VALUE SPACES.                 
037280     05 FILLER                   PIC X(02)  VALUE ' |'.                   
037290     05 PV-ENT-NAME-DESC-3       PIC X(30)  VALUE SPACES.                 
037300     05 FILLER                   PIC X(02)  VALUE ' |'.                   
037400     05 PV-ENT-ID                PIC X(09)  VALUE ZEROES.                 
039300                                                                          
P48867 01  PV-OUTPUT-ERROR-VARIABLES.                                           
P48867     05 FILLER                   PIC X(03)  VALUE 'PO:'.                  
P48867     05 PV-PO-NBR-ERROR          PIC X(09)  VALUE SPACES.                 
P48867     05 FILLER                   PIC X(01)  VALUE ','.                    
P48867     05 FILLER                   PIC X(06)  VALUE 'ENTID:'.               
P48867     05 PV-ENT-ID-ERROR          PIC X(09)  VALUE SPACES.                 
P48867     05 FILLER                   PIC X(01)  VALUE ','.                    
P48867     05 FILLER                   PIC X(08)  VALUE 'ERR MSG:'.             
P48867     05 PV-ERR-MSG               PIC X(43)  VALUE SPACES.                 
P48867                                                                          
041609 01  ABEND-CODE                        PIC S9(04) VALUE ZEROS             
041610                                                  COMP SYNC.              
041611     88  PC-CONTROL-CARD-ERROR                    VALUE +4001.            
041612     88  PC-DB2-ERROR                             VALUE +4013.            
041613                                                                          
102349*---------------------------------------------------------------*         
102350*    DB2 AREA FOR TENTNME                                       *         
102351*---------------------------------------------------------------*         
102352     EXEC SQL                                                             
102353          INCLUDE TENTNME                                                 
102354     END-EXEC.                                                            
102355*---------------------------------------------------------------*         
102356*    DB2 AREA FOR TPURCHS                                       *         
102357*---------------------------------------------------------------*         
102358     EXEC SQL                                                             
102359          INCLUDE TPURCHS                                                 
102360     END-EXEC.                                                            
102361*---------------------------------------------------------------*         
102362*    DB2 AREA FOR TBUSLOC                                       *         
102363*---------------------------------------------------------------*         
102364     EXEC SQL                                                             
102365          INCLUDE TBUSLOC                                                 
102366     END-EXEC.                                                            
117600*---------------------------------------------------------------*         
117601*  ABEND PROCESSING COPYBOOK.                                    *        
117602*---------------------------------------------------------------*         
117603     COPY DPWS004.                                                        
117604*---------------------------------------------------------------*         
117700*  DB2 COMMUNICATION AREA.                                                
117800*---------------------------------------------------------------*         
117900     EXEC SQL                                                             
118000         INCLUDE SQLCA                                                    
118100     END-EXEC.                                                            
130101                                                                          
130102*****************************************************************         
130103 PROCEDURE DIVISION.                                                      
130104*****************************************************************         
130105                                                                          
130106*--------------------------------------------------------------*          
130107* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *          
130108* PROGRAM.                                                     *          
130109*--------------------------------------------------------------*          
130110 0000-PROGRAM-MAINLINE.                                                   
130120                                                                          
130130     PERFORM 1000-INITIALIZE.                                             
130140                                                                          
130190     PERFORM 2000-PROCESS-PO-CURSOR                                       
130200       UNTIL PS-END-OF-INPUT.                                             
130300                                                                          
130301     CLOSE TMS-BUSLOC-INFO-FILE                                           
130302           TMS-PO-NBR-FILE                                                
P48867           TMS-ENT-ID-FILE                                                
P48867           TMS-PO-ENT-ID-ERROR-FILE.                                      
130304                                                                          
132800     GOBACK.                                                              
132801                                                                          
132802*--------------------------------------------------------------*          
132803 1000-INITIALIZE.                                                         
132804                                                                          
132805     OPEN INPUT  TMS-PO-NBR-FILE                                          
132806          OUTPUT TMS-BUSLOC-INFO-FILE                                     
P48867                 TMS-ENT-ID-FILE                                          
P48867                 TMS-PO-ENT-ID-ERROR-FILE.                                
132808                                                                          
132809     INITIALIZE PV-PROGRAM-VARIABLES.                                     
132810     PERFORM 7000-READ-INPUT-FILE.                                        
132820                                                                          
423470*--------------------------------------------------------------*          
423480* PROCESS EACH PURCHASE ORDER GETTING BUSINESS INFO AND                   
423490* CREATEING OUTPUT FILE                                                   
423500*--------------------------------------------------------------*          
423600 2000-PROCESS-PO-CURSOR.                                                  
423700                                                                          
424000     PERFORM 3000-SELECT-ENT-INFO.                                        
424001*    ***  PKE9607 - ABEND IF NO ENTID FOUND - TKMA508                     
P48867*    IF PS-ENT-ID-FOUND                                                   
P48867*       CONTINUE                                                          
P48867*    ELSE                                                                 
P48867*      MOVE '2000-PROCESS-PO-CURSOR'                                      
P48867*                                TO PV-PARAGRAPH-NAME                     
P48867*      MOVE 'NO QUALIFYING ROWS FOUND ON ENT OR PO TBLS FOR PO '          
P48867*                                TO PV-DB2-OPERATION-MESSAGE(1:50)        
P48867*      MOVE PV-PO-NBR            TO PV-DB2-OPERATION-MESSAGE(51:9)        
P48867*      PERFORM 9100-DB2-ABEND                                             
P48867*    END-IF.                                                              
424020                                                                          
424030*    ***  PKE9607 - TRY 2ND SELECT IF NO BUSLOC FOUND - TKMA508           
P48867*    PERFORM 3100-SELECT-BUSLOC-CORP.                                     
P48867*    IF PS-CORP-ADDR-FOUND                                                
P48867*       CONTINUE                                                          
P48867*     ELSE                                                                
P48867*       PERFORM 3200-SELECT-BUSLOC-PRIMARY                                
P48867*    END-IF.                                                              
424200                                                                          
424201*    ***  PKE9607 - ABEND IF NO BUSLOC FOUND - TKMA508                    
P48867*    IF PS-CORP-ADDR-FOUND                                                
P48867*       CONTINUE                                                          
P48867*     ELSE                                                                
P48867*      MOVE '2000-PROCESS-PO-CURSOR'                                      
P48867*                                TO PV-PARAGRAPH-NAME                     
P48867*      MOVE 'NO QUALIFYING ROWS FOUND ON TBUSLOC FOR PO '                 
P48867*                                TO PV-DB2-OPERATION-MESSAGE(1:43)        
P48867*      MOVE PV-PO-NBR            TO PV-DB2-OPERATION-MESSAGE(44:9)        
P48867*      PERFORM 9100-DB2-ABEND                                             
P48867*    END-IF.                                                              
424225                                                                          
P48867*    PERFORM 8000-WRITE-RECORD.                                           
P48867*    PERFORM 7000-READ-INPUT-FILE.                                        
424230                                                                          
496494*-----------------------------------------------------------------*       
496495* SELECT ENTERPRISE INFO:                                                 
496496*                                                                         
496497* PKE9607 - SELECT CORPORATE ADDRESS - TKMA508                            
496498*  REMOVED "DISTINCT" FROM SELECT                                         
496499*  ADDED "AND C.VER_STATUS_CDE = 'A'"                                     
496500*-----------------------------------------------------------------*       
496501 3000-SELECT-ENT-INFO.                                                    
496502                                                                          
P48867*        SET PS-ENT-ID-FOUND-NO TO TRUE.                                  
496504                                                                          
496505         EXEC SQL                                                         
496510          SELECT                                                          
496600                 A.ENT_ID                                                 
496700                ,A.ENT_NAME_DESC                                          
496800                ,A.ENT_NAME_DESC                                          
496801                ,B.ENT_NAME_DESC                                          
496802          INTO  :ENTNME-ENT-ID                                            
496803               ,:PV-ENT-NAME-DESC-1                                       
496804               ,:PV-ENT-NAME-DESC-2                                       
496805               ,:PV-ENT-NAME-DESC-3                                       
496806            FROM                                                          
496807                 TENTNME   A,                                             
496808                 TENTNME   B,                                             
496809                 TPURCHS   C                                              
496810            WHERE                                                         
496820                   A.ENT_ID = C.ENT_ID                                    
496830              AND  A.ENT_ID = B.ENT_ID                                    
496840              AND  A.TYPE_CDE = '04'                                      
496850              AND  B.TYPE_CDE = '01'                                      
496851              AND  C.PO_NBR   = :PURCHS-PO-NBR                            
496852              AND  C.VER_STATUS_CDE = 'A'                                 
496853         END-EXEC.                                                        
496854                                                                          
496855     EVALUATE TRUE                                                        
496856         WHEN SQLCODE = ZERO                                              
P48867*              SET PS-ENT-ID-FOUND    TO TRUE                             
P48867               PERFORM 3100-SELECT-BUSLOC-CORP                            
496858         WHEN SQLCODE = -811                                              
P48867*              SET PS-ENT-ID-FOUND    TO TRUE                             
496861               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
496862               DISPLAY '@@@E  MORE THAN 1 ROW SELECTED'                   
496863               DISPLAY '@@@E     ENTID ' ENTNME-ENT-ID                    
496864               DISPLAY '@@@E    ENTNME ' PV-ENT-NAME-DESC-1               
496865               DISPLAY '@@@E        PO ' PV-PO-NBR                        
P48867               PERFORM 3100-SELECT-BUSLOC-CORP                            
P48867*        WHEN SQLCODE = -305                                              
496869         WHEN SQLCODE = +100                                              
496870               DISPLAY 'NOT A VALID PO, NO ENTERPRISE FOUND'              
496871               DISPLAY 'PO NBR = ' PURCHS-PO-NBR                          
P48867               MOVE 'PO NOT FOUND' TO PV-ERR-MSG                          
P48867               PERFORM 9000-WRITE-ERROR-RECORD                            
P48867               PERFORM 7000-READ-INPUT-FILE                               
496872         WHEN SQLWARN0 NOT = SPACES                                       
496873         WHEN SQLCODE < ZERO                                              
496874         WHEN SQLCODE > ZERO                                              
P48867*            MOVE '3100-FETCH-PO-ENT-ID-CSR'                              
P48867*                                TO PV-PARAGRAPH-NAME                     
P48867*            MOVE 'ERROR FETCHING WITH THE PO-ENT-ID-CSR'                 
P48867*                                TO PV-DB2-OPERATION-MESSAGE              
P48867             MOVE '3000-SELECT-ENT-INFO'                                  
P48867                                 TO PV-PARAGRAPH-NAME                     
P48867             MOVE 'ERROR FETCHING WITH THE PO-ENT-ID'                     
P48867                                 TO PV-DB2-OPERATION-MESSAGE              
496879             MOVE 'TPURCHS'      TO PV-DB2-TABLE-NAME-1                   
P48867*            MOVE 'TALLPLN'      TO PV-DB2-TABLE-NAME-2                   
P48867*            MOVE 'TPLNSHP'      TO PV-DB2-TABLE-NAME-3                   
P48867             MOVE 'TENTNME'      TO PV-DB2-TABLE-NAME-2                   
496882             PERFORM 9100-DB2-ABEND                                       
496883     END-EVALUATE.                                                        
496884 EJECT                                                                    
496885                                                                          
496886*----------------------------------------------------------------*        
496887*  SELECT BUSINESS INFO FROM CORP LOCATION - FUNCTION_ID = 4              
496888*        ***  PKE9607 - SELECT CORPORATE ADDRESS - TKMA508                
496890*        ***  ADD TO WHERE --> "AND A.FUNCTION_ID = 4"                    
496891*        ***  REMOVE "AND A.SEQ_NBR = 1"                                  
496892*----------------------------------------------------------------*        
496893 3100-SELECT-BUSLOC-CORP.                                                 
496894                                                                          
P48867*        SET PS-CORP-ADDR-FOUND-NO TO TRUE.                               
496898                                                                          
496899         EXEC SQL                                                         
497000          SELECT A.ADDRESS_LINE1_DESC                                     
497100                ,A.CITY_NAME                                              
497200                ,A.STATE_CDE                                              
497300                ,A.ZIP_CDE                                                
497301                ,A.CNTRY_CDE                                              
497302          INTO  :PV-ADDRESS-LINE1-DESC                                    
497303               ,:PV-CITY-NAME                                             
497304               ,:PV-STATE-CDE                                             
497305               ,:PV-ZIP-CDE                                               
497306               ,:PV-CNTRY-CDE                                             
497307          FROM   TBUSLOC   A                                              
497308          WHERE A.ENT_ID  = :ENTNME-ENT-ID                                
497309*--PKE9607  AND A.SEQ_NBR = 1                                             
497310            AND A.FUNCTION_ID = 4                                         
497311            AND A.PRIM_ADDR_IND = 'Y'                                     
497312         END-EXEC.                                                        
497313                                                                          
497314     EVALUATE TRUE                                                        
497315         WHEN SQLCODE = ZERO                                              
P48867*              SET PS-CORP-ADDR-FOUND TO TRUE                             
P48867               PERFORM 8000-WRITE-RECORD                                  
P48867               PERFORM 7000-READ-INPUT-FILE                               
P48867*        WHEN SQLCODE = -305                                              
497325         WHEN SQLCODE = +100                                              
P48867*              CONTINUE                                                   
P48867               PERFORM 3200-SELECT-BUSLOC-PRIMARY                         
497331*        *** PKE9607 - DISPLAY WHEN DUPLICATE ROWS - TKMA508              
497332         WHEN SQLCODE = -811                                              
P48867*              SET PS-CORP-ADDR-FOUND TO TRUE                             
497334               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
497335               DISPLAY '@@@1  MORE THAN 1 ROW SELECTED'                   
497336               DISPLAY '@@@1     ENTID ' ENTNME-ENT-ID                    
497337               DISPLAY '@@@1    ENTNME ' PV-ENT-NAME-DESC-1               
497338               DISPLAY '@@@1        PO ' PV-PO-NBR                        
497339               DISPLAY '@@@1   FUNCTION_ID ' BUSLOC-FUNCTION-ID           
497340               DISPLAY '@@@1   PRIM ADDR   ' BUSLOC-PRIM-ADDR-IND         
P48867               PERFORM 8000-WRITE-RECORD                                  
P48867               PERFORM 7000-READ-INPUT-FILE                               
497341         WHEN SQLWARN0 NOT = SPACES                                       
497342         WHEN SQLCODE < ZERO                                              
497343         WHEN SQLCODE > ZERO                                              
P48867*            MOVE '3100-FETCH-PO-ENT-ID-CSR'                              
P48867*                                TO PV-PARAGRAPH-NAME                     
P48867*            MOVE 'ERROR FETCHING WITH THE PO-ENT-ID-CSR'                 
P48867*                                TO PV-DB2-OPERATION-MESSAGE              
P48867*            MOVE 'TPURCHS'      TO PV-DB2-TABLE-NAME-1                   
P48867*            MOVE 'TALLPLN'      TO PV-DB2-TABLE-NAME-2                   
P48867*            MOVE 'TPLNSHP'      TO PV-DB2-TABLE-NAME-3                   
P48867             MOVE '3100-SELECT-BUSLOC-CORP'                               
P48867                                 TO PV-PARAGRAPH-NAME                     
P48867             MOVE 'ERROR FETCHING WITH THE BUSINESS-INFO'                 
P48867                                 TO PV-DB2-OPERATION-MESSAGE              
P48867             MOVE 'TBUSLOC'      TO PV-DB2-TABLE-NAME-1                   
P48867             MOVE ENTNME-ENT-ID  TO PV-ENT-ID                             
497351             PERFORM 9100-DB2-ABEND                                       
497352     END-EVALUATE.                                                        
497353 EJECT                                                                    
497354                                                                          
497355*----------------------------------------------------------------*        
497356* SELECT BUSINESS INFO - 2ND TRY WHEN THERE IS NO CORP LOCATION           
497357*                                                                         
497358* PKE9607 - 2ND TRY SELECT CORP ADDRESS USING PRELIM                      
497359*           NEW PARAGRAPH                                                 
497360*                                                                         
497361*----------------------------------------------------------------*        
497362 3200-SELECT-BUSLOC-PRIMARY.                                              
497363                                                                          
497366                                                                          
P48867*        SET PS-CORP-ADDR-FOUND-NO TO TRUE.                               
497368                                                                          
497369         EXEC SQL                                                         
497370          SELECT DISTINCT                                                 
497371                 A.ADDRESS_LINE1_DESC                                     
497372                ,A.CITY_NAME                                              
497373                ,A.STATE_CDE                                              
497374                ,A.ZIP_CDE                                                
497375                ,A.CNTRY_CDE                                              
497376          INTO  :PV-ADDRESS-LINE1-DESC                                    
497377               ,:PV-CITY-NAME                                             
497378               ,:PV-STATE-CDE                                             
497379               ,:PV-ZIP-CDE                                               
497380               ,:PV-CNTRY-CDE                                             
497381          FROM   TBUSLOC   A                                              
497382          WHERE A.ENT_ID  = :ENTNME-ENT-ID                                
497383            AND A.PRIM_ADDR_IND = 'Y'                                     
497384         END-EXEC.                                                        
497385                                                                          
497386     EVALUATE TRUE                                                        
497387         WHEN SQLCODE = ZERO                                              
P48867*              SET PS-CORP-ADDR-FOUND TO TRUE                             
P48867               PERFORM 8000-WRITE-RECORD                                  
P48867               PERFORM 7000-READ-INPUT-FILE                               
497393*        ***  PKE9607 - DISPLAY WHEN NO ROW OR NULLS - TKMA508            
P48867*        WHEN SQLCODE = -305                                              
497395         WHEN SQLCODE = +100                                              
497396               DISPLAY '++++++++++++++++++++++++++++++++++++'             
497397               DISPLAY '++++  NO TBUSLOC DATA AVAILABLE FOR'              
497398               DISPLAY '++++     ENTID ' ENTNME-ENT-ID                    
497399               DISPLAY '++++    ENTNME ' PV-ENT-NAME-DESC-1               
497400               DISPLAY '++++        PO ' PV-PO-NBR                        
P48867               MOVE 'INFORMATION RELATED TO THIS ENTID NOT FOUND'         
P48867                                      TO PV-ERR-MSG                       
P48867               PERFORM 9000-WRITE-ERROR-RECORD                            
P48867               PERFORM 7000-READ-INPUT-FILE                               
497401*        *** PKE9607 - DISPLAY WHEN DUPLICATE ROWS - TKMA508              
497402         WHEN SQLCODE = -811                                              
P48867*              SET PS-CORP-ADDR-FOUND TO TRUE                             
497404               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
497405               DISPLAY '@@@2  MORE THAN 1 ROW SELECTED'                   
497406               DISPLAY '@@@2     ENTID ' ENTNME-ENT-ID                    
497407               DISPLAY '@@@2    ENTNME ' PV-ENT-NAME-DESC-1               
497408               DISPLAY '@@@2        PO ' PV-PO-NBR                        
497409               DISPLAY '@@@2   PRIM ADDR = Y'                             
P48867               PERFORM 8000-WRITE-RECORD                                  
P48867               PERFORM 7000-READ-INPUT-FILE                               
497410         WHEN SQLWARN0 NOT = SPACES                                       
497411         WHEN SQLCODE < ZERO                                              
497412         WHEN SQLCODE > ZERO                                              
497413             MOVE '3200-SELECT-BUSLOC-PRIMARY'                            
497414                                 TO PV-PARAGRAPH-NAME                     
P48867*            MOVE 'ERROR FETCHING WITH THE PO-ENT-ID-CSR'                 
P48867*                                TO PV-DB2-OPERATION-MESSAGE              
P48867*            MOVE 'TPURCHS'      TO PV-DB2-TABLE-NAME-1                   
P48867*            MOVE 'TALLPLN'      TO PV-DB2-TABLE-NAME-2                   
P48867*            MOVE 'TPLNSHP'      TO PV-DB2-TABLE-NAME-3                   
P48867             MOVE 'ERROR FETCHING WITH THE BUSINESS-INFO'                 
P48867                                 TO PV-DB2-OPERATION-MESSAGE              
P48867             MOVE 'TBUSLOC'      TO PV-DB2-TABLE-NAME-1                   
P48867             MOVE ENTNME-ENT-ID  TO PV-ENT-ID                             
497420             PERFORM 9100-DB2-ABEND                                       
497421     END-EVALUATE.                                                        
497422 EJECT                                                                    
497423                                                                          
497424*--------------------------------------------------------------*          
497425 7000-READ-INPUT-FILE.                                                    
497426                                                                          
497427*  OPEN AND READ PO CONTROL FILE.                                         
497428     READ TMS-PO-NBR-FILE                                                 
497429     INTO PV-INPUT-VARIABLES                                              
497430         AT END                                                           
497431           SET PS-END-OF-INPUT TO TRUE.                                   
497432                                                                          
497433     MOVE PV-PO-NBR TO PURCHS-PO-NBR.                                     
497434                                                                          
497435*----------------------------------------------------------------*        
497436* WRITE RECORD TO FILE                                                    
497437*----------------------------------------------------------------*        
497438 8000-WRITE-RECORD.                                                       
497439                                                                          
497440     MOVE ENTNME-ENT-ID             TO PV-ENT-ID.                         
497441                                                                          
497442     WRITE TMS-ENT-ID-RECORD                                              
497443      FROM PV-ENT-ID.                                                     
497444                                                                          
497445     INSPECT PV-ENT-ID                                                    
497446          REPLACING LEADING '0' BY ' '.                                   
497447     INSPECT PV-ENT-NAME-DESC-1                                           
497448          REPLACING ALL ' ' BY '_'.                                       
497449                                                                          
497450     WRITE TMS-BUSLOC-INFO-RECORD                                         
497451      FROM PV-OUTPUT-VARIABLES.                                           
497452                                                                          
497453     INITIALIZE PV-OUTPUT-VARIABLES                                       
497454                ENTNME-ENT-ID.                                            
497455                                                                          
P48867*----------------------------------------------------------------*        
P48867* WRITE RECORD TO ERROR FILE                                              
P48867*----------------------------------------------------------------*        
P48867 9000-WRITE-ERROR-RECORD.                                                 
P48867                                                                          
P48867     MOVE ENTNME-ENT-ID             TO PV-ENT-ID-ERROR.                   
P48867     MOVE PURCHS-PO-NBR             TO PV-PO-NBR-ERROR.                   
P48867                                                                          
P48867     INSPECT PV-ENT-ID-ERROR                                              
P48867          REPLACING LEADING '0' BY ' '.                                   
P48867                                                                          
P48867     WRITE TMS-PO-ENT-ID-ERROR-RECORD                                     
P48867      FROM PV-OUTPUT-ERROR-VARIABLES.                                     
P48867                                                                          
P48867     INITIALIZE PV-OUTPUT-ERROR-VARIABLES                                 
P48867                PURCHS-PO-NBR                                             
P48867                ENTNME-ENT-ID.                                            
P48867                                                                          
497456*----------------------------------------------------------------*        
497457*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
497458*  ERROR OR WARNING CODE OCCURS.                                          
497459*----------------------------------------------------------------*        
497460 9100-DB2-ABEND.                                                          
497461                                                                          
497462     DISPLAY '9100-DB2-ABEND'.                                            
497463     DISPLAY '** ABEND           **'.                                     
497464     DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.                      
497465     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
497466     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME-1.              
497467     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-2.              
497468     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-3.              
497469     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE.         
497470     DISPLAY '** PO NUMBER       ** = ' PV-PO-NBR.                        
497471     DISPLAY '** ENT ID          ** = ' PV-ENT-ID.                        
497472                                                                          
497473*----------------------------------------------------------------*        
497474* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
497475* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
497476* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
497477* FORMAT.                                                                 
497478*----------------------------------------------------------------*        
497479     COPY DPPD004.                                                        
497480                                                                          
497481     SET PC-DB2-ERROR TO TRUE.                                            
497482**** CALL 'ILBOABN0' USING ABEND-CODE.                                    
P48867     CALL 'ILBOABN0' USING ABEND-CODE.                                    
497490*                                                                         
