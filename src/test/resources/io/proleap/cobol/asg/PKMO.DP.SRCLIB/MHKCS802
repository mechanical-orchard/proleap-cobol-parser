      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    MHKCS802.                                                 
       AUTHOR.        KRISTIN PETERSON.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-30-2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *               CICS PROGRAM - MHKCS802 - F802                   *        
      *                                                                *        
      *        MERCHANDISE INQUIRY - SKU SELECTION LIST SCREEN         *        
      *                                                                *        
      *  THIS PROGRAM GENERATES A LIST OF SKU'S WHICH MEET THE         *        
      *  CRITERIA ENTERED IN F801 - SKU LIST CRITERA SCREEN.           *        
      *  THIS TRANSACTION IS ONLY INVOKED WHEN A NON-FULLY QUALIFIED   *        
      *  SKU IS ENTERED IN F801.                                       *        
      *                                                                *        
      ******************************************************************        
      *  MAINTENANCE                                                   *        
      ******************************************************************        
      *  07/30/2004 - KRISTIN PETERSON  (WR# 26354)                    *        
      *    INITIAL INSTALLATION.  NOTE THAT THIS IS A REVISED VERSION  *        
      *    OF PROGRAM FSKCS036.                                        *        
SR3403*  05/01/2012 - JOHN SELWITSCHKA  (CRQ000000023799)              *        
SR3403*    LARGE RETAIL EXPANSION.                                     *        
      ******************************************************************        
262978* CHANGED 10/21/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
CICS  * CHANGED 10/28/2022    BY JIM HUNTER      WR: CHG0277420                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           15  DK-DEPT-C3                     PIC S9(4)V COMP-3.                
           15  DK-MCL-LOW-C3                  PIC S9(4)V COMP-3.                
           15  DK-MCL-HIGH-C3                 PIC S9(4)V COMP-3.                
           15  DK-SCL-LOW-C3                  PIC S9(4)V COMP-3.                
           15  DK-SCL-HIGH-C3                 PIC S9(4)V COMP-3.                
           15  DK-SKU-LOW-C3                  PIC S9(8)V COMP-3.                
           15  DK-SKU-HIGH-C3                 PIC S9(8)V COMP-3.                
           15  DK-STORE                       PIC S9(04) VALUE +0               
                                                         COMP.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'MH802A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'MHKM802 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'MHKCS802'.           
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
           05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
           05  PC-KEY-QUEUE-SEQ               PIC  9(01)  VALUE 4.              
           05  PC-ONE                         PIC S9(04)  COMP VALUE +1.        
           05  PC-30-STARS                    PIC X(30)   VALUE                 
                                       '******************************'.        
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
                                                          +1000000              
                                                          COMP-3.               
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
                                                          COMP-3.               
           05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
           05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
           05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
           05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
                                                                                
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
               10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
               10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
               10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
               10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
               10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
               10  PC-TSYMSG-00370            PIC  9(05)  VALUE 00370.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
               88  PS-NO-ERROR                            VALUE 'Y'.            
               88  PS-ERROR                               VALUE 'N'.            
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-COMMAREA-VALID                      VALUE 'Y'.            
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
           05  PS-RCP-PRICE-SW                PIC  X(01)  VALUE 'N'.            
               88  PS-RCP-PRICE-FOUND                     VALUE 'Y'.            
               88  PS-RCP-PRICE-NOT-FOUND                 VALUE 'N'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER-LONG           PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  FILLER  REDEFINES  PV-BLOCK-NUMBER-LONG.                         
               10                             PIC  X(02).                       
               10  PV-BLOCK-NUMBER            PIC S9(04)  COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
                                                                                
           05  PV-ITEM-LONG                   PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  FILLER  REDEFINES  PV-ITEM-LONG.                                 
               10                             PIC  X(02).                       
               10  PV-ITEM                    PIC S9(04)  COMP SYNC.            
                                                                                
           05  PV-SEL-QUEUE-ITEM-LONG         PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  FILLER  REDEFINES  PV-SEL-QUEUE-ITEM-LONG.                       
               10                             PIC  X(02).                       
               10  PV-SEL-QUEUE-ITEM          PIC S9(04)  COMP SYNC.            
                                                                                
           05  PV-SAVE-ITEM-LONG              PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  FILLER  REDEFINES  PV-SAVE-ITEM-LONG.                            
               10                             PIC  X(02).                       
               10  PV-SAVE-ITEM               PIC S9(04)  COMP SYNC.            
                                                                                
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-START-OF-SELECTED-RANGE     PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-OF-SELECTED-RANGE       PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-ENT-ID                      PIC S9(09)  VALUE +0              
                                                          COMP.                 
SR3403*    05  PV-CUR-RTL-FMT                 PIC ZZZZ.99.                      
SR3403     05  PV-CUR-RTL-FMT                 PIC ZZZZZ.99.                     
           05  PV-STATUS-CODE-9               PIC 9(02)   VALUE ZERO.           
           05  PV-STORE-NBR-INT               PIC S9(04)  VALUE +0              
                                                          COMP.                 
      ******************************************************************        
      **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
      **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
      **  APPLICATION-SPECIFIC COMMAREA.                              **        
      ******************************************************************        
       01  LQW-LIST-QUEUE-WORK-AREA.                                            
           05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
           05  LQW-ITEM-ENTRIES               OCCURS 14 TIMES                   
                                              INDEXED BY LQW-IDX.               
               10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
               10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
               10  LQW-SELECTED-SW            PIC  X(01).                       
                   88  LQW-SELECT                         VALUE 'S'.            
                   88  LQW-DESELECT                       VALUE ' '.            
               10  LQW-ACTION                 PIC  X(03).                       
               10  LQW-MCL-NMBR               PIC  X(02).                       
               10  LQW-SCL-NMBR               PIC  X(02).                       
               10  LQW-SKU-NMBR               PIC  X(08).                       
               10  LQW-SKU-DESC               PIC  X(15).                       
SR3403*        10  LQW-CUR-RTL                PIC  X(07).                       
SR3403         10  LQW-CUR-RTL                PIC  X(08).                       
               10  LQW-VENDOR-NAME            PIC  X(18).                       
SR3403*        10  LQW-VENDOR-STYLE-CODE      PIC  X(19).                       
SR3403         10  LQW-VENDOR-STYLE-CODE      PIC  X(18).                       
               10  LQW-STATUS-CODE            PIC  S9(3)  COMP-3.               
               10  LQW-ITEM-NMBR              PIC  S9(15) COMP-3.               
               10  LQW-STORE-NMBR             PIC  9(05).                       
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE SKU SELECTION TEMP STORAGE QUEUE.      *         
      *---------------------------------------------------------------*         
                                                                                
           COPY MHWS001.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY MHKM802.                                                        
                                                                                
       01  MR-OCCURS-LAYOUT                   REDEFINES  MH802AO.               
           05  FILLER                         PIC X(87).                        
           05  MR-SCROLL-AREA                 OCCURS 14 TIMES.                  
               10  MR-SELECTL                 PIC S9(04) COMP.                  
               10  MR-SELECTA                 PIC  X(01).                       
               10  MR-SELECTC                 PIC  X(01).                       
               10  MR-SELECTH                 PIC  X(01).                       
               10  MR-SELECTI                 PIC  X(01).                       
                                                                                
               10  MR-SUBCLASSL               PIC S9(04) COMP.                  
               10  MR-SUBCLASSA               PIC  X(01).                       
               10  MR-SUBCLASSC               PIC  X(01).                       
               10  MR-SUBCLASSH               PIC  X(01).                       
               10  MR-SUBCLASS                PIC  X(02).                       
                                                                                
               10  MR-SKUL                    PIC S9(04) COMP.                  
               10  MR-SKUA                    PIC  X(01).                       
               10  MR-SKUC                    PIC  X(01).                       
               10  MR-SKUH                    PIC  X(01).                       
               10  MR-SKU                     PIC  X(08).                       
                                                                                
               10  MR-SKU-DESCL               PIC S9(04) COMP.                  
               10  MR-SKU-DESCA               PIC  X(01).                       
               10  MR-SKU-DESCC               PIC  X(01).                       
               10  MR-SKU-DESCH               PIC  X(01).                       
               10  MR-SKU-DESC                PIC  X(15).                       
                                                                                
               10  MR-CUR-RTLL                PIC S9(04) COMP.                  
               10  MR-CUR-RTLA                PIC  X(01).                       
               10  MR-CUR-RTLC                PIC  X(01).                       
               10  MR-CUR-RTLH                PIC  X(01).                       
SR3403*        10  MR-CUR-RTL                 PIC  X(07).                       
SR3403         10  MR-CUR-RTL                 PIC  X(08).                       
                                                                                
               10  MR-VENDOR-NAMEL            PIC S9(04) COMP.                  
               10  MR-VENDOR-NAMEA            PIC  X(01).                       
               10  MR-VENDOR-NAMEC            PIC  X(01).                       
               10  MR-VENDOR-NAMEH            PIC  X(01).                       
               10  MR-VENDOR-NAME             PIC  X(18).                       
                                                                                
               10  MR-VENDOR-STYLE-CODEL      PIC S9(04) COMP.                  
               10  MR-VENDOR-STYLE-CODEA      PIC  X(01).                       
               10  MR-VENDOR-STYLE-CODEC      PIC  X(01).                       
               10  MR-VENDOR-STYLE-CODEH      PIC  X(01).                       
SR3403*        10  MR-VENDOR-STYLE-CODE       PIC  X(19).                       
SR3403         10  MR-VENDOR-STYLE-CODE       PIC  X(18).                       
                                                                                
               10  MR-STATUS-CODEL            PIC S9(04) COMP.                  
               10  MR-STATUS-CODEA            PIC  X(01).                       
               10  MR-STATUS-CODEC            PIC  X(01).                       
               10  MR-STATUS-CODEH            PIC  X(01).                       
               10  MR-STATUS-CODE.                                              
                   15  MR-STATUS-CODE-9       PIC  9(02).                       
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           COPY MHWS000.                                                        
      *---------------------------------------------------------------*         
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-HOLD-ENT-ID                     PIC S9(09)           
                                                           COMP.                
                   15  ASC-HOLD-VENDOR-NAME                PIC  X(30).          
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
                       20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
                           88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
                           88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
                                                                                
                       20  ASC-ITEM-ARRAY.                                      
                           25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.                
                               30  ASC-LIST-ITEM-NUMBER                         
                                                           PIC S9(09)           
                                                           COMP-3.              
                               30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
                                   88  ASC-VALID-SELECT    VALUE ' '            
                                                                 'S'            
                                                                 'B'            
                                                                 'E'.           
                                   88  ASC-TEMP-NO-SELECT  VALUE ' '.           
                                   88  ASC-TEMP-SELECT     VALUE 'S'.           
                                   88  ASC-TEMP-BEGIN      VALUE 'B'.           
                                   88  ASC-TEMP-END        VALUE 'E'.           
                               30  ASC-SELECTED-SW         PIC  X(01).          
                                   88  ASC-NO-SELECT       VALUE ' '.           
                                   88  ASC-SELECT          VALUE 'S'.           
                                   88  ASC-BEGIN           VALUE 'B'.           
                                   88  ASC-END             VALUE 'E'.           
                               30  ASC-ACTION              PIC  X(03).          
                               30  ASC-MCL-NMBR            PIC  X(02).          
                               30  ASC-MCL-NMBR-9 REDEFINES                     
                                      ASC-MCL-NMBR         PIC  9(02).          
                               30  ASC-SCL-NMBR            PIC  X(02).          
                               30  ASC-SCL-NMBR-9 REDEFINES                     
                                      ASC-SCL-NMBR         PIC  9(02).          
                               30  ASC-SKU-NMBR            PIC  X(08).          
                               30  ASC-SKU-NMBR-9 REDEFINES                     
                                      ASC-SKU-NMBR         PIC  9(08).          
                               30  ASC-SKU-DESC            PIC  X(15).          
SR3403*                        30  ASC-CUR-RTL             PIC  X(07).          
SR3403                         30  ASC-CUR-RTL             PIC  X(08).          
                               30  ASC-VENDOR-NAME         PIC  X(18).          
SR3403*                        30  ASC-VENDOR-STYLE-CODE   PIC  X(19).          
SR3403                         30  ASC-VENDOR-STYLE-CODE   PIC  X(18).          
                               30  ASC-STATUS-CODE         PIC  S9(3)           
                                                                COMP-3.         
                               30  ASC-ITEM-NMBR           PIC  S9(15)          
                                                                COMP-3.         
                               30  ASC-STORE-NMBR          PIC  9(05).          
                                                                                
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(08)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
                   15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
                       88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
                       88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-LIST-SEQ                    PIC  9(01).          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                   15  ASC-KEY-QUEUE-NAME.                                      
                       20  ASC-KEY-TERM-ID                 PIC  X(04).          
                       20  ASC-KEY-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-KEY-SEQ                     PIC  9(01).          
                                                                                
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSK                                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSK                                                     
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSKXREF                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSKST                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSKST                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    PRICE LOOKUP TABLE                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    VENDOR TABLE XMT_VND                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE XMT00000                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMESSAGE CURSOR WHEN MESSAGE NUMBER IS REQUESTED           *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE TSK_CURSOR CURSOR                                       
                FOR SELECT                                                      
                      DEPT                                                      
                     ,CLASS                                                     
                     ,SUBCLASS                                                  
                     ,SKU                                                       
                     ,CUR_UNIT_RTL_AMT                                          
                     ,VENDOR_STYLE_CODE                                         
                     ,STATUS_CODE                                               
                     ,SKU_DESC                                                  
                     ,ENT_ID                                                    
                     ,TSK.ITEM_NMBR                                             
                     ,LONG_VENDOR_STYLE                                         
                     ,REGN_PRIC_IND                                             
                FROM  TSK                                                       
                     ,TSKST                                                     
                     ,TSKXREF                                                   
               WHERE ( DEPT                   = :DK-DEPT-C3                     
                 AND  (CLASS            BETWEEN :DK-MCL-LOW-C3 AND              
                                                :DK-MCL-HIGH-C3)                
                 AND  (SUBCLASS         BETWEEN :DK-SCL-LOW-C3 AND              
                                                :DK-SCL-HIGH-C3)                
                 AND  (SKU              BETWEEN :DK-SKU-LOW-C3 AND              
                                                :DK-SKU-HIGH-C3)                
                 AND   LOC_NBR                = :DK-STORE                       
                 AND   TSKXREF.SKU_STAT_CDE  ^= '00'                            
                 AND   TSK.ITEM_NMBR          =  TSKXREF.ITM_NBR                
                 AND   TSK.ITEM_NMBR          =  TSKST.ITEM_NMBR )              
                                                                                
                ORDER BY                                                        
                      DEPT                                                      
                     ,CLASS                                                     
                     ,SUBCLASS                                                  
                     ,SKU                                                       
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF MH802AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
      ******************************************************************        
       0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          MH802AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE '0001-CALL-CICS-ARCH-API'                                   
                                         TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
                                            ASC-SEL-TERM-ID                     
                                            ASC-KEY-TERM-ID                     
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                         TO ASC-LIST-TASK-NUMBER                
                                            ASC-SEL-TASK-NUMBER                 
                                            ASC-KEY-TASK-NUMBER                 
                   MOVE PC-LIST-QUEUE-SEQ                                       
                                         TO ASC-LIST-SEQ                        
                   MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
                   MOVE PC-KEY-QUEUE-SEQ TO ASC-KEY-SEQ                         
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  PS-COMMAREA-NOT-VALID                                    
      *-->             INVALID SKU PASSED TO PROGRAM                            
                       MOVE PC-TSYMSG-00370                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                         TO TRUE                                
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
                   PERFORM 4130-READ-KEY-QUEUE                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                         TO DP013-MESSAGE-TEXT(1)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * EDIT COMMAREA DATA                                             *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID       TO TRUE.                                 
           MOVE SPACE                  TO ASC-START-ITEM-REQUEST.               
                                                                                
           IF MH000-DEPT-NUMBER-X NOT NUMERIC                                   
               SET  PS-COMMAREA-NOT-VALID TO TRUE                               
               MOVE SPACES     TO  MH000-DEPT-NUMBER-X                          
                                   MH000-DEPT-NAME                              
           END-IF.                                                              
                                                                                
           IF MH000-MCL-NUMBER-X NOT NUMERIC                                    
               MOVE SPACES     TO  MH000-MCL-NUMBER-X                           
                                   MH000-CLASS-DESCRIPTION                      
           END-IF.                                                              
                                                                                
           IF MH000-SCL-NUMBER-X NOT NUMERIC                                    
               MOVE SPACES     TO  MH000-SCL-NUMBER-X                           
                                   MH000-CLASS-DESCRIPTION                      
           END-IF.                                                              
                                                                                
           IF MH000-SKU-NUMBER-X NOT NUMERIC                                    
               MOVE SPACES     TO  MH000-SKU-NUMBER-X                           
                                   MH000-SKU-DESCRIPTION                        
           END-IF.                                                              
                                                                                
           IF MH000-STORE-NUMBER-X NOT NUMERIC                                  
               MOVE SPACES     TO  MH000-STORE-NUMBER-X                         
                                   MH000-STORE-NAME                             
                                   MH000-STORE-SHORT-NAME                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITING OF THE DATA    *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 2050-DELETE-LIST-QUEUE                               
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
                   PERFORM 2140-DESELECT-ALL-ITEMS                              
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
                   PERFORM 2150-SELECT-ALL-ITEMS                                
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   END-IF                                                       
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
      *----------------------------------------------------------------*        
       2050-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
                                                                                
               MOVE '2050-DELETE-LIST-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
      *******  PF 14 KEY - SKU/STORE INQUIRY                                    
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                   IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
                       INITIALIZE MH000-TS-SELECTION-DATA                       
                       MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
                                      TO MH000-NUMBER-OF-SELECTED-ITEMS         
                       PERFORM 5000-PREPARE-SEL-QUEUE                           
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO TO TRUE                            
                       MOVE PC-TSYMSG-00057  TO DP020-MSG-NUMBER                
                       SET DP020-MSG-WARNING TO TRUE                            
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                   MOVE +1            TO PV-TOP-ITEM                            
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                   PERFORM 2110-BROWSE-BACKWARD                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
      ********                                                                  
                   PERFORM                                                      
                       VARYING PV-SUB FROM 1 BY 1                               
                       UNTIL PV-SUB > ASC-ITEMS-DISPLAYED                       
                          MOVE DP015-UNP-ALP-NOR-OFF                            
                                               TO MR-SELECTA (PV-SUB)           
                          MOVE DP015-GREEN     TO MR-SELECTC (PV-SUB)           
                          MOVE DP015-UNDERLINE TO MR-SELECTH (PV-SUB)           
                   END-PERFORM                                                  
      ********                                                                  
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                   PERFORM 2120-BROWSE-FORWARD                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
                   IF  PS-ERROR                                                 
                       CONTINUE                                                 
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
      ********                                                                  
                       PERFORM                                                  
                         VARYING PV-SUB FROM 1 BY 1                             
                         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED                     
                            MOVE DP015-UNP-ALP-NOR-OFF                          
                                                 TO MR-SELECTA (PV-SUB)         
                            MOVE DP015-GREEN     TO MR-SELECTC (PV-SUB)         
                            MOVE DP015-UNDERLINE TO MR-SELECTH (PV-SUB)         
                       END-PERFORM                                              
      ********                                                                  
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
      *    MESSAGE.                                                    *        
      *----------------------------------------------------------------*        
                                                                                
       2110-BROWSE-BACKWARD.                                                    
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
               GIVING PV-TOP-ITEM.                                              
                                                                                
           IF  ((PV-TOP-ITEM < +1)                                              
             OR (PV-TOP-ITEM = +1))                                             
               MOVE +1                   TO PV-TOP-ITEM                         
      *        -- TOP OF LIST --                                                
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
      *----------------------------------------------------------------*        
                                                                                
       2120-BROWSE-FORWARD.                                                     
                                                                                
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
               GIVING PV-TOP-ITEM.                                              
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
      *        -- BOTTOM OF LIST --                                             
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           ELSE                                                                 
               COMPUTE PV-BOTTOM-ITEM =                                         
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
      *            -- BOTTOM OF LIST --                                         
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *----------------------------------------------------------------*        
                                                                                
       2130-CHECK-FOR-JUMP-BROWSE.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           IF (ASC-START-ITEM-REQUEST = SPACE)                                  
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262978              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO  ASC-START-ITEM-REQUEST-N           
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *                        -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *                --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-MDT                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-MDT                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
      *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
      *----------------------------------------------------------------*        
       2140-DESELECT-ALL-ITEMS.                                                 
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER-LONG                
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER-LONG > PV-PREV-BLOCK-IN-ARRAY          
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB FROM 1 BY 1                         
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER-LONG                
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER-LONG                
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER-LONG                
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
           MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
           MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2141-CLEAR-SELECTED-INDS.                                                
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                   SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
      *  FOR ALL ITEMS.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2150-SELECT-ALL-ITEMS.                                                   
                                                                                
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER-LONG                
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER-LONG > PV-PREV-BLOCK-IN-ARRAY          
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER-LONG                
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER-LONG                
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER-LONG                
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
                             ASC-NUMBER-OF-SELECTED-ITEMS.                      
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *----------------------------------------------------------------*        
                                                                                
       2151-SET-SELECTED-INDS.                                                  
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                   SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                       ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
      ******************************************************************        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
           MOVE +1                       TO PV-SUB.                             
                                                                                
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           IF  ASTRTITL > ZERO                                                  
               MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
           ELSE                                                                 
               IF  ASTRTITF = DP015-ERASE-EOF                                   
                   INITIALIZE ASC-START-ITEM-REQUEST                            
               END-IF                                                           
           END-IF.                                                              
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
       2210-MOVE-LINES-TO-COMMAREA.                                             
           IF  MR-SELECTL (PV-SUB) > ZERO                                       
               SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
               MOVE MR-SELECTI (PV-SUB)                                         
                                         TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
           ELSE                                                                 
               IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
                       SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
                       MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
               END-IF                                                           
           END-IF.                                                              
           ADD +1                        TO ASC-ITEM-SUB.                       
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               MOVE +1                   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      ******************************************************************        
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
      ******************************************************************        
       3000-EDIT-DATA-IN-COMMAREA.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
      **                                                                        
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
      * EDITING IS DONE FROM THE BOTTOM UP.                                     
      **                                                                        
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
               ASC-ITEMS-DISPLAYED - 1.                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           PERFORM 3300-EDIT-SELECTIONS                                         
               VARYING PV-SUB                                                   
               FROM PV-SUB BY -1                                                
               UNTIL PV-SUB < +1                                                
                                                                                
           IF  PS-NO-ERROR                                                      
               PERFORM 3400-CHECK-RANGE-SEQUENCE                                
           END-IF.                                                              
                                                                                
           IF  ((PS-NO-ERROR)                                                   
             AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
             AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
                   PERFORM 3500-MARK-RANGE-AS-SELECTED                          
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
                                                                                
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
           ELSE                                                                 
               MOVE +1                     TO ASC-BLK-SUB                       
           END-IF.                                                              
      ******************************************************************        
      ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
      ******************************************************************        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED                               
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-SELECTA (PV-SUB)                 
               MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
               MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
           END-PERFORM.                                                         
      ******************************************************************        
      ** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
      ** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
      ** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
      ** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
      ******************************************************************        
       3300-EDIT-SELECTIONS.                                                    
           EVALUATE TRUE                                                        
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                   = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
                       CONTINUE                                                 
               WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                   IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
                       ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
                       MOVE ASC-TEMP-SELECTED-SW                                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                                      TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                       = SPACE                                                  
                   EVALUATE TRUE                                                
                       WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
                           SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
                           MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
                           MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-EVALUATE                                                 
               WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
                   SET DP020-MSG-FATAL    TO TRUE                               
                   MOVE -1                TO MR-SELECTL (PV-SUB)                
                   SET DP030-SET-CURSOR-APPL-1                                  
                                          TO TRUE                               
                   MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
                   MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
                   SET PS-ERROR TO TRUE                                         
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
                       MOVE SPACE         TO ASC-SELECTED-SW                    
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < 1                                                 
               SUBTRACT +1 FROM ASC-BLK-SUB                                     
               MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      ******************************************************************        
      ** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
      ** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
      ** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
      ** THE ENDING.                                                  **        
      ******************************************************************        
       3310-EDIT-RANGE.                                                         
           IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
               IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
                   MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
                                         TO ASC-SELECTED-SW                     
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
               ELSE                                                             
                   IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
                       MOVE PC-TSYMSG-00054                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
                       MOVE DP015-UNDERLINE                                     
                                         TO MR-SELECTH (PV-SUB)                 
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO ASC-SELECTED-SW                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
                   MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
               ELSE                                                             
                   IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE END-OF-RANGE ALLOWED --                      
                       MOVE PC-TSYMSG-00055                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
      ** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
      ******************************************************************        
       3400-CHECK-RANGE-SEQUENCE.                                               
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
             AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
             AND  ASC-START-OF-SELECTED-RANGE >                                 
                                               ASC-END-OF-SELECTED-RANGE        
      *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
               MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-FATAL TO TRUE                                      
               SET PS-ERROR TO TRUE                                             
               PERFORM 3410-POSITION-AT-ERROR                                   
           END-IF.                                                              
      ******************************************************************        
      ** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
      ** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
      ** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
      ******************************************************************        
       3410-POSITION-AT-ERROR.                                                  
           SET DP030-SET-CURSOR-APPL-1                                          
                                         TO TRUE.                               
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
      *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
      *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
      *----------------------------------------------------------------*        
                                                                                
       3500-MARK-RANGE-AS-SELECTED.                                             
           MOVE +1                       TO  ASC-BLK-SUB.                       
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO  PV-LIST-ITEM-NUMBER.               
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-START-OF-SELECTED-RANGE                                     
                                         TO PV-START-OF-SELECTED-RANGE.         
           COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
               1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-END-OF-SELECTED-RANGE                                       
                                         TO  PV-END-OF-SELECTED-RANGE.          
           COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
               1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
                     PC-ITEMS-PER-PANEL).                                       
           COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
               PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
                                                                                
           IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
              (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
               MOVE +1                   TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
                         PV-PREV-BLOCK-IN-ARRAY                                 
                   MOVE PV-FIRST-BLOCK-IN-RANGE                                 
                                         TO PV-BLOCK-NUMBER-LONG                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         PV-LAST-BLOCK-IN-RANGE                                 
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER-LONG                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER-LONG                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
                                            ASC-END-OF-SELECTED-RANGE.          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
      *----------------------------------------------------------------*        
                                                                                
       3510-MARK-SELECTED-INDS.                                                 
                                                                                
           IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) >                           
               ASC-START-OF-SELECTED-RANGE)                                     
           OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) =                           
               ASC-START-OF-SELECTED-RANGE)                                     
                                                                                
               IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) <                       
                   ASC-END-OF-SELECTED-RANGE)                                   
               OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) =                       
                   ASC-END-OF-SELECTED-RANGE)                                   
                                                                                
                   IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
                           ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
                                     TO TRUE                                    
                       SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
                                     TO TRUE                                    
                       ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
                                       ASC-BLOCK-ENTRIES (2).                   
           MOVE +1                 TO ASC-BLK-SUB.                              
           SET ASC-ITEMS-LEFT-ON-DB                                             
                                   TO TRUE.                                     
                                                                                
           MOVE +0                 TO ASC-ITEM-SUB                              
                                      ASC-NUMBER-OF-ITEMS-READ                  
                                      ASC-NUMBER-OF-SELECTED-ITEMS              
                                      ASC-HIGHEST-BLOCK-WRITTEN                 
                                      ASC-START-OF-SELECTED-RANGE               
                                      ASC-END-OF-SELECTED-RANGE                 
                                      ASC-ITEM-AT-TOP-OF-PANEL.                 
           PERFORM 4100-PREPARE-CURSOR                                          
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                            
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB.                                     
                                                                                
           PERFORM 4020-CLOSE-CURSOR.                                           
                                                                                
           MOVE +1                   TO  PV-TOP-ITEM.                           
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP030-SET-CURSOR-APPL-1                                      
                                     TO TRUE                                    
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
       EJECT                                                                    
       4010-OPEN-CURSOR.                                                        
                                                                                
           EXEC SQL                                                             
                OPEN TSK_CURSOR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4010-OPEN-CURSOR'                                     
                                     TO DP013-PARAGRAPH                         
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * CLOSE THE TMESSAGE CURSOR.                                     *        
      *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
      *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
      *   USER.                                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4020-CLOSE-CURSOR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE TSK_CURSOR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4020-CLOSE-CURSOR'                                    
                                     TO DP013-PARAGRAPH                         
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4100-PREPARE-CURSOR.                                                     
                                                                                
           IF DP020-FK-REFRESH(DP020-SRC-AID)                                   
      *-->     THIS TRANSACTION WAS ORIGINALLY INVOKED W/ A PARTIAL             
      *-->     KEY.  THE LOWER LEVEL (SELECTED) TRANSACTION (F803)              
      *-->     FULLY QUALIFIES THE MH000-SKU-STORE KEY FIELD.  A                
      *-->     REFRESH WOULD RESULT IN A SINGLE SKU BEING RETURNED              
      *-->     (THE LAST ONE VIEWED IN F803).                                   
      *-->                                                                      
      *-->     THE ORIGINAL MH000-SKU-STORE IS STORED IN THE                    
      *-->     TS-KEY-QUEUE.  PLACE IT'S CONTENTS INTO MH000-SKU-STORE          
      *-->     AND PROCEED AS NORMAL.                                           
      *-->                                                                      
               PERFORM 4130-READ-KEY-QUEUE.                                     
                                                                                
           MOVE MH000-DEPT-NUMBER-9        TO DK-DEPT-C3.                       
                                                                                
           IF  MH000-MCL-NUMBER-X = (SPACES OR LOW-VALUES)                      
               MOVE ZERO TO DK-MCL-LOW-C3                                       
               MOVE 99   TO DK-MCL-HIGH-C3                                      
      ****     SUBCLASS WILL ALWAYS BE BLANK WHEN MAJOR CLASS IS BLANK          
               MOVE ZERO TO DK-SCL-LOW-C3                                       
               MOVE 99   TO DK-SCL-HIGH-C3                                      
           ELSE                                                                 
               IF  (MH000-MCL-NUMBER-9 > 0)                                     
               AND (MH000-SCL-NUMBER-X = (SPACES OR LOW-VALUES))                
                   MOVE MH000-MCL-NUMBER-9 TO DK-MCL-LOW-C3                     
                   MOVE MH000-MCL-NUMBER-9 TO DK-MCL-HIGH-C3                    
                   MOVE ZERO               TO DK-SCL-LOW-C3                     
                   MOVE 99                 TO DK-SCL-HIGH-C3                    
               ELSE                                                             
                   IF  (MH000-MCL-NUMBER-9 > 0)                                 
                   AND (MH000-SCL-NUMBER-9 > 0)                                 
                       MOVE MH000-MCL-NUMBER-9 TO DK-MCL-LOW-C3                 
                       MOVE MH000-MCL-NUMBER-9 TO DK-MCL-HIGH-C3                
                       MOVE MH000-SCL-NUMBER-9 TO DK-SCL-LOW-C3                 
                       MOVE MH000-SCL-NUMBER-9 TO DK-SCL-HIGH-C3                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  MH000-SKU-NUMBER-X = (SPACES OR LOW-VALUES)                      
               MOVE ZERO               TO DK-SKU-LOW-C3                         
               MOVE 99999999           TO DK-SKU-HIGH-C3                        
           ELSE                                                                 
               MOVE MH000-SKU-NUMBER-9 TO DK-SKU-LOW-C3                         
                                          DK-SKU-HIGH-C3                        
           END-IF.                                                              
                                                                                
           IF  MH000-STORE-NUMBER-X = (SPACES OR LOW-VALUES OR '00000')         
               MOVE ZERO                 TO DK-STORE                            
           ELSE                                                                 
               MOVE MH000-STORE-NUMBER-9 TO DK-STORE                            
           END-IF.                                                              
                                                                                
           PERFORM 4110-DELETE-KEY-QUEUE.                                       
                                                                                
           PERFORM 4120-WRITE-KEY-QUEUE.                                        
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE KEY TEMP STORAGE QUEUE.                          *        
      *----------------------------------------------------------------*        
       4110-DELETE-KEY-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '4110-DELETE-KEY-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       4120-WRITE-KEY-QUEUE.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   FROM  (MH000-SKU-STORE)                                      
                   ITEM  (PC-ONE)                                               
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '4120-WRITE-KEY-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------------*        
      *    IF REFRESHING, PUT THE MH000-SKU-STORE KEY FIELD BACK TO    *        
      *    THE WAY IT WAS WHEN THIS TRANSACTION WAS ORIGINALLY INVOKED.*        
      *----------------------------------------------------------------*        
                                                                                
       4130-READ-KEY-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   INTO  (MH000-SKU-STORE)                                      
                   ITEM  (PC-ONE)                                               
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4130-READ-KEY-QUEUE'                                       
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
                                                                                
           PERFORM                                                              
               VARYING PV-READ-COUNT                                            
               FROM 1 BY 1                                                      
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
                                                                                
               PERFORM 4310-FETCH                                               
                                                                                
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                   IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
                       ADD +1            TO ASC-ITEM-SUB                        
                   ELSE                                                         
                       ADD +1            TO ASC-BLK-SUB                         
                       MOVE +1           TO ASC-ITEM-SUB                        
                                                                                
                       IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
                           MOVE +1       TO ASC-BLK-SUB                         
                           SET ASC-BLOCK-MODIFIED (1)                           
                                         TO TRUE                                
                           PERFORM 5100-WRITE-TEMP-STORAGE                      
                           MOVE ASC-BLOCK-ENTRIES                               
                                     (PC-MAX-BLOCKS-IN-ARRAY)                   
                                      TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
                           ADD +1        TO ASC-BLK-SUB                         
                           INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
                           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
                                         TO TRUE                                
                       END-IF                                                   
                   END-IF                                                       
                   ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
                   MOVE ASC-NUMBER-OF-ITEMS-READ                                
                                         TO ASC-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   MOVE SKXREF-CLASS     TO ASC-MCL-NMBR-9                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE SKXREF-SUBCLASS  TO ASC-SCL-NMBR-9                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE SKXREF-SKU       TO ASC-SKU-NMBR-9                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE TSK-SKU-DESC     TO ASC-SKU-DESC                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE ASC-HOLD-VENDOR-NAME                                    
                                         TO ASC-VENDOR-NAME                     
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
      *MED                                                                      
                   IF TSK-LONG-VENDOR-STYLE = SPACES                            
                      MOVE TSK-VENDOR-STYLE-CODE                                
                                         TO ASC-VENDOR-STYLE-CODE               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ELSE                                                         
                      MOVE TSK-LONG-VENDOR-STYLE                                
                                         TO ASC-VENDOR-STYLE-CODE               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   END-IF                                                       
      *MED                                                                      
                   MOVE TSK-ITEM-NMBR    TO ASC-ITEM-NMBR                       
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE DK-STORE         TO ASC-STORE-NMBR                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   IF TSK-REGN-PRIC-IND = 'Y'                                   
                      IF DK-STORE = ZERO                                        
                         PERFORM 4340-MOVE-RCP-CORP-DATA                        
                      ELSE                                                      
                         PERFORM 4330-GET-TUPCPLS-DATA                          
                         PERFORM 4350-MOVE-RCP-STORE-DATA                       
                      END-IF                                                    
                   ELSE                                                         
                      MOVE TSK-STATUS-CODE  TO ASC-STATUS-CODE                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                      MOVE TSK-CUR-UNIT-RTL-AMT TO PV-CUR-RTL-FMT               
                      MOVE PV-CUR-RTL-FMT TO ASC-CUR-RTL                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
               MOVE +1               TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE 1                TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER-LONG                    
               PERFORM 4410-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2            TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER-LONG                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE TSK CURSOR.  LOOKUP THE VENDOR NAME WHEN THE      *        
      *    TSK-ENT-ID CHANGES.                                         *        
      *----------------------------------------------------------------*        
                                                                                
       4310-FETCH.                                                              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = ZERO                                             
                  OR SQLCODE = +100                                             
                                                                                
                        EXEC SQL                                                
                             FETCH TSK_CURSOR                                   
                             INTO   :SKXREF-DEPT                                
                                   ,:SKXREF-CLASS                               
                                   ,:SKXREF-SUBCLASS                            
                                   ,:SKXREF-SKU                                 
                                   ,:TSK-CUR-UNIT-RTL-AMT                       
                                   ,:TSK-VENDOR-STYLE-CODE                      
                                   ,:TSK-STATUS-CODE                            
                                   ,:TSK-SKU-DESC                               
                                   ,:TSK-ENT-ID                                 
                                   ,:TSK-ITEM-NMBR                              
                                   ,:TSK-LONG-VENDOR-STYLE                      
                                   ,:TSK-REGN-PRIC-IND                          
                        END-EXEC                                                
                                                                                
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4310-FETCH-CURSOR'                                
                                         TO DP013-PARAGRAPH                     
                        MOVE 'FETCH MESSAGE CURSOR'                             
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4310-FETCH-CURSOR'  TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                     
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET ASC-NO-ITEMS-LEFT-ON-DB                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               IF TSK-ENT-ID = ASC-HOLD-ENT-ID                                  
                   NEXT SENTENCE                                                
               ELSE                                                             
                   PERFORM 4320-GET-VENDOR-NAME                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       4320-GET-VENDOR-NAME.                                                    
      ******************************************************************        
      *  READ THE VENDOR TABLE AND RETRIEVE THE RELATED VENDOR NAME.   *        
      *  IF ANY ERRORS ARE DETECTED, AN ERROR MESSAGE WILL             *        
      *  BE FORMATTED AND SENT TO THE SCREEN AND CONTROL WILL BE RE-   *        
      *  TURNED TO NATIVE CICS.                                        *        
      ******************************************************************        
                                                                                
           MOVE TSK-ENT-ID                 TO PV-ENT-ID                         
                                              ASC-HOLD-ENT-ID.                  
           MOVE SPACES                     TO ASC-HOLD-VENDOR-NAME.             
                                                                                
           PERFORM                                                              
               EXEC SQL                                                         
                    SELECT PRIM_VND_NM                                          
                      INTO :XMT00000-PRIM-VND-NM                                
                      FROM XMT_VND                                              
                     WHERE ENT_ID = :PV-ENT-ID                                  
                     WITH UR                                                    
               END-EXEC                                                         
           END-PERFORM.                                                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE XMT00000-PRIM-VND-NM TO ASC-HOLD-VENDOR-NAME            
               WHEN OTHER                                                       
                   MOVE PC-30-STARS          TO XMT00000-PRIM-VND-NM            
                                                ASC-HOLD-VENDOR-NAME            
           END-EVALUATE.                                                        
                                                                                
           EJECT                                                                
                                                                                
       4330-GET-TUPCPLS-DATA.                                                   
      ******************************************************************        
      *  READ THE TUPCPLS ROW FOR ANY SKU/STORE THAT IS REGIONALLY     *        
      *  CLEARANCE PRICED.  THE CURRENT RETAIL AND STATUS FIELDS WILL  *        
      *  BE NEEDED FOR THE SCREEN.  IF THE SKU/STORE COMBINATION DOES  *        
      *  NOT EXIST IN TUPCPLS, THE CURRENT RETAIL AND STATUS WILL BE   *        
      *  DERIVED FROM THE TSK TABLE VALUES.                            *        
      *  IF ANY ERRORS ARE DETECTED, AN ERROR MESSAGE WILL             *        
      *  BE FORMATTED AND SENT TO THE SCREEN AND CONTROL WILL BE RE-   *        
      *  TURNED TO NATIVE CICS.                                        *        
      ******************************************************************        
                                                                                
           INITIALIZE PV-RETRY-COUNTER.                                         
                                                                                
           MOVE DK-STORE TO PV-STORE-NBR-INT.                                   
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               UNTIL SQLCODE = ZERO                                             
                  OR SQLCODE = +100                                             
                  OR PV-RETRY-COUNTER > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                    SELECT  UPC_STAT_CDE                                        
                           ,UNT_RTL_AMT                                         
                           ,ITM_NBR                                             
                      INTO :UPCPLS-UPC-STAT-CDE                                 
                          ,:UPCPLS-UNT-RTL-AMT                                  
                          ,:UPCPLS-ITM-NBR                                      
                       FROM TUPCPLS                                             
                      WHERE OPERCO_NBR   = '03'                                 
                        AND ITM_NBR      = :TSK-ITEM-NMBR                       
                        AND STR_NBR      = :PV-STORE-NBR-INT                    
                        AND ((EFF_BEG_TMST <= CURRENT TIMESTAMP                 
                        AND EFF_END_TMST >= CURRENT TIMESTAMP)                  
                         OR (EFF_BEG_TMST <= CURRENT TIMESTAMP                  
                        AND EFF_END_TMST IS NULL))                              
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD +1 TO PV-RETRY-COUNTER                              
                   WHEN SQLCODE < ZERO                                          
                   WHEN ((SQLCODE > ZERO) AND (SQLCODE NOT = +100))             
                   WHEN SQLWARN0 NOT = SPACES                                   
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE 'Y' TO PS-RCP-PRICE-SW                                  
               WHEN SQLCODE = +100                                              
                   MOVE 'N' TO PS-RCP-PRICE-SW                                  
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
           EJECT                                                                
                                                                                
       4340-MOVE-RCP-CORP-DATA.                                                 
      ******************************************************************        
      *  MOVE THE VALUE 'MIXED' TO THE CURRENT RETAIL VALUE WHEN THE   *        
      *  REGIONAL PRICE INDICATOR IS SET TO 'Y' AND THE STORE NUMBER   *        
      *  IS 00000.  THE STATUS WILL BE THE STATUS FROM TSK.            *        
      *  NO CALL IS NEEDED TO GET THE UPCPLS CURRENT RETAIL BECAUSE    *        
      *  THERE WILL BE MULTIPLE PRICE POINTS.                          *        
      *                                                                *        
      *  THE RC PRICE SCREEN (F804) SHOWS THE DIFFERENT PRICES BY      *        
      *  STORE.  THE USER CANNOT DIRECTLY ACCESS THE RC PRICE SCREEN   *        
      *  FROM THIS SCREEN - THE RC PRICE SCREEN MAY BE ACCESSED FROM   *        
      *  THE SKU/STORE INQUIRY SCREEN (F803).                          *        
      ******************************************************************        
                                                                                
SR3403*    MOVE 'MIXED  '        TO ASC-CUR-RTL                                 
SR3403     MOVE 'MIXED   '       TO ASC-CUR-RTL                                 
                        (ASC-BLK-SUB ASC-ITEM-SUB).                             
                                                                                
           MOVE TSK-STATUS-CODE  TO ASC-STATUS-CODE                             
                        (ASC-BLK-SUB ASC-ITEM-SUB).                             
           EJECT                                                                
                                                                                
       4350-MOVE-RCP-STORE-DATA.                                                
      ******************************************************************        
      *  IF A ROW WAS FOUND ON THE TUPCPLS TABLE -                     *        
      *  MOVE THE UNIT RETAIL PRICE AND STATUS FROM TUPCPLS.           *        
      *  IF A ROW WAS NOT FOUND FOR THE SPECIFIC STORE -               *        
      *  MOVE THE TSK UNIT RETAIL PRICE AND CHECK THE STATUS CODE.     *        
      *  IF THE STATUS CODE IS MIXED (25) MOVE THE ACTIVE              *        
      *  STATUS (20) OTHERWISE MOVE THE TSK STATUS CODE.               *        
      ******************************************************************        
                                                                                
           IF PS-RCP-PRICE-FOUND                                                
              MOVE UPCPLS-UNT-RTL-AMT TO PV-CUR-RTL-FMT                         
              MOVE PV-CUR-RTL-FMT TO ASC-CUR-RTL                                
                                     (ASC-BLK-SUB ASC-ITEM-SUB)                 
              MOVE UPCPLS-UPC-STAT-CDE TO PV-STATUS-CODE-9                      
              MOVE PV-STATUS-CODE-9 TO ASC-STATUS-CODE                          
                                       (ASC-BLK-SUB ASC-ITEM-SUB)               
           ELSE                                                                 
              MOVE TSK-CUR-UNIT-RTL-AMT TO PV-CUR-RTL-FMT                       
              MOVE PV-CUR-RTL-FMT TO ASC-CUR-RTL                                
                                     (ASC-BLK-SUB ASC-ITEM-SUB)                 
              IF TSK-STATUS-CODE = 025                                          
                 MOVE 020 TO ASC-STATUS-CODE                                    
                             (ASC-BLK-SUB ASC-ITEM-SUB)                         
              ELSE                                                              
                 MOVE TSK-STATUS-CODE  TO ASC-STATUS-CODE                       
                                          (ASC-BLK-SUB ASC-ITEM-SUB)            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE MH000-DEPT-NUMBER-X   TO ADEPTI.                                
           MOVE MH000-DEPT-NAME       TO ADPTDSCI.                              
                                                                                
           MOVE MH000-MCL-NUMBER-X    TO AMJCLSI.                               
                                                                                
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1     TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER-LONG                              
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1 TO PV-BLOCK-NUMBER-LONG                           
                   END-IF                                                       
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   ADD +1     TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER-LONG                           
                   PERFORM 4410-READ-TEMP-STORAGE                               
           END-IF.                                                              
           MOVE +1            TO ASC-BLK-SUB.                                   
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
                                                                                
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1         TO ASC-BLK-SUB                                    
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
                                                                                
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
           PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
           PERFORM 4420-FORMAT-LIST-LINES                                       
               VARYING PV-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PV-SUB < +1.                                               
                                                                                
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
                                            ASTRTITO.                           
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO ASELITO.                            
                                                                                
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
      *            --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
                                                                                
       4410-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4410-READ-TEMP-STORAGE'                                    
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *----------------------------------------------------------------*        
                                                                                
       4420-FORMAT-LIST-LINES.                                                  
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
               MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
                                          MR-SKU (PV-SUB)                       
                                          MR-SUBCLASS (PV-SUB)                  
                                          MR-SKU-DESC (PV-SUB)                  
                                          MR-CUR-RTL (PV-SUB)                   
                                          MR-VENDOR-NAME (PV-SUB)               
                                          MR-VENDOR-STYLE-CODE (PV-SUB)         
                                          MR-STATUS-CODE (PV-SUB)               
           ELSE                                                                 
               IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                                         > SPACE                
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-SELECTI (PV-SUB)                   
               ELSE                                                             
                   MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                       TO MR-SELECTI (PV-SUB)                   
               END-IF                                                           
                                                                                
               IF  DP030-SET-CURSOR-TRAILER                                     
                   IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
                       > SPACE)                                                 
                     OR (PV-SUB = 1))                                           
                      SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
                      MOVE -1          TO MR-SELECTL (PV-SUB)                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   MOVE ASC-SCL-NMBR   (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-SUBCLASS (PV-SUB)                  
                   MOVE ASC-SKU-NMBR   (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-SKU (PV-SUB)                       
                   MOVE ASC-SKU-DESC   (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-SKU-DESC (PV-SUB)                  
                   MOVE ASC-CUR-RTL   (ASC-BLK-SUB ASC-ITEM-SUB)                
                                       TO MR-CUR-RTL (PV-SUB)                   
                   MOVE ASC-VENDOR-NAME(ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-VENDOR-NAME (PV-SUB)               
                   MOVE ASC-VENDOR-STYLE-CODE(ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-VENDOR-STYLE-CODE(PV-SUB)          
                   MOVE ASC-STATUS-CODE(ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-STATUS-CODE-9(PV-SUB)              
               END-IF                                                           
           END-IF.                                                              
           SUBTRACT +1 FROM ASC-ITEM-SUB                                        
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
      ******************************************************************        
      ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
      ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
      ** ISSUE A WARNING.                                             **        
      ******************************************************************        
       4430-CHECK-FOR-BEGIN-AND-END.                                            
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
      *            -- NO END-OF-RANGE SPECFIED ---                              
                   MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
      *            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
                   MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
      ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
      ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
      ** POSITION THE CURSOR THERE.                                   **        
      ******************************************************************        
       4431-POSITION-AT-WARNING.                                                
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
      * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
      * FOR THE SELECTED ITEMS QUEUE.                                  *        
      *                                                                *        
      * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
      * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
      * THE ARRAY IS FULL.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE ASC-SEL-QUEUE-NAME   TO MH000-SEL-QUEUE-NAME.                   
           PERFORM 6010-DELETE-SEL-QUEUE.                                       
                                                                                
           SET ASC-UPDATE-TSQ        TO TRUE.                                   
                                                                                
           MOVE ZERO                 TO MH000-NUMBER-OF-TS-ITEMS.               
           INITIALIZE MH001-TS-SEL-REC.                                         
                                                                                
           SET MH001-IDX             TO 1.                                      
           MOVE 20                   TO MH001-MAX-ITEMS                         
                                                                                
           PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
                   VARYING PV-ITEM-LONG                                         
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM-LONG > ASC-HIGHEST-BLOCK-WRITTEN.              
                                                                                
           IF  MH001-IDX > 1                                                    
               PERFORM 5205-WRITE-SEL-QUEUE                                     
               ADD +1                TO MH000-NUMBER-OF-TS-ITEMS                
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       5100-WRITE-TEMP-STORAGE.                                                 
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER-LONG =                                       
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER-LONG < ASC-HIGHEST-BLOCK-WRITTEN)                
           OR (PV-BLOCK-NUMBER-LONG = ASC-HIGHEST-BLOCK-WRITTEN)                
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 5105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 5110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5105-REWRITE-ASC-LISTQ.                                                  
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5110-WRITE-ASC-LISTQ.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       5200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 6112-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM                                                          
               VARYING LQW-IDX                                                  
               FROM 1 BY 1                                                      
               UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
                 OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
                                        TO MH001-ITEM (MH001-IDX)               
                       MOVE MH000-DEPT-NUMBER-X                                 
                                        TO MH001-DEPT-NMBR (MH001-IDX)          
                       MOVE LQW-MCL-NMBR (LQW-IDX)                              
                                        TO MH001-CLASS-NMBR (MH001-IDX)         
                       MOVE LQW-SKU-NMBR (LQW-IDX)                              
                                        TO MH001-SKU-NMBR (MH001-IDX)           
                       MOVE LQW-STORE-NMBR (LQW-IDX)                            
                                        TO MH001-STORE-NMBR (MH001-IDX)         
                       MOVE LQW-ITEM-NMBR (LQW-IDX)                             
                                        TO MH001-ITEM-NMBR (MH001-IDX)          
                       SET MH001-IDX UP BY 1                                    
                       IF  MH001-IDX > MH001-MAX-ITEMS                          
                           PERFORM 5205-WRITE-SEL-QUEUE                         
                           ADD +1        TO MH000-NUMBER-OF-TS-ITEMS            
                           INITIALIZE MH001-SELECTED-ITEM-ARRAY                 
                           SET MH001-IDX TO  1                                  
                       END-IF                                                   
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
      *----------------------------------------------------------------*        
                                                                                
       5205-WRITE-SEL-QUEUE.                                                    
                                                                                
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO MH001-SELECTED-ITEMS.               
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    FROM  (MH001-TS-SEL-REC)                                    
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5205-WRITE-SEL-QUEUE'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      ** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
      ** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
      ** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
      ** THE USER.                                                    **        
      ******************************************************************        
       6000-REFRESH-PANEL-FROM-TEMP.                                            
           IF  ASC-UPDATE-TSQ                                                   
               SET ASC-NO-TSQ-UPDATE TO TRUE                                    
               MOVE ZERO                 TO PV-SAVE-ITEM-LONG                   
                                            PV-SEL-QUEUE-ITEM-LONG              
               MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
                                         TO PV-HOLD-SELECTED-ITEMS              
               PERFORM 6100-UPDATE-TSQ                                          
                   UNTIL PV-ITEMS-PROCESSED >                                   
                                           ASC-NUMBER-OF-SELECTED-ITEMS         
               PERFORM 6010-DELETE-SEL-QUEUE                                    
               INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
                          ASC-BLOCK-ENTRIES (2)                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
      *----------------------------------------------------------------*        
       6010-DELETE-SEL-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '6010-DELETE-SEL-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       6100-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-SEL-QUEUE-ITEM-LONG.                
           PERFORM 6105-READ-SEL-QUEUE.                                         
           PERFORM                                                              
               VARYING MH001-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL ((PV-ITEMS-PROCESSED >                                     
                                    PV-HOLD-SELECTED-ITEMS)                     
               OR (MH001-IDX GREATER THAN MH001-MAX-ITEMS))                     
               IF  MH001-ACTION-SW (MH001-IDX) > SPACE                          
                   PERFORM 6110-PROCESS-ITEMS                                   
               END-IF                                                           
               ADD +1                TO PV-ITEMS-PROCESSED                      
           END-PERFORM.                                                         
           PERFORM 6111-WRITE-LIST-QUEUE.                                       
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *----------------------------------------------------------------*        
                                                                                
       6105-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    INTO  (MH001-TS-SEL-REC)                                    
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '6105-READ-SEL-QUEUE'                                       
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *----------------------------------------------------------------*        
                                                                                
       6110-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM-LONG = 1 + ((MH001-ITEM (MH001-IDX) - 1)             
                                / PC-ITEMS-PER-PANEL).                          
                                                                                
           IF  PV-ITEM-LONG NOT EQUAL PV-SAVE-ITEM-LONG                         
               IF  PV-SAVE-ITEM-LONG NOT EQUAL ZERO                             
                   PERFORM 6111-WRITE-LIST-QUEUE                                
               END-IF                                                           
               MOVE PV-ITEM-LONG     TO PV-SAVE-ITEM-LONG                       
                                                                                
               PERFORM 6112-READ-LIST-QUEUE                                     
               IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
                   CONTINUE                                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = (MH001-ITEM (MH001-IDX)                             
                          - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
           SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
           MOVE LQW-SELECTED-SW (PV-SUB)                                        
                                     TO LQW-TEMP-SELECTED-SW (PV-SUB).          
           EVALUATE TRUE                                                        
               WHEN MH001-INQ (MH001-IDX)                                       
                    MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN MH001-UPD (MH001-IDX)                                       
                    MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN MH001-DEL (MH001-IDX)                                       
                    MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN MH001-ERR (MH001-IDX)                                       
                    MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *----------------------------------------------------------------*        
                                                                                
       6111-WRITE-LIST-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-SAVE-ITEM)                                         
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6111-WRITE-LIST-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       6112-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
