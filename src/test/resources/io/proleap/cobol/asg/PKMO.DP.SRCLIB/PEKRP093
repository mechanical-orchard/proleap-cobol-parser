       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PEKRP093.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           26/12/2010.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * Title        - Perm Clearance Report -Reason Code CE2          *        
      *                                                                *        
      * Description  - This Program gets the Store details from PO     *        
      *                system and writes CE2 report Records            *        
      *                CE2 - On Order After Mark 1                              
      * Change History                                                 *        
      ******************************************************************        
      * WR Number     Programmer       Date                            *        
      * ---------------------------------------------------------------*        
      * WR34795       COGNIZANT        26/12/2010                               
      * Original Version                                               *        
      ******************************************************************        
C98147* CHG0098147    Linda Friess     11/11/2014                               
C98147*    EXPANDED PV-REP-PO-NBR to 8 DIGITS                                   
C98147******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PERM-MARK1-SKU-CE2  ASSIGN TO INP01.                          
                                                                                
           SELECT PERM-RPT-OUTFILE    ASSIGN TO OUT01.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PERM-MARK1-SKU-CE2                                                   
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PERM-MARK1-SKU-REC.                                   
       01  PERM-MARK1-SKU-REC          PIC X(336).                              
                                                                                
       FD  PERM-RPT-OUTFILE                                                     
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PERM-RPT-OUTFILE-REC        PIC X(200).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      *****************************************************************         
      *  Copybook for po plan shipment table                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TPLNSHP                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for sku store table                                           
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for PO - SKU store on order                                   
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKSTOO                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * Copybook for SQL communication area                           *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook of working storage for SQL abend routine                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  PC-program constants                                          *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           03  PC-PGM-NAME               PIC  X(08)  VALUE 'PEKRP093'.          
           03  PC-MAXIMUM-RETRY-COUNT    PIC S9(04)  VALUE +2 COMP SYNC.        
                                                                                
      *****************************************************************         
      *  Switches                                                      *        
      *****************************************************************         
       01  PS-RVOGRAM-SWITCHES.                                                 
           03  PS-MARK1-SKU-FILE-SW      PIC  X(01)  VALUE 'N'.                 
               88  EOF-MARK1-FILE                    VALUE 'Y'.                 
               88  NOT-EOF-MARK1-FILE                VALUE 'N'.                 
                                                                                
      *****************************************************************         
      *  General workarea                                             *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           03  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.              
           03  PV-DB2-OPER-ATTEMPTED     PIC  X(30)  VALUE SPACES.              
           03  PV-INP-RECS-CNT           PIC  S9(9)V COMP-3 VALUE 0.            
           03  PV-RECS-CNT               PIC  S9(9)V COMP-3 VALUE 0.            
           03  PV-CURR-DT-ED             PIC  S9(6)  COMP-3 VALUE +0.           
           03  PV-CENTURY                PIC  9(02)  VALUE ZERO.                
           03  PV-RETRY-COUNT            PIC  S9(04) VALUE ZERO.                
           03  PV-CRP-LST-RVD-DT         PIC  9(08)  VALUE ZERO.                
           03  PV-ED-LST-RCVD-DT         PIC  9(08)  VALUE ZERO.                
           03  PV-CURRENT-DATE           PIC  X(10)  VALUE SPACES.              
           03  PV-EXPT-RCT-DT            PIC  X(10)  VALUE SPACES.              
                                                                                
           03  PV-MARK1-EFF-DTE-X        PIC  X(08).                            
           03  PV-MARK1-EFF-DTE-9  REDEFINES  PV-MARK1-EFF-DTE-X.               
               05  PV-MARK1-EFF-DTE-NUM  PIC  9(08).                       CL**1
                                                                                
           03  PV-DONT-SHIP-FMT          PIC  X(08).                            
           03  PV-DONT-SHIP-RED    REDEFINES  PV-DONT-SHIP-FMT.                 
               05  PV-DONT-SHIP-BFOR-NUM PIC  9(08).                       CL**1
                                                                                
           03  PV-CURR-DT-YYMMDD.                                               
               05  PV-CURR-DT-YY         PIC  X(02).                            
               05  PV-CURR-DT-MM         PIC  X(02).                            
               05  PV-CURR-DT-DD         PIC  X(02).                            
                                                                                
           03  PV-MARK1-FILE.                                                   
               05  PV-DMA-NBR            PIC  9(04)  VALUE 0.                   
               05  PV-DMM-FRST-NM        PIC  X(20)  VALUE SPACES.              
               05  PV-DMM-LAST-NM        PIC  X(30)  VALUE SPACES.              
               05  PV-BYR-NBR            PIC  9(04)  VALUE 0.                   
               05  PV-FRST-NM            PIC  X(40)  VALUE SPACES.              
               05  PV-LAST-NM            PIC  X(40)  VALUE SPACES.              
               05  PV-DEPT-NBR           PIC  9(04)  VALUE 0.                   
               05  PV-DEPT-NM            PIC  X(20)  VALUE SPACES.              
               05  PV-SUB-CL-NBR         PIC  9(04)  VALUE 0.                   
               05  PV-VS-NBR             PIC  X(20)  VALUE SPACES.              
               05  PV-VS-DESC            PIC  X(30)  VALUE SPACES.              
               05  PV-SKU-NBR            PIC  9(08)  VALUE 0.                   
               05  PV-SKU-STAT-CDE       PIC  X(02)  VALUE SPACES.              
               05  PV-SKU-DESC           PIC  X(30)  VALUE SPACES.              
               05  PV-CSTM-CLOR-DESC     PIC  X(10)  VALUE SPACES.              
               05  PV-PRCHG-ID           PIC  9(10)  VALUE 0.                   
               05  PV-ALL-RMNG-STR-IND   PIC  X(01)  VALUE SPACES.              
               05  PV-EFF-DTE            PIC  X(10)  VALUE SPACES.              
               05  PV-PRCHG-TYP-CDE      PIC  X(03)  VALUE SPACES.              
               05  PV-PRCHG-ST-CDE       PIC  X(03)  VALUE SPACES.              
               05  PV-ITM-NBR            PIC  9(15)  VALUE 0.                   
               05  PV-STYL-ID            PIC  9(08)  VALUE 0.                   
               05  PV-INTR-UPC-ID        PIC  9(10)  VALUE 0.                   
               05  PV-SKU-STAT-DESC      PIC  X(10)  VALUE SPACES.              
                                                                                
           03  PV-REP-MARK1-FILE.                                               
               05  PV-REP-BYR.                                                  
                   10  PV-REP-BYR-NBR    PIC  9(03)  VALUE ZERO.                
                   10  FILLER            PIC  X(01)  VALUE '-'.                 
                   10  PV-REP-BYR-NM     PIC  X(29)  VALUE SPACES.              
C98147         05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-DEPT.                                                 
                   10  PV-REP-DEPT-NBR   PIC  9(03)  VALUE ZERO.                
                   10  FILLER            PIC  X(01)  VALUE '-'.                 
                   10  PV-REP-DEPT-NM    PIC  X(20)  VALUE SPACES.              
               05  FILLER                PIC  X(02)  VALUE SPACES.              
               05  PV-REP-SUB-CL-NBR     PIC  9(02)  VALUE ZERO.                
               05  FILLER                PIC  X(02)  VALUE SPACES.              
               05  PV-REP-VS-NBR         PIC  X(20)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-SKU-NBR        PIC  X(08)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-SKU-STAT-CDE   PIC  X(02)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-SKU-DESC       PIC  X(24)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-CSTM-CLOR-DESC PIC  X(10)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-ONHAND-QTY     PIC S9(07)  VALUE ZERO.                
               05  FILLER                PIC  X(01)  VALUE SPACES.              
C98147         05  PV-REP-PO-NBR         PIC  9(08)  VALUE ZERO.                
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-DONT-SHIP-BEF-DT.                                     
                   10  PV-REP-POSHIP-MM  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER1    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-POSHIP-DD  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER2    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-POSHIP-YY  PIC  X(02)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-LAST-RCVD-DT.                                         
                   10  PV-REP-CORPDT-MM  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER3    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-CORPDT-DD  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER4    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-CORPDT-YY  PIC  X(02)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-EXPT-DATE.                                       CL**1
                   10  PV-REP-EXPTDT-MM  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER5    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-EXPTDT-DD  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER6    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-EXPTDT-YY  PIC  X(02)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-PRCHG-ID       PIC  9(06)  VALUE ZERO.                
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-EFF-DTE.                                              
                   10  PV-REP-EFF-DT-MM  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER7    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-EFF-DT-DD  PIC  X(02)  VALUE SPACES.              
                   10  PV-REP-FILLER8    PIC  X(01)  VALUE SPACES.              
                   10  PV-REP-EFF-DT-YER PIC  X(02)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-ALL-RMNG-IND   PIC  X(03)  VALUE SPACES.              
               05  FILLER                PIC  X(01)  VALUE SPACES.              
               05  PV-REP-REASON-CODE    PIC  X(03)  VALUE SPACES.              
                                                                                
      *****************************************************************         
      *  General workarea sql abend                                    *        
      *****************************************************************         
       01  ABEND-CODE                    PIC S9(04) VALUE +0 COMP SYNC.         
           88  ABEND-4013                           VALUE +4013.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      * Description     : Control main processing flow                *         
      *****************************************************************         
       000-MAIN-CONTROL.                                                        
                                                                                
           PERFORM 0500-INITIALISATION.                                         
                                                                                
           PERFORM 1000-MAIN-PROCESS                                            
             UNTIL EOF-MARK1-FILE.                                              
                                                                                
           PERFORM 9000-TERMINATE.                                              
           STOP RUN.                                                            
                                                                                
       0500-INITIALISATION.                                                     
      *****************************************************************         
      * Description     : Initialise fields and working storage       *         
      *****************************************************************         
                                                                                
           MOVE "*0500-INITIALISATION*"  TO PV-PARAGRAPH-NAME                   
                                                                                
           INITIALIZE                       PV-PROGRAM-VARIABLES                
                                                                                
           OPEN INPUT                       PERM-MARK1-SKU-CE2                  
                OUTPUT                      PERM-RPT-OUTFILE.                   
                                                                                
           MOVE SPACES                   TO PV-CURRENT-DATE                     
           PERFORM 0510-GET-CURRENT-DTE                                         
           MOVE PV-CURRENT-DATE(3:2)     TO PV-CURR-DT-YY                       
           MOVE PV-CURRENT-DATE(6:2)     TO PV-CURR-DT-MM                       
           MOVE PV-CURRENT-DATE(9:2)     TO PV-CURR-DT-DD                       
           COMPUTE PV-CURR-DT-ED = FUNCTION NUMVAL(PV-CURR-DT-YYMMDD)           
           END-COMPUTE.                                                         
                                                                                
           INITIALIZE                       PV-MARK1-FILE                       
                                            PV-REP-MARK1-FILE.                  
           PERFORM 2000-READ-MARK1-SKU-CE2.                                     
                                                                                
       0510-GET-CURRENT-DTE.                                                    
      *****************************************************************         
      * Description     : To get the current date                     *         
      *****************************************************************         
                                                                                
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   SELECT  CURRENT DATE                                         
                     INTO :PV-CURRENT-DATE                                      
                     FROM  SYSIBM.SYSDUMMY1                                     
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1                 TO PV-RETRY-COUNT                 
                   WHEN OTHER                                                   
                       MOVE '0510-GET-CURRENT-DTE'                              
                                              TO PV-PARAGRAPH-NAME              
                       MOVE 'GET CURRENT DATE'                                  
                                              TO PV-DB2-OPER-ATTEMPTED          
                       PERFORM 9999-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '0510-GET-CURRENT-DTE'    TO PV-PARAGRAPH-NAME              
               MOVE 'GET CURRENT DATE'        TO PV-DB2-OPER-ATTEMPTED          
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       1000-MAIN-PROCESS.                                                       
      *****************************************************************         
      * Description     : Main Processing                             *         
      *****************************************************************         
                                                                                
           MOVE "*1000-MAIN-PROCESS      *"   TO PV-PARAGRAPH-NAME              
                                                                                
           PERFORM 1100-SELECT-TSKSTOO-TPLNSHP                                  
                                                                                
           MOVE ZEROS                         TO TSKST-LAST-RCVD-DATE           
           PERFORM 1200-SELECT-TSKST-CORP-LVL                                   
                                                                                
           PERFORM 1400-VALIDATE-DATES                                          
                                                                                
           INITIALIZE                            PV-MARK1-FILE                  
                                                 PV-REP-MARK1-FILE.             
           PERFORM 2000-READ-MARK1-SKU-CE2.                                     
                                                                                
       1100-SELECT-TSKSTOO-TPLNSHP.                                             
      ******************************************************************        
      * Description : To get PO number, PO Ship date                            
      ******************************************************************        
                                                                                
           MOVE "*1100-SELECT-TSKSTOO-TPLNSHP"                                  
                                              TO PV-PARAGRAPH-NAME              
           MOVE PV-ITM-NBR                    TO SKSTOO-ITM-NBR                 
                                                                                
           EXEC SQL                                                             
               SELECT  TOO.PO_NBR                                               
                      ,TP.DONT_SHIP_BEF_DTE                                     
                      ,(TP.DONT_SHIP_BEF_DTE  + 8 DAYS) EXPECT_RECPT_DT         
                 INTO :SKSTOO-PO-NBR                                            
                     ,:PLNSHP-DONT-SHIP-BEF-DTE                                 
                     ,:PV-EXPT-RCT-DT                                           
                 FROM  TSKSTOO  TOO                                             
                      ,TPLNSHP  TP                                              
                WHERE  TOO.ITM_NBR        = DEC(:SKSTOO-ITM-NBR,15,0)           
                  AND  TP.PO_NBR          = TOO.PO_NBR                          
                  AND  TP.VER_STATUS_CDE  = 'A'                                 
                ORDER BY 2 ASC                                                  
                        ,1 ASC                                                  
                FETCH FIRST 1 ROW ONLY                                          
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                                                                                
                   IF  PV-EXPT-RCT-DT NOT = SPACES                              
                       MOVE '/'                 TO PV-REP-FILLER5               
                                                   PV-REP-FILLER6               
                       MOVE PV-EXPT-RCT-DT(3:2) TO PV-REP-EXPTDT-YY             
                       MOVE PV-EXPT-RCT-DT(6:2) TO PV-REP-EXPTDT-MM             
                       MOVE PV-EXPT-RCT-DT(9:2) TO PV-REP-EXPTDT-DD             
                   ELSE                                                         
                       MOVE SPACES              TO PV-REP-EXPT-DATE             
                   END-IF                                                       
                   MOVE '/'                   TO PV-REP-FILLER1                 
                                                 PV-REP-FILLER2                 
                   MOVE PLNSHP-DONT-SHIP-BEF-DTE(1:4)                           
                                              TO PV-DONT-SHIP-FMT(1:4)          
                   MOVE PLNSHP-DONT-SHIP-BEF-DTE(3:2)                           
                                              TO PV-REP-POSHIP-YY               
                   MOVE PLNSHP-DONT-SHIP-BEF-DTE(6:2)                           
                                              TO PV-DONT-SHIP-FMT(5:2)          
                                                 PV-REP-POSHIP-MM               
                   MOVE PLNSHP-DONT-SHIP-BEF-DTE(9:2)                           
                                              TO PV-DONT-SHIP-FMT(7:2)          
                                                 PV-REP-POSHIP-DD               
               WHEN +100                                                        
                   MOVE ZEROES                TO SKSTOO-PO-NBR                  
                   MOVE SPACES                TO PV-REP-DONT-SHIP-BEF-DT        
                                                 PV-REP-EXPT-DATE               
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TSKSTOO & TPLNSHP"                         
                                              TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY "TSKSTOO.ITM_NBR = "  PV-ITM-NBR                     
                   DISPLAY "TSKSTOO.STR_NBR = 0"                                
                   DISPLAY "TPLNSHP.STATUS_CDE = A"                             
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1200-SELECT-TSKST-CORP-LVL.                                              
      ******************************************************************        
      * Description : To get RECENT Last received date at CORP LEVEL            
      ******************************************************************        
                                                                                
           MOVE "*1200-SELECT-TSKST-CORP-LVL" TO PV-PARAGRAPH-NAME              
           MOVE PV-ITM-NBR                    TO TSKST-ITEM-NMBR                
                                                                                
           EXEC SQL                                                             
               SELECT  T.LAST_RCVD_DATE                                         
                      ,T.ONHAND_QTY                                             
                 INTO :TSKST-LAST-RCVD-DATE                                     
                     ,:TSKST-ONHAND-QTY                                         
                 FROM  TSKST    T                                               
                WHERE  T.LOC_NBR = 0                                            
                  AND  T.ITEM_NMBR = DEC(:TSKST-ITEM-NMBR,15,0)                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   PERFORM 1300-CONVERT-LAST-RECPT-DT                           
                   MOVE PV-ED-LST-RCVD-DT     TO PV-CRP-LST-RVD-DT              
                   MOVE TSKST-ONHAND-QTY      TO PV-REP-ONHAND-QTY              
                   MOVE ZEROES                TO PV-ED-LST-RCVD-DT              
                                                 PV-CENTURY                     
               WHEN +100                                                        
                   MOVE ZEROES                TO PV-CRP-LST-RVD-DT              
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TSKST "  TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY "TSKST.LOC_NBR = 0 "                                 
                   DISPLAY "TSKST.ITM_NBR = "    PV-ITM-NBR                     
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1300-CONVERT-LAST-RECPT-DT.                                              
      ******************************************************************        
      * Description : Convert Last receipt date Format                          
      ******************************************************************        
                                                                                
           MOVE "*1300-CONVERT-LAST-RECPT-DT"                                   
                                            TO PV-PARAGRAPH-NAME                
                                                                                
           IF  TSKST-LAST-RCVD-DATE <= ZERO                                     
               MOVE ZEROS                   TO PV-ED-LST-RCVD-DT                
           ELSE                                                                 
                                                                                
               IF  TSKST-LAST-RCVD-DATE > PV-CURR-DT-ED                         
                   MOVE 19                  TO PV-CENTURY                       
               ELSE                                                             
                   MOVE 20                  TO PV-CENTURY                       
               END-IF                                                           
                                                                                
               COMPUTE PV-ED-LST-RCVD-DT =  (TSKST-LAST-RCVD-DATE +             
                                           (PV-CENTURY * 1000000))              
               END-COMPUTE                                                      
                                                                                
           END-IF.                                                              
                                                                                
       1400-VALIDATE-DATES.                                                     
      ******************************************************************        
      * Description : Validate Dates                                            
      ******************************************************************        
                                                                                
           MOVE "*1400-VALIDATE-DATES"      TO PV-PARAGRAPH-NAME                
                                                                                
           MOVE PV-EFF-DTE(1:4)             TO PV-MARK1-EFF-DTE-X(1:4)          
           MOVE PV-EFF-DTE(6:2)             TO PV-MARK1-EFF-DTE-X(5:2)          
           MOVE PV-EFF-DTE(9:2)             TO PV-MARK1-EFF-DTE-X(7:2)          
                                                                                
           PERFORM 7000-LOAD-OUTPUT-FILE.                                       
                                                                                
           IF  PV-MARK1-EFF-DTE-NUM < PV-DONT-SHIP-BFOR-NUM                     
           AND SKSTOO-PO-NBR   > 0                                              
               MOVE SKSTOO-PO-NBR         TO PV-REP-PO-NBR                      
               PERFORM 8000-WRITE-VALID-FILE                                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * Description     : Read Mark 1 SKU file                        *         
      ******************************************************************        
       2000-READ-MARK1-SKU-CE2.                                                 
                                                                                
           MOVE '*2000-READ-MARK1-SKU-CE2'  TO PV-PARAGRAPH-NAME.               
                                                                                
           READ PERM-MARK1-SKU-CE2        INTO PV-MARK1-FILE                    
             AT END SET EOF-MARK1-FILE      TO TRUE                             
           END-READ.                                                            
                                                                                
           IF  NOT-EOF-MARK1-FILE                                               
               ADD 1                        TO PV-INP-RECS-CNT                  
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
      * LOAD The Mark1 CE2 Report file                                          
      *-----------------------------------------------------------------        
       7000-LOAD-OUTPUT-FILE.                                                   
                                                                                
           MOVE "*7000-LOAD-OUTPUT-FILE*"   TO PV-PARAGRAPH-NAME                
                                                                                
           MOVE PV-BYR-NBR                  TO PV-REP-BYR-NBR                   
           MOVE PV-DEPT-NBR                 TO PV-REP-DEPT-NBR                  
           MOVE PV-DEPT-NM                  TO PV-REP-DEPT-NM                   
           MOVE PV-SUB-CL-NBR               TO PV-REP-SUB-CL-NBR                
           MOVE PV-VS-NBR                   TO PV-REP-VS-NBR                    
           MOVE PV-SKU-NBR                  TO PV-REP-SKU-NBR                   
           MOVE PV-SKU-STAT-CDE             TO PV-REP-SKU-STAT-CDE              
           MOVE PV-SKU-DESC                 TO PV-REP-SKU-DESC                  
           MOVE PV-CSTM-CLOR-DESC           TO PV-REP-CSTM-CLOR-DESC            
           MOVE PV-PRCHG-ID                 TO PV-REP-PRCHG-ID                  
           MOVE PV-ALL-RMNG-STR-IND         TO PV-REP-ALL-RMNG-IND              
           MOVE 'CE2'                       TO PV-REP-REASON-CODE.              
                                                                                
           IF  PV-EFF-DTE NOT = SPACES                                          
               MOVE '/'                     TO PV-REP-FILLER7                   
                                               PV-REP-FILLER8                   
               MOVE PV-EFF-DTE(3:2)         TO PV-REP-EFF-DT-YER                
               MOVE PV-EFF-DTE(6:2)         TO PV-REP-EFF-DT-MM                 
               MOVE PV-EFF-DTE(9:2)         TO PV-REP-EFF-DT-DD                 
           ELSE                                                                 
               MOVE SPACES                  TO PV-REP-EFF-DTE                   
           END-IF                                                               
                                                                                
           IF  PV-CRP-LST-RVD-DT NOT = ZEROES                                   
               MOVE '/'                     TO PV-REP-FILLER3                   
                                               PV-REP-FILLER4                   
               MOVE PV-CRP-LST-RVD-DT(3:2)  TO PV-REP-CORPDT-YY                 
               MOVE PV-CRP-LST-RVD-DT(5:2)  TO PV-REP-CORPDT-MM                 
               MOVE PV-CRP-LST-RVD-DT(7:2)  TO PV-REP-CORPDT-DD                 
           ELSE                                                                 
               MOVE SPACES                  TO PV-REP-LAST-RCVD-DT              
           END-IF                                                               
                                                                                
           STRING PV-FRST-NM      DELIMITED BY SPACE                            
                  ' '             DELIMITED BY SIZE                             
                  PV-LAST-NM      DELIMITED BY SPACE                            
                                          INTO PV-REP-BYR-NM.                   
                                                                                
      *-----------------------------------------------------------------        
      * WRITE The Mark1 CE2 Report file                                         
      *-----------------------------------------------------------------        
       8000-WRITE-VALID-FILE.                                                   
                                                                                
           MOVE "*8000-WRITE-VALID-FILE*"   TO PV-PARAGRAPH-NAME                
                                                                                
           WRITE PERM-RPT-OUTFILE-REC     FROM PV-REP-MARK1-FILE                
                                                                                
           ADD +1                           TO PV-RECS-CNT.                     
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      * 9000-TERMINATE - CLOSES THE FILES ASSOCIATED WITH THE JOB.              
      *-----------------------------------------------------------------        
       9000-TERMINATE.                                                          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '============================================='.             
           DISPLAY ' '.                                                         
           DISPLAY ' ******  PEKRP093 PROCESSING SUMMARY  ******  '.            
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                                
           DISPLAY '============================================='.             
                                                                                
           CLOSE                               PERM-MARK1-SKU-CE2               
                                               PERM-RPT-OUTFILE.                
                                                                                
      *-----------------------------------------------------------------        
      * DISPLAY PROCESSED RECORDS COUNTS                                        
      *-----------------------------------------------------------------        
       9100-DISPLAY-PROCESSING-INFO.                                            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' INPUT RECORD COUNT     = ' PV-INP-RECS-CNT                
                                                                                
            DISPLAY ' COUNT OF RECS REPORTED = ' PV-RECS-CNT                    
            DISPLAY ' '.                                                        
                                                                                
       9999-SQL-ERROR.                                                          
      ******************************************************************        
      * SQL abend routine                                                       
      ******************************************************************        
                                                                                
           SET ABEND-4013                     TO TRUE.                          
                                                                                
           DISPLAY '**  ABEND     **'.                                          
                                                                                
           DISPLAY '**  PROGRAM   **  =  PEKRP093'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      * The following copybook utilizes the DB2 abend module                    
      * DSNTIAR to convert SQLCA into understandable verbage.                   
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
