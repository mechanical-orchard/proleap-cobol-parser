       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT305.                                       
       AUTHOR.                  JAN BLAZICH.                                    
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            02/10/2006.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  CREATE INVENTORY STORE SORT SELECTION CONTROL FILE                     
      *  FOR SELECTING ONLY STORES THAT ARE IN INVENTORY FROM TWO               
      *  FILES THAT ARE PRODUCED BY MLKUT410 (OUT02 AND OUT03).                 
      *                                                                *        
      *  THIS PROGRAM WILL EXAMINE THE INVENTORY PARAMETER TABLE TO    *        
      *  FIND ALL STORES BEING PROCESSED FOR THE INVENTORY CUTOFF      *        
      *  DATABASE.                                                              
      *                                                                *        
      *  THE INPUT TO THIS PROGRAM WILL CONTAIN A CONTROL CARD FOR     *        
      *  THE PROCESSING DATE.                                                   
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * XXXXXX   9999-99-09  WHO AND WHAT                                       
W28545* W28545   08-10-2006  ADD LOGIC FOR DEPARTMENT LEVEL                     
W28545*                      INVENTORY PROCESS. JOIN TINVPAR/TINCNTL            
W28545*                      -B. GRAHAM - STRATAGEM                             
W33304* W33304   08-27-2008  PRAVEEN HOLDING INVENTORY LEDGER OPEN-HILO         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CC-CONTROL-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-CNTL-FILE-INV                                           
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STORE-CNTL-FILE-BYR                                           
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CC-CONTROL-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-CONTROL-REC                  PIC X(80).                           
                                                                                
                                                                                
       FD  STORE-CNTL-FILE-INV                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-CNTL-REC-INV              PIC X(80).                           
                                                                                
       FD  STORE-CNTL-FILE-BYR                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-CNTL-REC-BYR              PIC X(80).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-TINVPAR-CSR       PIC X(1)        VALUE 'N'.           
               88  END-OF-TINVPAR-CSR                      VALUE 'Y'.           
           05  PS-FIRST-STORE-INV-SW       PIC X(1)        VALUE 'Y'.           
               88  PS-FIRST-STORE-INV                      VALUE 'Y'.           
               88  PS-NOT-FIRST-STORE-INV                  VALUE 'N'.           
           05  PS-FIRST-STORE-BYR-SW       PIC X(1)        VALUE 'Y'.           
               88  PS-FIRST-STORE-BYR                      VALUE 'Y'.           
               88  PS-NOT-FIRST-STORE-BYR                  VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLES BEGIN ***'.                               
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                                                           COMP SYNC.           
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-MAX-RETRY-COUNT          PIC S9(4)       VALUE +2             
                                                           COMP SYNC.           
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-RECS-WRITTEN             PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-RETRY-COUNT              PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-FILE-COUNT         PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-FILE               PIC S9(4)       VALUE +1             
                                                           COMP SYNC.           
           05  PV-STORE-FILE-CNT           PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-REQUEST-COUNT            PIC S9(9)       VALUE ZEROES.        
           05  PV-NUMERIC-DISPLAY          PIC Z(8)9       VALUE ZEROES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(40)       VALUE SPACES.        
                                                                                
       01  PV-PROCESS-DATE-AREA.                                                
           05  PV-PROCESS-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-PROCESS-DATE.                                
               10  PV-PROCESS-CCYY         PIC X(04).                           
               10  PV-PROCESS-HYPHEN1      PIC X(01).                           
               10  PV-PROCESS-MM           PIC X(02).                           
               10  PV-PROCESS-HYPHEN2      PIC X(01).                           
               10  PV-PROCESS-DD           PIC X(02).                           
           05  FILLER                      PIC X(70)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** STORE TABLE BEGIN ***'.                                         
                                                                                
       01  PT-STORE-TBL-WORK-AREA.                                              
           05  PT-STORE-TBL-LOADED         PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL-INDEX          PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL-MAX            PIC S9(4)       VALUE +9999          
                               COMP.                                            
           05  PT-STORE-TBL-AREA.                                               
               10  PT-STORE-TBL OCCURS 9999 TIMES.                              
                   15  PT-STORE-NBR        PIC S9(4)       COMP.                
                                                                                
       01  OR-OUTPUT-RECORD.                                                    
           05  FILLER                        PIC X(02)   VALUE SPACE.           
           05  FILLER                        PIC X(01)   VALUE SPACE.           
               88  OR-UNLOAD-COMMA                       VALUE ','.             
               88  OR-UNLOAD-NO-COMMA                    VALUE SPACE.           
               88  PURGE-UNLOAD-COMMA                    VALUE ','.             
               88  PURGE-UNLOAD-NO-COMMA                 VALUE SPACE.           
           05  FILLER                        PIC X(01)   VALUE SPACE.           
           05  OR-UNLOAD-STORE-NBR           PIC 9(05)   VALUE ZEROES.          
           05  FILLER                        PIC X(71)   VALUE SPACES.          
                                                                                
      * FOR OUT03 OF MLKUT410                                                   
       01  SR-SORT-INV-REC.                                                     
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  SR-SORT-PART-1                PIC X(26)   VALUE SPACES.          
               88  SR-SORT-COPY-CMD                      VALUE                  
                   'SORT   FIELDS=COPY     '.                                   
               88  SR-SORT-OUT-FILE-A                    VALUE                  
                   'OUTFIL FILES=01,        '.                                  
               88  SR-SORT-INCLUDE-BEG                   VALUE                  
                   "   INCLUDE=(80,05,CH,EQ,C'".                                
               88  SR-SORT-INCLUDE-MID                   VALUE                  
                   "            80,05,CH,EQ,C'".                                
           05  SR-SORT-STORE-NBR             VALUE SPACES.                      
               10  SR-SORT-STORE-NBR-9       PIC 9(05).                         
                   88  SR-SORT-STORE-0000                VALUE 0.               
           05  SR-SORT-PART-2                PIC X(24)   VALUE SPACES.          
               88  SR-SORT-INCLUDE-END                   VALUE                  
                   "')       * IN050-LOC-NBR".                                  
               88  SR-SORT-INCLUDE-CONT                  VALUE                  
                   "',OR,    * IN050-LOC-NBR".                                  
           05  FILLER                        PIC X(23)   VALUE SPACES.          
       01  SR-SORT-COMMENT-RECORD REDEFINES SR-SORT-INV-REC.                    
           05  FILLER                        PIC X(50).                         
               88  SR-SORT-COMMENT-1                     VALUE                  
                   '* THIS SORT CARD IS GENERATED WITHIN INKUT305.    '.        
               88  SR-SORT-COMMENT-2                     VALUE                  
                   '* SEE INRD050 FOR DATA LAYOUT DETAIL.      '.               
           05  FILLER                        PIC X(30).                         
                                                                                
      * FOR OUT02 OF MLKUT410                                                   
       01  SR-SORT-BYR-REC.                                                     
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  SR-SORT-BYR-PART-1            PIC X(26)   VALUE SPACES.          
               88  SR-SORT-BYR-COPY-CMD                  VALUE                  
                   'SORT   FIELDS=COPY     '.                                   
               88  SR-SORT-BYR-OUT-FILE-A                VALUE                  
                   'OUTFIL FILES=01,        '.                                  
               88  SR-SORT-BYR-INCLUDE-BEG               VALUE                  
                   "  INCLUDE=(135,05,CH,EQ,C'".                                
               88  SR-SORT-BYR-INCLUDE-MID               VALUE                  
                   "           135,05,CH,EQ,C'".                                
           05  SR-SORT-BYR-STORE-NBR         VALUE SPACES.                      
               10  SR-SORT-BYR-STORE-NBR-9   PIC 9(05).                         
                   88  SR-SORT-BYR-STORE-0000            VALUE 0.               
           05  SR-SORT-BYR-PART-2            PIC X(24)   VALUE SPACES.          
               88  SR-SORT-BYR-INCLUDE-END               VALUE                  
                   "')       * ML113-LOC-NBR".                                  
               88  SR-SORT-BYR-INCLUDE-CONT              VALUE                  
                   "',OR,    * ML113-LOC-NBR".                                  
           05  FILLER                        PIC X(23)   VALUE SPACES.          
       01  SR-SORT-BYR-COMMENT-RECORD REDEFINES SR-SORT-BYR-REC.                
           05  FILLER                        PIC X(50).                         
               88  SR-SORT-BYR-COMMENT-1                 VALUE                  
                   '* THIS SORT CARD IS GENERATED WITHIN INKUT305.    '.        
               88  SR-SORT-BYR-COMMENT-2                 VALUE                  
                   '* SEE MLRD113 FOR DATA LAYOUT DETAIL.      '.               
           05  FILLER                        PIC X(30).                         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** DB2 STORAGE/DEFINITION BEGIN ***'.                              
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
28545                                                                           
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                *         
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT USED FOR INVENTORY PARMS TABLE                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL DB2 TINVPAR CURSOR DECLARATION                                     
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TINVPAR_CSR CURSOR FOR                                   
                   SELECT LOC_NBR                                               
                         ,PLND_INV_DTE                                          
28545                FROM TINVPAR A                                             
28545                    ,TINCNTL B                                             
                    WHERE ACTL_FIN_BK_DTE   = '9999-09-09'                      
                      AND PLND_INV_DTE     >= :PV-PROCESS-DATE                  
W33304                AND SCHD_PHY_CNT_CDE IN ('PI','NS','CL')                  
W33304                AND A.LOC_INV_STAT_CDE = 'IN'                             
28545                 AND A.INV_ID = B.INV_ID                                   
28545                 AND B.ACTV_IND = 'Y'                                      
                 ORDER BY LOC_NBR                                               
                         ,PLND_INV_DTE                                          
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-PROCESS-STORES.                                         
           PERFORM 2500-PROCESS-BUYER-STORES.                                   
                                                                                
           PERFORM 3000-PROCESS-TINVPAR.                                        
                                                                                
           PERFORM 4400-WRITE-LAST-SORT-REC.                                    
           PERFORM 4800-WRITE-LAST-BYR-REC.                                     
                                                                                
           PERFORM 3900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * OPEN ALL FILES AND GET THE PROCESS DATE.                                
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  CC-CONTROL-FILE                                          
                OUTPUT STORE-CNTL-FILE-INV                                      
                OUTPUT STORE-CNTL-FILE-BYR.                                     
                                                                                
           PERFORM 1500-READ-PROCESS-DATE.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE.                        
                                                                                
           IF PV-PROCESS-CCYY NOT NUMERIC                                       
             OR PV-PROCESS-MM NOT NUMERIC                                       
             OR PV-PROCESS-DD NOT NUMERIC                                       
             OR PV-PROCESS-HYPHEN1 NOT = '-'                                    
             OR PV-PROCESS-HYPHEN2 NOT = '-'                                    
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUT305'                          
               DISPLAY '**  INVALID PROCESSING DATE '                           
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    READ THE PROCESS DATE CONTROL CARD FILE                              
      ******************************************************************        
       1500-READ-PROCESS-DATE.                                                  
                                                                                
           READ CC-CONTROL-FILE INTO PV-PROCESS-DATE-AREA                       
               AT END                                                           
                   MOVE +4001                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT305'                      
                   DISPLAY '**  EMPTY DATE CONTROL FILE'                        
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *    PROCESS INVENTORY STORES                                             
      ******************************************************************        
       2000-PROCESS-STORES.                                                     
                                                                                
           SET SR-SORT-COMMENT-1         TO TRUE.                               
           PERFORM 5000-WRITE-CNTL-INV.                                         
                                                                                
           SET SR-SORT-COMMENT-2         TO TRUE                                
           PERFORM 5000-WRITE-CNTL-INV.                                         
                                                                                
           SET SR-SORT-COPY-CMD          TO TRUE.                               
           PERFORM 5000-WRITE-CNTL-INV.                                         
                                                                                
           SET SR-SORT-OUT-FILE-A        TO TRUE                                
           PERFORM 5000-WRITE-CNTL-INV                                          
                                                                                
           SET PS-NOT-FIRST-STORE-INV   TO TRUE                                 
           SET SR-SORT-INCLUDE-BEG  TO TRUE                                     
           SET SR-SORT-INCLUDE-CONT TO TRUE                                     
           SET SR-SORT-STORE-0000   TO TRUE                                     
           PERFORM 5000-WRITE-CNTL-INV                                          
                                                                                
           SET SR-SORT-INCLUDE-MID  TO TRUE.                                    
                                                                                
       2500-PROCESS-BUYER-STORES.                                               
                                                                                
           SET SR-SORT-BYR-COMMENT-1     TO TRUE.                               
           PERFORM 5500-WRITE-CNTL-BYR.                                         
                                                                                
           SET SR-SORT-BYR-COMMENT-2     TO TRUE                                
           PERFORM 5500-WRITE-CNTL-BYR.                                         
                                                                                
           SET SR-SORT-BYR-COPY-CMD      TO TRUE.                               
           PERFORM 5500-WRITE-CNTL-BYR.                                         
                                                                                
           SET SR-SORT-BYR-OUT-FILE-A    TO TRUE                                
           PERFORM 5500-WRITE-CNTL-BYR                                          
                                                                                
           SET PS-NOT-FIRST-STORE-BYR   TO TRUE                                 
           SET SR-SORT-BYR-INCLUDE-BEG  TO TRUE                                 
           SET SR-SORT-BYR-INCLUDE-CONT TO TRUE                                 
           SET SR-SORT-BYR-STORE-0000   TO TRUE                                 
           PERFORM 5500-WRITE-CNTL-BYR                                          
                                                                                
           SET SR-SORT-BYR-INCLUDE-MID TO TRUE.                                 
                                                                                
                                                                                
      ******************************************************************        
      *    PROCESS TINVPAR                                                      
      ******************************************************************        
       3000-PROCESS-TINVPAR.                                                    
                                                                                
           PERFORM 7000-OPEN-TINVPAR-CSR.                                       
                                                                                
           PERFORM 7200-FETCH-TINVPAR-CSR.                                      
                                                                                
           MOVE ZERO   TO PT-STORE-TBL-LOADED.                                  
           PERFORM 4000-LOAD-STORE-TABLE                                        
               UNTIL END-OF-TINVPAR-CSR.                                        
                                                                                
           MOVE PT-STORE-TBL-LOADED  TO PV-STORE-FILE-COUNT.                    
                                                                                
           PERFORM 7400-CLOSE-TINVPAR-CSR.                                      
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       3900-FINALIZE.                                                           
                                                                                
           CLOSE   CC-CONTROL-FILE                                              
                   STORE-CNTL-FILE-INV                                          
                   STORE-CNTL-FILE-BYR.                                         
                                                                                
           DISPLAY ' ------------------------------------------------'.         
           MOVE PT-STORE-TBL-LOADED          TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' TOTAL STORE(S) PROCESSED:  ' PV-NUMERIC-DISPLAY.           
           DISPLAY ' '.                                                         
           MOVE PV-STORE-FILE-CNT            TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' SORT CONTROL FILE COUNT:   ' PV-NUMERIC-DISPLAY.           
                                                                                
      ******************************************************************        
      *    LOAD INVENTORYING STORES FROM INVENTORY PARM TABLE                   
      ******************************************************************        
       4000-LOAD-STORE-TABLE.                                                   
                                                                                
           IF PT-STORE-TBL-LOADED = 0                                           
               ADD 1 TO PT-STORE-TBL-LOADED                                     
               MOVE INVPAR-LOC-NBR TO PT-STORE-NBR (PT-STORE-TBL-LOADED).       
               PERFORM 4200-CREATE-STORE-FILES                                  
               PERFORM 4600-CREATE-BYR-FILES                                    
           ELSE                                                                 
           IF INVPAR-LOC-NBR > PT-STORE-NBR (PT-STORE-TBL-LOADED)               
               ADD 1 TO PT-STORE-TBL-LOADED                                     
               IF PT-STORE-TBL-LOADED > PT-STORE-TBL-MAX                        
                   MOVE +4003 TO ABEND-CODE                                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT305'                      
                   DISPLAY '**  INTERNAL STORE TABLE OVERFLOW'                  
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
               END-IF                                                           
               MOVE INVPAR-LOC-NBR TO PT-STORE-NBR (PT-STORE-TBL-LOADED).       
               PERFORM 4200-CREATE-STORE-FILES                                  
               PERFORM 4600-CREATE-BYR-FILES                                    
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 7200-FETCH-TINVPAR-CSR.                                      
                                                                                
      ******************************************************************        
      *    PRODUCE INVENTORY CUTOFF STORES COMMA DELIMITED FILE                 
      ******************************************************************        
       4200-CREATE-STORE-FILES.                                                 
                                                                                
           SET PS-FIRST-STORE-INV TO TRUE.                                      
           SET OR-UNLOAD-NO-COMMA   TO TRUE.                                    
                                                                                
           SET SR-SORT-INCLUDE-MID TO TRUE                                      
                                                                                
           MOVE PT-STORE-NBR (PT-STORE-TBL-LOADED)                              
               TO OR-UNLOAD-STORE-NBR                                           
                  SR-SORT-STORE-NBR-9.                                          
                                                                                
           SET OR-UNLOAD-COMMA      TO TRUE.                                    
           SET SR-SORT-INCLUDE-CONT TO TRUE                                     
                                                                                
           PERFORM 5000-WRITE-CNTL-INV.                                         
                                                                                
       4400-WRITE-LAST-SORT-REC.                                                
           SET OR-UNLOAD-COMMA      TO TRUE.                                    
                                                                                
           SET SR-SORT-INCLUDE-MID TO TRUE                                      
                                                                                
           SET SR-SORT-STORE-0000     TO TRUE                                   
           SET SR-SORT-INCLUDE-END TO TRUE                                      
           PERFORM 5000-WRITE-CNTL-INV.                                         
                                                                                
      ******************************************************************        
      *    PRODUCE INVENTORY CUTOFF STORES COMMA DELIMITED FILE                 
      ******************************************************************        
       4600-CREATE-BYR-FILES.                                                   
                                                                                
           SET PS-FIRST-STORE-BYR TO TRUE.                                      
           SET OR-UNLOAD-NO-COMMA   TO TRUE.                                    
                                                                                
           SET SR-SORT-BYR-INCLUDE-MID TO TRUE                                  
                                                                                
           MOVE PT-STORE-NBR (PT-STORE-TBL-LOADED)                              
               TO OR-UNLOAD-STORE-NBR                                           
                  SR-SORT-BYR-STORE-NBR-9.                                      
                                                                                
           SET OR-UNLOAD-COMMA      TO TRUE.                                    
           SET SR-SORT-BYR-INCLUDE-CONT TO TRUE                                 
                                                                                
           PERFORM 5500-WRITE-CNTL-BYR.                                         
                                                                                
       4800-WRITE-LAST-BYR-REC.                                                 
           SET OR-UNLOAD-COMMA      TO TRUE.                                    
                                                                                
           SET SR-SORT-BYR-INCLUDE-MID TO TRUE                                  
                                                                                
           SET SR-SORT-BYR-STORE-0000 TO TRUE                                   
           SET SR-SORT-BYR-INCLUDE-END TO TRUE                                  
           PERFORM 5500-WRITE-CNTL-BYR.                                         
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE SORT CONTROL FILE RECORD                           
      ******************************************************************        
       5000-WRITE-CNTL-INV.                                                     
                                                                                
           ADD +1 TO PV-STORE-FILE-CNT.                                         
                                                                                
           WRITE STORE-CNTL-REC-INV FROM SR-SORT-INV-REC.                       
           MOVE SPACES                  TO SR-SORT-INV-REC.                     
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE SORT CONTROL FILE RECORD                           
      ******************************************************************        
       5500-WRITE-CNTL-BYR.                                                     
                                                                                
           ADD +1 TO PV-STORE-FILE-CNT.                                         
                                                                                
           WRITE STORE-CNTL-REC-BYR FROM SR-SORT-BYR-REC.                       
           MOVE SPACES                  TO SR-SORT-BYR-REC.                     
                                                                                
      ******************************************************************        
      *    OPEN INVENTORY PARAMETER CURSOR                                      
      ******************************************************************        
       7000-OPEN-TINVPAR-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7000-OPEN-TINVPAR-CSR  '                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'OPEN TINVPAR       '                                   
                       TO PV-OPER-ATTEMPTED                                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    FETCH INVENTORY PARAMETER CURSOR                                     
      ******************************************************************        
       7200-FETCH-TINVPAR-CSR.                                                  
                                                                                
           PERFORM                                                              
           WITH TEST AFTER                                                      
           VARYING PV-RETRY-COUNT FROM 1 BY 1                                   
           UNTIL (PV-RETRY-COUNT > PC-MAX-RETRY-COUNT                           
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100)                                            
                                                                                
               EXEC SQL                                                         
                   FETCH TINVPAR_CSR                                            
                   INTO  :INVPAR-LOC-NBR                                        
                        ,:INVPAR-PLND-INV-DTE                                   
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       DISPLAY '** STORE NUMBER: ' INVPAR-LOC-NBR               
                               '  PLANNED INVENTORY DATE: '                     
                               INVPAR-PLND-INV-DTE                              
                   WHEN -904                                                    
                   WHEN -911                                                    
                       CONTINUE                                                 
                   WHEN +100                                                    
                       SET END-OF-TINVPAR-CSR TO TRUE                           
                   WHEN OTHER                                                   
                       MOVE '7200-FETCH-TINVPAR-CSR '                           
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'FETCH TINVPAR      '                               
                           TO PV-OPER-ATTEMPTED                                 
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRY-COUNT                               
               MOVE '7200-FETCH-TINVPAR-CSR' TO                                 
                     PV-PARAGRAPH-NAME                                          
               MOVE 'MAX FETCH RETRY COUNT EXCEEDED' TO                         
                     PV-OPER-ATTEMPTED                                          
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    CLOSE INVENTORY PARAMETER CURSOR                                     
      ******************************************************************        
       7400-CLOSE-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7400-CLOSE-TINVPAR-CSR '                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'CLOSE TINVPAR      '                                   
                       TO PV-OPER-ATTEMPTED                                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUT305'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-OPER-ATTEMPTED.                      
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
