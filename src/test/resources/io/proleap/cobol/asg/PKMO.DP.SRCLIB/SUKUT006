000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT006.                                                 
000600 AUTHOR.        LARRY SCHLEICHER - STRATAGEM                              
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   SEPTEMBER, 1996.                                          
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM DELETE THE SPECIFIED ROWS FROM THE TSHPUNT,               
001300*  TINPKSU, TSHUUPC AND TSHUNLO TABLES.                                   
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000     SELECT SHIPMENT-LOC-FILE                                             
003100         ASSIGN TO INP01.                                                 
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003900 FD  SHIPMENT-LOC-FILE                                                    
004000     RECORDING MODE IS F                                                  
004100     LABEL RECORDS ARE STANDARD                                           
004200     BLOCK CONTAINS 0 RECORDS.                                            
004300                                                                          
004400 01  SHIPMENT-LOC-RECORD        PIC  X(056).                              
004500*                                                                         
004600*                                                                         
004700******************************************************************        
004800 WORKING-STORAGE SECTION.                                                 
004900******************************************************************        
005000*                                                                         
005100*                                                                         
005600*                                                                         
005700*                                                                         
005800******************************************************************        
005900*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
006000******************************************************************        
006100     COPY RCRD011.                                                        
006200*                                                                         
006300*                                                                         
006400*                                                                         
006500******************************************************************        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006700******************************************************************        
006800     COPY DPWS004.                                                        
006900*                                                                         
007000*                                                                         
007100*                                                                         
007200******************************************************************        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007400******************************************************************        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
007900******************************************************************        
008000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
008100*  TABLE                                                                  
008200******************************************************************        
008300     EXEC SQL                                                             
008400          INCLUDE TSHPUNT                                                 
008500     END-EXEC.                                                            
008600******************************************************************        
008700*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
008800*  LOCATION TABLE                                                         
008900******************************************************************        
009000     EXEC SQL                                                             
009100          INCLUDE TSHUNLO                                                 
009200     END-EXEC.                                                            
009300*                                                                         
009400 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
009500                                                   COMP SYNC.             
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.             
009900         88  PS-END-OF-FILE                        VALUE 'Y'.             
010000         88  PS-NOT-END-OF-FILE                    VALUE 'N'.             
010100                                                                          
010200                                                                          
010300 01  PC-PROGRAM-CONSTANTS.                                                
010400     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
010500                                                   COMP SYNC.             
010600     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
010700         'SUKUT006'.                                                      
010800     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 020.             
010800     05  PC-READ-VALUE               PIC 9(6)      VALUE 200000.          
010900                                                                          
011000 01  PV-PROGRAM-VARIABLES.                                                
011100     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
011200     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
011300     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
011400     05  PV-READ-COUNTER             PIC  9(06)    VALUE ZERO.            
011400     05  PV-READ-ACCUM               PIC S9(04)    VALUE ZERO             
011500                                                   COMP SYNC.             
011400     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
011500                                                   COMP SYNC.             
011600     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
011700                                                   COMP SYNC.             
011800     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
011900                                                   COMP SYNC.             
012000     05  PV-DELETE-COUNT             PIC S9(09)    VALUE ZERO             
012100                                                   COMP SYNC.             
012110     05  PV-NOT-FOUND-COUNT          PIC S9(09)    VALUE ZERO             
012120                                                   COMP SYNC.             
012200     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
012300     05  PV-COMMIT-COUNT             PIC S9(09)    VALUE +0               
012400                                                   COMP SYNC.             
012500     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.            
012600                                                                          
012700     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
012800                                                   COMP-3.                
013100 01  SD-SYSTEM-DATE.                                                      
013200     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
013300     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
013400     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
013500                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014100 0000-PROGRAM-MAINLINE.                                                   
014200******************************************************************        
014300* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014400*      PROGRAM.                                                           
014500******************************************************************        
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
014800     IF PS-NOT-END-OF-FILE                                                
014900         PERFORM 2000-PROCESS-SHIPMENT-LOCS                               
015000            UNTIL PS-END-OF-FILE                                          
015100     END-IF.                                                              
015200     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015600 1000-INITIALIZE.                                                         
015700******************************************************************        
015800*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015900*    -- OPEN INPUT FILE                                                   
016000*    -- READ FILE                                                         
016100******************************************************************        
016200                                                                          
016300     OPEN INPUT  SHIPMENT-LOC-FILE.                                       
016400     PERFORM 7000-READ-SHIPMT-UNIT-FILE.                                  
016500                                                                          
016600                                                                          
016700                                                                          
016800 2000-PROCESS-SHIPMENT-LOCS.                                              
016900******************************************************************        
017000*  PROCESS THE CERTIFICATION REQUESTS (LOOP THROUGH THE RESULTS           
017100*  TABLE).  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT             
017200*  MEET THE CRITERIA.                                                     
017300******************************************************************        
017400                                                                          
017500     PERFORM 8000-DELETE-SHIPMT-UNIT-RECS.                                
017600     ADD +1 TO PV-COMMIT-COUNTER.                                         
017700                                                                          
017800     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                                
017900         EXEC SQL                                                         
018000            COMMIT                                                        
018100         END-EXEC                                                         
018200                                                                          
018300         IF SQLCODE NOT = +0                                              
018400             DISPLAY '### COMMIT FAILED ! ###'                            
018500             PERFORM 9100-SQL-ABEND                                       
018600         END-IF                                                           
018700                                                                          
018800         ADD +1                          TO PV-COMMIT-COUNT               
018900         MOVE +0 TO PV-COMMIT-COUNTER                                     
019000     END-IF.                                                              
019100                                                                          
019200     PERFORM 7000-READ-SHIPMT-UNIT-FILE.                                  
019300                                                                          
019400 7000-READ-SHIPMT-UNIT-FILE.                                              
019500******************************************************************        
019600      READ SHIPMENT-LOC-FILE INTO                                         
019700          DCLTSHPUNT                                                      
019800          AT END SET PS-END-OF-FILE TO TRUE.                              
019900                                                                          
023400     ADD 1 TO PV-READ-COUNTER                                             
023400              PV-READ-ACCUM.                                              
023400     IF PV-READ-COUNTER > PC-READ-VALUE                                   
023400         MOVE 0 TO PV-READ-COUNTER                                        
023400         MOVE PV-READ-ACCUM   TO PV-DISPLAY-COUNT                         
023500         DISPLAY 'SUKUT006 READ THIS MANY RECS ' PV-DISPLAY-COUNT         
023400     END-IF.                                                              
020000*                                                                         
020100 8000-DELETE-SHIPMT-UNIT-RECS.                                            
020200******************************************************************        
020300*  DELETE THE ROW FROM THE SHIPMENT UNIT TABLE                            
020400******************************************************************        
020500                                                                          
020600     EXEC SQL                                                             
020700         DELETE                                                           
020800             FROM  TSHPUNT                                                
020900             WHERE SHIPT_UNT_ID = :SHPUNT-SHIPT-UNT-ID                    
021000     END-EXEC.                                                            
021100                                                                          
021200     EVALUATE TRUE                                                        
021300         WHEN SQLCODE = 0                                                 
021400             ADD +1           TO PV-DELETE-COUNT                          
021500         WHEN SQLCODE = +100                                              
021600             ADD +1           TO PV-NOT-FOUND-COUNT                       
021700         WHEN SQLCODE < 0                                                 
021800         WHEN SQLCODE > 0                                                 
021900         WHEN SQLWARN0 = 'W'                                              
022000             MOVE '8000-DELETE-SHIPMT-UNIT-RECS'                          
022100                         TO PV-PARAGRAPH-NAME                             
022200             MOVE 'DELETE OF TSHOUNT'                                     
022300                         TO PV-DB2-OPERATION-ATTEMPTED                    
022400             PERFORM 9100-SQL-ABEND                                       
022500     END-EVALUATE.                                                        
022600                                                                          
022700                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022900******************************************************************        
023000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
023100******************************************************************        
023200     CLOSE SHIPMENT-LOC-FILE.                                             
023300                                                                          
023400     MOVE PV-DELETE-COUNT  TO PV-DISPLAY-COUNT.                           
023500     DISPLAY 'TOTAL SHIP LOC RECORDS DELETED   ' PV-DISPLAY-COUNT.        
023510     MOVE PV-NOT-FOUND-COUNT TO PV-DISPLAY-COUNT.                         
023520     DISPLAY 'TOTAL SHIP LOC RECORDS NOT FOUND ' PV-DISPLAY-COUNT.        
023600     MOVE PV-COMMIT-COUNT  TO PV-DISPLAY-COUNT.                           
023700     DISPLAY 'TOTAL COMMITS TAKEN              ' PV-DISPLAY-COUNT.        
023800                                                                          
023900                                                                          
024000 9100-SQL-ABEND.                                                          
024100******************************************************************        
024200*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
024300*  ERROR OR WARNING CODE OCCURS.                                          
024400******************************************************************        
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
