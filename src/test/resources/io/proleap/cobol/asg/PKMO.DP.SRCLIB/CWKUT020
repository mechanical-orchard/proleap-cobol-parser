      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT020.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  MERCHANDISE SUBCLASS EXTRACT PROGRAM                          *        
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING       *        
      *  TWO GENERATIONS OF THE RMS COMMON EXTRACT FILE.               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(73).                        
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(73).                        
                                                                                
       FD  CW-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CHANGE-RECORD                   PIC   X(74).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  TEMP-REC.                                                            
           05  TP-DEPT-NBR                    PIC   X(04).                      
           05  TP-FILL1                       PIC   X(02).                      
           05  TP-MAJ-CL-NBR                  PIC   X(04).                      
           05  TP-FILL2                       PIC   X(02).                      
           05  TP-SUB-CL-NBR                  PIC   X(04).                      
           05  TP-FILL3                       PIC   X(02).                      
           05  TP-SUB-CL-DESC                 PIC   X(20).                      
           05  TP-FILL4                       PIC   X(01).                      
           05  TP-POS-MDSE-DESC               PIC   X(30).                      
           05  TP-FILL5                       PIC   X(01).                      
           05  TP-TAX-DFLT-IND                PIC   X(01).                      
           05  TP-FILL6                       PIC   X(01).                      
           05  TP-DISC-ELIG-IND               PIC   X(01).                      
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-CURR-KEY                    PIC   X(16).                      
           05  CR-CURR-DATA                   PIC   X(57).                      
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-PREV-KEY                    PIC   X(16).                      
           05  PR-PREV-DATA                   PIC   X(57).                      
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(16).                      
           05  COR-CHANGE-DATA                PIC   X(57).                      
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT020'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PS-RUN-TYPE                   PIC X     VALUE 'Y'.                   
           88  PS-INITIAL-RUN                      VALUE 'Y'.                   
           88  PS-NOT-INITIAL-RUN                  VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY   = HIGH-VALUES                                
                     AND                                                        
                     PR-PREV-KEY   = HIGH-VALUES.                               
                                                                                
           IF  PS-INITIAL-RUN                                                   
               PERFORM 4000-ADD-EXTRA-DEPT-MCL-SCL                              
           END-IF.                                                              
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-CHANGE-FILE.                                          
                                                                                
           SET PS-INITIAL-RUN  TO  TRUE.                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CHANGE-OUTPUT-REC.                                        
                                                                                
           IF CR-CURR-KEY  =  PR-PREV-KEY                                       
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
                                                                                
           ELSE                                                                 
               IF CR-CURR-KEY  <  PR-PREV-KEY                                   
                   PERFORM 2200-INSERT-CURRENT                                  
                                                                                
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS SECOND KEY FOR EQUALITY                                         
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
                                                                                
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE CR-CURR-KEY       TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT FROM CURRENT RECORD                                             
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE CR-CURR-KEY   TO COR-CHANGE-KEY                                 
           MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                                
           MOVE 'I'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE FROM PREVIOUS RECORD                                            
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE PR-PREV-KEY   TO COR-CHANGE-KEY                                 
           MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                                
           MOVE 'D'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
      ******************************************************************        
      *  ADD DEPT/MCL/SCL 981/00/00 AND 983/00/00                               
      *  THIS IS ONLY DONE DURING THE INITIAL INSERT                            
      ******************************************************************        
       4000-ADD-EXTRA-DEPT-MCL-SCL.                                             
                                                                                
      *                                                                         
      **** DEPT 981 MCL 0000 SCL 0000                                           
      *                                                                         
           INITIALIZE TEMP-REC                                                  
           MOVE       '0981'               TO   TP-DEPT-NBR                     
           MOVE       '0000'               TO   TP-MAJ-CL-NBR                   
           MOVE       '0000'               TO   TP-SUB-CL-NBR                   
           MOVE       'N'                  TO   TP-TAX-DFLT-IND                 
                                                TP-DISC-ELIG-IND                
           MOVE       TEMP-REC             TO   CHANGE-OUTPUT-REC               
           MOVE       'I'                  TO   COR-CHANGE-INDICATOR            
           PERFORM    7100-WRITE.                                               
                                                                                
      *                                                                         
      **** DEPT 983 MCL 0000                                                    
      *                                                                         
           INITIALIZE TEMP-REC                                                  
           MOVE       '0983'               TO   TP-DEPT-NBR                     
           MOVE       '0000'               TO   TP-MAJ-CL-NBR                   
           MOVE       '0000'               TO   TP-SUB-CL-NBR                   
           MOVE       'N'                  TO   TP-TAX-DFLT-IND                 
                                                TP-DISC-ELIG-IND                
           MOVE       TEMP-REC             TO   CHANGE-OUTPUT-REC               
           MOVE       'I'                  TO   COR-CHANGE-INDICATOR            
           PERFORM    7100-WRITE.                                               
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
                                                                                
           WRITE  CW-CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                      
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
                   SET PS-NOT-INITIAL-RUN TO  TRUE                              
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-CHANGE-FILE.                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
