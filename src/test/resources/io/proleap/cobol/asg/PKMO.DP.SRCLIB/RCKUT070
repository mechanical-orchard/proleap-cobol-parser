000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RCKUT070.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   OCTOBER, 1999.                                            
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL DETERMINE DELETION DATE BASED ON 18 MONTHS           
001310*  PRIOR TO CURRENT DATE.  THIS DATE WILL BE USED TO DELETE ROWS          
001320*  FROM THE LABEL TRACKING SUMMARY TABLE. (TLBTKSM)                       
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002910                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003901******************************************************************        
003910 WORKING-STORAGE SECTION.                                                 
004000******************************************************************        
004010*-----------------------------------------------------------------        
004020*                                                                         
004030*-----------------------------------------------------------------        
004040 01  ABEND-CODE                     PIC S9(04) VALUE +0                   
004050                                               COMP SYNC.                 
004060                                                                          
004061*-----------------------------------------------------------------        
004062* PROGRAM CONSTANTS                                                       
004063*-----------------------------------------------------------------        
004070 01  PC-PROGRAM-CONSTANTS.                                                
004080     05  PC-CURRENT-PROGRAM-NAME    PIC  X(08) VALUE                      
004090         'RCKUT070'.                                                      
004092                                                                          
004131*-----------------------------------------------------------------        
004132* PROGRAM VARIABLES                                                       
004133*-----------------------------------------------------------------        
004140 01  PV-PROGRAM-VARIABLES.                                                
004150     05  PV-CURRENT-DATE            PIC X(10).                            
004160     05  PV-PROCESS-DATE            PIC X(10).                            
004170     05  PV-CURRENT-SATURDAY        PIC X(10).                            
004190     05  PV-DELETE-THRU             PIC X(10)  VALUE ZEROES.              
004191     05  PV-DAYS-BACK               PIC 9(05)  VALUE ZEROES.              
004193     05  PV-DB2-DELETE-COUNT        PIC S9(09) VALUE ZEROES               
004194                                               COMP.                      
004195     05  PV-DELETE-THRU-DATE        PIC  X(10) VALUE ZEROES.              
004196     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
004197                                                                          
004198     05  PV-CURRENT-PARAGRAPH       PIC X(30)  VALUE SPACES.              
004199     05  PV-PARAGRAPH-NAME          PIC  X(30) VALUE SPACES.              
004200     05  PV-DB2-OPERATION-ATTEMPTED PIC  X(30) VALUE SPACES.              
004201                                                                          
004202     05  PV-WEEK-DAY                PIC  X(09) VALUE SPACES.              
004210         88  PV-WEEK-DAY-SUNDAY                VALUE 'SUNDAY   '.         
004220         88  PV-WEEK-DAY-MONDAY                VALUE 'MONDAY   '.         
004230         88  PV-WEEK-DAY-TUESDAY               VALUE 'TUESDAY  '.         
004240         88  PV-WEEK-DAY-WEDNESDAY             VALUE 'WEDNESDAY'.         
004250         88  PV-WEEK-DAY-THRUSDAY              VALUE 'THRUSDAY '.         
004260         88  PV-WEEK-DAY-FRIDAY                VALUE 'FRIDAY   '.         
004270         88  PV-WEEK-DAY-SATURDAY              VALUE 'SATURDAY '.         
004280                                                                          
004290                                                                          
006300*-----------------------------------------------------------------        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006610*-----------------------------------------------------------------        
006800     COPY DPWS004.                                                        
007101                                                                          
007102*-----------------------------------------------------------------        
007103*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PROCESS CONTROL            
007104*  TABLE                                                                  
007105*-----------------------------------------------------------------        
007106     EXEC SQL                                                             
007107          INCLUDE TCOCNTL                                                 
007108     END-EXEC.                                                            
007109                                                                          
007110*-----------------------------------------------------------------        
007111*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE LABEL TRACKING             
007112*  SUMMARY TABLE                                                          
007113*-----------------------------------------------------------------        
007114     EXEC SQL                                                             
007115          INCLUDE TLBTKSM                                                 
007116     END-EXEC.                                                            
007117                                                                          
007181                                                                          
007190*-----------------------------------------------------------------        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007310*-----------------------------------------------------------------        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
012994                                                                          
012995*-----------------------------------------------------------------        
012996*  WORK AREA FOR DPKUT500 DATE ROUTINE                                    
012997*-----------------------------------------------------------------        
012998     COPY DPWS500.                                                        
013000                                                                          
013600                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014010                                                                          
014020*-----------------------------------------------------------------        
014030* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014040*      PROGRAM.                                                           
014050*-----------------------------------------------------------------        
014100 0000-PROGRAM-MAINLINE.                                                   
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
014710                                                                          
014800     PERFORM 2000-SUMMARY-PROCESS.                                        
015120                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015501                                                                          
015510*-----------------------------------------------------------------        
015520*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015530*-----------------------------------------------------------------        
015600 1000-INITIALIZE.                                                         
016200                                                                          
016310     PERFORM 1100-GET-DELETE-DATE.                                        
016500                                                                          
016501                                                                          
016502*-----------------------------------------------------------------        
016503*  CREATE THE CONTROL CARD.                                               
016504*-----------------------------------------------------------------        
016505 1100-GET-DELETE-DATE.                                                    
016508                                                                          
016509     EXEC SQL                                                             
016510          SELECT DISTINCT                                                 
016512                 DATE(BTCH_PRC_DTE)                                       
016513            INTO                                                          
016515                 :PV-CURRENT-DATE                                         
016516            FROM                                                          
016517                 TCOCNTL                                                  
016518     END-EXEC.                                                            
016519                                                                          
016520     EVALUATE TRUE                                                        
016521         WHEN SQLCODE = ZERO                                              
016522             CONTINUE                                                     
016533                                                                          
016534         WHEN SQLWARN0 NOT = SPACES                                       
016535         WHEN SQLCODE < ZERO                                              
016536         WHEN SQLCODE > ZERO                                              
016537             MOVE '1100-CREATE CONTROL CARD'                              
016538                                 TO PV-PARAGRAPH-NAME                     
016539             MOVE 'ATTEMPTING TO SELECT CURRENT TIMESTAMP                 
016540-                 'FROM TCOCNTL'                                          
016541                                 TO PV-DB2-OPERATION-ATTEMPTED            
016542             PERFORM 9100-SQL-ABEND                                       
016543     END-EVALUATE.                                                        
016544                                                                          
016545     PERFORM 1200-DETERMINE-SATURDAY.                                     
016546                                                                          
016547*-------------------------------------------------------------*           
016548* GET DATE 18 MONTHS PRIOR TO CURRENT DATE (SATURDAY)         *           
016549*  52 WEEKS IN YEAR + 26 WEEKS IN 6 MONTHS = 78 WEEKS         *           
016550*  78 WEEKS * 7 DAYS = 546 DAYS                               *           
016551*-------------------------------------------------------------*           
016552     MOVE 546  TO  PV-DAYS-BACK                                           
016555                                                                          
016558     PERFORM 1250-WEEKS-PRIOR.                                            
016562                                                                          
016567     DISPLAY ' RCKUT070 PURGE DATE -- ' PV-DELETE-THRU.                   
016569                                                                          
016570                                                                          
016571*-----------------------------------------------------------------        
016572*  DETERMINE IF CURRENT DATE IS A SATURDAY - IF NOT FIND THE DATE         
016573*  FOR THE PRIOR SATURDAY                                                 
016574*-----------------------------------------------------------------        
016575 1200-DETERMINE-SATURDAY.                                                 
016576                                                                          
016577     MOVE  '1200-DETERMINE-SATURDAY'  TO  PV-CURRENT-PARAGRAPH.           
016578     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016579                                                                          
016580     DISPLAY 'CURRENT DATE  = '  PV-CURRENT-DATE.                         
016581     MOVE PV-CURRENT-DATE   TO  DPG52-LK-DATE-INPUT.                      
016582                                                                          
016583     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016584         DPG51-DO-NOT-INCR-DECR-DATE                                      
016585         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016586                                                                          
016587     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016588         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016589                                                                          
016590     EVALUATE TRUE                                                        
016591         WHEN DPG54-WARNING-ERROR                                         
016592           DISPLAY '** ABEND **'                                          
016593           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016594           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016595           DISPLAY '** INVALID DATE SUPPLIED'                             
016596           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016597           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016598           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016599           MOVE +4003 TO ABEND-CODE                                       
016600           CALL 'ILBOABN0' USING ABEND-CODE                               
016601         WHEN DPG54-SEVERE-ERROR                                          
016602           DISPLAY '** ABEND **'                                          
016603           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016604           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016605           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016606           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016607           MOVE +4003 TO ABEND-CODE                                       
016608           CALL 'ILBOABN0' USING ABEND-CODE                               
016609         WHEN DPG54-NO-ERROR                                              
016610           CONTINUE                                                       
016611     END-EVALUATE.                                                        
016612                                                                          
016613     IF  NOT DPG55-ALPHA-WEEK-DAY-SATURDAY                                
016614         PERFORM  1220-FIND-SATURDAY                                      
016615           UNTIL  DPG55-ALPHA-WEEK-DAY-SATURDAY                           
016620     END-IF.                                                              
016627                                                                          
016628                                                                          
016629 1220-FIND-SATURDAY.                                                      
016630                                                                          
016631     MOVE  DPG55-ALPHA-WEEK-DAY  TO  PV-WEEK-DAY.                         
016632                                                                          
016633     MOVE  '1220-FIND-SATURDAY'  TO  PV-CURRENT-PARAGRAPH.                
016634     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016635                                                                          
016636     MOVE PV-CURRENT-DATE   TO  DPG52-LK-DATE-INPUT.                      
016637                                                                          
016638     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016639         DPG51-DECREMENT-DATE                                             
016640         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016641                                                                          
016642     IF  PV-WEEK-DAY-SUNDAY                                               
016643         MOVE  1               TO  DPG51-INCR-DECR-DAYS-9                 
016644     ELSE                                                                 
016645     IF  PV-WEEK-DAY-MONDAY                                               
016646         MOVE  2               TO  DPG51-INCR-DECR-DAYS-9                 
016647     ELSE                                                                 
016648     IF  PV-WEEK-DAY-TUESDAY                                              
016649         MOVE  3               TO  DPG51-INCR-DECR-DAYS-9                 
016650     ELSE                                                                 
016651     IF  PV-WEEK-DAY-WEDNESDAY                                            
016652         MOVE  4               TO  DPG51-INCR-DECR-DAYS-9                 
016653     ELSE                                                                 
016654     IF  PV-WEEK-DAY-THRUSDAY                                             
016655         MOVE  5               TO  DPG51-INCR-DECR-DAYS-9                 
016656     ELSE                                                                 
016657     IF  PV-WEEK-DAY-FRIDAY                                               
016658         MOVE  6               TO  DPG51-INCR-DECR-DAYS-9                 
016659     END-IF                                                               
016660     END-IF                                                               
016661     END-IF                                                               
016662     END-IF                                                               
016663     END-IF                                                               
016664     END-IF.                                                              
016666                                                                          
016667     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016668         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016669                                                                          
016670     EVALUATE TRUE                                                        
016671         WHEN DPG54-WARNING-ERROR                                         
016672           DISPLAY '** ABEND **'                                          
016673           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016674           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016675           DISPLAY '** INVALID DATE SUPPLIED'                             
016676           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016677           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016678           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016679           MOVE +4003 TO ABEND-CODE                                       
016680           CALL 'ILBOABN0' USING ABEND-CODE                               
016681         WHEN DPG54-SEVERE-ERROR                                          
016682           DISPLAY '** ABEND **'                                          
016683           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016684           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016685           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016686           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016687           MOVE +4003 TO ABEND-CODE                                       
016688           CALL 'ILBOABN0' USING ABEND-CODE                               
016689         WHEN DPG54-NO-ERROR                                              
016690           CONTINUE                                                       
016691     END-EVALUATE.                                                        
016692                                                                          
016693     DISPLAY 'SATURDAY DATE = '  DPG55-DB2-ISO-DATE-FMT.                  
016694     MOVE  DPG55-DB2-ISO-DATE-FMT  TO  PV-CURRENT-SATURDAY.               
016695                                                                          
016696                                                                          
016697*-----------------------------------------------------------------        
016698* KEEP 4 WEEKS OF DATA ON HISTORY TABLE DETERMINE WEEKENDING DATES        
016699*-----------------------------------------------------------------        
016700 1250-WEEKS-PRIOR.                                                        
016701                                                                          
016702     MOVE  '1250-WEEKS-PRIOR'  TO  PV-CURRENT-PARAGRAPH.                  
016703     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016704                                                                          
016705     MOVE PV-CURRENT-SATURDAY   TO  DPG52-LK-DATE-INPUT.                  
016706                                                                          
016707     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016708         DPG51-DECREMENT-DATE                                             
016709         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016710                                                                          
016711     MOVE  PV-DAYS-BACK        TO  DPG51-INCR-DECR-DAYS-9.                
016713                                                                          
016714     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016715         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016716                                                                          
016717     EVALUATE TRUE                                                        
016718         WHEN DPG54-WARNING-ERROR                                         
016719           DISPLAY '** ABEND **'                                          
016720           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016721           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016722           DISPLAY '** INVALID DATE SUPPLIED'                             
016723           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016724           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016725           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016726           MOVE +4003 TO ABEND-CODE                                       
016727           CALL 'ILBOABN0' USING ABEND-CODE                               
016728         WHEN DPG54-SEVERE-ERROR                                          
016729           DISPLAY '** ABEND **'                                          
016730           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016731           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016732           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016733           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016734           MOVE +4003 TO ABEND-CODE                                       
016735           CALL 'ILBOABN0' USING ABEND-CODE                               
016736         WHEN DPG54-NO-ERROR                                              
016737           CONTINUE                                                       
016738     END-EVALUATE.                                                        
016739                                                                          
016740     MOVE  DPG55-DB2-ISO-DATE-FMT  TO  PV-DELETE-THRU.                    
016750                                                                          
016784                                                                          
016785                                                                          
016786*-----------------------------------------------------------------        
016787*  CREATE HISTORY SUMMARY RECORD PROCESS                                  
016788*-----------------------------------------------------------------        
016789 2000-SUMMARY-PROCESS.                                                    
016790                                                                          
016800     PERFORM 3000-PURGE-COUNT.                                            
016801                                                                          
016802     PERFORM 3100-PURGE-SUMMARY.                                          
016803                                                                          
016815                                                                          
016817******************************************************************        
016818*  COUNT NUMBER OF ROWS THAT WILL BE DELETED                              
016819******************************************************************        
016820 3000-PURGE-COUNT.                                                        
016821                                                                          
016822     MOVE  PV-DELETE-THRU  TO  PV-DELETE-THRU-DATE.                       
016823                                                                          
016824     EXEC SQL                                                             
016825         SELECT  COUNT(*)                                                 
016826           INTO  :PV-DB2-DELETE-COUNT                                     
016827           FROM  TLBTKSM                                                  
016828          WHERE  WK_END_DTE  <=  :PV-DELETE-THRU-DATE                     
016829     END-EXEC.                                                            
016830                                                                          
016831     EVALUATE TRUE                                                        
016832         WHEN SQLCODE = ZERO                                              
016833         WHEN SQLCODE = +100                                              
016834             CONTINUE                                                     
016835         WHEN SQLWARN0 NOT = SPACES                                       
016836         WHEN SQLCODE < ZERO                                              
016837         WHEN SQLCODE > ZERO                                              
016838             MOVE '3000-PURGE-COUNT'                                      
016839                         TO PV-PARAGRAPH-NAME                             
016840             MOVE 'COUNT OF TLBTKSM'                                      
016841                         TO PV-DB2-OPERATION-ATTEMPTED                    
016842             PERFORM 9100-SQL-ABEND                                       
016843     END-EVALUATE.                                                        
016844                                                                          
016845     MOVE  PV-DB2-DELETE-COUNT  TO  PV-DISPLAY-COUNT.                     
016847     DISPLAY 'RCKUT070 - PURGE COUNT = ' PV-DISPLAY-COUNT.                
016848                                                                          
016849                                                                          
016850******************************************************************        
016851*  DELETE THE ROW FROM THE LABEL PRINT DATA TABLE                         
016852******************************************************************        
016853 3100-PURGE-SUMMARY.                                                      
016854                                                                          
016855     MOVE  PV-DELETE-THRU  TO  PV-DELETE-THRU-DATE.                       
016856                                                                          
016857     EXEC SQL                                                             
016858         DELETE                                                           
016859             FROM  TLBTKSM                                                
016860             WHERE WK_END_DTE  <=  :PV-DELETE-THRU-DATE                   
016861     END-EXEC.                                                            
016862                                                                          
016863     EVALUATE TRUE                                                        
016864         WHEN SQLCODE = ZERO                                              
016865             CONTINUE                                                     
016866         WHEN SQLCODE = +100                                              
016869              DISPLAY 'RCKUT070 - NO ROWS PURGED FROM TLBTKSM'            
016870         WHEN SQLWARN0 NOT = SPACES                                       
016871         WHEN SQLCODE < ZERO                                              
016872         WHEN SQLCODE > ZERO                                              
016873             MOVE '3100-PURGE-SUMMARY'                                    
016874                         TO PV-PARAGRAPH-NAME                             
016875             MOVE 'DELETE OF TLBTKSM'                                     
016876                         TO PV-DB2-OPERATION-ATTEMPTED                    
016877             PERFORM 9100-SQL-ABEND                                       
016878     END-EVALUATE.                                                        
016880                                                                          
023900                                                                          
023910*-----------------------------------------------------------------        
023920*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
023930*  ERROR OR WARNING CODE OCCURS.                                          
023940*-----------------------------------------------------------------        
024000 9100-SQL-ABEND.                                                          
024100                                                                          
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
