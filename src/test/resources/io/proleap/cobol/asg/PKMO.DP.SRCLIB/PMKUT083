      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT083.                                        
       AUTHOR.                 COLLEEN CUMMINGS.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           JUNE 2003.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT083 - ELIMINATE S/D STAT CDE DUPLICATE 110'S FOR       *        
      *               DATA INTEGRITY PROCESS (PMKUT078).               *        
      *    NOTE:  THIS IS A NON-DATABASE PROGRAM (NOT DB2 OR ORACLE).  *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *                                                                *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRCG-EXCL-FILE                                                
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT PRCG-EXCL-FILE-OUT                                            
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRCG-EXCL-FILE                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCG-EXCL-LINE                   PIC  X(250).                        
                                                                                
       FD  PRCG-EXCL-FILE-OUT                                                   
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCG-EXCL-LINE-OUT               PIC  X(250).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
          05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)     VALUE                 
                                                          'PMKUT083'.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-READ-FILE-SW           PIC  X(01)     VALUE 'N'.              
               88  PS-READ-FILE-END                     VALUE 'Y'.              
               88  PS-READ-FILE-START                   VALUE 'N'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACES.         
           05  PV-OPERATION-MSG             PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-1           PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-2           PIC  X(40)    VALUE SPACES.         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-SKIPPED-CNT           PIC 9(11) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
                                                                                
       01  PV-PREV-PM708-RECORD.                                                
         05 PV-PREV-BUSINESS-EVNT-ID       PIC 9(03) VALUE ZEROS.               
         05 PV-PREV-ACTION-CDE             PIC X(01) VALUE SPACES.              
         05 PV-PREV-BUSINESS-EVNT-CRE-TMST PIC X(26) VALUE SPACES.              
         05 PV-PREV-PROMO-INTFC-ID         PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-PROMO-INTFC-SUB-ID     PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-MDSE-LNE-ID            PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-MDSE-LNE-EXCLN-ID      PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-MDSE-HIER-CDE          PIC X(03) VALUE SPACES.              
         05 PV-PREV-DEPT-NBR               PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-MAJ-CL-NBR             PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-SUB-CL-NBR             PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-STYLE-ID               PIC 9(08) VALUE ZEROS.               
         05 PV-PREV-SKU-NBR                PIC 9(08) VALUE ZEROS.               
         05 PV-PREV-STAT-CDE               PIC X(01) VALUE SPACES.              
         05 PV-PREV-LAST-UPD-TMST          PIC X(26) VALUE SPACES.              
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING EXCLUSION BUSINESS EVENT.                   
      ******************************************************************        
           COPY PMRD708.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B150-READ-PRCG-EXCL-FILE.                                    
           IF PS-READ-FILE-END                                                  
               MOVE 'NO PRCG EXCL FILE TO PROCESS'                              
                   TO PV-OPERATION-MSG-1                                        
               SET ABEND-4002-READ-ERROR   TO TRUE                              
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B200-PROCESS-AND-WRITE-FILE                                  
             UNTIL PS-READ-FILE-END.                                            
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'RECORDS SKIPPED         = ' PA-RECS-SKIPPED-CNT.            
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES                                               *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD AND THE TWO OUTPUT FILES             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  PRCG-EXCL-FILE                                           
                OUTPUT PRCG-EXCL-FILE-OUT.                                      
                                                                                
      *****************************************************************         
      * B150-READ-PRCG-EXCL-FILE                                      *         
      *****************************************************************         
       B150-READ-PRCG-EXCL-FILE.                                                
                                                                                
           MOVE 'B150-READ-PRCG-EXCL-FILE'      TO PV-PARAGRAPH-NAME.           
           SET PS-READ-FILE-START       TO TRUE.                                
                                                                                
           READ PRCG-EXCL-FILE INTO PM708-PRICING-EXCLSNS-RECORD                
               AT END                                                           
                   SET PS-READ-FILE-END TO TRUE                                 
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * B200-PROCESS-AND-WRITE-FILE                                   *         
      *****************************************************************         
       B200-PROCESS-AND-WRITE-FILE.                                             
                                                                                
           IF PM708-ACTION-CDE = 'T'                                            
               MOVE PM708-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID            
               MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS                  
               WRITE PRCG-EXCL-LINE-OUT FROM PM700-TOTAL-RECORD                 
           ELSE                                                                 
               IF PM708-PRICING-EXCLSNS-RECORD =                                
                  PV-PREV-PM708-RECORD                                          
      *        IF PM708-PROMO-INTFC-ID =                                        
      *               PV-PREV-PROMO-INTFC-ID                                    
      *           AND PM708-PROMO-INTFC-SUB-ID =                                
      *               PV-PREV-PROMO-INTFC-SUB-ID                                
      *           AND PM708-MDSE-LNE-ID =                                       
      *               PV-PREV-MDSE-LNE-ID                                       
      *           AND PM708-MDSE-LNE-EXCLN-ID =                                 
      *               PV-PREV-MDSE-LNE-EXCLN-ID                                 
      *           AND PM708-MDSE-HIER-CDE =                                     
      *               PV-PREV-MDSE-HIER-CDE                                     
      *           AND PM708-DEPT-NBR =                                          
      *               PV-PREV-DEPT-NBR                                          
      *           AND PM708-MAJ-CL-NBR =                                        
      *               PV-PREV-MAJ-CL-NBR                                        
      *           AND PM708-SUB-CL-NBR =                                        
      *               PV-PREV-SUB-CL-NBR                                        
      *           AND PM708-STYLE-ID =                                          
      *               PV-PREV-STYLE-ID                                          
      *           AND PM708-SKU-NBR =                                           
      *               PV-PREV-SKU-NBR                                           
      *           AND PM708-STAT-CDE =                                          
      *               PV-PREV-STAT-CDE                                          
      *           AND PM708-LAST-UPD-TMST =                                     
      *               PV-PREV-LAST-UPD-TMST                                     
                                                                                
                  ADD +1 TO PA-RECS-SKIPPED-CNT                                 
                  DISPLAY 'SKIPPED THIS RECORD - DUP:'                          
                  DISPLAY 'PM708-BUSINESS-EVNT-ID       = '                     
                      PM708-BUSINESS-EVNT-ID                                    
                  DISPLAY 'PM708-ACTION-CDE             = '                     
                      PM708-ACTION-CDE                                          
                  DISPLAY 'PM708-BUSINESS-EVNT-CRE-TMST = '                     
                      PM708-BUSINESS-EVNT-CRE-TMST                              
                  DISPLAY 'PM708-PROMO-INTFC-ID         = '                     
                      PM708-PROMO-INTFC-ID                                      
                  DISPLAY 'PM708-MDSE-LNE-ID            = '                     
                      PM708-MDSE-LNE-ID                                         
                  DISPLAY 'PM708-MDSE-LNE-EXCLN-ID      = '                     
                      PM708-MDSE-LNE-EXCLN-ID                                   
                  DISPLAY 'PM708-MDSE-HIER-CDE          = '                     
                      PM708-MDSE-HIER-CDE                                       
                  DISPLAY 'PM708-DEPT-NBR               = '                     
                      PM708-DEPT-NBR                                            
                  DISPLAY 'PM708-MAJ-CL-NBR             = '                     
                      PM708-MAJ-CL-NBR                                          
                  DISPLAY 'PM708-SUB-CL-NBR             = '                     
                      PM708-SUB-CL-NBR                                          
                  DISPLAY 'PM708-STYLE-ID               = '                     
                      PM708-STYLE-ID                                            
                  DISPLAY 'PM708-SKU-NBR                = '                     
                      PM708-SKU-NBR                                             
                  DISPLAY 'PM708-STAT-CDE               = '                     
                      PM708-STAT-CDE                                            
                  DISPLAY 'PM708-LAST-UPD-TMST          = '                     
                      PM708-LAST-UPD-TMST                                       
               ELSE                                                             
                  WRITE PRCG-EXCL-LINE-OUT FROM                                 
                        PRCG-EXCL-LINE                                          
                  ADD +1 TO PA-RECS-WRITTEN                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-READ-FILE-START                                                
               MOVE PM708-PRICING-EXCLSNS-RECORD TO                             
                    PV-PREV-PM708-RECORD                                        
               PERFORM B150-READ-PRCG-EXCL-FILE                                 
           ELSE                                                                 
               DISPLAY 'END'                                                    
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD AND TWO OUTPUT FILES.               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  PRCG-EXCL-FILE                                                
                  PRCG-EXCL-FILE-OUT.                                           
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
