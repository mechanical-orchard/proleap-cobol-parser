       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT158.                                                 
       AUTHOR.        WADE GROVE.                                               
       DATE-WRITTEN.  12/01/2009.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM CREATES A FILE OF RECORD COUNTS OF SALES HISTORY *        
      *  RECORDS BY VENDOR                                             *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           COPY ETRD019.                                                        
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           DATA RECORD IS ET035-RECORD-COUNT-RECORD.                            
                                                                                
           COPY ETRD035.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-ENT-ID-RECORDS              PIC 9(9) VALUE ZERO.                  
       01  WS-DEPARTMENT-RECORDS          PIC 9(9) VALUE ZERO.                  
       01  WS-DEPARTMENTS                 PIC 9(9) VALUE 1.                     
                                                                                
       01  WS-PREV-ENT-ID                 PIC 9(10) VALUE ZERO.                 
       01  WS-PREV-DEPARTMENT             PIC X(03) VALUE SPACES.               
                                                                                
       01  END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE                      VALUE 'Y'.                
                                                                                
       01  TBL-RECORD-COUNTS.                                                   
           05  TBL-COUNT-ENTRIES-DEPARTMENT OCCURS 2000 TIMES.                  
               10  TBL-X-ENT-ID              PIC 9(10).                         
               10  TBL-X-DEPARTMENT          PIC X(03).                         
               10  TBL-X-DEPARTMENT-RECORDS  PIC 9(08).                         
               10  TBL-X-ENT-ID-RECORDS      PIC 9(09).                         
           05  TBL-COUNT-ENTRIES-ENT-ID  OCCURS 2000 TIMES.                     
               10  TBL-Y-ENT-ID              PIC 9(10).                         
               10  TBL-Y-ENT-ID-RECORDS      PIC 9(09).                         
               10  TBL-Y-DEPARTMENTS         PIC 9(09).                         
                                                                                
       01  X                   PIC 9(9)  VALUE 1.                               
       01  Y                   PIC 9(9)  VALUE 1.                               
       01  MAX-X               PIC 9(9)  VALUE ZERO.                            
       01  MAX-Y               PIC 9(9)  VALUE ZERO.                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           MOVE ET019-ENT-ID     TO  WS-PREV-ENT-ID.                            
           MOVE ET019-DEPARTMENT TO  WS-PREV-DEPARTMENT.                        
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           MOVE WS-PREV-ENT-ID TO TBL-X-ENT-ID (X).                             
           MOVE WS-PREV-ENT-ID TO TBL-Y-ENT-ID (Y).                             
           MOVE WS-PREV-DEPARTMENT TO TBL-X-DEPARTMENT (X).                     
           MOVE WS-ENT-ID-RECORDS TO TBL-Y-ENT-ID-RECORDS (Y).                  
           MOVE WS-DEPARTMENT-RECORDS TO TBL-X-DEPARTMENT-RECORDS (X).          
           MOVE WS-DEPARTMENTS TO TBL-Y-DEPARTMENTS (Y).                        
           MOVE Y TO MAX-Y.                                                     
           MOVE 1 TO Y.                                                         
                                                                                
                                                                                
           PERFORM 4000-CREATE-OUTPUT THRU                                      
                   4000-CREATE-OUTPUT-EXIT                                      
           VARYING X FROM 1 BY 1 UNTIL                                          
                   X > MAX-X.                                                   
                                                                                
      **   PERFORM 9000-DISPLAY THRU                                            
      **           9000-DISPLAY-EXIT                                            
      **   VARYING X FROM 1 BY 1 UNTIL                                          
      **           X > MAX-X.                                                   
      **   PERFORM 9001-DISPLAY THRU                                            
      **           9001-DISPLAY-EXIT                                            
      **   VARYING Y FROM 1 BY 1 UNTIL                                          
      **           Y > MAX-Y.                                                   
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF ET019-HEADER-RECORD                                               
              IF ET019-ENT-ID = WS-PREV-ENT-ID                                  
                 IF ET019-DEPARTMENT NOT EQUAL WS-PREV-DEPARTMENT               
                    MOVE WS-PREV-ENT-ID      TO TBL-X-ENT-ID (X)                
                    MOVE WS-PREV-DEPARTMENT  TO TBL-X-DEPARTMENT (X)            
                    MOVE WS-DEPARTMENT-RECORDS TO                               
                         TBL-X-DEPARTMENT-RECORDS (X)                           
                    MOVE 1 TO WS-DEPARTMENT-RECORDS                             
                    ADD 1 TO WS-DEPARTMENTS                                     
                    ADD 1 TO WS-ENT-ID-RECORDS                                  
                    COMPUTE X = X + 1                                           
                    MOVE ET019-DEPARTMENT TO WS-PREV-DEPARTMENT                 
                 ELSE                                                           
                    ADD 1 TO WS-ENT-ID-RECORDS                                  
                    ADD 1 TO WS-DEPARTMENT-RECORDS                              
              ELSE                                                              
                 MOVE WS-PREV-DEPARTMENT  TO TBL-X-DEPARTMENT (X)               
                 MOVE WS-PREV-ENT-ID      TO TBL-X-ENT-ID (X)                   
                 MOVE WS-PREV-ENT-ID      TO TBL-Y-ENT-ID (Y)                   
                 MOVE WS-DEPARTMENT-RECORDS TO                                  
                      TBL-X-DEPARTMENT-RECORDS (X)                              
                 MOVE WS-ENT-ID-RECORDS   TO TBL-Y-ENT-ID-RECORDS (Y)           
                 MOVE WS-DEPARTMENTS      TO TBL-Y-DEPARTMENTS (Y)              
                 MOVE 1 TO WS-DEPARTMENT-RECORDS                                
                 MOVE 1 TO WS-ENT-ID-RECORDS                                    
                 MOVE 1 TO WS-DEPARTMENTS                                       
                 COMPUTE X = X + 1                                              
                 COMPUTE Y = Y + 1                                              
                 MOVE ET019-DEPARTMENT TO WS-PREV-DEPARTMENT                    
                 MOVE ET019-ENT-ID     TO WS-PREV-ENT-ID                        
           ELSE                                                                 
              ADD 1 TO WS-ENT-ID-RECORDS                                        
              ADD 1 TO WS-DEPARTMENT-RECORDS.                                   
                                                                                
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE                                  
                 MOVE X TO MAX-X.                                               
                                                                                
       2000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
       4000-CREATE-OUTPUT.                                                      
           PERFORM 5000-FIND-ENT-ID-COUNT                                       
              THRU 5000-FIND-ENT-ID-COUNT-EXIT                                  
                   VARYING Y FROM Y BY 1                                        
                   UNTIL TBL-X-ENT-ID (X) = TBL-Y-ENT-ID (Y)                    
                   OR    Y = MAX-Y.                                             
           MOVE TBL-X-ENT-ID (X) TO ET035-ENT-ID.                               
           MOVE TBL-X-DEPARTMENT (X) TO ET035-DEPARTMENT.                       
           MOVE TBL-X-DEPARTMENT-RECORDS (X) TO                                 
                ET035-DEPARTMENT-RECORDS.                                       
           MOVE TBL-Y-ENT-ID-RECORDS (Y) TO ET035-ENT-ID-RECORDS.               
           MOVE TBL-Y-DEPARTMENTS (Y) TO ET035-DEPARTMENTS.                     
           WRITE ET035-RECORD-COUNT-RECORD.                                     
                                                                                
       4000-CREATE-OUTPUT-EXIT.                                                 
           EXIT.                                                                
       5000-FIND-ENT-ID-COUNT.                                                  
      *                                                                         
       5000-FIND-ENT-ID-COUNT-EXIT.                                             
           EXIT.                                                                
       9000-DISPLAY.                                                            
           DISPLAY 'ENT ' TBL-X-ENT-ID (X) ' '                                  
                   'DEPT ' TBL-X-DEPARTMENT (X) ' '                             
                   'DEPT RECS ' TBL-X-DEPARTMENT-RECORDS (X) ' '                
                   'ENT RECS ' TBL-X-ENT-ID-RECORDS (X).                        
                                                                                
       9000-DISPLAY-EXIT.                                                       
           EXIT.                                                                
       9001-DISPLAY.                                                            
           DISPLAY 'ENT ' TBL-Y-ENT-ID (Y) ' '                                  
                   'ENT RECS ' TBL-Y-ENT-ID-RECORDS (Y) ' '                     
                   'DEPTS ' TBL-Y-DEPARTMENTS (Y).                              
       9001-DISPLAY-EXIT.                                                       
           EXIT.                                                                
