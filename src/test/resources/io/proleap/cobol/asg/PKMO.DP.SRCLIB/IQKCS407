000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    IQKCS407.                                                 
000500 AUTHOR.        GREG SHEFF.                                               
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  02-19-03.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    I407 - SIZE LIST                                           *         
001200*                                                               *         
001300*    YOU CAN ONLY COME TO THIS SCREEN FROM IQLCOLOR (IQKCS406). *         
001400*                                                               *         
001500*    DISPLAYS A LIST OF SIZES ASSOCIATED WITH A PARTICULAR      *         
001600*    DEPARTMENT AND COLOR.                                      *         
001700*        IT MAY ALSO BE SPECIFIC FOR                            *         
001800*           VENDOR NAME / LABEL   AND/OR                        *         
001900*           MAJOR AND SUB CLASS   AND/OR                        *         
002000*           STYLE                                               *         
002100*        DEPENDING ON WHAT SCREENS IT CAME THROUGH.             *         
002200*                                                               *         
002300*    ALLOWS THE USER TO SELECT ONE SIZE AND PASS IT, ALONG WITH *         
002400*        ALL OTHER SPECIFIC INFORMATION TO IQKSKU (IQKCS408).   *         
002500*                                                               *         
002510*---------------------------------------------------------------*         
002520* DATE:        09/14/2021                                                 
002530* WR/IR#:      CHG0261678                                                 
002540* PROGRAMMER:  GERMAN BANDA                                               
002550* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
002560*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
002570*              MAKING THE CALL DYNAMIC VS STATIC                          
002580*---------------------------------------------------------------*         
CICS  * CHANGED 11/01/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
002600 ENVIRONMENT DIVISION.                                                    
002700                                                                          
002800 DATA DIVISION.                                                           
002900                                                                          
003000 WORKING-STORAGE SECTION.                                                 
003100 01  DK-DB2-KEYS.                                                         
003200     15  DK-DEPT-NBR                    PIC S9(04)  COMP-3                
003300                                                    VALUE ZERO.           
003400     15  DK-MAJ-CL                      PIC S9(04)  COMP-3                
003500                                                    VALUE ZERO.           
003600     15  DK-SUB-CL                      PIC S9(04)  COMP-3                
003700                                                    VALUE ZERO.           
003800     15  DK-VS-NBR                      PIC  X(20)  VALUE SPACES.         
003900     15  DK-CLOR-DESC                   PIC  X(10)  VALUE SPACE.          
004000     15  DK-LBL-NM                      PIC  X(30)  VALUE SPACES.         
004100                                                                          
004200 01  PC-PROGRAM-CONSTANTS.                                                
004300     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
004400                                                    'IQ407A  '.           
004500     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
004600                                                    'IQKM407 '.           
004700     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004800                                                    'IQKCS407'.           
004900     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
005000     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
005100     05  PC-FORMAT-IND                  PIC  X(01)  VALUE ' '.            
005200     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +10             
005300                                                    COMP SYNC.            
005400     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
005500                                                    COMP SYNC.            
005600     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
005700                                                    +1000000              
005800                                                    COMP-3.               
005900     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
006000                                                    COMP SYNC.            
006100     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
006200                                                    COMP-3.               
006300*                                                                         
006400     05  PC-TSYMSG-NUMBERS.                                               
006500         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
006600         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
006700         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006800         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
006900         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
007000         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
007100         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
007200         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
007300         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
007400         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
007500         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
007600         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
007700         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007800         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007900*                                                                         
008000 01  PS-PROGRAM-SWITCHES.                                                 
008100     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
008200         88  PS-NO-ERROR                            VALUE 'Y'.            
008300         88  PS-ERROR                               VALUE 'N'.            
008400     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008500         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
008600         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
008700     05  PS-CLASS-SW                    PIC  X(01)  VALUE 'N'.            
008800         88  PS-CLASS-NOT-MISSING                   VALUE 'Y'.            
008900         88  PS-CLASS-MISSING                       VALUE 'N'.            
009000     05  PS-DATA-CURSOR-SW              PIC  X(01)  VALUE ' '.            
009100         88  PS-CSR-VENDOR-CLASS-COLOR              VALUE 'A'.            
009200         88  PS-CSR-VENDOR-NOCLS-COLOR              VALUE 'B'.            
009300         88  PS-CSR-VENDOR-STYLE-COLOR              VALUE 'C'.            
009400         88  PS-CSR-NOVND-CLASS-COLOR               VALUE 'D'.            
009500         88  PS-CSR-NOVND-NOCLS-COLOR               VALUE 'E'.            
009600         88  PS-CSR-NOVND-STYLE-COLOR               VALUE 'F'.            
009700     05  PS-COUNT-CURSOR-SW             PIC  X(01)  VALUE ' '.            
009800         88  PS-CNT-VENDOR-CLASS-COLOR              VALUE 'A'.            
009900         88  PS-CNT-LABEL-CLASS-COLOR               VALUE 'B'.            
010000         88  PS-CNT-VENDOR-NOCLS-COLOR              VALUE 'C'.            
010100         88  PS-CNT-LABEL-NOCLS-COLOR               VALUE 'D'.            
010200         88  PS-CNT-VENDOR-STYLE-COLOR              VALUE 'E'.            
010300         88  PS-CNT-LABEL-STYLE-COLOR               VALUE 'F'.            
010400         88  PS-CNT-NOVND-CLASS-COLOR               VALUE 'G'.            
010500         88  PS-CNT-NOVND-NOCLS-COLOR               VALUE 'H'.            
010600         88  PS-CNT-NOVND-STYLE-COLOR               VALUE 'I'.            
010700*                                                                         
010800 01  PV-PROGRAM-VARIABLES.                                                
010900     05  PV-SUB                         PIC S9(04)  VALUE +0              
011000                                                    COMP SYNC.            
011100     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
011200                                                    COMP SYNC.            
011300     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
011400                                                    COMP-3.               
011500     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
011600                                                    COMP-3.               
011700     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
011800                                                    COMP-3.               
011900     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
012000                                                    COMP-3.               
012100     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
012200                                                    COMP-3.               
012300     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
012400                                                    COMP-3.               
012500     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
012600                                                    COMP SYNC.            
012700     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
012800                                                    COMP SYNC.            
012900     05  PV-ITEM                        PIC S9(04)  VALUE +0              
013000                                                    COMP SYNC.            
013100     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
013200                                                    COMP SYNC.            
013300     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
013400                                                    COMP SYNC.            
013500     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
013600                                                    COMP SYNC.            
013700     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
013800                                                    COMP SYNC.            
013900     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
014000                                                    COMP SYNC.            
014100     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
014200                                                    COMP SYNC.            
014300     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
014400                                                    COMP SYNC.            
014500     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
014600                                                    COMP SYNC.            
014700     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
014800                                                    COMP SYNC.            
014900     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
015000                                                    COMP SYNC.            
015100     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
015200                                                    COMP SYNC.            
015300     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
015400                                                    COMP SYNC.            
015500     05  PV-SKU-CNT                     PIC S9(09)  VALUE +0              
015600                                                    COMP-3.               
015700     05  PV-CURSOR-NAME                 PIC X(18)   VALUE SPACES.         
015800******************************************************************        
015900**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
016000**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
016100**  APPLICATION-SPECIFIC COMMAREA.                              **        
016200******************************************************************        
016300 01  LQW-LIST-QUEUE-WORK-AREA.                                            
016400     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
016500     05  LQW-ITEM-ENTRIES               OCCURS 10 TIMES                   
016600                                        INDEXED BY LQW-IDX.               
016700         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
016800         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
016900         10  LQW-SELECTED-SW            PIC  X(01).                       
017000             88  LQW-SELECT                         VALUE 'S'.            
017100             88  LQW-DESELECT                       VALUE ' '.            
017200         10  LQW-SIZE-DESC              PIC  X(10).                       
017300                                                                          
017400     EJECT                                                                
017500*---------------------------------------------------------------*         
017600*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.                    
017700*---------------------------------------------------------------*         
017800     COPY IQWS014.                                                        
017900                                                                          
018000     EJECT                                                                
018100*---------------------------------------------------------------*         
018200*    MAP LAYOUT                                                 *         
018300*---------------------------------------------------------------*         
018400                                                                          
018500     COPY IQKM407.                                                        
018600*                                                                         
018700 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ407AO.               
018800     05  FILLER                         PIC X(219).                       
018900     05  MR-SCROLL-AREA                 OCCURS 10 TIMES.                  
019000         10  MR-SELECTL                 PIC S9(04) COMP.                  
019100         10  MR-SELECTA                 PIC  X(01).                       
019200         10  MR-SELECTC                 PIC  X(01).                       
019300         10  MR-SELECTH                 PIC  X(01).                       
019400         10  MR-SELECTI                 PIC  X(01).                       
019500                                                                          
019600         10  MR-SIZEL                   PIC S9(04) COMP.                  
019700         10  MR-SIZEA                   PIC  X(01).                       
019800         10  MR-SIZEC                   PIC  X(01).                       
019900         10  MR-SIZEH                   PIC  X(01).                       
020000         10  MR-SIZE                    PIC  X(10).                       
020100     EJECT                                                                
020200*---------------------------------------------------------------*         
020300*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
020400*---------------------------------------------------------------*         
020500                                                                          
020600     COPY DPWS015.                                                        
020700                                                                          
020800*---------------------------------------------------------------*         
020900*    FUNCTION KEYS COPYBOOK.                                    *         
021000*---------------------------------------------------------------*         
021100                                                                          
021200     COPY DPWS016.                                                        
021300                                                                          
021400*---------------------------------------------------------------*         
021500*    NUMERIC EDIT LAYOUT.                                       *         
021600*---------------------------------------------------------------*         
021700                                                                          
021800     COPY DPWS010I.                                                       
021900 EJECT                                                                    
022000*---------------------------------------------------------------*         
022100*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
022200*---------------------------------------------------------------*         
022300                                                                          
022400     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
022500 EJECT                                                                    
022600*---------------------------------------------------------------*         
022700*    STANDARD COMMAREA.                                         *         
022800*---------------------------------------------------------------*         
022900                                                                          
023000     COPY DPWS020.                                                        
023100     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
023200*---------------------------------------------------------------*         
023300* INTER-APPLICATION COMMAREA.                                   *         
023400*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
023500*             PROGARM.                                          *         
023600*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
023700*             THIS LIST PROGRAM.                                *         
023800*---------------------------------------------------------------*         
023900                                                                          
024000         10  INTER-APPL-COMM-AREA       PIC X(200).                       
024100     COPY IQWS016.                                                        
024200*---------------------------------------------------------------*         
024300*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
024400*        CURRENT LENGTH IS  600 FOR IQSSIZE MNEMONIC.                     
024500*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
024600*            IF YOU ADD ANY MORE FIELDS.                                  
024700*---------------------------------------------------------------*         
024800         10  ASC-SPECIFIC-COMMAREA.                                       
024900             15  ASC-LIST-KEYS.                                           
025000                 20  ASC-LBL-NM                      PIC  X(30).          
025100                 20  ASC-GP-NBR                      PIC  9(03).          
025200                 20  ASC-GP-DESC                     PIC  X(25).          
025300                 20  ASC-DEPT-NBR                    PIC  9(04).          
025400                 20  ASC-DEPT-DESC                   PIC  X(20).          
025500                 20  ASC-MAJ-CL                      PIC  9(04).          
025600                 20  ASC-SUB-CL                      PIC  9(04).          
025700                 20  ASC-SUB-CL-NM                   PIC  X(20).          
025800                 20  ASC-CLOR-DESC                   PIC  X(10).          
025900                 20  ASC-SKU-CNT                     PIC  9(09).          
026000                 20  ASC-VS-NBR                      PIC  9(09).          
026100             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
026200                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
026300                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
026400                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
026500                                                                          
026600                 20  ASC-ITEM-ARRAY.                                      
026700                     25  ASC-ITEM-ENTRIES OCCURS 10 TIMES.                
026800                         30  ASC-LIST-ITEM-NUMBER                         
026900                                                     PIC S9(09)           
027000                                                     COMP-3.              
027100                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
027200                             88  ASC-VALID-SELECT    VALUE ' '            
027300                                                           'S'            
027400                                                           'B'            
027500                                                           'E'.           
027600                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
027700                             88  ASC-TEMP-SELECT     VALUE 'S'.           
027800                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
027900                             88  ASC-TEMP-END        VALUE 'E'.           
028000                         30  ASC-SELECTED-SW         PIC  X(01).          
028100                             88  ASC-NO-SELECT       VALUE ' '.           
028200                             88  ASC-SELECT          VALUE 'S'.           
028300                             88  ASC-BEGIN           VALUE 'B'.           
028400                             88  ASC-END             VALUE 'E'.           
028500                         30  ASC-SIZE-DESC           PIC  X(10).          
028600                                                                          
028700             15  ASC-BLK-SUB                         PIC S9(04)           
028800                                                          COMP            
028900                                                          SYNC.           
029000             15  ASC-ITEM-SUB                        PIC S9(04)           
029100                                                          COMP            
029200                                                          SYNC.           
029300             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
029400                                                          COMP-3.         
029500             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
029600                                                          COMP-3.         
029700             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
029800                                                          COMP-3.         
029900             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
030000                                                          COMP-3.         
030100             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
030200                                                          COMP-3.         
030300             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
030400                                                          COMP            
030500                                                          SYNC.           
030600             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
030700                                                          COMP-3.         
030800             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
030900                                                          COMP-3.         
031000             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
031100                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
031200                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
031300             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
031400                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
031500                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
031600             15  ASC-LIST-QUEUE-NAME.                                     
031700                 20  ASC-LIST-TERM-ID                PIC  X(04).          
031800                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
031900                                                          COMP-3.         
032000                 20  ASC-LIST-SEQ                    PIC  9(01).          
032100             15  ASC-SEL-QUEUE-NAME.                                      
032200                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
032300                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
032400                                                          COMP-3.         
032500                 20  ASC-SEL-SEQ                     PIC  9(01).          
032600             15  ASC-START-ITEM-REQUEST.                                  
032700                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
032800             15  ASC-FILLER                          PIC  X(58).          
032900         10  FILLER                                  PIC  X(2265).        
033000     EJECT                                                                
033100*---------------------------------------------------------------*         
033200*    ABEND PROCESSING COPYBOOK.                                 *         
033300*---------------------------------------------------------------*         
033400                                                                          
033500     COPY DPWS013.                                                        
033600                                                                          
033700 EJECT                                                                    
033800*---------------------------------------------------------------*         
033900*    DB2 AREA FOR XST_VND_STYL                                  *         
034000*---------------------------------------------------------------*         
034100     EXEC SQL                                                             
034200          INCLUDE XST00013                                                
034300     END-EXEC.                                                            
034400*---------------------------------------------------------------*         
034500*    DB2 AREA FOR XST_SKU                                                 
034600*---------------------------------------------------------------*         
034700     EXEC SQL                                                             
034800          INCLUDE XST00010                                                
034900     END-EXEC.                                                            
035000*---------------------------------------------------------------*         
035100*    DB2 AREA FOR XST_VND                                       *         
035200*---------------------------------------------------------------*         
035300     EXEC SQL                                                             
035400          INCLUDE XST00018                                                
035500     END-EXEC.                                                            
035600*---------------------------------------------------------------*         
035700*    DB2 AREA FOR COMMUNICATIONS                                *         
035800*---------------------------------------------------------------*         
035900     EXEC SQL                                                             
036000          INCLUDE SQLCA                                                   
036100     END-EXEC.                                                            
036200*---------------------------------------------------------------*         
036300*    FETCH ALL SIZES                                                      
036400*---------------------------------------------------------------*         
036500     EXEC SQL                                                             
036600          DECLARE VENDOR-CLASS-COLOR CURSOR FOR                           
036700            SELECT  DISTINCT                                              
036800                     F.SIZE_DESC                                          
036900             FROM  XST_VND_STYL  D,                                       
037000                   XST_VND       E,                                       
037100                   XST_SKU       F                                        
037200              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
037300                AND  E.ENT_NM_DESC    = :DK-LBL-NM                        
037400                AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                        
037500                AND  D.SUB_CL_NBR     = :DK-SUB-CL                        
037600                AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                     
037700                AND  D.STYL_ID        =  F.STYL_ID                        
037800                AND  D.ENT_ID         =  E.ENT_ID                         
037900            UNION                                                         
038000            SELECT  DISTINCT                                              
038100                     F.SIZE_DESC                                          
038200             FROM  XST_VND_STYL  D,                                       
038300                   XST_SKU       F                                        
038400              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
038500                AND  D.LBL_NM         = :DK-LBL-NM                        
038600                AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                        
038700                AND  D.SUB_CL_NBR     = :DK-SUB-CL                        
038800                AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                     
038900                AND  D.STYL_ID        =  F.STYL_ID                        
039000                 ORDER BY 1                                               
039100     END-EXEC.                                                            
039200                                                                          
039300*    FETCH ALL SIZES (MISSING CLASS)                                      
039400     EXEC SQL                                                             
039500          DECLARE VENDOR-NOCLS-COLOR CURSOR FOR                           
039600            SELECT  DISTINCT                                              
039700                     F.SIZE_DESC                                          
039800             FROM  XST_VND_STYL  D,                                       
039900                   XST_VND       E,                                       
040000                   XST_SKU       F                                        
040100              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
040200                AND  E.ENT_NM_DESC    = :DK-LBL-NM                        
040300                AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                      
040400                AND  D.STYL_ID        =  F.STYL_ID                        
040500                AND  D.ENT_ID         =  E.ENT_ID                         
040600            UNION                                                         
040700            SELECT  DISTINCT                                              
040800                     F.SIZE_DESC                                          
040900             FROM  XST_VND_STYL  D,                                       
041000                   XST_SKU       F                                        
041100              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
041200                AND  D.LBL_NM         = :DK-LBL-NM                        
041300                AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                      
041400                AND  D.STYL_ID        =  F.STYL_ID                        
041500                 ORDER BY 1                                               
041600     END-EXEC.                                                            
041700                                                                          
041800*    FETCH ALL SIZES (USING STYLE)                                        
041900     EXEC SQL                                                             
042000          DECLARE VENDOR-STYLE-COLOR CURSOR FOR                           
042100            SELECT  DISTINCT                                              
042200                     F.SIZE_DESC                                          
042300             FROM  XST_VND_STYL  D,                                       
042400                   XST_VND       E,                                       
042500                   XST_SKU       F                                        
042600              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
042700                AND  D.VS_NBR         = :DK-VS-NBR                        
042800                AND  E.ENT_NM_DESC    = :DK-LBL-NM                        
042900                AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                      
043000                AND  D.STYL_ID        =  F.STYL_ID                        
043100                AND  D.ENT_ID         =  E.ENT_ID                         
043200            UNION                                                         
043300            SELECT  DISTINCT                                              
043400                     F.SIZE_DESC                                          
043500             FROM  XST_VND_STYL  D,                                       
043600                   XST_SKU       F                                        
043700              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
043800                AND  D.VS_NBR         = :DK-VS-NBR                        
043900                AND  D.LBL_NM         = :DK-LBL-NM                        
044000                AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                      
044100                AND  D.STYL_ID        =  F.STYL_ID                        
044200                 ORDER BY 1                                               
044300     END-EXEC.                                                            
044400                                                                          
044500*---------------------------------------------------------------*         
044600*    FETCH ALL SIZES                                                      
044700*---------------------------------------------------------------*         
044800     EXEC SQL                                                             
044900          DECLARE NOVND-CLASS-COLOR CURSOR FOR                            
045000            SELECT  DISTINCT                                              
045100                     F.SIZE_DESC                                          
045200             FROM  XST_VND_STYL  D,                                       
045300                   XST_SKU       F                                        
045400              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
045500                AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                        
045600                AND  D.SUB_CL_NBR     = :DK-SUB-CL                        
045700                AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                     
045800                AND  D.STYL_ID        =  F.STYL_ID                        
045900                 ORDER BY 1                                               
046000     END-EXEC.                                                            
046100                                                                          
046200*    FETCH ALL SIZES (MISSING CLASS)                                      
046300     EXEC SQL                                                             
046400          DECLARE NOVND-NOCLS-COLOR CURSOR FOR                            
046500            SELECT  DISTINCT                                              
046600                     F.SIZE_DESC                                          
046700             FROM  XST_VND_STYL  D,                                       
046800                   XST_SKU       F                                        
046900              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
047000                AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                     
047100                AND  D.STYL_ID        =  F.STYL_ID                        
047200                 ORDER BY 1                                               
047300     END-EXEC.                                                            
047400                                                                          
047500*    FETCH ALL SIZES (USING STYLE)                                        
047600     EXEC SQL                                                             
047700          DECLARE NOVND-STYLE-COLOR CURSOR FOR                            
047800            SELECT  DISTINCT                                              
047900                     F.SIZE_DESC                                          
048000             FROM  XST_VND_STYL  D,                                       
048100                   XST_SKU       F                                        
048200              WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                      
048300                AND  D.VS_NBR         = :DK-VS-NBR                        
048400                AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                     
048500                AND  D.STYL_ID        =  F.STYL_ID                        
048600                 ORDER BY 1                                               
048700     END-EXEC.                                                            
048800                                                                          
048900*    COUNT AVAILABLE SKU'S (VENDOR NAMES)                                 
049000     EXEC SQL                                                             
049100        DECLARE CNT-VND-CLS-CLR CURSOR FOR                                
049200           SELECT  COUNT(*)                                               
049300             FROM  XST_VND_STYL  D,                                       
049400                   XST_VND       E,                                       
049500                   XST_SKU       F                                        
049600               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
049700                 AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                       
049800                 AND  D.SUB_CL_NBR     = :DK-SUB-CL                       
049900                 AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                     
050000                 AND  E.ENT_NM_DESC    = :DK-LBL-NM                       
050100                 AND  D.STYL_ID        =  F.STYL_ID                       
050200                 AND  D.ENT_ID         =  E.ENT_ID                        
050300     END-EXEC.                                                            
050400                                                                          
050500*    COUNT AVAILABLE SKU'S (VENDOR LABELS)                                
050600     EXEC SQL                                                             
050700        DECLARE CNT-LBL-CLS-CLR CURSOR FOR                                
050800           SELECT  COUNT(*)                                               
050900             FROM  XST_VND_STYL  D,                                       
051000                   XST_SKU       F                                        
051100               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
051200                 AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                       
051300                 AND  D.SUB_CL_NBR     = :DK-SUB-CL                       
051400                 AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                     
051500                 AND  D.LBL_NM         = :DK-LBL-NM                       
051600                 AND  D.STYL_ID        =  F.STYL_ID                       
051700     END-EXEC.                                                            
051800                                                                          
051900*    COUNT AVAILABLE SKU'S (VENDOR NAMES, MISSING CLASS)                  
052000     EXEC SQL                                                             
052100        DECLARE CNT-VND-NOCLS-CLR CURSOR FOR                              
052200           SELECT  COUNT(*)                                               
052300             FROM  XST_VND_STYL  D,                                       
052400                   XST_VND       E,                                       
052500                   XST_SKU       F                                        
052600               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
052700                 AND  E.ENT_NM_DESC    = :DK-LBL-NM                       
052800                 AND  F.NRF_CLOR_DESC = :DK-CLOR-DESC                     
052900                 AND  D.STYL_ID        =  F.STYL_ID                       
053000                 AND  D.ENT_ID         =  E.ENT_ID                        
053100     END-EXEC.                                                            
053200                                                                          
053300*    COUNT AVAILABLE SKU'S (VENDOR LABELS, MISSING CLASS)                 
053400     EXEC SQL                                                             
053500        DECLARE CNT-LBL-NOCLS-CLR CURSOR FOR                              
053600           SELECT  COUNT(*)                                               
053700             FROM  XST_VND_STYL  D,                                       
053800                   XST_SKU       F                                        
053900               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
054000                 AND  D.LBL_NM         = :DK-LBL-NM                       
054100                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
054200                 AND  D.STYL_ID        =  F.STYL_ID                       
054300     END-EXEC.                                                            
054400                                                                          
054500*    COUNT AVAILABLE SKU'S (VENDOR NAMES, MISSING CLASS)                  
054600     EXEC SQL                                                             
054700        DECLARE CNT-VND-STYLE-CLR CURSOR FOR                              
054800           SELECT  COUNT(*)                                               
054900             FROM  XST_VND_STYL  D,                                       
055000                   XST_VND       E,                                       
055100                   XST_SKU       F                                        
055200               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
055300                 AND  D.VS_NBR         = :DK-VS-NBR                       
055400                 AND  E.ENT_NM_DESC    = :DK-LBL-NM                       
055500                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
055600                 AND  D.STYL_ID        =  F.STYL_ID                       
055700                 AND  D.ENT_ID         =  E.ENT_ID                        
055800     END-EXEC.                                                            
055900                                                                          
056000*    COUNT AVAILABLE SKU'S (VENDOR LABELS, MISSING CLASS)                 
056100     EXEC SQL                                                             
056200        DECLARE CNT-LBL-STYLE-CLR CURSOR FOR                              
056300           SELECT  COUNT(*)                                               
056400             FROM  XST_VND_STYL  D,                                       
056500                   XST_SKU       F                                        
056600               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
056700                 AND  D.VS_NBR         = :DK-VS-NBR                       
056800                 AND  D.LBL_NM         = :DK-LBL-NM                       
056900                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
057000                 AND  D.STYL_ID        =  F.STYL_ID                       
057100     END-EXEC.                                                            
057200                                                                          
057300                                                                          
057400                                                                          
057500*    COUNT AVAILABLE SKU'S (VENDOR NAMES)                                 
057600     EXEC SQL                                                             
057700        DECLARE CNT-NOVND-CLS-CLR CURSOR FOR                              
057800           SELECT  COUNT(*)                                               
057900             FROM  XST_VND_STYL  D,                                       
058000                   XST_SKU       F                                        
058100               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
058200                 AND  D.MAJ_CL_NBR     = :DK-MAJ-CL                       
058300                 AND  D.SUB_CL_NBR     = :DK-SUB-CL                       
058400                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
058500                 AND  D.STYL_ID        =  F.STYL_ID                       
058600     END-EXEC.                                                            
058700                                                                          
058800*    COUNT AVAILABLE SKU'S (VENDOR NAMES, MISSING CLASS)                  
058900     EXEC SQL                                                             
059000        DECLARE CNT-NOVND-NOCL-CLR CURSOR FOR                             
059100           SELECT  COUNT(*)                                               
059200             FROM  XST_VND_STYL  D,                                       
059300                   XST_SKU       F                                        
059400               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
059500                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
059600                 AND  D.STYL_ID        =  F.STYL_ID                       
059700     END-EXEC.                                                            
059800                                                                          
059900*    COUNT AVAILABLE SKU'S (VENDOR NAMES, MISSING CLASS)                  
060000     EXEC SQL                                                             
060100        DECLARE CNT-NOVND-STY-CLR CURSOR FOR                              
060200           SELECT  COUNT(*)                                               
060300             FROM  XST_VND_STYL  D,                                       
060400                   XST_SKU       F                                        
060500               WHERE  D.DEPT_NBR       = :DK-DEPT-NBR                     
060600                 AND  D.VS_NBR         = :DK-VS-NBR                       
060700                 AND  F.NRF_CLOR_DESC  = :DK-CLOR-DESC                    
060800                 AND  D.STYL_ID        =  F.STYL_ID                       
060900     END-EXEC.                                                            
061000                                                                          
061100 LINKAGE SECTION.                                                         
061200                                                                          
061300 01  DFHCOMMAREA.                                                         
061400     05  FILLER                         OCCURS  1 TO 4072 TIMES           
061500                                        DEPENDING ON EIBCALEN.            
061600         10  FILLER                     PIC X.                            
061700                                                                          
061800 EJECT                                                                    
061900 PROCEDURE DIVISION.                                                      
062000******************************************************************        
062100**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
062200**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
062300**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
062400******************************************************************        
062500 0000-MAIN-MODULE.                                                        
062600     INITIALIZE DP030-CICS-API-FIELDS.                                    
062700     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
062800     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
062900     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
063000     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
063100                                                                          
063200     MOVE LENGTH OF IQ407AI        TO DP030-MAP-LENGTH (1).               
063300     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
063400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
063500*                                                                         
063600     PERFORM 1000-CONTROL-PROCESSING                                      
063700*                                                                         
063800     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
063900     PERFORM 0001-CALL-CICS-ARCH-API.                                     
064000                                                                          
064100******************************************************************        
064200**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
064300**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
064400******************************************************************        
064500 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
064700                                    DFHCOMMAREA                           
064800                                    DP030-CICS-API-FIELDS                 
064900                                    DP020-STANDARD-COMMAREA               
065000                                    IQ407AI.                              
065100*                                                                         
065200     IF  DP030-RC-CALL-SUCCESSFUL                                         
065300         CONTINUE                                                         
065400     ELSE                                                                 
065500         SET DP013-NO-ROLLBACK                                            
065600             DP013-XCTL-DISPLAY-RESTART                                   
065700             DP013-CICS-ABEND      TO TRUE                                
065800         MOVE '0001-CALL-CICS-ARCH-API'                                   
065900                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
066200         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
066300         PERFORM DP013-0000-PROCESS-ABEND                                 
066400     END-IF.                                                              
066500*----------------------------------------------------------------*        
066600* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
066700* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
066800*                                                                *        
066900* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
067000*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
067100*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
067200*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
067300*----------------------------------------------------------------*        
067400                                                                          
067500 1000-CONTROL-PROCESSING.                                                 
067600                                                                          
067700     EVALUATE TRUE                                                        
067800         WHEN DP020-NEXT-ACT-INITIAL                                      
067900             INITIALIZE ASC-SPECIFIC-COMMAREA                             
068000             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
068100                                      ASC-SEL-TERM-ID                     
068200             MOVE DP020-SRC-TASK-NUMBER                                   
068300                                   TO ASC-LIST-TASK-NUMBER                
068400                                      ASC-SEL-TASK-NUMBER                 
068500             MOVE PC-LIST-QUEUE-SEQ                                       
068600                                   TO ASC-LIST-SEQ                        
068700             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
068800             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
068900***-----$TEMPLATE NOTE$-------------------------------------------        
069000*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
069100*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
069200*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
069300***---------------------------------------------------------------        
069400             IF  PS-COMMAREA-NOT-VALID                                    
069500                 SET DP020-NEXT-ACT-APPL-ERROR                            
069600                                   TO TRUE                                
069700             ELSE                                                         
069800                 PERFORM 4000-BUILD-INITIAL-PANEL                         
069900             END-IF                                                       
070000                                                                          
070100         WHEN DP020-NEXT-ACT-READ-MAP                                     
070200             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
070300                                   TO PV-TOP-ITEM                         
070400             PERFORM 2000-PROCESS-PANEL                                   
070500                                                                          
070600         WHEN DP020-NEXT-ACT-RETURN                                       
070700             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
070800                                   TO PV-TOP-ITEM                         
070900             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
071000             PERFORM 6500-REINIT-ASC-FLDS                                 
071100             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
071300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
071400                                                                          
071500         WHEN OTHER                                                       
071600             SET DP013-LOGIC-ABEND TO TRUE                                
071700             SET DP013-NO-ROLLBACK TO TRUE                                
071800             MOVE '1000-CONTROL-PROCESSING'                               
071900                                   TO DP013-PARAGRAPH                     
072000             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
072100                                   TO DP013-MESSAGE-TEXT(1)               
072200             PERFORM DP013-0000-PROCESS-ABEND                             
072300     END-EVALUATE.                                                        
072400                                                                          
072500 EJECT                                                                    
072600*----------------------------------------------------------------*        
072700* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
072800* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
072900*----------------------------------------------------------------*        
073000                                                                          
073100 1100-PROCESS-INTER-APPL-COMM.                                            
073200     SET PS-COMMAREA-VALID         TO TRUE.                               
073300     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
073400                                                                          
073500     IF IQ016-SUB-CL-NM = 'CLASS SCREEN BYPASSD'                          
073600        SET PS-CLASS-MISSING TO TRUE                                      
073700     ELSE                                                                 
073800        SET PS-CLASS-NOT-MISSING TO TRUE                                  
073900     END-IF.                                                              
074000                                                                          
074100     MOVE SPACE                    TO DP020-INT-APPL-FORMAT-IND.          
074200     IF  DP020-INT-APPL-FORMAT-IND = PC-FORMAT-IND                        
074300         MOVE IQ016-LBL-NM       TO ASC-LBL-NM                            
074400                                    DK-LBL-NM                             
074500         MOVE IQ016-GP-NBR       TO ASC-GP-NBR                            
074600         MOVE IQ016-GP-DESC      TO ASC-GP-DESC                           
074700         MOVE IQ016-DEPT-NBR     TO ASC-DEPT-NBR                          
074800                                    DK-DEPT-NBR                           
074900         MOVE IQ016-DEPT-DESC    TO ASC-DEPT-DESC                         
075000         MOVE IQ016-MAJ-CL       TO ASC-MAJ-CL                            
075100                                    DK-MAJ-CL                             
075200         MOVE IQ016-SUB-CL       TO ASC-SUB-CL                            
075300                                    DK-SUB-CL                             
075400         MOVE IQ016-SUB-CL-NM    TO ASC-SUB-CL-NM                         
075500         MOVE IQ016-CLOR-DESC    TO ASC-CLOR-DESC                         
075600                                    DK-CLOR-DESC                          
075700         MOVE IQ016-VS-NBR       TO ASC-VS-NBR                            
075800                                    DK-VS-NBR                             
075900     ELSE                                                                 
076000         IF  DP020-NAV-KEY-DATA > SPACE                                   
076100             MOVE DP020-NAV-KEY-DATA TO                                   
076200                                    ASC-LBL-NM                            
076300                                    DK-LBL-NM                             
076400                                    ASC-DEPT-NBR                          
076500                                    DK-DEPT-NBR                           
076600                                    ASC-MAJ-CL                            
076700                                    DK-MAJ-CL                             
076800                                    ASC-SUB-CL                            
076900                                    DK-SUB-CL                             
077000                                    ASC-CLOR-DESC                         
077100                                    DK-CLOR-DESC                          
077200                                    ASC-VS-NBR                            
077300                                    DK-VS-NBR                             
077400         END-IF                                                           
077500     END-IF.                                                              
077600                                                                          
077700 EJECT                                                                    
077800*----------------------------------------------------------------*        
077900* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
078000* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
078100* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
078200* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
078300* FUNCTION KEYS CAN GET THIS FAR.                                *        
078400*----------------------------------------------------------------*        
078500                                                                          
078600 2000-PROCESS-PANEL.                                                      
078700     EVALUATE TRUE                                                        
078800         WHEN DP020-SRC-AID = DP016-CLEAR                                 
078900             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
079000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
079100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
079200                                                                          
079300         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
079400             MOVE ASC-LBL-NM    TO DK-LBL-NM                              
079500             MOVE ASC-DEPT-NBR  TO DK-DEPT-NBR                            
079600             MOVE ASC-MAJ-CL    TO DK-MAJ-CL                              
079700             MOVE ASC-SUB-CL    TO DK-SUB-CL                              
079800             MOVE ASC-CLOR-DESC TO DK-CLOR-DESC                           
079900             MOVE ASC-VS-NBR    TO DK-VS-NBR                              
080000                                                                          
080100             PERFORM 2050-DELETE-LIST-QUEUE                               
080200             PERFORM 4000-BUILD-INITIAL-PANEL                             
080300                                                                          
080400         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
080500             PERFORM 2140-DESELECT-ALL-ITEMS                              
080600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
080700                                                                          
080800         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
080900             PERFORM 2150-SELECT-ALL-ITEMS                                
081000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
081100                                                                          
081200         WHEN OTHER                                                       
081300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
081400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
081500             IF  PS-NO-ERROR                                              
081600                 SET DP030-CONTINUE-GO                                    
081700                                 TO TRUE                                  
081800                 PERFORM 2100-CHECK-FUNCTION-KEY                          
081900             ELSE                                                         
082000                 SET DP030-OVERRIDE-GO                                    
082100                                 TO TRUE                                  
082200             END-IF                                                       
082300     END-EVALUATE.                                                        
082400 EJECT                                                                    
082500*----------------------------------------------------------------*        
082600*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
082700*----------------------------------------------------------------*        
082800 2050-DELETE-LIST-QUEUE.                                                  
082900                                                                          
083000     EXEC CICS                                                            
083100         DELETEQ TS                                                       
083200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
083300             RESP  (PV-CICS-RESP)                                         
083400     END-EXEC.                                                            
083500                                                                          
083600     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
083700        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
083800         CONTINUE                                                         
083900     ELSE                                                                 
084000         SET DP013-CICS-ABEND      TO TRUE                                
084100         SET DP013-NO-ROLLBACK     TO TRUE                                
084200                                                                          
084300         MOVE '2050-DELETE-LIST-QUEUE'                                    
084400                                   TO DP013-PARAGRAPH                     
084500         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
084600                                   TO DP013-MESSAGE-TEXT (1)              
084700         PERFORM DP013-0000-PROCESS-ABEND                                 
084800     END-IF.                                                              
084900                                                                          
085000     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
085100 EJECT                                                                    
085200*----------------------------------------------------------------*        
085300* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
085400* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
085500* CICS ARCHITECTURE API.                                         *        
085600*----------------------------------------------------------------*        
085700 2100-CHECK-FUNCTION-KEY.                                                 
085800     EVALUATE TRUE                                                        
085900*        LOCAL-01   SKU LIST    IQLSKU                                    
086000         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
086100             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
086200*                --  A LIST ITEM MUST BE SELECTED --                      
086300                 MOVE PC-TSYMSG-00057                                     
086400                                   TO DP020-MSG-NUMBER                    
086500                 SET DP020-MSG-FATAL                                      
086600                     DP030-OVERRIDE-GO                                    
086700                                   TO TRUE                                
086800             ELSE                                                         
086900               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
087000*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
087100                   MOVE PC-TSYMSG-00047                                   
087200                                   TO DP020-MSG-NUMBER                    
087300                   SET DP020-MSG-FATAL                                    
087400                       DP030-OVERRIDE-GO                                  
087500                                   TO TRUE                                
087600               ELSE                                                       
087700                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
087800                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
087900                 PERFORM 5000-PREPARE-SEL-QUEUE                           
088000               END-IF                                                     
088100             END-IF                                                       
088200                                                                          
088300         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
088400             MOVE +1               TO PV-TOP-ITEM                         
088500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
088600                                                                          
088700         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
088800             PERFORM 2110-BROWSE-BACKWARD                                 
088900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
089000                                                                          
089100         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
089200             PERFORM 2120-BROWSE-FORWARD                                  
089300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
089400                                                                          
089500         WHEN DP020-SRC-AID = DP016-ENTER                                 
089600             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
089700             IF  PS-ERROR                                                 
089800                 CONTINUE                                                 
089900             ELSE                                                         
090000                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
090100             END-IF                                                       
090200                                                                          
090300         WHEN OTHER                                                       
090400             SET DP013-NO-ROLLBACK                                        
090500                 DP013-XCTL-DISPLAY-RESTART                               
090600                 DP013-CICS-ABEND  TO TRUE                                
090700             MOVE '2100-CHECK-FUNCTION-KEY'                               
090800                                   TO DP013-PARAGRAPH                     
090900             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
091000                                   TO DP013-MESSAGE-TEXT (1)              
091100             PERFORM DP013-0000-PROCESS-ABEND                             
091200     END-EVALUATE.                                                        
091300*                                                                         
091400*----------------------------------------------------------------*        
091500*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
091600*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
091700*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
091800*----------------------------------------------------------------*        
091900                                                                          
092000 2110-BROWSE-BACKWARD.                                                    
092100     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
092200         GIVING PV-TOP-ITEM.                                              
092300*                                                                         
092400     IF  ((PV-TOP-ITEM < +1)                                              
092500       OR (PV-TOP-ITEM = +1))                                             
092600         MOVE +1                   TO PV-TOP-ITEM                         
092700*        -- TOP OF LIST --                                                
092800         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
092900         SET DP020-MSG-INFORMATIONAL TO TRUE                              
093000     END-IF.                                                              
093100                                                                          
093200 EJECT                                                                    
093300*----------------------------------------------------------------*        
093400*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
093500*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
093600*    PAST THE END, ISSUE THE BOTTOM OF LIST                      *        
093700*----------------------------------------------------------------*        
093800                                                                          
093900 2120-BROWSE-FORWARD.                                                     
094000                                                                          
094100     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
094200         GIVING PV-TOP-ITEM.                                              
094300     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
094400         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
094500                                   TO PV-TOP-ITEM                         
094600*        -- BOTTOM OF LIST --                                             
094700         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
094800         SET DP020-MSG-INFORMATIONAL                                      
094900                                   TO TRUE                                
095000     ELSE                                                                 
095100         COMPUTE PV-BOTTOM-ITEM =                                         
095200             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
095300         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
095400*            -- BOTTOM OF LIST --                                         
095500             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
095600             SET DP020-MSG-INFORMATIONAL                                  
095700                                   TO TRUE                                
095800         END-IF                                                           
095900     END-IF.                                                              
096000 EJECT                                                                    
096100*----------------------------------------------------------------*        
096200*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
096300*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
096400*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
096500*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
096600*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
096700*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
096800*----------------------------------------------------------------*        
096900                                                                          
097000 2130-CHECK-FOR-JUMP-BROWSE.                                              
097100     SET PS-NO-ERROR               TO TRUE.                               
097200     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
097300         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
097400                                   TO PV-TOP-ITEM                         
097500     ELSE                                                                 
097600         MOVE ASC-START-ITEM-REQUEST                                      
097700                                   TO DP010I-UNEDITED-FIELD               
097800         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
097900         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
098000         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
098100                                   TO TRUE                                
098210         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
098220                         USING DP010I-NUMERIC-EDIT-AREA                   
098300                                                                          
098400         IF  NOT DP010I-ERROR-DETECTED                                    
098500             MOVE DP010I-NUMERIC-FIELD                                    
098600                                   TO  ASC-START-ITEM-REQUEST-N           
098700             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
098800                 IF  ASC-START-ITEM-REQUEST-N >                           
098900                                          ASC-NUMBER-OF-ITEMS-READ        
099000                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
099100                     COMPUTE PV-TOP-ITEM =                                
099200                             ASC-NUMBER-OF-ITEMS-READ                     
099300                             - PC-ITEMS-PER-PANEL + 1                     
099400                     IF  PV-TOP-ITEM < +1                                 
099500                         MOVE +1   TO PV-TOP-ITEM                         
099600                         MOVE PC-TSYMSG-00070                             
099700                                   TO DP020-MSG-NUMBER                    
099800                         SET DP020-MSG-INFORMATIONAL                      
099900                                   TO TRUE                                
100000                     END-IF                                               
100100                 ELSE                                                     
100200                     MOVE ASC-START-ITEM-REQUEST-N                        
100300                                   TO PV-TOP-ITEM                         
100400                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
100500                     COMPUTE PV-BOTTOM-ITEM =                             
100600                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
100700                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
100800*                        -- BOTTOM OF LIST --                             
100900                         MOVE PC-TSYMSG-00070                             
101000                                   TO DP020-MSG-NUMBER                    
101100                         SET DP020-MSG-INFORMATIONAL                      
101200                                   TO TRUE                                
101300                     END-IF                                               
101400                 END-IF                                                   
101500             ELSE                                                         
101600*                --- VALUE OUT OF RANGE ----                              
101700                 MOVE PC-TSYMSG-00101                                     
101800                                   TO DP020-MSG-NUMBER                    
101900                 MOVE -1           TO ASTRTITL                            
102000                 SET DP030-SET-CURSOR-APPL-1                              
102100                                   TO TRUE                                
102200                 MOVE DP015-REVERSE                                       
102300                                   TO ASTRTITH                            
102400                 MOVE DP015-RED    TO ASTRTITC                            
102500                 MOVE DP015-UNP-NUM-BRT-MDT                               
102600                                   TO ASTRTITA                            
102700                 SET DP020-MSG-FATAL                                      
102800                                   TO TRUE                                
102900                 SET PS-ERROR      TO TRUE                                
103000             END-IF                                                       
103100                                                                          
103200         ELSE                                                             
103300             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
103400             MOVE -1               TO ASTRTITL                            
103500             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
103600             MOVE DP015-REVERSE    TO ASTRTITH                            
103700             MOVE DP015-RED        TO ASTRTITC                            
103800             MOVE DP015-UNP-NUM-BRT-MDT                                   
103900                                   TO ASTRTITA                            
104000             SET DP020-MSG-FATAL   TO TRUE                                
104100             SET PS-ERROR          TO TRUE                                
104200         END-IF                                                           
104300     END-IF.                                                              
104400 EJECT                                                                    
104500*----------------------------------------------------------------*        
104600*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
104700*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
104800*----------------------------------------------------------------*        
104900 2140-DESELECT-ALL-ITEMS.                                                 
105000     MOVE +1                       TO ASC-BLK-SUB.                        
105100                                                                          
105200     PERFORM 2141-CLEAR-SELECTED-INDS                                     
105300         VARYING ASC-ITEM-SUB                                             
105400         FROM 1 BY 1                                                      
105500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
105600                                                                          
105700     ADD +1                        TO ASC-BLK-SUB.                        
105800                                                                          
105900     PERFORM 2141-CLEAR-SELECTED-INDS                                     
106000         VARYING ASC-ITEM-SUB                                             
106100         FROM 1 BY 1                                                      
106200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
106300                                                                          
106400     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
106500                                   TO PV-LIST-ITEM-NUMBER.                
106600                                                                          
106700     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
106800         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
106900                                                                          
107000     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
107100        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
107200         MOVE +1                   TO ASC-BLK-SUB                         
107300                                                                          
107400         PERFORM 5100-WRITE-TEMP-STORAGE                                  
107500                                                                          
107600         MOVE +1                   TO PV-BLOCK-NUMBER                     
107700                                                                          
107800         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
107900             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
108000                                                                          
108100         PERFORM                                                          
108200             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
108300                                                                          
108400             PERFORM 4410-READ-TEMP-STORAGE                               
108500                                                                          
108600             PERFORM 2141-CLEAR-SELECTED-INDS                             
108700                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
108800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
108900                                                                          
109000             PERFORM 5100-WRITE-TEMP-STORAGE                              
109100                                                                          
109200             ADD +1                TO PV-BLOCK-NUMBER                     
109300                                                                          
109400         END-PERFORM                                                      
109500                                                                          
109600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
109700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
109800                                                                          
109900         PERFORM                                                          
110000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
110100                   ASC-HIGHEST-BLOCK-WRITTEN                              
110200                                                                          
110300             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
110400                                   TO PV-BLOCK-NUMBER                     
110500                                                                          
110600             PERFORM 4410-READ-TEMP-STORAGE                               
110700                                                                          
110800             PERFORM 2141-CLEAR-SELECTED-INDS                             
110900                 VARYING ASC-ITEM-SUB                                     
111000                 FROM 1 BY 1                                              
111100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
111200                                                                          
111300             PERFORM 5100-WRITE-TEMP-STORAGE                              
111400                                                                          
111500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
111600                                                                          
111700         END-PERFORM                                                      
111800                                                                          
111900         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
112000                                   TO PV-BLOCK-NUMBER                     
112100                                                                          
112200         PERFORM 4410-READ-TEMP-STORAGE                                   
112300     END-IF.                                                              
112400                                                                          
112500                                                                          
112600     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
112700     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
112800     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
112900                                                                          
113000     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
113100                                                                          
113200 EJECT                                                                    
113300*----------------------------------------------------------------*        
113400*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
113500*----------------------------------------------------------------*        
113600                                                                          
113700 2141-CLEAR-SELECTED-INDS.                                                
113800                                                                          
113900     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
114000             NOT EQUAL ZERO                                               
114100         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
114200             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
114300                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
114400                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
114500                                   TO TRUE                                
114600         END-IF                                                           
114700     END-IF.                                                              
114800                                                                          
114900 EJECT                                                                    
115000*----------------------------------------------------------------*        
115100*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
115200*  FOR ALL ITEMS.                                                *        
115300*----------------------------------------------------------------*        
115400                                                                          
115500 2150-SELECT-ALL-ITEMS.                                                   
115600                                                                          
115700     MOVE +1                       TO ASC-BLK-SUB.                        
115800                                                                          
115900     PERFORM 2151-SET-SELECTED-INDS                                       
116000         VARYING ASC-ITEM-SUB                                             
116100         FROM 1 BY 1                                                      
116200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
116300                                                                          
116400     ADD +1                        TO ASC-BLK-SUB.                        
116500                                                                          
116600     PERFORM 2151-SET-SELECTED-INDS                                       
116700         VARYING ASC-ITEM-SUB                                             
116800         FROM 1 BY 1                                                      
116900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
117000                                                                          
117100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
117200                                   TO PV-LIST-ITEM-NUMBER.                
117300                                                                          
117400     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
117500         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
117600                                                                          
117700     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
117800        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
117900         MOVE +1                   TO ASC-BLK-SUB                         
118000                                                                          
118100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
118200                                                                          
118300         MOVE +1                   TO PV-BLOCK-NUMBER                     
118400                                                                          
118500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
118600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
118700                                                                          
118800         PERFORM                                                          
118900             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
119000                                                                          
119100             PERFORM 4410-READ-TEMP-STORAGE                               
119200                                                                          
119300             PERFORM 2151-SET-SELECTED-INDS                               
119400                 VARYING ASC-ITEM-SUB                                     
119500                 FROM 1 BY 1                                              
119600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
119700                                                                          
119800             PERFORM 5100-WRITE-TEMP-STORAGE                              
119900                                                                          
120000             ADD +1                TO PV-BLOCK-NUMBER                     
120100                                                                          
120200         END-PERFORM                                                      
120300                                                                          
120400         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
120500             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
120600                                                                          
120700         PERFORM                                                          
120800             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
120900                   ASC-HIGHEST-BLOCK-WRITTEN                              
121000                                                                          
121100             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
121200                                   TO PV-BLOCK-NUMBER                     
121300                                                                          
121400             PERFORM 4410-READ-TEMP-STORAGE                               
121500                                                                          
121600             PERFORM 2151-SET-SELECTED-INDS                               
121700                 VARYING ASC-ITEM-SUB                                     
121800                 FROM 1 BY 1                                              
121900                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
122000                                                                          
122100             PERFORM 5100-WRITE-TEMP-STORAGE                              
122200                                                                          
122300             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
122400                                                                          
122500         END-PERFORM                                                      
122600                                                                          
122700         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
122800                                   TO PV-BLOCK-NUMBER                     
122900                                                                          
123000         PERFORM 4410-READ-TEMP-STORAGE                                   
123100     END-IF.                                                              
123200                                                                          
123300                                                                          
123400     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
123500                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
123600                                                                          
123700     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
123800                                                                          
123900 EJECT                                                                    
124000*----------------------------------------------------------------*        
124100*    MARK EACH ITEM AS BEING SELECTED.                           *        
124200*----------------------------------------------------------------*        
124300                                                                          
124400 2151-SET-SELECTED-INDS.                                                  
124500                                                                          
124600     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
124700             NOT EQUAL ZERO                                               
124800         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
124900             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
125000                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
125100                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
125200                               TO  TRUE                                   
125300         END-IF                                                           
125400     END-IF.                                                              
125500******************************************************************        
125600** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
125700** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
125800******************************************************************        
125900 2200-MOVE-SCREEN-TO-COMMAREA.                                            
126000                                                                          
126100     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
126200     MOVE +1                       TO PV-SUB.                             
126300*                                                                         
126400     PERFORM 3100-SET-SUBSCRIPTS.                                         
126500     IF  ASTRTITL > ZERO                                                  
126600         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
126700     ELSE                                                                 
126800         IF  ASTRTITF = DP015-ERASE-EOF                                   
126900             INITIALIZE ASC-START-ITEM-REQUEST                            
127000         END-IF                                                           
127100     END-IF.                                                              
127200     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
127300         VARYING PV-SUB                                                   
127400         FROM 1 BY 1                                                      
127500         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
127600******************************************************************        
127700** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
127800******************************************************************        
127900 2210-MOVE-LINES-TO-COMMAREA.                                             
128000     IF  MR-SELECTL (PV-SUB) > ZERO                                       
128100         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
128200         MOVE MR-SELECTI (PV-SUB)                                         
128300                                   TO ASC-TEMP-SELECTED-SW                
128400                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
128500     ELSE                                                                 
128600         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
128700                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
128800                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
128900                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
129000         END-IF                                                           
129100     END-IF.                                                              
129200     ADD +1                        TO ASC-ITEM-SUB.                       
129300     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
129400         ADD +1                    TO ASC-BLK-SUB                         
129500         MOVE +1                   TO ASC-ITEM-SUB                        
129600     END-IF.                                                              
129700******************************************************************        
129800** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
129900** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
130000******************************************************************        
130100 3000-EDIT-DATA-IN-COMMAREA.                                              
130200     SET PS-NO-ERROR               TO TRUE.                               
130300     PERFORM 3200-RESET-ATTRIBUTES.                                       
130400     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
130500**                                                                        
130600* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
130700* EDITTING IS DONE FROM THE BOTTOM UP.                                    
130800**                                                                        
130900     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
131000         ASC-ITEMS-DISPLAYED - 1.                                         
131100     PERFORM 3100-SET-SUBSCRIPTS.                                         
131200     PERFORM 3300-EDIT-SELECTIONS                                         
131300         VARYING PV-SUB                                                   
131400         FROM PV-SUB BY -1                                                
131500         UNTIL PV-SUB < +1                                                
131600*                                                                         
131700     IF  PS-NO-ERROR                                                      
131800         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
131900     END-IF.                                                              
132000*                                                                         
132100     IF  ((PS-NO-ERROR)                                                   
132200       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
132300       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
132400             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
132500     END-IF.                                                              
132600 EJECT                                                                    
132700*----------------------------------------------------------------*        
132800*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
132900*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
133000*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
133100*                                                                *        
133200*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
133300*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
133400*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
133500*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
133600*----------------------------------------------------------------*        
133700                                                                          
133800 3100-SET-SUBSCRIPTS.                                                     
133900                                                                          
134000     COMPUTE ASC-ITEM-SUB =                                               
134100         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
134200                                                                          
134300     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
134400         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
134500                                                                          
134600         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
134700     ELSE                                                                 
134800         MOVE +1                     TO ASC-BLK-SUB                       
134900     END-IF.                                                              
135000******************************************************************        
135100** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
135200******************************************************************        
135300                                                                          
135400 3200-RESET-ATTRIBUTES.                                                   
135500     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
135600     MOVE DP015-GREEN              TO ASTRTITC.                           
135700     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
135800*                                                                         
135900     PERFORM                                                              
136000         VARYING PV-SUB                                                   
136100         FROM 1 BY 1                                                      
136200         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
136300         MOVE DP015-UNP-ALP-NOR-OFF                                       
136400                                   TO MR-SELECTA (PV-SUB)                 
136500         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
136600         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
136700     END-PERFORM.                                                         
136800******************************************************************        
136900** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
137000** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
137100** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
137200** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
137300******************************************************************        
137400 3300-EDIT-SELECTIONS.                                                    
137500     EVALUATE TRUE                                                        
137600         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
137700             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
137800                 CONTINUE                                                 
137900         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
138000             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
138100                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
138200                 MOVE ASC-TEMP-SELECTED-SW                                
138300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
138400                                TO ASC-SELECTED-SW                        
138500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
138600             END-IF                                                       
138700         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
138800                 = SPACE                                                  
138900             EVALUATE TRUE                                                
139000                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
139100                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
139200                     MOVE SPACE TO ASC-SELECTED-SW                        
139300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
139400                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
139500                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
139600                     MOVE SPACE TO ASC-SELECTED-SW                        
139700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
139800                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
139900                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
140000                     MOVE SPACE TO ASC-SELECTED-SW                        
140100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
140200             END-EVALUATE                                                 
140300         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
140400             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
140500                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
140600             END-IF                                                       
140700             PERFORM 3310-EDIT-RANGE                                      
140800         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
140900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
141000                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
141100             END-IF                                                       
141200             PERFORM 3310-EDIT-RANGE                                      
141300         WHEN OTHER                                                       
141400             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
141500             SET DP020-MSG-FATAL    TO TRUE                               
141600             MOVE -1                TO MR-SELECTL (PV-SUB)                
141700             SET DP030-SET-CURSOR-APPL-1                                  
141800                                    TO TRUE                               
141900             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
142000             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
142100             SET PS-ERROR TO TRUE                                         
142200             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
142300                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
142400                 MOVE SPACE         TO ASC-SELECTED-SW                    
142500                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
142600             END-IF                                                       
142700     END-EVALUATE.                                                        
142800                                                                          
142900     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
143000     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
143100     IF  ASC-ITEM-SUB < 1                                                 
143200         SUBTRACT +1 FROM ASC-BLK-SUB                                     
143300         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
143400     END-IF.                                                              
143500******************************************************************        
143600** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
143700** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
143800** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
143900** THE ENDING.                                                  **        
144000******************************************************************        
144100 3310-EDIT-RANGE.                                                         
144200     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
144300         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
144400             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
144500                                   TO ASC-SELECTED-SW                     
144600                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
144700             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
144800         ELSE                                                             
144900             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
145000                 CONTINUE                                                 
145100             ELSE                                                         
145200*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
145300                 MOVE PC-TSYMSG-00054                                     
145400                                   TO DP020-MSG-NUMBER                    
145500                 SET DP020-MSG-FATAL                                      
145600                                   TO TRUE                                
145700                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
145800                 SET DP030-SET-CURSOR-APPL-1                              
145900                                   TO TRUE                                
146000                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
146100                 MOVE DP015-UNDERLINE                                     
146200                                   TO MR-SELECTH (PV-SUB)                 
146300                 SET PS-ERROR      TO TRUE                                
146400             END-IF                                                       
146500         END-IF                                                           
146600     ELSE                                                                 
146700         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
146800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
146900                                   TO ASC-SELECTED-SW                     
147000                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
147100             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
147200         ELSE                                                             
147300             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
147400                 CONTINUE                                                 
147500             ELSE                                                         
147600*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
147700                 MOVE PC-TSYMSG-00055                                     
147800                                   TO DP020-MSG-NUMBER                    
147900                 SET DP020-MSG-FATAL                                      
148000                                   TO TRUE                                
148100                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
148200                 SET DP030-SET-CURSOR-APPL-1                              
148300                                   TO TRUE                                
148400                 SET PS-ERROR      TO TRUE                                
148500             END-IF                                                       
148600         END-IF                                                           
148700     END-IF.                                                              
148800*                                                                         
148900******************************************************************        
149000** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
149100** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
149200******************************************************************        
149300 3400-CHECK-RANGE-SEQUENCE.                                               
149400     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
149500       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
149600       AND  ASC-START-OF-SELECTED-RANGE >                                 
149700                                         ASC-END-OF-SELECTED-RANGE        
149800*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
149900         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
150000         SET DP020-MSG-FATAL TO TRUE                                      
150100         SET PS-ERROR TO TRUE                                             
150200         PERFORM 3410-POSITION-AT-ERROR                                   
150300     END-IF.                                                              
150400******************************************************************        
150500** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
150600** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
150700** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
150800******************************************************************        
150900 3410-POSITION-AT-ERROR.                                                  
151000     SET DP030-SET-CURSOR-APPL-1                                          
151100                                   TO TRUE.                               
151200     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
151300                    - PV-TOP-ITEM + 1.                                    
151400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
151500         CONTINUE                                                         
151600     ELSE                                                                 
151700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
151800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
151900         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
152000     END-IF.                                                              
152100*                                                                         
152200     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
152300                    - PV-TOP-ITEM + 1.                                    
152400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
152500         CONTINUE                                                         
152600     ELSE                                                                 
152700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
152800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
152900         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
153000     END-IF.                                                              
153100 EJECT                                                                    
153200*----------------------------------------------------------------*        
153300*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
153400*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
153500*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
153600*----------------------------------------------------------------*        
153700                                                                          
153800 3500-MARK-RANGE-AS-SELECTED.                                             
153900     MOVE +1                       TO  ASC-BLK-SUB.                       
154000     PERFORM 3510-MARK-SELECTED-INDS                                      
154100         VARYING ASC-ITEM-SUB                                             
154200         FROM 1 BY 1                                                      
154300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
154400                                                                          
154500     ADD +1                        TO ASC-BLK-SUB.                        
154600     PERFORM 3510-MARK-SELECTED-INDS                                      
154700         VARYING ASC-ITEM-SUB                                             
154800         FROM 1 BY 1                                                      
154900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
155000                                                                          
155100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
155200                                   TO  PV-LIST-ITEM-NUMBER.               
155300     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
155400         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
155500               PC-ITEMS-PER-PANEL).                                       
155600                                                                          
155700     MOVE ASC-START-OF-SELECTED-RANGE                                     
155800                                   TO PV-START-OF-SELECTED-RANGE.         
155900     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
156000         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
156100               PC-ITEMS-PER-PANEL).                                       
156200                                                                          
156300     MOVE ASC-END-OF-SELECTED-RANGE                                       
156400                                   TO  PV-END-OF-SELECTED-RANGE.          
156500     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
156600         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
156700               PC-ITEMS-PER-PANEL).                                       
156800     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
156900         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
157000                                                                          
157100     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
157200        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
157300         MOVE +1                   TO ASC-BLK-SUB                         
157400         PERFORM 5100-WRITE-TEMP-STORAGE                                  
157500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
157600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
157700                                                                          
157800         PERFORM                                                          
157900             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
158000                   PV-PREV-BLOCK-IN-ARRAY                                 
158100             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
158200                                   TO PV-BLOCK-NUMBER                     
158300             PERFORM 4410-READ-TEMP-STORAGE                               
158400             PERFORM 3510-MARK-SELECTED-INDS                              
158500                 VARYING ASC-ITEM-SUB                                     
158600                 FROM 1 BY 1                                              
158700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
158800             PERFORM 5100-WRITE-TEMP-STORAGE                              
158900             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
159000         END-PERFORM                                                      
159100                                                                          
159200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
159300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
159400         PERFORM                                                          
159500             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
159600                   PV-LAST-BLOCK-IN-RANGE                                 
159700             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
159800                                   TO PV-BLOCK-NUMBER                     
159900             PERFORM 4410-READ-TEMP-STORAGE                               
160000             PERFORM 3510-MARK-SELECTED-INDS                              
160100                 VARYING ASC-ITEM-SUB                                     
160200                 FROM 1 BY 1                                              
160300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
160400             PERFORM 5100-WRITE-TEMP-STORAGE                              
160500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
160600         END-PERFORM                                                      
160700                                                                          
160800         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
160900                                   TO PV-BLOCK-NUMBER                     
161000         PERFORM 4410-READ-TEMP-STORAGE                                   
161100     END-IF.                                                              
161200                                                                          
161300     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
161400                                      ASC-END-OF-SELECTED-RANGE.          
161500                                                                          
161600 EJECT                                                                    
161700*----------------------------------------------------------------*        
161800* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
161900*----------------------------------------------------------------*        
162000                                                                          
162100 3510-MARK-SELECTED-INDS.                                                 
162200                                                                          
162300     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
162400                                ASC-ITEM-SUB) >                           
162500         ASC-START-OF-SELECTED-RANGE)                                     
162600     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
162700                                ASC-ITEM-SUB) =                           
162800         ASC-START-OF-SELECTED-RANGE)                                     
162900                                                                          
163000         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
163100                                    ASC-ITEM-SUB) <                       
163200             ASC-END-OF-SELECTED-RANGE)                                   
163300         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
163400                                    ASC-ITEM-SUB) =                       
163500             ASC-END-OF-SELECTED-RANGE)                                   
163600                                                                          
163700             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
163800                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
163900                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
164000                               TO TRUE                                    
164100                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
164200                               TO TRUE                                    
164300                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
164400             END-IF                                                       
164500         END-IF                                                           
164600     END-IF.                                                              
164700                                                                          
164800 EJECT                                                                    
164900*----------------------------------------------------------------*        
165000*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
165100*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
165200*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
165300*    DISPLAY THE PANEL TO USER.                                  *        
165400*----------------------------------------------------------------*        
165500                                                                          
165600 4000-BUILD-INITIAL-PANEL.                                                
165700                                                                          
165800     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
165900                                 ASC-BLOCK-ENTRIES (2).                   
166000     MOVE +1                 TO ASC-BLK-SUB.                              
166100     SET ASC-ITEMS-LEFT-ON-DB                                             
166200                             TO TRUE.                                     
166300                                                                          
166400     MOVE +0                 TO ASC-ITEM-SUB                              
166500                                ASC-NUMBER-OF-ITEMS-READ                  
166600                                ASC-NUMBER-OF-SELECTED-ITEMS              
166700                                ASC-HIGHEST-BLOCK-WRITTEN                 
166800                                ASC-START-OF-SELECTED-RANGE               
166900                                ASC-END-OF-SELECTED-RANGE                 
167000                                ASC-ITEM-AT-TOP-OF-PANEL.                 
167100                                                                          
167200     EVALUATE IQ016-SUB-CL-NM                                             
167300         WHEN 'CLASS SCREEN BYPASSD'                                      
167400              IF IQ016-LBL-NM = SPACES                                    
167700                 SET PS-CNT-NOVND-NOCLS-COLOR TO TRUE                     
167710                 MOVE 'CNT-NOVND-NOCL-CLR' TO PV-CURSOR-NAME              
167800                 PERFORM 4011-OPEN-COUNT-CURSOR                           
167900                 PERFORM 4340-FETCH-SKU-COUNTS                            
168000                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
168020                                                                          
168030                 SET PS-CSR-NOVND-NOCLS-COLOR TO TRUE                     
168040                 MOVE 'NOVND-NOCLS-COLOR ' TO PV-CURSOR-NAME              
168100              ELSE                                                        
168400                 SET PS-CNT-VENDOR-NOCLS-COLOR TO TRUE                    
168410                 MOVE 'CNT-VND-NOCLS-CLR ' TO PV-CURSOR-NAME              
168500                 PERFORM 4011-OPEN-COUNT-CURSOR                           
168600                 PERFORM 4340-FETCH-SKU-COUNTS                            
168700                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
168800                                                                          
168900                 SET PS-CNT-LABEL-NOCLS-COLOR TO TRUE                     
168910                 MOVE 'CNT-LBL-NOCLS-CLR ' TO PV-CURSOR-NAME              
169000                 PERFORM 4011-OPEN-COUNT-CURSOR                           
169100                 PERFORM 4340-FETCH-SKU-COUNTS                            
169200                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
169220                                                                          
169230                 SET PS-CSR-VENDOR-NOCLS-COLOR TO TRUE                    
169240                 MOVE 'VENDOR-NOCLS-COLOR' TO PV-CURSOR-NAME              
169300              END-IF                                                      
169400                                                                          
169500         WHEN 'STYLE SCREEN USED   '                                      
169600              IF IQ016-LBL-NM = SPACES                                    
169900                 SET PS-CNT-NOVND-STYLE-COLOR TO TRUE                     
169910                 MOVE 'CNT-NOVND-STY-CLR ' TO PV-CURSOR-NAME              
170000                 PERFORM 4011-OPEN-COUNT-CURSOR                           
170100                 PERFORM 4340-FETCH-SKU-COUNTS                            
170200                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
170220                                                                          
170230                 SET PS-CSR-NOVND-STYLE-COLOR TO TRUE                     
170240                 MOVE 'NOVND-STYLE-COLOR ' TO PV-CURSOR-NAME              
170300              ELSE                                                        
170600                 SET PS-CNT-VENDOR-STYLE-COLOR TO TRUE                    
170610                 MOVE 'CNT-VND-STYLE-CLR ' TO PV-CURSOR-NAME              
170700                 PERFORM 4011-OPEN-COUNT-CURSOR                           
170800                 PERFORM 4340-FETCH-SKU-COUNTS                            
170900                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
171000                                                                          
171100                 SET PS-CNT-LABEL-STYLE-COLOR TO TRUE                     
171110                 MOVE 'CNT-LBL-STYLE-CLR ' TO PV-CURSOR-NAME              
171200                 PERFORM 4011-OPEN-COUNT-CURSOR                           
171300                 PERFORM 4340-FETCH-SKU-COUNTS                            
171400                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
171420                                                                          
171430                 SET PS-CSR-VENDOR-STYLE-COLOR TO TRUE                    
171440                 MOVE 'VENDOR-STYLE-COLOR' TO PV-CURSOR-NAME              
171500              END-IF                                                      
171600                                                                          
171700         WHEN OTHER                                                       
171800              IF IQ016-LBL-NM = SPACES                                    
172100                 SET PS-CNT-NOVND-CLASS-COLOR TO TRUE                     
172110                 MOVE 'CNT-NOVND-CLS-CLR ' TO PV-CURSOR-NAME              
172200                 PERFORM 4011-OPEN-COUNT-CURSOR                           
172300                 PERFORM 4340-FETCH-SKU-COUNTS                            
172400                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
172420                                                                          
172430                 SET PS-CSR-NOVND-CLASS-COLOR TO TRUE                     
172440                 MOVE 'NOVND-CLASS-COLOR ' TO PV-CURSOR-NAME              
172500              ELSE                                                        
172800                 SET PS-CNT-VENDOR-CLASS-COLOR TO TRUE                    
172810                 MOVE 'CNT-VND-CLS-CLR   ' TO PV-CURSOR-NAME              
172900                 PERFORM 4011-OPEN-COUNT-CURSOR                           
173000                 PERFORM 4340-FETCH-SKU-COUNTS                            
173100                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
173200                                                                          
173300                 SET PS-CNT-LABEL-CLASS-COLOR TO TRUE                     
173310                 MOVE 'CNT-LBL-CLS-CLR   ' TO PV-CURSOR-NAME              
173400                 PERFORM 4011-OPEN-COUNT-CURSOR                           
173500                 PERFORM 4340-FETCH-SKU-COUNTS                            
173600                 PERFORM 4021-CLOSE-COUNT-CURSOR                          
173620                                                                          
173630                 SET PS-CSR-VENDOR-CLASS-COLOR TO TRUE                    
173640                 MOVE 'VENDOR-CLASS-COLOR' TO PV-CURSOR-NAME              
173700              END-IF                                                      
173800                                                                          
173900     END-EVALUATE.                                                        
174000                                                                          
174100     PERFORM 4010-OPEN-DATA-CURSOR.                                       
174200                                                                          
174300     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
174400                                                                          
174500     PERFORM 4020-CLOSE-DATA-CURSOR.                                      
174600                                                                          
174700     MOVE +1                   TO  PV-TOP-ITEM.                           
174800     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
174900         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
175000         SET DP020-MSG-FATAL   TO  TRUE                                   
175100         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
175200     ELSE                                                                 
175300         MOVE -1               TO MR-SELECTL (1)                          
175400         SET DP030-SET-CURSOR-APPL-1                                      
175500                               TO TRUE                                    
175600         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
175700     END-IF.                                                              
175800                                                                          
175900*--------------------------------------------------------------*          
176000*    OPEN THE DATA CURSOR USED TO DISPLAY THE INFORMATION.     *          
176200*--------------------------------------------------------------*          
176300 4010-OPEN-DATA-CURSOR.                                                   
176400                                                                          
176500     MOVE '4010-OPEN-DATA-CURSOR'  TO DP013-PARAGRAPH.                    
176600                                                                          
176700     PERFORM                                                              
176800         WITH TEST AFTER                                                  
176900         VARYING PV-RETRY-COUNTER                                         
177000         FROM 1 BY 1                                                      
177100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
177200                   OR (SQLCODE = ZERO))                                   
177300                                                                          
177400         EVALUATE TRUE                                                    
177500            WHEN PS-CSR-VENDOR-CLASS-COLOR                                
177600                 EXEC SQL                                                 
177700                      OPEN VENDOR-CLASS-COLOR                             
177800                 END-EXEC                                                 
177900                                                                          
178000            WHEN PS-CSR-VENDOR-NOCLS-COLOR                                
178100                 EXEC SQL                                                 
178200                      OPEN VENDOR-NOCLS-COLOR                             
178300                 END-EXEC                                                 
178400                                                                          
178500            WHEN PS-CSR-VENDOR-STYLE-COLOR                                
178600                 EXEC SQL                                                 
178700                      OPEN VENDOR-STYLE-COLOR                             
178800                 END-EXEC                                                 
178900                                                                          
179000            WHEN PS-CSR-NOVND-CLASS-COLOR                                 
179100                 EXEC SQL                                                 
179200                      OPEN NOVND-CLASS-COLOR                              
179300                 END-EXEC                                                 
179400                                                                          
179500            WHEN PS-CSR-NOVND-NOCLS-COLOR                                 
179600                 EXEC SQL                                                 
179700                      OPEN NOVND-NOCLS-COLOR                              
179800                 END-EXEC                                                 
179900                                                                          
180000            WHEN PS-CSR-NOVND-STYLE-COLOR                                 
180100                 EXEC SQL                                                 
180200                      OPEN NOVND-STYLE-COLOR                              
180300                 END-EXEC                                                 
180400                                                                          
180500         END-EVALUATE                                                     
180600                                                                          
180700         EVALUATE TRUE                                                    
180800           WHEN SQLCODE = ZERO                                            
180900           WHEN SQLCODE = -904                                            
181000           WHEN SQLCODE = -913                                            
181100               CONTINUE                                                   
181200           WHEN SQLWARN0 NOT = SPACES                                     
181300           WHEN SQLCODE < ZERO                                            
181400           WHEN SQLCODE > ZERO                                            
181500               STRING 'OPEN '                                             
181600                       PV-CURSOR-NAME                                     
181700                      ' CURSOR'                                           
181800                       DELIMITED BY SIZE                                  
181900                       INTO DP013-MESSAGE-TEXT (1)                        
182000               MOVE SQLCA        TO DP013-SQLCA                           
182100               MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)                
182200               MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)                
182300              SET DP013-DB2-ABEND                                         
182400                   DP013-XCTL-DISPLAY-RESTART TO TRUE                     
182500               PERFORM DP013-0000-PROCESS-ABEND                           
182600         END-EVALUATE                                                     
182700     END-PERFORM.                                                         
182800                                                                          
182900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
183110         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
183120                 PV-CURSOR-NAME                                           
183130                ' CURSOR'                                                 
183140                 DELIMITED BY SIZE                                        
183150                 INTO DP013-MESSAGE-TEXT (1)                              
183200         MOVE SQLCA                TO DP013-SQLCA                         
183300         MOVE 'SKU, VND'           TO DP013-DB2-TABLE-NAME (1)            
183400         MOVE 'VND_STYL'           TO DP013-DB2-TABLE-NAME (2)            
183500         SET DP013-DB2-ABEND                                              
183600             DP013-XCTL-DISPLAY-RESTART                                   
183700                                   TO TRUE                                
183800         PERFORM DP013-0000-PROCESS-ABEND                                 
183900     END-IF.                                                              
184000                                                                          
184100                                                                          
184200*----------------------------------------------------------------*        
184300* OPEN THE CURSORS DEPENDING ON THE CRITERIA SPECIFIED BY THE             
184400*   USER.                                                        *        
184500*----------------------------------------------------------------*        
184600 4011-OPEN-COUNT-CURSOR.                                                  
184700                                                                          
184800     MOVE '4011-OPEN-COUNT-CURSOR'  TO DP013-PARAGRAPH.                   
184900                                                                          
185000     PERFORM                                                              
185100         WITH TEST AFTER                                                  
185200         VARYING PV-RETRY-COUNTER                                         
185300         FROM 1 BY 1                                                      
185400         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
185500                   OR (SQLCODE = ZERO))                                   
185600                                                                          
185700         EVALUATE TRUE                                                    
185800            WHEN PS-CNT-VENDOR-CLASS-COLOR                                
185900                 EXEC SQL                                                 
186000                      OPEN CNT-VND-CLS-CLR                                
186100                 END-EXEC                                                 
186200                                                                          
186300            WHEN PS-CNT-LABEL-CLASS-COLOR                                 
186400                 EXEC SQL                                                 
186500                      OPEN CNT-LBL-CLS-CLR                                
186600                 END-EXEC                                                 
186700                                                                          
186800            WHEN PS-CNT-VENDOR-NOCLS-COLOR                                
186900                 EXEC SQL                                                 
187000                      OPEN CNT-VND-NOCLS-CLR                              
187100                 END-EXEC                                                 
187200                                                                          
187300            WHEN PS-CNT-LABEL-NOCLS-COLOR                                 
187400                 EXEC SQL                                                 
187500                      OPEN CNT-LBL-NOCLS-CLR                              
187600                 END-EXEC                                                 
187700                                                                          
187800            WHEN PS-CNT-VENDOR-STYLE-COLOR                                
187900                 EXEC SQL                                                 
188000                      OPEN CNT-VND-STYLE-CLR                              
188100                 END-EXEC                                                 
188200                                                                          
188300            WHEN PS-CNT-LABEL-STYLE-COLOR                                 
188400                 EXEC SQL                                                 
188500                      OPEN CNT-LBL-STYLE-CLR                              
188600                 END-EXEC                                                 
188700                                                                          
188800            WHEN PS-CNT-NOVND-CLASS-COLOR                                 
188900                 EXEC SQL                                                 
189000                      OPEN CNT-NOVND-CLS-CLR                              
189100                 END-EXEC                                                 
189200                                                                          
189300            WHEN PS-CNT-NOVND-NOCLS-COLOR                                 
189400                 EXEC SQL                                                 
189500                      OPEN CNT-NOVND-NOCL-CLR                             
189600                 END-EXEC                                                 
189700                                                                          
189800            WHEN PS-CNT-NOVND-STYLE-COLOR                                 
189900                 EXEC SQL                                                 
190000                      OPEN CNT-NOVND-STY-CLR                              
190100                 END-EXEC                                                 
190200                                                                          
190300         END-EVALUATE                                                     
190400                                                                          
190500         EVALUATE TRUE                                                    
190600           WHEN SQLCODE = ZERO                                            
190700           WHEN SQLCODE = -904                                            
190800           WHEN SQLCODE = -913                                            
190900               CONTINUE                                                   
191000           WHEN SQLWARN0 NOT = SPACES                                     
191100           WHEN SQLCODE < ZERO                                            
191200           WHEN SQLCODE > ZERO                                            
191300               STRING 'OPEN '                                             
191400                       PV-CURSOR-NAME                                     
191500                      ' CURSOR'                                           
191600                       DELIMITED BY SIZE                                  
191700                       INTO DP013-MESSAGE-TEXT (1)                        
191800               MOVE SQLCA        TO DP013-SQLCA                           
191900               MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)                
192000               MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)                
192100              SET DP013-DB2-ABEND                                         
192200                   DP013-XCTL-DISPLAY-RESTART TO TRUE                     
192300               PERFORM DP013-0000-PROCESS-ABEND                           
192400         END-EVALUATE                                                     
192500     END-PERFORM.                                                         
192600                                                                          
192700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
192910         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
192920                 PV-CURSOR-NAME                                           
192930                ' CURSOR'                                                 
192940                 DELIMITED BY SIZE                                        
192950                 INTO DP013-MESSAGE-TEXT (1)                              
193000         MOVE SQLCA                TO DP013-SQLCA                         
193100         MOVE 'SKU, VND'           TO DP013-DB2-TABLE-NAME (1)            
193200         MOVE 'VND_STYL'           TO DP013-DB2-TABLE-NAME (2)            
193300         SET DP013-DB2-ABEND                                              
193400             DP013-XCTL-DISPLAY-RESTART                                   
193500                                   TO TRUE                                
193600         PERFORM DP013-0000-PROCESS-ABEND                                 
193700     END-IF.                                                              
193800                                                                          
193900                                                                          
194000*----------------------------------------------------------------*        
194100* CLOSE THE DATA CURSORS                                                  
194200*----------------------------------------------------------------*        
194300                                                                          
194400 4020-CLOSE-DATA-CURSOR.                                                  
194500                                                                          
194600     MOVE '4020-CLOSE-DATA-CURSOR' TO DP013-PARAGRAPH.                    
194700                                                                          
194800     EVALUATE TRUE                                                        
194900        WHEN PS-CSR-VENDOR-CLASS-COLOR                                    
195000             EXEC SQL                                                     
195100                  CLOSE VENDOR-CLASS-COLOR                                
195200             END-EXEC                                                     
195300                                                                          
195400        WHEN PS-CSR-VENDOR-NOCLS-COLOR                                    
195500             EXEC SQL                                                     
195600                  CLOSE VENDOR-NOCLS-COLOR                                
195700             END-EXEC                                                     
195800                                                                          
195900        WHEN PS-CSR-VENDOR-STYLE-COLOR                                    
196000             EXEC SQL                                                     
196100                  CLOSE VENDOR-STYLE-COLOR                                
196200             END-EXEC                                                     
196300                                                                          
196400        WHEN PS-CSR-NOVND-CLASS-COLOR                                     
196500             EXEC SQL                                                     
196600                  CLOSE NOVND-CLASS-COLOR                                 
196700             END-EXEC                                                     
196800                                                                          
196900        WHEN PS-CSR-NOVND-NOCLS-COLOR                                     
197000             EXEC SQL                                                     
197100                  CLOSE NOVND-NOCLS-COLOR                                 
197200             END-EXEC                                                     
197300                                                                          
197400        WHEN PS-CSR-NOVND-STYLE-COLOR                                     
197500             EXEC SQL                                                     
197600                  CLOSE NOVND-STYLE-COLOR                                 
197700             END-EXEC                                                     
197800                                                                          
197900     END-EVALUATE                                                         
198000                                                                          
198100     EVALUATE TRUE                                                        
198200         WHEN SQLCODE = ZERO                                              
198300             CONTINUE                                                     
198400         WHEN SQLWARN0 NOT = SPACES                                       
198500         WHEN SQLCODE < ZERO                                              
198600         WHEN SQLCODE > ZERO                                              
198810             STRING 'CLOSE '                                              
198820                     PV-CURSOR-NAME                                       
198830                    ' CURSOR'                                             
198840                     DELIMITED BY SIZE                                    
198850                     INTO DP013-MESSAGE-TEXT (1)                          
198900             MOVE SQLCA        TO DP013-SQLCA                             
199000             MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)                  
199100             MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)                  
199200             SET DP013-DB2-ABEND                                          
199300                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
199400             PERFORM DP013-0000-PROCESS-ABEND                             
199500     END-EVALUATE.                                                        
199600                                                                          
199700*----------------------------------------------------------------*        
199800* CLOSE THE DATA CURSORS                                                  
199900*----------------------------------------------------------------*        
200000                                                                          
200100 4021-CLOSE-COUNT-CURSOR.                                                 
200200                                                                          
200300     MOVE '4021-CLOSE-COUNT-CURSOR' TO DP013-PARAGRAPH.                   
200400                                                                          
200500     EVALUATE TRUE                                                        
200600         WHEN PS-CNT-VENDOR-CLASS-COLOR                                   
200700              EXEC SQL                                                    
200800                   CLOSE CNT-VND-CLS-CLR                                  
200900              END-EXEC                                                    
201000                                                                          
201100         WHEN PS-CNT-LABEL-CLASS-COLOR                                    
201200              EXEC SQL                                                    
201300                   CLOSE CNT-LBL-CLS-CLR                                  
201400              END-EXEC                                                    
201500                                                                          
201600         WHEN PS-CNT-VENDOR-NOCLS-COLOR                                   
201700              EXEC SQL                                                    
201800                   CLOSE CNT-VND-NOCLS-CLR                                
201900              END-EXEC                                                    
202000                                                                          
202100         WHEN PS-CNT-LABEL-NOCLS-COLOR                                    
202200              EXEC SQL                                                    
202300                   CLOSE CNT-LBL-NOCLS-CLR                                
202400              END-EXEC                                                    
202500                                                                          
202600         WHEN PS-CNT-VENDOR-STYLE-COLOR                                   
202700              EXEC SQL                                                    
202800                   CLOSE CNT-VND-STYLE-CLR                                
202900              END-EXEC                                                    
203000                                                                          
203100         WHEN PS-CNT-LABEL-STYLE-COLOR                                    
203200              EXEC SQL                                                    
203300                   CLOSE CNT-LBL-STYLE-CLR                                
203400              END-EXEC                                                    
203500                                                                          
203600         WHEN PS-CNT-NOVND-CLASS-COLOR                                    
203700              EXEC SQL                                                    
203800                   CLOSE CNT-NOVND-CLS-CLR                                
203900              END-EXEC                                                    
204000                                                                          
204100         WHEN PS-CNT-NOVND-NOCLS-COLOR                                    
204200              EXEC SQL                                                    
204300                   CLOSE CNT-NOVND-NOCL-CLR                               
204400              END-EXEC                                                    
204500                                                                          
204600         WHEN PS-CNT-NOVND-STYLE-COLOR                                    
204700              EXEC SQL                                                    
204800                   CLOSE CNT-NOVND-STY-CLR                                
204900              END-EXEC                                                    
205000                                                                          
205100     END-EVALUATE.                                                        
205200                                                                          
205300     EVALUATE TRUE                                                        
205400         WHEN SQLCODE = ZERO                                              
205500             CONTINUE                                                     
205600         WHEN SQLWARN0 NOT = SPACES                                       
205700         WHEN SQLCODE < ZERO                                              
205800         WHEN SQLCODE > ZERO                                              
206010             STRING 'CLOSE '                                              
206020                     PV-CURSOR-NAME                                       
206030                    ' CURSOR'                                             
206040                     DELIMITED BY SIZE                                    
206050                     INTO DP013-MESSAGE-TEXT (1)                          
206100             MOVE SQLCA        TO DP013-SQLCA                             
206200             MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)                  
206300             MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)                  
206400             SET DP013-DB2-ABEND                                          
206500                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
206600             PERFORM DP013-0000-PROCESS-ABEND                             
206700                                                                          
206800     END-EVALUATE.                                                        
206900                                                                          
207000*----------------------------------------------------------------*        
207100*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
207200*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
207300*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
207400*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
207500*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
207600*                                                                *        
207700*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
207800*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
207900*    UPON INITIALIZATION.                                        *        
208000*----------------------------------------------------------------*        
208100                                                                          
208200 4300-READ-ITEMS-FROM-DB.                                                 
208300                                                                          
208400     PERFORM                                                              
208500         VARYING PV-READ-COUNT                                            
208600         FROM 1 BY 1                                                      
208700         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
208800            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
208900                                                                          
209000         PERFORM 4310-FETCH-SIZES                                         
209100                                                                          
209200         IF  ASC-ITEMS-LEFT-ON-DB                                         
209300             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
209400                 ADD +1            TO ASC-ITEM-SUB                        
209500             ELSE                                                         
209600                 ADD +1            TO ASC-BLK-SUB                         
209700                 MOVE +1           TO ASC-ITEM-SUB                        
209800                                                                          
209900                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
210000                     MOVE +1       TO ASC-BLK-SUB                         
210100                     SET ASC-BLOCK-MODIFIED (1)                           
210200                                   TO TRUE                                
210300                     PERFORM 5100-WRITE-TEMP-STORAGE                      
210400                     MOVE ASC-BLOCK-ENTRIES                               
210500                               (PC-MAX-BLOCKS-IN-ARRAY)                   
210600                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
210700                     ADD +1        TO ASC-BLK-SUB                         
210800                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
210900                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
211000                                   TO TRUE                                
211100                 END-IF                                                   
211200             END-IF                                                       
211300             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
211400             MOVE ASC-NUMBER-OF-ITEMS-READ                                
211500                                   TO ASC-LIST-ITEM-NUMBER                
211600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
211700             MOVE XST00010-SIZE-DESC  TO ASC-SIZE-DESC                    
211800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
211900         END-IF                                                           
212000     END-PERFORM.                                                         
212100                                                                          
212200     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
212300         MOVE +1               TO ASC-BLK-SUB                             
212400         PERFORM 5100-WRITE-TEMP-STORAGE                                  
212500         ADD +1                TO ASC-BLK-SUB                             
212600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
212700                                                                          
212800         MOVE 1                TO ASC-BLK-SUB                             
212900                                  PV-BLOCK-NUMBER                         
213000         PERFORM 4410-READ-TEMP-STORAGE                                   
213100         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
213200             MOVE 2            TO ASC-BLK-SUB                             
213300                                  PV-BLOCK-NUMBER                         
213400             PERFORM 4410-READ-TEMP-STORAGE                               
213500         END-IF                                                           
213600     END-IF.                                                              
213700                                                                          
213800*----------------------------------------------------------------*        
213900*    FETCH THE DATA CURSOR USED TO DISPLAY THE DATA.                      
214000*----------------------------------------------------------------*        
214100                                                                          
214200 4310-FETCH-SIZES.                                                        
214201                                                                          
214220     MOVE '4310-FETCH-SIZES' TO DP013-PARAGRAPH.                          
214300                                                                          
214310     PERFORM                                                              
214320         WITH TEST AFTER                                                  
214330         VARYING PV-RETRY-COUNTER                                         
214340         FROM 1 BY 1                                                      
214350         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
214360                   OR  SQLCODE = ZERO                                     
214370                   OR  SQLCODE = +100)                                    
214380                                                                          
214400         EVALUATE TRUE                                                    
214500            WHEN PS-CSR-VENDOR-CLASS-COLOR                                
214600                 EXEC SQL                                                 
214700                     FETCH VENDOR-CLASS-COLOR                             
214800                           INTO :XST00010-SIZE-DESC                       
214900                 END-EXEC                                                 
215000                                                                          
215100            WHEN PS-CSR-VENDOR-NOCLS-COLOR                                
215200                 EXEC SQL                                                 
215300                     FETCH VENDOR-NOCLS-COLOR                             
215400                           INTO :XST00010-SIZE-DESC                       
215500                 END-EXEC                                                 
215600                                                                          
215700            WHEN PS-CSR-VENDOR-STYLE-COLOR                                
215800                 EXEC SQL                                                 
215900                     FETCH VENDOR-STYLE-COLOR                             
216000                           INTO :XST00010-SIZE-DESC                       
216100                 END-EXEC                                                 
216200                                                                          
216300            WHEN PS-CSR-NOVND-CLASS-COLOR                                 
216400                 EXEC SQL                                                 
216500                     FETCH NOVND-CLASS-COLOR                              
216600                           INTO :XST00010-SIZE-DESC                       
216700                 END-EXEC                                                 
216800                                                                          
216900            WHEN PS-CSR-NOVND-NOCLS-COLOR                                 
217000                 EXEC SQL                                                 
217100                     FETCH NOVND-NOCLS-COLOR                              
217200                           INTO :XST00010-SIZE-DESC                       
217300                 END-EXEC                                                 
217400                                                                          
217500            WHEN PS-CSR-NOVND-STYLE-COLOR                                 
217600                 EXEC SQL                                                 
217700                     FETCH NOVND-STYLE-COLOR                              
217800                           INTO :XST00010-SIZE-DESC                       
217900                 END-EXEC                                                 
218000                                                                          
218100         END-EVALUATE                                                     
218200                                                                          
218300         EVALUATE TRUE                                                    
218400             WHEN SQLCODE = ZERO                                          
218500             WHEN SQLCODE = +100                                          
218600                  CONTINUE                                                
218700             WHEN SQLWARN0 NOT = SPACE                                    
218800             WHEN SQLCODE NOT = ZERO                                      
219210                  STRING 'FETCH '                                         
219220                          PV-CURSOR-NAME                                  
219230                         ' CURSOR'                                        
219240                          DELIMITED BY SIZE                               
219250                          INTO DP013-MESSAGE-TEXT (1)                     
219300                  MOVE SQLCA       TO DP013-SQLCA                         
219400                  SET DP013-DB2-ABEND                                     
219500                                   TO TRUE                                
219600                  PERFORM DP013-0000-PROCESS-ABEND                        
219700         END-EVALUATE                                                     
219710                                                                          
219720     END-PERFORM.                                                         
219730                                                                          
219740     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
219771         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
219772                 PV-CURSOR-NAME                                           
219773                ' CURSOR'                                                 
219774                 DELIMITED BY SIZE                                        
219775                 INTO DP013-MESSAGE-TEXT (1)                              
219780         MOVE SQLCA                TO DP013-SQLCA                         
219791         SET DP013-DB2-ABEND                                              
219792             DP013-XCTL-DISPLAY-RESTART                                   
219793                                   TO TRUE                                
219794         PERFORM DP013-0000-PROCESS-ABEND                                 
219795     END-IF.                                                              
219796                                                                          
219800                                                                          
219900     IF  SQLCODE EQUAL +100                                               
220000         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
220100                                 TO TRUE                                  
220200     END-IF.                                                              
220300                                                                          
220400                                                                          
220500*-----------------------------------------------------                    
220600*    FETCH ANY OF THE SKU COUNTS.                                         
220700*-----------------------------------------------------                    
220800                                                                          
220900 4340-FETCH-SKU-COUNTS.                                                   
221000                                                                          
221020     MOVE '4340-FETCH-SKU-COUNTS' TO DP013-PARAGRAPH.                     
221030                                                                          
221040     PERFORM                                                              
221050         WITH TEST AFTER                                                  
221060         VARYING PV-RETRY-COUNTER                                         
221070         FROM 1 BY 1                                                      
221080         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
221090                   OR  SQLCODE = ZERO                                     
221091                   OR  SQLCODE = +100)                                    
221092                                                                          
221100         EVALUATE TRUE                                                    
221200             WHEN PS-CNT-VENDOR-CLASS-COLOR                               
221300                  EXEC SQL                                                
221400                       FETCH CNT-VND-CLS-CLR                              
221500                             INTO  :PV-SKU-CNT                            
221600                  END-EXEC                                                
221700                                                                          
221800             WHEN PS-CNT-LABEL-CLASS-COLOR                                
221900                  EXEC SQL                                                
222000                       FETCH CNT-LBL-CLS-CLR                              
222100                             INTO  :PV-SKU-CNT                            
222200                  END-EXEC                                                
222300                                                                          
222400             WHEN PS-CNT-VENDOR-NOCLS-COLOR                               
222500                  EXEC SQL                                                
222600                       FETCH CNT-VND-NOCLS-CLR                            
222700                             INTO  :PV-SKU-CNT                            
222800                  END-EXEC                                                
222900                                                                          
223000             WHEN PS-CNT-LABEL-NOCLS-COLOR                                
223100                  EXEC SQL                                                
223200                       FETCH CNT-LBL-NOCLS-CLR                            
223300                             INTO  :PV-SKU-CNT                            
223400                  END-EXEC                                                
223500                                                                          
223600             WHEN PS-CNT-VENDOR-STYLE-COLOR                               
223700                  EXEC SQL                                                
223800                       FETCH CNT-VND-STYLE-CLR                            
223900                             INTO  :PV-SKU-CNT                            
224000                  END-EXEC                                                
224100                                                                          
224200             WHEN PS-CNT-LABEL-STYLE-COLOR                                
224300                  EXEC SQL                                                
224400                       FETCH CNT-LBL-STYLE-CLR                            
224500                             INTO  :PV-SKU-CNT                            
224600                  END-EXEC                                                
224700                                                                          
224800             WHEN PS-CNT-NOVND-CLASS-COLOR                                
224900                  EXEC SQL                                                
225000                       FETCH CNT-NOVND-CLS-CLR                            
225100                             INTO  :PV-SKU-CNT                            
225200                  END-EXEC                                                
225300                                                                          
225400             WHEN PS-CNT-NOVND-NOCLS-COLOR                                
225500                  EXEC SQL                                                
225600                       FETCH CNT-NOVND-NOCL-CLR                           
225700                             INTO  :PV-SKU-CNT                            
225800                  END-EXEC                                                
225900                                                                          
226000             WHEN PS-CNT-NOVND-STYLE-COLOR                                
226100                  EXEC SQL                                                
226200                       FETCH CNT-NOVND-STY-CLR                            
226300                             INTO  :PV-SKU-CNT                            
226400                  END-EXEC                                                
226500                                                                          
226600         END-EVALUATE                                                     
226700                                                                          
226800         EVALUATE TRUE                                                    
226900             WHEN SQLCODE = ZERO                                          
227000             WHEN SQLCODE = +100                                          
227100                  MOVE PV-SKU-CNT TO ASC-SKU-CNT                          
227200                  MOVE ZEROES     TO PV-SKU-CNT                           
227300             WHEN SQLWARN0 NOT = SPACE                                    
227400             WHEN SQLCODE NOT = ZERO                                      
227500                  MOVE '4340-FETCH-SKU-COUNTS'                            
227600                                   TO DP013-PARAGRAPH                     
227810                  STRING 'FETCH '                                         
227820                          PV-CURSOR-NAME                                  
227830                         ' CURSOR'                                        
227840                          DELIMITED BY SIZE                               
227850                          INTO DP013-MESSAGE-TEXT (1)                     
227900                  MOVE SQLCA       TO DP013-SQLCA                         
228000                  SET DP013-DB2-ABEND                                     
228100                                   TO TRUE                                
228200                  PERFORM DP013-0000-PROCESS-ABEND                        
228300         END-EVALUATE                                                     
228400                                                                          
228500     END-PERFORM.                                                         
228510                                                                          
228520     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
228530         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
228540                 PV-CURSOR-NAME                                           
228550                ' CURSOR'                                                 
228560                 DELIMITED BY SIZE                                        
228570                 INTO DP013-MESSAGE-TEXT (1)                              
228580         MOVE SQLCA                TO DP013-SQLCA                         
228590         SET DP013-DB2-ABEND                                              
228591             DP013-XCTL-DISPLAY-RESTART                                   
228592                                   TO TRUE                                
228593         PERFORM DP013-0000-PROCESS-ABEND                                 
228594     END-IF.                                                              
228600                                                                          
228700                                                                          
228800*----------------------------------------------------------------*        
228900* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
229000*----------------------------------------------------------------*        
229100                                                                          
229200 4400-MOVE-COMMAREA-TO-SCREEN.                                            
229300                                                                          
229400     IF ASC-LBL-NM = SPACES                                               
229500        MOVE 'ALL' TO AVENDNMO                                            
229600     ELSE                                                                 
229700        MOVE ASC-LBL-NM TO AVENDNMO                                       
229800     END-IF.                                                              
229900                                                                          
230000     MOVE ASC-GP-NBR          TO ABUSGRPO.                                
230100     MOVE ASC-GP-DESC         TO ABUSDESO.                                
230200     MOVE ASC-SKU-CNT         TO ASKUO.                                   
230300     MOVE ASC-DEPT-NBR        TO ADEPTNOO.                                
230400     MOVE ASC-DEPT-DESC       TO ADPTDESO.                                
230500     MOVE ASC-MAJ-CL          TO AMAJORO.                                 
230600     MOVE ASC-SUB-CL          TO ASUBO.                                   
230700     MOVE ASC-SUB-CL-NM       TO ASUBDESO.                                
230800     MOVE ASC-CLOR-DESC       TO ACOLORO.                                 
230900                                                                          
231000     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
231100         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
231200         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
231300             MOVE +1    TO ASC-BLK-SUB                                    
231400             PERFORM 5100-WRITE-TEMP-STORAGE                              
231500             ADD +1     TO ASC-BLK-SUB                                    
231600             PERFORM 5100-WRITE-TEMP-STORAGE                              
231700             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
231800                 GIVING PV-BLOCK-NUMBER                                   
231900                 REMAINDER PV-REMAINDER                                   
232000             IF  PV-REMAINDER > ZERO                                      
232100                 ADD +1 TO PV-BLOCK-NUMBER                                
232200             END-IF                                                       
232300             MOVE +1    TO ASC-BLK-SUB                                    
232400             PERFORM 4410-READ-TEMP-STORAGE                               
232500             ADD +1     TO ASC-BLK-SUB                                    
232600                           PV-BLOCK-NUMBER                                
232700             PERFORM 4410-READ-TEMP-STORAGE                               
232800     END-IF.                                                              
232900     MOVE +1            TO ASC-BLK-SUB.                                   
233000     COMPUTE ASC-ITEM-SUB =                                               
233100         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
233200         + PC-ITEMS-PER-PANEL).                                           
233300*                                                                         
233400     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
233500         ADD +1         TO ASC-BLK-SUB                                    
233600         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
233700     END-IF.                                                              
233800*                                                                         
233900     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
234000         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
234100     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
234200     PERFORM 4420-FORMAT-LIST-LINES                                       
234300         VARYING PV-SUB                                                   
234400         FROM PC-ITEMS-PER-PANEL BY -1                                    
234500         UNTIL PV-SUB < +1.                                               
234600*                                                                         
234700     COMPUTE ASC-ITEMS-DISPLAYED =                                        
234800         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
234900     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
235000                                      ASTRTITO.                           
235100     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
235200     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
235300*                                                                         
235400     IF  DP020-MSG-NUMBER-X = SPACE                                       
235500         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
235600*            --- MORE ITEMS TO DISPLAY ----                               
235700             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
235800             SET DP020-MSG-INFORMATIONAL TO TRUE                          
235900         END-IF                                                           
236000     END-IF.                                                              
236100*----------------------------------------------------------------*        
236200*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
236300*----------------------------------------------------------------*        
236400                                                                          
236500 4410-READ-TEMP-STORAGE.                                                  
236600                                                                          
236700     EXEC CICS                                                            
236800         READQ TS                                                         
236900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
237000             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
237100             ITEM  (PV-BLOCK-NUMBER)                                      
237200             RESP  (PV-CICS-RESP)                                         
237300     END-EXEC.                                                            
237400                                                                          
237500     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
237600       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
237700         CONTINUE                                                         
237800     ELSE                                                                 
237900         SET DP013-CICS-ABEND     TO TRUE                                 
238000         SET DP013-NO-ROLLBACK    TO TRUE                                 
238100         MOVE '4410-READ-TEMP-STORAGE'                                    
238200                                  TO DP013-PARAGRAPH                      
238300         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
238400                                  TO DP013-MESSAGE-TEXT(1)                
238500         PERFORM DP013-0000-PROCESS-ABEND                                 
238600     END-IF.                                                              
238700                                                                          
238800     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
238900*                                                                         
239000     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
239100         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
239200     END-IF.                                                              
239300 EJECT                                                                    
239400*----------------------------------------------------------------*        
239500*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
239600*----------------------------------------------------------------*        
239700                                                                          
239800 4420-FORMAT-LIST-LINES.                                                  
239900     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
240000         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
240100         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
240200         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
240300         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
240400                                    MR-SIZE (PV-SUB)                      
240500     ELSE                                                                 
240600         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
240700                                                   > SPACE                
240800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
240900                                 TO MR-SELECTI (PV-SUB)                   
241000         ELSE                                                             
241100             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
241200                                 TO MR-SELECTI (PV-SUB)                   
241300         END-IF                                                           
241400*                                                                         
241500         IF  DP030-SET-CURSOR-TRAILER                                     
241600             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
241700                 > SPACE)                                                 
241800               OR (PV-SUB = 1))                                           
241900                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
242000                MOVE -1          TO MR-SELECTL (PV-SUB)                   
242100             END-IF                                                       
242200         END-IF                                                           
242300*                                                                         
242400         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
242500                                                                          
242600             MOVE ASC-SIZE-DESC (ASC-BLK-SUB ASC-ITEM-SUB)                
242700                                 TO MR-SIZE (PV-SUB)                      
242800         END-IF                                                           
242900     END-IF.                                                              
243000     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
243100     IF  ASC-ITEM-SUB < + 1                                               
243200         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
243300         MOVE +1                 TO ASC-BLK-SUB                           
243400     END-IF.                                                              
243500******************************************************************        
243600** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
243700** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
243800** ISSUE A WARNING.                                             **        
243900******************************************************************        
244000 4430-CHECK-FOR-BEGIN-AND-END.                                            
244100     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
244200         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
244300*            -- NO END-OF-RANGE SPECFIED ---                              
244400             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
244500             SET DP020-MSG-WARNING TO TRUE                                
244600             PERFORM 4431-POSITION-AT-WARNING                             
244700         END-IF                                                           
244800     ELSE                                                                 
244900         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
245000*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
245100             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
245200             SET DP020-MSG-WARNING TO TRUE                                
245300             PERFORM 4431-POSITION-AT-WARNING                             
245400         END-IF                                                           
245500     END-IF.                                                              
245600******************************************************************        
245700** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
245800** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
245900** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
246000** POSITION THE CURSOR THERE.                                   **        
246100******************************************************************        
246200 4431-POSITION-AT-WARNING.                                                
246300     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
246400                    - PV-TOP-ITEM + 1.                                    
246500     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
246600         CONTINUE                                                         
246700     ELSE                                                                 
246800         SET DP030-SET-CURSOR-APPL-1                                      
246900                                   TO TRUE                                
247000         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
247100         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
247200         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
247300     END-IF.                                                              
247400*                                                                         
247500     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
247600                    - PV-TOP-ITEM + 1.                                    
247700     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
247800         CONTINUE                                                         
247900     ELSE                                                                 
248000         SET DP030-SET-CURSOR-APPL-1                                      
248100                                   TO TRUE                                
248200         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
248300         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
248400         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
248500     END-IF.                                                              
248600 EJECT                                                                    
248700*----------------------------------------------------------------*        
248800* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
248900* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
249000* FOR THE SELECTED ITEMS QUEUE.                                  *        
249100*                                                                *        
249200* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
249300* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
249400* THE ARRAY IS FULL.                                             *        
249500*----------------------------------------------------------------*        
249600                                                                          
249700 5000-PREPARE-SEL-QUEUE.                                                  
249800                                                                          
249900     MOVE +1                   TO  ASC-BLK-SUB.                           
250000     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
250100     ADD +1                    TO  ASC-BLK-SUB.                           
250200     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
250300                                                                          
250400     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
250500     PERFORM 6010-DELETE-SEL-QUEUE.                                       
250600                                                                          
250700     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
250800                                                                          
250900     SET IQ014-IDX             TO  1.                                     
251000     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
251100     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
251200                                                                          
251300     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
251400             VARYING PV-ITEM                                              
251500             FROM 1 BY 1                                                  
251600             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
251700                                                                          
251800     IF  IQ014-IDX > 1                                                    
251900         PERFORM 5205-WRITE-SEL-QUEUE                                     
252000         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
252100     END-IF.                                                              
252200 EJECT                                                                    
252300*----------------------------------------------------------------*        
252400*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
252500*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
252600*----------------------------------------------------------------*        
252700                                                                          
252800 5100-WRITE-TEMP-STORAGE.                                                 
252900     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
253000                                   TO PV-LIST-ITEM-NUMBER.                
253100     COMPUTE PV-BLOCK-NUMBER =                                            
253200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
253300                                                                          
253400     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
253500     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
253600         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
253700             PERFORM 5105-REWRITE-ASC-LISTQ                               
253800         END-IF                                                           
253900     ELSE                                                                 
254000         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
254100         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
254200         PERFORM 5110-WRITE-ASC-LISTQ                                     
254300         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
254400     END-IF.                                                              
254500                                                                          
254600     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
254700******************************************************************        
254800** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
254900** SPECIFIC COMMAREA.                                           **        
255000******************************************************************        
255100 5105-REWRITE-ASC-LISTQ.                                                  
255200     EXEC CICS                                                            
255300         WRITEQ TS                                                        
255400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
255500             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
255600             ITEM  (PV-BLOCK-NUMBER)                                      
255700             REWRITE                                                      
255800             RESP  (PV-CICS-RESP)                                         
255900     END-EXEC.                                                            
256000                                                                          
256100     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
256200         CONTINUE                                                         
256300     ELSE                                                                 
256400         SET DP013-CICS-ABEND            TO TRUE                          
256500         SET DP013-NO-ROLLBACK           TO TRUE                          
256600         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
256700         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
256800                                         TO DP013-MESSAGE-TEXT(1)         
256900         PERFORM DP013-0000-PROCESS-ABEND                                 
257000     END-IF.                                                              
257100                                                                          
257200******************************************************************        
257300** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
257400** SPECIFIC COMMAREA.                                           **        
257500******************************************************************        
257600 5110-WRITE-ASC-LISTQ.                                                    
257700     EXEC CICS                                                            
257800         WRITEQ TS                                                        
257900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
258000             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
258100             ITEM  (PV-BLOCK-NUMBER)                                      
258200             RESP  (PV-CICS-RESP)                                         
258300     END-EXEC.                                                            
258400     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
258500         SET DP013-CICS-ABEND      TO TRUE                                
258600         SET DP013-NO-ROLLBACK     TO TRUE                                
258700         MOVE '5110-WRITE-ASC-LISTQ'                                      
258800                                   TO DP013-PARAGRAPH                     
258900         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
259000                                   TO DP013-MESSAGE-TEXT(1)               
259100         PERFORM DP013-0000-PROCESS-ABEND                                 
259200     END-IF.                                                              
259300 EJECT                                                                    
259400*----------------------------------------------------------------*        
259500* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
259600* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
259700*----------------------------------------------------------------*        
259800                                                                          
259900 5200-FILL-SEL-QUEUE-RECORD.                                              
260000                                                                          
260100     PERFORM 6112-READ-LIST-QUEUE.                                        
260200                                                                          
260300     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
260400         PERFORM                                                          
260500         VARYING LQW-IDX                                                  
260600         FROM 1 BY 1                                                      
260700         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
260800           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
260900                                                                          
261000             IF  LQW-SELECT (LQW-IDX)                                     
261100                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
261200                                   TO IQ014-ITEM (IQ014-IDX)              
261300                 MOVE LQW-SIZE-DESC (LQW-IDX)                             
261400                             TO IQ014-SIZE-DESC (IQ014-IDX)               
261500                                IQ016-SIZE-DESC                           
261600                 SET IQ014-IDX UP BY 1                                    
261700                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
261800                     PERFORM 5205-WRITE-SEL-QUEUE                         
261900                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
262000                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
262100                     SET IQ014-IDX TO  1                                  
262200                 END-IF                                                   
262300             END-IF                                                       
262400         END-PERFORM                                                      
262500     END-IF.                                                              
262600                                                                          
262700*----------------------------------------------------------------*        
262800*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
262900*----------------------------------------------------------------*        
263000                                                                          
263100 5205-WRITE-SEL-QUEUE.                                                    
263200                                                                          
263300     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
263400                                   TO IQ014-SELECTED-ITEMS.               
263500                                                                          
263600     EXEC CICS                                                            
263700          WRITEQ TS                                                       
263800              QUEUE (ASC-SEL-QUEUE-NAME)                                  
263900              FROM  (IQ014-MESSAGE-SEL-REC)                               
264000              ITEM  (PV-SEL-QUEUE-ITEM)                                   
264100              RESP  (PV-CICS-RESP)                                        
264200     END-EXEC.                                                            
264300                                                                          
264400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
264500         CONTINUE                                                         
264600     ELSE                                                                 
264700         SET DP013-CICS-ABEND      TO TRUE                                
264800         SET DP013-NO-ROLLBACK     TO TRUE                                
264900         MOVE '5205-WRITE-SEL-QUEUE'                                      
265000                                   TO DP013-PARAGRAPH                     
265100         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
265200                                   TO DP013-MESSAGE-TEXT(1)               
265300         PERFORM DP013-0000-PROCESS-ABEND                                 
265400     END-IF.                                                              
265500 EJECT                                                                    
265600******************************************************************        
265700** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
265800** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
265900** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
266000** THE USER.                                                    **        
266100******************************************************************        
266200 6000-REFRESH-PANEL-FROM-TEMP.                                            
266300     IF  ASC-UPDATE-TSQ                                                   
266400         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
266500         MOVE ZERO                 TO PV-SAVE-ITEM                        
266600                                      PV-SEL-QUEUE-ITEM                   
266700         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
266800                                   TO PV-HOLD-SELECTED-ITEMS              
266900         MOVE +1                   TO PV-ITEMS-PROCESSED                  
267000         PERFORM 6100-UPDATE-TSQ                                          
267100             UNTIL PV-ITEMS-PROCESSED >                                   
267200                                     PV-HOLD-SELECTED-ITEMS               
267300         PERFORM 6010-DELETE-SEL-QUEUE                                    
267400         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
267500                    ASC-BLOCK-ENTRIES (2)                                 
267600     END-IF.                                                              
267700                                                                          
267800*----------------------------------------------------------------*        
267900*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
268000*----------------------------------------------------------------*        
268100 6010-DELETE-SEL-QUEUE.                                                   
268200                                                                          
268300     EXEC CICS                                                            
268400         DELETEQ TS                                                       
268500             QUEUE (ASC-SEL-QUEUE-NAME)                                   
268600             RESP  (PV-CICS-RESP)                                         
268700     END-EXEC.                                                            
268800*                                                                         
268900     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
269000        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
269100         CONTINUE                                                         
269200     ELSE                                                                 
269300         SET DP013-CICS-ABEND TO TRUE                                     
269400         SET DP013-NO-ROLLBACK TO TRUE                                    
269500         MOVE '6010-DELETE-SEL-QUEUE'                                     
269600                        TO DP013-PARAGRAPH                                
269700         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
269800                        TO DP013-MESSAGE-TEXT (1)                         
269900         PERFORM DP013-0000-PROCESS-ABEND                                 
270000     END-IF.                                                              
270100                                                                          
270200*----------------------------------------------------------------*        
270300*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
270400*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
270500*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
270600*    THE TEMP QUEUE.                                             *        
270700*----------------------------------------------------------------*        
270800                                                                          
270900 6100-UPDATE-TSQ.                                                         
271000                                                                          
271100     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
271200     PERFORM 6105-READ-SEL-QUEUE.                                         
271300                                                                          
271400     PERFORM                                                              
271500         VARYING IQ014-IDX                                                
271600         FROM 1 BY 1                                                      
271700         UNTIL ((PV-ITEMS-PROCESSED >                                     
271800                              PV-HOLD-SELECTED-ITEMS)                     
271900         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
272000         PERFORM 6110-PROCESS-ITEMS                                       
272100         ADD +1                     TO PV-ITEMS-PROCESSED                 
272200     END-PERFORM.                                                         
272300     PERFORM 6111-WRITE-LIST-QUEUE.                                       
272400*----------------------------------------------------------------*        
272500*    READ THE LIST QUEUE RECORD.                                 *        
272600*----------------------------------------------------------------*        
272700                                                                          
272800 6105-READ-SEL-QUEUE.                                                     
272900                                                                          
273000     EXEC CICS                                                            
273100          READQ TS                                                        
273200              QUEUE (ASC-SEL-QUEUE-NAME)                                  
273300              INTO  (IQ014-MESSAGE-SEL-REC)                               
273400              ITEM  (PV-SEL-QUEUE-ITEM)                                   
273500              RESP  (PV-CICS-RESP)                                        
273600     END-EXEC.                                                            
273700*                                                                         
273800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
273900         CONTINUE                                                         
274000     ELSE                                                                 
274100         SET DP013-CICS-ABEND      TO  TRUE                               
274200         SET DP013-NO-ROLLBACK     TO  TRUE                               
274300         MOVE '6105-READ-SEL-QUEUE'                                       
274400                                   TO  DP013-PARAGRAPH                    
274500         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
274600                                   TO  DP013-MESSAGE-TEXT(1)              
274700         PERFORM DP013-0000-PROCESS-ABEND                                 
274800     END-IF.                                                              
274900 EJECT                                                                    
275000*----------------------------------------------------------------*        
275100*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
275200*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
275300*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
275400*----------------------------------------------------------------*        
275500                                                                          
275600 6110-PROCESS-ITEMS.                                                      
275700                                                                          
275800     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
275900                     / PC-ITEMS-PER-PANEL).                               
276000                                                                          
276100     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
276200         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
276300             PERFORM 6111-WRITE-LIST-QUEUE                                
276400         END-IF                                                           
276500         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
276600                                                                          
276700         PERFORM 6112-READ-LIST-QUEUE                                     
276800         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
276900             CONTINUE                                                     
277000         END-IF                                                           
277100     END-IF.                                                              
277200                                                                          
277300     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
277400                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
277500     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
277600     MOVE LQW-SELECTED-SW (PV-SUB)                                        
277700                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
277800                                                                          
277900     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
278000                                                                          
278100*----------------------------------------------------------------*        
278200*    WRITE THE TS QUEUE RECORD JUST UPDATED                      *        
278300*----------------------------------------------------------------*        
278400                                                                          
278500 6111-WRITE-LIST-QUEUE.                                                   
278600                                                                          
278700     EXEC CICS                                                            
278800         WRITEQ TS                                                        
278900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
279000             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
279100             ITEM  (PV-SAVE-ITEM)                                         
279200             REWRITE                                                      
279300             RESP  (PV-CICS-RESP)                                         
279400     END-EXEC.                                                            
279500*                                                                         
279600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
279700         CONTINUE                                                         
279800     ELSE                                                                 
279900         SET DP013-CICS-ABEND  TO  TRUE                                   
280000         SET DP013-NO-ROLLBACK TO  TRUE                                   
280100         MOVE '6111-WRITE-LIST-QUEUE'                                     
280200                               TO  DP013-PARAGRAPH                        
280300         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
280400                               TO  DP013-MESSAGE-TEXT(1)                  
280500         PERFORM DP013-0000-PROCESS-ABEND                                 
280600     END-IF.                                                              
280700                                                                          
280800 EJECT                                                                    
280900*----------------------------------------------------------------*        
281000*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
281100*----------------------------------------------------------------*        
281200                                                                          
281300 6112-READ-LIST-QUEUE.                                                    
281400                                                                          
281500     EXEC CICS                                                            
281600         READQ TS                                                         
281700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
281800             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
281900             ITEM  (PV-ITEM)                                              
282000             RESP  (PV-CICS-RESP)                                         
282100     END-EXEC.                                                            
282200*                                                                         
282300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
282400         CONTINUE                                                         
282500     ELSE                                                                 
282600         SET DP013-CICS-ABEND        TO TRUE                              
282700         SET DP013-NO-ROLLBACK       TO TRUE                              
282800         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
282900         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
283000                                     TO DP013-MESSAGE-TEXT(1)             
283100         PERFORM DP013-0000-PROCESS-ABEND                                 
283200     END-IF.                                                              
283300                                                                          
283400 EJECT                                                                    
283500*----------------------------------------------------------------*        
283600*    RE-INITIALIZE ASC- FIELDS AFTER F3 RETURN                   *        
283700*----------------------------------------------------------------*        
283800 6500-REINIT-ASC-FLDS.                                                    
283900                                                                          
284000     MOVE ASC-LBL-NM       TO   IQ016-LBL-NM.                             
284100     MOVE ASC-GP-NBR       TO   IQ016-GP-NBR.                             
284200     MOVE ASC-GP-DESC      TO   IQ016-GP-DESC.                            
284300     MOVE ASC-DEPT-NBR     TO   IQ016-DEPT-NBR.                           
284400     MOVE ASC-DEPT-DESC    TO   IQ016-DEPT-DESC.                          
284500     MOVE ASC-MAJ-CL       TO   IQ016-MAJ-CL.                             
284600     MOVE ASC-SUB-CL       TO   IQ016-SUB-CL.                             
284700     MOVE ASC-SUB-CL-NM    TO   IQ016-SUB-CL-NM.                          
284800     MOVE ASC-CLOR-DESC    TO   IQ016-CLOR-DESC.                          
284900                                                                          
285000*----------------------------------------------------------------*        
285100*    ABEND PROCESSOR MODULE                                      *        
285200*----------------------------------------------------------------*        
285300                                                                          
285400     COPY DPPD013.                                                        
