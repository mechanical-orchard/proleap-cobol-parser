                                                                        00010000
       IDENTIFICATION DIVISION.                                         00020000
                                                                        00030000
       PROGRAM-ID.    MLKUT430.                                         00040000
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00050000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060000
       DATE-WRITTEN.  03-30-1999.                                       00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      ******************************************************************00100000
      * MLKUT430 - M/L PURGE AND RECALC WEEKLY SHRINKAGE TABLE          00110000
      ******************************************************************00120000
      *     THIS PROGRAM WILL PURGE TWKSHNK ROWS BY ELIMINATING TWKSHNK 00130000
      *  UNLOAD FILE RECORDS WHICH HAVE A FISCAL MONTH END DATE WHICH   00140000
      *  IS NOT BETWEEN TMLCNTL OPN_FSCL_MN_DTE AND MXPSTG_FSCL_MN_DTE. 00150000
      *  ANY REMAINING RECORDS WHICH WILL BE USED TO RELOAD THE TWKSHNK 00160000
      *  TABLE WILL HAVE THE SHNK_RTL_AMT FIELD RECALCULATED.           00170000
      *     TABLE TMLCNTL CONTAINS DATES RELATIVE TO PROCESSING DATES.  00180000
      *  MLKUT430 USES THE CURRENT 'OPEN' FISCAL MONTH-END-DATE AND THE 00190000
      *  MAX MONTH-END POSTDATE (MXPSTG) THAT WILL BE RECALCED THIS RUN.00200000
      *  A SYSIN CONTROL CARD MAY BE USED TO OVERRIDE THE TMLCNTL DATES.00210000
      *                                                                 00220000
      *                                                                 00230000
      *CHANGE HISTORY:                                                  00240000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00250000
      * ------- ---------- ----------- -------------------------------- 00260000
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00270000
C14666* CRQ000000014666                                                 00280000
C14666*         07-10-2012 RAVI GUNTHA REVISE SHRINK ACCRUAL RATE       00290004
C14666*                                PROCESS. CHANGES MADE TO         00300000
C14666*                                HANDLE ECOM, EXPANDED AND        00310000
C14666*                                NEGATIVE SHRINK PERCENTAGES.     00320000
      ******************************************************************00330000
                                                                        00340000
       ENVIRONMENT DIVISION.                                            00350000
                                                                        00360000
       CONFIGURATION SECTION.                                           00370000
       SOURCE-COMPUTER.        IBM-3090.                                00380000
       OBJECT-COMPUTER.        IBM-3090.                                00390000
       INPUT-OUTPUT SECTION.                                            00400000
       FILE-CONTROL.                                                    00410000
      *                                                                 00420000
           SELECT WKSHNK-UNLOAD-FILE                                    00430000
               ASSIGN TO INP01.                                         00440000
                                                                        00450000
           SELECT WKSHNK-LOAD-FILE                                      00460000
               ASSIGN TO OUT01.                                         00470000
                                                                        00480000
           SELECT WKSHNK-PURGE-FILE                                     00490000
               ASSIGN TO OUT02.                                         00500000
                                                                        00510000
      *                                                                 00520000
       DATA DIVISION.                                                   00530000
       FILE SECTION.                                                    00540000
      **                                                                00550000
       FD  WKSHNK-UNLOAD-FILE                                           00560000
           RECORDING MODE IS F                                          00570000
           LABEL RECORDS ARE STANDARD                                   00580000
           BLOCK CONTAINS 0 RECORDS.                                    00590000
                                                                        00600000
C14666 01  WKSHNK-UNLOAD-REC           PIC X(43).                       00610000
                                                                        00620000
       FD  WKSHNK-LOAD-FILE                                             00630000
           RECORDING MODE IS F                                          00640000
           LABEL RECORDS ARE STANDARD                                   00650000
           BLOCK CONTAINS 0 RECORDS.                                    00660000
                                                                        00670000
W26603 01  WKSHNK-LOAD-REC             PIC X(41).                       00680000
                                                                        00690000
       FD  WKSHNK-PURGE-FILE                                            00700000
           RECORDING MODE IS F                                          00710000
           LABEL RECORDS ARE STANDARD                                   00720000
           BLOCK CONTAINS 0 RECORDS.                                    00730000
                                                                        00740000
W26603 01  WKSHNK-PURGE-REC            PIC X(41).                       00750000
                                                                        00760000
      *                                                                 00770000
       WORKING-STORAGE SECTION.                                         00780000
                                                                        00790000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00800000
                                                                        00810000
       01  PA-PROGRAM-ACCUMULATORS.                                     00820000
           05  PA-UNLOAD-RECS-READ             PIC  9(09) VALUE ZEROES. 00830000
           05  PA-LOAD-RECS-WRITTEN            PIC  9(09) VALUE ZEROES. 00840000
           05  PA-PURGE-RECS-WRITTEN           PIC  9(09) VALUE ZEROES. 00850000
                                                                        00860000
       01  PS-PROGRAM-SWITCHES.                                         00870000
           05  PS-EOF-UNLOAD-FILE-SW           PIC X(01) VALUE 'N'.     00880000
               88  PS-EOF-UNLOAD-FILE                    VALUE 'Y'.     00890000
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.     00900000
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.     00910000
           05  PS-LOAD-DATE-TABLE-SW           PIC X     VALUE 'N'.     00920000
               88  PS-DATE-TABLE-LOADED                  VALUE 'Y'.     00930000
                                                                        00940000
       01  PC-PROGRAM-CONSTANTS.                                        00950000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUT430'.  00960000
           05  PC-MAX-DATE-TABLE       PIC  9(02)    VALUE 24.          00970000
                                                                        00980000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00990000
           05  PE-MSG-01                       PIC X(30) VALUE          01000000
                'INSPECT WHERE CLAUSE VARIABLE'.                        01010000
           05  PE-MSG-02                       PIC X(30) VALUE          01020000
                'DB2 SELECT ATTEMPTED         '.                        01030000
           05  PE-MSG-03                       PIC X(30) VALUE          01040000
                'ATTEMPTING TO OPEN DB2 CURSOR'.                        01050000
           05  PE-MSG-04                       PIC X(30) VALUE          01060000
                'ATTEMPT TO FETCH CURSOR ROW  '.                        01070000
           05  PE-MSG-05                       PIC X(30) VALUE          01080000
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                        01090000
           05  PE-MSG-06                       PIC X(30) VALUE          01100000
                'SHRINK NOT FND FOR DEPT/DATE '.                        01110000
           05  PE-MSG-07                       PIC X(30) VALUE          01120000
                'MAX DATE TBL ENTRIES EXCEEDED'.                        01130000
      *                                                                 01140000
       01  PROGRAM-VARIABLES.                                           01150000
           05  PV-DEPT-SHRINK-VARIABLES.                                01160000
C14666         10  PV-DEPT-SHRINK-PCT          PIC SV9(04)     COMP-3.  01170000
               10  PREV-SHRINK-DEPT            PIC  X(03) VALUE SPACES. 01180000
               10  PREV-SHRINK-MONTH           PIC  9(02) VALUE 0.      01190000
C14666         10  PREV-BUS-IND                PIC  X(01) VALUE SPACE.  01200000
      *                                                                 01210000
       01  PV-VARIABLE-FIELDS.                                          01220000
           05  PV-SUB1                         PIC  9(2)  VALUE ZEROES. 01230000
           05  PV-MAX-DATE-SUB                 PIC  9(2)  VALUE ZEROES. 01240000
           05  PV-HOLD-DATE                    PIC  X(10) VALUE SPACES. 01250000
           05  PV-HOLD-OPN-MN-END-DTE          PIC  X(10) VALUE SPACES. 01260000
           05  PV-HOLD-FSCL-YR-NBR             PIC  X(04) VALUE SPACES. 01270000
           05  PV-HOLD-DEPT-NBR                PIC  X(03) VALUE SPACES. 01280000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01290000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01300000
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES. 01310000
      *                                                                 01320000
      * REFORMAT CALENDAR NUMBER TO PIC 9 FIELD FOR USE AS A SUBSCRIPT  01330000
       01  REFORMAT-ALPHA-CALENDAR.                                     01340000
           05  FMT-CALENDAR-MO-NUMERIC          PIC 9(2)  VALUE ZEROES. 01350000
                                                                        01360000
       01  CC-CONTROL-CARD.                                             01370000
           05  CC-OPEN-FISCAL-DATE                   VALUE SPACES.      01380000
               10  CC-OPEN-FISCAL-YEAR   PIC 9(04).                     01390000
               10  CC-OPEN-FISCAL-DSH1   PIC X(01).                     01400000
               10  CC-OPEN-FISCAL-MONTH  PIC 9(02).                     01410000
               10  CC-OPEN-FISCAL-DSH2   PIC X(01).                     01420000
               10  CC-OPEN-FISCAL-DAY    PIC 9(02).                     01430000
           05  FILLER                    PIC X(01)   VALUE SPACE.       01440000
           05  CC-MAX-FISCAL-DATE                    VALUE SPACES.      01450000
               10  CC-MAX-FISCAL-YEAR    PIC 9(04).                     01460000
               10  CC-MAX-FISCAL-DSH1    PIC X(01).                     01470000
               10  CC-MAX-FISCAL-MONTH   PIC 9(02).                     01480000
               10  CC-MAX-FISCAL-DSH2    PIC X(01).                     01490000
               10  CC-MAX-FISCAL-DAY     PIC 9(02).                     01500000
           05  FILLER                    PIC X(59)   VALUE SPACES.      01510000
                                                                        01520000
      *                                                                 01530000
      * TABLE TO CONTAIN DEPT SHRINK AMOUNTS                            01540000
       01  SA-DPTSHK-ROW.                                               01550000
           05  FILLER                              PIC S9(6)V  COMP-3.  01560000
           05  FILLER                              PIC  X(4).           01570000
           05  FILLER                              PIC  X(26).          01580000
           05  SA-DPTSHK-SHRINK-PCTS OCCURS 12 TIMES.                   01590000
               10  SA-DPTSHK-SHRINK-PCT            PIC SV9(4)  COMP-3.  01600000
       01  SA-DPTSHK-SUB                           PIC S9(4) VALUE ZERO 01610000
                                                             COMP SYNC. 01620000
C14666*                                                                 01630000
C14666* IMPORTANT NOTE: THE 10 LEVEL PV-INP-E-BUS-IND MUST              01640000
C14666* IMMEDIATELY FOLLOW THE INCLUDE OF TWKSHNK BECAUSE IT            01650000
C14666* HOLDS THE E-BUS-IND FROM THE XMT_LOC JOIN TO TWKSHNK IN         01660000
C14666* THE UNLOAD SELECTION CRITERIA. THE TWKSHNK DCLGEN AND THE       01670000
C14666* 1 BYTE PV-INPUT-E-BUS-IND ARE THE RECORD LAYOUT FOR THE INPUT   01680000
C14666*                                                                 01690000
      *** TWKSHNK UNLOAD AND LOAD FILE RECORD                           01700000
           EXEC SQL                                                     01710000
               INCLUDE TWKSHNK                                          01720000
           END-EXEC.                                                    01730000
C14666         10  PV-INPUT-E-BUS-IND          PIC X(01) VALUE SPACE.   01740000
C14666         10  FILLER                      PIC X(01) VALUE SPACE.   01750000
      *                                                                 01760000
      * TABLE TO CONTAIN DATE INFORMATION FOR ALL MONTHS RE-CALCULATED  01770000
       01  DATE-TABLE.                                                  01780000
           05  CONTROL-DATES OCCURS 24 TIMES.                           01790000
               10  DT-OPN-MN-END-DTE            PIC X(10) VALUE SPACES. 01800000
               10  DT-CAL-MN-NBR                PIC X(02) VALUE SPACES. 01810000
               10  DT-FSCL-YR-NBR               PIC X(04) VALUE SPACES. 01820000
                                                                        01830000
      ****************************************************              01840000
      *  COMMUNICATION AREAS FOR DB2                     *              01850000
      ****************************************************              01860000
           EXEC SQL                                                     01870000
               INCLUDE SQLCA                                            01880000
           END-EXEC.                                                    01890000
      ****************************************************              01900000
      *  DB2 MERCHANDISE AREA TABLE - BUSINESS STRUCTURE *              01910000
      ****************************************************              01920000
           EXEC SQL                                                     01930000
               INCLUDE TMDSEAR                                          01940000
           END-EXEC.                                                    01950000
      ****************************************************              01960000
      *  DEPARTMENT SHRINK TABLE                         *              01970000
      ****************************************************              01980000
           EXEC SQL                                                     01990000
               INCLUDE TDPTSHK                                          02000000
           END-EXEC.                                                    02010000
      ****************************************************              02020000
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *              02030000
      ****************************************************              02040000
           EXEC SQL                                                     02050000
               INCLUDE TDTATTR                                          02060000
           END-EXEC.                                                    02070000
      ****************************************************              02080000
      * M/L DATE CONTROL INFO                            *              02090000
      ****************************************************              02100000
           EXEC SQL                                                     02110000
               INCLUDE TMLCNTL                                          02120000
           END-EXEC.                                                    02130000
      *                                                                 02140000
      * MONTHS TO BE PROCESSED - ALL DATES BETWEEN OPEN AND MAX DATES   02150000
           EXEC SQL                                                     02160000
               DECLARE FISCAL_DATE_CSR CURSOR FOR                       02170000
                SELECT  DISTINCT FSCL_MN_END_DTE                        02180000
                  FROM  TDTATTR                                         02190000
                 WHERE FSCL_MN_END_DTE BETWEEN                          02200000
                                            :MLCNTL-OPN-FSCL-MN-DTE     02210000
                                        AND :MLCNTL-MXPSTG-FSCL-MN-DTE  02220000
                 ORDER BY FSCL_MN_END_DTE                               02230000
                 FOR FETCH ONLY                                         02240000
           END-EXEC.                                                    02250000
      *                                                                 02260000
      ****************************************************              02270000
      *  ERROR MESSAGE AREA FOR DB2                      *              02280000
      ****************************************************              02290000
           COPY DPWS004.                                                02300000
      *                                                                 02310000
           EJECT                                                        02320000
      *                                                                 02330000
      *-------------------*                                             02340000
       PROCEDURE DIVISION.                                              02350000
      *-------------------*                                             02360000
                                                                        02370000
       0000-MLKUT430.                                                   02380000
                                                                        02390000
           PERFORM 1000-INITIALIZATION.                                 02400000
                                                                        02410000
           MOVE 1 TO PV-SUB1.                                           02420000
           PERFORM 2000-MAINLINE                                        02430000
                UNTIL PS-EOF-UNLOAD-FILE.                               02440000
                                                                        02450000
           PERFORM 8000-EOJ-ROUTINE.                                    02460000
                                                                        02470000
           STOP RUN.                                                    02480000
      *                                                                 02490000
      *                                                                 02500000
      ******************************************************************02510000
      * LOAD TABLE WITH MONTHS TO BE RECALCULATED                       02520000
      ******************************************************************02530000
       1000-INITIALIZATION.                                             02540000
      *                                                                 02550000
           ACCEPT CC-CONTROL-CARD.                                      02560000
           IF CC-OPEN-FISCAL-DATE = SPACES                              02570000
             AND CC-MAX-FISCAL-DATE = SPACES                            02580000
               PERFORM 7000-READ-CONTROL-TABLE                          02590000
           ELSE                                                         02600000
               DISPLAY 'USING INPUT CONTROL CARD FISCAL DATES'          02610000
               DISPLAY '   OPEN FISCAL DATE: ' CC-OPEN-FISCAL-DATE      02620000
               DISPLAY '   MAX FISCAL DATE:  ' CC-MAX-FISCAL-DATE       02630000
               IF CC-OPEN-FISCAL-YEAR IS NUMERIC                        02640000
                 AND CC-OPEN-FISCAL-DSH1 = '-'                          02650000
                 AND CC-OPEN-FISCAL-MONTH IS NUMERIC                    02660000
                 AND CC-OPEN-FISCAL-DSH2 = '-'                          02670000
                 AND CC-OPEN-FISCAL-DAY IS NUMERIC                      02680000
                 AND CC-MAX-FISCAL-YEAR IS NUMERIC                      02690000
                 AND CC-MAX-FISCAL-DSH1 = '-'                           02700000
                 AND CC-MAX-FISCAL-MONTH IS NUMERIC                     02710000
                 AND CC-MAX-FISCAL-DSH2 = '-'                           02720000
                 AND CC-MAX-FISCAL-DAY IS NUMERIC                       02730000
                   MOVE CC-OPEN-FISCAL-DATE TO MLCNTL-OPN-FSCL-MN-DTE   02740000
                   MOVE CC-MAX-FISCAL-DATE  TO MLCNTL-MXPSTG-FSCL-MN-DTE02750000
               ELSE                                                     02760000
                   DISPLAY '** INVALID INPUT CONTROL CARD FISCAL DATES' 02770000
085200             DISPLAY '** ABEND IN PGM  ' PC-PROGRAM-NAME          02780000
                   MOVE +4003 TO ABEND-CODE                             02790000
085400             CALL 'ILBOABN0' USING ABEND-CODE                     02800000
               END-IF                                                   02810000
           END-IF.                                                      02820000
                                                                        02830000
           PERFORM 7025-PROCESS-CNTL-TBL.                               02840000
                                                                        02850000
           SET PS-FIRST-TIME-THRU TO TRUE.                              02860000
      *                                                                 02870000
           ADD 1 TO PV-SUB1.                                            02880000
                                                                        02890000
           OPEN INPUT  WKSHNK-UNLOAD-FILE                               02900000
                OUTPUT WKSHNK-LOAD-FILE                                 02910000
                       WKSHNK-PURGE-FILE.                               02920000
                                                                        02930000
           PERFORM 4000-READ-UNLOAD-FILE.                               02940000
                                                                        02950000
      *                                                                 02960000
      ******************************************************************02970000
      * MAIN PROCESSING PERFORM UNTIL END OF UNLOAD FILE                02980000
      ******************************************************************02990000
       2000-MAINLINE.                                                   03000000
                                                                        03010000
           IF WKSHNK-FSCL-MN-END-DTE < DT-OPN-MN-END-DTE(PV-SUB1)       03020000
               PERFORM 5100-WRITE-PURGE-REC                             03030000
           ELSE                                                         03040000
               IF WKSHNK-FSCL-MN-END-DTE = DT-OPN-MN-END-DTE(PV-SUB1)   03050000
                   PERFORM 2200-CREATE-TWKSHNK-LOAD                     03060000
               ELSE                                                     03070000
                   IF PV-SUB1 = PV-MAX-DATE-SUB                         03080000
                       PERFORM 5100-WRITE-PURGE-REC                     03090000
                   ELSE                                                 03100000
                       PERFORM 2100-PROCESS-TWKSHNK-UNLOAD              03110000
                           UNTIL PV-SUB1 = PV-MAX-DATE-SUB              03120000
                             OR  WKSHNK-FSCL-MN-END-DTE =               03130000
                                 DT-OPN-MN-END-DTE(PV-SUB1)             03140000
                   END-IF                                               03150000
               END-IF                                                   03160000
           END-IF.                                                      03170000
                                                                        03180000
           PERFORM 4000-READ-UNLOAD-FILE.                               03190000
                                                                        03200000
      ******************************************************************03210000
      * FIND A TWKSHNK UNLOAD RECORD FISCAL MONTH END DATE.             03220000
      ******************************************************************03230000
       2100-PROCESS-TWKSHNK-UNLOAD.                                     03240000
                                                                        03250000
           ADD 1 TO PV-SUB1.                                            03260000
                                                                        03270000
           IF WKSHNK-FSCL-MN-END-DTE = DT-OPN-MN-END-DTE(PV-SUB1)       03280000
               PERFORM 2200-CREATE-TWKSHNK-LOAD                         03290000
           ELSE                                                         03300000
               IF PV-SUB1 = PV-MAX-DATE-SUB                             03310000
                   PERFORM 5100-WRITE-PURGE-REC                         03320000
               END-IF                                                   03330000
           END-IF.                                                      03340000
                                                                        03350000
      ******************************************************************03360000
      * PROCESS TWKSHNK UNLOAD RECORD INTO A TWKSHNK LOAD RECORD        03370000
      ******************************************************************03380000
       2200-CREATE-TWKSHNK-LOAD.                                        03390000
                                                                        03400000
           MOVE WKSHNK-DEPT-NBR   TO PV-HOLD-DEPT-NBR.                  03410000
           MOVE DT-OPN-MN-END-DTE(PV-SUB1)  TO PV-HOLD-OPN-MN-END-DTE.  03420000
           MOVE DT-FSCL-YR-NBR(PV-SUB1)     TO PV-HOLD-FSCL-YR-NBR.     03430000
                                                                        03440000
           PERFORM 7200-OBTAIN-SHRINK-PCTS.                             03450000
                                                                        03460000
           COMPUTE WKSHNK-SHNK-RTL-AMT ROUNDED =                        03470000
               WKSHNK-SLS-RTL-AMT * SA-DPTSHK-SHRINK-PCT(SA-DPTSHK-SUB).03480000
                                                                        03490000
           PERFORM 5000-WRITE-LOAD-REC.                                 03500000
      *                                                                 03510000
      *                                                                 03520000
      ******************************************************************03530000
      * READ A TWKSHNK UNLOAD FILE RECORD                               03540000
      ******************************************************************03550000
       4000-READ-UNLOAD-FILE.                                           03560000
                                                                        03570000
           READ WKSHNK-UNLOAD-FILE INTO DCLTWKSHNK                      03580000
               AT END                                                   03590000
                  SET PS-EOF-UNLOAD-FILE TO TRUE.                       03600000
                                                                        03610000
           IF PS-EOF-UNLOAD-FILE                                        03620000
               CONTINUE                                                 03630000
           ELSE                                                         03640000
               ADD 1 TO PA-UNLOAD-RECS-READ                             03650000
C14666                                                                  03651002
C14666* IF E-BUS-IND IS NOT 'Y' AND NOT 'N', DEFAULT IT TO 'N'          03651102
C14666         IF PV-INPUT-E-BUS-IND = 'Y' OR 'N'                       03652002
C14666             CONTINUE                                             03652102
C14666         ELSE                                                     03653002
C14666             MOVE 'N'                TO PV-INPUT-E-BUS-IND        03654002
C14666         END-IF                                                   03656002
           END-IF.                                                      03660000
      *                                                                 03670000
      *                                                                 03680000
      ******************************************************************03690000
      * WRITE A TWKSHNK LOAD FILE RECORD                                03700000
      ******************************************************************03710000
       5000-WRITE-LOAD-REC.                                             03720000
                                                                        03730000
           WRITE WKSHNK-LOAD-REC FROM DCLTWKSHNK.                       03740000
                                                                        03750000
           ADD 1 TO PA-LOAD-RECS-WRITTEN.                               03760000
      *                                                                 03770000
      *                                                                 03780000
      ******************************************************************03790000
      * WRITE A TWKSHNK PURGE FILE RECORD                               03800000
      ******************************************************************03810000
       5100-WRITE-PURGE-REC.                                            03820000
                                                                        03830000
           WRITE WKSHNK-PURGE-REC FROM DCLTWKSHNK.                      03840000
                                                                        03850000
           ADD 1 TO PA-PURGE-RECS-WRITTEN.                              03860000
      *                                                                 03870000
      *                                                                 03880000
      ******************************************************************03890000
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO     03900000
      ******************************************************************03910000
       7000-READ-CONTROL-TABLE.                                         03920000
      *                                                                 03930000
            EXEC SQL                                                    03940000
                SELECT OPN_FSCL_MN_DTE                                  03950000
                      ,MXPSTG_FSCL_MN_DTE                               03960000
                 INTO  :MLCNTL-OPN-FSCL-MN-DTE                          03970000
                      ,:MLCNTL-MXPSTG-FSCL-MN-DTE                       03980000
                 FROM  TMLCNTL                                          03990000
            END-EXEC.                                                   04000000
      *                                                                 04010000
           EVALUATE SQLCODE                                             04020000
               WHEN 0                                                   04030000
                   DISPLAY 'CONTROL DATES (TMLCNTL)'                    04040000
                   DISPLAY 'FISCAL OPEN END DATE  : ',                  04050000
                                                 MLCNTL-OPN-FSCL-MN-DTE 04060000
                   DISPLAY 'MAXPSTG  MONTH END DTE: ',                  04070000
                                              MLCNTL-MXPSTG-FSCL-MN-DTE 04080000
               WHEN OTHER                                               04090000
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME   04100000
                  MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED              04110000
                  PERFORM 9999-SQL-ABEND                                04120000
           END-EVALUATE.                                                04130000
      *                                                                 04140000
      *                                                                 04150000
      ******************************************************************04160000
      * LOAD ALL DATES TO BE PROCESSED TO INTERNAL TABLE                04170000
      ******************************************************************04180000
       7025-PROCESS-CNTL-TBL.                                           04190000
      *                                                                 04200000
            PERFORM 7050-OPEN-DATE-CURSOR.                              04210000
            PERFORM 7075-PROCESS-DATE-CURSOR                            04220000
                UNTIL PS-DATE-TABLE-LOADED.                             04230000
            PERFORM 7060-CLOSE-DATE-CURSOR.                             04240000
      *                                                                 04250000
      *                                                                 04260000
      ***************************************************************** 04270000
      * OPEN FISCAL DATE CURSOR                                         04280000
      ***************************************************************** 04290000
       7050-OPEN-DATE-CURSOR.                                           04300000
      *                                                                 04310000
           EXEC SQL                                                     04320000
               OPEN FISCAL_DATE_CSR                                     04330000
           END-EXEC                                                     04340000
                                                                        04350000
           EVALUATE SQLCODE                                             04360000
               WHEN 0                                                   04370000
                    CONTINUE                                            04380000
               WHEN OTHER                                               04390000
                  MOVE '7050-OPEN-DATE-CSR' TO PV-PARAGRAPH-NAME        04400000
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED              04410000
                  PERFORM 9999-SQL-ABEND                                04420000
           END-EVALUATE.                                                04430000
      *                                                                 04440000
      *                                                                 04450000
      ***************************************************************** 04460000
      * CLOSE FISCAL DATE CURSOR                                        04470000
      ***************************************************************** 04480000
       7060-CLOSE-DATE-CURSOR.                                          04490000
      *                                                                 04500000
           EXEC SQL                                                     04510000
               CLOSE FISCAL_DATE_CSR                                    04520000
           END-EXEC                                                     04530000
                                                                        04540000
           EVALUATE SQLCODE                                             04550000
               WHEN 0                                                   04560000
                    CONTINUE                                            04570000
               WHEN OTHER                                               04580000
                  MOVE '7060-CLOSE-DATE-CURSOR' TO PV-PARAGRAPH-NAME    04590000
                  MOVE PE-MSG-05 TO PV-OPERATION-ATTEMPTED              04600000
                  PERFORM 9999-SQL-ABEND                                04610000
           END-EVALUATE.                                                04620000
      *                                                                 04630000
      *                                                                 04640000
      ***************************************************************** 04650000
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE         04660000
      ***************************************************************** 04670000
       7075-PROCESS-DATE-CURSOR.                                        04680000
      *                                                                 04690000
           EXEC SQL                                                     04700000
                  FETCH FISCAL_DATE_CSR                                 04710000
                   INTO :PV-HOLD-DATE                                   04720000
           END-EXEC                                                     04730000
                                                                        04740000
           EVALUATE SQLCODE                                             04750000
               WHEN +0                                                  04760000
                   ADD 1 TO PV-SUB1                                     04770000
                   IF PV-SUB1 > PC-MAX-DATE-TABLE                       04780000
                       MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME04790000
                       MOVE PE-MSG-07 TO PV-OPERATION-ATTEMPTED         04800000
                       PERFORM 9999-SQL-ABEND                           04810000
                   END-IF                                               04820000
                   DISPLAY 'HOLD-DTE: ', PV-HOLD-DATE                   04830000
                   DISPLAY 'PV-SUB1 : ', PV-SUB1                        04840000
                   PERFORM 7100-SELECT-DB2-DATES                        04850000
                   PERFORM 7125-LOAD-DATE-TABLE                         04860000
               WHEN +100                                                04870000
                   DISPLAY 'NBR OF MONTHS PROCESSED THIS RUN: ', PV-SUB104880000
                   IF PV-SUB1 > 1                                       04890000
                       DISPLAY '   ', MLCNTL-OPN-FSCL-MN-DTE, ' THRU ' ,04900000
                               MLCNTL-MXPSTG-FSCL-MN-DTE                04910000
                   END-IF                                               04920000
                   MOVE PV-SUB1 TO PV-MAX-DATE-SUB                      04930000
                   MOVE ZEROES  TO PV-SUB1                              04940000
                   SET PS-DATE-TABLE-LOADED TO TRUE                     04950000
               WHEN OTHER                                               04960000
                   MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME    04970000
                   MOVE PE-MSG-04 TO PV-OPERATION-ATTEMPTED             04980000
                   PERFORM 9999-SQL-ABEND                               04990000
           END-EVALUATE.                                                05000000
      *                                                                 05010000
      *                                                                 05020000
      ******************************************************************05030000
      * SELECT INFORMATION NEEDED FOR THE FISCAL MONTH END DATE BEING   05040000
      * PROCESSED                                                       05050000
      ******************************************************************05060000
       7100-SELECT-DB2-DATES.                                           05070000
      *                                                                 05080000
           EXEC SQL                                                     05090000
               SELECT  CAL_MN_NBR                                       05100000
                      ,FSCL_YR_NBR                                      05110000
                INTO  :DTATTR-CAL-MN-NBR                                05120000
                     ,:DTATTR-FSCL-YR-NBR                               05130000
                FROM TDTATTR                                            05140000
                WHERE KY_DTE  = :PV-HOLD-DATE                           05150000
           END-EXEC.                                                    05160000
      *                                                                 05170000
           EVALUATE SQLCODE                                             05180000
               WHEN 0                                                   05190000
                   DISPLAY 'DTATTR-CAL-MN-NBR ', DTATTR-CAL-MN-NBR      05200000
                   DISPLAY 'DTATTR-FSCL-YR-NBR ', DTATTR-FSCL-YR-NBR    05210000
               WHEN +100                                                05220000
                   DISPLAY 'DATE NOT FOUND ON DTATTR ' PV-HOLD-DATE     05230000
               WHEN OTHER                                               05240000
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME     05250000
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED              05260000
                  PERFORM 9999-SQL-ABEND                                05270000
           END-EVALUATE.                                                05280000
      *                                                                 05290000
      *                                                                 05300000
      ***************************************************************** 05310000
      * POPULATE INTERNAL TABLE WITH DATES RELATED TO MONTH-END-DATE    05320000
      ***************************************************************** 05330000
       7125-LOAD-DATE-TABLE.                                            05340000
           MOVE PV-HOLD-DATE            TO DT-OPN-MN-END-DTE(PV-SUB1).  05350000
           MOVE DTATTR-CAL-MN-NBR       TO DT-CAL-MN-NBR(PV-SUB1).      05360000
           MOVE DTATTR-FSCL-YR-NBR      TO DT-FSCL-YR-NBR(PV-SUB1).     05370000
      *                                                                 05380000
      *                                                                 05390000
      ***************************************************************** 05400000
      * RETRIEVES CURRENT MONTH SHRINK PERCENT                          05410000
      ***************************************************************** 05420000
       7200-OBTAIN-SHRINK-PCTS.                                         05430000
      *                                                                 05440000
      * REFORMAT-MONTH CALENDAR MONTH TO NUMERIC FIELD                  05450000
           MOVE DT-CAL-MN-NBR(PV-SUB1)  TO REFORMAT-ALPHA-CALENDAR.     05460000
           MOVE FMT-CALENDAR-MO-NUMERIC TO SA-DPTSHK-SUB.               05470000
C14666     MOVE PV-INPUT-E-BUS-IND      TO DPTSHK-E-BUS-IND             05471002
                                                                        05550000
      *-- RETRIEVE SHRINK WHEN DEPT NOT = PREV DEPT                     05560000
      *-- AND MONTH NOT =  PREV MONTH                                   05570000
      *-- THIS NEEDS TO BE DONE TO ENSURE THE THAT THE MOST CURRENT     05580000
      *-- TMDSEAR KEY AND SHRINK PERCENTAGES ARE CAPTURED.  THIS        05590000
      *-- COULD OCCUR WHEN 2 MONTHS ARE LEFT "OPEN" FOR RECALCING       05600000
      *-- AND THE DEPARTMENT IS REALIGNED INTO ANOTHER GMM/DMM AND      05610000
      *-- THE DKEY CHANGES, AND THE SHRINK PERCENTAGES CHANGE.          05620000
           IF (WKSHNK-DEPT-NBR = PREV-SHRINK-DEPT)                      05630000
               AND                                                      05640000
              (FMT-CALENDAR-MO-NUMERIC = PREV-SHRINK-MONTH)             05650000
C14666         AND                                                      05660000
C14666        (PV-INPUT-E-BUS-IND = PREV-BUS-IND)                       05670000
               CONTINUE                                                 05680000
           ELSE                                                         05690000
               INITIALIZE SA-DPTSHK-ROW                                 05700000
               PERFORM 7220-SELECT-SHRINK-KEY                           05710000
               PERFORM 7240-LOAD-SHRINK-TABLE                           05720000
               MOVE DCLTDPTSHK              TO SA-DPTSHK-ROW            05730000
               MOVE WKSHNK-DEPT-NBR         TO PREV-SHRINK-DEPT         05740000
               MOVE FMT-CALENDAR-MO-NUMERIC TO PREV-SHRINK-MONTH        05750000
C14666         MOVE PV-INPUT-E-BUS-IND      TO PREV-BUS-IND             05760000
           END-IF.                                                      05770000
                                                                        05780000
           MOVE SA-DPTSHK-SHRINK-PCT(SA-DPTSHK-SUB) TO                  05790000
                                                    PV-DEPT-SHRINK-PCT. 05800000
      *                                                                 05810000
      *                                                                 05820000
      ***************************************************************** 05830000
      * OBTAIN MDSEAR_KEY AND DEPT FROM SHRINK AND MBS TABLE            05840000
      ***************************************************************** 05850000
       7220-SELECT-SHRINK-KEY.                                          05860000
      *                                                                 05870000
           EXEC SQL                                                     05880000
               SELECT  A.MDSEAR_DKEY                                    05890000
                      ,A.DEPT_NBR                                       05900000
                      ,B.SHRINK_YR_NBR                                  05910000
               INTO    :MDSEAR-DKEY                                     05920000
                      ,:MDSEAR-DEPT-NBR                                 05930000
                      ,:DPTSHK-SHRINK-YR-NBR                            05940000
               FROM  TMDSEAR A                                          05950000
                    ,TDPTSHK B                                          05960000
               WHERE A.OPERCO_NBR    = '03'                             05970000
                 AND A.MDSELV_CDE    = 'DPT'                            05980000
                 AND A.MDSEAR_DKEY   =  B.MDSEAR_DKEY                   05990000
                 AND A.DEPT_NBR      = :PV-HOLD-DEPT-NBR                06000000
C14666           AND B.E_BUS_IND     = :DPTSHK-E-BUS-IND                06010000
                 AND B.SHRINK_YR_NBR = :PV-HOLD-FSCL-YR-NBR             06020000
                 AND :PV-HOLD-OPN-MN-END-DTE    BETWEEN                 06030000
                                        A.STRT_DTE AND A.END_DTE        06040000
           END-EXEC.                                                    06050000
                                                                        06060000
           EVALUATE TRUE                                                06070000
               WHEN SQLCODE = 0                                         06080000
                   CONTINUE                                             06090000
               WHEN OTHER                                               06100000
                   DISPLAY '                                     '      06110000
                   DISPLAY 'DEPT IN PROCESS : ', PV-HOLD-DEPT-NBR       06120000
                   DISPLAY '    OPEN MONTH  : ',                        06130000
                                             PV-HOLD-OPN-MN-END-DTE     06140000
                   MOVE PE-MSG-06             TO PV-OPERATION-ATTEMPTED 06150000
                   MOVE '7220-SELECT-SHRINK-KEY'                        06160000
                                          TO PV-PARAGRAPH-NAME          06170000
                   PERFORM 9999-SQL-ABEND                               06180000
           END-EVALUATE.                                                06190000
      *                                                                 06200000
      *                                                                 06210000
      ******************************************************************06220000
      * SHRINK PCTS ARE STORED IN TABLE DPTSHK BY CALENDAR MONTH        06230000
      ******************************************************************06240000
       7240-LOAD-SHRINK-TABLE.                                          06250000
                                                                        06260000
           EXEC SQL                                                     06270000
               SELECT  JAN_SHRINK_PCT                                   06280000
                    ,  FEB_SHRINK_PCT                                   06290000
                    ,  MAR_SHRINK_PCT                                   06300000
                    ,  APR_SHRINK_PCT                                   06310000
                    ,  MAY_SHRINK_PCT                                   06320000
                    ,  JUN_SHRINK_PCT                                   06330000
                    ,  JUL_SHRINK_PCT                                   06340000
                    ,  AUG_SHRINK_PCT                                   06350000
                    ,  SEP_SHRINK_PCT                                   06360000
                    ,  OCT_SHRINK_PCT                                   06370000
                    ,  NOV_SHRINK_PCT                                   06380000
                    ,  DEC_SHRINK_PCT                                   06390000
               INTO   :DPTSHK-JAN-SHRINK-PCT                            06400000
                    , :DPTSHK-FEB-SHRINK-PCT                            06410000
                    , :DPTSHK-MAR-SHRINK-PCT                            06420000
                    , :DPTSHK-APR-SHRINK-PCT                            06430000
                    , :DPTSHK-MAY-SHRINK-PCT                            06440000
                    , :DPTSHK-JUN-SHRINK-PCT                            06450000
                    , :DPTSHK-JUL-SHRINK-PCT                            06460000
                    , :DPTSHK-AUG-SHRINK-PCT                            06470000
                    , :DPTSHK-SEP-SHRINK-PCT                            06480000
                    , :DPTSHK-OCT-SHRINK-PCT                            06490000
                    , :DPTSHK-NOV-SHRINK-PCT                            06500000
                    , :DPTSHK-DEC-SHRINK-PCT                            06510000
               FROM    TDPTSHK                                          06520000
               WHERE   MDSEAR_DKEY   = :MDSEAR-DKEY                     06530000
C14666           AND   E_BUS_IND     = :DPTSHK-E-BUS-IND                06540000
                 AND   SHRINK_YR_NBR = :DPTSHK-SHRINK-YR-NBR            06550000
           END-EXEC                                                     06560000
                                                                        06570000
               EVALUATE TRUE                                            06580000
                   WHEN SQLCODE = 0                                     06590000
                       CONTINUE                                         06600000
                   WHEN OTHER                                           06610000
                       MOVE PE-MSG-02         TO PV-OPERATION-ATTEMPTED 06620000
                       MOVE '7240-LOAD-SHRINK-TABLES'                   06630000
                                              TO PV-PARAGRAPH-NAME      06640000
                       PERFORM 9999-SQL-ABEND                           06650000
               END-EVALUATE.                                            06660000
      *                                                                 06670000
      *                                                                 06680000
      ******************************************************************06690000
      *  CLOSE FILES AND DISPLAY FINAL COUNTS                           06700000
      ******************************************************************06710000
       8000-EOJ-ROUTINE.                                                06720000
      *                                                                 06730000
           CLOSE WKSHNK-UNLOAD-FILE                                     06740000
                 WKSHNK-LOAD-FILE                                       06750000
                 WKSHNK-PURGE-FILE.                                     06760000
      *                                                                 06770000
           DISPLAY 'TWKSHNK UNLOAD RECS READ:   ' PA-UNLOAD-RECS-READ.  06780000
           DISPLAY 'TWKSHNK LOAD RECS WRITTEN:  ' PA-LOAD-RECS-WRITTEN. 06790000
           DISPLAY 'TWKSHNK PURGE RECS WRITTEN: ' PA-PURGE-RECS-WRITTEN.06800000
      *                                                                 06810000
      *                                                                 06820000
      ******************************************************************06830000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               06840000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             06850000
      ******************************************************************06860000
       9999-SQL-ABEND.                                                  06870000
                                                                        06880000
           MOVE +4013                 TO ABEND-CODE.                    06890000
           DISPLAY '**  ABEND     **'.                                  06900000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              06910000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            06920000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       06930000
                                                                        06940000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         06950000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        06960000
                                                                        06970000
           COPY DPPD004.                                                06980000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           06990000
      *                                                                 07000000
