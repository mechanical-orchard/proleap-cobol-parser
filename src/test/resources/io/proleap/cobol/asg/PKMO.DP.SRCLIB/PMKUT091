      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.      PMKUT091.                                               
       AUTHOR.          AMANDA NEWHART.                                         
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    FEBRUARY 2003.                                          
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *    --- GET DB2 INFO FOR ORACLE PROGRAMS ---                   *         
      *                                                               *         
      *   GET THE ONHAND-QTY FOR ALL SKUS OF A GIVEN MDSE SELN LINE.  *         
      *****************************************************************         
      * DB2 TABLES:                                                   *         
      *  1.                                  (TSKXREF)                *         
      *  2.                                  (TMDARVS)                *         
      *  3.                                  (TUPC)                   *         
      *  4.                                  (TUPCPLS)                *         
      *                                                               *         
      *****************************************************************         
      *   CHANGES:                                                    *         
      * DATE:         03/01/2004                                                
      * WR/IR#:       P000637338                                                
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  IN STYCSR, CHANGED:                                       
      *   AND ((DATE(C.EFF_END_TMST) > DATE(:PV-FINAL-RUN-DATE))                
      *   TO BE:                                                                
      *   AND ((DATE(C.EFF_END_TMST) >= DATE(:PV-FINAL-RUN-DATE))               
      *   THIS WAS BECAUSE IF SOMETHING CHANGED ON TUPCPLS AS OF THE            
      *   FINAL RUN DATE, WE WOULD GET A NOT FOUND - WHEN IT SHOULD BE          
      *   FOUND.                                                                
      *                                                               *         
      * DATE:         01/30/2005                                      *         
      * PROGRAMMER:   KELLY WIESNER                                   *         
      * DESCRIPTION:  STORE NUMBER EXPANSION PROJECT                  *         
      *               CHANGED STORE_NMBR TO BE LOC_NBR IN TSKST QUERY *         
      *               ALSO ADDED COMMIT LOGIC                         *         
      *                                                               *         
      * 11/08/2005 - DARYL WOELFEL - PM00820409                       *         
      *     - REMOVED EXCESSIVE DISPLAY STATEMENTS.                   *         
      *                                                               *         
AJ1107* 11/09/2007 - AMANDA JEFFORDS - INC000000150399                *         
AJ1107*     - NEED TO SET END OF CURSOR TO TRUE IF THERE IS AN        *         
AJ1107*       ERROR.  CURRENTLY NOT DOING THAT AND IT KEEPS LOOPING   *         
AJ1107*       THROUGH THE CURSOR.  ADDED EXTRA DISPLAY AS WELL.       *         
AJ1107*                                                               *         
AJ1107* 11/11/2007 - AMANDA JEFFORDS - INC000000152176                *         
AJ1107*     - ADD ERROR DISPLAYS AND END CURSOR IN THE OPEN ERROR.    *         
AJ1107*                                                               *         
AJ1107* 11/11/2007 - AMANDA JEFFORDS - INC000000152176                *         
AJ1107*     - ADD THE DATE TO THE STYCSR ERROR MESSAGES.              *         
      *                                                                         
      *    03/06/2014 - COGNIZANT     - INC0592171                    *         
      *    INCREASED THE INTERNAL TABLE SIZE IN COPYBOOK PMRD801      *         
      *    FROM 50000 TO 99999                                        *         
      *                                                                         
      *    03/07/2014 - COGNIZANT     - INC0592171                    *         
      *    CHANGED THE PIC CLAUSE OF PM801-INDEX FROM PIC S9(04)      *         
      *    TO PIC 9(05) IN COPYBOOK PMRD801.                          *         
      *                                                               *         
      *    03/18/2014 - COGNIZANT     - CHG0077249                    *         
      *    CHANGED THE B200-GET-ONHAND-QTY PARA TO MOVE ZEROS TO      *         
      *    PM801-ONHAND-QTY WHEN THE ITEM IS NOT FOUND ON TSKST       *         
      *                                                               *         
P61176*    07/28/2016 - COGNIZANT     - CHG0147460                    *         
P61176*    ADDED LOGIC TO CHECK IF PV-SKU-NBR IS EMPTY OR NOT.        *         
P61176*    IN CASE IF THE SKU FIELD IS HAVING VALUE DIRECTLY GO TO SKU*         
P61176*    CURSOR IRRESPECTIVE OF IT'S MDSE HIER CODE.                *         
      *                                                               *         
161213*    12/21/2016 - COGNIZANT     - CHG0161213                    *         
161213*    MODIFIED CODE TO DETECT CORRECT HIER CODE WHICH WILL       *         
161213*    IMPROVE CPU UTILIZATION.                                   *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-DPT                  PIC  X(03)  VALUE 'DPT'.                 
           05  PC-MCL                  PIC  X(03)  VALUE 'MCL'.                 
           05  PC-SCL                  PIC  X(03)  VALUE 'SCL'.                 
           05  PC-STY                  PIC  X(03)  VALUE 'STY'.                 
           05  PC-SKU                  PIC  X(03)  VALUE 'SKU'.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DPT-NBR              PIC S9(04)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-MCL-NBR              PIC S9(04)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-SCL-NBR              PIC S9(04)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-VS-ID                PIC S9(09)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-SKU-NBR              PIC S9(08)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-ONHAND-QTY           PIC S9(07)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-SKU-OH-SUB           PIC  9(05)  VALUE ZEROES.                
           05  PV-ITM-NBR              PIC S9(15)V COMP-3                       
                                                   VALUE ZEROES.                
           05  PV-FINAL-RUN-DATE       PIC  X(10)  VALUE SPACES.                
           05  PV-PASS-IN-RUN-DATE.                                             
               10  PV-PI-RUN-DATE-YY   PIC  X(04)  VALUE SPACES.                
               10  PV-PI-RUN-DATE-MM   PIC  X(02)  VALUE SPACES.                
               10  PV-PI-RUN-DATE-DD   PIC  X(02)  VALUE SPACES.                
           05  PV-RUN-DATE.                                                     
               10  PV-RUN-DATE-YY      PIC  X(04)  VALUE SPACES.                
               10  FILLER              PIC  X(01)  VALUE '-'.                   
               10  PV-RUN-DATE-MM      PIC  X(02)  VALUE SPACES.                
               10  FILLER              PIC  X(01)  VALUE '-'.                   
               10  PV-RUN-DATE-DD      PIC  X(02)  VALUE SPACES.                
           05  PV-RUN-TIME                  PIC X(26)  VALUE SPACES.            
           05  WS-TMST                      PIC X(26)  VALUE SPACES.            
                                                                                
       01  BAD-SQL-ERR-MSG.                                                     
           05  BAD-SQL-ERROR-MSG       PIC  X(60)  VALUE SPACES.                
           05  FILLER                  PIC  X(11)  VALUE ' SQLCODE = '.         
           05  BAD-SQL-CODE            PIC  S9(4)  VALUE ZERO.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-DPT-ITEMS-RETRIEVED       PIC S9(11)  COMP-3                  
                                                           VALUE ZEROES.        
           05  PA-MCL-ITEMS-RETRIEVED       PIC S9(11)  COMP-3                  
                                                           VALUE ZEROES.        
           05  PA-SCL-ITEMS-RETRIEVED       PIC S9(11)  COMP-3                  
                                                           VALUE ZEROES.        
           05  PA-STY-ITEMS-RETRIEVED       PIC S9(11)  COMP-3                  
                                                           VALUE ZEROES.        
           05  PA-SKU-ITEMS-RETRIEVED       PIC S9(11)  COMP-3                  
                                                           VALUE ZEROES.        
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DPT-CSR-SW            PIC X(01)     VALUE 'N'.         
               88  PS-NOT-END-OF-DPT-CSR                     VALUE 'N'.         
               88  PS-END-OF-DPT-CSR                         VALUE 'Y'.         
           05  PS-END-OF-MCL-CSR-SW            PIC X(01)     VALUE 'N'.         
               88  PS-NOT-END-OF-MCL-CSR                     VALUE 'N'.         
               88  PS-END-OF-MCL-CSR                         VALUE 'Y'.         
           05  PS-END-OF-SCL-CSR-SW            PIC X(01)     VALUE 'N'.         
               88  PS-NOT-END-OF-SCL-CSR                     VALUE 'N'.         
               88  PS-END-OF-SCL-CSR                         VALUE 'Y'.         
           05  PS-END-OF-STY-CSR-SW            PIC X(01)     VALUE 'N'.         
               88  PS-NOT-END-OF-STY-CSR                     VALUE 'N'.         
               88  PS-END-OF-STY-CSR                         VALUE 'Y'.         
                                                                                
      *****************************************************************         
      *  DB2 MESSAGE AREA                                                       
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *    DB2 AREA FOR TSKXREF                                       *         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    DB2 AREA FOR TMDARVS                                       *         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMDARVS                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    DB2 AREA FOR TUPC                                          *         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPC                                                    
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    DB2 AREA FOR TUPCPLS                                       *         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    DB2 AREA FOR TSKST                                         *         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKST                                                   
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA DCLGEN                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DPTCSR                                                                 
      *    12/14/2004 - ADDED WITH HOLD TO THIS CURSOR BECAUSE                  
      *    OF THE COMMIT LOGIC I'M ADDING - IF WITH HOLD ISN'T                  
      *    SPECIFIED - THE CURSOR WILL CLOSE UPON A COMMIT                      
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE DPTCSR CURSOR WITH HOLD                                  
               FOR SELECT DISTINCT                                              
                   ITM_NBR,                                                     
                   SKU                                                          
               FROM                                                             
                   TSKXREF                                                      
               WHERE                                                            
                     DEPT = :PV-DPT-NBR                                         
                 AND SKU_STAT_CDE IN ('10','20','25','40','90')                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  MCLCSR                                                                 
      *    12/14/2004 - ADDED WITH HOLD TO THIS CURSOR BECAUSE                  
      *    OF THE COMMIT LOGIC I'M ADDING - IF WITH HOLD ISN'T                  
      *    SPECIFIED - THE CURSOR WILL CLOSE UPON A COMMIT                      
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE MCLCSR CURSOR WITH HOLD                                  
               FOR SELECT DISTINCT                                              
                   ITM_NBR,                                                     
                   SKU                                                          
               FROM                                                             
                   TSKXREF                                                      
               WHERE                                                            
                     DEPT = :PV-DPT-NBR                                         
                 AND CLASS = :PV-MCL-NBR                                        
                 AND SKU_STAT_CDE IN ('10','20','25','40','90')                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  SCLCSR                                                                 
      *    12/14/2004 - ADDED WITH HOLD TO THIS CURSOR BECAUSE                  
      *    OF THE COMMIT LOGIC I'M ADDING - IF WITH HOLD ISN'T                  
      *    SPECIFIED - THE CURSOR WILL CLOSE UPON A COMMIT                      
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE SCLCSR CURSOR WITH HOLD                                  
               FOR SELECT DISTINCT                                              
                   ITM_NBR,                                                     
                   SKU                                                          
               FROM                                                             
                   TSKXREF                                                      
               WHERE                                                            
                     DEPT = :PV-DPT-NBR                                         
                 AND CLASS = :PV-MCL-NBR                                        
                 AND SUBCLASS = :PV-SCL-NBR                                     
                 AND SKU_STAT_CDE IN ('10','20','25','40','90')                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  STYCSR                                                                 
      *    12/14/2004 - ADDED WITH HOLD TO THIS CURSOR BECAUSE                  
      *    OF THE COMMIT LOGIC I'M ADDING - IF WITH HOLD ISN'T                  
      *    SPECIFIED - THE CURSOR WILL CLOSE UPON A COMMIT                      
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE STYCSR CURSOR WITH HOLD                                  
               FOR SELECT DISTINCT                                              
                   C.ITM_NBR,                                                   
                   D.SKU                                                        
               FROM                                                             
                   TMDARVS A,                                                   
                   TUPC B,                                                      
                   TUPCPLS C,                                                   
                   TSKXREF D                                                    
               WHERE                                                            
                     A.DEPT_NBR = :PV-DPT-NBR                                   
                 AND A.MAJ_CL_NBR = :PV-MCL-NBR                                 
                 AND A.SUB_CL_NBR = :PV-SCL-NBR                                 
                 AND A.VS_ID = :PV-VS-ID                                        
                 AND A.VS_ID = B.VS_ID                                          
                 AND A.SUB_CL_MDSEAR_ID = B.SUB_CL_MDSEAR_ID                    
                 AND B.UPC_TYP_CDE = 'I'                                        
                 AND B.UPC_ID = C.UPC_ID                                        
                 AND C.ITM_NBR = D.ITM_NBR                                      
                 AND C.OPERCO_NBR = '03'                                        
                 AND C.STR_NBR = 0                                              
                 AND C.UPC_STAT_CDE IN ('10','20','25','40','90')               
                 AND DATE(C.EFF_BEG_TMST) <= DATE(:PV-FINAL-RUN-DATE)           
                 AND ((DATE(C.EFF_END_TMST) >= DATE(:PV-FINAL-RUN-DATE))        
                   OR (C.EFF_END_TMST IS NULL))                                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  SKUCSR                                                                 
      *    12/14/2004 - ADDED WITH HOLD TO THIS CURSOR BECAUSE                  
      *    OF THE COMMIT LOGIC I'M ADDING - IF WITH HOLD ISN'T                  
      *    SPECIFIED - THE CURSOR WILL CLOSE UPON A COMMIT                      
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE SKUCSR CURSOR WITH HOLD                                  
               FOR SELECT DISTINCT                                              
                   ITM_NBR                                                      
               FROM                                                             
                   TSKXREF                                                      
               WHERE                                                            
                     SKU = :PV-SKU-NBR                                          
                 AND SKU_STAT_CDE IN ('10','20','25','40','90')                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  BAD SQL CALL DATA AREAS                                       *        
      *****************************************************************         
       01  BAD-SQL-CALL-AREA.                                                   
           05  BAD-ONHAND-CALL-MSG        PIC  X(60)  VALUE                     
            'UNSUCCESSFUL ONHAND SELECT ON TSKST TABLE'.                        
           05  NOT-FOUND-CALL-MSG         PIC  X(60)  VALUE                     
            'ROW NOT FOUND ON TSKST TABLE'.                                     
           05  INVALID-FUNCTION-CODE-MSG  PIC  X(60)  VALUE                     
            'INVALID FUNCTION CODE PASSED'.                                     
           05  INVALID-DPTCSR-OPEN-MSG    PIC  X(60)  VALUE                     
            'OPEN ERROR ON DPTCSR'.                                             
           05  INVALID-MCLCSR-OPEN-MSG    PIC  X(60)  VALUE                     
            'OPEN ERROR ON MCLCSR'.                                             
           05  INVALID-SCLCSR-OPEN-MSG    PIC  X(60)  VALUE                     
            'OPEN ERROR ON SCLCSR'.                                             
           05  INVALID-STYCSR-OPEN-MSG    PIC  X(60)  VALUE                     
            'OPEN ERROR ON STYCSR'.                                             
           05  INVALID-SKUCSR-OPEN-MSG    PIC  X(60)  VALUE                     
            'OPEN ERROR ON SKUCSR'.                                             
           05  INVALID-DPTCSR-FETCH-MSG PIC    X(60)  VALUE                     
            'FETCH ERROR ON DPTCSR'.                                            
           05  INVALID-MCLCSR-FETCH-MSG   PIC  X(60)  VALUE                     
            'FETCH ERROR ON MCLCSR'.                                            
           05  INVALID-SCLCSR-FETCH-MSG   PIC  X(60)  VALUE                     
            'FETCH ERROR ON SCLCSR'.                                            
           05  INVALID-STYCSR-FETCH-MSG   PIC  X(60)  VALUE                     
            'FETCH ERROR ON STYCSR'.                                            
           05  INVALID-SKUCSR-FETCH-MSG   PIC  X(60)  VALUE                     
            'FETCH ERROR ON SKUCSR'.                                            
           05 INVALID-DPTCSR-CLOSE-MSG    PIC  X(60)  VALUE                     
            'CLOSE ERROR ON DPTCSR'.                                            
           05 INVALID-MCLCSR-CLOSE-MSG    PIC  X(60)  VALUE                     
            'CLOSE ERROR ON MCLCSR'.                                            
           05 INVALID-SCLCSR-CLOSE-MSG    PIC  X(60)  VALUE                     
            'CLOSE ERROR ON SCLCSR'.                                            
           05 INVALID-STYCSR-CLOSE-MSG    PIC  X(60)  VALUE                     
            'CLOSE ERROR ON STYCSR'.                                            
           05 INVALID-SKUCSR-CLOSE-MSG    PIC  X(60)  VALUE                     
            'CLOSE ERROR ON SKUCSR'.                                            
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
        LINKAGE SECTION.                                                        
      ********************                                                      
           COPY PMRD801.                                                        
      *******************************************                               
       PROCEDURE DIVISION USING PM801-PARAMETERS.                               
      *******************************************                               
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET THE HIERARCHY CURSORS                                *         
      *  - MOVE PMRD VARIABLES TO LOCAL VARIABLES                     *         
      *  - DEPENDED ON MDSE HIER CODE OPEN UP THE CORRECT CURSOR      *         
      * ==> PERFORMED FROM:  START                                    *         
      *****************************************************************         
       A100-MAIN.                                                               
                                                                                
           SET PS-NOT-END-OF-DPT-CSR TO TRUE.                                   
           SET PS-NOT-END-OF-MCL-CSR TO TRUE.                                   
           SET PS-NOT-END-OF-SCL-CSR TO TRUE.                                   
           SET PS-NOT-END-OF-STY-CSR TO TRUE.                                   
                                                                                
           PERFORM A200-FIND-TIME.                                              
           INITIALIZE PM801-GET-QTY-OUTPUT-PARMS.                               
                                                                                
           EVALUATE TRUE                                                        
               WHEN PM801-GET-ONHAND-QTY                                        
                   MOVE PM801-DEPT-NBR          TO PV-DPT-NBR                   
                   MOVE PM801-MCL-NBR           TO PV-MCL-NBR                   
                   MOVE PM801-SCL-NBR           TO PV-SCL-NBR                   
                   MOVE PM801-VS-ID             TO PV-VS-ID                     
                   MOVE PM801-SKU-NBR           TO PV-SKU-NBR                   
                   MOVE PM801-RUN-DATE          TO PV-PASS-IN-RUN-DATE          
                   MOVE PV-PI-RUN-DATE-YY       TO PV-RUN-DATE-YY               
                   MOVE PV-PI-RUN-DATE-MM       TO PV-RUN-DATE-MM               
                   MOVE PV-PI-RUN-DATE-DD       TO PV-RUN-DATE-DD               
                   MOVE PV-RUN-DATE             TO PV-FINAL-RUN-DATE            
                   MOVE 0                       TO PV-SKU-OH-SUB                
                   MOVE 0                       TO PM801-INDEX                  
                                                                                
161213             IF PM801-SKU-NBR NOT EQUAL TO ZEROS                          
161213               MOVE 'SKU' TO PM801-HIER-LVL-CDE                           
161213             ELSE                                                         
161213                IF PM801-VS-ID NOT EQUAL TO ZEROS                         
161213                  MOVE 'STY' TO PM801-HIER-LVL-CDE                        
161213                ELSE                                                      
161213                   IF PM801-SCL-NBR NOT EQUAL TO ZEROS                    
161213                     MOVE 'SCL' TO PM801-HIER-LVL-CDE                     
161213                   ELSE                                                   
161213                      IF PM801-MCL-NBR NOT EQUAL TO ZEROS                 
161213                        MOVE 'MCL' TO PM801-HIER-LVL-CDE                  
161213                      ELSE                                                
161213                        MOVE 'DPT' TO PM801-HIER-LVL-CDE                  
161213                      END-IF                                              
161213                   END-IF                                                 
161213                END-IF                                                    
161213             END-IF                                                       
                                                                                
                   EVALUATE TRUE                                                
                       WHEN PM801-HIER-LVL-CDE = PC-DPT                         
                            PERFORM B100-OPEN-DPTCSR                            
                            PERFORM C100-FETCH-DPTCSR                           
                                UNTIL PS-END-OF-DPT-CSR                         
                            PERFORM D100-CLOSE-DPTCSR                           
                       WHEN PM801-HIER-LVL-CDE = PC-MCL                         
                            PERFORM B110-OPEN-MCLCSR                            
                            PERFORM C110-FETCH-MCLCSR                           
                                UNTIL PS-END-OF-MCL-CSR                         
                            PERFORM D110-CLOSE-MCLCSR                           
                       WHEN PM801-HIER-LVL-CDE = PC-SCL                         
                            PERFORM B120-OPEN-SCLCSR                            
                            PERFORM C120-FETCH-SCLCSR                           
                                UNTIL PS-END-OF-SCL-CSR                         
                            PERFORM D120-CLOSE-SCLCSR                           
                       WHEN PM801-HIER-LVL-CDE = PC-STY                         
                            PERFORM B130-OPEN-STYCSR                            
                            PERFORM C130-FETCH-STYCSR                           
                                UNTIL PS-END-OF-STY-CSR                         
                            PERFORM D130-CLOSE-STYCSR                           
                       WHEN PM801-HIER-LVL-CDE = PC-SKU                         
                            PERFORM C140-SELECT-SKU                             
                   END-EVALUATE                                                 
                                                                                
               WHEN OTHER                                                       
                   MOVE ZEROES TO PM801-SQLCODE                                 
                   MOVE INVALID-FUNCTION-CODE-MSG                               
                                               TO PM801-ERROR-MESSAGE           
                   SET PM801-UNSUCCESSFUL-CALL TO TRUE                          
           END-EVALUATE.                                                        
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
                                                                                
       A200-FIND-TIME.                                                          
                                                                                
           EXEC SQL                                                             
               SELECT (CURRENT_TIMESTAMP + 30 SECONDS)                          
               INTO   :PV-RUN-TIME                                              
               FROM   SYSIBM.SYSDUMMY1                                          
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      *            DISPLAY 'PV-RUN-TIME = ' PV-RUN-TIME                         
                   CONTINUE                                                     
                                                                                
                WHEN OTHER                                                      
                 MOVE ZEROES                 TO PM801-ONHAND-QTY(1)             
                 MOVE ZEROES                 TO PM801-SKU-NBR-OUT(1)            
                 SET PM801-UNSUCCESSFUL-CALL TO TRUE                            
                 MOVE 'A200-FIND-TIME'       TO BAD-SQL-ERROR-MSG               
                 MOVE SQLCODE                TO BAD-SQL-CODE                    
                 MOVE BAD-SQL-ERR-MSG        TO PM801-ERROR-MESSAGE             
           END-EVALUATE.                                                        
                                                                                
       A300-CHECK-TIME.                                                         
                                                                                
           EXEC SQL                                                             
               SET :WS-TMST = CURRENT_TIMESTAMP                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      *            DISPLAY 'WS-TMST = ' WS-TMST                                 
                   CONTINUE                                                     
                                                                                
                WHEN OTHER                                                      
                 MOVE ZEROES                 TO PM801-ONHAND-QTY(1)             
                 MOVE ZEROES                 TO PM801-SKU-NBR-OUT(1)            
                 SET PM801-UNSUCCESSFUL-CALL TO TRUE                            
                 MOVE 'A300-CHECK-TIME'      TO BAD-SQL-ERROR-MSG               
                 MOVE SQLCODE                TO BAD-SQL-CODE                    
                 MOVE BAD-SQL-ERR-MSG        TO PM801-ERROR-MESSAGE             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       A400-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'COMMIT'                                             
                                                                                
                WHEN OTHER                                                      
                 MOVE ZEROES                 TO PM801-ONHAND-QTY(1)             
                 MOVE ZEROES                 TO PM801-SKU-NBR-OUT(1)            
                 SET PM801-UNSUCCESSFUL-CALL TO TRUE                            
                 MOVE 'A400-COMMIT'          TO BAD-SQL-ERROR-MSG               
                 MOVE SQLCODE                TO BAD-SQL-CODE                    
                 MOVE BAD-SQL-ERR-MSG        TO PM801-ERROR-MESSAGE             
           END-EVALUATE.                                                        
      *****************************************************************         
      *     B100-OPEN-DPTCSR                                           *        
      * ==> PROCESSES:                                                 *        
      *     -OPENS THE DPT CURSOR                                     *         
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       B100-OPEN-DPTCSR.                                                        
                                                                                
           EXEC SQL                                                             
               OPEN DPTCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-DPTCSR-OPEN-MSG TO BAD-SQL-ERROR-MSG            
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-DPT-CSR         TO TRUE                        
AJ1107             DISPLAY 'B100-OPEN-DPTCSR'                                   
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      *     B110-OPEN-MCLCSR                                          *         
      * ==> PROCESSES:                                                *         
      *     -OPENS THE MCL CURSOR                                     *         
      * ==> PERFORMED FROM:  A100-MAIN                                *         
      *****************************************************************         
       B110-OPEN-MCLCSR.                                                        
                                                                                
           EXEC SQL                                                             
               OPEN MCLCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-MCLCSR-OPEN-MSG TO BAD-SQL-ERROR-MSG            
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-MCL-CSR         TO TRUE                        
AJ1107             DISPLAY 'B110-OPEN-MCLCSR'                                   
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      *     B120-OPEN-SCLCSR                                          *         
      * ==> PROCESSES:                                                *         
      *     -OPENS THE SCL CURSOR                                     *         
      * ==> PERFORMED FROM:  A100-MAIN                                *         
      *****************************************************************         
       B120-OPEN-SCLCSR.                                                        
                                                                                
           EXEC SQL                                                             
               OPEN SCLCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-SCLCSR-OPEN-MSG TO BAD-SQL-ERROR-MSG            
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-SCL-CSR         TO TRUE                        
AJ1107             DISPLAY 'B120-OPEN-SCLCSR'                                   
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 SCL NBR: ' PV-SCL-NBR                      
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      *     B130-OPEN-STYCSR                                          *         
      * ==> PROCESSES:                                                *         
      *     -OPENS THE STY CURSOR                                     *         
      * ==> PERFORMED FROM:  A100-MAIN                                *         
      *****************************************************************         
       B130-OPEN-STYCSR.                                                        
                                                                                
           EXEC SQL                                                             
               OPEN STYCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-STYCSR-OPEN-MSG TO BAD-SQL-ERROR-MSG            
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-STY-CSR         TO TRUE                        
AJ1107             DISPLAY 'B130-OPEN-STYCSR'                                   
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 SCL NBR: ' PV-SCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 VS ID: ' PV-VS-ID                          
AJ1107             DISPLAY 'PMKUT091 RUN DATE: ' PV-FINAL-RUN-DATE              
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      *     B200-GET-ONHAND-QTY                                       *         
      * ==> PROCESSES:                                                *         
      *     -FIND THE ON-HAND QTY FOR EACH ITEM.                      *         
      * ==> PERFORMED FROM:  C100-FETCH-DPTCSR, C110-FETCH-MCLCSR,    *         
      *     C120-FETCH-SCLCSR, C130-FETCH-STYCSR, C140-SELECT-SKUCSR  *         
      *****************************************************************         
       B200-GET-ONHAND-QTY.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   ONHAND_QTY                                                   
               INTO                                                             
                   :TSKST-ONHAND-QTY                                            
               FROM                                                             
                   TSKST                                                        
               WHERE                                                            
                     ITEM_NMBR = :PV-ITM-NBR                                    
                 AND LOC_NBR   = SMALLINT(0)                                    
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
                 ADD +1                      TO PV-SKU-OH-SUB                   
                 MOVE TSKST-ONHAND-QTY                                          
                                   TO PM801-ONHAND-QTY(PV-SKU-OH-SUB)           
                 MOVE PV-SKU-NBR                                                
                                   TO PM801-SKU-NBR-OUT(PV-SKU-OH-SUB)          
                 SET PM801-SUCCESSFUL-CALL   TO TRUE                            
                 MOVE SPACES                 TO PM801-ERROR-MESSAGE             
             WHEN SQLCODE = +100                                                
                 ADD +1                      TO PV-SKU-OH-SUB                   
                 MOVE ZEROES                                                    
                                   TO PM801-ONHAND-QTY(PV-SKU-OH-SUB)           
                 MOVE PV-SKU-NBR                                                
                                   TO PM801-SKU-NBR-OUT(PV-SKU-OH-SUB)          
                 SET PM801-SUCCESSFUL-CALL   TO TRUE                            
                 MOVE SPACES                 TO PM801-ERROR-MESSAGE             
                                                                                
             WHEN OTHER                                                         
                 MOVE ZEROES                 TO PM801-ONHAND-QTY(1)             
                 MOVE ZEROES                 TO PM801-SKU-NBR-OUT(1)            
                 SET PM801-UNSUCCESSFUL-CALL TO TRUE                            
                 MOVE BAD-ONHAND-CALL-MSG    TO BAD-SQL-ERROR-MSG               
                 MOVE SQLCODE                TO BAD-SQL-CODE                    
                 MOVE BAD-SQL-ERR-MSG        TO PM801-ERROR-MESSAGE             
           END-EVALUATE.                                                        
                                                                                
           PERFORM A300-CHECK-TIME.                                             
           IF WS-TMST > PV-RUN-TIME                                             
              PERFORM A400-COMMIT                                               
              PERFORM A200-FIND-TIME                                            
           END-IF.                                                              
                                                                                
           MOVE PV-SKU-OH-SUB                TO PM801-INDEX.                    
           MOVE SQLCODE                      TO PM801-SQLCODE.                  
                                                                                
      ******************************************************************        
      *     C100-FETCH-DPTCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     -FETCHES A ROW FROM THE DPT CURSOR                         *        
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       C100-FETCH-DPTCSR.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH  DPTCSR                                                    
                INTO  :SKXREF-ITM-NBR                                           
                     ,:PV-SKU-NBR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1            TO PA-DPT-ITEMS-RETRIEVED                  
                   MOVE SKXREF-ITM-NBR TO PV-ITM-NBR                            
                   PERFORM B200-GET-ONHAND-QTY                                  
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES TO PA-DPT-ITEMS-RETRIEVED                        
                   SET PM801-SUCCESSFUL-CALL      TO TRUE                       
                   SET PS-END-OF-DPT-CSR           TO TRUE                      
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-DPTCSR-FETCH-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-DPT-CSR         TO TRUE                        
AJ1107             DISPLAY 'C100-FETCH-DPTCSR'                                  
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C110-FETCH-MCLCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     -FETCHES A ROW FROM THE MCL CURSOR                         *        
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       C110-FETCH-MCLCSR.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH  MCLCSR                                                    
                INTO  :SKXREF-ITM-NBR                                           
                     ,:PV-SKU-NBR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1            TO PA-MCL-ITEMS-RETRIEVED                  
                   MOVE SKXREF-ITM-NBR TO PV-ITM-NBR                            
                   PERFORM B200-GET-ONHAND-QTY                                  
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES TO PA-MCL-ITEMS-RETRIEVED                        
                   SET PM801-SUCCESSFUL-CALL      TO TRUE                       
                   SET PS-END-OF-MCL-CSR          TO TRUE                       
               WHEN OTHER                                                       
                   DISPLAY '  PMKUT091 ERROR'                                   
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-MCLCSR-FETCH-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-MCL-CSR         TO TRUE                        
AJ1107             DISPLAY 'C110-FETCH-MCLCSR'                                  
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C120-FETCH-SCLCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     -FETCHES A ROW FROM THE SCL CURSOR                         *        
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       C120-FETCH-SCLCSR.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH  SCLCSR                                                    
                INTO :SKXREF-ITM-NBR                                            
                     ,:PV-SKU-NBR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1            TO PA-SCL-ITEMS-RETRIEVED                  
                   MOVE SKXREF-ITM-NBR TO PV-ITM-NBR                            
                   PERFORM B200-GET-ONHAND-QTY                                  
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES TO PA-SCL-ITEMS-RETRIEVED                        
                   SET PM801-SUCCESSFUL-CALL      TO TRUE                       
                   SET PS-END-OF-SCL-CSR          TO TRUE                       
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-SCLCSR-FETCH-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-SCL-CSR         TO TRUE                        
AJ1107             DISPLAY 'C120-FETCH-SCLCSR'                                  
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 SCL NBR: ' PV-SCL-NBR                      
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C130-FETCH-STYCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     -FETCHES A ROW FROM THE STY CURSOR                         *        
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       C130-FETCH-STYCSR.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH  STYCSR                                                    
                INTO :UPCPLS-ITM-NBR                                            
                     ,:PV-SKU-NBR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1            TO PA-STY-ITEMS-RETRIEVED                  
                   MOVE UPCPLS-ITM-NBR TO PV-ITM-NBR                            
                   PERFORM B200-GET-ONHAND-QTY                                  
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES TO PA-STY-ITEMS-RETRIEVED                        
                   SET PM801-SUCCESSFUL-CALL      TO TRUE                       
                   SET PS-END-OF-STY-CSR          TO TRUE                       
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-STYCSR-FETCH-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             SET PS-END-OF-STY-CSR         TO TRUE                        
AJ1107             DISPLAY 'C130-FETCH-STYCSR'                                  
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 DEPT NBR: ' PV-DPT-NBR                     
AJ1107             DISPLAY 'PMKUT091 MCL NBR: ' PV-MCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 SCL NBR: ' PV-SCL-NBR                      
AJ1107             DISPLAY 'PMKUT091 VS ID: ' PV-VS-ID                          
AJ1107             DISPLAY 'PMKUT091 RUN DATE: ' PV-FINAL-RUN-DATE              
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C140-SELECT-SKU                                            *        
      * ==> PROCESSES:                                                 *        
      *     -FETCHES A ROW FROM THE SKU CURSOR                         *        
      * ==> PERFORMED FROM:  A100-MAIN                                 *        
      ******************************************************************        
       C140-SELECT-SKU.                                                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   ITM_NBR                                                      
               INTO                                                             
                   :SKXREF-ITM-NBR                                              
               FROM                                                             
                   TSKXREF                                                      
               WHERE                                                            
                     SKU = :PV-SKU-NBR                                          
                 AND SKU_STAT_CDE IN ('10','20','25','40','90')                 
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1            TO PA-SKU-ITEMS-RETRIEVED                  
                   MOVE SKXREF-ITM-NBR TO PV-ITM-NBR                            
                   PERFORM B200-GET-ONHAND-QTY                                  
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES TO PA-SKU-ITEMS-RETRIEVED                        
                   SET PM801-SUCCESSFUL-CALL      TO TRUE                       
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-SKUCSR-FETCH-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
AJ1107             DISPLAY 'PMKUT091 SQLCODE ERROR: ' BAD-SQL-CODE              
AJ1107             DISPLAY 'PMKUT091 SKU NBR: ' PV-SKU-NBR                      
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D100-CLOSE-DPTCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE DPT CURSOR                                    *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       D100-CLOSE-DPTCSR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE DPTCSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-DPTCSR-CLOSE-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D110-CLOSE-MCLCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE MCL CURSOR                                    *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       D110-CLOSE-MCLCSR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE MCLCSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-MCLCSR-CLOSE-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D120-CLOSE-SCLCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE SCL CURSOR                                    *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       D120-CLOSE-SCLCSR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE SCLCSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL    TO TRUE                       
                   MOVE INVALID-SCLCSR-CLOSE-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D130-CLOSE-STYCSR                                          *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE STY CURSOR                                    *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       D130-CLOSE-STYCSR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE STYCSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE ZEROES                   TO PM801-SKU-NBR-OUT(1)        
                   MOVE ZEROES                   TO PM801-ONHAND-QTY(1)         
                   SET PM801-UNSUCCESSFUL-CALL   TO TRUE                        
                   MOVE INVALID-STYCSR-CLOSE-MSG TO BAD-SQL-ERROR-MSG           
                   MOVE SQLCODE                  TO BAD-SQL-CODE                
                   MOVE BAD-SQL-ERR-MSG          TO PM801-ERROR-MESSAGE         
           END-EVALUATE.                                                        
                                                                                
