      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    TFKCS438.                                                 
       AUTHOR.        JACK KRAMER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  OCTOBER 1996.                                             
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    P438 - TRANSFER LINE REVERSAL COMMENTS.                    *         
      *                                                               *         
      *    THIS PROGRAM ALLOWS ENTRY OF COMMENTS FOR AUTOMATIC OR     *         
      *    MANUAL REVERSALS OF POSTING ADJUSTMENTS.                   *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *****************************************************************         
100404**DATE:10/04/2004 CHANGED BY: RAY GRAF                                    
100404*      EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                        
      *****************************************************************         
SR3403**DATE:02/29/2012 CHANGED BY: JALAJ KUMAR                                 
SR3403*      EXPAND AMOUNT FIELD FROM 4 DIGIT TO 5 DIGITS                       
      *****************************************************************         
CICS  * CHANGED 11/17/2022    BY JIM HUNTER      WR: CHG0278345                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-XFER-DKEY-ID                PIC S9(09)  COMP.                 
           05  DK-XFER-LNE-NBR                PIC S9(04)  COMP.                 
           05  DK-PSTG-SEQ-NBR                PIC S9(04)  COMP.                 
           05  DK-STORE-NBR                   PIC S9(04)  COMP.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'TF438A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'TFKM438 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'TFKCS438'.           
           05  PC-AUTO-REVERSAL-FORMAT        PIC  X(01)  VALUE '1'.            
           05  PC-MANUAL-REVERSAL-FORMAT      PIC  X(01)  VALUE '2'.            
           05  PC-DISPLAY-FORMAT              PIC  X(01)  VALUE '3'.            
           05  PC-COMPLETE-CODE               PIC  X(02)  VALUE 'CM'.           
           05  PC-MANUAL-REVERSAL             PIC  X(02)  VALUE 'MR'.           
           05  PC-AUTO-REVERSAL               PIC  X(02)  VALUE 'AR'.           
           05  PC-ADJUSTMENT                  PIC  X(02)  VALUE 'AD'.           
           05  PC-PRICE-ONLY                  PIC  X(02)  VALUE 'ML'.           
           05  PC-POSITIVE-LITERAL            PIC  X(09)  VALUE                 
               '-POSITIVE'.                                                     
           05  PC-POSITIVE-RECEIPT            PIC  X(02)  VALUE 'PR'.           
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +8              
                                                          COMP SYNC.            
           05  PC-COMMENT-LENGTH              PIC S9(04)  VALUE +316            
                                                          COMP SYNC.            
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
                                                           COMP-3.              
                                                                                
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00007                PIC 9(5)    VALUE 00007.          
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
           05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.          
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'Y'.           
               88  PS-ERROR                                VALUE 'N'.           
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-LIST-END                             VALUE 'Y'.           
               88  PS-NO-LIST-END                          VALUE 'N'.           
           EJECT                                                                
       01  VF-VARCHAR-FIELDS.                                                   
           05  VF-VARCHAR-SW                  PIC X(01)  VALUE ' '.             
               88  VF-VARCHAR-BEGIN                      VALUE 'B'.             
               88  VF-VARCHAR-END                        VALUE 'E'.             
           05  VF-VARCHAR-WORK-FIELD.                                           
               10  VF-VARCHAR                 OCCURS 320 TIMES                  
                                              INDEXED BY VF-IND                 
                                              PIC X.                            
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SPACE-COUNTER               PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-ITEM                        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-RETRY-COUNTER               PIC S9(4)    COMP.                
           05  PV-XFER-NBR.                                                     
100404*        10  PV-XFER-FROM-STORE         PIC X(03).                        
100404         10  PV-XFER-FROM-STORE         PIC X(04).                        
               10                             PIC X   VALUE '-'.                
               10  PV-XFER-SEQ-NBR            PIC X(06).                        
           05  PV-DATE.                                                         
               10  PV-MONTH                   PIC X(02).                        
               10                             PIC X(01)  VALUE '/'.             
               10  PV-DAY                     PIC X(02).                        
               10                             PIC X(01)  VALUE '/'.             
               10  PV-YEAR                    PIC X(04).                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      *    INTER-APPLICATION COMMAREA.                                *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY TFWS003.                                                        
      *---------------------------------------------------------------*         
      *    SPECIFIC COMMMAREA FOR DPKCS040.                           *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-XFER-DKEY-ID                    PIC S9(09)           
                                                           COMP.                
                   15  ASC-XFER-LNE-NBR                    PIC S9(04)           
                                                           COMP.                
                   15  ASC-XFER-SEQ-NBR                    PIC 9(06).           
100404*            15  ASC-FR-STR-NBR                      PIC 9(03).           
100404             15  ASC-FR-STR-NBR                      PIC 9(04).           
                   15  ASC-FR-STR-NAM                      PIC X(10).           
100404*            15  ASC-TO-STR-NBR                      PIC 9(03).           
100404             15  ASC-TO-STR-NBR                      PIC 9(04).           
                   15  ASC-TO-STR-NAM                      PIC X(10).           
100404*            15  ASC-RC-STR-NBR                      PIC 9(03).           
100404             15  ASC-RC-STR-NBR                      PIC 9(04).           
                   15  ASC-RC-STR-NAM                      PIC X(10).           
                   15  ASC-NBR-OF-POSTING-LINES            PIC 9(03).           
                   15  ASC-USERID                          PIC X(08).           
                   15  ASC-ITEM-ARRAY.                                          
                       20  ASC-ITEM-ENTRIES OCCURS 8 TIMES.                     
                           25  ASC-DATE.                                        
                               30  ASC-DTE-YEAR        PIC X(04).               
                               30                      PIC X(01).               
                               30  ASC-DTE-MONTH       PIC X(02).               
                               30                      PIC X(01).               
                               30  ASC-DTE-DAY         PIC X(02).               
100404*                    25  ASC-DTL-FR-NBR          PIC 9(03).               
100404                     25  ASC-DTL-FR-NBR          PIC 9(04).               
                           25  ASC-DTL-FR-NAM-10       PIC X(10).               
                           25  ASC-DTL-FR-NAM-5        PIC X(05).               
100404*                    25  ASC-DTL-TO-NBR          PIC 9(03).               
100404                     25  ASC-DTL-TO-NBR          PIC 9(04).               
                           25  ASC-DTL-TO-NAM-10       PIC X(10).               
                           25  ASC-DTL-TO-NAM-5        PIC X(05).               
                           25  ASC-DTL-FR-QTY          PIC S9(04)               
                                                           COMP-3.              
                           25  ASC-DTL-PRICE           PIC S9(07)V99            
                                                           COMP-3.              
                           25  ASC-DTL-FR-EXT          PIC S9(07)V99            
                                                           COMP-3.              
                           25  ASC-REASON-CDE          PIC X(02).               
100404*                    25  ASC-REASON              PIC X(23).               
SR3403                     25  ASC-REASON              PIC X(18).               
                           25  ASC-PSTG-SEQ-NBR        PIC S9(04)               
                                                           COMP-3.              
                                                                                
                   15  ASC-COMMENT-ARRAY.                                       
                       20  ASC-COMMENT OCCURS 4 TIMES  PIC X(79).               
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY TFKM438.                                                        
                                                                                
       01  MR-MAP-REDEFINITION       REDEFINES  TF438AO.                        
           05  FILLER                PIC X(12).                                 
                                                                                
           05  MR-XFER-NBR-L         PIC S9(04) COMP.                           
           05  MR-XFER-NBR-A         PIC  X(01).                                
           05  MR-XFER-NBR-C         PIC  X(01).                                
           05  MR-XFER-NBR-H         PIC  X(01).                                
100404*    05  MR-XFER-NBR           PIC  X(10).                                
100404     05  MR-XFER-NBR           PIC  X(11).                                
                                                                                
           05  MR-FR-STR-NBR-L       PIC S9(04) COMP.                           
           05  MR-FR-STR-NBR-A       PIC  X(01).                                
           05  MR-FR-STR-NBR-C       PIC  X(01).                                
           05  MR-FR-STR-NBR-H       PIC  X(01).                                
100404*    05  MR-FR-STR-NBR         PIC  X(03).                                
100404     05  MR-FR-STR-NBR         PIC  X(04).                                
                                                                                
           05  MR-FR-STR-NAM-L       PIC S9(04) COMP.                           
           05  MR-FR-STR-NAM-A       PIC  X(01).                                
           05  MR-FR-STR-NAM-C       PIC  X(01).                                
           05  MR-FR-STR-NAM-H       PIC  X(01).                                
           05  MR-FR-STR-NAM         PIC  X(10).                                
                                                                                
           05  MR-TO-STR-NBR-L       PIC S9(04) COMP.                           
           05  MR-TO-STR-NBR-A       PIC  X(01).                                
           05  MR-TO-STR-NBR-C       PIC  X(01).                                
           05  MR-TO-STR-NBR-H       PIC  X(01).                                
100404*    05  MR-TO-STR-NBR         PIC  X(03).                                
100404     05  MR-TO-STR-NBR         PIC  X(04).                                
                                                                                
           05  MR-TO-STR-NAM-L       PIC S9(04) COMP.                           
           05  MR-TO-STR-NAM-A       PIC  X(01).                                
           05  MR-TO-STR-NAM-C       PIC  X(01).                                
           05  MR-TO-STR-NAM-H       PIC  X(01).                                
           05  MR-TO-STR-NAM         PIC  X(10).                                
                                                                                
           05  MR-RC-STR-NBR-L       PIC S9(04) COMP.                           
           05  MR-RC-STR-NBR-A       PIC  X(01).                                
           05  MR-RC-STR-NBR-C       PIC  X(01).                                
           05  MR-RC-STR-NBR-H       PIC  X(01).                                
100404*    05  MR-RC-STR-NBR         PIC  X(03).                                
100404     05  MR-RC-STR-NBR         PIC  X(04).                                
                                                                                
           05  MR-RC-STR-NAM-L       PIC S9(04) COMP.                           
           05  MR-RC-STR-NAM-A       PIC  X(01).                                
           05  MR-RC-STR-NAM-C       PIC  X(01).                                
           05  MR-RC-STR-NAM-H       PIC  X(01).                                
           05  MR-RC-STR-NAM         PIC  X(10).                                
                                                                                
           05  MR-SCROLL-AREA OCCURS 8 TIMES.                                   
               10  MR-DATE-L         PIC S9(04) COMP.                           
               10  MR-DATE-A         PIC  X(01).                                
               10  MR-DATE-C         PIC  X(01).                                
               10  MR-DATE-H         PIC  X(01).                                
               10  MR-DATE           PIC  X(10).                                
      *                                                                         
               10  MR-DTL-FR-NBR-L   PIC S9(04) COMP.                           
               10  MR-DTL-FR-NBR-A   PIC  X(01).                                
               10  MR-DTL-FR-NBR-C   PIC  X(01).                                
               10  MR-DTL-FR-NBR-H   PIC  X(01).                                
100404*        10  MR-DTL-FR-NBR     PIC  ZZZ.                                  
100404         10  MR-DTL-FR-NBR     PIC  ZZZZ.                                 
      *                                                                         
               10  MR-DTL-FR-NAM-L   PIC S9(04) COMP.                           
               10  MR-DTL-FR-NAM-A   PIC  X(01).                                
               10  MR-DTL-FR-NAM-C   PIC  X(01).                                
               10  MR-DTL-FR-NAM-H   PIC  X(01).                                
               10  MR-DTL-FR-NAM     PIC  X(05).                                
      *                                                                         
               10  MR-DTL-TO-NBR-L   PIC S9(04) COMP.                           
               10  MR-DTL-TO-NBR-A   PIC  X(01).                                
               10  MR-DTL-TO-NBR-C   PIC  X(01).                                
               10  MR-DTL-TO-NBR-H   PIC  X(01).                                
100404*        10  MR-DTL-TO-NBR     PIC  ZZZ.                                  
100404         10  MR-DTL-TO-NBR     PIC  ZZZZ.                                 
      *                                                                         
               10  MR-DTL-TO-NAM-L   PIC S9(04) COMP.                           
               10  MR-DTL-TO-NAM-A   PIC  X(01).                                
               10  MR-DTL-TO-NAM-C   PIC  X(01).                                
               10  MR-DTL-TO-NAM-H   PIC  X(01).                                
               10  MR-DTL-TO-NAM     PIC  X(05).                                
      *                                                                         
               10  MR-DTL-FR-QTY-L   PIC S9(04) COMP.                           
               10  MR-DTL-FR-QTY-A   PIC  X(01).                                
               10  MR-DTL-FR-QTY-C   PIC  X(01).                                
               10  MR-DTL-FR-QTY-H   PIC  X(01).                                
               10  MR-DTL-FR-QTY.                                               
                   15  MR-DTL-FR-QTY-N  PIC  ZZZ.                               
      *                                                                         
               10  MR-DTL-FR-PRC-L   PIC S9(04) COMP.                           
               10  MR-DTL-FR-PRC-A   PIC  X(01).                                
               10  MR-DTL-FR-PRC-C   PIC  X(01).                                
               10  MR-DTL-FR-PRC-H   PIC  X(01).                                
               10  MR-DTL-FR-PRC.                                               
SR3403             15  MR-DTL-FR-PRC-N  PIC  ZZZZZ.ZZ.                          
      *                                                                         
               10  MR-DTL-FR-EXT-L   PIC S9(04) COMP.                      L**1 
               10  MR-DTL-FR-EXT-A   PIC  X(01).                           CL**1
               10  MR-DTL-FR-EXT-C   PIC  X(01).                           CL**1
               10  MR-DTL-FR-EXT-H   PIC  X(01).                           CL**1
               10  MR-DTL-FR-EXT.                                          CL**1
                   15  MR-DTL-FR-EXT-N   PIC  ZZZZZZ.ZZ.                   CL**1
      *                                                                    CL**1
               10  MR-REASON-L       PIC S9(04) COMP.                      CL**1
               10  MR-REASON-A       PIC  X(01).                           CL**1
               10  MR-REASON-C       PIC  X(01).                           CL**1
               10  MR-REASON-H       PIC  X(01).                           CL**1
100404*        10  MR-REASON         PIC  X(23).                           CL**1
SR3403         10  MR-REASON         PIC  X(18).                           CL**1
                                                                                
           05  MR-USERID-L           PIC S9(04) COMP.                      CL**1
           05  MR-USERID-A           PIC  X(01).                           CL**1
           05  MR-USERID-C           PIC  X(01).                           CL**1
           05  MR-USERID-H           PIC  X(01).                           CL**1
           05  MR-USERID             PIC  X(08).                                
                                                                                
           05  MR-COMMENTS-AREA OCCURS 4 TIMES.                                 
               10  MR-COMMENT-L      PIC S9(04) COMP.                           
               10  MR-COMMENT-A      PIC  X(01).                                
               10  MR-COMMENT-C      PIC  X(01).                                
               10  MR-COMMENT-H      PIC  X(01).                                
               10  MR-COMMENT        PIC  X(79).                                
      *                                                                         
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TTFAJRS (TRANSFER ADJUSTMENT REASON CODE TABLE)         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TTFAJRS                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TTFITPS (TRANSFER ITEM POSTING TABLE)         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TTFITPS                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR XST_LOC TABLE                                 *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
           MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
           MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
           SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
           MOVE LENGTH OF TF438AI       TO DP030-MAP-LENGTH (1).                
           MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING.                                     
                                                                                
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      *----------------------------------------------------------------*        
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
      *----------------------------------------------------------------*        
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TF438AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK TO TRUE                                    
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                     TO DP013-MESSAGE-TEXT (3)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
      ***-----$TEMPLATE NOTE$-------------------------------------------        
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
      ***---------------------------------------------------------------        
                   IF  PS-COMMAREA-NOT-VALID                                    
      ***              MOVE PC-TSYMSG-99901                                     
      ***                            TO  DP020-MSG-NUMBER                       
      ***              SET DP020-MSG-FATAL                                      
      ***                            TO  TRUE                                   
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                     TO  TRUE                                   
                   ELSE                                                         
                       PERFORM 4100-READ-POSTINGS                               
                       PERFORM 3200-RESET-ATTRIBUTES                            
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   EVALUATE DP020-INT-APPL-FORMAT-IND                           
                      WHEN PC-MANUAL-REVERSAL-FORMAT                            
                      WHEN PC-AUTO-REVERSAL-FORMAT                              
                          PERFORM 3000-EDIT-DATA-IN-COMMAREA                    
                   END-EVALUATE                                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.      *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID     TO TRUE.                                   
           EVALUATE DP020-INT-APPL-FORMAT-IND                                   
               WHEN PC-MANUAL-REVERSAL-FORMAT                                   
               WHEN PC-AUTO-REVERSAL-FORMAT                                     
                   MOVE DP020-USERID         TO ASC-USERID                      
                   MOVE TF003-XFER-DKEY-ID   TO ASC-XFER-DKEY-ID                
                                                DK-XFER-DKEY-ID                 
                   MOVE TF003-XFER-LNE-NBR   TO ASC-XFER-LNE-NBR                
                                                DK-XFER-LNE-NBR                 
                   MOVE TF003-XFER-SEQ-NBR   TO ASC-XFER-SEQ-NBR                
                   MOVE TF003-FR-STR-NBR     TO ASC-FR-STR-NBR                  
                   MOVE TF003-FR-STR-NAM     TO ASC-FR-STR-NAM                  
                   MOVE TF003-TO-STR-NBR     TO ASC-TO-STR-NBR                  
                   MOVE TF003-TO-STR-NAM     TO ASC-TO-STR-NAM                  
                   MOVE TF003-RC-STR-NBR     TO ASC-RC-STR-NBR                  
                   MOVE TF003-RC-STR-NAM     TO ASC-RC-STR-NAM                  
                   MOVE TF003-NBR-OF-POSTING-LINES                              
                                             TO ASC-NBR-OF-POSTING-LINES        
                   PERFORM                                                      
                     VARYING PV-ITEM                                            
                       FROM 1 BY 1                                              
                       UNTIL PV-ITEM > ASC-NBR-OF-POSTING-LINES                 
                       MOVE TF003-PSTG-SEQ-NBR (PV-ITEM)                        
                                               TO ASC-PSTG-SEQ-NBR              
                                                  (PV-ITEM)                     
                   END-PERFORM                                                  
                                                                                
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00148                                         
                                     TO DP020-MSG-NUMBER                        
                   SET DP020-MSG-FATAL TO TRUE                                  
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                  PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                          
                  PERFORM 3000-EDIT-DATA-IN-COMMAREA                            
                  IF  PS-NO-ERROR                                               
                      SET DP030-CONTINUE-GO   TO TRUE                           
                      PERFORM 2100-CHECK-FUNCTION-KEY                           
                  ELSE                                                          
                      SET DP030-OVERRIDE-GO   TO TRUE                           
                  END-IF                                                        
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
                                                                                
      *----- UPDATE ---------------------------------------------------*        
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                   PERFORM 7000-UPDATE                                          
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
       EJECT                                                                    
                                                                                
      ******************************************************************        
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
      ******************************************************************        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > 4.                                                
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
       2210-MOVE-LINES-TO-COMMAREA.                                             
           IF  MR-COMMENT-L (PV-SUB) > ZERO                                     
               MOVE MR-COMMENT (PV-SUB)                                         
                                         TO ASC-COMMENT (PV-SUB)                
           ELSE                                                                 
               IF  MR-COMMENT-A (PV-SUB) = DP015-ERASE-EOF                      
                       MOVE SPACE        TO ASC-COMMENT (PV-SUB)                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
      ******************************************************************        
       3000-EDIT-DATA-IN-COMMAREA.                                              
           SET PS-NO-ERROR                 TO TRUE.                             
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           IF ASC-COMMENT-ARRAY NOT > SPACES                                    
               MOVE -1                     TO MR-COMMENT-L (1)                  
               MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER                  
               SET DP020-MSG-FATAL                                              
                   PS-ERROR                                                     
                   DP030-SET-CURSOR-APPL-1 TO TRUE                              
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RESET ATTRIBUTES.                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > 4                                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-COMMENT-A (PV-SUB)               
               MOVE DP015-GREEN          TO MR-COMMENT-C (PV-SUB)               
               MOVE DP015-UNDERLINE      TO MR-COMMENT-H (PV-SUB)               
           END-PERFORM.                                                         
           MOVE -1                       TO MR-COMMENT-L (1).                   
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE POSTING DATA                                       *        
      *----------------------------------------------------------------*        
                                                                                
       4100-READ-POSTINGS.                                                      
                                                                                
           PERFORM 4200-READ-TTFITPS                                            
               VARYING PV-ITEM                                                  
               FROM 1 BY 1                                                      
               UNTIL PV-ITEM > ASC-NBR-OF-POSTING-LINES.                        
                                                                                
      *----------------------------------------------------------------*        
      *    READ THE POSTING DATA                                       *        
      *----------------------------------------------------------------*        
                                                                                
       4200-READ-TTFITPS.                                                       
                                                                                
           MOVE ASC-PSTG-SEQ-NBR (PV-ITEM)    TO DK-PSTG-SEQ-NBR.               
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES               
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
                                                                                
               EXEC SQL                                                         
                  SELECT                                                        
                      I.ADJ_RSN_CDE                                             
                    , ITM_PSTG_PRPD_TMST                                        
                    , FRM_PSTG_STR_NBR                                          
                    , TO_PSTG_STR_NBR                                           
                    , ITM_RTL_PRCE_AMT                                          
                    , ITM_QTY                                                   
                    , ADJ_RSN_DESC                                              
                  INTO                                                          
                      :TFITPS-ADJ-RSN-CDE                                       
                    , :TFITPS-ITM-PSTG-PRPD-TMST                                
                    , :TFITPS-FRM-PSTG-STR-NBR                                  
                    , :TFITPS-TO-PSTG-STR-NBR                                   
                    , :TFITPS-ITM-RTL-PRCE-AMT                                  
                    , :TFITPS-ITM-QTY                                           
                    , :TFAJRS-ADJ-RSN-DESC                                      
                  FROM                                                          
                      TTFITPS I                                                 
                     ,TTFAJRS A                                                 
                  WHERE                                                         
                      XFER_DKEY_ID   = :DK-XFER-DKEY-ID     AND                 
                      XFER_LNE_NBR   = :DK-XFER-LNE-NBR     AND                 
                      PSTG_SEQ_NBR   = :DK-PSTG-SEQ-NBR     AND                 
                      A.ADJ_RSN_CDE  = I.ADJ_RSN_CDE                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN OTHER                                                   
                        MOVE '4200-READ-TTFITPS'                                
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'READ TRANSFER POSTING DATA'                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        MOVE 'TTFITPS'    TO DP013-DB2-TABLE-NAME (1)           
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES                     
               MOVE '4200-READ-TTFITPS'                                         
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR READ TTFITPS'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               MOVE 'TTFITPS'        TO DP013-DB2-TABLE-NAME (1)                
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE = ZERO                                                   
                   MOVE TFITPS-ITM-PSTG-PRPD-TMST                               
                                     TO  ASC-DATE (PV-ITEM)                     
                   MOVE TFITPS-FRM-PSTG-STR-NBR                                 
                                     TO  DK-STORE-NBR                           
                                         ASC-DTL-FR-NBR (PV-ITEM)               
                   PERFORM 5550-READ-XST-LOC                                    
                   MOVE XST00001-LOC-5-ABBR-NM                                  
                                     TO  ASC-DTL-FR-NAM-5 (PV-ITEM)             
                   MOVE TFITPS-TO-PSTG-STR-NBR                                  
                                     TO  DK-STORE-NBR                           
                                         ASC-DTL-TO-NBR (PV-ITEM)               
                   PERFORM 5550-READ-XST-LOC                                    
                   MOVE XST00001-LOC-5-ABBR-NM                                  
                                     TO  ASC-DTL-TO-NAM-5 (PV-ITEM)             
                   MOVE TFITPS-ITM-RTL-PRCE-AMT                                 
                                     TO  ASC-DTL-PRICE (PV-ITEM)                
                   MOVE TFITPS-ITM-QTY                                          
                                     TO  ASC-DTL-FR-QTY (PV-ITEM)               
                   MOVE TFITPS-ADJ-RSN-CDE                                      
                                     TO  ASC-REASON-CDE (PV-ITEM)               
                   EVALUATE TFITPS-ADJ-RSN-CDE                                  
                     WHEN PC-COMPLETE-CODE                                      
                       STRING TFAJRS-ADJ-RSN-DESC DELIMITED BY '  '             
                       PC-POSITIVE-LITERAL        DELIMITED BY SIZE             
                                     INTO ASC-REASON (PV-ITEM)                  
      *              WHEN PC-MANUAL-REVERSAL                                    
      *              WHEN PC-AUTO-REVERSAL                                      
      *              WHEN PC-ADJUSTMENT                                         
      *                IF TFITPS-EXTL-SYS-PRCG-CDE = PC-PRICE-ONLY              
      *                  STRING TFAJRS-ADJ-RSN-DESC DELIMITED BY '  '           
      *                  PC-PRICE-ONLY-LITERAL      DELIMITED BY SIZE           
      *                              INTO ASC-REASON (PV-ITEM)                  
      *                ELSE                                                     
      *                  STRING TFAJRS-ADJ-RSN-DESC DELIMITED BY '  '           
      *                  PC-UNIT-AND-PRICE-LITERAL  DELIMITED BY SIZE           
      *                              INTO ASC-REASON (PV-ITEM)                  
      *                END-IF                                                   
                     WHEN OTHER                                                 
                         MOVE TFAJRS-ADJ-RSN-DESC                               
                                     TO  ASC-REASON (PV-ITEM)                   
                   END-EVALUATE                                                 
                   COMPUTE ASC-DTL-FR-EXT (PV-ITEM) =                           
                      (ASC-DTL-PRICE    (PV-ITEM)   *                           
                       ASC-DTL-FR-QTY   (PV-ITEM))                              
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE ASC-FR-STR-NBR         TO PV-XFER-FROM-STORE                    
                                          MR-FR-STR-NBR.                        
           MOVE ASC-XFER-SEQ-NBR       TO PV-XFER-SEQ-NBR.                      
           MOVE PV-XFER-NBR            TO MR-XFER-NBR.                          
           MOVE ASC-FR-STR-NAM         TO MR-FR-STR-NAM.                        
           MOVE ASC-TO-STR-NBR         TO MR-TO-STR-NBR.                        
           MOVE ASC-TO-STR-NAM         TO MR-TO-STR-NAM.                        
           MOVE ASC-RC-STR-NBR         TO MR-RC-STR-NBR.                        
           MOVE ASC-RC-STR-NAM         TO MR-RC-STR-NAM.                        
           MOVE ASC-USERID             TO MR-USERID.                            
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > 4                                                 
               MOVE ASC-COMMENT (PV-SUB) TO MR-COMMENT (PV-SUB)                 
           END-PERFORM.                                                         
           PERFORM                                                              
             VARYING                                                            
               PV-ITEM FROM 1 BY 1                                              
               UNTIL PV-ITEM > PC-ITEMS-PER-PANEL                               
                   IF ASC-DTE-YEAR (PV-ITEM) > SPACES                           
                       MOVE ASC-DTE-YEAR (PV-ITEM)                              
                                       TO PV-YEAR                               
                       MOVE ASC-DTE-MONTH (PV-ITEM)                             
                                       TO PV-MONTH                              
                       MOVE ASC-DTE-DAY  (PV-ITEM)                              
                                       TO PV-DAY                                
                       MOVE PV-DATE        TO MR-DATE   (PV-ITEM)               
                   END-IF                                                       
                   MOVE ASC-DTL-FR-NBR     (PV-ITEM)                            
                                       TO MR-DTL-FR-NBR    (PV-ITEM)            
                   MOVE ASC-DTL-FR-NAM-5   (PV-ITEM)                            
                                       TO MR-DTL-FR-NAM    (PV-ITEM)            
                   MOVE ASC-DTL-FR-QTY     (PV-ITEM)                            
                                       TO MR-DTL-FR-QTY-N  (PV-ITEM)            
                   MOVE ASC-DTL-PRICE      (PV-ITEM)                            
                                       TO MR-DTL-FR-PRC-N  (PV-ITEM)            
                   MOVE ASC-DTL-FR-EXT     (PV-ITEM)                            
                                       TO MR-DTL-FR-EXT-N  (PV-ITEM)            
                   MOVE ASC-REASON         (PV-ITEM)                            
                                       TO MR-REASON        (PV-ITEM)            
                   MOVE ASC-DTL-TO-NBR     (PV-ITEM)                            
                                       TO MR-DTL-TO-NBR    (PV-ITEM)            
                   MOVE ASC-DTL-TO-NAM-5   (PV-ITEM)                            
                                       TO MR-DTL-TO-NAM    (PV-ITEM)            
           END-PERFORM.                                                         
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE COMMENTS TO BE DISPLAYED                           *        
      *----------------------------------------------------------------*        
                                                                                
       5000-READ-COMMENTS.                                                      
                                                                                
           MOVE ASC-PSTG-SEQ-NBR (1)          TO DK-PSTG-SEQ-NBR.               
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES               
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
                                                                                
               EXEC SQL                                                         
                  SELECT                                                        
                      ADJ_BY_USR_ID                                             
                     ,ADJ_CMNT_TXT                                              
                  INTO                                                          
                      :TFITPS-ADJ-BY-USR-ID                                     
                     ,:TFITPS-ADJ-CMNT-TXT                                      
                  FROM                                                          
                      TTFITPS P                                                 
                  WHERE                                                         
                      XFER_DKEY_ID   = :DK-XFER-DKEY-ID     AND                 
                      XFER_LNE_NBR   = :DK-XFER-LNE-NBR     AND                 
                      PSTG_SEQ_NBR   = :DK-PSTG-SEQ-NBR                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       MOVE TFITPS-ADJ-CMNT-TXT-TEXT                            
                                                 TO ASC-COMMENT-ARRAY           
                       MOVE TFITPS-ADJ-BY-USR-ID TO ASC-USERID                  
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN OTHER                                                   
                        MOVE '5000-READ-COMMENTS'                               
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'READ TRANSFER POSTING DATA'                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        MOVE 'TTFITPS'    TO DP013-DB2-TABLE-NAME (1)           
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES                     
               MOVE '5000-READ-COMMENTS'                                        
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR READ TTFITPS'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               MOVE 'TTFITPS'        TO DP013-DB2-TABLE-NAME (1)                
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  GET THE 10 CHARACTER ABBREVIATED STORE NAME.                  *        
      *----------------------------------------------------------------*        
       5550-READ-XST-LOC.                                                       
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES)                        
                    OR SQLCODE = ZERO                                           
                    OR SQLCODE = +100                                           
               EXEC SQL                                                         
                   SELECT LOC_5_ABBR_NM                                         
                       INTO :XST00001-LOC-5-ABBR-NM                             
                       FROM XST_LOC                                             
                       WHERE (LOC_NBR  = :DK-STORE-NBR)                         
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                       MOVE '?????'       TO XST00001-LOC-5-ABBR-NM             
                   WHEN OTHER                                                   
                       MOVE '5550-READ-XST-LOC'                                 
                                         TO DP013-PARAGRAPH                     
                       MOVE 'SELECT A ROW FROM XST_LOC'                         
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                         TO TRUE                                
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5550-READ-XST-LOC'                                         
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'A ROW FROM XST_LOC' TO DP013-MESSAGE-TEXT (2)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'XST_LOC'            TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  CREATE A REVERSE POSTING ENTRY FOR SELECTED ITEMS, THEN       *        
      *  SET UP THE INTER APPLICATION COMMAREA FOR THE REVERSAL        *        
      *  COMMENTS UPDATE PROGRAM.                                      *        
      *----------------------------------------------------------------*        
       7000-UPDATE.                                                             
                                                                                
           MOVE ASC-XFER-DKEY-ID              TO DK-XFER-DKEY-ID.               
           MOVE ASC-XFER-LNE-NBR              TO DK-XFER-LNE-NBR.               
           MOVE ASC-USERID                 TO TFITPS-ADJ-BY-USR-ID.             
           MOVE ASC-COMMENT-ARRAY          TO TFITPS-ADJ-CMNT-TXT-TEXT          
                                              VF-VARCHAR-WORK-FIELD.            
           SET VF-IND                      TO +320.                             
           PERFORM 7050-PROCESS-VARCHAR.                                        
           SET TFITPS-ADJ-CMNT-TXT-LEN     TO VF-IND.                           
           PERFORM 7100-UPDATE-COMMENTS                                         
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER ASC-NBR-OF-POSTING-LINES.              
      *----------------------------------------------------------------*        
      *  DETERMINE THE LENGTH OF THE VARCHAR FIELD.                    *        
      *----------------------------------------------------------------*        
       7050-PROCESS-VARCHAR.                                                    
           IF VF-VARCHAR-WORK-FIELD = SPACES                                    
               SET VF-IND                     TO +1                             
               SET VF-IND                     DOWN BY +1                        
           ELSE                                                                 
               SET VF-VARCHAR-BEGIN           TO TRUE                           
               PERFORM                                                          
                 UNTIL VF-VARCHAR-END                                           
                   IF VF-VARCHAR (VF-IND) = ' '                                 
                       IF VF-IND > +0                                           
                           SET VF-IND         DOWN BY +1                        
                       ELSE                                                     
                           SET VF-VARCHAR-END TO TRUE                           
                       END-IF                                                   
                   ELSE                                                         
                       SET VF-VARCHAR-END     TO TRUE                           
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * INSERT RETAIL ITEMS.                                           *        
      *----------------------------------------------------------------*        
       7100-UPDATE-COMMENTS.                                                    
           MOVE ASC-PSTG-SEQ-NBR (PV-ITEM)    TO DK-PSTG-SEQ-NBR.               
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
               UNTIL SQLCODE = 0 OR                                             
                     PV-RETRY-COUNTER > PC-MAX-RETRIES                          
                                                                                
               EXEC SQL                                                         
                    UPDATE TTFITPS                                              
                      SET ADJ_BY_USR_ID     = :TFITPS-ADJ-BY-USR-ID             
                         ,ADJ_CMNT_TXT      = :TFITPS-ADJ-CMNT-TXT              
                    WHERE                                                       
                      XFER_DKEY_ID   = :DK-XFER-DKEY-ID     AND                 
                      XFER_LNE_NBR   = :DK-XFER-LNE-NBR     AND                 
                      PSTG_SEQ_NBR   = :DK-PSTG-SEQ-NBR                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN OTHER                                                   
                        MOVE '7100-UPDATE-COMMENTS'                             
                                          TO DP013-PARAGRAPH                    
                        MOVE 'UPDATE COMMENTS IN TTFITPS'                       
                                          TO DP013-MESSAGE-TEXT(1)              
                        MOVE SQLCA        TO DP013-SQLCA                        
                        MOVE 'TTFITPS'    TO DP013-DB2-TABLE-NAME (1)           
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART TO TRUE                  
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES AND                             
              SQLCODE NOT = 0                                                   
               MOVE '7100-UPDATE-COMMENTS'  TO DP013-PARAGRAPH                  
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO UPDATE'             
                                          TO DP013-MESSAGE-TEXT (1)             
               MOVE 'A TRANSFER POSTING'  TO DP013-MESSAGE-TEXT (2)             
               MOVE SQLCA                 TO DP013-SQLCA                        
               MOVE 'TTFITPS'             TO DP013-DB2-TABLE-NAME (1)           
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                          TO TRUE                               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
