00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP013
00003  PROGRAM-ID.    SNKUP013.                                            LV003
00004                                                                   SNKUP013
00005  AUTHOR.        ANNE YEKO.                                        SNKUP013
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP013
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP013
00008  DATE-COMPILED.                                                   SNKUP013
00009                                                                   SNKUP013
00010 ******************************************************************SNKUP013
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP013
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP013
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASHADT TABLE.          **SNKUP013
00014 ******************************************************************SNKUP013
00015 * CHANGED __/__/__ BY ____________                              **SNKUP013
00016 *   P_________  ________________________________________________**SNKUP013
00017 *               ________________________________________________**SNKUP013
00018 *                                                               **SNKUP013
00019 ******************************************************************SNKUP013
00020                                                                   SNKUP013
00021      EJECT                                                        SNKUP013
00022                                                                   SNKUP013
00023  ENVIRONMENT DIVISION.                                            SNKUP013
00024                                                                   SNKUP013
00025  INPUT-OUTPUT SECTION.                                            SNKUP013
00026                                                                   SNKUP013
00027  FILE-CONTROL.                                                    SNKUP013
00028                                                                   SNKUP013
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP013
00030             ASSIGN TO INP01.                                      SNKUP013
00031                                                                   SNKUP013
00032  DATA DIVISION.                                                   SNKUP013
00033                                                                   SNKUP013
00034  FILE SECTION.                                                    SNKUP013
00035                                                                   SNKUP013
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP013
00037      RECORDING MODE IS F                                          SNKUP013
00038      LABEL RECORDS ARE STANDARD                                   SNKUP013
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP013
00041                                                                   SNKUP013
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP013
00044      EJECT                                                        SNKUP013
00045  WORKING-STORAGE SECTION.                                         SNKUP013
00046                                                                   SNKUP013
00047  01  FILLER.                                                      SNKUP013
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP013
00049          ' ***** WORKING STORAGE PGM=SNKUP013 '.                  SNKUP013
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP013
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP013
00052          ' LOCATION='.                                            SNKUP013
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP013
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP013
00055          ' *****'.                                                SNKUP013
00056                                                                   SNKUP013
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP013
00058                                              COMP SYNC.           SNKUP013
00059                                                                   SNKUP013
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP013
00061                                                                   SNKUP013
00062      EJECT                                                        SNKUP013
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP013
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP013
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP013
00066                                                                   SNKUP013
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP013
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP013
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP013
00070                                                                   SNKUP013
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP013
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP013
00073                                              COMP-3.              SNKUP013
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP013
00075                                              COMP-3.              SNKUP013
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP013
00077                                              COMP-3.              SNKUP013
00078                                                                   SNKUP013
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP013
00080                                                                   SNKUP013
00081      COPY SNRD002I.                                               SNKUP013
00082      EJECT                                                        SNKUP013
00083 ******************************************************************SNKUP013
00084 *    DB2 DATABASE AREA                                            SNKUP013
00085 ******************************************************************SNKUP013
00086                                                                   SNKUP013
00087 *    ASN HEADER ADDRESS TEXT                                      SNKUP013
00088                                                                   SNKUP013
00089      EXEC SQL                                                     SNKUP013
00090          INCLUDE TASHADT                                          SNKUP013
00091      END-EXEC.                                                    SNKUP013
00092                                                                   SNKUP013
00093      EJECT                                                        SNKUP013
00094 *    SQL COMMUNICATION AREA                                       SNKUP013
00095                                                                   SNKUP013
00096      EXEC SQL                                                     SNKUP013
00097          INCLUDE SQLCA                                            SNKUP013
00098      END-EXEC.                                                    SNKUP013
00099      EJECT                                                        SNKUP013
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP013
00101                                                                   SNKUP013
00102      COPY SQLCA2.                                                 SNKUP013
00103      EJECT                                                        SNKUP013
00104                                                                   SNKUP013
00105 ***************************************************************** SNKUP013
00106  PROCEDURE DIVISION.                                              SNKUP013
00107 ***************************************************************** SNKUP013
00108                                                                   SNKUP013
00109  A000-MAIN-DRIVER.                                                SNKUP013
00110                                                                   SNKUP013
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP013
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP013
00113                                                                   SNKUP013
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP013
00115                                                                   SNKUP013
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP013
00117                                                                   SNKUP013
00118      PERFORM B100-DELETE-ROWS                                     SNKUP013
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP013
00120                                                                   SNKUP013
00121      DISPLAY 'SNKUP013 RECORDS READ:    ' PV-RECS-READ.           SNKUP013
00122      DISPLAY 'SNKUP013 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP013
00123                                                                   SNKUP013
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP013
00125                                                                   SNKUP013
00126      GOBACK.                                                      SNKUP013
00127                                                                   SNKUP013
00128      EJECT                                                        SNKUP013
00129                                                                   SNKUP013
00130 ***************************************************************** SNKUP013
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP013
00132 ***************************************************************** SNKUP013
00133  B100-DELETE-ROWS.                                                SNKUP013
00134                                                                   SNKUP013
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP013
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP013
00137                                  TO ABEND-PARAGRAPH.              SNKUP013
00138                                                                   SNKUP013
00139      PERFORM B300-DELETE-ASHADT.                                  SNKUP013
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP013
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP013
00143      END-IF.                                                      SNKUP013
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP013
00145                                                                   SNKUP013
00146      EJECT                                                        SNKUP013
00147                                                                   SNKUP013
00148 ***************************************************************** SNKUP013
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP013
00150 ***************************************************************** SNKUP013
00151  B200-READ-INPUT-FILE.                                            SNKUP013
00152                                                                   SNKUP013
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP013
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP013
00155                                  TO ABEND-PARAGRAPH.              SNKUP013
00156                                                                   SNKUP013
00157      READ ASNHDR-DELETE-FILE                                      SNKUP013
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP013
00159          AT END                                                   SNKUP013
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP013
00161                                                                   SNKUP013
00162      IF NOT PS-END-OF-INPUT                                       SNKUP013
00163          ADD 1                   TO PV-RECS-READ                  SNKUP013
00164      END-IF.                                                      SNKUP013
00165                                                                   SNKUP013
00166      EJECT                                                        SNKUP013
00167                                                                   SNKUP013
00168 ***************************************************************** SNKUP013
00169 *    THIS PARAGRAPH DELETES ALL ASHADT ROWS THAT ARE EQUAL TO     SNKUP013
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP013
00171 ***************************************************************** SNKUP013
00172  B300-DELETE-ASHADT.                                              SNKUP013
00173                                                                   SNKUP013
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP013
00175      MOVE 'B300-DELETE-ASHADT        '                            SNKUP013
00176                                  TO ABEND-PARAGRAPH.              SNKUP013
00177                                                                   SNKUP013
00178      EXEC SQL                                                     SNKUP013
00179          DELETE                                                   SNKUP013
00180          FROM   TASHADT                                           SNKUP013
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP013
00183                                                                   SNKUP013
00184      IF SQLCODE = ZERO                                            SNKUP013
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP013
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP013
00187      ELSE                                                         SNKUP013
00188          IF SQLCODE = +100                                        SNKUP013
00189              CONTINUE                                             SNKUP013
00190          ELSE                                                     SNKUP013
00191              PERFORM Z998-SQL-ERROR                               SNKUP013
00192          END-IF                                                   SNKUP013
00193      END-IF.                                                      SNKUP013
00194                                                                   SNKUP013
00195      EJECT                                                        SNKUP013
00196                                                                   SNKUP013
00197 ***************************************************************** SNKUP013
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP013
00199 ***************************************************************** SNKUP013
00200  B400-COMMIT.                                                     SNKUP013
00201                                                                   SNKUP013
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP013
00203      MOVE 'B400-COMMIT               '                            SNKUP013
00204                                  TO ABEND-PARAGRAPH.              SNKUP013
00205                                                                   SNKUP013
00206      EXEC SQL                                                     SNKUP013
00207          COMMIT                                                   SNKUP013
00208      END-EXEC.                                                    SNKUP013
00209                                                                   SNKUP013
00210      IF SQLCODE = ZERO                                            SNKUP013
00211          CONTINUE                                                 SNKUP013
00212      ELSE                                                         SNKUP013
00213          PERFORM Z998-SQL-ERROR                                   SNKUP013
00214      END-IF.                                                      SNKUP013
00215                                                                   SNKUP013
00216      EJECT                                                        SNKUP013
00217                                                                   SNKUP013
00218 ***************************************************************** SNKUP013
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP013
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP013
00221 ***************************************************************** SNKUP013
00222  Z998-SQL-ERROR.                                                  SNKUP013
00223                                                                   SNKUP013
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP013
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP013
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP013
00227      DISPLAY '** PROGRAM          ** = SNKUP013'.                 SNKUP013
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP013
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP013
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP013
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP013
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP013
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP013
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP013
