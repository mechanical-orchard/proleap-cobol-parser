00001 ******************************************************************10/09/95
00002  IDENTIFICATION DIVISION.                                         ARKUT054
00003 ******************************************************************   LV017
00004                                                                      CL**6
00005  PROGRAM-ID.             ARKUT054.                                   CL**6
00006  AUTHOR.                 SUSAN COONEY.                               CL**6
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**6
00008  DATE-WRITTEN            06-09-1998.                                 CL**6
00009  DATE-COMPILED.                                                      CL**6
00010                                                                      CL**6
00011 *-------------------------------------------------------------*      CL**6
00012 * THIS PROGRAM IS RUN IF THERE IS A PROBLEM PRINTING THE CREDIT      CL**6
00013 * REFUND CHECKS.  THE PROGRAM ACCEPTS THE FOLLOWING OPERATOR         CL**6
00014 * INPUT:                                                             CL**6
      *   POS 01-09 - FIRST CHECK NUMBER TO BE VOIDED                  *   CL**6
      *   POS 10    - SPACE                                            *        
      *   POS 11-19 - LAST CHECK NUMBER TO BE VOIDED                   *   CL**6
      *   POS 20    - SPACE                                            *        
      *   POS 21-29 - NEW STARTING CHECK NUMBER                        *   CL**6
00017 *                                                                    CL**6
00018 * THE PROGRAM WILL VOID THE CHECKS THAT NEED TO BE VOIDED AND        CL**6
00019 * DELETE THE REST OF THE CHECKS THAT CAN BE REPRINTED.               CL**6
00011 *-------------------------------------------------------------*      CL**6
00035                                                                      CL**6
00036 ******************************************************************   CL**6
00037  ENVIRONMENT DIVISION.                                               CL**6
00038 ******************************************************************   CL**6
00039                                                                      CL**6
00040  CONFIGURATION SECTION.                                              CL**6
00041                                                                      CL**6
00042  SOURCE-COMPUTER.        IBM-3090.                                   CL**6
00043  OBJECT-COMPUTER.        IBM-3090.                                   CL**6
00044                                                                      CL**6
00045  INPUT-OUTPUT SECTION.                                               CL**6
00046                                                                      CL**6
00047  FILE-CONTROL.                                                       CL**6
00048                                                                      CL**6
00049       SELECT FIRST-CHECK-CARD-FILE   ASSIGN TO INP01.                CL**6
00049       SELECT RESTART-CHECK-CARD-FILE ASSIGN TO INP02.                CL**6
00050                                                                      CL**6
00051 ******************************************************************   CL**6
00052  DATA DIVISION.                                                      CL**6
00053 ******************************************************************   CL**6
00054                                                                      CL**6
00055  FILE SECTION.                                                       CL**6
00056  FD  FIRST-CHECK-CARD-FILE                                           CL**6
00057      LABEL RECORDS ARE STANDARD                                      CL**6
00058      BLOCK CONTAINS 0 RECORDS                                        CL**6
00059      RECORDING MODE IS F                                             CL**6
00060      RECORD CONTAINS 80  CHARACTERS                                  CL**6
00061      DATA RECORD IS FIRST-CHECK-CARD-RECORD.                         CL**6
00062                                                                      CL**6
00063  01  FIRST-CHECK-CARD-RECORD          PIC X(80).                     CL**6
                                                                                
00056  FD  RESTART-CHECK-CARD-FILE                                         CL**6
00057      LABEL RECORDS ARE STANDARD                                      CL**6
00058      BLOCK CONTAINS 0 RECORDS                                        CL**6
00059      RECORDING MODE IS F                                             CL**6
00060      RECORD CONTAINS 80  CHARACTERS                                  CL**6
00061      DATA RECORD IS CONTROL-CARD-RECORD.                             CL**6
00062                                                                      CL**6
00063  01  RESTART-CHECK-CARD-RECORD        PIC X(80).                     CL**6
                                                                                
                                                                                
00065 *------------------------------------------------------*             CL**6
00066  WORKING-STORAGE SECTION.                                            CL**6
00067 *------------------------------------------------------*             CL**6
00075  01 RESTART-CHECK-CARD-LAYOUT.                                       CL**6
00076     05 RESTART-FIRST-VOID-CHECK     PIC X(09).                       CL**6
          05 FILLER                       PIC X(01).                            
00076     05 RESTART-LAST-VOID-CHECK      PIC X(09).                       CL**6
          05 FILLER                       PIC X(01).                            
00077     05 RESTART-FIRST-START-CHECK    PIC X(09).                       CL**6
                                                                                
00187  01 FIRST-CHECK-CARD-LAYOUT.                                         CL**1
00188     05  FIRST-CARD-CHECK-NBR        PIC X(09).                       CL**1
                                                                                
00080  01 PS-PROGRAM-SWITCHES.                                             CL**6
          05 PS-END-OF-CR-BAL-RFND-SW     PIC X(01)  VALUE 'N'.                 
             88 PS-END-OF-CR-BAL-RFND                VALUE 'Y'.                 
                                                                                
00080  01 PC-PROGRAM-CONSTANTS.                                            CL**6
00083     05 PC-VOID-IND                  PIC X(01)  VALUE 'V'.            CL**6
00084     05 PC-PROGRAM-NAME              PIC X(08)  VALUE 'ARKUT054'.     CL**6
00087                                                                      CL**6
00094  01 PROGRAM-VARIABLES.                                               CL**6
00095     05 PV-ABEND-CODE                 PIC 9(04) VALUE ZEROES.         CL**6
00111     05 PV-ABEND-PARA-NAME            PIC X(30) VALUE SPACES.         CL**6
00112     05 PV-DB2-OPERATION-MSG-1        PIC X(40) VALUE SPACES.         CL**6
00113     05 PV-DB2-TABLE-NAME             PIC X(12) VALUE SPACE.          CL**6
00114     05 PV-SQLCODE                    PIC Z(8)9-.                     CL**6
00115     05 PV-PARAGRAPH-NAME             PIC X(30) VALUE SPACES.         CL**6
00117     05 PV-LAST-VOID-CHECK            PIC S9(09) VALUE ZEROES COMP.   CL**6
00117     05 PV-NEXT-GOOD-CHECK            PIC 9(09) VALUE ZEROES.         CL**6
          05 PV-NEXT-GOOD-CHECK-X REDEFINES                                     
             PV-NEXT-GOOD-CHECK            PIC X(09).                           
00071 ***********************************************************       ARKRP001
00072 *  STANDARD WORKING STORAGE FOR THE CALENDAR ROUTINE              ARKRP001
00071 ***********************************************************       ARKRP001
00168      COPY DPWS500.                                                   CL**1
                                                                                
00274      COPY ARWS051.                                                   CL**1
00071 ***********************************************************       ARKRP001
00119 **** DB2 MESSAGE AREAS ****                                          CL**6
00071 ***********************************************************       ARKRP001
00120      COPY DPWS004.                                                   CL**6
00121                                                                      CL**6
00122      EXEC SQL                                                        CL**6
00123           INCLUDE SQLCA                                              CL**6
00124      END-EXEC.                                                       CL**6
00125                                                                      CL**6
00126      EXEC SQL                                                        CL**6
00127           INCLUDE TCRRFND                                            CL**6
00128      END-EXEC.                                                       CL**6
00129                                                                      CL**6
00130 ********************************************************             CL**6
00131  PROCEDURE DIVISION.                                                 CL**6
00132 ********************************************************             CL**6
00133  0000-MAIN.                                                          CL**6
           PERFORM 1000-OPEN.                                                   
00134      PERFORM 2000-INITIALIZATION.                                    CL**6
00135      PERFORM 3000-UPDATE-TCRRFND-VOID-IND.                           CL**6
00136      PERFORM 4000-DELETE-TCRRFND-UNUSED.                             CL**6
           PERFORM 5000-CLOSE.                                                  
           STOP RUN.                                                            
                                                                                
00138                                                                      CL**6
      *******************************                                           
       1000-OPEN.                                                          CL**6
      *******************************                                           
                                                                                
           OPEN INPUT RESTART-CHECK-CARD-FILE                                   
                      FIRST-CHECK-CARD-FILE.                                    
                                                                                
      *******************************                                           
00139  2000-INITIALIZATION.                                                CL**6
      *******************************                                           
      *-------------------------------------------------------------            
      *    READ INPUT CONTROL CARD                                              
      *-------------------------------------------------------------            
           READ RESTART-CHECK-CARD-FILE                                         
              INTO RESTART-CHECK-CARD-LAYOUT.                                   
                                                                                
           READ FIRST-CHECK-CARD-FILE                                           
              INTO FIRST-CHECK-CARD-LAYOUT.                                     
                                                                                
      *-------------------------------------------------------------            
      *  VALIDATE THE FIRST CHECK TO BE VOIDED.  MAKE SURE IT IS                
      *  NUMERIC, NOT EQUAL TO ZERO, LESS THAN THE LAST CHECK TO BE             
      *  VOIDED AND LESS THAN THE NEW CHECK TO START WITH.                      
      *-------------------------------------------------------------            
00158      IF RESTART-FIRST-VOID-CHECK IS NOT NUMERIC OR                   CL**6
00159         RESTART-FIRST-VOID-CHECK = ZEROES OR                         CL**6
              RESTART-FIRST-VOID-CHECK > RESTART-LAST-VOID-CHECK OR             
              RESTART-FIRST-VOID-CHECK > RESTART-FIRST-START-CHECK OR           
              RESTART-FIRST-VOID-CHECK < FIRST-CARD-CHECK-NBR                   
00160            DISPLAY '***************************'                     CL**6
00161            DISPLAY 'FIRST CHECK TO BE VOIDED '                       CL**6
00162                     RESTART-FIRST-VOID-CHECK                         CL**6
00163                    ' IS INVALID. PLEASE REENTER'                     CL**6
00163                    ' IN POSITIONS 01-09.'                            CL**6
00164            DISPLAY ' RUN ABORTED'                                    CL**6
00165            DISPLAY '***************************'                     CL**6
00166            MOVE 4003  TO PV-ABEND-CODE                               CL**6
00167            PERFORM 9999-ABORT                                        CL**6
00168      END-IF.                                                         CL**6
      *-------------------------------------------------------------            
      *  VALIDATE THE LAST CHECK TO BE VOIDED.  MAKE SURE IT IS                 
      *  NUMERIC, NOT EQUAL TO ZERO, AND LESS THAN THE NEW CHECK TO             
      *  START WITH.                                                            
      *-------------------------------------------------------------            
00158      IF RESTART-LAST-VOID-CHECK IS NOT NUMERIC OR                    CL**6
00159         RESTART-LAST-VOID-CHECK = ZEROES OR                          CL**6
              RESTART-LAST-VOID-CHECK > RESTART-FIRST-START-CHECK OR            
              RESTART-LAST-VOID-CHECK < FIRST-CARD-CHECK-NBR                    
00160            DISPLAY '***************************'                     CL**6
00161            DISPLAY 'LAST CHECK TO BE VOIDED '                        CL**6
00162                     RESTART-LAST-VOID-CHECK                          CL**6
00163                    ' IS INVALID. PLEASE REENTER IT'                  CL**6
00163                    ' IN POSITIONS 11-19.'                            CL**6
00164            DISPLAY 'RUN ABORTED'                                     CL**6
00165            DISPLAY '***************************'                     CL**6
00166            MOVE 4003  TO PV-ABEND-CODE                               CL**6
00167            PERFORM 9999-ABORT                                        CL**6
           ELSE                                                                 
              MOVE RESTART-LAST-VOID-CHECK TO PV-NEXT-GOOD-CHECK                
              ADD 1 TO PV-NEXT-GOOD-CHECK                                       
00168      END-IF.                                                         CL**6
      *-------------------------------------------------------------            
      *  VALIDATE THE NEW START CHECK NUMBER.  MAKE SURE IT IS                  
      *  NUMERIC, NOT EQUAL TO ZERO AND IT IS AN ODD NUMBER.                    
      *-------------------------------------------------------------            
00158      IF RESTART-FIRST-START-CHECK NOT NUMERIC OR                     CL**6
00159         RESTART-FIRST-START-CHECK = ZEROES OR                        CL**6
              RESTART-FIRST-START-CHECK <= FIRST-CARD-CHECK-NBR                 
00160         DISPLAY '***************************'                        CL**6
00161         DISPLAY 'NEW START CHECK NUMBER '                            CL**6
00162                  RESTART-FIRST-START-CHECK                           CL**6
00163                 ' IS INVALID. PLEASE REENTER IT'                     CL**6
00163                 ' IN POSITIONS 21-29.'                               CL**6
00164         DISPLAY 'RUN ABORTED'                                        CL**6
00165         DISPLAY '***************************'                        CL**6
00166         MOVE 4003  TO PV-ABEND-CODE                                  CL**6
00167         PERFORM 9999-ABORT                                           CL**6
00168      END-IF.                                                         CL**6
                                                                                
           IF RESTART-FIRST-START-CHECK NOT = PV-NEXT-GOOD-CHECK-X              
00160         DISPLAY '***************************'                        CL**6
00161         DISPLAY 'NEW START CHECK NUMBER '                            CL**6
00162                  RESTART-FIRST-START-CHECK                           CL**6
00163                 ' MAY BE INVALID.'                                   CL**6
00163         DISPLAY 'IF IT IS VALID, CONTACT SUPPORT.'                   CL**6
              DISPLAY 'IF IT IS INVALID, RE-ENTER.'                             
00164         DISPLAY 'RUN ABORTED'                                        CL**6
00165         DISPLAY '***************************'                        CL**6
00166         MOVE 4003  TO PV-ABEND-CODE                                  CL**6
00167         PERFORM 9999-ABORT                                           CL**6
00168      END-IF.                                                         CL**6
00222                                                                      CL**6
00379      DISPLAY '------------------------------------'.                 CL**1
           DISPLAY '        ARKUT054 CNTL CARDS         '.                      
00379      DISPLAY '------------------------------------'.                 CL**1
00226      DISPLAY 'RESTART CNTL CARD : ' RESTART-CHECK-CARD-LAYOUT.       CL**6
           DISPLAY 'FIRST CK CNTL CARD: ' FIRST-CHECK-CARD-LAYOUT.              
00379      DISPLAY '------------------------------------'.                 CL**1
                                                                                
      *-------------------------------------------------------------            
      *  THIS SECTION WILL VOID THE CHECKS IN THE TABLE THAT                    
      *  WILL NEED TO BE REPRINTED.                                             
      *-------------------------------------------------------------            
      *******************************                                           
00254  3000-UPDATE-TCRRFND-VOID-IND.                                       CL**6
      *******************************                                           
                                                                                
            MOVE RESTART-FIRST-VOID-CHECK TO CRRFND-CK-NBR.                     
            MOVE RESTART-LAST-VOID-CHECK  TO PV-LAST-VOID-CHECK.                
                                                                                
00284       EXEC SQL                                                       CL**6
00285          UPDATE TCRRFND                                              CL**6
00286             SET VOID_IND = :PC-VOID-IND                              CL**6
00287             WHERE CK_NBR  BETWEEN :CRRFND-CK-NBR                     CL**6
00290                               AND :PV-LAST-VOID-CHECK                CL**6
            END-EXEC.                                                           
                                                                                
00292       IF SQLCODE NOT = ZEROES                                        CL**6
00160         DISPLAY '*******************************************'        CL**6
00161         DISPLAY '!!!CHECK NUMBER NOT FOUND ON TABLE!!!.'             CL**6
00163         DISPLAY 'PLEASE VERIFY OPERATOR INPUT.'                      CL**6
00163         DISPLAY '*******************************************'        CL**6
00297         MOVE '2100-VOID-CHECKS'  TO PV-ABEND-PARA-NAME               CL**6
00298         MOVE 'UPDATE TABLE'      TO PV-DB2-OPERATION-MSG-1           CL**6
00299         MOVE 'TCRRFND'           TO PV-DB2-TABLE-NAME                CL**6
00300         PERFORM 9998-SQL-ABEND                                       CL**6
00301       END-IF.                                                        CL**6
                                                                                
      *-------------------------------------------------------------            
      *  THIS SECTION WILL DELETE THE CHECKS THAT WERE ENTERED IN THE           
      *  TABLE THAT CAN STILL BE USED.                                          
      *-------------------------------------------------------------            
      *******************************                                           
00304  4000-DELETE-TCRRFND-UNUSED.                                         CL**6
      *******************************                                           
                                                                                
           MOVE RESTART-FIRST-START-CHECK TO CRRFND-CK-NBR                      
                                                                                
00305      EXEC SQL                                                        CL**6
00306         DELETE FROM TCRRFND                                          CL**6
00307            WHERE  CK_NBR >= :CRRFND-CK-NBR                           CL**6
00310      END-EXEC.                                                       CL**6
00311                                                                      CL**6
00312      EVALUATE TRUE                                                   CL**6
00313         WHEN SQLCODE = 0                                             CL**6
00314         WHEN SQLCODE = +100                                          CL**6
00315              CONTINUE                                                CL**6
00316         WHEN OTHER                                                   CL**6
00317              MOVE '2500-DELETE-NOT-USED-CHECKS'                      CL**6
00318                                       TO PV-ABEND-PARA-NAME          CL**6
00319              MOVE 'DELETE'            TO PV-DB2-OPERATION-MSG-1      CL**6
00320              MOVE 'TCRRFND'           TO PV-DB2-TABLE-NAME           CL**6
00321              PERFORM 9998-SQL-ABEND                                  CL**6
00322      END-EVALUATE.                                                   CL**6
                                                                                
                                                                                
      *******************************                                           
       5000-CLOSE.                                                         CL**6
      *******************************                                           
                                                                                
           CLOSE RESTART-CHECK-CARD-FILE                                        
                 FIRST-CHECK-CARD-FILE.                                         
                                                                                
      *-------------------------------------------------------------            
      *  DB2 ABEND.                                                             
      *-------------------------------------------------------------            
      *******************************                                           
00367  9998-SQL-ABEND.                                                     CL**6
      *******************************                                           
                                                                                
00368      MOVE SQLCODE     TO PV-SQLCODE.                                 CL**6
00369      DISPLAY '** ABEND           ** = SQLABEND'.                     CL**6
00370      DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.             CL**6
00371      DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.           CL**6
00372      DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME.           CL**6
00373      DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.      CL**6
00374      DISPLAY '** SQLCODE         ** = ' PV-SQLCODE.                  CL**6
00375      COPY DPPD004.                                                   CL**6
00376                                                                      CL**6
00377      EXEC SQL                                                        CL**6
00378           ROLLBACK                                                   CL**6
00379      END-EXEC.                                                       CL**6
00380                                                                      CL**6
00381      MOVE +4013                  TO PV-ABEND-CODE.                   CL**6
00382      CALL 'ILBOABN0'   USING PV-ABEND-CODE.                          CL**6
00384                                                                      CL**6
      *-------------------------------------------------------------            
      *  NON-DB2 ABEND.                                                         
      *-------------------------------------------------------------            
      *******************************                                           
00385  9999-ABORT.                                                         CL**6
      *******************************                                           
00386       DISPLAY 'PV-ABEND-CODE = ' PV-ABEND-CODE                       CL**6
00387       CALL 'ILBOABN0' USING PV-ABEND-CODE.                           CL**6
00389                                                                      CL**6
