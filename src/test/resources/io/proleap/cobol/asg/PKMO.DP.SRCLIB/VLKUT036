000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT036.                                        
000400 AUTHOR.                 PATRICK BURKE.                                   
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900* ****************************************************************        
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING RECEIVER DETAIL             
001200*  INFORMATION AND AN ASN_RECEIVER_XREF FILE TO LINK THE RECEIVER         
001300*  INFORMATION WITH THE ASN'S                                             
001410* ****************************************************************        
001500*                                                                         
001600 ENVIRONMENT DIVISION.                                                    
001700                                                                          
001800 CONFIGURATION SECTION.                                                   
001900                                                                          
002000 SOURCE-COMPUTER.                        IBM-3090.                        
002100 OBJECT-COMPUTER.                        IBM-3090.                        
002200                                                                          
002300 INPUT-OUTPUT SECTION.                                                    
002400                                                                          
002500 FILE-CONTROL.                                                            
002600                                                                          
002700     SELECT VL-EXTRACT-FILE                                               
002800         ASSIGN TO INP01.                                                 
002900                                                                          
003000     SELECT VL-RECEIVER-DTL-FILE                                          
003100         ASSIGN TO OUT01.                                                 
003200                                                                          
003300     SELECT  VL-ASN-RCVR-XREF-FILE                                        
003400         ASSIGN TO OUT02.                                                 
003500                                                                          
003600     SELECT  VL-MARKER-FILE                                               
003700         ASSIGN TO OUT03.                                                 
003800                                                                          
003900******************************************************************        
004000 DATA DIVISION.                                                           
004100******************************************************************        
004200                                                                          
004300 FILE SECTION.                                                            
004400*                                                                         
004500 FD  VL-EXTRACT-FILE                                                      
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900 01  VL-EXTRACT-RECORD                PIC  X(50).                         
005000                                                                          
005100 FD  VL-RECEIVER-DTL-FILE                                                 
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  VL-RECEIVER-DTL-RECORD           PIC  X(239).                        
005600*                                                                         
005700 FD  VL-ASN-RCVR-XREF-FILE                                                
005800     RECORDING MODE IS F                                                  
005900     LABEL RECORDS ARE STANDARD                                           
006000     BLOCK CONTAINS 0 RECORDS.                                            
006100 01  VL-XREF-RECORD                   PIC  X(122).                        
006200*                                                                         
006300 FD  VL-MARKER-FILE                                                       
006400     RECORDING MODE IS F                                                  
006500     LABEL RECORDS ARE STANDARD                                           
006600     BLOCK CONTAINS 0 RECORDS.                                            
006700 01  VL-MARKER-RECORD                 PIC  X(78).                         
006800*                                                                         
006900*                                                                         
007000 WORKING-STORAGE SECTION.                                                 
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
007100*****************************************************************         
007200 01  DK-DB2-KEYS.                                                         
007300     05  DK-PO-NBR                     PIC S9(09)  COMP.                  
007400     05  DK-REC-SEQ-NBR                PIC S9(09)  COMP.                  
007500     05  DK-UPC-NBR                    PIC S9(13)  COMP.                  
007600                                                                          
007700*****************************************************************         
007800*    PROGRAM CONSTANTS                                                    
007900*****************************************************************         
008000 01  PC-CONSTANTS.                                                        
008100     05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE 'VLKUT036'.        
008200     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
008300                                                 COMP SYNC.               
008400                                                                          
008500*****************************************************************         
008600*    PROGRAM SWITCHES                                                     
008700*****************************************************************         
008800 01  PS-SWITCHES.                                                         
008900     05  PS-END-INPUT-SWITCH          PIC  X(01) VALUE 'N'.               
009000         88  PS-END-INPUT                        VALUE 'Y'.               
009100         88  PS-NOT-END-INPUT                    VALUE 'N'.               
009201                                                                          
009310     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
009420         88  PS-EOF-DETAIL-CSR                   VALUE 'Y'.               
009530         88  PS-NOT-END-DETAIL-CSR               VALUE 'N'.               
009640                                                                          
009310     05  PS-PKSLIP-CURSOR-SWITCH      PIC  X(01) VALUE 'N'.               
009420         88  PS-END-PKSLIP-CSR                   VALUE 'Y'.               
009530         88  PS-NOT-END-PKSLIP-CSR               VALUE 'N'.               
009640                                                                          
009310     05  PS-PBS-IND-SWITCH            PIC  X(01) VALUE 'N'.               
009420         88  PS-PBS-ORDER                        VALUE 'Y'.               
009530         88  PS-NOT-PBS-ORDER                    VALUE 'N'.               
009640                                                                          
009310     05  PS-FIRST-DTL-FOR-RCVR-SWITCH PIC  X(01) VALUE 'N'.               
009420         88  PS-FIRST-DTL-FOR-RCVR               VALUE 'Y'.               
009530         88  PS-NOT-FIRST-DTL-FOR-RCVR           VALUE 'N'.               
009640                                                                          
009700*****************************************************************         
009800*    PROGRAM VARIABLES                                                    
009900*****************************************************************         
010000 01  PV-VARIABLES.                                                        
010100     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
010200                                                 COMP SYNC.               
011000     05  PV-READ-COUNT                PIC S9(04) VALUE +0                 
011100                                                 COMP SYNC.               
           05  PV-CURR-STR-NBR              PIC S9(04) VALUE +0                 
010200                                                 COMP SYNC.               
           05  PV-CURR-ITM-NBR              PIC S9(15)V VALUE +0                
                                                        COMP-3.                 
011200     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
011300     05  PV-XREF-RECS-WRITTEN         PIC  9(09) VALUE ZERO.              
011300     05  PV-ITEM-NBR                  PIC  9(15) VALUE ZERO.              
011400     05  PV-RECEIVER.                                                     
011500         10  FILLER                   PIC  9(06) VALUE ZERO.              
011600         10  PV-REC                   PIC  9(03) VALUE ZERO.              
011700     05  PV-PO-RECEIVER.                                                  
011800         10  PV-PO-NBR                PIC  X(08) VALUE SPACES.            
011900         10  PV-REC-SEQ-NBR           PIC  X(03) VALUE SPACES.            
012000         10  FILLER                   PIC  X(09) VALUE SPACES.            
012100     05  PV-ITEM-COST-UNPACK          PIC  9(4)V999.                      
012100     05  PV-ITEM-COST-4               PIC  9(09)V9999.                    
012100     05  PV-ITEM-RTL-UNPACK           PIC  9(6)V99.                       
012400     05  PV-ITEM-RETAIL-4             PIC  9(09)V9999.                    
013300     05  PV-STORE-RCV-UNITS-4.                                            
013400         10  PV-STORE-RCV-UNITS       PIC 9(09) VALUE ZERO.               
013500         10  FILLER                   PIC 9(04) VALUE ZERO.               
013800     05  PV-CCYYMMDD-DATE.                                                
013900         10  PV-CC                    PIC  X(02).                         
014000         10  PV-YY                    PIC  X(02).                         
014100         10  PV-MM                    PIC  X(02).                         
014200         10  PV-DD                    PIC  X(02).                         
014300     05  PV-DB2-DATE.                                                     
014400         10  PV-DB2-CC                  PIC X(02).                        
014500         10  PV-DB2-YY                  PIC X(02).                        
014600         10  FILLER                     PIC X(01).                        
014700         10  PV-DB2-MM                  PIC X(02).                        
014800         10  FILLER                     PIC X(01).                        
014900         10  PV-DB2-DD                  PIC X(02).                        
015000         10  FILLER                     PIC X(16).                        
000076     05  PV-RUN-DATE-CN.                                                  
000077         10  PV-RUN-CCYY-CN           PIC  X(04) VALUE SPACES.            
000078         10  PV-RUN-MO-CN             PIC  X(02) VALUE SPACES.            
000079         10  PV-RUN-DD-CN             PIC  X(02) VALUE SPACES.            
000080     05  PV-RUN-TIME                  PIC  X(08) VALUE SPACES.            
000081     05  PV-RUN-TIME-R REDEFINES PV-RUN-TIME.                             
000082         10  PV-RUN-HH                PIC  X(02).                         
000083         10  PV-RUN-MM                PIC  X(02).                         
000084         10  PV-RUN-SS                PIC  X(02).                         
000085         10  FILLER                   PIC  X(02).                         
000086     05  PV-RUN-TIME-CN.                                                  
000087         10  PV-RUN-HH-CN             PIC  X(02) VALUE SPACES.            
000088         10  PV-RUN-MM-CN             PIC  X(02) VALUE SPACES.            
000089         10  PV-RUN-SS-CN             PIC  X(02) VALUE SPACES.            
009120***** FILE NAME CONSISTS OF RUN-DATE_RECEIVER.DAT                         
009130*****     EXAMPLE: 20050317_RECEIVER.DAT                                  
009140     05  PV-FILE-NAME.                                                    
009150         10  PV-CREATE-DATE             PIC  X(08) VALUE SPACES.          
009150         10  PV-UNDERSCORE              PIC  X(01) VALUE '_'.             
009170         10  PV-RECEIVER-DAT            PIC  X(12) VALUE SPACES.          
014300     05  PV-EDIISA-CNTL-DATE            PIC  X(10).                       
014300     05  PV-ASN-REC-DTE.                                                  
014400         10  PV-ASN-REC-CC              PIC  X(02).                       
014500         10  PV-ASN-REC-YY              PIC  X(02).                       
014600         10  FILLER                     PIC  X(01).                       
014700         10  PV-ASN-REC-MM              PIC  X(02).                       
014800         10  FILLER                     PIC  X(01).                       
014900         10  PV-ASN-REC-DD              PIC  X(02).                       
010500     05  PV-PKSLIP-SEQ-NBR              PIC  9(04) VALUE ZEROS.           
010500     05  PV-PKSLIP-SEQ-NBR-R REDEFINES PV-PKSLIP-SEQ-NBR                  
010500                                        PIC  X(04).                       
015600                                                                          
015700 01  PV-ABEND-FIELDS.                                                     
010300     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
010400                                                 COMP SYNC.               
010500     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
010600     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
010700     05  PV-ABEND-TABLE-1             PIC  X(20) VALUE SPACES.            
010800     05  PV-ABEND-TABLE-2             PIC  X(20) VALUE SPACES.            
010900     05  PV-ERROR-MSG1                PIC  X(60) VALUE SPACES.            
010900     05  PV-ERROR-MSG2                PIC  X(60) VALUE SPACES.            
010900     05  PV-ERROR-MSG3                PIC  X(60) VALUE SPACES.            
008960     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
008960     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
008970     05  PV-ABEND-STRUCTURE-1         PIC  X(15) VALUE SPACES.            
008980     05  PV-ABEND-STRUCTURE-2         PIC  X(15) VALUE SPACES.            
008990     05  PV-ABEND-STRUCTURE-3         PIC  X(15) VALUE SPACES.            
008991     05  PV-ABEND-PO                  PIC  X(09) VALUE SPACES.            
008992     05  PV-ABEND-RCVR                PIC  X(09) VALUE SPACES.            
008998                                                                          
009180                                                                          
009190***  MARKER FILE LAYOUT                                                   
009191     COPY VLRD029.                                                        
009192                                                                          
010023******************************************************************        
010024*  INPUT LAYOUT FOR FEEDER FILE                                           
010025******************************************************************        
010026     COPY VLRD038.                                                        
010027                                                                          
009200*****************************************************************         
009310***  RECEIVER DETAIL FILE LAYOUT                                          
009311*****************************************************************         
009320     COPY VLRD036.                                                        
009330                                                                          
009200*****************************************************************         
009310***  ASN RECEIVER XREF FILE LAYOUT                                        
009311*****************************************************************         
009320     COPY VLRD042.                                                        
009330                                                                          
009340*****************************************************************         
009700***  DB2 ABEND COPYBOOK                                                   
009710******************************************************************        
009800     COPY DPWS004.                                                        
009900/                                                                         
009902******************************************************************        
009903*    ABEND PROCESSING                                                     
009904******************************************************************        
009905     COPY DPWS013.                                                        
009906/                                                                         
009902******************************************************************        
009903*    CALENDAR ROUTINE COPYBOOK                                            
009904******************************************************************        
009905     COPY DPWS500.                                                        
009906/                                                                         
009910*****************************************************************         
009920*    DB2 AREA FOR ITEM TABLE                                    *         
009930*****************************************************************         
009940     EXEC SQL                                                             
009950          INCLUDE TPURITM                                                 
009960     END-EXEC.                                                            
009970/                                                                         
009971*****************************************************************         
009972*    DB2 AREA FOR RECEIVER TABLE                                *         
009973*****************************************************************         
009974     EXEC SQL                                                             
009975          INCLUDE TRECEIV                                                 
009976     END-EXEC.                                                            
009977/                                                                         
009978*****************************************************************         
009979*    DB2 AREA FOR DISTRIBUTION TABLE                            *         
009980*****************************************************************         
009981     EXEC SQL                                                             
009982          INCLUDE TRECDIS                                                 
009983     END-EXEC.                                                            
009984/                                                                         
009985*****************************************************************         
009990*    DB2 AREA FOR ITEM TABLE                                    *         
009991*****************************************************************         
009992     EXEC SQL                                                             
009993          INCLUDE TRECITM                                                 
009994     END-EXEC.                                                            
009995/                                                                         
010003*****************************************************************         
010004*    DB2 AREA FOR TPKSLIP                                       *         
010005*****************************************************************         
010006     EXEC SQL                                                             
010007          INCLUDE TPKSLIP                                                 
010008     END-EXEC.                                                            
009995                                                                          
010003*****************************************************************         
010004*    DB2 AREA FOR TASNHDR                                       *         
010005*****************************************************************         
010006     EXEC SQL                                                             
010007          INCLUDE TASNHDR                                                 
010008     END-EXEC.                                                            
009995                                                                          
010003*****************************************************************         
010004*    DB2 AREA FOR TASNORD                                       *         
010005*****************************************************************         
010006     EXEC SQL                                                             
010007          INCLUDE TASNORD                                                 
010008     END-EXEC.                                                            
009995                                                                          
010003*****************************************************************         
010004*    DB2 AREA FOR XLT_SKU                                       *         
010005*****************************************************************         
010006     EXEC SQL                                                             
010007          INCLUDE XLT00003                                                
010008     END-EXEC.                                                            
010009/                                                                         
010010******************************************************************        
010011*    DB2 DCLGEN FOR THE XLT-UPC TABLE                                     
010012******************************************************************        
010013     EXEC SQL                                                             
010014          INCLUDE XLT00006                                                
010015     END-EXEC.                                                            
010016                                                                          
010028*****************************************************************         
010030     EXEC SQL                                                             
010100         INCLUDE SQLCA                                                    
010200     END-EXEC.                                                            
010300                                                                          
010400     COPY SQLCA2.                                                         
010500                                                                          
010600******************************************************************        
010700         EXEC SQL                                                         
010800              DECLARE DETAIL-CSR CURSOR FOR                               
010900              SELECT  A.ITM_NBR                                           
011000                     ,A.ORD_LNE_NBR                                       
011100                     ,B.AP_VERIFY_ITM_QTY                                 
011500                     ,B.STR_NBR                                           
011800                     ,C.CRE_TMST                                          
011800                     ,C.PACK_BY_ST_IND                                    
011900              FROM    TRECITM A                                           
012000                     ,TRECDIS B                                           
012200                     ,TRECEIV C                                           
012300            WHERE A.ORD_NBR     = :DK-PO-NBR                              
012400              AND A.REC_SEQ_NBR = :DK-REC-SEQ-NBR                         
012500              AND A.ORD_NBR     = B.ORD_NBR                               
012600              AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                           
012610              AND A.ORD_LNE_NBR = B.ORD_LNE_NBR                           
012700              AND A.VER_STATUS_CDE  = 'A'                                 
012710              AND A.ORD_NBR     = C.ORD_NBR                               
012800              AND A.REC_SEQ_NBR = C.REC_SEQ_NBR                           
010900             ORDER BY A.ITM_NBR                                           
011500                     ,B.STR_NBR                                           
013000         END-EXEC.                                                        
013100                                                                          
010600******************************************************************        
010600*  THIS PKSLIP CSR WILL BE USED IF THE RECEIVER IS A PBS ORDER            
010600******************************************************************        
010700         EXEC SQL                                                         
010800              DECLARE PBS-PKSLIP-CSR CURSOR FOR                           
010900               SELECT P.SEQ_NBR                                           
010900                    , P.PKSL_NBR                                          
011000                    , P.ASN_NBR                                           
011000                    , P.PO_NBR                                            
010900                    , DATE(A.EDIISA_CNTL_TMST)                            
011900                 FROM TPKSLIP P                                           
011000                    , TASNHDR A                                           
011000                    , TASNORD O                                           
012300                WHERE P.PO_NBR        = :DK-PO-NBR                        
012400                  AND P.REC_SEQ_NBR   = :DK-REC-SEQ-NBR                   
012710                  AND P.VER_STAT_CDE  = 'A'                               
011900                  AND P.ASN_NBR       = A.ASNHDR_NBR                      
011900                  AND A.ASNHDR_ID     = O.ASNHDR_ID                       
011900                  AND CHAR(P.PO_NBR)  = O.PO_NBR                          
011900                  AND P.STR_DEST_NBR  = O.LOC_NBR                         
013000         END-EXEC.                                                        
013100                                                                          
010600******************************************************************        
010600*  THIS PKSLIP CSR WILL BE USED IF THE RECEIVER IS NOT A PBS ORDER        
010600******************************************************************        
010700         EXEC SQL                                                         
010800              DECLARE PKSLIP-CSR CURSOR FOR                               
010900               SELECT P.SEQ_NBR                                           
010900                    , P.PKSL_NBR                                          
011000                    , P.ASN_NBR                                           
011000                    , P.PO_NBR                                            
010900                    , DATE(A.EDIISA_CNTL_TMST)                            
011900                 FROM TPKSLIP P                                           
011000                    , TASNHDR A                                           
011000                    , TASNORD O                                           
012300                WHERE P.PO_NBR        = :DK-PO-NBR                        
012400                  AND P.REC_SEQ_NBR   = :DK-REC-SEQ-NBR                   
012710                  AND P.VER_STAT_CDE  = 'A'                               
011900                  AND P.ASN_NBR       = A.ASNHDR_NBR                      
011900                  AND A.ASNHDR_ID     = O.ASNHDR_ID                       
011900                  AND CHAR(P.PO_NBR)  = O.PO_NBR                          
013000         END-EXEC.                                                        
013100                                                                          
013500                                                                          
013600 LINKAGE SECTION.                                                         
013700                                                                          
013800 PROCEDURE DIVISION.                                                      
013900*****************************************************************         
014000*  MAINLINE LOGIC FOR PROGRAM                                             
014100*****************************************************************         
014200 0000-MAINLINE.                                                           
014300     PERFORM 1000-INITIALIZE.                                             
014400                                                                          
014500     PERFORM 1100-PROCESS                                                 
014600         UNTIL PS-END-INPUT.                                              
014700                                                                          
014800     PERFORM 9000-EOJ.                                                    
014900                                                                          
015000     STOP RUN.                                                            
015100                                                                          
015200*****************************************************************         
015300*  INITIALIZATION PARAGRAPH                                               
015310*  OPEN INPUT AND OUTPUT FILES, INITIAL READ AND PROCESS FOR FIRST        
015320*  PO/RECEIVER.                                                           
015400*****************************************************************         
015500 1000-INITIALIZE.                                                         
015501                                                                          
015510     OPEN INPUT  VL-EXTRACT-FILE.                                         
015610     OPEN OUTPUT VL-RECEIVER-DTL-FILE                                     
015620                 VL-ASN-RCVR-XREF-FILE                                    
015620                 VL-MARKER-FILE.                                          
015621                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000127     DISPLAY '************************************************'           
000128     DISPLAY '*******  CONTROL CARD ====>  ' CC-CONTROL-CARD.             
000129                                                                          
000130     PERFORM 1050-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED WITHIN CURRENT-DATE IN POSITION 9 FOR 8         
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME.                      
000135                                                                          
000136     DISPLAY '*******  RUN TIME ====> '  PV-RUN-TIME.                     
000137                                                                          
000138     MOVE PV-RUN-HH          TO PV-RUN-HH-CN.                             
000139     MOVE PV-RUN-MM          TO PV-RUN-MM-CN.                             
000140     MOVE PV-RUN-SS          TO PV-RUN-SS-CN.                             
000141                                                                          
015710     PERFORM 2100-READ-EXTRACT.                                           
015703     IF PS-NOT-END-INPUT                                                  
               INITIALIZE PV-CURR-STR-NBR                                       
                          PV-CURR-ITM-NBR                                       
015720         SET PS-FIRST-DTL-FOR-RCVR TO TRUE                                
015720         PERFORM 4500-OPEN-DETAIL-CSR                                     
015720**     2000-PROCESS IS PERFORMED WITHIN 4520-FETCH-DETAIL-CSR             
015730         PERFORM 4520-FETCH-DETAIL-CSR                                    
015740            UNTIL PS-EOF-DETAIL-CSR                                       
016010         PERFORM 4510-CLOSE-DETAIL-CSR                                    
021100** THIS IS THE LAST DETAIL RECORD PROCESSED IN THE CURSOR.  IT WAS        
021100** NOT WRITTEN TO THE OUTPUT FILE AS PART OF THE DETAIL CURSOR.           
021100** IT NEEDS TO BE WRITTEN TO THE OUTPUT FILE HERE.                        
021000         PERFORM 8000-WRITE-DETAIL                                        
016020         SET PS-NOT-END-DETAIL-CSR TO TRUE                                
021100                                                                          
021100**     GET PACKSLIP AND ASN INFORMATION                                   
021100**     THE PACK BY STORE INDICATOR FROM TRECEIV WAS MOVED TO              
021100**     PS-PBS-IND-SWITCH IN 2000- PROCESS                                 
021100**     IF IT IS A PACK BY STORE ORDER, USE THE PBS-PKSLIP CURSOR          
021100**     THIS WILL GET ALL UNIQUE PACKSLIP/SEQ FOR A RECEIVER AND           
021100**     THE EDIISA_TMST FROM THE ASN HEADER TABLE                          
021100**     IF NOT A PACK BY STORE ORDER USE THE PKSLIP-CURSOR                 
021100**     THIS WILL GET THE EDIISA_TMST FROM THE ASN HEADER TABLE            
021100**     AND ONLY RETRIEVE 1 RECORD WHEN THERE ARE MULTIPLE RECORDS         
021100**     FOR A PACKSLIP DUE TO CHANGES TO THE PACKSLIP.  WE ONLY            
021100**     WANT THE EDIISA-TMST FOR THE 1ST TIME THE ASN WAS RECEIVED         
021100**     FOR THAT PO AND VENDOR                                             
021100         SET PS-NOT-END-PKSLIP-CSR TO TRUE                                
021100         IF PS-PBS-ORDER                                                  
021000             PERFORM 7300-OPEN-PBS-PKSLIP-CSR                             
021000             PERFORM 7500-FETCH-PBS-PKSLIP-CSR                            
021000                 UNTIL PS-END-PKSLIP-CSR                                  
021000             PERFORM 7400-CLOSE-PBS-PKSLIP-CSR                            
015703         ELSE                                                             
021000             PERFORM 7600-OPEN-PKSLIP-CSR                                 
021000             PERFORM 7700-FETCH-PKSLIP-CSR                                
021000                 UNTIL PS-END-PKSLIP-CSR                                  
021000             PERFORM 7800-CLOSE-PKSLIP-CSR                                
015703         END-IF                                                           
015703     ELSE                                                                 
015703         DISPLAY '***********************************************'        
015703         DISPLAY '**** EMPTY INPUT FILE - NO RECORDS TO PROCESS'          
015703         DISPLAY '***********************************************'        
015703     END-IF.                                                              
016030                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1050-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT036 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1050-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
016040*****************************************************************         
016050*  READ EXTRACT, OPEN CURSOR, FETCH UNTIL END OF CSR, CLOSE CSR           
016051*  REPEAT UNTIL END OF EXTRACT FILE.                                      
016060*****************************************************************         
016070 1100-PROCESS.                                                            
016092                                                                          
016093     PERFORM 2100-READ-EXTRACT.                                           
016094     IF PS-NOT-END-INPUT                                                  
               INITIALIZE PV-CURR-STR-NBR                                       
                          PV-CURR-ITM-NBR                                       
015720         SET PS-FIRST-DTL-FOR-RCVR TO TRUE                                
016095         PERFORM 4500-OPEN-DETAIL-CSR                                     
015720**     2000-PROCESS IS PERFORMED WITHIN 4520-FETCH-DETAIL-CSR             
016096         PERFORM 4520-FETCH-DETAIL-CSR                                    
016097            UNTIL PS-EOF-DETAIL-CSR                                       
016098         PERFORM 4510-CLOSE-DETAIL-CSR                                    
021100** THIS IS THE LAST DETAIL RECORD PROCESSED IN THE CURSOR.  IT WAS        
021100** NOT WRITTEN TO THE OUTPUT FILE AS PART OF THE DETAIL CURSOR.           
021100** IT NEEDS TO BE WRITTEN TO THE OUTPUT FILE HERE.                        
021000         PERFORM 8000-WRITE-DETAIL                                        
016099         SET PS-NOT-END-DETAIL-CSR    TO TRUE                             
021100                                                                          
021100** GET PACKSLIP AND ASN INFORMATION                                       
021100************** SEE COMMENT IN 1000-INITIALIZE  FOR EXPLANATION            
021100**************    OF  IF STATEMENT                                        
021100         SET PS-NOT-END-PKSLIP-CSR TO TRUE                                
021100         IF PS-PBS-ORDER                                                  
021000             PERFORM 7300-OPEN-PBS-PKSLIP-CSR                             
021000             PERFORM 7500-FETCH-PBS-PKSLIP-CSR                            
021000                 UNTIL PS-END-PKSLIP-CSR                                  
021000             PERFORM 7400-CLOSE-PBS-PKSLIP-CSR                            
015703         ELSE                                                             
021000             PERFORM 7600-OPEN-PKSLIP-CSR                                 
021000             PERFORM 7700-FETCH-PKSLIP-CSR                                
021000                 UNTIL PS-END-PKSLIP-CSR                                  
021000             PERFORM 7800-CLOSE-PKSLIP-CSR                                
015703         END-IF                                                           
016100     END-IF.                                                              
016101                                                                          
016110*****************************************************************         
  6200* PROCESS DATA FROM TABLES AND WRITE DETAIL OUTPUT RECORD.  A             
016200* CHECK NEEDS TO BE DONE BEFORE WRITING A RECORD TO THE OUTPUT            
016200* FILE.  IF THE NEXT DETAIL RECORD READ IS FOR THE SAME ITEM              
  6200* NUMBER AND STORE NUMBER AS THE CURRENT DETAIL RECORD BEING              
016200* PROCESSED,  ADD THE RECDIS-AP-VERIFY-ITM-QTY FROM THE NEXT              
016200* DETAIL RECORD TO THIS FIELD ON THE CURRENT DETAIL RECORD AND            
  6200* THEN FETCH THE NEXT DETAIL RECORD FROM THE CURSOR.  WHEN THE            
016200* ITEM NUMBER AND STORE NUMBER ON THE NEXT DETAIL RECORD IS               
016200* NOT THE SAME AS THE CURRENT DETAIL RECORD, THEN WRITE THE               
  6200* CURRENT DETAIL RECORD DATA TO THE EXTRACT FILE.                         
016200* A RECORD IS WRITTEN TO THE ASN-RECEIVER XREF FILE AFTER ALL             
016200* RECORDS IN THE DETAIL CURSOR HAS BEEN PROCESSED FOR THE                 
016200* SPECIFIED PO/RECEIVER.                                                  
016300*****************************************************************         
021420                                                                          
016400 2000-PROCESS.                                                            
017100     IF  RECITM-ITM-NBR = PV-CURR-ITM-NBR AND                             
017100         RECDIS-STR-NBR = PV-CURR-STR-NBR                                 
018500         ADD RECDIS-AP-VERIFY-ITM-QTY                                     
                                        TO PV-STORE-RCV-UNITS                   
018501         MOVE PV-STORE-RCV-UNITS-4                                        
                                        TO VL036-STORE-RCV-UNITS                
017100     ELSE                                                                 
017100         IF  PS-FIRST-DTL-FOR-RCVR                                        
017100             SET PS-NOT-FIRST-DTL-FOR-RCVR                                
017100                                  TO TRUE                                 
021410             PERFORM 2050-BUILD-RCVR-DTL-REC                              
 17100         ELSE                                                             
021000             PERFORM 8000-WRITE-DETAIL                                    
021410             PERFORM 2050-BUILD-RCVR-DTL-REC                              
017100         END-IF                                                           
 17100     END-IF.                                                              
020900                                                                          
021000     MOVE RECEIV-PACK-BY-ST-IND    TO PS-PBS-IND-SWITCH.                  
016101                                                                          
021200******************************************************************        
021300*  POPULATE THE FIELDS IN THE COMPLIANCE NETWORKS RECEIVER DETAIL         
021300*  RECORD.                                                                
021400******************************************************************        
021410 2050-BUILD-RCVR-DTL-REC.                                                 
021420                                                                          
016600     INITIALIZE VL036-RECEIVER-DETAIL-RECORD.                             
016700     MOVE 'A'                     TO VL036-ACD-INDICATOR.                 
016800     MOVE PV-PO-RECEIVER          TO VL036-RECEIVER-NBR.                  
016900     MOVE PV-PO-NBR               TO VL036-PO-NBR.                        
017100     MOVE SPACES                  TO VL036-UNIQUE-ADDENDA.                
017100                                                                          
017100*  ONLY DO THE UPC LOOKUP AND THE RETAIL/COST LOOKUP WHENEVER             
017100*  THE ITEM NUMBER CHANGES.                                               
           IF  RECITM-ITM-NBR NOT = PV-CURR-ITM-NBR                             
               MOVE RECITM-ITM-NBR      TO PV-CURR-ITM-NBR                      
               PERFORM 4600-GET-UPC-NBR                                         
               PERFORM 7200-SELECT-TPURITM                                      
           END-IF.                                                              
017100                                                                          
017309     MOVE DK-UPC-NBR              TO VL036-UPC.                           
017300     MOVE XLT00003-SKU-NBR        TO VL036-SKU.                           
017400     MOVE SPACES                  TO VL036-LINE-NBR.                      
071500     MOVE SPACES                  TO VL036-ITEM-COST-SIGN.                
017600     MOVE PURITM-UNT-COST-AMT     TO PV-ITEM-COST-UNPACK.                 
017600     MOVE PV-ITEM-COST-UNPACK     TO PV-ITEM-COST-4.                      
017610     MOVE PV-ITEM-COST-4          TO VL036-ITEM-COST.                     
017700     MOVE SPACES                  TO VL036-ITEM-RETAIL-SIGN.              
  7800     MOVE PURITM-UNT-RTL-PR-AMT   TO PV-ITEM-RTL-UNPACK.                  
017800     MOVE PV-ITEM-RTL-UNPACK      TO PV-ITEM-RETAIL-4.                    
017810     MOVE PV-ITEM-RETAIL-4        TO VL036-ITEM-RETAIL.                   
017900     MOVE RECDIS-STR-NBR          TO VL036-STORE-NBR                      
017900                                     PV-CURR-STR-NBR.                     
018000     MOVE SPACES                  TO VL036-STORE-COST-SIGN.               
018100     MOVE ZEROES                  TO VL036-STORE-COST.                    
018200     MOVE SPACES                  TO VL036-STORE-RETAIL-SIGN.             
018300     MOVE ZEROES                  TO VL036-STORE-RETAIL.                  
018400     MOVE SPACES                  TO VL036-STORE-RCV-UNITS-SIGN.          
018500     MOVE RECDIS-AP-VERIFY-ITM-QTY                                        
                                        TO PV-STORE-RCV-UNITS.                  
018501     MOVE PV-STORE-RCV-UNITS-4    TO VL036-STORE-RCV-UNITS.               
018510     MOVE RECEIV-CRE-TMST         TO PV-DB2-DATE.                         
018520     MOVE PV-DB2-CC               TO VL036-RCV-CREATE-CC.                 
018530     MOVE PV-DB2-YY               TO VL036-RCV-CREATE-YY.                 
018540     MOVE PV-DB2-MM               TO VL036-RCV-CREATE-MM.                 
018550     MOVE PV-DB2-DD               TO VL036-RCV-CREATE-DD.                 
018700     MOVE SPACES                  TO VL036-CASE-PACK-SIGN.                
018800     MOVE ZEROES                  TO VL036-CASE-PACK.                     
018900     MOVE 'VE'                    TO VL036-DETAIL-STATUS.                 
019000     MOVE SPACES                  TO VL036-PO-UNIQUE-ADDENDA.             
020800     MOVE 'X'                     TO VL036-ANCHOR-CONSTANT.               
021100                                                                          
021200******************************************************************        
021300*  READ INPUT FILE RECORD.                                                
021400******************************************************************        
021410 2100-READ-EXTRACT.                                                       
021420                                                                          
021430     READ VL-EXTRACT-FILE INTO VL038-RECEIVER-FEEDER-RECORD               
021440        AT END                                                            
021450           SET PS-END-INPUT    TO TRUE                                    
021460        NOT AT END                                                        
021470           ADD +1                     TO PV-READ-COUNT                    
021480           MOVE VL038-PO-NBR          TO PV-PO-NBR                        
021481                                         DK-PO-NBR                        
021490           MOVE VL038-REC-SEQ-NBR     TO PV-REC                           
021491                                         DK-REC-SEQ-NBR                   
021492           MOVE PV-REC                TO PV-REC-SEQ-NBR                   
021493     END-READ.                                                            
016101                                                                          
016110*****************************************************************         
016200* CREATE ASN RECEIVER XREF FILE                                           
016300*****************************************************************         
016400 2200-CREATE-XREF.                                                        
016700     INITIALIZE VL042-ASN-RCVR-XREF-RECORD.                               
016700                                                                          
016700     MOVE 'A'                     TO VL042-ACD-INDICATOR.                 
016800     MOVE PKSLIP-ASN-NBR          TO VL042-SEQUENCE-NUMBER.               
016800     MOVE PV-PO-RECEIVER          TO VL042-RECEIVER-NUMBER.               
018520     MOVE PV-EDIISA-CNTL-DATE     TO PV-ASN-REC-DTE.                      
018520     MOVE PV-ASN-REC-CC           TO PV-CC.                               
018530     MOVE PV-ASN-REC-YY           TO PV-YY.                               
018540     MOVE PV-ASN-REC-MM           TO PV-MM.                               
018550     MOVE PV-ASN-REC-DD           TO PV-DD.                               
016800     MOVE PV-CCYYMMDD-DATE        TO VL042-RECEIVED-DATE.                 
016900     MOVE PKSLIP-SEQ-NBR          TO PV-PKSLIP-SEQ-NBR.                   
016900     STRING PKSLIP-PKSL-NBR      DELIMITED BY ' '                         
016900            PV-PKSLIP-SEQ-NBR-R  DELIMITED BY SIZE                        
016900                                INTO VL042-PACKSLIP.                      
017100     MOVE 'X'                     TO VL042-ANCHOR-CONSTANT.               
021494                                                                          
021494     PERFORM 8100-WRITE-XREF.                                             
021494                                                                          
021601*----------------------------------------------------------------*        
021602* OPEN THE DETAIL CURSOR                                         *        
021603*----------------------------------------------------------------*        
021604 4500-OPEN-DETAIL-CSR.                                                    
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
021605                                                                          
021606         EXEC SQL                                                         
021607              OPEN DETAIL-CSR                                             
021610         END-EXEC                                                         
021700                                                                          
021800         EVALUATE TRUE                                                    
021900             WHEN SQLCODE = +0                                            
022000             WHEN SQLCODE = -904                                          
022100             WHEN SQLCODE = -913                                          
022200                  CONTINUE                                                
022300             WHEN SQLWARN0 NOT = SPACE                                    
022400             WHEN SQLCODE NOT = +0                                        
022500                 MOVE '4500-OPEN-DETAIL-CSR' TO PV-ABEND-PARAGRAPH        
022600                 MOVE 'OPEN DETAIL CURSOR'   TO PV-ABEND-OPERATION        
022700                 MOVE 'DETAIL EXTRACT    ' TO PV-ABEND-STRUCTURE-1        
022800                 MOVE 'DB2 - TRECDIS     ' TO PV-ABEND-STRUCTURE-2        
022900                 MOVE 'PO - RCVR NBR     ' TO PV-ABEND-STRUCTURE-3        
023000                 MOVE PV-PO-NBR            TO PV-ABEND-PO                 
023100                 MOVE PV-REC-SEQ-NBR       TO PV-ABEND-RCVR               
023200                 PERFORM 9200-SQL-ERROR                                   
023300         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '4500-OPEN-DETAIL-CSR'   TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TRECDIS'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
023400                                                                          
023500*----------------------------------------------------------------*        
023600* CLOSE THE DETAIL CURSOR                                        *        
023700*----------------------------------------------------------------*        
023800 4510-CLOSE-DETAIL-CSR.                                                   
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
023900                                                                          
024000         EXEC SQL                                                         
024100              CLOSE DETAIL-CSR                                            
024200         END-EXEC                                                         
024300                                                                          
024400         EVALUATE TRUE                                                    
024500             WHEN SQLCODE = +0                                            
024600                  CONTINUE                                                
024700             WHEN SQLWARN0 NOT = SPACE                                    
024800             WHEN SQLCODE NOT = +0                                        
024900                 MOVE '4510-CLOSE-DETL-CSR' TO PV-ABEND-PARAGRAPH         
025000                 MOVE 'CLOSE D TAIL CURSOR' TO PV-ABEND-OPERATION         
025100                 MOVE 'DETAIL EXTRACT    ' TO PV-ABEND-STRUCTURE-1        
025200                 MOVE 'DB2 - TRECDIS     ' TO PV-ABEND-STRUCTURE-2        
025300                 MOVE 'PO - RCVR NBR     ' TO PV-ABEND-STRUCTURE-3        
025400                 MOVE PV-PO-NBR            TO PV-ABEND-PO                 
025500                 MOVE PV-REC-SEQ-NBR       TO PV-ABEND-RCVR               
025600                 PERFORM 9200-SQL-ERROR                                   
025700         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '4510-CLOSE-DETL-CSR'   TO PV-PARAGRAPH                     
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TRECDIS'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025800                                                                          
025900*----------------------------------------------------------------*        
026000*    FETCH A DETAIL ROW                                          *        
026100*----------------------------------------------------------------*        
026200 4520-FETCH-DETAIL-CSR.                                                   
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0) OR (SQLCODE = +100))                          
026300                                                                          
026400         EXEC SQL                                                         
026500              FETCH DETAIL-CSR                                            
026600              INTO :RECITM-ITM-NBR                                        
026610                  ,:RECITM-ORD-LNE-NBR                                    
026700                  ,:RECDIS-AP-VERIFY-ITM-QTY                              
026800                  ,:RECDIS-STR-NBR                                        
027100                  ,:RECEIV-CRE-TMST                                       
027100                  ,:RECEIV-PACK-BY-ST-IND                                 
027600         END-EXEC                                                         
027700                                                                          
027800         EVALUATE TRUE                                                    
027900             WHEN SQLCODE = +0                                            
028000                  PERFORM 2000-PROCESS                                    
028100             WHEN SQLCODE = +100                                          
028200                  SET PS-EOF-DETAIL-CSR TO TRUE                           
028300             WHEN SQLWARN0 NOT = SPACE                                    
028400             WHEN SQLCODE < +0                                            
028500             WHEN SQLCODE > +0                                            
028600                 MOVE '4520-FETCH-DETAL-CSR' TO PV-ABEND-PARAGRAPH        
028700                 MOVE 'FETCH DETAIL CURSOR' TO PV-ABEND-OPERATION         
028800                 MOVE 'DETAIL EXTRACT    ' TO PV-ABEND-STRUCTURE-1        
028900                 MOVE 'DB2 - TRECDIS     ' TO PV-ABEND-STRUCTURE-2        
029000                 MOVE 'PO - RCVR NBR     ' TO PV-ABEND-STRUCTURE-3        
029100                 MOVE PV-PO-NBR        TO PV-ABEND-PO                     
029200                 MOVE PV-REC-SEQ-NBR   TO PV-ABEND-RCVR                   
029300                 PERFORM 9200-SQL-ERROR                                   
029400         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '4520-FETCH-DETAIL-CSR ' TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TRECDIS'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
029500                                                                          
029501******************************************************************        
029502*  GET UPC NUMBER                                                         
029503******************************************************************        
029504 4600-GET-UPC-NBR.                                                        
029505                                                                          
029513     PERFORM 4650-SELECT-VENDOR-UPC                                       
029514       EVALUATE SQLCODE                                                   
029515          WHEN ZERO                                                       
029516              MOVE XLT00006-UPC-NBR TO DK-UPC-NBR                         
029518          WHEN OTHER                                                      
029519              PERFORM 4660-SELECT-INTERNAL-UPC                            
029520       END-EVALUATE.                                                      
029522                                                                          
029535*****************************************************************         
029536* FETCH THE VENDOR UPC FROM THE NEW XLT_UPC TABLE. THERE SHOULD *         
029537* ONLY BE ONE VENDOR UPC BEING SENT EDI.  IF WE DO NOT FIND A   *         
029538* VENDOR UPC, WE WILL FETCH THE INTERNAL UPC FROM THE TABLE AND *         
029539* USE THAT ONE.                                                 *         
029540*****************************************************************         
029541 4650-SELECT-VENDOR-UPC.                                                  
029543     INITIALIZE DCLXLT00006                                               
029543                DCLXLT00003.                                              
029544                                                                          
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0) OR (SQLCODE = +100)                           
P45907         OR  (SQLCODE = -811))                                            
029542                                                                          
029545         EXEC SQL                                                         
029546              SELECT B.UPC_NBR                                            
029546                   , A.SKU_NBR                                            
029547                INTO :XLT00006-UPC-NBR                                    
029547                   , :XLT00003-SKU-NBR                                    
029548                FROM XLT_SKU A                                            
029549                   , XLT_UPC B                                            
029550               WHERE A.ITM_NBR    = :RECITM-ITM-NBR                       
029551                 AND A.INTR_UPC_ID = B.INTR_UPC_ID                        
029552                 AND B.UPC_TYP_CDE = 'V'                                  
029553                 AND EDI_ORD_IND = 'Y'                                    
029554         END-EXEC                                                         
029555                                                                          
029556         EVALUATE TRUE                                                    
029557             WHEN SQLCODE = ZERO                                          
029558             WHEN SQLCODE = +100                                          
029558             WHEN SQLCODE = -811                                          
029560                  CONTINUE                                                
029570                                                                          
029580             WHEN SQLWARN0 NOT = SPACES                                   
029590             WHEN SQLCODE NOT = ZERO                                      
029591                 MOVE '4650-SELECT-VEND-UPC' TO PV-ABEND-PARAGRAPH        
029592                 MOVE 'SELECT VENDOR UPC '   TO PV-ABEND-OPERATION        
029593                 MOVE 'XLT_SKU'            TO PV-ABEND-STRUCTURE-1        
029594                 MOVE 'XLT_UPC'            TO PV-ABEND-STRUCTURE-2        
029596                 MOVE PV-PO-NBR            TO PV-ABEND-PO                 
029597                 MOVE PV-REC-SEQ-NBR       TO PV-ABEND-RCVR               
032140                 MOVE RECITM-ITM-NBR       TO PV-ITEM-NBR                 
032140                 STRING 'ITEM-NBR - ' DELIMITED BY SIZE                   
032140                          PV-ITEM-NBR DELIMITED BY SIZE                   
032140                   INTO PV-ERROR-MSG2                                     
029598                 PERFORM 9200-SQL-ERROR                                   
029615         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '4650-SELECT-VENDOR-UPC ' TO PV-PARAGRAPH                   
025400         MOVE 'SELECT VENDOR UPC    '   TO PV-DB2-FUNCTION                
025500         MOVE 'XLT_UPC'                 TO PV-ABEND-STRUCTURE-1           
025500         MOVE 'XLT_SKU'                 TO PV-ABEND-STRUCTURE-2           
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
029616                                                                          
029617****************************************************************          
029618* FETCH THE INTERNAL UPC FROM THE XLT_UPC TABLE.  WE ARE USING *          
029619* 'FETCH FIRST ROW ONLY' TO FORCE DB2 TO BRING BACK ONLY ONE   *          
029620* ROW FROM THE QUERY.  THIS METHOD IS MUCH MORE EFFICIENT THAN *          
029621* CHECKING FOR A -811.  THIS METHOD IS NEW IN DB2V7.           *          
029622****************************************************************          
029623 4660-SELECT-INTERNAL-UPC.                                                
029624                                                                          
029625     INITIALIZE DCLXLT00006                                               
029625                DCLXLT00006.                                              
029626                                                                          
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
029542                                                                          
029627         EXEC SQL                                                         
029628              SELECT B.UPC_NBR                                            
029628                   , A.SKU_NBR                                            
029629                INTO :XLT00006-UPC-NBR                                    
030300                   , :XLT00003-SKU-NBR                                    
029630                FROM XLT_SKU A                                            
029631                   , XLT_UPC B                                            
029632               WHERE A.ITM_NBR    = :RECITM-ITM-NBR                       
029633                 AND A.INTR_UPC_ID = B.INTR_UPC_ID                        
029634                 AND B.UPC_TYP_CDE = 'I'                                  
029635               FETCH FIRST ROW ONLY                                       
029636         END-EXEC                                                         
029637                                                                          
029638         EVALUATE TRUE                                                    
029639             WHEN SQLCODE = ZERO                                          
029640                  MOVE XLT00006-UPC-NBR TO DK-UPC-NBR                     
029642                                                                          
029643             WHEN SQLWARN0 NOT = SPACES                                   
029644             WHEN SQLCODE NOT = ZERO                                      
029645                 MOVE '4660-SELECT-INTL UPC' TO PV-ABEND-PARAGRAPH        
029646                 MOVE 'SELECT INTERNAL UPC ' TO PV-ABEND-OPERATION        
029647                 MOVE 'XLT_SKU'        TO PV-ABEND-STRUCTURE-1            
029648                 MOVE 'XLT_UPC'        TO PV-ABEND-STRUCTURE-2            
029649                 MOVE PV-PO-NBR        TO PV-ABEND-PO                     
029650                 MOVE PV-REC-SEQ-NBR   TO PV-ABEND-RCVR                   
032140                 MOVE RECITM-ITM-NBR       TO PV-ITEM-NBR                 
032140                 STRING 'ITEM-NBR - ' DELIMITED BY SIZE                   
032140                        PV-ITEM-NBR DELIMITED BY SIZE                     
032140                   INTO PV-ERROR-MSG2                                     
029651                 PERFORM 9200-SQL-ERROR                                   
029668         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '4660-SELECT-INTERNAL-UPC ' TO PV-PARAGRAPH                 
025400         MOVE 'SELECT INTERNAL UPC  '     TO PV-DB2-FUNCTION              
025500         MOVE 'XLT_UPC'                   TO PV-ABEND-STRUCTURE-1         
025500         MOVE 'XLT_SKU'                   TO PV-ABEND-STRUCTURE-2         
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
029669                                                                          
032700*----------------------------------------------------------------*        
032800*    SELECT ITEM COST AND ITEM RETAIL                                     
032900*----------------------------------------------------------------*        
033000 7200-SELECT-TPURITM.                                                     
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0) OR (SQLCODE = +100))                          
029542                                                                          
033100         EXEC SQL                                                         
033200             SELECT UNT_RTL_PR_AMT                                        
033300                  , UNT_COST_AMT                                          
033400               INTO :PURITM-UNT-RTL-PR-AMT                                
033500                  , :PURITM-UNT-COST-AMT                                  
033600               FROM TPURITM                                               
033700              WHERE PO_NBR          = :DK-PO-NBR                          
033800                AND APP_PO_LNE_NBR  = :RECITM-ORD-LNE-NBR                 
033900                AND VER_STATUS_CDE  = 'A'                                 
034000         END-EXEC                                                         
034200                                                                          
034300         EVALUATE TRUE                                                    
034400             WHEN SQLCODE = ZERO                                          
034500                  CONTINUE                                                
034600             WHEN SQLCODE = +100                                          
034700                  MOVE 0 TO PURITM-UNT-RTL-PR-AMT                         
034800                              PURITM-UNT-COST-AMT                         
034900             WHEN SQLWARN0 NOT = SPACE                                    
035000             WHEN SQLCODE NOT = +0                                        
035001                 MOVE '7200-SELECT-TPURITM' TO PV-PARAGRAPH               
035003                 MOVE 'READ TPURITM '       TO PV-ABEND-OPERATION         
035004                 MOVE 'PO - RCVR NBR     '  TO PV-ERROR-MSG1              
035005                 MOVE PV-PO-NBR             TO PV-ABEND-PO                
035006                 MOVE PV-REC-SEQ-NBR        TO PV-ABEND-RCVR              
035007                 PERFORM 9200-SQL-ERROR                                   
035008         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7200-SELECT-TPURITM'   TO PV-PARAGRAPH                     
025400         MOVE 'READ TPURITM '         TO PV-ABEND-OPERATION               
025500         MOVE 'XLT_UPC'               TO PV-ABEND-STRUCTURE-1             
025500         MOVE 'XLT_SKU'               TO PV-ABEND-STRUCTURE-2             
035004                 MOVE 'PO - RCVR NBR' TO PV-ERROR-MSG1                    
035005                 MOVE PV-PO-NBR       TO PV-ABEND-PO                      
035006                 MOVE PV-REC-SEQ-NBR  TO PV-ABEND-RCVR                    
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
035008                                                                          
032700******************************************************************        
032800*    SELECT PACKSLIP AND ASN NBR  FOR ASN RECEIVER XREF FROM              
032800*      TPKSLIP AND THEN GET ASN CREATE DATE FROM TASNHDR                  
032900******************************************************************        
021601******************************************************************        
021602* OPEN THE PBS PKSLIP CURSOR                                              
021603******************************************************************        
021604 7300-OPEN-PBS-PKSLIP-CSR.                                                
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
021605                                                                          
021606         EXEC SQL                                                         
021607              OPEN PBS-PKSLIP-CSR                                         
021610         END-EXEC                                                         
021700                                                                          
021800         EVALUATE TRUE                                                    
021900             WHEN SQLCODE = +0                                            
022000             WHEN SQLCODE = -904                                          
022100             WHEN SQLCODE = -913                                          
022200                  CONTINUE                                                
022300             WHEN SQLWARN0 NOT = SPACE                                    
022400             WHEN SQLCODE NOT = +0                                        
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = VLKUT036 '                         
024300                 DISPLAY '** PARAGRAPH = 7300-OPEN-PBS-PKSLIP-CSR'        
024400                 MOVE '7300-OPEN-PBS-PKSLIP-CSR' TO PV-PARAGRAPH          
024500                 MOVE 'OPEN CURSOR          ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TPKSLIP'           TO PV-ABEND-STRUCTURE-1         
023200                 PERFORM 9200-SQL-ERROR                                   
023300         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7300-OPEN-PBS-PKSLIP-CSR'   TO PV-PARAGRAPH                
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
023500******************************************************************        
023600* CLOSE THE PBS PKSLIP CURSOR                                             
023700******************************************************************        
023800 7400-CLOSE-PBS-PKSLIP-CSR.                                               
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
024000         EXEC SQL                                                         
024100             CLOSE PBS-PKSLIP-CSR                                         
024200         END-EXEC                                                         
024300                                                                          
024400         EVALUATE TRUE                                                    
024500             WHEN SQLCODE = +0                                            
024600                  CONTINUE                                                
024700             WHEN SQLWARN0 NOT = SPACE                                    
024800             WHEN SQLCODE NOT = +0                                        
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = VLKUT036 '                         
024300                 DISPLAY '** PARAGRAPH = 7400-CLOSE-PBSPKSLIP-CSR'        
024400                 MOVE '7400-CLOSE-PBS-PKSLIP-CSR' TO PV-PARAGRAPH         
024500                 MOVE 'CLOSE PKSLIP CURSOR  ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TPKSLIP'         TO PV-ABEND-STRUCTURE-1           
024600                 MOVE 'TASNHDR'         TO PV-ABEND-STRUCTURE-2           
025600                 PERFORM 9200-SQL-ERROR                                   
025700         END-EVALUATE                                                     
025700     END-PERFORM.                                                         
025800                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7400-CLOSE-PBS-PKSLIP-CSR'   TO PV-PARAGRAPH               
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
025900******************************************************************        
026000*    FETCH A PKSLIP INFO - PACKSLIP NBR AND ASN NBR - PBS ORDER           
026100******************************************************************        
026200 7500-FETCH-PBS-PKSLIP-CSR.                                               
030700     INITIALIZE DCLTPKSLIP                                                
030700                DCLTASNHDR.                                               
030700                                                                          
030700     PERFORM                                                              
030800         WITH TEST AFTER                                                  
030900         VARYING PV-RETRY-COUNT                                           
031000         FROM 1 BY 1                                                      
031100         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
031200         OR  (SQLCODE = +0)                                               
031200         OR (PS-END-PKSLIP-CSR))                                          
031300                                                                          
026400         EXEC SQL                                                         
026500             FETCH PBS-PKSLIP-CSR                                         
026600             INTO :PKSLIP-SEQ-NBR                                         
026600                , :PKSLIP-PKSL-NBR                                        
026610                , :PKSLIP-ASN-NBR                                         
026610                , :PKSLIP-PO-NBR                                          
026610                , :PV-EDIISA-CNTL-DATE                                    
027600         END-EXEC                                                         
027700                                                                          
027800         EVALUATE TRUE                                                    
032200             WHEN SQLCODE = ZERO                                          
034500                 PERFORM 2200-CREATE-XREF                                 
032300             WHEN SQLCODE = -904                                          
032400             WHEN SQLCODE = -911                                          
032500             WHEN SQLCODE = -913                                          
032600                 CONTINUE                                                 
032700             WHEN SQLCODE = +100                                          
032800                 SET PS-END-PKSLIP-CSR TO TRUE                            
032900             WHEN SQLWARN0 NOT EQUAL SPACE                                
033000             WHEN SQLCODE NOT EQUAL ZERO                                  
033100                 DISPLAY '** ABEND **'                                    
033200                 DISPLAY '** PROGRAM = VLKUT036 **'                       
033300                 DISPLAY '** PARAGRAPH = 7500-PBSFETCH-PKSLIP-CSR'        
033400                 MOVE '7500-FETCH-PBS-PKSLIP-CSR' TO PV-PARAGRAPH         
033500                 MOVE 'FETCH FROM CURSOR ' TO PV-DB2-FUNCTION             
033600                 MOVE 'TPKSLIP'            TO PV-ABEND-STRUCTURE-1        
033600                 MOVE 'TASNHDR'            TO PV-ABEND-STRUCTURE-1        
035004                 MOVE 'PO - RCVR NBR     '  TO PV-ERROR-MSG1              
035005                 MOVE PV-PO-NBR             TO PV-ABEND-PO                
035006                 MOVE PV-REC-SEQ-NBR        TO PV-ABEND-RCVR              
033800                 PERFORM 9200-SQL-ERROR                                   
033900         END-EVALUATE                                                     
034000     END-PERFORM.                                                         
034100                                                                          
034200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
034300         MOVE '7500-FETCH-PBS-PKSLIP-CSR'  TO PV-PARAGRAPH                
034400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
034500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
035004         MOVE 'PO - RCVR NBR     '     TO PV-ERROR-MSG1                   
035005         MOVE PV-PO-NBR                TO PV-ABEND-PO                     
035006         MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR                   
034700         PERFORM 9200-SQL-ERROR                                           
034800     END-IF.                                                              
034900                                                                          
021601******************************************************************        
021602* OPEN THE NON-PBS PKSLIP CURSOR                                          
021603******************************************************************        
021604 7600-OPEN-PKSLIP-CSR.                                                    
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
021605                                                                          
021606         EXEC SQL                                                         
021607              OPEN PKSLIP-CSR                                             
021610         END-EXEC                                                         
021700                                                                          
021800         EVALUATE TRUE                                                    
021900             WHEN SQLCODE = +0                                            
022000             WHEN SQLCODE = -904                                          
022100             WHEN SQLCODE = -913                                          
022200                  CONTINUE                                                
022300             WHEN SQLWARN0 NOT = SPACE                                    
022400             WHEN SQLCODE NOT = +0                                        
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = VLKUT036 '                         
024300                 DISPLAY '** PARAGRAPH = 7600-OPEN-PKSLIP-CSR'            
024400                 MOVE '7600-OPEN-PKSLIP-CSR' TO PV-PARAGRAPH              
024500                 MOVE 'OPEN CURSOR          ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TPKSLIP'           TO PV-ABEND-STRUCTURE-1         
023200                 PERFORM 9200-SQL-ERROR                                   
023300         END-EVALUATE                                                     
023400     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7600-OPEN-PKSLIP-CSR'   TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
025900******************************************************************        
026000*  FETCH A PKSLIP INFO - PACKSLIP NBR AND ASN NBR - NON-PBS ORDER         
026100******************************************************************        
026200 7700-FETCH-PKSLIP-CSR.                                                   
030700     INITIALIZE DCLTPKSLIP                                                
030700                DCLTASNHDR.                                               
030700                                                                          
030700     PERFORM                                                              
030800         WITH TEST AFTER                                                  
030900         VARYING PV-RETRY-COUNT                                           
031000         FROM 1 BY 1                                                      
031100         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
031200         OR  (SQLCODE = +0)                                               
031200         OR (PS-END-PKSLIP-CSR))                                          
031300                                                                          
026400         EXEC SQL                                                         
026500             FETCH PKSLIP-CSR                                             
026600             INTO :PKSLIP-SEQ-NBR                                         
026600                , :PKSLIP-PKSL-NBR                                        
026610                , :PKSLIP-ASN-NBR                                         
026610                , :PKSLIP-PO-NBR                                          
026610                , :PV-EDIISA-CNTL-DATE                                    
027600         END-EXEC                                                         
027700                                                                          
027800         EVALUATE TRUE                                                    
032200             WHEN SQLCODE = ZERO                                          
034500                 PERFORM 2200-CREATE-XREF                                 
032300             WHEN SQLCODE = -904                                          
032400             WHEN SQLCODE = -911                                          
032500             WHEN SQLCODE = -913                                          
032600                 CONTINUE                                                 
032700             WHEN SQLCODE = +100                                          
032800                 SET PS-END-PKSLIP-CSR TO TRUE                            
032900             WHEN SQLWARN0 NOT EQUAL SPACE                                
033000             WHEN SQLCODE NOT EQUAL ZERO                                  
033100                 DISPLAY '** ABEND **'                                    
033200                 DISPLAY '** PROGRAM = VLKUT036 **'                       
033300                 DISPLAY '** PARAGRAPH = 7700-FETCH-PKSLIP-CSR'           
033400                 MOVE '7700-FETCH-PKSLIP-CSR' TO PV-PARAGRAPH             
033500                 MOVE 'FETCH FROM CURSOR ' TO PV-DB2-FUNCTION             
033600                 MOVE 'TPKSLIP'            TO PV-ABEND-STRUCTURE-1        
033600                 MOVE 'TASNHDR'            TO PV-ABEND-STRUCTURE-2        
033600                 MOVE 'TASNORD'            TO PV-ABEND-STRUCTURE-2        
035004                 MOVE 'PO - RCVR NBR     '  TO PV-ERROR-MSG1              
035005                 MOVE PV-PO-NBR             TO PV-ABEND-PO                
035006                 MOVE PV-REC-SEQ-NBR        TO PV-ABEND-RCVR              
033800                 PERFORM 9200-SQL-ERROR                                   
033900         END-EVALUATE                                                     
034000     END-PERFORM.                                                         
034100                                                                          
034200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
034300         MOVE '7700-FETCH-PKSLIP-CSR'  TO PV-PARAGRAPH                    
034400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
034500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
035004         MOVE 'PO - RCVR NBR     '     TO PV-ERROR-MSG1                   
035005         MOVE PV-PO-NBR                TO PV-ABEND-PO                     
035006         MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR                   
034700         PERFORM 9200-SQL-ERROR                                           
034800     END-IF.                                                              
034900                                                                          
023500******************************************************************        
023600* CLOSE THE PKSLIP CURSOR                                        *        
023700******************************************************************        
023800 7800-CLOSE-PKSLIP-CSR.                                                   
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
024000         EXEC SQL                                                         
024100             CLOSE PKSLIP-CSR                                             
024200         END-EXEC                                                         
024300                                                                          
024400         EVALUATE TRUE                                                    
024500             WHEN SQLCODE = +0                                            
024600                  CONTINUE                                                
024700             WHEN SQLWARN0 NOT = SPACE                                    
024800             WHEN SQLCODE NOT = +0                                        
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = VLKUT036 '                         
024300                 DISPLAY '** PARAGRAPH = 7800-CLOSE-PKSLIP-CSR'           
024400                 MOVE '7800-CLOSE-PKSLIP-CSR' TO PV-PARAGRAPH             
024500                 MOVE 'CLOSE PKSLIP CURSOR  ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TPKSLIP'         TO PV-ABEND-STRUCTURE-1           
024600                 MOVE 'TASNHDR'         TO PV-ABEND-STRUCTURE-2           
024600                 MOVE 'TASNORD'         TO PV-ABEND-STRUCTURE-3           
025600                 PERFORM 9200-SQL-ERROR                                   
025700         END-EVALUATE                                                     
025700     END-PERFORM.                                                         
025800                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7400-CLOSE-PKSLIP-CSR'  TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TPKSLIP'                TO PV-ABEND-STRUCTURE-1            
024600         MOVE 'TASNHDR'                TO PV-ABEND-STRUCTURE-2            
024600         MOVE 'TASNORD'                TO PV-ABEND-STRUCTURE-3            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
035010*****************************************************************         
035020*  WRITE DETAIL FILE                                                      
035030*****************************************************************         
035040 8000-WRITE-DETAIL.                                                       
035050     WRITE VL-RECEIVER-DTL-RECORD                                         
035051                            FROM VL036-RECEIVER-DETAIL-RECORD.            
035060                                                                          
035070     ADD 1                TO PV-RECS-WRITTEN.                             
035080                                                                          
035010*****************************************************************         
035020*  WRITE ASN RECEIVER XREF FILE                                           
035030*****************************************************************         
035040 8100-WRITE-XREF.                                                         
035050     WRITE VL-XREF-RECORD FROM VL042-ASN-RCVR-XREF-RECORD.                
035060                                                                          
035070     ADD 1                TO PV-XREF-RECS-WRITTEN.                        
035080                                                                          
035800*****************************************************************         
035900*  CREATE MARKER FILE - BECAUSE 2 FILES ARE CREATED - 2 MARKER            
035900*     RECORDS NEED TO BE WRITTEN                                          
036000*****************************************************************         
035093 8500-CREATE-MARKER.                                                      
035095     MOVE 'RCVRDETL.DAT'  TO PV-RECEIVER-DAT.                             
035096     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
035097     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
035100     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
035102     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
035103                                                                          
035095     MOVE 'ASNRCVRX.DAT'       TO PV-RECEIVER-DAT.                        
035096     MOVE PV-FILE-NAME         TO VL029-FILE-NAME.                        
035097     MOVE PV-XREF-RECS-WRITTEN TO VL029-RECORD-COUNT.                     
035100     MOVE 'X'                  TO VL029-ANCHOR-CONSTANT.                  
035102     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
035103                                                                          
035110*****************************************************************         
035200*  END OF JOB PROCESSING                                                  
035300*****************************************************************         
035400 9000-EOJ.                                                                
035600                                                                          
035710     DISPLAY 'DETAIL RECORDS WRITTEN ===> ' PV-RECS-WRITTEN.              
035800                                                                          
035710     DISPLAY 'ASN XREF RECORDS WRITTEN ===> ' PV-XREF-RECS-WRITTEN.       
035800                                                                          
020300***** FILL IN COMMON FIELDS FOR MARKER FILE  *************                
036200     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
036600     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
036700     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
036800     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
035820     PERFORM 8500-CREATE-MARKER.                                          
035830                                                                          
035900     CLOSE VL-EXTRACT-FILE                                                
036000           VL-RECEIVER-DTL-FILE                                           
036700           VL-ASN-RCVR-XREF-FILE                                          
036010           VL-MARKER-FILE.                                                
036100                                                                          
036200*****************************************************************         
036300*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
036400*****************************************************************         
036500 9200-SQL-ERROR.                                                          
036600     CLOSE VL-EXTRACT-FILE                                                
036610           VL-RECEIVER-DTL-FILE                                           
036700           VL-ASN-RCVR-XREF-FILE                                          
036700           VL-MARKER-FILE.                                                
036710                                                                          
036800     MOVE SQLCODE                     TO SQLCODE2.                        
036900     MOVE SQLERRD(3)                  TO SQLERRD3.                        
037000                                                                          
037100     DISPLAY '** ABEND **         ** = SQLERROR'.                         
037200     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
037300     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
037400     DISPLAY '** OPERATION ** = ' PV-ABEND-OPERATION.                     
037500     DISPLAY '**           ** = ' PV-ABEND-TABLE-1.                       
037600     DISPLAY '**           ** = ' PV-ABEND-TABLE-2.                       
037700     DISPLAY '**           ** = ' PV-ERROR-MSG1.                          
037700     DISPLAY '** PO        ** = ' PV-ABEND-PO.                            
037700     DISPLAY '** RCVR      ** = ' PV-ABEND-RCVR.                          
037700     DISPLAY '** ' PV-ERROR-MSG2.                                         
037700     DISPLAY '** ' PV-ERROR-MSG3.                                         
037800                                                                          
037900     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
038000     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
038100     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
038200     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
038300                                                                          
038400     COPY DPPD004.                                                        
038500                                                                          
038600     MOVE +4013                       TO PV-ABEND-CODE.                   
038700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
038800                                                                          
038900*************  END OF VLKUT036  ********************************          
