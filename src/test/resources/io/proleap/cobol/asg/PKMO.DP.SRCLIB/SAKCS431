000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    SAKCS431.                                                 
000500 AUTHOR.        MICHELE RINDFLEISCH.                                      
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  07/30/2001.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*----------------------------------------------------------------*        
001100*    S431 - ORIGINAL TRANSACTION LIST                            *        
001200*                                                                *        
001300*    THIS PROGRAM GENERATES A LIST OF ORIGINAL TRANSACTIONS      *        
001400*    THAT RETURNS WERE MADE AGAINST.  ONE TRANSACTION FROM       *        
001500*    THE LIST MAY BE SELECTED FOR FURTHER INFORMATION (F14 -     *        
001600*    ITEM INQUIRY OR F20 - TENDER INQUIRY).  THE RETURN          *        
001700*    TRANSACTION INFORMATION IS OBTAINED FROM SAKCS426 (CORP.    *        
001800*    EJ TRAN. LIST).  ONLY ONE TRANSACTION MAY BE SELECTED FROM  *        
001900*    SAKCS426 AS INPUT TO SAKCS431.                              *        
002000*                                                                *        
002100*----------------------------------------------------------------*        
002200*                         CHANGES                                *        
002300******************************************************************        
002400* CHANGED:02/26/02                                               *        
002500* BY:     CHRIS CLARK                                            *        
002600* WHY:    FIXED ISSURE WHERE IF A RETURN TRANSACTION WAS         *        
002700*         WAS OPERATIONAL AND ORIGINAL WAS HISTORICAL, THE F24   *        
002800*         KEY COULD NOT FIND THE ORIGINAL TRANSACTION FOR A RETRN*        
002900******************************************************************        
003000* CHANGED:12/10/02                                               *        
003100* BY:     NANCY MARTIN                    WR24311                *        
003200* WHY:    CHANGE TO INCREASE ASSOCIATE ID DISPLAY FROM 6 TO 7    *        
003300*         RELATED TO UNIQUE ASSOCIATE ID WORK.                   *        
003400******************************************************************        
006101* CHANGED: 01/18/2012                                            *        
006102* BY:      ROB SWAGER                                            *        
006103* FOR:     WR24719                                               *        
006104* WHY:     EXPANDED SIZE OF DL-TRAN-AMOUNT-LEFT FIELD            *        
006105*          FOR LARGE RETAIL AMOUNT.                              *        
006106******************************************************************        
C62728* CHANGED  10/04/2013                                            *        
C62728* BY:      COGNIZANT                                             *        
C62728* FOR:     CHG0062728                                            *        
C62728* WHY:     REMOVED LOGIC THAT REFERENCING THE FOLLOWING          *        
C62728*          COSA HISTORY TABLES FOR 9607 - FSH 2013-CFMC PROJECT  *        
C62728*                                                                *        
C62728*          TEPIRTH,TEPTRNH                                       *        
C62728******************************************************************        
C00000*  CHANGED 09/02/2021    BY GERMAN BANDA    WR: CHG0261091                
C00000*                                                                         
C00000*  CHANGE CALL OF DPKUT100 TO                                             
C00000*  CALL DP010I-NUMERIC-EDIT-ROUTINE                                       
C00000*  MAKING THE CALL DYNAMIC VS STATIC                                      
C00000*-----------------------------------------------------------------        
CICS  *  CHANGED 10/12/2022    BY JIM HUNTER      WR: CHG0276153                
CICS  *                                                                         
CICS  *  ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL               
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.              
CICS  *-----------------------------------------------------------------        
003500                                                                          
003600 DATA DIVISION.                                                           
003700                                                                          
003800 WORKING-STORAGE SECTION.                                                 
003900                                                                          
004000 01  DK-DB2-KEYS.                                                         
004100     15  DK-SALES-DATE                  PIC  X(10) VALUE SPACE.           
004200     15  DK-STORE                       PIC S9(04) VALUE ZERO             
004300                                                   COMP.                  
004400     15  DK-REGISTER                    PIC S9(04) VALUE ZERO             
004500                                                   COMP.                  
004600     15  DK-TRANS                       PIC S9(04) VALUE ZERO             
004700                                                   COMP.                  
004800     15  DK-PARTITION                   PIC S9(04) VALUE ZERO             
004900                                                   COMP.                  
005000     15  DK-SALES-CORR-SEQ-NBR          PIC S9(04) VALUE ZERO             
005100                                                   COMP.                  
005200     15  DK-START-TIME                  PIC  X(08) VALUE SPACE.           
005300                                                                          
005400 01  PC-PROGRAM-CONSTANTS.                                                
005500     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
005600                                                    'SA431A  '.           
005700     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
005800                                                    'SAKM431 '.           
005900     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
006000                                                    'SAKCS431'.           
006100     05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.              
006200**  OUTPUT LIST FROM SAKCS431                                             
006300     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
006400**  SELECTED ITEM PASSED TO SAKCS431                                      
006500     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
006600**  OUTPUT SELECTED ITEM FROM SAKCS431 LIST                               
006700     05  PC-SEL2-QUEUE-SEQ              PIC  9(01)  VALUE 3.              
006800     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
006900     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12             
007000                                                    COMP SYNC.            
007100     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
007200                                                    COMP SYNC.            
007300     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
007400                                                    +1000000              
007500                                                    COMP-3.               
007600     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
007700                                                    COMP-3.               
007800     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
007900     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
008000     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
008100                                                    COMP SYNC.            
008200     05  PC-QUOTE                       PIC  X(01)  VALUE QUOTE.          
008300                                                                          
008400     05  PC-TSYMSG-NUMBERS.                                               
008500         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
008600         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
008700         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
008800         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
008900         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
009000         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
009100         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
009200         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
009300         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
009400         10  PC-TSYMSG-03469            PIC  9(05)  VALUE 03469.          
009500                                                                          
009600 01  PS-PROGRAM-SWITCHES.                                                 
009700     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
009800         88  PS-NO-ERROR                            VALUE 'Y'.            
009900         88  PS-ERROR                               VALUE 'N'.            
010000     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
010100         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
010200         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
010300     05  PS-LIST-END-SW                 PIC  X(01)  VALUE 'N'.            
010400         88  PS-LIST-END                            VALUE 'Y'.            
010500         88  PS-NO-LIST-END                         VALUE 'N'.            
010900     05  PS-TEPTRN-RECS-AVAIL-SW        PIC  X(01)  VALUE 'Y'.            
011000         88  PS-YES-TEPTRN-RECS-AVAIL               VALUE 'Y'.            
011100         88  PS-NO-TEPTRN-RECS-AVAIL                VALUE 'N'.            
011500                                                                          
011600 01  RF-PROCESSING-DATE.                                                  
011700     05  RF-PRCG-DTE-CC                 PIC  XX.                          
011800     05  RF-PRCG-DTE-YY                 PIC  XX.                          
011900     05  FILLER                         PIC  X.                           
012000     05  RF-PRCG-DTE-MM                 PIC  XX.                          
012100     05  FILLER                         PIC  X.                           
012200     05  RF-PRCG-DTE-DD                 PIC  XX.                          
012300                                                                          
012400 01  RF-DISPLAY-DATE.                                                     
012500     05  RF-DISPLAY-DTE-MM              PIC  XX.                          
012600     05  RF-DISPLAY-DTE-DD              PIC  XX.                          
012700     05  RF-DISPLAY-DTE-YY              PIC  XX.                          
012800                                                                          
012900 01  RF-STATUS-TEXT.                                                      
013000     05  RF-STATUS-TEXT-MSG             PIC  X(06).                       
013100     05  RF-STATUS-TEXT-DATE            PIC  X(06).                       
013200                                                                          
013300 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-ORIG-STR-NBR                PIC S9(04)  VALUE +0              
013500                                                    COMP.                 
013600     05  PV-ORIG-SLS-DTE-ISO            PIC  X(10)  VALUE SPACE.          
013700     05  PV-ORIG-PARTN-NBR              PIC S9(04)  VALUE +0              
013800                                                    COMP.                 
013900     05  PV-ORIG-RGST-ID                PIC S9(04)  VALUE +0              
014000                                                    COMP.                 
014100     05  PV-ORIG-TRN-NBR                PIC S9(04)  VALUE +0              
014200                                                    COMP.                 
014300     05  PV-ORIG-ASSOC-ID               PIC  9(09)  VALUE  0.             
014400     05  PV-ORIG-STRT-TM                PIC  X(08)  VALUE SPACE.          
014600     05  PV-TRANS-TIME.                                                   
014700         10  PV-TRANS-TIME-HR           PIC  X(02).                       
014800         10  FILLER                     PIC  X(01)                        
014900                                                    VALUE '.'.            
015000         10  PV-TRANS-TIME-MIN          PIC  X(02).                       
015100         10  FILLER                     PIC  X(03)                        
015200                                                    VALUE '%%%'.          
015300     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
015400                                                    COMP SYNC.            
015500     05  PV-SUB                         PIC S9(04)  VALUE +0              
015600                                                    COMP SYNC.            
015700     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
015800                                                    COMP SYNC.            
015900     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
016000                                                    COMP-3.               
016100     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
016200                                                    COMP-3.               
016300     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
016400                                                    COMP-3.               
016500     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
016600                                                    COMP-3.               
016700     05  PV-ITEM-IN-USE                 PIC S9(04)  VALUE +0              
016800                                                    COMP SYNC.            
016900     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
017000                                                    COMP-3.               
017100     05  PV-ITEM                        PIC S9(04)  VALUE +0              
017200                                                    COMP SYNC.            
017300     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
017400                                                    COMP SYNC.            
017500     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
017600                                                    COMP SYNC.            
017700     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
017800                                                    COMP SYNC.            
017900     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
018000                                                    COMP SYNC.            
018100     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
018200                                                    COMP SYNC.            
018300     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
018400                                                    COMP SYNC.            
018500     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
018600                                                    COMP SYNC.            
018700     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
018800                                                    COMP SYNC.            
018900                                                                          
019000     05  PV-CURR-TIMESTAMP              PIC  X(26)  VALUE SPACES.         
019100     05  PV-STR-GP-CDE                  PIC  X(01)  VALUE SPACES.         
019200                                                                          
019300     05  PV-DISP-ERR-INFO.                                                
019400         10  PV-DISP-SLS-DTE            PIC  X(10)  VALUE SPACE.          
019500         10  PV-DISP-STR                PIC  X(04)  VALUE SPACE.          
019600         10  PV-DISP-RGST-ID            PIC  X(04)  VALUE SPACE.          
019700         10  PV-DISP-TRN-NBR            PIC  X(04)  VALUE SPACE.          
019800                                                                          
019900     05  PV-SLS-DTE-FORMATTED.                                            
020000         10  PV-SLS-DTE-FORMATTED-MM    PIC X(02)  VALUE SPACES.          
020100         10  FILLER                     PIC X(01)  VALUE '/'.             
020200         10  PV-SLS-DTE-FORMATTED-DD    PIC X(02)  VALUE SPACES.          
020300         10  FILLER                     PIC X(01)  VALUE '/'.             
020400         10  PV-SLS-DTE-FORMATTED-YYYY  PIC X(04)  VALUE SPACES.          
020500     05  PV-SLS-DTE-ISO.                                                  
020600         10  PV-SLS-DTE-ISO-YYYY        PIC X(04)  VALUE SPACES.          
020700         10  FILLER                     PIC X(01)  VALUE '-'.             
020800         10  PV-SLS-DTE-ISO-MM          PIC X(02)  VALUE SPACES.          
020900         10  FILLER                     PIC X(01)  VALUE '-'.             
021000         10  PV-SLS-DTE-ISO-DD          PIC X(02)  VALUE SPACES.          
021100                                                                          
021200*----------------------------------------------------------------*        
021300*   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *        
021400*   THIS MUST BE THE EXACT SAME FORMAT AS A BLOCK IN THE         *        
021500*   APPLICATION-SPECIFIC COMMAREA.                               *        
021600*----------------------------------------------------------------*        
021700 01  LQW-LIST-QUEUE-WORK-AREA.                                            
021800     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
021900     05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES                   
022000                                        INDEXED BY LQW-IDX.               
022100         10  LQW-LIST-ITEM-NUMBER       PIC S9(07)  COMP-3.               
022200         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
022300         10  LQW-SELECTED-SW            PIC  X(01).                       
022400             88  LQW-SELECT                         VALUE 'S'.            
022500             88  LQW-DESELECT                       VALUE ' '.            
022600         10  LQW-ACTION                 PIC  X(03).                       
022700         10  LQW-PARTITION-NBR          PIC S9(04)  COMP.                 
022800         10  LQW-SALES-DATE             PIC  X(10).                       
022900         10  LQW-STORE                  PIC S9(04)  COMP.                 
023000         10  LQW-REGISTER               PIC S9(04)  COMP.                 
023100         10  LQW-TRANSACTION            PIC S9(04)  COMP.                 
023200         10  LQW-START-TIME             PIC  X(08).                       
023300         10  LQW-REGISTER-OPERATOR      PIC  9(07)  COMP-3.               
023400         10  LQW-TYPE                   PIC  X(02).                       
023500         10  LQW-ONLINE-IND             PIC  X(01).                       
023600         10  LQW-AMOUNT                 PIC S9(07)V9(02)                  
023700                                                    COMP-3.               
023800         10  LQW-SALES-CORR-SEQ-NBR     PIC S9(04)  COMP.                 
023900         10  LQW-SOURCE-TEXT            PIC  X(03).                       
024000         10  LQW-STATUS                 PIC  X(02).                       
024100         10  LQW-STATUS-TEXT            PIC  X(12).                       
024200         10  LQW-VOID-CODE              PIC  X(01).                       
024300         10  LQW-VOID-CODE-TEXT         PIC  X(12).                       
024400                                                                          
024500                                                                          
024600*---------------------------------------------------------------*         
024700*  RECORD LAYOUT FOR THE TRANSACTION SELECTION TEMP STORAGE Q.  *         
024800*---------------------------------------------------------------*         
024900                                                                          
025000     COPY SAWS001.                                                        
025100                                                                          
025200*---------------------------------------------------------------*         
025300*    MAP LAYOUT                                                 *         
025400*---------------------------------------------------------------*         
025500                                                                          
025600     COPY SAKM431.                                                        
025700                                                                          
025800 01  MR-OCCURS-LAYOUT                   REDEFINES  SA431AO.               
025900     05  FILLER                         PIC X(132).                       
026000     05  MR-SCROLL-AREA                 OCCURS 12 TIMES.                  
026100         10  MR-SELECTL                 PIC S9(04) COMP.                  
026200         10  MR-SELECTA                 PIC  X(01).                       
026300         10  MR-SELECTC                 PIC  X(01).                       
026400         10  MR-SELECTH                 PIC  X(01).                       
026500         10  MR-SELECTI                 PIC  X(01).                       
026600                                                                          
026700         10  MR-ACTIONL                 PIC S9(04) COMP.                  
026800         10  MR-ACTIONA                 PIC  X(01).                       
026900         10  MR-ACTIONC                 PIC  X(01).                       
027000         10  MR-ACTIONH                 PIC  X(01).                       
027100         10  MR-ACTION                  PIC  X(03).                       
027200                                                                          
027300         10  MR-DATEL                   PIC S9(04) COMP.                  
027400         10  MR-DATEA                   PIC  X(01).                       
027500         10  MR-DATEC                   PIC  X(01).                       
027600         10  MR-DATEH                   PIC  X(01).                       
027700         10  MR-DATE                    PIC  X(10).                       
027800                                                                          
027900         10  MR-STOREL                  PIC S9(04) COMP.                  
028000         10  MR-STOREA                  PIC  X(01).                       
028100         10  MR-STOREC                  PIC  X(01).                       
028200         10  MR-STOREH                  PIC  X(01).                       
028300         10  MR-STORE-X.                                                  
028400             15  MR-STORE               PIC  9(04).                       
028500                                                                          
028600         10  MR-REGL                    PIC S9(04) COMP.                  
028700         10  MR-REGA                    PIC  X(01).                       
028800         10  MR-REGC                    PIC  X(01).                       
028900         10  MR-REGH                    PIC  X(01).                       
029000         10  MR-REG-X.                                                    
029100             15  MR-REG                 PIC  9(04).                       
029200                                                                          
029300         10  MR-TRANSL                  PIC S9(04) COMP.                  
029400         10  MR-TRANSA                  PIC  X(01).                       
029500         10  MR-TRNASC                  PIC  X(01).                       
029600         10  MR-TRANSH                  PIC  X(01).                       
029700         10  MR-TRANS-X.                                                  
029800             15  MR-TRANS               PIC  9(04).                       
029900                                                                          
030000         10  MR-VAR-AREAL               PIC S9(04) COMP.                  
030100         10  MR-VAR-AREAA               PIC  X(01).                       
030200         10  MR-VAR-AREAC               PIC  X(01).                       
030300         10  MR-VAR-AREAH               PIC  X(01).                       
030400         10  MR-VAR-AREA                PIC  X(36).                       
030500                                                                          
030600*---------------------------------------------------------------*         
030700*  SCROLLABLE AREA OF SCREEN                                    *         
030800*---------------------------------------------------------------*         
030900 01  DL-DETAIL-LINE-LEFT.                                                 
031000     05  DL-REG-OP-LEFT                 PIC  9(07) VALUE ZERO.            
031100     05  FILLER                         PIC  X(02) VALUE SPACES.          
031200     05  DL-TYPE-LEFT                   PIC  X(02) VALUE SPACES.          
031300     05  FILLER                         PIC  X(05) VALUE SPACES.          
031400     05  DL-ONLINE-IND-LEFT             PIC  X(01) VALUE SPACES.          
031500     05  FILLER                         PIC  X(04) VALUE SPACES.          
031600     05  DL-VOID-IND-LEFT               PIC  X(01) VALUE SPACES.          
031700     05  FILLER                         PIC  X(01) VALUE SPACES.          
031800     05  DL-TRAN-AMOUNT-LEFT            PIC  Z,ZZZ,ZZ9.99-.               
031900                                                                          
032000 01  DL-DETAIL-LINE-RIGHT REDEFINES DL-DETAIL-LINE-LEFT.                  
032100     05  DL-TRAN-SRC-RIGHT              PIC  X(03).                       
032200     05  FILLER                         PIC  X(02).                       
032300     05  DL-STATUS-RIGHT                PIC  X(13).                       
032400     05  FILLER                         PIC  X(02).                       
032500     05  DL-VOID-INFO-RIGHT             PIC  X(13).                       
032600     05  FILLER                         PIC  X(03).                       
032700                                                                          
032800 01  HL-HEADING-LINES.                                                    
032900     05  HL-HEADING-LINE-LEFT           PIC  X(36)  VALUE                 
033000       'REG OP  TYPE  LINE VOID   AMOUNT    '.                            
033100     05  HL-HEADING-LINE-RIGHT          PIC  X(36)  VALUE                 
033200       'SRC  STATUS         VOID INFO       '.                            
033300                                                                          
033400*---------------------------------------------------------------*         
033500*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
033600*---------------------------------------------------------------*         
033700                                                                          
033800     COPY DPWS015.                                                        
033900                                                                          
034000*---------------------------------------------------------------*         
034100*    FUNCTION KEYS COPYBOOK.                                    *         
034200*---------------------------------------------------------------*         
034300                                                                          
034400     COPY DPWS016.                                                        
034500                                                                          
034600*---------------------------------------------------------------*         
034700*    NUMERIC EDIT LAYOUT.                                       *         
034800*---------------------------------------------------------------*         
034900                                                                          
035000     COPY DPWS010I.                                                       
035100                                                                          
035200*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
035400*---------------------------------------------------------------*         
035500                                                                          
035600     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
035700                                                                          
035800*---------------------------------------------------------------*         
035900*    STANDARD COMMAREA.                                         *         
036000*---------------------------------------------------------------*         
036100                                                                          
036200     COPY DPWS020.                                                        
036300     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
036400                                                                          
036500*---------------------------------------------------------------*         
036600* INTER-APPLICATION COMMAREA.                                   *         
036700*   SAWS004 - IS THE INPUT LAYOUT COMING FROM EJ TRANSACTION    *         
036800*             LIST SCREEN (SAKCS426).                           *         
036900*   SAWS004 - IS ALSO THE OUTPUT LAYOUT THAT WILL BE PASSED     *         
037000*             FROM THE LIST GENERATED BY THIS PROGRAM FOR A     *         
037100*             LOCAL-GO FUNCTION.                                *         
037200*---------------------------------------------------------------*         
037300                                                                          
037400         10  INTER-APPL-COMM-AREA       PIC X(200).                       
037500     COPY SAWS004.                                                        
037600                                                                          
037700*---------------------------------------------------------------*         
037800*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
037900*---------------------------------------------------------------*         
038000         10  ASC-SPECIFIC-COMMAREA.                                       
038100             15  ASC-LIST-KEYS.                                           
038200                 20  ASC-KEY-SALES-DATE-ISO      PIC  X(10).              
038300                 20  ASC-KEY-SALES-DATE-DISPLAY  PIC  X(10).              
038400                 20  ASC-KEY-STORE               PIC S9(04)               
038500                                                 COMP.                    
038600                 20  ASC-KEY-REGISTER            PIC S9(04)               
038700                                                 COMP.                    
038800                 20  ASC-KEY-TRANSACTION         PIC S9(04)               
038900                                                 COMP.                    
039000                 20  ASC-KEY-TRANSACTION-AMOUNT  PIC S9(07)V9(02)         
039100                                                 COMP-3.                  
039200                 20  ASC-KEY-OPERATOR-ID         PIC S9(09)               
039300                                                 COMP.                    
039400                 20  ASC-KEY-TENDER-ID           PIC  X(33).              
039500                 20  ASC-KEY-TENDER-TYPE         PIC  X(02).              
039600                 20  ASC-KEY-KEYED-TENDER-IND    PIC  X(01).              
039700                 20  ASC-KEY-TRANSACTION-TYPE    PIC  X(02).              
039800                 20  ASC-KEY-VOID-TRANSACTION-IND                         
039900                                                 PIC  X(01).              
040000                 20  ASC-KEY-PARTN-NBR           PIC S9(04) COMP.         
040100                 20  ASC-KEY-STRUC-CDE           PIC  X(01).              
040200                 20  ASC-SAME-DATE-SW            PIC  X(01).              
040300                 20  ASC-KEY-SLS-CORR-SEQ-NBR    PIC S9(04) COMP.         
040400                 20  ASC-KEY-START-TIME          PIC  X(08).              
040500             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
040600                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
040700                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
040800                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
040900                                                                          
041000                 20  ASC-ITEM-ARRAY.                                      
041100                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.                
041200                         30  ASC-LIST-ITEM-NUMBER                         
041300                                                     PIC S9(07)           
041400                                                     COMP-3.              
041500                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
041600                             88  ASC-VALID-SELECT    VALUE ' '            
041700                                                           'S'            
041800                                                           'B'            
041900                                                           'E'.           
042000                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
042100                             88  ASC-TEMP-SELECT     VALUE 'S'.           
042200                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
042300                             88  ASC-TEMP-END        VALUE 'E'.           
042400                         30  ASC-SELECTED-SW         PIC  X(01).          
042500                             88  ASC-NO-SELECT       VALUE ' '.           
042600                             88  ASC-SELECT          VALUE 'S'.           
042700                             88  ASC-BEGIN           VALUE 'B'.           
042800                             88  ASC-END             VALUE 'E'.           
042900                         30  ASC-ACTION              PIC  X(03).          
043000                         30  ASC-PARTITION-NBR       PIC S9(04)           
043100                                                         COMP.            
043200                         30  ASC-SALES-DATE          PIC  X(10).          
043300                         30  ASC-STORE               PIC S9(04)           
043400                                                          COMP.           
043500                         30  ASC-REGISTER            PIC S9(04)           
043600                                                          COMP.           
043700                         30  ASC-TRANSACTION         PIC S9(04)           
043800                                                          COMP.           
043900                         30  ASC-START-TIME          PIC X(08).           
044000                         30  ASC-REGISTER-OPERATOR   PIC 9(07)            
044100                                                          COMP-3.         
044200                         30  ASC-TYPE                PIC  X(02).          
044300                         30  ASC-ONLINE-IND          PIC  X(01).          
044400                         30  ASC-AMOUNT          PIC S9(07)V9(02)         
044500                                                          COMP-3.         
044600                         30  ASC-SALES-CORR-SEQ-NBR  PIC S9(04)           
044700                                                           COMP.          
044800                         30  ASC-SOURCE-TEXT         PIC  X(03).          
044900                         30  ASC-STATUS              PIC  X(02).          
045000                             88 ASC-STAT-VALID-CORR  VALUE 'V'.           
045100                             88 ASC-STAT-VALID-POS   VALUE 'Z'.           
045200                             88 ASC-STAT-MIS-TOT     VALUE 'S'.           
045300                             88 ASC-STAT-MIS-TND     VALUE 'T'.           
045400                             88 ASC-STAT-OUT-BAL-TND VALUE 'B'.           
045500                             88 ASC-STAT-OUT-BAL-ITM VALUE 'O'.           
045600                             88 ASC-STAT-NON-NUM-ACT VALUE 'N'.           
045700                             88 ASC-STAT-MIS-ACT     VALUE 'M'.           
045800                             88 ASC-STAT-INVAL-COR   VALUE 'W'.           
045900                             88 ASC-STAT-INVAL-PRE   VALUE 'P'.           
046000                             88 ASC-STAT-INVAL-C-D   VALUE 'C'.           
046100                             88 ASC-STAT-INVAL-STR   VALUE 'I'.           
046200                             88 ASC-STAT-REVERSAL    VALUE 'R'.           
046300                             88 ASC-STAT-LATE-DC     VALUE 'L'.           
046400                             88 ASC-STAT-INVAL-HD-TOT                     
046500                                                     VALUE 'H'.           
046600                             88 ASC-STAT-ERROR-GEN   VALUE 'E'.           
046700                             88 ASC-STAT-INCOMP-DATA VALUE 'D'.           
046800                         30  ASC-STATUS-TEXT         PIC  X(12).          
046900                         30  ASC-VOID-CODE           PIC  X(01).          
047000                             88 ASC-VOID-AFTER       VALUE 'V'.           
047100                             88 ASC-VOID-DURING      VALUE 'A'.           
047200                             88 ASC-VOID-AFTER-DURING                     
047300                                                     VALUE 'I'.           
047400                             88 ASC-VOID-DURING-AFTER                     
047500                                                     VALUE 'B'.           
047600                             88 ASC-VOID-REVERSED    VALUE 'R'.           
047700                             88 ASC-VOID-UNCORRECTED VALUE 'U'.           
047800                             88 ASC-VOID-NORMAL      VALUE 'N'.           
047900                         30  ASC-VOID-CODE-TEXT      PIC  X(12).          
048000             15  ASC-BLK-SUB                         PIC S9(04)           
048100                                                          COMP            
048200                                                          SYNC.           
048300             15  ASC-ITEM-SUB                        PIC S9(04)           
048400                                                          COMP            
048500                                                          SYNC.           
048600             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
048700                                                          COMP-3.         
048800             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
048900                                                          COMP-3.         
049000             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
049100                                                          COMP-3.         
049200             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
049300                                                          COMP-3.         
049400             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
049500                                                          COMP-3.         
049600             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
049700                                                          COMP            
049800                                                          SYNC.           
049900             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
050000                                                          COMP-3.         
050100             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
050200                                                          COMP-3.         
050300             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
050400                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
050500                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
050600             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
050700                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
050800                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
050900             15  ASC-LIST-QUEUE-NAME.                                     
051000                 20  ASC-LIST-TERM-ID                PIC  X(04).          
051100                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
051200                                                          COMP-3.         
051300                 20  ASC-LIST-SEQ                    PIC  9(01).          
051400             15  ASC-SEL-QUEUE-NAME.                                      
051500                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
051600                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
051700                                                          COMP-3.         
051800                 20  ASC-SEL-SEQ                     PIC  9(01).          
051900             15  ASC-SEL2-QUEUE-NAME.                                     
052000                 20  ASC-SEL2-TERM-ID                PIC  X(04).          
052100                 20  ASC-SEL2-TASK-NUMBER            PIC S9(05)           
052200                                                          COMP-3.         
052300                 20  ASC-SEL2-SEQ                    PIC  9(01).          
052400                                                                          
052500             15  ASC-START-ITEM-REQUEST.                                  
052600                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
052700             15  ASC-VIEW-IND                        PIC  X(01).          
052800                 88  ASC-LEFT-VIEW                   VALUE 'L'.           
052900                 88  ASC-RIGHT-VIEW                  VALUE 'R'.           
053000             15  ASC-TRAN-SEL-QUEUE-NAME             PIC  X(08).          
053100             15  ASC-TRAN-SEL-ITEM                   PIC S9(04)           
053200                                                     COMP SYNC.           
053300             15  ASC-TRAN-SEL-SUB                    PIC  9(02).          
053400             15  ASC-NBR-OF-TRAN-TS-ITEMS            PIC S9(05)           
053500                                                     COMP-3.              
053600         10  FILLER                                  PIC  X(793).         
053700                                                                          
053800*---------------------------------------------------------------*         
053900*    ABEND PROCESSING COPYBOOK.                                 *         
054000*---------------------------------------------------------------*         
054100                                                                          
054200     COPY DPWS013.                                                        
054300                                                                          
054400*---------------------------------------------------------------*         
054500*    DB2 AREA FOR TEPTRN (TRANSACTION TABLE)                    *         
054600*---------------------------------------------------------------*         
054700     EXEC SQL                                                             
054800          INCLUDE TEPTRN                                                  
054900     END-EXEC.                                                            
055000                                                                          
055800*---------------------------------------------------------------*         
055900*    DB2 AREA FOR TEPPART (PARTITION TABLE)                     *         
056000*---------------------------------------------------------------*         
056100     EXEC SQL                                                             
056200          INCLUDE TEPPART                                                 
056300     END-EXEC.                                                            
056400                                                                          
056500*---------------------------------------------------------------*         
056600*    DB2 AREA FOR TEPSTGP (STORE GROUP TABLE)                   *         
056700*---------------------------------------------------------------*         
056800     EXEC SQL                                                             
056900          INCLUDE TEPSTGP                                                 
057000     END-EXEC.                                                            
057100                                                                          
057200*---------------------------------------------------------------*         
057300*    DB2 AREA FOR TEPIRT  (ITEM RETURN INFORMATION TABLE)       *         
057400*---------------------------------------------------------------*         
057500     EXEC SQL                                                             
057600          INCLUDE TEPIRT                                                  
057700     END-EXEC.                                                            
057800                                                                          
058600*---------------------------------------------------------------*         
058700*    DB2 AREA FOR COMMUNICATIONS                                *         
058800*---------------------------------------------------------------*         
058900     EXEC SQL                                                             
059000          INCLUDE SQLCA                                                   
059100     END-EXEC.                                                            
059200                                                                          
059300*---------------------------------------------------------------*         
059400*    TEPIRT-CSR CURSOR -- THIS CURSOR WILL RETRIEVE ALL ORIG.   *         
059500*    TRANSACTIONS AGAINST THE RETURN TRANSACTION FROM THE       *         
059600*    ITEM RETURN INFO TABLE.                                    *         
059700*---------------------------------------------------------------*         
059800                                                                          
059900     EXEC SQL                                                             
060000          DECLARE TEPIRT-CSR CURSOR                                       
060100          FOR SELECT                                                      
060200                DISTINCT (ORIG_SLS_DTE)                                   
060300              , ORIG_STR_NBR                                              
060400              , ORIG_RGST_ID                                              
060500              , ORIG_TRN_NBR                                              
060600              , ORIG_STRT_TM                                              
060700          FROM  TEPIRT                                                    
060800         WHERE  PARTN_NBR              = :DK-PARTITION                    
060900          AND   STR_NBR                = :DK-STORE                        
061000          AND   RGST_ID                = :DK-REGISTER                     
061100          AND   TRN_NBR                = :DK-TRANS                        
061200          AND   STRT_TM                = :DK-START-TIME                   
061300        ORDER BY ORIG_SLS_DTE, ORIG_STR_NBR                               
061400        WITH UR                                                           
061500     END-EXEC.                                                            
061600                                                                          
064200*---------------------------------------------------------------*         
064300*    TEPTRN-CSR.                                                *         
064400*---------------------------------------------------------------*         
064500                                                                          
064600     EXEC SQL                                                             
064700          DECLARE TEPTRN-CSR CURSOR                                       
064800          FOR SELECT                                                      
064900                  TRN.SLS_DTE                                             
065000                , STR_NBR                                                 
065100                , RGST_ID                                                 
065200                , TRN_NBR                                                 
065300                , STRT_TM                                                 
065400                , SLS_CORR_SEQ_NBR                                        
065500                , TRN_TYP_CDE                                             
065600                , ASSOC_ID                                                
065700                , TRN_TOT_AMT                                             
065800                , SRVR_ONLN_IND                                           
065900                , VOID_ABRT_CDE                                           
066000                , TRN_STAT_CDE                                            
066100                , PRCG_DTE                                                
066200           FROM   TEPTRN  TRN                                             
066300                , TEPPART PRT                                             
066400          WHERE   PRT.PARTN_NBR      = :PV-ORIG-PARTN-NBR                 
066500           AND    TRN.PARTN_NBR      =  PRT.PARTN_NBR                     
066600           AND    TRN.STR_NBR        = :PV-ORIG-STR-NBR                   
066700           AND    TRN.SLS_DTE        = :PV-ORIG-SLS-DTE-ISO               
066800           AND    TRN.RGST_ID        = :PV-ORIG-RGST-ID                   
066900           AND    TRN.TRN_NBR        = :PV-ORIG-TRN-NBR                   
067000           AND    CHAR(TRN.STRT_TM) LIKE :PV-ORIG-STRT-TM                 
067200        WITH UR                                                           
067300     END-EXEC.                                                            
070700                                                                          
070800**************************                                                
070900 LINKAGE SECTION.                                                         
071000                                                                          
071100 01  DFHCOMMAREA.                                                         
071200     05  FILLER                         OCCURS  1 TO 4072 TIMES           
071300                                        DEPENDING ON EIBCALEN.            
071400         10  FILLER                     PIC X.                            
071500                                                                          
071600 PROCEDURE DIVISION.                                                      
071700*----------------------------------------------------------------*        
071800*   THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM.  *        
071900*   FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM   *        
072000*   PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT.  *        
072100*----------------------------------------------------------------*        
072200 0000-MAIN-MODULE.                                                        
072300     INITIALIZE DP030-CICS-API-FIELDS.                                    
072400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
072500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
072600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
072700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
072800     MOVE LENGTH OF SA431AI        TO DP030-MAP-LENGTH (1).               
072900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
073000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
073100                                                                          
073200     PERFORM 1000-CONTROL-PROCESSING                                      
073300                                                                          
073400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
073500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
073600                                                                          
073700*----------------------------------------------------------------*        
073800*   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
073900*   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
074000*----------------------------------------------------------------*        
074100 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
074300                                    DFHCOMMAREA                           
074400                                    DP030-CICS-API-FIELDS                 
074500                                    DP020-STANDARD-COMMAREA               
074600                                    SA431AI.                              
074700                                                                          
074800     IF  DP030-RC-CALL-SUCCESSFUL                                         
074900         CONTINUE                                                         
075000     ELSE                                                                 
075100         SET DP013-NO-ROLLBACK                                            
075200             DP013-XCTL-DISPLAY-RESTART                                   
075300             DP013-CICS-ABEND      TO TRUE                                
075400         MOVE '0001-CALL-CICS-ARCH-API'                                   
075500                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
075800         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
075900         PERFORM DP013-0000-PROCESS-ABEND                                 
076000     END-IF.                                                              
076100*----------------------------------------------------------------*        
076200* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
076300* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
076400*                                                                *        
076500* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
076600*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
076700*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
076800*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
076900*----------------------------------------------------------------*        
077000                                                                          
077100 1000-CONTROL-PROCESSING.                                                 
077200                                                                          
077300     EVALUATE TRUE                                                        
077400         WHEN DP020-NEXT-ACT-INITIAL                                      
077500             INITIALIZE ASC-SPECIFIC-COMMAREA                             
077600             SET ASC-LEFT-VIEW     TO TRUE                                
077700             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
077800                                      ASC-SEL-TERM-ID                     
077900                                      ASC-SEL2-TERM-ID                    
078000             MOVE DP020-SRC-TASK-NUMBER                                   
078100                                   TO ASC-LIST-TASK-NUMBER                
078200                                      ASC-SEL-TASK-NUMBER                 
078300                                      ASC-SEL2-TASK-NUMBER                
078400             MOVE PC-LIST-QUEUE-SEQ                                       
078500                                   TO ASC-LIST-SEQ                        
078600             MOVE PC-SEL-QUEUE-SEQ                                        
078700                                   TO ASC-SEL-SEQ                         
078800             MOVE PC-SEL2-QUEUE-SEQ                                       
078900                                   TO ASC-SEL2-SEQ                        
079000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
079100***-----$TEMPLATE NOTE$-------------------------------------------        
079200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
079300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
079400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
079500***---------------------------------------------------------------        
079600             IF  PS-COMMAREA-NOT-VALID                                    
079700                 SET DP020-NEXT-ACT-APPL-ERROR                            
079800                                   TO TRUE                                
079900             ELSE                                                         
080000                 SET SA001-INQ (ASC-TRAN-SEL-SUB)                         
080100                                   TO TRUE                                
080200                 PERFORM 9200-REWRITE-SEL-QUEUE                           
080300                 PERFORM 4000-BUILD-INITIAL-PANEL                         
080400             END-IF                                                       
080500                                                                          
080600         WHEN DP020-NEXT-ACT-READ-MAP                                     
080700             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
080800                                   TO PV-TOP-ITEM                         
080900             PERFORM 2000-PROCESS-PANEL                                   
081000                                                                          
081100         WHEN DP020-NEXT-ACT-RETURN                                       
081200             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
081300                                   TO PV-TOP-ITEM                         
081400             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
081500             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
081600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
081700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
081800                                                                          
081900         WHEN OTHER                                                       
082000             SET DP013-LOGIC-ABEND TO TRUE                                
082100             SET DP013-NO-ROLLBACK TO TRUE                                
082200             MOVE '1000-CONTROL-PROCESSING'                               
082300                                   TO DP013-PARAGRAPH                     
082400             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
082500                                   TO DP013-MESSAGE-TEXT(1)               
082600             PERFORM DP013-0000-PROCESS-ABEND                             
082700     END-EVALUATE.                                                        
082800                                                                          
082900*----------------------------------------------------------------*        
083000* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
083100* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
083200*----------------------------------------------------------------*        
083300                                                                          
083400 1100-PROCESS-INTER-APPL-COMM.                                            
083500                                                                          
083600     SET PS-COMMAREA-VALID     TO TRUE.                                   
083700     MOVE SPACE                TO ASC-START-ITEM-REQUEST.                 
083800     EVALUATE TRUE                                                        
083900         WHEN DP020-INT-APPL-FORMAT-IND                                   
084000                                   = PC-LIST-QUEUE-SEQ                    
084100             MOVE SA004-NUMBER-OF-SELECTED-ITEMS                          
084200                                   TO ASC-NUMBER-OF-SELECTED-ITEMS        
084300             MOVE SA004-NUMBER-OF-TS-ITEMS                                
084400                                   TO ASC-NBR-OF-TRAN-TS-ITEMS            
084500             MOVE SA004-SEL-QUEUE-NAME                                    
084600                                   TO ASC-TRAN-SEL-QUEUE-NAME             
084700             MOVE SA004-SALES-DATE-ISO                                    
084800                                   TO ASC-KEY-SALES-DATE-ISO              
084900                                      DK-SALES-DATE                       
085000             MOVE SA004-SALES-DATE-DISPLAY                                
085100                                   TO ASC-KEY-SALES-DATE-DISPLAY          
085200             PERFORM 1120-PREPARE-LIST-ENTRY                              
085300                                                                          
085400         WHEN OTHER                                                       
085500             SET DP013-LOGIC-ABEND TO TRUE                                
085600             SET DP013-NO-ROLLBACK TO TRUE                                
085700             MOVE '1100-PROCESS-INTER-APPL-COMM'                          
085800                                   TO DP013-PARAGRAPH                     
085900             MOVE 'INVALID INTER APPL FORMAT INDICATOR'                   
086000                                   TO DP013-MESSAGE-TEXT(1)               
086100             PERFORM DP013-0000-PROCESS-ABEND                             
086200                                                                          
086300     END-EVALUATE.                                                        
086400                                                                          
086500*----------------------------------------------------------------*        
086600*    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
086700*----------------------------------------------------------------*        
086800                                                                          
086900 1120-PREPARE-LIST-ENTRY.                                                 
087000                                                                          
087100     MOVE SA001-MAX-ITEMS TO ASC-TRAN-SEL-SUB.                            
087200     MOVE ZERO            TO ASC-TRAN-SEL-ITEM.                           
087300     PERFORM 9100-READ-TRAN-SEL-QUEUE.                                    
087400     MOVE SA001-PARTITION (ASC-TRAN-SEL-SUB)                              
087500                          TO DK-PARTITION                                 
087600                             ASC-KEY-PARTN-NBR.                           
087700     MOVE SA001-STORE (ASC-TRAN-SEL-SUB)                                  
087800                          TO DK-STORE                                     
087900                             ASC-KEY-STORE.                               
088000     MOVE SA001-REGISTER (ASC-TRAN-SEL-SUB)                               
088100                          TO DK-REGISTER                                  
088200                             ASC-KEY-REGISTER.                            
088300     MOVE SA001-TRANS (ASC-TRAN-SEL-SUB)                                  
088400                          TO DK-TRANS                                     
088500                             ASC-KEY-TRANSACTION.                         
088600     MOVE SA001-SALES-CORR-SEQ-NBR (ASC-TRAN-SEL-SUB)                     
088700                          TO DK-SALES-CORR-SEQ-NBR                        
088800                             ASC-KEY-SLS-CORR-SEQ-NBR.                    
088900     MOVE SA001-START-TIME (ASC-TRAN-SEL-SUB)                             
089000                          TO DK-START-TIME                                
089100                             ASC-KEY-START-TIME.                          
089200                                                                          
089300*----------------------------------------------------------------*        
089400* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITING OF THE DATA    *        
089500* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
089600* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
089700* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
089800* FUNCTION KEYS CAN GET THIS FAR.                                *        
089900*----------------------------------------------------------------*        
090000                                                                          
090100 2000-PROCESS-PANEL.                                                      
090200     EVALUATE TRUE                                                        
090300         WHEN DP020-SRC-AID = DP016-CLEAR                                 
090400             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
090500             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
090600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
090700                                                                          
090800         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
090900                                                                          
091000*** THE REFRESH KEY JUST DOES A MOVE IN THIS CASE.  THIS IS OK            
091100*** BECAUSE THE DATA THAT THE SCREEN DISPLAYS DOES NOT CHANGE.            
091200*** THERE IS NO NEED TO HAVE DB2 DO ALL THE WORK WHEN IT IS NOT           
091300*** NECESSARY.                                                            
091400                                                                          
091500             SET ASC-LEFT-VIEW     TO TRUE                                
091600             MOVE +1               TO PV-TOP-ITEM                         
091700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
091800                                                                          
091900         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
092000             PERFORM 2140-DESELECT-ALL-ITEMS                              
092100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
092200                                                                          
092300         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
092400             PERFORM 2150-SELECT-ALL-ITEMS                                
092500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
092600                                                                          
092700         WHEN OTHER                                                       
092800             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
092900             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
093000             IF  PS-NO-ERROR                                              
093100                 SET DP030-CONTINUE-GO                                    
093200                                 TO TRUE                                  
093300                 PERFORM 2100-CHECK-FUNCTION-KEY                          
093400             ELSE                                                         
093500                 SET DP030-OVERRIDE-GO                                    
093600                                 TO TRUE                                  
093700             END-IF                                                       
093800     END-EVALUATE.                                                        
093900                                                                          
094000*----------------------------------------------------------------*        
094100*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
094200*----------------------------------------------------------------*        
094300 2050-DELETE-LIST-QUEUE.                                                  
094400                                                                          
094500     EXEC CICS                                                            
094600         DELETEQ TS                                                       
094700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
094800             RESP  (PV-CICS-RESP)                                         
094900     END-EXEC.                                                            
095000                                                                          
095100     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
095200        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
095300         CONTINUE                                                         
095400     ELSE                                                                 
095500         SET DP013-CICS-ABEND      TO TRUE                                
095600         SET DP013-NO-ROLLBACK     TO TRUE                                
095700                                                                          
095800         MOVE '2050-DELETE-LIST-QUEUE'                                    
095900                                   TO DP013-PARAGRAPH                     
096000         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
096100                                   TO DP013-MESSAGE-TEXT (1)              
096200         PERFORM DP013-0000-PROCESS-ABEND                                 
096300     END-IF.                                                              
096400                                                                          
096500     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
096600                                                                          
096700*----------------------------------------------------------------*        
096800* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
096900* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
097000* CICS ARCHITECTURE API.                                         *        
097100*----------------------------------------------------------------*        
097200 2100-CHECK-FUNCTION-KEY.                                                 
097300     EVALUATE TRUE                                                        
097400         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
097500         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
097600         WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
097700                                                                          
097800             IF  ASC-NUMBER-OF-SELECTED-ITEMS = +1                        
097900                 INITIALIZE SA004-INTER-APPL-COMMAREA                     
098000                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
098100                                TO SA004-NUMBER-OF-SELECTED-ITEMS         
098200                 PERFORM 5000-PREPARE-SEL-QUEUE                           
098300             ELSE                                                         
098400                 IF ASC-NUMBER-OF-SELECTED-ITEMS > +1                     
098500                     SET DP030-OVERRIDE-GO                                
098600                                   TO TRUE                                
098700                     MOVE PC-TSYMSG-00047                                 
098800                                   TO DP020-MSG-NUMBER                    
098900                     SET DP020-MSG-FATAL                                  
099000                                   TO TRUE                                
099100                 ELSE                                                     
099200                     SET DP030-OVERRIDE-GO                                
099300                                   TO TRUE                                
099400                     MOVE PC-TSYMSG-00057                                 
099500                                   TO DP020-MSG-NUMBER                    
099600                     SET DP020-MSG-WARNING                                
099700                                   TO TRUE                                
099800                 END-IF                                                   
099900             END-IF                                                       
100000                                                                          
100100         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
100200             MOVE +1               TO PV-TOP-ITEM                         
100300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
100400                                                                          
100500         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
100600             PERFORM 2110-BROWSE-BACKWARD                                 
100700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
100800                                                                          
100900         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
101000             PERFORM 2120-BROWSE-FORWARD                                  
101100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
101200                                                                          
101300         WHEN DP020-FK-LEFT (DP020-SRC-AID)                               
101400             SET ASC-LEFT-VIEW  TO TRUE                                   
101500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
101600                                                                          
101700         WHEN DP020-FK-RIGHT (DP020-SRC-AID)                              
101800             SET ASC-RIGHT-VIEW TO TRUE                                   
101900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
102000                                                                          
102100         WHEN DP020-SRC-AID = DP016-ENTER                                 
102200             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
102300             IF  PS-ERROR                                                 
102400                 CONTINUE                                                 
102500             ELSE                                                         
102600                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
102700             END-IF                                                       
102800                                                                          
102900         WHEN OTHER                                                       
103000             SET DP013-NO-ROLLBACK                                        
103100                 DP013-XCTL-DISPLAY-RESTART                               
103200                 DP013-CICS-ABEND  TO TRUE                                
103300             MOVE '2100-CHECK-FUNCTION-KEY'                               
103400                                   TO DP013-PARAGRAPH                     
103500             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
103600                                   TO DP013-MESSAGE-TEXT (1)              
103700             PERFORM DP013-0000-PROCESS-ABEND                             
103800     END-EVALUATE.                                                        
103900                                                                          
104000*----------------------------------------------------------------*        
104100*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
104200*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
104300*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
104400*    MESSAGE.                                                    *        
104500*----------------------------------------------------------------*        
104600                                                                          
104700 2110-BROWSE-BACKWARD.                                                    
104800     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
104900         GIVING PV-TOP-ITEM.                                              
105000                                                                          
105100     IF  ((PV-TOP-ITEM < +1)                                              
105200       OR (PV-TOP-ITEM = +1))                                             
105300         MOVE +1                   TO PV-TOP-ITEM                         
105400*        -- TOP OF LIST --                                                
105500         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
105600         SET DP020-MSG-INFORMATIONAL TO TRUE                              
105700     END-IF.                                                              
105800                                                                          
105900*----------------------------------------------------------------*        
106000*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
106100*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
106200*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
106300*----------------------------------------------------------------*        
106400 2120-BROWSE-FORWARD.                                                     
106500                                                                          
106600     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
106700         GIVING PV-TOP-ITEM.                                              
106800     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
106900         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
107000                                   TO PV-TOP-ITEM                         
107100*        -- BOTTOM OF LIST --                                             
107200         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
107300         SET DP020-MSG-INFORMATIONAL                                      
107400                                   TO TRUE                                
107500     ELSE                                                                 
107600         COMPUTE PV-BOTTOM-ITEM =                                         
107700             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
107800         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
107900*            -- BOTTOM OF LIST --                                         
108000             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
108100             SET DP020-MSG-INFORMATIONAL                                  
108200                                   TO TRUE                                
108300         END-IF                                                           
108400     END-IF.                                                              
108500                                                                          
108600*----------------------------------------------------------------*        
108700*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
108800*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
108900*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
109000*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
109100*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
109200*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
109300*----------------------------------------------------------------*        
109400 2130-CHECK-FOR-JUMP-BROWSE.                                              
109500                                                                          
109600     SET PS-NO-ERROR               TO TRUE.                               
109700     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
109800         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
109900                                   TO PV-TOP-ITEM                         
110000     ELSE                                                                 
110100         MOVE ASC-START-ITEM-REQUEST                                      
110200                                   TO DP010I-UNEDITED-FIELD               
110300         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
110400         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
110500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
110600                                   TO TRUE                                
139218         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
139218                         USING DP010I-NUMERIC-EDIT-AREA                   
110800                                                                          
110900         IF  NOT DP010I-ERROR-DETECTED                                    
111000             MOVE DP010I-NUMERIC-FIELD                                    
111100                                   TO  ASC-START-ITEM-REQUEST-N           
111200             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
111300                 IF  ASC-START-ITEM-REQUEST-N >                           
111400                                          ASC-NUMBER-OF-ITEMS-READ        
111500                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
111600                     COMPUTE PV-TOP-ITEM =                                
111700                             ASC-NUMBER-OF-ITEMS-READ                     
111800                             - PC-ITEMS-PER-PANEL + 1                     
111900                     IF  PV-TOP-ITEM < +1                                 
112000                         MOVE +1   TO PV-TOP-ITEM                         
112100                         MOVE PC-TSYMSG-00070                             
112200                                   TO DP020-MSG-NUMBER                    
112300                         SET DP020-MSG-INFORMATIONAL                      
112400                                   TO TRUE                                
112500                     END-IF                                               
112600                 ELSE                                                     
112700                     MOVE ASC-START-ITEM-REQUEST-N                        
112800                                   TO PV-TOP-ITEM                         
112900                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
113000                     COMPUTE PV-BOTTOM-ITEM =                             
113100                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
113200                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
113300*                        -- BOTTOM OF LIST --                             
113400                         MOVE PC-TSYMSG-00070                             
113500                                   TO DP020-MSG-NUMBER                    
113600                         SET DP020-MSG-INFORMATIONAL                      
113700                                   TO TRUE                                
113800                     END-IF                                               
113900                 END-IF                                                   
114000             ELSE                                                         
114100*                --- VALUE OUT OF RANGE ----                              
114200                 MOVE PC-TSYMSG-00101                                     
114300                                   TO DP020-MSG-NUMBER                    
114400                 MOVE -1           TO ASTRTITL                            
114500                 SET DP030-SET-CURSOR-APPL-1                              
114600                                   TO TRUE                                
114700                 MOVE DP015-REVERSE                                       
114800                                   TO ASTRTITH                            
114900                 MOVE DP015-RED    TO ASTRTITC                            
115000                 MOVE DP015-UNP-NUM-BRT-MDT                               
115100                                   TO ASTRTITA                            
115200                 SET DP020-MSG-FATAL                                      
115300                                   TO TRUE                                
115400                 SET PS-ERROR      TO TRUE                                
115500             END-IF                                                       
115600                                                                          
115700         ELSE                                                             
115800             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
115900             MOVE -1               TO ASTRTITL                            
116000             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
116100             MOVE DP015-REVERSE    TO ASTRTITH                            
116200             MOVE DP015-RED        TO ASTRTITC                            
116300             MOVE DP015-UNP-NUM-BRT-MDT                                   
116400                                   TO ASTRTITA                            
116500             SET DP020-MSG-FATAL   TO TRUE                                
116600             SET PS-ERROR          TO TRUE                                
116700         END-IF                                                           
116800     END-IF.                                                              
116900                                                                          
117000*----------------------------------------------------------------*        
117100*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
117200*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
117300*----------------------------------------------------------------*        
117400 2140-DESELECT-ALL-ITEMS.                                                 
117500                                                                          
117600     MOVE +1                       TO ASC-BLK-SUB.                        
117700                                                                          
117800     PERFORM 2141-CLEAR-SELECTED-INDS                                     
117900         VARYING ASC-ITEM-SUB                                             
118000         FROM 1 BY 1                                                      
118100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
118200                                                                          
118300     ADD +1                        TO ASC-BLK-SUB.                        
118400                                                                          
118500     PERFORM 2141-CLEAR-SELECTED-INDS                                     
118600         VARYING ASC-ITEM-SUB                                             
118700         FROM 1 BY 1                                                      
118800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
118900                                                                          
119000     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
119100                                   TO PV-LIST-ITEM-NUMBER.                
119200                                                                          
119300     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
119400         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
119500                                                                          
119600     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
119700        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
119800         MOVE +1                   TO ASC-BLK-SUB                         
119900                                                                          
120000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
120100                                                                          
120200         MOVE +1                   TO PV-BLOCK-NUMBER                     
120300                                                                          
120400         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
120500             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
120600                                                                          
120700         PERFORM                                                          
120800             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
120900                                                                          
121000             PERFORM 4410-READ-TEMP-STORAGE                               
121100                                                                          
121200             PERFORM 2141-CLEAR-SELECTED-INDS                             
121300                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
121400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
121500                                                                          
121600             PERFORM 5100-WRITE-TEMP-STORAGE                              
121700                                                                          
121800             ADD +1                TO PV-BLOCK-NUMBER                     
121900                                                                          
122000         END-PERFORM                                                      
122100                                                                          
122200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
122300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
122400                                                                          
122500         PERFORM                                                          
122600             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
122700                   ASC-HIGHEST-BLOCK-WRITTEN                              
122800                                                                          
122900             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
123000                                   TO PV-BLOCK-NUMBER                     
123100                                                                          
123200             PERFORM 4410-READ-TEMP-STORAGE                               
123300                                                                          
123400             PERFORM 2141-CLEAR-SELECTED-INDS                             
123500                 VARYING ASC-ITEM-SUB                                     
123600                 FROM 1 BY 1                                              
123700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
123800                                                                          
123900             PERFORM 5100-WRITE-TEMP-STORAGE                              
124000                                                                          
124100             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
124200                                                                          
124300         END-PERFORM                                                      
124400                                                                          
124500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
124600                                   TO PV-BLOCK-NUMBER                     
124700                                                                          
124800         PERFORM 4410-READ-TEMP-STORAGE                                   
124900     END-IF.                                                              
125000                                                                          
125100                                                                          
125200     MOVE +0                      TO ASC-NUMBER-OF-SELECTED-ITEMS.        
125300                                                                          
125400     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
125500                                                                          
125600*----------------------------------------------------------------*        
125700*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
125800*----------------------------------------------------------------*        
125900 2141-CLEAR-SELECTED-INDS.                                                
126000                                                                          
126100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
126200             NOT EQUAL ZERO                                               
126300         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
126400             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
126500                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
126600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
126700                                   TO TRUE                                
126800         END-IF                                                           
126900     END-IF.                                                              
127000                                                                          
127100*----------------------------------------------------------------*        
127200*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTION        *        
127300*  SWITCH FOR ALL ITEMS.                                         *        
127400*----------------------------------------------------------------*        
127500 2150-SELECT-ALL-ITEMS.                                                   
127600                                                                          
127700     MOVE +1                       TO ASC-BLK-SUB.                        
127800                                                                          
127900     PERFORM 2151-SET-SELECTED-INDS                                       
128000         VARYING ASC-ITEM-SUB                                             
128100         FROM 1 BY 1                                                      
128200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
128300                                                                          
128400     ADD +1                        TO ASC-BLK-SUB.                        
128500                                                                          
128600     PERFORM 2151-SET-SELECTED-INDS                                       
128700         VARYING ASC-ITEM-SUB                                             
128800         FROM 1 BY 1                                                      
128900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
129000                                                                          
129100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
129200                                   TO PV-LIST-ITEM-NUMBER.                
129300                                                                          
129400     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
129500         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
129600                                                                          
129700     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
129800        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
129900         MOVE +1                   TO ASC-BLK-SUB                         
130000                                                                          
130100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
130200                                                                          
130300         MOVE +1                   TO PV-BLOCK-NUMBER                     
130400                                                                          
130500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
130600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
130700                                                                          
130800         PERFORM                                                          
130900             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
131000                                                                          
131100             PERFORM 4410-READ-TEMP-STORAGE                               
131200                                                                          
131300             PERFORM 2151-SET-SELECTED-INDS                               
131400                 VARYING ASC-ITEM-SUB                                     
131500                 FROM 1 BY 1                                              
131600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
131700                                                                          
131800             PERFORM 5100-WRITE-TEMP-STORAGE                              
131900                                                                          
132000             ADD +1                TO PV-BLOCK-NUMBER                     
132100                                                                          
132200         END-PERFORM                                                      
132300                                                                          
132400         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
132500             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
132600                                                                          
132700         PERFORM                                                          
132800             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
132900                   ASC-HIGHEST-BLOCK-WRITTEN                              
133000                                                                          
133100             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
133200                                   TO PV-BLOCK-NUMBER                     
133300                                                                          
133400             PERFORM 4410-READ-TEMP-STORAGE                               
133500                                                                          
133600             PERFORM 2151-SET-SELECTED-INDS                               
133700                 VARYING ASC-ITEM-SUB                                     
133800                 FROM 1 BY 1                                              
133900                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
134000                                                                          
134100             PERFORM 5100-WRITE-TEMP-STORAGE                              
134200                                                                          
134300             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
134400                                                                          
134500         END-PERFORM                                                      
134600                                                                          
134700         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
134800                                   TO PV-BLOCK-NUMBER                     
134900                                                                          
135000         PERFORM 4410-READ-TEMP-STORAGE                                   
135100     END-IF.                                                              
135200                                                                          
135300                                                                          
135400     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
135500                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
135600                                                                          
135700     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
135800                                                                          
135900*----------------------------------------------------------------*        
136000*    MARK EACH ITEM AS BEING SELECTED.                           *        
136100*----------------------------------------------------------------*        
136200 2151-SET-SELECTED-INDS.                                                  
136300                                                                          
136400     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
136500             NOT EQUAL ZERO                                               
136600         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
136700             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
136800                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
136900                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
137000                               TO  TRUE                                   
137100         END-IF                                                           
137200     END-IF.                                                              
137300*----------------------------------------------------------------*        
137400*  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *        
137500*  SCREEN INTO THE COMMAREA.  ALL EDITING IS DONE IN THE COMM.   *        
137600*----------------------------------------------------------------*        
137700 2200-MOVE-SCREEN-TO-COMMAREA.                                            
137800                                                                          
137900     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
138000     MOVE +1                       TO PV-SUB.                             
138100                                                                          
138200     PERFORM 3100-SET-SUBSCRIPTS.                                         
138300     IF  ASTRTITL > ZERO                                                  
138400         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
138500     ELSE                                                                 
138600         IF  ASTRTITF = DP015-ERASE-EOF                                   
138700             INITIALIZE ASC-START-ITEM-REQUEST                            
138800         END-IF                                                           
138900     END-IF.                                                              
139000     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
139100         VARYING PV-SUB                                                   
139200         FROM 1 BY 1                                                      
139300         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
139400*----------------------------------------------------------------*        
139500*  MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA *        
139600*----------------------------------------------------------------*        
139700 2210-MOVE-LINES-TO-COMMAREA.                                             
139800     IF  MR-SELECTL (PV-SUB) > ZERO                                       
139900         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
140000         MOVE MR-SELECTI (PV-SUB)                                         
140100                                   TO ASC-TEMP-SELECTED-SW                
140200                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
140300     ELSE                                                                 
140400         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
140500                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
140600                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
140700                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
140800         END-IF                                                           
140900     END-IF.                                                              
141000     ADD +1                        TO ASC-ITEM-SUB.                       
141100     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
141200         ADD +1                    TO ASC-BLK-SUB                         
141300         MOVE +1                   TO ASC-ITEM-SUB                        
141400     END-IF.                                                              
141500*----------------------------------------------------------------*        
141600*  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *        
141700*  COMMAREA (EDITING IS NOT DONE AGAINST THE SCREEN DIRECTLY).   *        
141800*----------------------------------------------------------------*        
141900 3000-EDIT-DATA-IN-COMMAREA.                                              
142000     SET PS-NO-ERROR               TO TRUE.                               
142100     PERFORM 3200-RESET-ATTRIBUTES.                                       
142200     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
142300**                                                                        
142400* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
142500* EDITING IS DONE FROM THE BOTTOM UP.                                     
142600**                                                                        
142700     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
142800         ASC-ITEMS-DISPLAYED - 1.                                         
142900     PERFORM 3100-SET-SUBSCRIPTS.                                         
143000     PERFORM 3300-EDIT-SELECTIONS                                         
143100         VARYING PV-SUB                                                   
143200         FROM PV-SUB BY -1                                                
143300         UNTIL PV-SUB < +1.                                               
143400                                                                          
143500                                                                          
143600*----------------------------------------------------------------*        
143700*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
143800*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
143900*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
144000*                                                                *        
144100*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
144200*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
144300*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
144400*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
144500*----------------------------------------------------------------*        
144600                                                                          
144700 3100-SET-SUBSCRIPTS.                                                     
144800                                                                          
144900     COMPUTE ASC-ITEM-SUB =                                               
145000         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
145100                                                                          
145200     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
145300         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
145400                                                                          
145500         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
145600     ELSE                                                                 
145700         MOVE +1                     TO ASC-BLK-SUB                       
145800     END-IF.                                                              
145900*----------------------------------------------------------------*        
146000*  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *        
146100*----------------------------------------------------------------*        
146200                                                                          
146300 3200-RESET-ATTRIBUTES.                                                   
146400     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
146500     MOVE DP015-GREEN              TO ASTRTITC.                           
146600     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
146700                                                                          
146800     PERFORM                                                              
146900         VARYING PV-SUB                                                   
147000         FROM 1 BY 1                                                      
147100         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
147200         MOVE DP015-UNP-ALP-NOR-OFF                                       
147300                                   TO MR-SELECTA (PV-SUB)                 
147400         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
147500         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
147600     END-PERFORM.                                                         
147700*----------------------------------------------------------------*        
147800*  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *        
147900*  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *        
148000*  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *        
148100*  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *        
148200*----------------------------------------------------------------*        
148300 3300-EDIT-SELECTIONS.                                                    
148400     EVALUATE TRUE                                                        
148500         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
148600             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
148700                 CONTINUE                                                 
148800         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
148900             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
149000                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
149100                 MOVE ASC-TEMP-SELECTED-SW                                
149200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
149300                                TO ASC-SELECTED-SW                        
149400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
149500             END-IF                                                       
149600         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
149700                 = SPACE                                                  
149800             EVALUATE TRUE                                                
149900                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
150000                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
150100                     MOVE SPACE TO ASC-SELECTED-SW                        
150200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
150300                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
150400                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
150500                     MOVE SPACE TO ASC-SELECTED-SW                        
150600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
150700                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
150800                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
150900                     MOVE SPACE TO ASC-SELECTED-SW                        
151000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
151100             END-EVALUATE                                                 
151200         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
151300             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
151400                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
151500             END-IF                                                       
151600         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
151700             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
151800                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
151900             END-IF                                                       
152000         WHEN OTHER                                                       
152100             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
152200             SET DP020-MSG-FATAL    TO TRUE                               
152300             MOVE -1                TO MR-SELECTL (PV-SUB)                
152400             SET DP030-SET-CURSOR-APPL-1                                  
152500                                    TO TRUE                               
152600             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
152700             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
152800             SET PS-ERROR TO TRUE                                         
152900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
153000                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
153100                 MOVE SPACE         TO ASC-SELECTED-SW                    
153200                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
153300             END-IF                                                       
153400     END-EVALUATE.                                                        
153500                                                                          
153600     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
153700     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
153800     IF  ASC-ITEM-SUB < 1                                                 
153900         SUBTRACT +1 FROM ASC-BLK-SUB                                     
154000         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
154100     END-IF.                                                              
154200*----------------------------------------------------------------*        
154300*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
154400*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
154500*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
154600*    DISPLAY THE PANEL TO USER.                                  *        
154700*----------------------------------------------------------------*        
154800                                                                          
154900 4000-BUILD-INITIAL-PANEL.                                                
155000                                                                          
155100     INITIALIZE                 ASC-BLOCK-ENTRIES (1)                     
155200                                ASC-BLOCK-ENTRIES (2).                    
155300     MOVE +1                 TO ASC-BLK-SUB.                              
155400                                                                          
155500     SET ASC-ITEMS-LEFT-ON-DB                                             
155600                             TO TRUE.                                     
155700                                                                          
155800     MOVE +0                 TO ASC-ITEM-SUB                              
155900                                ASC-NUMBER-OF-ITEMS-READ                  
156000                                ASC-NUMBER-OF-SELECTED-ITEMS              
156100                                ASC-HIGHEST-BLOCK-WRITTEN                 
156200                                ASC-ITEM-AT-TOP-OF-PANEL.                 
156300                                                                          
156700     PERFORM 4020-OPEN-TEPIRT-CSR.                                        
157100                                                                          
157200     PERFORM 4100-LOAD-BLOCKS-IN-ARRAY.                                   
157300                                                                          
157500     PERFORM 4040-CLOSE-TEPIRT-CSR.                                       
157900                                                                          
158100     IF PS-ERROR                                                          
158200         CONTINUE                                                         
158300     ELSE                                                                 
158400         MOVE +1                   TO  PV-TOP-ITEM                        
158500         IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                              
158600             SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
158700             SET DP020-MSG-FATAL   TO  TRUE                               
158800             MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                   
158900         ELSE                                                             
159000             MOVE -1               TO MR-SELECTL (1)                      
159100             SET DP030-SET-CURSOR-APPL-1                                  
159200                                   TO TRUE                                
159300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
159400         END-IF                                                           
159500     END-IF.                                                              
159600                                                                          
163500*----------------------------------------------------------------*        
163600*  OPEN THE TEPIRT CURSOR (TEPIRT = ITEM RETURN INFO TABLE)      *        
163700*----------------------------------------------------------------*        
163800 4020-OPEN-TEPIRT-CSR.                                                    
163900                                                                          
164000     EXEC SQL                                                             
164100         OPEN TEPIRT-CSR                                                  
164200     END-EXEC.                                                            
164300                                                                          
164400     EVALUATE TRUE                                                        
164500         WHEN SQLCODE = ZERO                                              
164600              CONTINUE                                                    
164700         WHEN SQLWARN0 NOT = SPACE                                        
164800         WHEN SQLCODE NOT = ZERO                                          
164900              MOVE '4020-OPEN-TEPIRT-CSR'                                 
165000                               TO DP013-PARAGRAPH                         
165100              MOVE 'OPEN TEPIRT-CSR CURSOR'                               
165200                               TO DP013-MESSAGE-TEXT (1)                  
165300              MOVE SQLCA       TO DP013-SQLCA                             
165400              SET DP013-DB2-ABEND                                         
165500                               TO TRUE                                    
165600              PERFORM DP013-0000-PROCESS-ABEND                            
165700     END-EVALUATE.                                                        
165800                                                                          
168500*----------------------------------------------------------------*        
168600*  CLOSE THE TEPIRT CURSOR (TEPIRT = ITEM RETURN INFO TABLE)     *        
168700*----------------------------------------------------------------*        
168800 4040-CLOSE-TEPIRT-CSR.                                                   
168900                                                                          
169000     EXEC SQL                                                             
169100         CLOSE TEPIRT-CSR                                                 
169200     END-EXEC.                                                            
169300                                                                          
169400     EVALUATE TRUE                                                        
169500         WHEN SQLCODE = ZERO                                              
169600              CONTINUE                                                    
169700         WHEN SQLWARN0 NOT = SPACE                                        
169800         WHEN SQLCODE NOT = ZERO                                          
169900              MOVE '4040-CLOSE-TEPIRT-CSR'                                
170000                               TO DP013-PARAGRAPH                         
170100              MOVE 'CLOSE TEPIRT-CSR CURSOR'                              
170200                               TO DP013-MESSAGE-TEXT (1)                  
170300              MOVE SQLCA       TO DP013-SQLCA                             
170400              SET DP013-DB2-ABEND                                         
170500                               TO TRUE                                    
170600              PERFORM DP013-0000-PROCESS-ABEND                            
170700     END-EVALUATE.                                                        
170800                                                                          
173400*----------------------------------------------------------------*        
173500*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
173600*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
173700*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
173800*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
173900*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
174000*                                                                *        
174100*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
174200*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
174300*    UPON INITIALIZATION.                                        *        
174400*----------------------------------------------------------------*        
174500                                                                          
174600 4100-LOAD-BLOCKS-IN-ARRAY.                                               
174700                                                                          
174800     PERFORM                                                              
174900        VARYING PV-READ-COUNT                                             
175000         FROM 1 BY 1                                                      
175100          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                           
175200             OR (ASC-NO-ITEMS-LEFT-ON-DB))                                
175300                                                                          
175500         PERFORM 4110-FETCH-TEPIRT-CSR                                    
175900                                                                          
176000         IF  ASC-ITEMS-LEFT-ON-DB                                         
176100                                                                          
176200             MOVE EPIRT-ORIG-SLS-DTE   TO PV-ORIG-SLS-DTE-ISO             
176300             MOVE EPIRT-ORIG-STR-NBR   TO PV-ORIG-STR-NBR                 
176400             MOVE EPIRT-ORIG-RGST-ID   TO PV-ORIG-RGST-ID                 
176500             MOVE EPIRT-ORIG-TRN-NBR   TO PV-ORIG-TRN-NBR                 
176600             MOVE EPIRT-ORIG-STRT-TM(1:2) TO PV-TRANS-TIME-HR             
176700             MOVE EPIRT-ORIG-STRT-TM(4:2) TO PV-TRANS-TIME-MIN            
176800             MOVE PV-TRANS-TIME        TO PV-ORIG-STRT-TM                 
176900                                                                          
177000             PERFORM 4130-GET-PART-NUMBER                                 
177100                                                                          
177200             IF PS-ERROR                                                  
177300                 CONTINUE                                                 
177400             ELSE                                                         
177500                 MOVE 'Y' TO PS-TEPTRN-RECS-AVAIL-SW                      
177700                                                                          
177900                 PERFORM 4160-OPEN-TEPTRN-CSR                             
178000                                                                          
178100                 PERFORM 4200-PROCESS-ORIG-TRAN                           
178200                     UNTIL PS-NO-TEPTRN-RECS-AVAIL                        
178300                                                                          
178400                 PERFORM 4170-CLOSE-TEPTRN-CSR                            
179300             END-IF                                                       
179400         END-IF                                                           
179500     END-PERFORM.                                                         
179600                                                                          
179700     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
179800         MOVE +1               TO ASC-BLK-SUB                             
179900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
180000         ADD +1                TO ASC-BLK-SUB                             
180100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
180200                                                                          
180300         MOVE 1                TO ASC-BLK-SUB                             
180400                                  PV-BLOCK-NUMBER                         
180500         PERFORM 4410-READ-TEMP-STORAGE                                   
180600         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
180700             MOVE 2            TO ASC-BLK-SUB                             
180800                                  PV-BLOCK-NUMBER                         
180900             PERFORM 4410-READ-TEMP-STORAGE                               
181000         END-IF                                                           
181100     END-IF.                                                              
181200                                                                          
181300*----------------------------------------------------------------*        
181400*  FETCH THE TEPIRT CURSOR (TEPIRT = ITEM RETURN INFO TABLE)     *        
181500*----------------------------------------------------------------*        
181600                                                                          
181700 4110-FETCH-TEPIRT-CSR.                                                   
181800                                                                          
181900     EXEC SQL                                                             
182000         FETCH TEPIRT-CSR                                                 
182100         INTO  :EPIRT-ORIG-SLS-DTE                                        
182200              ,:EPIRT-ORIG-STR-NBR                                        
182300              ,:EPIRT-ORIG-RGST-ID                                        
182400              ,:EPIRT-ORIG-TRN-NBR                                        
182500              ,:EPIRT-ORIG-STRT-TM                                        
182600     END-EXEC.                                                            
182700                                                                          
182800     EVALUATE TRUE                                                        
182900         WHEN SQLCODE = ZERO                                              
183000             SET ASC-ITEMS-LEFT-ON-DB TO TRUE                             
183100         WHEN SQLCODE = +100                                              
183200             SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                          
183300         WHEN SQLWARN0 NOT = SPACE                                        
183400         WHEN SQLCODE NOT = ZERO                                          
183500              MOVE '4110-FETCH-TEPIRT-CSR'                                
183600                               TO DP013-PARAGRAPH                         
183700              MOVE 'FETCH ORIG CURSOR '                                   
183800                               TO DP013-MESSAGE-TEXT (1)                  
183900              MOVE SQLCA       TO DP013-SQLCA                             
184000              SET DP013-DB2-ABEND                                         
184100                               TO TRUE                                    
184200              PERFORM DP013-0000-PROCESS-ABEND                            
184300     END-EVALUATE.                                                        
184400                                                                          
187800*----------------------------------------------------------------*        
187900*    GET THE PARTITION NUMBER                                    *        
188200*----------------------------------------------------------------*        
188300                                                                          
188400 4130-GET-PART-NUMBER.                                                    
188500                                                                          
188600     EXEC SQL                                                             
188700         SELECT  PARTN_NBR                                                
188900           INTO :EPPART-PARTN-NBR                                         
189100           FROM  TEPPART                                                  
189200          WHERE  SLS_DTE            = :PV-ORIG-SLS-DTE-ISO                
189300     END-EXEC.                                                            
189400                                                                          
189500     EVALUATE TRUE                                                        
189600         WHEN SQLCODE = ZERO                                              
189700             CONTINUE                                                     
189800         WHEN SQLCODE = +100                                              
189900             SET DP030-OVERRIDE-GO TO TRUE                                
190000             MOVE PC-TSYMSG-03469  TO DP020-MSG-NUMBER                    
190100             SET DP020-MSG-FATAL   TO TRUE                                
190200             SET PS-ERROR          TO TRUE                                
191300         WHEN SQLWARN0 NOT = SPACE                                        
191400         WHEN SQLCODE NOT = ZERO                                          
191500             MOVE '4130-GET-PART-NUMBER'                                  
191600                                   TO DP013-PARAGRAPH                     
191700             MOVE 'GET ORIG PART NUMBER '                                 
191800                                   TO DP013-MESSAGE-TEXT (1)              
191900             MOVE SQLCA            TO DP013-SQLCA                         
192000             SET DP013-DB2-ABEND   TO TRUE                                
192100             PERFORM DP013-0000-PROCESS-ABEND                             
192200     END-EVALUATE.                                                        
192300                                                                          
192400     MOVE EPPART-PARTN-NBR     TO PV-ORIG-PARTN-NBR.                      
192600                                                                          
199100*----------------------------------------------------------------*        
199200*  OPEN THE TEPTRN-CSR                                           *        
199300*----------------------------------------------------------------*        
199400 4160-OPEN-TEPTRN-CSR.                                                    
199500                                                                          
199600     EXEC SQL                                                             
199700         OPEN TEPTRN-CSR                                                  
199800     END-EXEC.                                                            
199900                                                                          
200000     EVALUATE TRUE                                                        
200100         WHEN SQLCODE = ZERO                                              
200200             CONTINUE                                                     
200300         WHEN SQLWARN0 NOT = SPACE                                        
200400         WHEN SQLCODE NOT = ZERO                                          
200500              MOVE '4160-OPEN-TEPTRN-CSR'                                 
200600                               TO DP013-PARAGRAPH                         
200700              MOVE 'OPEN TEPTRN-CSR CURSOR'                               
200800                               TO DP013-MESSAGE-TEXT (1)                  
200900              MOVE SQLCA       TO DP013-SQLCA                             
201000              SET DP013-DB2-ABEND                                         
201100                               TO TRUE                                    
201200              PERFORM DP013-0000-PROCESS-ABEND                            
201300     END-EVALUATE.                                                        
201400                                                                          
201500*----------------------------------------------------------------*        
201600*   CLOSE THE TEPTRN CURSOR                                      *        
201700*----------------------------------------------------------------*        
201800 4170-CLOSE-TEPTRN-CSR.                                                   
201900                                                                          
202000     EXEC SQL                                                             
202100         CLOSE TEPTRN-CSR                                                 
202200     END-EXEC.                                                            
202300                                                                          
202400     EVALUATE TRUE                                                        
202500         WHEN SQLCODE = ZERO                                              
202600              CONTINUE                                                    
202700         WHEN SQLWARN0 NOT = SPACE                                        
202800         WHEN SQLCODE NOT = ZERO                                          
202900              MOVE '4170-CLOSE-TEPTRN-CSR'                                
203000                               TO DP013-PARAGRAPH                         
203100              MOVE 'CLOSE TEPTRN-CSR CURSOR'                              
203200                               TO DP013-MESSAGE-TEXT (1)                  
203300              MOVE SQLCA       TO DP013-SQLCA                             
203400              SET DP013-DB2-ABEND                                         
203500                               TO TRUE                                    
203600              PERFORM DP013-0000-PROCESS-ABEND                            
203700     END-EVALUATE.                                                        
203800                                                                          
208800*----------------------------------------------------------------*        
208900* SIMPLY LOAD THE ORIGINAL TRANSACTIONS, THEN READ THE NEXT               
209000* RECORD ON THE CURSOR.                                                   
209100*----------------------------------------------------------------*        
209200 4200-PROCESS-ORIG-TRAN.                                                  
209300                                                                          
209400     PERFORM 4210-FETCH-TEPTRN-CSR.                                       
209500                                                                          
209600     IF  PS-YES-TEPTRN-RECS-AVAIL                                         
209700         PERFORM 4350-LOAD-SCREEN-DATA                                    
209800     END-IF.                                                              
209900                                                                          
210000*----------------------------------------------------------------*        
210100*  RETRIEVE TRANSACTION LEVEL INFORMATION ON ORIGINAL TRANS      *        
210200*  THROUGH OPERATIONAL TABLE, TO LIST ON SAKCS431 SCREEN AND TO  *        
210300*  PASS ONTO F14 (ITEM INQ) AND F20 (TENDER INQ) SCREENS.        *        
210400*----------------------------------------------------------------*        
210500                                                                          
210600 4210-FETCH-TEPTRN-CSR.                                                   
210700                                                                          
210800     EXEC SQL                                                             
210900         FETCH TEPTRN-CSR                                                 
211000           INTO :EPTRN-SLS-DTE                                            
211100              , :EPTRN-STR-NBR                                            
211200              , :EPTRN-RGST-ID                                            
211300              , :EPTRN-TRN-NBR                                            
211400              , :EPTRN-STRT-TM                                            
211500              , :EPTRN-SLS-CORR-SEQ-NBR                                   
211600              , :EPTRN-TRN-TYP-CDE                                        
211700              , :EPTRN-ASSOC-ID                                           
211800              , :EPTRN-TRN-TOT-AMT                                        
211900              , :EPTRN-SRVR-ONLN-IND                                      
212000              , :EPTRN-VOID-ABRT-CDE                                      
212100              , :EPTRN-TRN-STAT-CDE                                       
212200              , :EPTRN-PRCG-DTE                                           
212300     END-EXEC.                                                            
212400                                                                          
212500     EVALUATE TRUE                                                        
212600         WHEN SQLCODE = ZERO                                              
212700         WHEN SQLCODE = +100                                              
212800         WHEN SQLCODE = -811                                              
212900             CONTINUE                                                     
213000         WHEN SQLWARN0 NOT = SPACE                                        
213100         WHEN SQLCODE NOT = ZERO                                          
213200             MOVE '4210-GET-ORIG-TRAN-INFO'                               
213300                                        TO DP013-PARAGRAPH                
213400             MOVE 'GET ORIG TRAN INFO'  TO DP013-MESSAGE-TEXT (1)         
213500             MOVE SQLCA                 TO DP013-SQLCA                    
213600             SET DP013-DB2-ABEND        TO TRUE                           
213700             PERFORM DP013-0000-PROCESS-ABEND                             
213800     END-EVALUATE.                                                        
213900                                                                          
214000     EVALUATE TRUE                                                        
214100         WHEN SQLCODE = ZERO                                              
214200             MOVE 'Y' TO PS-TEPTRN-RECS-AVAIL-SW                          
214300         WHEN SQLCODE = +100                                              
214400             MOVE 'N' TO PS-TEPTRN-RECS-AVAIL-SW                          
214500         WHEN SQLCODE = -811                                              
214600             MOVE EPTRN-SLS-DTE         TO PV-DISP-SLS-DTE                
214700             MOVE EPTRN-STR-NBR         TO PV-DISP-STR                    
214800             MOVE EPTRN-RGST-ID         TO PV-DISP-RGST-ID                
214900             MOVE EPTRN-TRN-NBR         TO PV-DISP-TRN-NBR                
215000             MOVE '4210-GET-ORIG-TRAN-INFO'                               
215100                                        TO DP013-PARAGRAPH                
215200             MOVE PV-DISP-ERR-INFO      TO DP013-MESSAGE-TEXT (1)         
215300             MOVE SQLCA                 TO DP013-SQLCA                    
215400             SET DP013-DB2-ABEND        TO TRUE                           
215500             PERFORM DP013-0000-PROCESS-ABEND                             
215600     END-EVALUATE.                                                        
215700                                                                          
223100*----------------------------------------------------------------*        
223200* COMMON AREA TO SET UP THE BLOCKS IN THE ARRAY.                 *        
223300*----------------------------------------------------------------*        
223400                                                                          
223500 4350-LOAD-SCREEN-DATA.                                                   
223600                                                                          
223700     IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                                
223800         ADD +1            TO ASC-ITEM-SUB                                
223900     ELSE                                                                 
224000         ADD +1            TO ASC-BLK-SUB                                 
224100         MOVE +1           TO ASC-ITEM-SUB                                
224200                                                                          
224300         IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                         
224400             MOVE +1       TO ASC-BLK-SUB                                 
224500             SET ASC-BLOCK-MODIFIED (1)                                   
224600                           TO TRUE                                        
224700                                                                          
224800             PERFORM 5100-WRITE-TEMP-STORAGE                              
224900                                                                          
225000             MOVE ASC-BLOCK-ENTRIES (PC-MAX-BLOCKS-IN-ARRAY)              
225100                           TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)              
225200             ADD +1        TO ASC-BLK-SUB                                 
225300             INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                   
225400             SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                      
225500                           TO TRUE                                        
225600         END-IF                                                           
225700     END-IF.                                                              
225800                                                                          
225900     ADD +1                TO ASC-NUMBER-OF-ITEMS-READ.                   
226000                                                                          
226100     MOVE ASC-NUMBER-OF-ITEMS-READ                                        
226200                           TO ASC-LIST-ITEM-NUMBER                        
226300                              (ASC-BLK-SUB ASC-ITEM-SUB).                 
226400                                                                          
226500     MOVE PV-ORIG-PARTN-NBR TO ASC-PARTITION-NBR                          
226600                              (ASC-BLK-SUB ASC-ITEM-SUB).                 
226700                                                                          
226800     MOVE EPTRN-SLS-DTE (1:4)                                             
226900                           TO PV-SLS-DTE-FORMATTED-YYYY.                  
227000                                                                          
227100     MOVE EPTRN-SLS-DTE (6:2)                                             
227200                           TO PV-SLS-DTE-FORMATTED-MM.                    
227300                                                                          
227400     MOVE EPTRN-SLS-DTE (9:2)                                             
227500                           TO PV-SLS-DTE-FORMATTED-DD.                    
227600                                                                          
227700     MOVE PV-SLS-DTE-FORMATTED                                            
227800                           TO ASC-SALES-DATE                              
227900                              (ASC-BLK-SUB ASC-ITEM-SUB).                 
228000                                                                          
228100     MOVE EPTRN-STR-NBR    TO ASC-STORE                                   
228200                              (ASC-BLK-SUB ASC-ITEM-SUB).                 
228300                                                                          
228400     MOVE EPTRN-RGST-ID    TO ASC-REGISTER                                
228500                               (ASC-BLK-SUB ASC-ITEM-SUB).                
228600                                                                          
228700     MOVE EPTRN-TRN-NBR    TO ASC-TRANSACTION                             
228800                               (ASC-BLK-SUB ASC-ITEM-SUB).                
228900                                                                          
229000     MOVE EPTRN-STRT-TM    TO ASC-START-TIME                              
229100                               (ASC-BLK-SUB ASC-ITEM-SUB).                
229200                                                                          
229300     MOVE EPTRN-ASSOC-ID   TO PV-ORIG-ASSOC-ID.                           
229400                                                                          
229500     MOVE PV-ORIG-ASSOC-ID TO ASC-REGISTER-OPERATOR                       
229600                               (ASC-BLK-SUB ASC-ITEM-SUB).                
229700                                                                          
229800     MOVE EPTRN-TRN-TYP-CDE                                               
229900                           TO  ASC-TYPE                                   
230000                               (ASC-BLK-SUB ASC-ITEM-SUB).                
230100                                                                          
230200     MOVE EPTRN-SLS-CORR-SEQ-NBR                                          
230300                           TO ASC-SALES-CORR-SEQ-NBR                      
230400                               (ASC-BLK-SUB ASC-ITEM-SUB).                
230500                                                                          
230600     EVALUATE TRUE                                                        
230700         WHEN EPTRN-SLS-CORR-SEQ-NBR = ZERO                               
230800             MOVE 'POS'    TO ASC-SOURCE-TEXT                             
230900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
231000                                                                          
231100         WHEN EPTRN-SLS-CORR-SEQ-NBR > ZERO                               
231200             MOVE 'SA'     TO ASC-SOURCE-TEXT                             
231300                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
231400                                                                          
231500         WHEN EPTRN-SLS-CORR-SEQ-NBR < ZERO                               
231600             MOVE 'SYS'    TO ASC-SOURCE-TEXT                             
231700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
231800     END-EVALUATE.                                                        
231900                                                                          
232000     MOVE EPTRN-TRN-STAT-CDE                                              
232100                           TO ASC-STATUS                                  
232200                               (ASC-BLK-SUB ASC-ITEM-SUB).                
232300                                                                          
232400     EVALUATE TRUE                                                        
232500         WHEN ASC-STAT-VALID-CORR                                         
232600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
232700             MOVE 'CORR'       TO RF-STATUS-TEXT-MSG                      
232800             MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE                    
232900             MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM                     
233000             MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD                     
233100             MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY                     
233200             MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE                  
233300             MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT                       
233400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
233500                                                                          
233600         WHEN ASC-STAT-VALID-POS                                          
233700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
233800             MOVE 'VALID POS' TO ASC-STATUS-TEXT                          
233900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
234000                                                                          
234100         WHEN ASC-STAT-MIS-TOT                                            
234200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
234300             MOVE 'MISSNG TOTAL' TO ASC-STATUS-TEXT                       
234400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
234500                                                                          
234600         WHEN ASC-STAT-MIS-TND                                            
234700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
234800             MOVE 'MISSNG TENDR' TO ASC-STATUS-TEXT                       
234900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
235000                                                                          
235100         WHEN ASC-STAT-OUT-BAL-ITM                                        
235200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
235300             MOVE 'ITMS NOT BAL' TO ASC-STATUS-TEXT                       
235400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
235500                                                                          
235600         WHEN ASC-STAT-OUT-BAL-TND                                        
235700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
235800             MOVE 'TNDR NOT BAL' TO ASC-STATUS-TEXT                       
235900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
236000                                                                          
236100         WHEN ASC-STAT-NON-NUM-ACT                                        
236200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
236300             MOVE 'ACCT NOT NUM' TO ASC-STATUS-TEXT                       
236400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
236500                                                                          
236600         WHEN ASC-STAT-MIS-ACT                                            
236700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
236800             MOVE 'MISSNG ACT #' TO ASC-STATUS-TEXT                       
236900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
237000                                                                          
237100         WHEN ASC-STAT-INVAL-COR                                          
237200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
237300             MOVE 'INVALID CORR' TO ASC-STATUS-TEXT                       
237400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
237500                                                                          
237600         WHEN ASC-STAT-INVAL-PRE                                          
237700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
237800             MOVE 'INVALD PREFX' TO ASC-STATUS-TEXT                       
237900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
238000                                                                          
238100         WHEN ASC-STAT-INVAL-C-D                                          
238200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
238300             MOVE 'INVLD CK DGT' TO ASC-STATUS-TEXT                       
238400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
238500                                                                          
238600         WHEN ASC-STAT-INVAL-STR                                          
238700                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
238800             MOVE 'INVALD STORE' TO ASC-STATUS-TEXT                       
238900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
239000                                                                          
239100         WHEN ASC-STAT-REVERSAL                                           
239200                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
239300             MOVE 'REVRS'      TO RF-STATUS-TEXT-MSG                      
239400             MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE                    
239500             MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM                     
239600             MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD                     
239700             MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY                     
239800             MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE                  
239900             MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT                       
240000                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
240100                                                                          
240200         WHEN ASC-STAT-LATE-DC                                            
240300                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
240400             MOVE 'LATE '      TO RF-STATUS-TEXT-MSG                      
240500             MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE                    
240600             MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM                     
240700             MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD                     
240800             MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY                     
240900             MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE                  
241000             MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT                       
241100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
241200                                                                          
241300         WHEN ASC-STAT-INVAL-HD-TOT                                       
241400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
241500             MOVE 'HD TOTAL INC' TO ASC-STATUS-TEXT                       
241600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
241700                                                                          
241800         WHEN ASC-STAT-ERROR-GEN                                          
241900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
242000             MOVE 'ERROR IN TRN' TO ASC-STATUS-TEXT                       
242100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
242200                                                                          
242300         WHEN ASC-STAT-INCOMP-DATA                                        
242400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
242500             MOVE 'INCOMPL DATA' TO ASC-STATUS-TEXT                       
242600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
242700                                                                          
242800         WHEN OTHER                                                       
242900             MOVE 'UNKNOWN     ' TO ASC-STATUS-TEXT                       
243000                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
243100     END-EVALUATE.                                                        
243200                                                                          
243300     MOVE EPTRN-VOID-ABRT-CDE                                             
243400                           TO ASC-VOID-CODE                               
243500                               (ASC-BLK-SUB ASC-ITEM-SUB).                
243600                                                                          
243700     EVALUATE TRUE                                                        
243800         WHEN ASC-VOID-NORMAL                                             
243900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
244000             MOVE 'NORMAL TRAN' TO ASC-VOID-CODE-TEXT                     
244100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
244200                                                                          
244300         WHEN ASC-VOID-AFTER                                              
244400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
244500             MOVE 'VOID AFTER' TO ASC-VOID-CODE-TEXT                      
244600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
244700                                                                          
244800         WHEN ASC-VOID-DURING                                             
244900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
245000             MOVE 'VOID DURING' TO ASC-VOID-CODE-TEXT                     
245100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
245200                                                                          
245300         WHEN ASC-VOID-AFTER-DURING                                       
245400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
245500             MOVE 'VOID AFT/DUR' TO ASC-VOID-CODE-TEXT                    
245600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
245700                                                                          
245800         WHEN ASC-VOID-DURING-AFTER                                       
245900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
246000             MOVE 'VOID DUR/AFT' TO ASC-VOID-CODE-TEXT                    
246100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
246200                                                                          
246300         WHEN ASC-VOID-REVERSED                                           
246400                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
246500             MOVE 'REVERSD CORR' TO ASC-VOID-CODE-TEXT                    
246600                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
246700                                                                          
246800         WHEN ASC-VOID-UNCORRECTED                                        
246900                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
247000             MOVE 'UNCORR VOID' TO ASC-VOID-CODE-TEXT                     
247100                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
247200                                                                          
247300         WHEN OTHER                                                       
247400             MOVE 'UNKNOWN    ' TO ASC-VOID-CODE-TEXT                     
247500                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
247600     END-EVALUATE.                                                        
247700                                                                          
247800     MOVE EPTRN-TRN-TOT-AMT     TO ASC-AMOUNT                             
247900                               (ASC-BLK-SUB ASC-ITEM-SUB).                
248000                                                                          
248100     MOVE EPTRN-SRVR-ONLN-IND   TO ASC-ONLINE-IND                         
248200                               (ASC-BLK-SUB ASC-ITEM-SUB).                
248300                                                                          
248400*----------------------------------------------------------------*        
248500* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
248600*----------------------------------------------------------------*        
248700                                                                          
248800 4400-MOVE-COMMAREA-TO-SCREEN.                                            
248900     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
249000         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
249100         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
249200             MOVE +1    TO ASC-BLK-SUB                                    
249300             PERFORM 5100-WRITE-TEMP-STORAGE                              
249400             ADD +1     TO ASC-BLK-SUB                                    
249500             PERFORM 5100-WRITE-TEMP-STORAGE                              
249600             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
249700                 GIVING PV-BLOCK-NUMBER                                   
249800                 REMAINDER PV-REMAINDER                                   
249900             IF  PV-REMAINDER > ZERO                                      
250000                 ADD +1 TO PV-BLOCK-NUMBER                                
250100             END-IF                                                       
250200             MOVE +1    TO ASC-BLK-SUB                                    
250300             PERFORM 4410-READ-TEMP-STORAGE                               
250400             ADD +1     TO ASC-BLK-SUB                                    
250500                           PV-BLOCK-NUMBER                                
250600             PERFORM 4410-READ-TEMP-STORAGE                               
250700     END-IF.                                                              
250800     MOVE +1            TO ASC-BLK-SUB.                                   
250900     COMPUTE ASC-ITEM-SUB =                                               
251000         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
251100         + PC-ITEMS-PER-PANEL).                                           
251200                                                                          
251300     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
251400         ADD +1         TO ASC-BLK-SUB                                    
251500         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
251600     END-IF.                                                              
251700                                                                          
251800     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
251900         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
252000     PERFORM 4420-FORMAT-LIST-LINES                                       
252100         VARYING PV-SUB                                                   
252200         FROM PC-ITEMS-PER-PANEL BY -1                                    
252300         UNTIL PV-SUB < +1.                                               
252400                                                                          
252500     COMPUTE ASC-ITEMS-DISPLAYED =                                        
252600         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
252700     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
252800                                      ASTRTITO.                           
252900     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
253000     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
253100     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
253200                                   TO ASELITO.                            
253300     MOVE ASC-KEY-SALES-DATE-DISPLAY                                      
253400                                   TO AORSLDTO.                           
253500     MOVE ASC-KEY-STORE            TO ASTOREO.                            
253600     MOVE ASC-KEY-REGISTER         TO ARGSTIDO.                           
253700     MOVE ASC-KEY-TRANSACTION      TO ATRNNBRO.                           
253800                                                                          
253900     IF  DP020-MSG-NUMBER-X = SPACE                                       
254000         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
254100*            --- MORE ITEMS TO DISPLAY ----                               
254200             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
254300             SET DP020-MSG-INFORMATIONAL TO TRUE                          
254400         END-IF                                                           
254500     END-IF.                                                              
254600*----------------------------------------------------------------*        
254700*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
254800*----------------------------------------------------------------*        
254900                                                                          
255000 4410-READ-TEMP-STORAGE.                                                  
255100                                                                          
255200     EXEC CICS                                                            
255300         READQ TS                                                         
255400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
255500             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
255600             ITEM  (PV-BLOCK-NUMBER)                                      
255700             RESP  (PV-CICS-RESP)                                         
255800     END-EXEC.                                                            
255900                                                                          
256000     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
256100       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
256200         CONTINUE                                                         
256300     ELSE                                                                 
256400         SET DP013-CICS-ABEND     TO TRUE                                 
256500         SET DP013-NO-ROLLBACK    TO TRUE                                 
256600         MOVE '4410-READ-TEMP-STORAGE'                                    
256700                                  TO DP013-PARAGRAPH                      
256800         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
256900                                  TO DP013-MESSAGE-TEXT(1)                
257000         PERFORM DP013-0000-PROCESS-ABEND                                 
257100     END-IF.                                                              
257200                                                                          
257300     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
257400                                                                          
257500     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
257600         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
257700     END-IF.                                                              
257800                                                                          
257900*----------------------------------------------------------------*        
258000*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
258100*----------------------------------------------------------------*        
258200                                                                          
258300 4420-FORMAT-LIST-LINES.                                                  
258400     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
258500         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
258600         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
258700         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
258800         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
258900                                    MR-ACTION (PV-SUB)                    
259000                                    MR-DATE (PV-SUB)                      
259100                                    MR-STORE-X (PV-SUB)                   
259200                                    MR-REG-X (PV-SUB)                     
259300                                    MR-TRANS-X (PV-SUB)                   
259400                                    MR-VAR-AREA (PV-SUB)                  
259500     ELSE                                                                 
259600         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
259700                                                   > SPACE                
259800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
259900                                 TO MR-SELECTI (PV-SUB)                   
260000         ELSE                                                             
260100             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
260200                                 TO MR-SELECTI (PV-SUB)                   
260300         END-IF                                                           
260400                                                                          
260500         IF  DP030-SET-CURSOR-TRAILER                                     
260600             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
260700                 > SPACE)                                                 
260800               OR (PV-SUB = 1))                                           
260900                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
261000                MOVE -1          TO MR-SELECTL (PV-SUB)                   
261100             END-IF                                                       
261200         END-IF                                                           
261300                                                                          
261400*** THE IF STATEMENT BELOW IS COMMENTED OUT SO THAT THE SCREEN            
261500*** ALWAYS GETS REBUILT.  THIS ALLOWS THE LEFT AND RIGHT                  
261600*** FUNCTION KEYS TO OPERATE CORRECTLY.                                   
261700                                                                          
261800*        IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
261900             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
262000                                   TO MR-ACTION (PV-SUB)                  
262100             MOVE ASC-SALES-DATE (ASC-BLK-SUB ASC-ITEM-SUB)               
262200                                   TO MR-DATE (PV-SUB)                    
262300             MOVE ASC-STORE (ASC-BLK-SUB ASC-ITEM-SUB)                    
262400                                   TO MR-STORE (PV-SUB)                   
262500             MOVE ASC-REGISTER (ASC-BLK-SUB ASC-ITEM-SUB)                 
262600                                   TO MR-REG (PV-SUB)                     
262700             MOVE ASC-TRANSACTION (ASC-BLK-SUB ASC-ITEM-SUB)              
262800                                   TO MR-TRANS (PV-SUB)                   
262900             MOVE SPACES           TO DL-DETAIL-LINE-LEFT                 
263000             IF ASC-LEFT-VIEW                                             
263100                 MOVE DP015-ASK-NOR-OFF                                   
263200                                   TO AVARONA                             
263300                 MOVE DP015-WHITE  TO AVARONC                             
263400                 MOVE 'ON'         TO AVARONO                             
263500                 MOVE ASC-REGISTER-OPERATOR                               
263600                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
263700                                   TO DL-REG-OP-LEFT                      
263800                 MOVE ASC-TYPE (ASC-BLK-SUB ASC-ITEM-SUB)                 
263900                                   TO DL-TYPE-LEFT                        
264000                 MOVE ASC-ONLINE-IND (ASC-BLK-SUB ASC-ITEM-SUB)           
264100                                   TO DL-ONLINE-IND-LEFT                  
264200                 IF ASC-VOID-CODE (ASC-BLK-SUB ASC-ITEM-SUB)              
264300                                   = 'N'                                  
264400                     MOVE 'N'      TO DL-VOID-IND-LEFT                    
264500                 ELSE                                                     
264600                     MOVE 'Y'      TO DL-VOID-IND-LEFT                    
264700                 END-IF                                                   
264800                 MOVE ASC-AMOUNT (ASC-BLK-SUB ASC-ITEM-SUB)               
264900                                   TO DL-TRAN-AMOUNT-LEFT                 
265000                 MOVE DL-DETAIL-LINE-LEFT                                 
265100                                   TO MR-VAR-AREA (PV-SUB)                
265200                 MOVE HL-HEADING-LINE-LEFT                                
265300                                   TO AVARHDRO                            
265400             ELSE                                                         
265500                 MOVE ASC-SOURCE-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)          
265600                                   TO DL-TRAN-SRC-RIGHT                   
265700                 MOVE ASC-STATUS-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)          
265800                                   TO DL-STATUS-RIGHT                     
265900                 MOVE ASC-VOID-CODE-TEXT                                  
266000                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
266100                                   TO DL-VOID-INFO-RIGHT                  
266200                 MOVE DL-DETAIL-LINE-RIGHT                                
266300                                   TO MR-VAR-AREA (PV-SUB)                
266400                 MOVE DP015-ASK-DRK-OFF                                   
266500                                   TO AVARONA                             
266600                 MOVE HL-HEADING-LINE-RIGHT                               
266700                                   TO AVARHDRO                            
266800             END-IF                                                       
266900*        END-IF                                                           
267000     END-IF.                                                              
267100     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
267200     IF  ASC-ITEM-SUB < + 1                                               
267300         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
267400         MOVE +1                 TO ASC-BLK-SUB                           
267500     END-IF.                                                              
267600                                                                          
267700*----------------------------------------------------------------*        
267800* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
267900* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
268000* FOR THE SELECTED ITEMS QUEUE.                                  *        
268100*                                                                *        
268200* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
268300* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
268400* THE ARRAY IS FULL.                                             *        
268500*----------------------------------------------------------------*        
268600                                                                          
268700 5000-PREPARE-SEL-QUEUE.                                                  
268800                                                                          
268900     MOVE +1                   TO  ASC-BLK-SUB.                           
269000     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
269100     ADD +1                    TO  ASC-BLK-SUB.                           
269200     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
269300                                                                          
269400     MOVE ASC-SEL2-QUEUE-NAME   TO  SA004-SEL-QUEUE-NAME.                 
269500     PERFORM 6010-DELETE-SEL-QUEUE.                                       
269600                                                                          
269700     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
269800                                                                          
269900     SET SA001-IDX             TO  1.                                     
270000     MOVE ZERO                 TO SA004-NUMBER-OF-TS-ITEMS.               
270100     INITIALIZE SA001-SELECTED-ITEM-ARRAY.                                
270200                                                                          
270300     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
270400             VARYING PV-ITEM                                              
270500             FROM 1 BY 1                                                  
270600             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
270700                                                                          
270800     IF  SA001-IDX > 1                                                    
270900         PERFORM 5205-WRITE-SEL-QUEUE                                     
271000         ADD +1                TO SA004-NUMBER-OF-TS-ITEMS                
271100     END-IF.                                                              
271200                                                                          
271300*----------------------------------------------------------------*        
271400*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
271500*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
271600*----------------------------------------------------------------*        
271700                                                                          
271800 5100-WRITE-TEMP-STORAGE.                                                 
271900     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
272000                                   TO PV-LIST-ITEM-NUMBER.                
272100     COMPUTE PV-BLOCK-NUMBER =                                            
272200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
272300                                                                          
272400     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
272500     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
272600         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
272700             PERFORM 5105-REWRITE-ASC-LISTQ                               
272800         END-IF                                                           
272900     ELSE                                                                 
273000         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
273100         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
273200         PERFORM 5110-WRITE-ASC-LISTQ                                     
273300         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
273400     END-IF.                                                              
273500                                                                          
273600     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
273700*----------------------------------------------------------------*        
273800*  REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-     *        
273900*  SPECIFIC COMMAREA.                                            *        
274000*----------------------------------------------------------------*        
274100 5105-REWRITE-ASC-LISTQ.                                                  
274200     EXEC CICS                                                            
274300         WRITEQ TS                                                        
274400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
274500             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
274600             ITEM  (PV-BLOCK-NUMBER)                                      
274700             REWRITE                                                      
274800             RESP  (PV-CICS-RESP)                                         
274900     END-EXEC.                                                            
275000                                                                          
275100     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
275200         CONTINUE                                                         
275300     ELSE                                                                 
275400         SET DP013-CICS-ABEND            TO TRUE                          
275500         SET DP013-NO-ROLLBACK           TO TRUE                          
275600         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
275700         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
275800                                         TO DP013-MESSAGE-TEXT(1)         
275900         PERFORM DP013-0000-PROCESS-ABEND                                 
276000     END-IF.                                                              
276100                                                                          
276200*----------------------------------------------------------------*        
276300*  WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-       *        
276400*  SPECIFIC COMMAREA.                                            *        
276500*----------------------------------------------------------------*        
276600 5110-WRITE-ASC-LISTQ.                                                    
276700     EXEC CICS                                                            
276800         WRITEQ TS                                                        
276900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
277000             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
277100             ITEM  (PV-BLOCK-NUMBER)                                      
277200             RESP  (PV-CICS-RESP)                                         
277300     END-EXEC.                                                            
277400     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
277500         SET DP013-CICS-ABEND      TO TRUE                                
277600         SET DP013-NO-ROLLBACK     TO TRUE                                
277700         MOVE '5110-WRITE-ASC-LISTQ'                                      
277800                                   TO DP013-PARAGRAPH                     
277900         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
278000                                   TO DP013-MESSAGE-TEXT(1)               
278100         PERFORM DP013-0000-PROCESS-ABEND                                 
278200     END-IF.                                                              
278300                                                                          
278400*----------------------------------------------------------------*        
278500* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
278600* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
278700*----------------------------------------------------------------*        
278800                                                                          
278900 5200-FILL-SEL-QUEUE-RECORD.                                              
279000                                                                          
279100     PERFORM 6112-READ-LIST-QUEUE.                                        
279200                                                                          
279300     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
279400         PERFORM                                                          
279500         VARYING LQW-IDX                                                  
279600         FROM 1 BY 1                                                      
279700         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
279800           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
279900                                                                          
280000             IF  LQW-SELECT (LQW-IDX)                                     
280100                                                                          
280200***  THE MOVE TO THE SALES DATE VARIABLE IS OK HERE BECAUSE               
280300***  ONLY ONE ITEM CAN BE SELECTED                                        
280400                                                                          
280500                 MOVE LQW-SALES-DATE (LQW-IDX)                            
280600                                   TO SA004-SALES-DATE-DISPLAY            
280700                                      PV-SLS-DTE-FORMATTED                
280800                 MOVE PV-SLS-DTE-FORMATTED-YYYY                           
280900                                   TO PV-SLS-DTE-ISO-YYYY                 
281000                 MOVE PV-SLS-DTE-FORMATTED-MM                             
281100                                   TO PV-SLS-DTE-ISO-MM                   
281200                 MOVE PV-SLS-DTE-FORMATTED-DD                             
281300                                   TO PV-SLS-DTE-ISO-DD                   
281400                 MOVE PV-SLS-DTE-ISO                                      
281500                                   TO SA004-SALES-DATE-ISO                
281600                 MOVE LQW-PARTITION-NBR (LQW-IDX)                         
281700                                   TO SA001-PARTITION (SA001-IDX)         
281800                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
281900                                   TO SA001-ITEM (SA001-IDX)              
282000                 MOVE LQW-STORE (LQW-IDX)                                 
282100                                   TO SA001-STORE (SA001-IDX)             
282200                 MOVE LQW-REGISTER (LQW-IDX)                              
282300                                   TO SA001-REGISTER (SA001-IDX)          
282400                 MOVE LQW-TRANSACTION (LQW-IDX)                           
282500                                   TO SA001-TRANS (SA001-IDX)             
282600                 MOVE LQW-START-TIME (LQW-IDX)                            
282700                                   TO SA001-START-TIME                    
282800                                      (SA001-IDX)                         
282900                 MOVE LQW-REGISTER-OPERATOR (LQW-IDX)                     
283000                                   TO SA001-ID (SA001-IDX)                
283100                 MOVE LQW-TYPE (LQW-IDX)                                  
283200                                   TO SA001-TYPE (SA001-IDX)              
283300                 MOVE LQW-SALES-CORR-SEQ-NBR (LQW-IDX)                    
283400                                   TO SA001-SALES-CORR-SEQ-NBR            
283500                                       (SA001-IDX)                        
283600                 MOVE LQW-VOID-CODE (LQW-IDX)                             
283700                                   TO SA001-VOID-ABRT-CDE                 
283800                                       (SA001-IDX)                        
283900                 MOVE LQW-STATUS (LQW-IDX)                                
284000                                   TO SA001-TRAN-STAT-CDE                 
284100                                       (SA001-IDX)                        
284200                 MOVE SPACE        TO SA001-ACTION-SW (SA001-IDX)         
284300                 SET SA001-UPD-TRAN (SA001-IDX)                           
284400                                   TO TRUE                                
284500                 SET SA001-IDX UP BY 1                                    
284600                 IF  SA001-IDX > SA001-MAX-ITEMS                          
284700                     PERFORM 5205-WRITE-SEL-QUEUE                         
284800                     ADD +1        TO SA004-NUMBER-OF-TS-ITEMS            
284900                     INITIALIZE SA001-SELECTED-ITEM-ARRAY                 
285000                     SET SA001-IDX TO  1                                  
285100                 END-IF                                                   
285200             END-IF                                                       
285300         END-PERFORM                                                      
285400     END-IF.                                                              
285500                                                                          
285600*----------------------------------------------------------------*        
285700*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
285800*----------------------------------------------------------------*        
285900                                                                          
286000 5205-WRITE-SEL-QUEUE.                                                    
286100                                                                          
286200     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
286300                                   TO SA001-SELECTED-ITEMS.               
286400                                                                          
286500     EXEC CICS                                                            
286600          WRITEQ TS                                                       
286700              QUEUE (ASC-SEL2-QUEUE-NAME)                                 
286800              FROM  (SA001-TRANSACTION-SEL-REC)                           
286900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
287000              RESP  (PV-CICS-RESP)                                        
287100     END-EXEC.                                                            
287200                                                                          
287300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
287400         CONTINUE                                                         
287500     ELSE                                                                 
287600         SET DP013-CICS-ABEND      TO TRUE                                
287700         SET DP013-NO-ROLLBACK     TO TRUE                                
287800         MOVE '5205-WRITE-SEL-QUEUE'                                      
287900                                   TO DP013-PARAGRAPH                     
288000         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
288100                                   TO DP013-MESSAGE-TEXT(1)               
288200         PERFORM DP013-0000-PROCESS-ABEND                                 
288300     END-IF.                                                              
288400                                                                          
288500*----------------------------------------------------------------*        
288600*  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *        
288700*  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *        
288800*  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *        
288900*  THE USER.                                                     *        
289000*----------------------------------------------------------------*        
289100 6000-REFRESH-PANEL-FROM-TEMP.                                            
289200     IF  ASC-UPDATE-TSQ                                                   
289300         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
289400         MOVE ZERO                 TO PV-SAVE-ITEM                        
289500                                      PV-SEL-QUEUE-ITEM                   
289600         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
289700                                   TO PV-HOLD-SELECTED-ITEMS              
289800         MOVE +1                   TO PV-ITEMS-PROCESSED                  
289900         PERFORM 6100-UPDATE-TSQ                                          
290000             UNTIL PV-ITEMS-PROCESSED >                                   
290100                                     PV-HOLD-SELECTED-ITEMS               
290200         PERFORM 6010-DELETE-SEL-QUEUE                                    
290300         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
290400                    ASC-BLOCK-ENTRIES (2)                                 
290500     END-IF.                                                              
290600                                                                          
290700*----------------------------------------------------------------*        
290800*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
290900*----------------------------------------------------------------*        
291000 6010-DELETE-SEL-QUEUE.                                                   
291100                                                                          
291200     EXEC CICS                                                            
291300         DELETEQ TS                                                       
291400             QUEUE (ASC-SEL2-QUEUE-NAME)                                  
291500             RESP  (PV-CICS-RESP)                                         
291600     END-EXEC.                                                            
291700                                                                          
291800     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
291900        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
292000         CONTINUE                                                         
292100     ELSE                                                                 
292200         SET DP013-CICS-ABEND TO TRUE                                     
292300         SET DP013-NO-ROLLBACK TO TRUE                                    
292400         MOVE '6010-DELETE-SEL-QUEUE'                                     
292500                        TO DP013-PARAGRAPH                                
292600         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
292700                        TO DP013-MESSAGE-TEXT (1)                         
292800         PERFORM DP013-0000-PROCESS-ABEND                                 
292900     END-IF.                                                              
293000                                                                          
293100*----------------------------------------------------------------*        
293200*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
293300*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
293400*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
293500*    THE TEMP QUEUE.                                             *        
293600*----------------------------------------------------------------*        
293700                                                                          
293800 6100-UPDATE-TSQ.                                                         
293900                                                                          
294000     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
294100     PERFORM 6105-READ-SEL-QUEUE.                                         
294200     PERFORM                                                              
294300         VARYING SA001-IDX                                                
294400         FROM 1 BY 1                                                      
294500         UNTIL ((PV-ITEMS-PROCESSED >                                     
294600                              PV-HOLD-SELECTED-ITEMS)                     
294700         OR (SA001-IDX GREATER THAN SA001-MAX-ITEMS))                     
294800         IF  SA001-ACTION-SW (SA001-IDX) > SPACE                          
294900             PERFORM 6110-PROCESS-ITEMS                                   
295000         END-IF                                                           
295100         ADD +1                TO PV-ITEMS-PROCESSED                      
295200     END-PERFORM.                                                         
295300     PERFORM 6111-WRITE-LIST-QUEUE.                                       
295400*----------------------------------------------------------------*        
295500*    READ THE LIST QUEUE RECORD.                                 *        
295600*----------------------------------------------------------------*        
295700                                                                          
295800 6105-READ-SEL-QUEUE.                                                     
295900                                                                          
296000     EXEC CICS                                                            
296100          READQ TS                                                        
296200              QUEUE (ASC-SEL2-QUEUE-NAME)                                 
296300              INTO  (SA001-TRANSACTION-SEL-REC)                           
296400              ITEM  (PV-SEL-QUEUE-ITEM)                                   
296500              RESP  (PV-CICS-RESP)                                        
296600     END-EXEC.                                                            
296700                                                                          
296800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
296900         CONTINUE                                                         
297000     ELSE                                                                 
297100         SET DP013-CICS-ABEND      TO  TRUE                               
297200         SET DP013-NO-ROLLBACK     TO  TRUE                               
297300         MOVE '6105-READ-SEL-QUEUE'                                       
297400                                   TO  DP013-PARAGRAPH                    
297500         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
297600                                   TO  DP013-MESSAGE-TEXT(1)              
297700         PERFORM DP013-0000-PROCESS-ABEND                                 
297800     END-IF.                                                              
297900                                                                          
298000*----------------------------------------------------------------*        
298100*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
298200*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
298300*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
298400*----------------------------------------------------------------*        
298500                                                                          
298600 6110-PROCESS-ITEMS.                                                      
298700                                                                          
298800     COMPUTE PV-ITEM = 1 + ((SA001-ITEM (SA001-IDX) - 1)                  
298900                     / PC-ITEMS-PER-PANEL).                               
299000                                                                          
299100     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
299200         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
299300             PERFORM 6111-WRITE-LIST-QUEUE                                
299400         END-IF                                                           
299500         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
299600                                                                          
299700         PERFORM 6112-READ-LIST-QUEUE                                     
299800         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
299900             CONTINUE                                                     
300000         END-IF                                                           
300100     END-IF.                                                              
300200                                                                          
300300     COMPUTE PV-SUB = (SA001-ITEM (SA001-IDX)                             
300400                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
300500     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
300600     MOVE LQW-SELECTED-SW (PV-SUB)                                        
300700                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
300800     EVALUATE TRUE                                                        
300900         WHEN SA001-INQ (SA001-IDX)                                       
301000              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
301100                                                                          
301200         WHEN SA001-ERR (SA001-IDX)                                       
301300              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
301400     END-EVALUATE.                                                        
301500                                                                          
301600     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
301700                                                                          
301800*----------------------------------------------------------------*        
301900*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
302000*----------------------------------------------------------------*        
302100                                                                          
302200 6111-WRITE-LIST-QUEUE.                                                   
302300                                                                          
302400     EXEC CICS                                                            
302500         WRITEQ TS                                                        
302600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
302700             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
302800             ITEM  (PV-SAVE-ITEM)                                         
302900             REWRITE                                                      
303000             RESP  (PV-CICS-RESP)                                         
303100     END-EXEC.                                                            
303200                                                                          
303300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
303400         CONTINUE                                                         
303500     ELSE                                                                 
303600         SET DP013-CICS-ABEND  TO  TRUE                                   
303700         SET DP013-NO-ROLLBACK TO  TRUE                                   
303800         MOVE '6111-WRITE-LIST-QUEUE'                                     
303900                               TO  DP013-PARAGRAPH                        
304000         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
304100                               TO  DP013-MESSAGE-TEXT(1)                  
304200         PERFORM DP013-0000-PROCESS-ABEND                                 
304300     END-IF.                                                              
304400                                                                          
304500                                                                          
304600*----------------------------------------------------------------*        
304700*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
304800*----------------------------------------------------------------*        
304900                                                                          
305000 6112-READ-LIST-QUEUE.                                                    
305100                                                                          
305200     EXEC CICS                                                            
305300         READQ TS                                                         
305400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
305500             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
305600             ITEM  (PV-ITEM)                                              
305700             RESP  (PV-CICS-RESP)                                         
305800     END-EXEC.                                                            
305900                                                                          
306000     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
306100         CONTINUE                                                         
306200     ELSE                                                                 
306300         SET DP013-CICS-ABEND        TO TRUE                              
306400         SET DP013-NO-ROLLBACK       TO TRUE                              
306500         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
306600         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
306700                                     TO DP013-MESSAGE-TEXT(1)             
306800         PERFORM DP013-0000-PROCESS-ABEND                                 
306900     END-IF.                                                              
307000                                                                          
307100                                                                          
307200*----------------------------------------------------------------*        
307300*    READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.    *        
307400*----------------------------------------------------------------*        
307500                                                                          
307600 9100-READ-TRAN-SEL-QUEUE.                                                
307700                                                                          
307800     MOVE +1                         TO ASC-TRAN-SEL-ITEM.                
307900     MOVE +1                         TO ASC-TRAN-SEL-SUB.                 
308000                                                                          
308100     EXEC CICS                                                            
308200         READQ TS                                                         
308300             QUEUE (ASC-TRAN-SEL-QUEUE-NAME)                              
308400             INTO  (SA001-TRANSACTION-SEL-REC)                            
308500             ITEM  (ASC-TRAN-SEL-ITEM)                                    
308600             RESP  (PV-CICS-RESP)                                         
308700     END-EXEC.                                                            
308800                                                                          
308900     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
309000         CONTINUE                                                         
309100     ELSE                                                                 
309200         SET DP013-LOGIC-ABEND                                            
309300             DP013-NO-ROLLBACK       TO TRUE                              
309400         MOVE '9100-READ-SEL-QUEUE'  TO DP013-PARAGRAPH                   
309500         MOVE 'ATTEMPTING TO READ THE LIST SELECTION TS QUEUE'            
309600                                     TO DP013-MESSAGE-TEXT (1)            
309700         PERFORM DP013-0000-PROCESS-ABEND                                 
309800     END-IF.                                                              
309900                                                                          
310000                                                                          
310100*----------------------------------------------------------------*        
310200*    WRITE SELECTED ITEM TO TEMP STORAGE QUEUE.                  *        
310300*----------------------------------------------------------------*        
310400                                                                          
310500 9200-REWRITE-SEL-QUEUE.                                                  
310600     EXEC CICS                                                            
310700          WRITEQ TS                                                       
310800              QUEUE   (ASC-TRAN-SEL-QUEUE-NAME)                           
310900              FROM    (SA001-TRANSACTION-SEL-REC)                         
311000              ITEM    (ASC-TRAN-SEL-ITEM)                                 
311100              RESP    (PV-CICS-RESP)                                      
311200              REWRITE                                                     
311300     END-EXEC.                                                            
311400                                                                          
311500     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
311600         SET DP013-CICS-ABEND                                             
311700             DP013-NO-ROLLBACK     TO TRUE                                
311800         MOVE '9200-REWRITE-SEL-QUEUE'                                    
311900                                   TO DP013-PARAGRAPH                     
312000         MOVE 'ATTEMPTING TO WRITE THE LIST SELECTIONS QUEUE'             
312100                                   TO DP013-MESSAGE-TEXT (1)              
312200         PERFORM DP013-0000-PROCESS-ABEND                                 
312300     END-IF.                                                              
312400                                                                          
312500                                                                          
312600*----------------------------------------------------------------*        
312700*    ABEND PROCESSOR MODULE                                      *        
312800*----------------------------------------------------------------*        
312900                                                                          
313000     COPY DPPD013.                                                        
