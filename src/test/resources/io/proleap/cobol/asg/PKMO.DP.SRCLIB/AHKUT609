000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    AHKUT607.                                                 
000300 AUTHOR.        MIKE FRIESS.                                              
000400 INSTALLATION.  KOHLS.                                                    
000500 DATE-WRITTEN   AUG, 2003.                                                
000600 DATE-COMPILED.                                                           
000700                                                                          
000800* ************************************************************** *        
000900* * AHKUT607.  "ARCHIVE HISTORY" PROJECT.  EXTRACT PROGRAM.    * *        
001000* *                                                            * *        
001100* * PURPOSE:                                                   * *        
001200* * THIS PROGRAM CREATES AN EXTRACT FILE FROM: TVEHICL.        * *        
001300* * THIS EXTRACT FILE CONSISTS OF ROWS ELIGIBLE TO BE:         * *        
001400* * 1) LOADED TO THE "ARCHIVE/HISTORY" VERSIONS OF THE TABLES. * *        
001500* * 2) DELETED FROM THE OPERATIONAL VERSIONS OF THE TABLES.    * *        
001600* *                                                            * *        
001700* * PROCESSING OVERVIEW:                                       * *        
001800* * THIS PROGRAM READS A CONTROL FILE TO OBTAIN A DATE         * *        
001810* * TO BE USED IN A "DRIVER" CURSOR. FOR EACH ROW:             * *        
001900* * 1) INCREMENT COUNT OF RECORDS READ.                        * *        
002000* * 2) ARCHIVE THE DATA AS FOLLOWS:                            * *        
002900* *       A) INCREMENT COUNT OF ROW(S) FOUND.                  * *        
003000* *       B) WRITE THESE ROW(S) TO THE EXTRACT FILE.           * *        
003100* *       C) INCREMENT COUNT OF RECORDS WRITTEN.               * *        
003200* *       D) DO NOT REMOVE THE ROW(S) FROM THE TABLE           * *        
003300* *          AT THIS TIME.  THEY WILL BE REMOVED IN OTHER      * *        
003400* *          PROCESSING, AFTER HAVING BEEN LOADED TO THE       * *        
003500* *          ARCHIVE/HISTORY TABLE.                            * *        
003600* * 3) EVERY NTH RECORD (IE: INITIAL SETTING 1000), REPORT     * *        
003700* *    RECORD COUNT AND CURRENT DATE/TIME.  THIS WILL ASSIST   * *        
003800* *    WITH RUN-TIME ESTIMATE OF TIME REMAINING TO COMPLETE.   * *        
003900**************************************************************** *        
004202 ENVIRONMENT DIVISION.                                                    
004203 CONFIGURATION SECTION.                                                   
004204 SOURCE-COMPUTER.        IBM-3090.                                        
004300 OBJECT-COMPUTER.        IBM-3090.                                        
004400                                                                          
004500 INPUT-OUTPUT SECTION.                                                    
004600 FILE-CONTROL.                                                            
004700                                                                          
004800     SELECT IN-CONTROL-FILE               ASSIGN TO INP01.                
004900     SELECT OUT-EXTRACT-FILE              ASSIGN TO OUT01.                
005000                                                                          
005100******************************************************************        
005200 DATA DIVISION.                                                           
005300 FILE SECTION.                                                            
005400                                                                          
005500**** AHCC601                                                              
005600 FD  IN-CONTROL-FILE                                                      
005700     RECORDING MODE IS F                                                  
005800     LABEL RECORDS ARE STANDARD                                           
005900     BLOCK CONTAINS 0 RECORDS.                                            
005910 01  IN-CONTROL-RECORD               PIC  X(080).                         
006000                                                                          
006100**** TVEHICL:                                                             
006200 FD  OUT-EXTRACT-FILE                                                     
006300     RECORDING MODE IS F                                                  
006400     LABEL RECORDS ARE STANDARD                                           
006500     BLOCK CONTAINS 0 RECORDS.                                            
006610 01  OUT-EXTRACT-RECORD              PIC  X(150).                         
006900                                                                          
006910                                                                          
007000 WORKING-STORAGE SECTION.                                                 
007100                                                                          
007200 01  PC-PROGRAM-CONSTANTS.                                                
007300     05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'AHKUT607'.          
007400     05  PC-PGM-PROGRESS-DESC.                                            
007500         10  PC-BEGINNING            PIC X(09) VALUE 'BEGINNING'.         
007600         10  PC-CONTINUING           PIC X(10) VALUE                      
007700                                               'CONTINUING'.              
007800         10  PC-ENDING               PIC X(06) VALUE 'ENDING'.            
007900         10  PC-ABENDING             PIC X(08) VALUE 'ABENDING'.          
008000     05  PC-ABEND-INFO.                                                   
008100         10  PC-ABEND                PIC X(11) VALUE                      
008200                                               '** ABEND **'.             
008300         10  PC-ABEND-MSG-PARA       PIC X(15) VALUE                      
008400                                               '- PARA       : '.         
008500         10  PC-ABEND-MSG-DISPLAY-PARA                                    
008600                                     PIC X(15) VALUE                      
008700                                               '  CALLED PARA: '.         
008800         10  PC-ABEND-MSG-NUMBER     PIC X(15) VALUE                      
008900                                               '  FOR ABEND# : '.         
009000         10  PC-ABEND-MSG-DESC       PIC X(15) VALUE                      
009100                                               '- ERROR IS   : '.         
009200         10  PC-ABEND-MSG-INDENT-15  PIC X(15) VALUE SPACES.              
009300         10  PC-ABEND-MSG-ACTION     PIC X(15) VALUE                      
009400                                               '- ACTION     : '.         
009500     05  PC-HOW-OFTEN-TO-DISPLAY     PIC 9(9)  VALUE 5000.                
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-IN-DRIVER-EOF-SW         PIC X(01) VALUE 'N'.                 
009900         88  PS-IN-DRIVER-EOF                  VALUE 'Y'.                 
010000         88  PS-IN-DRIVER-NOT-EOF              VALUE 'N'.                 
010100     05  PS-CURSOR-HAS-DATA-SW       PIC X(01) VALUE 'N'.                 
010200         88  PS-CURSOR-HAS-DATA                VALUE 'Y'.                 
010300         88  PS-CURSOR-HAS-NO-DATA             VALUE 'N'.                 
010400     05  PS-TABLE-LOOP-DONE-SW       PIC X(01) VALUE 'N'.                 
010500         88  PS-TABLE-LOOP-DONE                VALUE 'Y'.                 
010600         88  PS-TABLE-LOOP-NOT-DONE            VALUE 'N'.                 
010700                                                                          
010800 01  PV-PROGRAM-VARIABLES.                                                
010900     05  PV-LENGTH-TVEHICL           PIC 9(09) VALUE ZERO.                
011200     05  PV-RECS-WITH-MATCHES-CNT    PIC  9(9) VALUE 0.                   
011300     05  PV-TABLE-ROW-SELECT-CNT     PIC  9(9) VALUE 0.                   
011400     05  PV-EXTRACT-WRITTEN-CNT      PIC  9(9) VALUE 0.                   
011500                                                                          
011600     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
011700     05  PV-DB2-OPERATION-MSG-1      PIC X(79) VALUE SPACES.              
011800     05  PV-DB2-TABLE-NAME           PIC X(18) VALUE SPACES.              
011900     05  PV-DISPLAY-NTH-MULTIPLE     PIC 9(9)  VALUE 0.                   
012000     05  PV-DISPLAY-NTH-REMAINDER    PIC 9(9)  VALUE 0.                   
012100     05  PV-SQLCODE                  PIC +999  VALUE ZERO.                
012200                                                                          
012300     05  PV-TMST                     PIC X(26) VALUE SPACES.              
012400     05  PV-TIMESTAMP-DETAIL                                              
012500         REDEFINES                                                        
012600         PV-TMST.                                                         
012700         10  PV-TIMESTAMP-19.                                             
012800             15  PV-TMS-DATE.                                             
012900                 20  PV-TMS-YEAR.                                         
013000                     25  PV-TMS-CC   PIC X(02).                           
013100                     25  PV-TMS-YY   PIC X(02).                           
013200                 20  FILLER          PIC X(01).                           
013300                 20  PV-TMS-MONTH    PIC X(02).                           
013400                 20  FILLER          PIC X(01).                           
013500                 20  PV-TMS-DAY      PIC X(02).                           
013600             15  FILLER              PIC X(01).                           
013700             15  PV-TMS-HOUR         PIC X(02).                           
013800             15  FILLER              PIC X(01).                           
013900             15  PV-TMS-MINUTE       PIC X(02).                           
014000             15  FILLER              PIC X(01).                           
014100             15  PV-TMS-SECOND       PIC X(02).                           
014200         10  FILLER                  PIC X(01).                           
014300         10  PV-TMS-MSECOND          PIC X(06).                           
014301                                                                          
014310     05  PV-NULL                     PIC S9(04) VALUE +0                  
014320                                                   COMP.                  
014340     05  PV-CONTROL-RECORD.                                               
014350         10  FILLER                  PIC X(50).                           
014360         10  PV-PURGE-DATE           PIC X(10).                           
014370         10  FILLER                  PIC X(20).                           
014400                                                                          
014500 01  DM-DISPLAY-MESSAGE.                                                  
014600     05  DM-01-10.                                                        
014700         10  DM-PROGRAM              PIC X(08) VALUE SPACE.               
014800         10  FILLER                  PIC X(02) VALUE ': '.                
014900     05  DM-11-80.                                                        
015200         10  FILLER                  PIC X(32) VALUE                      
015300                        'CREATING TVEHICL EXTRACT FILE - '.               
015301         10  FILLER                  PIC X(21) VALUE                      
015310                       'WHERE LAST_UPD_DTE > '.                           
015320         10  DM-PURGE-DATE           PIC X(10) VALUE SPACE.               
015400         10  FILLER                  PIC X(01) VALUE SPACE.               
015500         10  FILLER                  PIC X(01) VALUE ALL '*'.             
015600     05  DM-81-120.                                                       
015700         10  FILLER                  PIC X(30) VALUE ALL '*'.             
015800                                                                          
015900 01  DP-DISPLAY-PROGRESS-MSG.                                             
016000     05  DP-PROGRAM                  PIC X(08) VALUE SPACE.               
016100     05  FILLER                      PIC X(02) VALUE SPACE.               
016200     05  DP-STATUS-DESCRIPTION       PIC X(10) VALUE SPACE.               
016300     05  FILLER                      PIC X(04) VALUE ' AT '.              
016400     05  DP-TIMESTAMP-19             PIC X(19) VALUE SPACE.               
016500     05  FILLER                      PIC X(02) VALUE '. '.                
017600     05  FILLER                      PIC X(14) VALUE                      
017700                                         'ROWS SELECTED:'.                
017800     05  DP-ROWS-SELECTED-CNT        PIC Z,ZZZ,ZZ9                        
017900                                               VALUE ZERO.                
018000     05  FILLER                      PIC X(02) VALUE ', '.                
018100     05  FILLER                      PIC X(04) VALUE                      
018200                                         'OUT:'.                          
018300     05  DP-OUT-CNT                  PIC Z,ZZZ,ZZ9                        
018400                                               VALUE ZERO.                
018401     05  FILLER                      PIC X(02) VALUE ', '.                
018402     05  FILLER                      PIC X(05)                            
018403                                     VALUE 'SCAC:'.                       
018410     05  DP-SCAC                     PIC X(04) VALUE SPACE.               
018500     05  FILLER                      PIC X(02) VALUE ', '.                
018510     05  FILLER                      PIC X(08)                            
018520                                     VALUE 'VEHICLE:'.                    
018530     05  DP-VEH-NBR                  PIC X(10) VALUE SPACE.               
018540     05  FILLER                      PIC X(01) VALUE '.'.                 
018600                                                                          
018700 01  ABEND-CODE                      PIC S9(04)                           
018800                                               VALUE ZEROES               
018900                                               COMP SYNC.                 
019000                                                                          
019100*---------------------------------------------------------------*         
019200*  DB2 FORMATTED MESSAGE AREA                                             
019300*---------------------------------------------------------------*         
019400     COPY DPWS004.                                                        
019500                                                                          
020910*---------------------------------------------------------------*         
020920*    DB2 AREA FOR TVEHICL - VEHICLE TABLE                       *         
020930*---------------------------------------------------------------*         
020940     EXEC SQL                                                             
020950          INCLUDE TVEHICL                                                 
020960     END-EXEC.                                                            
020970                                                                          
020980*---------------------------------------------------------------*         
020990*    DB2 AREA FOR TVHTRIP - VEHICLE TRIP TABLE                  *         
020991*---------------------------------------------------------------*         
020992     EXEC SQL                                                             
020993          INCLUDE TVHTRIP                                                 
020994     END-EXEC.                                                            
020995                                                                          
021000*---------------------------------------------------------------*         
021100*    DB2 AREA FOR COMMUNICATIONS                                *         
021200*---------------------------------------------------------------*         
021300     EXEC SQL                                                             
021400          INCLUDE SQLCA                                                   
021500     END-EXEC.                                                            
021600                                                                          
021700*---------------------------------------------------------------*         
021800*    DEFINE CURSOR FOR TVEHICL :                                *         
021900*    PULL ALL AVAILABLE COLUMNS, SO EXTRACT FILE WILL CONTAIN   *         
022000*    ALL DATA TO POPULATE "ARCHIVE HISTORY" COPY OF THIS TABLE. *         
022100*---------------------------------------------------------------*         
022200     EXEC SQL                                                             
022300          DECLARE TABLE-CURSOR CURSOR FOR                                 
022400           SELECT *                                                       
027502           FROM  TVEHICL VH                                               
027503          WHERE  :PV-PURGE-DATE > VH.LAST_UPD_DTE                         
027504            AND  NOT EXISTS                                               
027505                       (SELECT TRIP_ID                                    
027506                          FROM TVHTRIP TR                                 
027507                         WHERE VH.OWNER_SCAC_ID = TR.OWNER_SCAC_ID        
027508                           AND VH.VEH_NBR = TR.VEH_NBR)                   
027509            FOR  FETCH ONLY                                               
027510           WITH  UR                                                       
027600     END-EXEC.                                                            
027700                                                                          
027800                                                                          
027900 PROCEDURE DIVISION.                                                      
028000********************                                                      
028100                                                                          
028200 0000-PROGRAM-MAINLINE.                                                   
028300                                                                          
028400     PERFORM 1000-INITIALIZE.                                             
028410                                                                          
028500     PERFORM 2000-EXTRACT-ROWS-FROM-TABLE                                 
028600         UNTIL PS-TABLE-LOOP-DONE.                                        
028900                                                                          
029000     PERFORM 6000-EOJ-ROUTINE.                                            
029100     STOP RUN.                                                            
029200                                                                          
029300                                                                          
029400* ************************************************************** *        
029500* * INITIALIZATIONS                                            * *        
029600* ************************************************************** *        
029700 1000-INITIALIZE.                                                         
029800                                                                          
029900     MOVE PC-CURRENT-PROGRAM-NAME TO DM-PROGRAM.                          
030100                                                                          
030200     MOVE PC-BEGINNING              TO DP-STATUS-DESCRIPTION.             
030300     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
030400                                                                          
030401     PERFORM 9000-OPEN-CONTROL-FILE.                                      
030402     PERFORM 9060-READ-CONTROL-FILE.                                      
030403     MOVE PV-PURGE-DATE TO DM-PURGE-DATE.                                 
030404                                                                          
030410     SET PS-CURSOR-HAS-NO-DATA    TO TRUE.                                
030420     PERFORM 9100-OPEN-TABLE-CURSOR.                                      
030600     PERFORM 9700-OPEN-OUT-EXTRACT-FILE.                                  
030700                                                                          
030710     DISPLAY DM-DISPLAY-MESSAGE.                                          
030800                                                                          
030900* ************************************************************** *        
031300* *    1 - LOOP THRU TABLE CURSOR TO EXTRACT ALL               * *        
031400* *        ROWS FOR THIS DATE RANGE                            * *        
031500* *    2 - FOR EACH ROW,                                       * *        
031600* *        A- INCREMENT COUNTS                                 * *        
031700* *        B- WRITE EXTRACT RECORD                             * *        
031900* ************************************************************** *        
031940 2000-EXTRACT-ROWS-FROM-TABLE.                                            
031950                                                                          
031960     INITIALIZE DCLTVEHICL.                                               
031970     PERFORM 9150-FETCH-TABLE-CURSOR.                                     
031980     IF PS-TABLE-LOOP-DONE                                                
031990         CONTINUE                                                         
031991     ELSE                                                                 
031993         MOVE VEHICL-OWNER-SCAC-ID TO DP-SCAC                             
031994         MOVE VEHICL-VEH-NBR       TO DP-VEH-NBR                          
031995         PERFORM 9750-WRITE-EXTRACT-RECORD                                
031996         PERFORM 2100-CHECK-PGM-PROGRESS                                  
031997     END-IF.                                                              
033700                                                                          
033800* ************************************************************** *        
033900* *    EVERY NTH RECORD (IE: INITIAL SETTING 1000), REPORT     * *        
034000* *    RECORD COUNT AND CURRENT DATE/TIME.  THIS WILL ASSIST   * *        
034100* *    WITH RUN-TIME ESTIMATE OF TIME REMAINING TO COMPLETE.   * *        
034200* ************************************************************** *        
034210                                                                          
034300 2100-CHECK-PGM-PROGRESS.                                                 
034310                                                                          
034400     DIVIDE    PV-TABLE-ROW-SELECT-CNT                                    
034500         BY        PC-HOW-OFTEN-TO-DISPLAY                                
034600         GIVING    PV-DISPLAY-NTH-MULTIPLE                                
034700         REMAINDER PV-DISPLAY-NTH-REMAINDER.                              
034800                                                                          
034900     IF PV-DISPLAY-NTH-REMAINDER = ZERO                                   
035000         MOVE PC-CONTINUING         TO DP-STATUS-DESCRIPTION              
035100         PERFORM 6200-DISPLAY-PGM-PROGRESS                                
035200     END-IF.                                                              
035300                                                                          
035400                                                                          
036800* ************************************************************** *        
036900* * DISPLAY END OF RUN MESSAGE, CLOSE THE OUTPUT FILES         * *        
037000* ************************************************************** *        
037100 6000-EOJ-ROUTINE.                                                        
037200                                                                          
037210     PERFORM 9190-CLOSE-TABLE-CURSOR.                                     
037300     PERFORM 9090-CLOSE-CONTROL-FILE.                                     
037400     PERFORM 9790-CLOSE-OUT-EXTRACT-FILE.                                 
037500                                                                          
037600     MOVE PC-ENDING                 TO DP-STATUS-DESCRIPTION.             
037700     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
037800                                                                          
037900                                                                          
038000* ************************************************************** *        
038100* * DISPLAY PROGRAM PROGRESS.                                  * *        
038200* ************************************************************** *        
038300 6200-DISPLAY-PGM-PROGRESS.                                               
038400                                                                          
038500     EXEC SQL                                                             
038600          SET :PV-TMST = CURRENT TIMESTAMP                                
038700     END-EXEC.                                                            
038800                                                                          
038900     MOVE PC-CURRENT-PROGRAM-NAME   TO DP-PROGRAM.                        
039000     MOVE PV-TIMESTAMP-19           TO DP-TIMESTAMP-19.                   
039400     MOVE PV-TABLE-ROW-SELECT-CNT   TO DP-ROWS-SELECTED-CNT.              
039500     MOVE PV-EXTRACT-WRITTEN-CNT    TO DP-OUT-CNT.                        
039600                                                                          
039700     DISPLAY DP-DISPLAY-PROGRESS-MSG.                                     
039800                                                                          
039900                                                                          
040000* ************************************************************** *        
040100* * FILE AND DB2 TABLE ACCESS PARAGRAPHS.                      * *        
040200* * 90XX- INPUT:  DRIVER FILE                                  * *        
040300* * 91XX- INPUT:  TABLE CURSOR                                 * *        
040400* * 97XX- OUTPUT: TABLE EXTRACT                                * *        
040500* ************************************************************** *        
040600                                                                          
040700* ************************************************************** *        
040800* * OPEN  INPUT DRIVER FILE                                    * *        
040900* ************************************************************** *        
041000 9000-OPEN-CONTROL-FILE.                                                  
041100     OPEN  INPUT  IN-CONTROL-FILE.                                        
041200                                                                          
041210* ************************************************************** *        
041220 9060-READ-CONTROL-FILE.                                                  
041230                                                                          
041240     READ IN-CONTROL-FILE                                                 
041250          INTO PV-CONTROL-RECORD                                          
041260          AT END                                                          
041270              MOVE '9060-READ-CONTROL-FILE'                               
041280                                 TO PV-PARAGRAPH-NAME                     
041290              MOVE 'EMPTY CONTROL FILE'                                   
041291                                 TO PV-DB2-OPERATION-MSG-1                
041292              PERFORM 9999-DB2-ABEND.                                     
041300                                                                          
043000* ************************************************************** *        
043100* * CLOSE INPUT DRIVER FILE                                    * *        
043200* ************************************************************** *        
043300 9090-CLOSE-CONTROL-FILE.                                                 
043400     CLOSE IN-CONTROL-FILE.                                               
043500                                                                          
043700* ************************************************************** *        
043800* * OPEN  TABLE CURSOR                                         * *        
043900* ************************************************************** *        
044000 9100-OPEN-TABLE-CURSOR.                                                  
044100     EXEC SQL                                                             
044200          OPEN TABLE-CURSOR                                               
044300     END-EXEC.                                                            
044400                                                                          
044500     EVALUATE TRUE                                                        
044600         WHEN SQLCODE = ZERO                                              
044700              ADD +1 TO PV-RECS-WITH-MATCHES-CNT                          
044800              SET PS-CURSOR-HAS-DATA TO TRUE                              
044900         WHEN SQLCODE = +100                                              
045000              SET PS-TABLE-LOOP-DONE TO TRUE                              
045100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
045200         WHEN SQLCODE  NOT EQUAL ZERO                                     
045300              MOVE '9100-OPEN-TABLE-CURSOR'                               
045400                                 TO PV-PARAGRAPH-NAME                     
045500              MOVE 'OPEN TABLE CURSOR'                                    
045600                                 TO PV-DB2-OPERATION-MSG-1                
045700              MOVE 'TVEHICL'     TO PV-DB2-TABLE-NAME                     
045800              PERFORM 9999-DB2-ABEND                                      
045900     END-EVALUATE.                                                        
046000                                                                          
046100                                                                          
046200* ************************************************************** *        
046300* * FETCH TABLE CURSOR                                         * *        
046400* ************************************************************** *        
046500 9150-FETCH-TABLE-CURSOR.                                                 
046600     EXEC SQL                                                             
046700         FETCH TABLE-CURSOR                                               
046800          INTO  :VEHICL-OWNER-SCAC-ID:PV-NULL                             
046900               ,:VEHICL-VEH-NBR:PV-NULL                                   
047000               ,:VEHICL-CRT-SRC-CDE:PV-NULL                               
047100               ,:VEHICL-LAST-UPD-DTE:PV-NULL                              
047200               ,:VEHICL-LAST-UPD-TME:PV-NULL                              
047300               ,:VEHICL-LAST-UPD-UID:PV-NULL                              
047400               ,:VEHICL-USAGE-CDE:PV-NULL                                 
047500               ,:VEHICL-VEH-TYP-CDE:PV-NULL                               
047600               ,:VEHICL-CURR-STATUS-CDE:PV-NULL                           
047700               ,:VEHICL-CURR-STATUS-DTE:PV-NULL                           
047800               ,:VEHICL-CURR-STATUS-TME:PV-NULL                           
047900               ,:VEHICL-CURR-OPER-UNT-ID:PV-NULL                          
048000               ,:VEHICL-CURR-FCLTY-ID:PV-NULL                             
048100               ,:VEHICL-CURR-FAC-AREA-ID:PV-NULL                          
048200               ,:VEHICL-CURR-LOC-DTE:PV-NULL                              
048300               ,:VEHICL-CURR-LOC-TME:PV-NULL                              
048400               ,:VEHICL-CURR-TRIP-ID:PV-NULL                              
048500               ,:VEHICL-VEH-SIZE-CDE:PV-NULL                              
051902     END-EXEC.                                                            
051903                                                                          
051904     EVALUATE TRUE                                                        
051905         WHEN SQLCODE = ZERO                                              
051907              ADD +1                 TO PV-TABLE-ROW-SELECT-CNT           
051908              SET PS-CURSOR-HAS-DATA TO TRUE                              
052000         WHEN SQLCODE = +100                                              
052100              SET PS-TABLE-LOOP-DONE TO TRUE                              
052200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
052300         WHEN SQLCODE NOT EQUAL ZERO                                      
052400              MOVE '9150-FETCH-TABLE-CURSOR'                              
052500                                 TO PV-PARAGRAPH-NAME                     
052600              MOVE 'FETCH TABLE CURSOR'                                   
052700                                 TO PV-DB2-OPERATION-MSG-1                
052800              MOVE 'TVEHICL'     TO PV-DB2-TABLE-NAME                     
052900              PERFORM 9999-DB2-ABEND                                      
053000     END-EVALUATE.                                                        
053100                                                                          
053200                                                                          
053300* ************************************************************** *        
053400* * CLOSE TABLE CURSOR                                         * *        
053500* ************************************************************** *        
053600 9190-CLOSE-TABLE-CURSOR.                                                 
053700     EXEC SQL                                                             
053800          CLOSE TABLE-CURSOR                                              
053900     END-EXEC.                                                            
054000                                                                          
054100     EVALUATE TRUE                                                        
054200         WHEN SQLCODE = ZERO                                              
054300              CONTINUE                                                    
054400         WHEN SQLWARN0 NOT EQUAL SPACE                                    
054500         WHEN SQLCODE NOT EQUAL ZERO                                      
054600              MOVE '9190-CLOSE-TABLE-CURSOR'                              
054700                                 TO PV-PARAGRAPH-NAME                     
054800              MOVE 'CLOSE TABLE CURSOR'                                   
054900                                 TO PV-DB2-OPERATION-MSG-1                
055000              MOVE 'TVEHICL'     TO PV-DB2-TABLE-NAME                     
055100              PERFORM 9999-DB2-ABEND                                      
055200     END-EVALUATE.                                                        
055300                                                                          
055400                                                                          
055500* ************************************************************** *        
055600* * OPEN OUTPUT EXTRACT FILE                                   * *        
055700* ************************************************************** *        
055800 9700-OPEN-OUT-EXTRACT-FILE.                                              
055900     OPEN OUTPUT OUT-EXTRACT-FILE.                                        
056000                                                                          
056100                                                                          
056200* ************************************************************** *        
056300* * WRITE A RECORD TO THE TABLE EXTRACT FILE                   * *        
056400* ************************************************************** *        
056500 9750-WRITE-EXTRACT-RECORD.                                               
056510     MOVE SPACES TO OUT-EXTRACT-RECORD                                    
056520     MOVE DCLTVEHICL TO OUT-EXTRACT-RECORD                                
056600     WRITE  OUT-EXTRACT-RECORD                                            
056700     ADD +1 TO PV-EXTRACT-WRITTEN-CNT.                                    
056800                                                                          
056900                                                                          
057000* ************************************************************** *        
057100* * CLOSE OUTPUT EXTRACT FILE                                  * *        
057200* ************************************************************** *        
057300 9790-CLOSE-OUT-EXTRACT-FILE.                                             
057400     CLOSE OUT-EXTRACT-FILE.                                              
057500                                                                          
057600                                                                          
057700* ************************************************************** *        
057800* * DISPLAY INFORMATION NEEDED FOR DEBUGGING/MAINTENANCE/SUPPORT *        
057900* * AND CALL THE ABEND PROCESSOR IN THE EVENT THAT AN          * *        
058000* * SQL ERROR OR WARNING CODE OCCURS.                          * *        
058100* ************************************************************** *        
058200 9999-DB2-ABEND.                                                          
058300                                                                          
058400     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058500             ' 9999-DB2-ABEND'.                                           
058600     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058700             ' ** ABEND           **'.                                    
058800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058900             ' ** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.               
059000     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059100             ' ** OWNER SCAC ID  ** =  ' DP-SCAC.                         
059101     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059102             ' ** VEHICLE NUMBER  ** = ' DP-VEH-NBR.                      
059110     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059120             ' ** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME.               
059200     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059300             ' ** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.          
059400                                                                          
059500     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059600             ' ** SQLCODE         ** = ' SQLCODE.                         
059700     MOVE SQLCODE TO PV-SQLCODE.                                          
059800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059900             ' ** PV-SQLCODE      ** = ' PV-SQLCODE '.'.                  
060000                                                                          
060100     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
060200             ' ** SQLWARN0        ** = ' SQLWARN0 '.'.                    
060300                                                                          
060400                                                                          
060500*----------------------------------------------------------------*        
060600* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
060700* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
060800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
060900* FORMAT.                                                        *        
061000*----------------------------------------------------------------*        
061100     COPY DPPD004.                                                        
061200                                                                          
061300     MOVE PC-ABENDING               TO DP-STATUS-DESCRIPTION.             
061400     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
061500     PERFORM 9090-CLOSE-CONTROL-FILE.                                     
061600     PERFORM 9790-CLOSE-OUT-EXTRACT-FILE.                                 
061700                                                                          
061800*----------------------------------------------------------------*        
061900* CALL ABEND                                                     *        
062000*----------------------------------------------------------------*        
062100     MOVE +4013                  TO ABEND-CODE.                           
062200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
062300*                                                                         
