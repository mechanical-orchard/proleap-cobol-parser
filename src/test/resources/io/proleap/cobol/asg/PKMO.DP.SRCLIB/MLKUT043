       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT043.                                                 
       AUTHOR.        DAN GRUBER.                                               
       DATE-WRITTEN.  12-06-96.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ************************************************************              
      **  THIS PROGRAM WILL READ IN THE FILE CREATED IN JOB      *              
      **  BPK115 WHICH HAS BEEN SORTED IN STORE/DEPARTMENT       *              
      **  ORDER.  THE PROGRAM WILL USE THE DATA TO CREATE FOUR   *              
      **  OUTPUT FILES IN EXCEL FORMAT.  THE FOUR FILES WILL     *              
      **  CONTAIN PURCHASES AT RETAIL, PURCHASES AT COST, NET    *              
      **  TRANSFERS, AND MARKDOWNS.                              *              
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      **  12/08/98 - PAUL KEBER - STRATAGEM   WR#:5701                          
      **  THIS PROGRAM WILL NOW READ IN THE TMNSCLS DEPARTMENT/                 
      **  STORE EXTRACT FILE WHICH HAS BEEN SORTED IN STORE/                    
      **  DEPARTMENT ORDER.  A STORE CURSOR HAS BEEN ADDED IN                   
      **  ORDER TO RETRIEVE ALL VALID STORE/DEPARTMENT                          
      **  COMBINATIONS.  OUTPUT RECORDS CONTAINING ZERO AMOUNTS                 
      **  WILL BE CREATED FOR STORE/DEPARTMENT COMBINATIONS                     
      **  MISSING FROM THE TMNSCLS EXTRACT FILE.  A NEW FIFTH                   
      **  OUTPUT FILE IN EXCEL FORMAT WILL CONTAIN ENDING                       
      **  INVENTORY RETAIL.                                                     
      **  01/12/99 - AMY LAWSON -                                               
      **  CHANGED WHERE CLAUSE ON STORES-CSR.  IT WAS EXCLUDING                 
      **  STORES THAT FINANCE NEEDED ON EXTRACT.                                
      *                                                                         
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
INC606* 2444606 11-25-2012 COGNIZANT   INCREASED PC-MAX-DEPTS TO 500.           
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-EXTRACT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EXCEL-PR-FILE                                                 
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT EXCEL-PC-FILE                                                 
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT EXCEL-NT-FILE                                                 
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT EXCEL-MD-FILE                                                 
               ASSIGN TO OUT04.                                                 
                                                                                
           SELECT EXCEL-IR-FILE                                                 
               ASSIGN TO OUT05.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-EXTRACT-FILE                                                   
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS  0 RECORDS                                            
           RECORD CONTAINS 85 CHARACTERS                                        
           DATA RECORD IS INPUT-EXTRACT-RECORD.                                 
                                                                                
       01  INPUT-EXTRACT-RECORD          PIC X(85).                             
                                                                                
       FD  EXCEL-PR-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26603     RECORD CONTAINS 4205 CHARACTERS                                      
           DATA RECORD IS EXCEL-PR-RECORD.                                      
                                                                                
W26603 01  EXCEL-PR-RECORD               PIC X(4205).                           
                                                                                
       FD  EXCEL-PC-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26603     RECORD CONTAINS 4205 CHARACTERS                                      
           DATA RECORD IS EXCEL-PC-RECORD.                                      
                                                                                
W26603 01  EXCEL-PC-RECORD               PIC X(4205).                           
                                                                                
       FD  EXCEL-NT-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26603     RECORD CONTAINS 4205 CHARACTERS                                      
           DATA RECORD IS EXCEL-NT-RECORD.                                      
                                                                                
W26603 01  EXCEL-NT-RECORD               PIC X(4205).                           
                                                                                
       FD  EXCEL-MD-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26603     RECORD CONTAINS 4205 CHARACTERS                                      
           DATA RECORD IS EXCEL-MD-RECORD.                                      
                                                                                
W26603 01  EXCEL-MD-RECORD               PIC X(4205).                           
                                                                                
       FD  EXCEL-IR-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26603     RECORD CONTAINS 4205 CHARACTERS                                      
           DATA RECORD IS EXCEL-IR-RECORD.                                      
                                                                                
W26603 01  EXCEL-IR-RECORD               PIC X(4205).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
INC606     05  PC-MAX-DEPTS              PIC  9(3)   VALUE 500.                 
           05  PC-MAX-RETRIES            PIC S9(01)  VALUE +3.                  
                                                                                
       01  ABEND-CODE                    PIC S9(4)   VALUE +0.                  
           88  ABEND-4004-TABLE-ERROR                VALUE +4004.               
                                                                                
       01  PV-HEADER-RECORD.                                                    
           05  FILLER                    PIC X(6)    VALUE                      
               'STORES'.                                                        
           05  FILLER                    PIC X       VALUE ','.                 
           05  FILLER                    PIC X(11)   VALUE                      
               'DEPARTMENTS'.                                                   
                                                                                
       01  PV-DEPT-HEADER-RECORD.                                               
           05  FILLER                    PIC X       VALUE ','.                 
INC606     05  PV-DEPT-COLUMNS OCCURS 500 TIMES.                                
               10  PV-DEPARTMENT-NUMBER  PIC X(3)      VALUE SPACES.            
               10  FILLER                PIC X         VALUE ','.               
                                                                                
       01  PV-STORE-DEPT-PR-TABLE.                                              
W26603     05  PV-STORE-NUMBER-PR    PIC X(4)    VALUE SPACES.                  
           05  FILLER                PIC X       VALUE ','.                     
INC606     05  PV-DEPT-SALES-PR-TABLE OCCURS 500 TIMES.                         
               10  PV-DEPT-PR-X      PIC X(13)         VALUE LOW-VALUES.        
               10  PV-DEPT-PR  REDEFINES PV-DEPT-PR-X                           
                                     PIC +999999999.99.                         
               10  FILLER            PIC X       VALUE ','.                     
                                                                                
       01  PV-STORE-DEPT-PC-TABLE.                                              
W26603     05  PV-STORE-NUMBER-PC    PIC X(4)    VALUE SPACES.                  
           05  FILLER                PIC X       VALUE ','.                     
INC606     05  PV-DEPT-SALES-PC-TABLE OCCURS 500 TIMES.                         
               10  PV-DEPT-PC-X      PIC X(13)         VALUE LOW-VALUES.        
               10  PV-DEPT-PC  REDEFINES PV-DEPT-PC-X                           
                                     PIC +999999999.99.                         
               10  FILLER            PIC X       VALUE ','.                     
                                                                                
       01  PV-STORE-DEPT-NT-TABLE.                                              
W26603     05  PV-STORE-NUMBER-NT    PIC X(4)    VALUE SPACES.                  
           05  FILLER                PIC X       VALUE ','.                     
INC606     05  PV-DEPT-SALES-NT-TABLE OCCURS 500 TIMES.                         
               10  PV-DEPT-NT-X      PIC X(13)         VALUE LOW-VALUES.        
               10  PV-DEPT-NT  REDEFINES PV-DEPT-NT-X                           
                                     PIC +999999999.99.                         
               10  FILLER            PIC X       VALUE ','.                     
                                                                                
       01  PV-STORE-DEPT-MD-TABLE.                                              
W26603     05  PV-STORE-NUMBER-MD    PIC X(4)    VALUE SPACES.                  
           05  FILLER                PIC X       VALUE ','.                     
INC606     05  PV-DEPT-SALES-MD-TABLE OCCURS 500 TIMES.                         
               10  PV-DEPT-MD-X      PIC X(13)         VALUE LOW-VALUES.        
               10  PV-DEPT-MD  REDEFINES PV-DEPT-MD-X                           
                                     PIC +999999999.99.                         
               10  FILLER            PIC X       VALUE ','.                     
                                                                                
       01  PV-STORE-DEPT-IR-TABLE.                                              
W26603     05  PV-STORE-NUMBER-IR    PIC X(4)    VALUE SPACES.                  
           05  FILLER                PIC X       VALUE ','.                     
INC606     05  PV-DEPT-SALES-IR-TABLE OCCURS 500 TIMES.                         
               10  PV-DEPT-IR-X      PIC X(13)         VALUE LOW-VALUES.        
               10  PV-DEPT-IR  REDEFINES PV-DEPT-IR-X                           
                                     PIC +999999999.99.                         
               10  FILLER            PIC X       VALUE ','.                     
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-DPT-SUB                PIC S9(04)     VALUE +0 COMP.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DEPTS-FOUND            PIC S9(4)      VALUE +0 COMP.          
           05  PV-RETRY-CNT              PIC S9(4)      VALUE +0 COMP.          
           05  PV-STORES-COUNT           PIC S9(4)      VALUE +0 COMP.          
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)      VALUE SPACES.           
           05  PV-FUNCTION               PIC X(30)      VALUE SPACES.           
           05  PV-PARAGRAPH-NAME         PIC X(30)      VALUE SPACES.           
                                                                                
       01  PV-INPUT-STR-NBR              PIC 9(04)      VALUE ZEROES.           
       01  PV-INPUT-STR-NBR-REDEF REDEFINES PV-INPUT-STR-NBR.                   
W26603***  05  FILLER                    PIC X.                                 
W26603     05  PV-INPUT-STR-NBR-X        PIC X(04).                             
                                                                                
W26603 01  PV-XMT00001-LOC-NBR           PIC 9(04)      VALUE ZEROES.           
W26603 01  PV-XMT00001-LOC-NBR-REDEF REDEFINES PV-XMT00001-LOC-NBR.             
W26603***  05  FILLER                    PIC X.                                 
W26603     05  PV-XMT00001-LOC-NBR-X     PIC X(04).                             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DATA-SW         PIC  X(01)    VALUE 'N'.               
               88 PS-END-OF-DATA                       VALUE 'Y'.               
               88 PS-NOT-END-OF-DATA                   VALUE 'N'.               
           05  PS-STORE-CHANGE-SW        PIC  X(01)    VALUE 'N'.               
               88 PS-STORE-CHANGE                      VALUE 'Y'.               
               88 PS-NO-STORE-CHANGE                   VALUE 'N'.               
           05  PS-END-OF-DEPARTMENTS-CSR PIC  X(01)    VALUE 'N'.               
               88  END-OF-DEPARTMENTS-CSR              VALUE 'Y'.               
           05  PS-END-OF-STORES-CSR      PIC  X(01)    VALUE 'N'.               
               88  END-OF-STORES-CSR                   VALUE 'Y'.               
                                                                                
       EJECT                                                                    
      *************************************************************             
      *    COPYBOOK FOR THIS MONTH MERCH LEDGER EXTRACT RECORD.   *             
      *    THIS IS THE INPUT FILE FOR THIS PROGRAM.               *             
      *************************************************************             
                                                                                
           COPY MLRD117.                                                        
                                                                                
       EJECT                                                                    
      *================================================================*        
      *  DB2 - AREA                                                    *        
      *================================================================*        
      ************************************************************              
      *  ERROR MESSAGE AREA FOR DB2                                             
      ************************************************************              
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    DB2 COMMUNICATION AREA2                                              
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
W26603*    DB2 DCLGEN FOR XMT_LOC - STORE MASTER TABLE                          
      ******************************************************************        
           EXEC SQL                                                             
W26603         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DB2 DCLGEN FOR TMDSEAR - MERCHANDISE AREA TABLE                      
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *  GET ALL VALID DEPARTMENT ROWS                                          
      ************************************************************              
                                                                                
           EXEC SQL                                                             
               DECLARE DEPARTMENTS-CSR CURSOR FOR                               
                SELECT DEPT_NBR                                                 
                   FROM TMDSEAR                                                 
                  WHERE OPERCO_NBR = '03'                                       
                    AND MDSELV_CDE = 'DPT'                                      
                    AND CURRENT DATE BETWEEN STRT_DTE AND END_DTE               
                  ORDER BY DEPT_NBR                                             
                  FOR FETCH ONLY                                                
W26603            WITH UR                                                       
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *  GET ALL VALID STORE ROWS                                               
      ************************************************************              
                                                                                
           EXEC SQL                                                             
               DECLARE STORES-CSR CURSOR FOR                                    
W26603          SELECT LOC_NBR                                                  
W26603             FROM XMT_LOC                                                 
W26603          WHERE LOC_NBR <> 000                                            
W26603            ORDER BY LOC_NBR                                              
                  FOR FETCH ONLY                                                
W26603            WITH UR                                                       
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
      ************************************************************              
       PROCEDURE DIVISION.                                                      
      ************************************************************              
                                                                                
       1000-MAINLINE.                                                           
      *****************************************************************         
      *  READS EXTRACTED MERCHANDISE LEDGER INFORMATION FOR EACH                
      *  STORE/DEPT AND ACCUMULATES THE PURCHASES AT RETAIL, PURCHASES          
      *  AT COST, NET TRANSFERS, AND MARKDOWNS FOR EACH DEPARTMENT.             
      *  ONE ROW IS WRITTEN TO EACH EXTRACT FILE WHEN A CHANGE IN               
      *  STORE OCCURS.  THE EXTRACT FILE IS WRITTEN IN A MATRIX FORMAT          
      *  WITH STORES GOING DOWN AND DEPARTMENTS ACROSS.                         
      *****************************************************************         
                                                                                
           PERFORM 2000-INITIALIZATION.                                         
                                                                                
           PERFORM 3000-PROCESS-INPUT-EXTRACT                                   
               UNTIL END-OF-STORES-CSR                                          
                 AND PS-END-OF-DATA.                                            
                                                                                
           PERFORM 8000-CLOSE-FILES.                                            
                                                                                
      *****************************************************************         
      *  END OF PROGRAM EXECTION                                                
      *****************************************************************         
                                                                                
           GOBACK.                                                              
                                                                                
      *****************************************************************         
      *  OPEN FILES, WRITE HEADER RECORDS, AND READ 1ST INPUT RECORD.           
      *****************************************************************         
       2000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  INPUT-EXTRACT-FILE                                       
                OUTPUT EXCEL-PR-FILE                                            
                OUTPUT EXCEL-PC-FILE                                            
                OUTPUT EXCEL-NT-FILE                                            
                OUTPUT EXCEL-MD-FILE                                            
                OUTPUT EXCEL-IR-FILE.                                           
                                                                                
           WRITE EXCEL-PR-RECORD                                                
               FROM PV-HEADER-RECORD.                                           
                                                                                
           WRITE EXCEL-PC-RECORD                                                
               FROM PV-HEADER-RECORD.                                           
                                                                                
           WRITE EXCEL-NT-RECORD                                                
               FROM PV-HEADER-RECORD.                                           
                                                                                
           WRITE EXCEL-MD-RECORD                                                
               FROM PV-HEADER-RECORD.                                           
                                                                                
           WRITE EXCEL-IR-RECORD                                                
               FROM PV-HEADER-RECORD.                                           
                                                                                
           PERFORM 2100-CREATE-DEPT-HEADERS.                                    
                                                                                
           PERFORM 7000-READ-INPUT-FILE.                                        
                                                                                
           PERFORM 6000-OPEN-STORES-CSR.                                        
           PERFORM 6100-FETCH-STORES-CSR.                                       
                                                                                
      *****************************************************************         
      *    BUILD A DEPARTMENT HEADER RECORD FOR EACH OF THE EXTRACT             
      *    FILES                                                                
      *****************************************************************         
       2100-CREATE-DEPT-HEADERS.                                                
                                                                                
           PERFORM 6300-OPEN-DEPARTMENTS-CSR.                                   
           PERFORM 6400-FETCH-DEPARTMENTS-CSR.                                  
                                                                                
           PERFORM 2150-FILL-DEPT-HEADER                                        
               UNTIL END-OF-DEPARTMENTS-CSR.                                    
                                                                                
           PERFORM 6500-CLOSE-DEPARTMENTS-CSR.                                  
                                                                                
           MOVE PS-DPT-SUB TO PV-DEPTS-FOUND.                                   
                                                                                
           WRITE EXCEL-PR-RECORD                                                
               FROM PV-DEPT-HEADER-RECORD.                                      
                                                                                
           WRITE EXCEL-PC-RECORD                                                
               FROM PV-DEPT-HEADER-RECORD.                                      
                                                                                
           WRITE EXCEL-NT-RECORD                                                
               FROM PV-DEPT-HEADER-RECORD.                                      
                                                                                
           WRITE EXCEL-MD-RECORD                                                
               FROM PV-DEPT-HEADER-RECORD.                                      
                                                                                
           WRITE EXCEL-IR-RECORD                                                
               FROM PV-DEPT-HEADER-RECORD.                                      
                                                                                
      *****************************************************************         
      *    FILL IN THE DEPARTMENT HEADER RECORD DEPARTMENT NUMBERS              
      *****************************************************************         
       2150-FILL-DEPT-HEADER.                                                   
                                                                                
           ADD +1               TO PS-DPT-SUB.                                  
           IF PS-DPT-SUB > PC-MAX-DEPTS                                         
               MOVE '2150-FILL-DEPT-HEADER'                                     
                                TO PV-PARAGRAPH-NAME                            
               MOVE 'TABLE OVERLOAD'                                            
                                TO PV-FUNCTION                                  
               SET ABEND-4004-TABLE-ERROR                                       
                                TO TRUE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE MDSEAR-DEPT-NBR TO PV-DEPARTMENT-NUMBER (PS-DPT-SUB).           
                                                                                
           PERFORM 6400-FETCH-DEPARTMENTS-CSR.                                  
                                                                                
      *****************************************************************         
      *  BUILDS A LINE ON THE OUTPUT FILE THAT HOLDS EACH STORE'S               
      *  INVENTORY INFORMATION BY DEPARTMENT FROM RECORDS ON THE INPUT          
      *  EXTRACT FILE.  ONE RECORD WILL BE BUILT FOR EACH STORE FOUND           
      *  ON THE STORE CURSOR WITH COLUMNS REPRESENTING EACH DEPARTMENT'S        
      *  INVENTORY.                                                             
      *****************************************************************         
       3000-PROCESS-INPUT-EXTRACT.                                              
                                                                                
W26603     IF XMT00001-LOC-NBR = PV-INPUT-STR-NBR                               
               MOVE PV-INPUT-STR-NBR-X TO PV-STORE-NUMBER-PR                    
                                          PV-STORE-NUMBER-PC                    
                                          PV-STORE-NUMBER-NT                    
                                          PV-STORE-NUMBER-MD                    
                                          PV-STORE-NUMBER-IR                    
               SET PS-NO-STORE-CHANGE  TO TRUE                                  
               PERFORM 4000-LOAD-DEPARTMENT-TABLE                               
                   VARYING PS-DPT-SUB                                           
                   FROM 1 BY 1                                                  
                   UNTIL PS-DPT-SUB > PV-DEPTS-FOUND                            
               PERFORM 5100-WRITE-STORE-INFO                                    
               PERFORM 6100-FETCH-STORES-CSR                                    
           ELSE                                                                 
W26603         IF XMT00001-LOC-NBR < PV-INPUT-STR-NBR                           
W26603             MOVE XMT00001-LOC-NBR TO PV-XMT00001-LOC-NBR                 
W26603             MOVE PV-XMT00001-LOC-NBR-X                                   
                                        TO PV-STORE-NUMBER-PR                   
                                           PV-STORE-NUMBER-PC                   
                                           PV-STORE-NUMBER-NT                   
                                           PV-STORE-NUMBER-MD                   
                                           PV-STORE-NUMBER-IR                   
                   PERFORM 4200-LOAD-EMPTY-DEPT-TABLE                           
                       VARYING PS-DPT-SUB                                       
                       FROM 1 BY 1                                              
                       UNTIL PS-DPT-SUB > PV-DEPTS-FOUND                        
                   PERFORM 5100-WRITE-STORE-INFO                                
                   PERFORM 6100-FETCH-STORES-CSR                                
               ELSE                                                             
                   PERFORM 7000-READ-INPUT-FILE                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *  LOAD-DEPARTMENT-TABLE - DATA FOR THE FIVE EXTRACTS IS                  
      *     ACCUMULATED IN VARIABLES FOR A GIVEN DEPARTMENT.  WHEN              
      *     A NEW DEPARTMENT IS READ IN, THE DATA FOR THE PREVIOUS              
      *     DEPARTMENT IS MOVED TO THE TABLE, THE ACCUMULATORS ARE              
      *     ZEROED OUT, AND THE PROCESS CONTINUES UNTIL A CHANGE IN             
      *     STORE OCCURS OR THERE IS NO MORE DATA TO PROCESS.                   
      *****************************************************************         
       4000-LOAD-DEPARTMENT-TABLE.                                              
                                                                                
           IF PS-END-OF-DATA                                                    
             OR PS-STORE-CHANGE                                                 
             OR ML117-DEPT-NBR > PV-DEPARTMENT-NUMBER (PS-DPT-SUB)              
               MOVE ZERO                    TO PV-DEPT-PR (PS-DPT-SUB)          
                                               PV-DEPT-PC (PS-DPT-SUB)          
                                               PV-DEPT-NT (PS-DPT-SUB)          
                                               PV-DEPT-MD (PS-DPT-SUB)          
                                               PV-DEPT-IR (PS-DPT-SUB)          
           ELSE                                                                 
               IF ML117-DEPT-NBR = PV-DEPARTMENT-NUMBER (PS-DPT-SUB)            
                   MOVE ML117-RCPT-NET-RTL-AMT                                  
                                            TO PV-DEPT-PR (PS-DPT-SUB)          
                   MOVE ML117-RCPT-NET-COST-AMT                                 
                                            TO PV-DEPT-PC (PS-DPT-SUB)          
                   MOVE ML117-NET-XFER-RTL-AMT                                  
                                            TO PV-DEPT-NT (PS-DPT-SUB)          
                   MOVE ML117-MKDN-RTL-AMT  TO PV-DEPT-MD (PS-DPT-SUB)          
                   MOVE ML117-END-INV-RTL-AMT                                   
                                            TO PV-DEPT-IR (PS-DPT-SUB)          
               END-IF                                                           
               PERFORM 7000-READ-INPUT-FILE                                     
W26603         IF PV-INPUT-STR-NBR NOT EQUAL XMT00001-LOC-NBR                   
                   SET PS-STORE-CHANGE TO TRUE                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *  LOAD-EMPTY-DEPT-TABLE - DATA FOR THE FIVE EXTRACTS IS                  
      *     CREATED WITH ZERO AMOUNTS FOR ALL DEPARTMENTS.                      
      *****************************************************************         
       4200-LOAD-EMPTY-DEPT-TABLE.                                              
                                                                                
           MOVE ZERO          TO PV-DEPT-PR (PS-DPT-SUB)                        
                                 PV-DEPT-PC (PS-DPT-SUB)                        
                                 PV-DEPT-NT (PS-DPT-SUB)                        
                                 PV-DEPT-MD (PS-DPT-SUB)                        
                                 PV-DEPT-IR (PS-DPT-SUB).                       
                                                                                
      *****************************************************************         
      *  WRITES THE FIVE DIFFERENT STORE RECORDS.  ONE FOR EACH OF              
      *  THE FIVE EXTRACTS CREATED BY THIS PROGRAM.                             
      *****************************************************************         
       5100-WRITE-STORE-INFO.                                                   
                                                                                
           WRITE EXCEL-PR-RECORD                                                
               FROM PV-STORE-DEPT-PR-TABLE.                                     
                                                                                
           WRITE EXCEL-PC-RECORD                                                
               FROM PV-STORE-DEPT-PC-TABLE.                                     
                                                                                
           WRITE EXCEL-NT-RECORD                                                
               FROM PV-STORE-DEPT-NT-TABLE.                                     
                                                                                
           WRITE EXCEL-MD-RECORD                                                
               FROM PV-STORE-DEPT-MD-TABLE.                                     
                                                                                
           WRITE EXCEL-IR-RECORD                                                
               FROM PV-STORE-DEPT-IR-TABLE.                                     
                                                                                
      ******************************************************************        
      *  OPEN STORES CURSOR - TO PROCESS ALL VALID STORES              *        
      ******************************************************************        
       6000-OPEN-STORES-CSR.                                                    
                                                                                
           MOVE ZERO TO PV-RETRY-CNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               UNTIL PV-RETRY-CNT > PC-MAX-RETRIES                              
                  OR SQLCODE = ZERO                                             
                                                                                
               EXEC SQL                                                         
                    OPEN STORES-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                  CONTINUE                                                      
                 WHEN OTHER                                                     
                     MOVE '6000-OPEN-STORES-CSR' TO PV-PARAGRAPH-NAME           
                     MOVE 'OPEN STORES-CSR'    TO PV-DB2-OPER-ATTEMPTED         
                     PERFORM 9000-SQL-ABEND                                     
               END-EVALUATE                                                     
               COMPUTE PV-RETRY-CNT = PV-RETRY-CNT + 1                          
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-CNT > PC-MAX-RETRIES                                    
               MOVE '6000-OPEN-STORES-CSR' TO PV-PARAGRAPH-NAME                 
               MOVE +4008                  TO ABEND-CODE                        
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FETCH ALL VALID STORE ROWS                                    *        
      ******************************************************************        
       6100-FETCH-STORES-CSR.                                                   
                                                                                
           MOVE ZERO TO PV-RETRY-CNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               UNTIL PV-RETRY-CNT > PC-MAX-RETRIES                              
                  OR SQLCODE = 0                                                
                  OR END-OF-STORES-CSR                                          
                                                                                
               EXEC SQL                                                         
                   FETCH STORES-CSR                                             
W26603              INTO :XMT00001-LOC-NBR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                     ADD +1 TO PV-STORES-COUNT                                  
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                     CONTINUE                                                   
                 WHEN SQLCODE = +100                                            
                     SET END-OF-STORES-CSR TO TRUE                              
W26603               MOVE +9999            TO XMT00001-LOC-NBR                  
                 WHEN OTHER                                                     
                     MOVE '6100-FETCH-STORES-CSR' TO PV-PARAGRAPH-NAME          
                     MOVE 'FETCH STORES-CSR'   TO PV-DB2-OPER-ATTEMPTED         
                     PERFORM 9000-SQL-ABEND                                     
               END-EVALUATE                                                     
               COMPUTE PV-RETRY-CNT = PV-RETRY-CNT + 1                          
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-CNT > PC-MAX-RETRIES                                    
               MOVE '6100-FETCH-STORES-CSR' TO PV-PARAGRAPH-NAME                
               MOVE +4013                    TO ABEND-CODE                      
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE MONTH-TO-DATE SUB-CLASS STORE CURSOR                    *        
      ******************************************************************        
       6200-CLOSE-STORES-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               CLOSE STORES-CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 CONTINUE                                                       
             WHEN OTHER                                                         
                 MOVE '6200-CLOSE-STORES-CSR' TO PV-PARAGRAPH-NAME              
                 MOVE 'CLOSE STORES-CSR'      TO PV-DB2-OPER-ATTEMPTED          
                 PERFORM 9000-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  OPEN DEPARTMENTS CURSOR - TO PROCESS ALL VALID DEPARTMENTS    *        
      ******************************************************************        
       6300-OPEN-DEPARTMENTS-CSR.                                               
                                                                                
           MOVE ZERO TO PV-RETRY-CNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               UNTIL PV-RETRY-CNT > PC-MAX-RETRIES                              
                  OR SQLCODE = ZERO                                             
                                                                                
               EXEC SQL                                                         
                    OPEN DEPARTMENTS-CSR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                  CONTINUE                                                      
                 WHEN OTHER                                                     
                     MOVE '6300-OPEN-DEPARTMENTS-CSR'                           
                                           TO PV-PARAGRAPH-NAME                 
                     MOVE 'OPEN DEPARTMENTS-CSR'                                
                                           TO PV-DB2-OPER-ATTEMPTED             
                     PERFORM 9000-SQL-ABEND                                     
               END-EVALUATE                                                     
               COMPUTE PV-RETRY-CNT = PV-RETRY-CNT + 1                          
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-CNT > PC-MAX-RETRIES                                    
               MOVE '6300-OPEN-DEPARTMENTS-CSR'                                 
                                           TO PV-PARAGRAPH-NAME                 
               MOVE +4008                  TO ABEND-CODE                        
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FETCH ALL VALID STORE ROWS                                    *        
      ******************************************************************        
       6400-FETCH-DEPARTMENTS-CSR.                                              
                                                                                
           MOVE ZERO TO PV-RETRY-CNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               UNTIL PV-RETRY-CNT > PC-MAX-RETRIES                              
                  OR SQLCODE = 0                                                
                  OR END-OF-DEPARTMENTS-CSR                                     
                                                                                
               EXEC SQL                                                         
                   FETCH DEPARTMENTS-CSR                                        
                    INTO :MDSEAR-DEPT-NBR                                       
                                                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                     CONTINUE                                                   
                 WHEN SQLCODE = +100                                            
                     SET END-OF-DEPARTMENTS-CSR TO TRUE                         
                 WHEN OTHER                                                     
                     MOVE '6400-FETCH-DEPARTMENTS-CSR'                          
                                           TO PV-PARAGRAPH-NAME                 
                     MOVE 'FETCH DEPARTMENTS-CSR'                               
                                           TO PV-DB2-OPER-ATTEMPTED             
                     PERFORM 9000-SQL-ABEND                                     
               END-EVALUATE                                                     
               COMPUTE PV-RETRY-CNT = PV-RETRY-CNT + 1                          
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-CNT > PC-MAX-RETRIES                                    
               MOVE '6400-FETCH-DEPARTMENTS-CSR'                                
                                           TO PV-PARAGRAPH-NAME                 
               MOVE +4013                  TO ABEND-CODE                        
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE MONTH-TO-DATE SUB-CLASS STORE CURSOR                    *        
      ******************************************************************        
       6500-CLOSE-DEPARTMENTS-CSR.                                              
                                                                                
           EXEC SQL                                                             
               CLOSE DEPARTMENTS-CSR                                            
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 CONTINUE                                                       
             WHEN OTHER                                                         
                 MOVE '6500-CLOSE-DEPARTMENTS-CSR'                              
                                           TO PV-PARAGRAPH-NAME                 
                 MOVE 'CLOSE DEPARTMENTS-CSR'                                   
                                           TO PV-DB2-OPER-ATTEMPTED             
                 PERFORM 9000-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      *  READ THE SORTED INPUT EXTRACT FILE.                                    
      *****************************************************************         
       7000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-EXTRACT-FILE                                              
               INTO ML117-REC                                                   
                   AT END                                                       
                       SET PS-END-OF-DATA TO TRUE                               
                       MOVE 9999          TO PV-INPUT-STR-NBR                   
                   NOT AT END                                                   
                       MOVE ML117-STR-NBR TO PV-INPUT-STR-NBR.                  
                                                                                
      *****************************************************************         
      *  CLOSES FILES.                                                          
      *****************************************************************         
       8000-CLOSE-FILES.                                                        
                                                                                
           PERFORM 6200-CLOSE-STORES-CSR.                                       
                                                                                
           CLOSE EXCEL-PR-FILE                                                  
                 EXCEL-PC-FILE                                                  
                 EXCEL-NT-FILE                                                  
                 EXCEL-MD-FILE                                                  
                 EXCEL-IR-FILE                                                  
                 INPUT-EXTRACT-FILE.                                            
                                                                                
           DISPLAY '*****************************************'.                 
           DISPLAY '*** DEPARTMENTS PROCESSED: ' PV-DEPTS-FOUND.                
           DISPLAY '*** STORES PROCESSED     : ' PV-STORES-COUNT.               
           DISPLAY '*****************************************'.                 
                                                                                
      ******************************************************************        
      *  SQL - DB2 ABEND PROCESSING                                    *        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           DISPLAY '*****************************************'.                 
           DISPLAY '*** DEPARTMENTS PROCESSED: ' PV-DEPTS-FOUND.                
           DISPLAY '*** STORES PROCESSED     : ' PV-STORES-COUNT.               
           DISPLAY '*****************************************'.                 
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKUT043'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    *------------------------------------------------------------        
      *    * THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE               
      *    * DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.              
      *    *------------------------------------------------------------        
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
      *  GENERAL ABEND                                                          
      *****************************************************************         
       9999-ABEND.                                                              
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PROGRAM ** = MLKUT043'.                                  
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-FUNCTION.                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
