       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT442.                                         00020000
       AUTHOR.        SCOTT JACKSON.                                    00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  07-13-1999.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *                                                                 00080000
      * MLKUT442                                                        00090000
      *                                                                 00100000
      * THIS PROGRAM WILL REFORMAT THE PRICE CHANGE (MARKDOWN/MARKUP)   00110000
      * FILE FROM THE TRANSACTIONS CAPTURED FOR DEPT 099-10. THESE      00120000
      * RECORDS ARE INITIALLY CREATED WITH AN OLD/NEW PRICE AND QTY.    00130000
      * TO PREPARE THESE TRANSACTIONS FOR RE-ALLOCATION TO VALID DEPTS, 00140000
      * EACH RECORD WILL NEED TO PROCESS AN EXTENDED RETAIL VALUE THAT  00150000
      * WILL BE SPREAD OVER SEVERAL DEPARTMENTS. QUANTITY * PRICE IS    00160000
      * CALCULATED AS EXTENDED RETAIL AND WRITTEN TO OUTPUT AS QTY = 1. 00170000
      *                                                                 00180000
      *CHANGE HISTORY:                                                  00191004
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00192004
      * ------- ---------- ----------- -------------------------------- 00193004
W21731* 12/12/01  RLB  NON-INTELLIGENT SKU                              00200001
W21731*                MLRD020 AND MLRD129 HAVE CHANGED.  THE DEPT,     00210001
W21731*                CLASS, AND SEQ DEFINED WITHIN THE SKU HAVE BEEN  00220001
W21731*                DELETED, AND THE SKU IS DEFINED AS A CHAR 8.     00230001
W21731*                DEPT AND CLASS HAVE BEEN MOVED INTO THE AVAILABLE00240001
W21731*                FILLER.  (THE CLASS IS REALLY THE SUBCLASS)      00250003
W21731*                MODIFIY LOGIC TO MOVE ZEROES                     00260003
W21731*                INTO THE LAST 3 DIGITS OF THE SKU.               00270001
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00284004
      ******************************************************************00290000
      *                                                                 00300000
      *                                                                 00310000
       ENVIRONMENT DIVISION.                                            00320000
      *                                                                 00330000
       CONFIGURATION SECTION.                                           00340000
      *                                                                 00350000
       SOURCE-COMPUTER.        IBM-3090.                                00360000
       OBJECT-COMPUTER.        IBM-3090.                                00370000
      *                                                                 00380000
       INPUT-OUTPUT SECTION.                                            00390000
       FILE-CONTROL.                                                    00400000
                                                                        00410000
           SELECT PRICE-CHANGE-INPUT                                    00420000
               ASSIGN TO INP01.                                         00430000
                                                                        00440000
           SELECT PRICE-CHANGE-REFORMAT-OUTPUT                          00450000
               ASSIGN TO OUT01.                                         00460000
                                                                        00470000
      ******************************************************************00480000
       DATA DIVISION.                                                   00490000
      ******************************************************************00500000
                                                                        00510000
       FILE SECTION.                                                    00520000
                                                                        00530000
       FD  PRICE-CHANGE-INPUT                                           00540000
           RECORDING MODE IS F                                          00550000
           LABEL RECORDS ARE STANDARD                                   00560000
           BLOCK CONTAINS 0 RECORDS.                                    00570000
      *                                                                 00580000
W26603 01  PCHG-INPUT-RECORD           PIC X(175).                      00590004
                                                                        00600000
       FD  PRICE-CHANGE-REFORMAT-OUTPUT                                 00610000
           RECORDING MODE IS F                                          00620000
           LABEL RECORDS ARE STANDARD                                   00630000
           BLOCK CONTAINS 0 RECORDS.                                    00640000
      *                                                                 00650000
W26603 01  PCHG-OUTPUT-RECORD          PIC X(175).                      00660004
                                                                        00670000
      ******************************************************************00690000
       WORKING-STORAGE SECTION.                                         00700000
      ******************************************************************00710000
      *                                                                 00720000
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     00730000
                                                           COMP SYNC.   00740000
      *                                                                 00750000
       01  PS-SWITCHES.                                                 00760000
           05  PS-END-OF-FILE-SW     PIC X(01)          VALUE 'N'.      00770000
               88  PS-END-OF-FILE                       VALUE 'Y'.      00780000
      *                                                                 00790000
       01  PV-PROGRAM-VARIABLES.                                        00800000
W26603     05  PV-PCHG-OLD-EXTD-PRICE  PIC S9(11)V99    VALUE ZEROES.   00810005
W26603     05  PV-PCHG-NEW-EXTD-PRICE  PIC S9(11)V99    VALUE ZEROES.   00820005
W21731     05  PV-SKU.                                                  00830002
W21731         10  PV-SKU-FIRST5-CHAR  PIC X(05)        VALUE SPACES.   00840002
W21731         10  PV-SKU-LAST3-CHAR   PIC X(03)        VALUE SPACES.   00850002
                                                                        00860000
       01  PV-PROGRAM-CONSTANTS.                                        00870000
           05  PC-PRICE-CHANGE-QTY     PIC S9(07)       VALUE +1.       00880000
           05  PC-PCHG-BATCH-NBR       PIC  X(04)       VALUE '0795'.   00890000
           05  PC-SEQ-NBR              PIC  X(03)       VALUE '000'.    00900000
      *                                                                 00920000
      ****************************************************              00930000
      * INPUT FILE FORMAT                                *              00940000
      ****************************************************              00950000
           COPY MLRD020.                                                00960000
      *                                                                 00970000
      ****************************************************              00990000
      * PRICE CHANGE REVERSALS-REFORMATTED OUTPUT LAYOUT *              01000000
      ****************************************************              01010000
           COPY MLRD129.                                                01020000
                                                                        01030000
      *-------------------*                                             01070000
       PROCEDURE DIVISION.                                              01080000
      *-------------------*                                             01090000
                                                                        01100000
       0000-MAIN-MODULE.                                                01110000
                                                                        01120000
           PERFORM 1000-INITIALIZATION.                                 01130000
                                                                        01140000
           PERFORM 2000-CALC-EXTENDED-PRICE                             01150000
               UNTIL PS-END-OF-FILE.                                    01160000
                                                                        01170000
           STOP RUN.                                                    01180000
      *                                                                 01190000
      *                                                                 01200000
      ******************************************************************01210000
      * OPEN INPUT AND OUTPUT DATASETS.                                 01220000
      ******************************************************************01230000
       1000-INITIALIZATION.                                             01240000
      *                                                                 01250000
           OPEN  INPUT PRICE-CHANGE-INPUT                               01260000
                OUTPUT PRICE-CHANGE-REFORMAT-OUTPUT.                    01270000
                                                                        01280000
           MOVE 'N' TO PS-END-OF-FILE-SW.                               01290000
      *                                                                 01300000
           PERFORM 4000-READ-PRICE-CHANGE-RECORD.                       01310000
      *                                                                 01320000
      *                                                                 01330000
      ******************************************************************01340000
      * REFORMAT OUTPUT FOR PRICE CHANGES                               01350000
      ******************************************************************01360000
       2000-CALC-EXTENDED-PRICE.                                        01370000
      *                                                                 01380000
           EVALUATE ML020-TRANS-CDE                                     01390000
               WHEN '40'                                                01400000
                   PERFORM 2100-PROCESS-MARKDOWN-RECORD                 01410000
               WHEN '50'                                                01420000
                   PERFORM 2200-PROCESS-MARKUP-RECORD                   01430000
               WHEN OTHER                                               01440000
                   DISPLAY 'TRAN-CODE: ', ML020-TRANS-CDE, ' IS NOT ',  01450000
                       'A VALID PRICE CHANGE CODE'                      01460000
                   MOVE +4003              TO ABEND-CODE                01470000
                   PERFORM 9999-ABEND                                   01480000
           END-EVALUATE.                                                01490000
      *                                                                 01500000
           PERFORM 4000-READ-PRICE-CHANGE-RECORD.                       01510000
      *                                                                 01520000
      *                                                                 01530000
      ******************************************************************01540000
      * REFORMAT OUTPUT FOR MARKDOWN PRICE CHANGES                      01550000
      ******************************************************************01560000
       2100-PROCESS-MARKDOWN-RECORD.                                    01570000
      *                                                                 01580000
      * ML020 AND ML129 ARE IN SAME FORMAT (SOME VALUES TO BE REPLACED) 01590000
           MOVE ML020-COMPOSITE-RECORD      TO ML129-PC-RECORD          01600000
      *                                                                 01610000
      * COMPUTE TOTAL DOLLAR AMT FOR STORE TRANSACTION (OLD/NEW TOTALS) 01620000
           COMPUTE PV-PCHG-OLD-EXTD-PRICE =                             01630000
                                    (ML020-PC-QTY * ML020-PC-OLD-PR-AMT)01640000
           COMPUTE PV-PCHG-NEW-EXTD-PRICE =                             01650000
                                    (ML020-PC-QTY * ML020-PC-NEW-PR-AMT)01660000
      *                                                                 01670000
      * REPLACE APPROPRIATE VALUES TO OUTPUT RECORD                     01680000
           MOVE PC-PRICE-CHANGE-QTY         TO ML129-PC-QTY.            01690000
           MOVE PV-PCHG-OLD-EXTD-PRICE      TO ML129-PC-EXTD-OLD-PR-AMT.01700000
           MOVE PV-PCHG-NEW-EXTD-PRICE      TO ML129-PC-EXTD-NEW-PR-AMT.01710000
           MOVE PC-PCHG-BATCH-NBR           TO ML129-PC-BATCH-NBR.      01720000
W21731*    MOVE PC-SEQ-NBR                  TO ML129-SEQ-NBR.           01730001
W21731     MOVE ML129-SKU                   TO PV-SKU.                  01740002
W21731     MOVE PC-SEQ-NBR                  TO PV-SKU-LAST3-CHAR.       01750002
W21731     MOVE PV-SKU                      TO ML129-SKU.               01760002
      *                                                                 01770000
           PERFORM 4100-WRITE-REVERSAL-RECORD.                          01780000
      *                                                                 01800000
      ******************************************************************01810000
      * REFORMAT OUTPUT FOR MARKUP PRICE CHANGES                        01820000
      ******************************************************************01830000
       2200-PROCESS-MARKUP-RECORD.                                      01840000
      *                                                                 01850000
           MOVE ML020-COMPOSITE-RECORD      TO ML129-PC-RECORD          01860000
      *                                                                 01870000
           COMPUTE PV-PCHG-OLD-EXTD-PRICE =                             01880000
                                    (ML020-PC-QTY * ML020-PC-OLD-PR-AMT)01890000
           COMPUTE PV-PCHG-NEW-EXTD-PRICE =                             01900000
                                    (ML020-PC-QTY * ML020-PC-NEW-PR-AMT)01910000
      *                                                                 01920000
           MOVE PC-PRICE-CHANGE-QTY         TO ML129-PC-QTY.            01930000
           MOVE PV-PCHG-OLD-EXTD-PRICE      TO ML129-PC-EXTD-OLD-PR-AMT.01940000
           MOVE PV-PCHG-NEW-EXTD-PRICE      TO ML129-PC-EXTD-NEW-PR-AMT.01950000
           MOVE PC-PCHG-BATCH-NBR           TO ML129-PC-BATCH-NBR.      01960000
W21731*    MOVE PC-SEQ-NBR                  TO ML129-SEQ-NBR.           01970002
W21731     MOVE ML129-SKU                   TO PV-SKU.                  01980002
W21731     MOVE PC-SEQ-NBR                  TO PV-SKU-LAST3-CHAR.       01990002
W21731     MOVE PV-SKU                      TO ML129-SKU.               02000002
      *                                                                 02010000
           PERFORM 4100-WRITE-REVERSAL-RECORD.                          02020000
      *                                                                 02030000
      *                                                                 02040000
      ******************************************************************02050000
      * READ INPUT FILE                                                 02060000
      ******************************************************************02070000
       4000-READ-PRICE-CHANGE-RECORD.                                   02080000
      *                                                                 02090000
            READ PRICE-CHANGE-INPUT                                     02100000
                INTO ML020-COMPOSITE-RECORD                             02110000
             AT END                                                     02120000
                SET PS-END-OF-FILE TO TRUE                              02130000
            END-READ.                                                   02140000
      *                                                                 02150000
      *                                                                 02160000
      ******************************************************************02170000
      * WRITE OUTPUT RECORD.                                            02180000
      ******************************************************************02190000
       4100-WRITE-REVERSAL-RECORD.                                      02200000
      *                                                                 02210000
            WRITE PCHG-OUTPUT-RECORD FROM ML129-PC-RECORD .             02220000
      *                                                                 02230000
      *                                                                 02240000
      ******************************************************************02250000
      *                                                                 02260000
      *                     ABEND ROUTINE                               02270000
      *                                                                 02280000
      ******************************************************************02290000
       9999-ABEND.                                                      02300000
                                                                        02310000
           CALL 'ILBOABN0'             USING ABEND-CODE.                02320000
