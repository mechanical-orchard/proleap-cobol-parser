       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT032.                                         MAKUT032
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      *  MA - CREATE ONHAND LOAD                                       *      32
      ******************************************************************      32
      *  THE PURPOSE OF THIS PROGRAM IS TO CREATE AN ONHAND TABLE LOAD *      32
      *  FILE CONSISTING OF THE ITEM CLUSTER ID, THE ORG UNIT CLUSTER  *      32
      *  ID, AND THE ONHAND QUANTITY ASSOCIATED WITH THIS SKU/STORE.   *      32
      *                                                                *        
      *  WR21120 - LINDA GRIESBACH                                     *        
      *            NI SKU                                              *        
      *  WR25478 - LINDA GRIESBACH                                     *        
      *            STORE NBR EXPANSION                                 *        
      ******************************************************************      32
221500*  04/08/2019 - CHG0221500 - JIM ARENDT                          *      32
221500*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
221500*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
221500*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
221500******************************************************************      32
                                                                        MAKUT032
       ENVIRONMENT DIVISION.                                            MAKUT032
       INPUT-OUTPUT SECTION.                                            MAKUT032
       FILE-CONTROL.                                                    MAKUT032
                                                                        MAKUT032
           SELECT SKU-STORE-INVENTORY-FILE                              MAKUT032
               ASSIGN TO INP01.                                         MAKUT032
                                                                        MAKUT032
           SELECT ORG-CLUSTR-FILE                                       MAKUT032
               ASSIGN TO INP02.                                         MAKUT032
                                                                        MAKUT032
           SELECT ITEM-CLUSTR-FILE                                      MAKUT032
               ASSIGN TO INP03.                                         MAKUT032
                                                                        MAKUT032
           SELECT INVENTORY-LOAD-FILE                                   MAKUT032
               ASSIGN TO OUT01.                                         MAKUT032
                                                                        MAKUT032
       DATA DIVISION.                                                   MAKUT032
       FILE SECTION.                                                    MAKUT032
       FD  SKU-STORE-INVENTORY-FILE                                     MAKUT032
           RECORDING MODE IS F                                          MAKUT032
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT032
       01  SKU-STORE-INVENTORY-RECORD  PIC X(109).                         CL**5
                                                                        MAKUT032
       FD  ORG-CLUSTR-FILE                                              MAKUT032
           RECORDING MODE IS F                                          MAKUT032
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT032
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT032
       FD  ITEM-CLUSTR-FILE                                             MAKUT032
           RECORDING MODE IS F                                          MAKUT032
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT032
21120  01  ITEM-CLUSTR-RECORD          PIC X(25).                          CL**5
                                                                        MAKUT032
       FD  INVENTORY-LOAD-FILE                                          MAKUT032
           RECORDING MODE IS F                                          MAKUT032
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT032
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT032
                                                                        MAKUT032
       WORKING-STORAGE SECTION.                                         MAKUT032
                                                                        MAKUT032
       01  PROGRAM-VARIABLES.                                           MAKUT032
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT032
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT032
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT032
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT032
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
21120      05  PV-PREV-ITEM-CLUSTR-SKU PIC X(15)  VALUE SPACES.                 
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT032
           05  PS-SKU-STORE-INV-EOF-SW PIC X(1)   VALUE 'N'.            MAKUT032
               88 PS-SKU-STORE-INV-EOF            VALUE 'Y'.            MAKUT032
               88 PS-SKU-STORE-INV-NOT-EOF        VALUE 'N'.            MAKUT032
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT032
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT032
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT032
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT032
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT032
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT032
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT032
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT032
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT032
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT032
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT032
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT032
           05  PS-EMPTY-ORG-CLUSTR-FILE-SW                              MAKUT032
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ORG-CLUSTR-FILE        VALUE 'Y'.            MAKUT032
               88 PS-NOT-EMPTY-ORG-CLUSTR-FILE    VALUE 'N'.            MAKUT032
           05  PS-EMPTY-ITEM-CLUSTR-FILE-SW                             MAKUT032
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ITEM-CLUSTR-FILE       VALUE 'Y'.            MAKUT032
               88 PS-NOT-EMPTY-ITEM-CLUSTR-FILE   VALUE 'N'.            MAKUT032
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT032
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT032
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT032
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT032
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT032
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT032
                                                                        MAKUT032
       01  ORG-CLUSTR-TABLE.                                                    
221500*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
221500     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT032'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT032
           05  FILLER                  PIC X(23)                        MAKUT032
                   VALUE '*** PROGRAM MAKUT032 - '.                     MAKUT032
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT032
                                                                        MAKUT032
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT032
           05  FILLER                  PIC X(37)                        MAKUT032
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT032
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT032
21120      05  FILLER                  PIC X(10)  VALUE SPACES.                 
                                                                        MAKUT032
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT032
           05  FILLER                  PIC X(35)                        MAKUT032
                   VALUE 'SKU STORE INVENTORY RECORDS READ = '.         MAKUT032
           05  RSLM1-SKU-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT032
                                                                        MAKUT032
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT032
           05  FILLER                  PIC X(33)                        MAKUT032
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT032
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT032
                                                                        MAKUT032
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT032
           05  FILLER                  PIC X(43)                        MAKUT032
                   VALUE 'NUMBER OF SKU STORE INVENTORY RECORDS DROPP'. MAKUT032
           05  FILLER                  PIC X(39)                        MAKUT032
                   VALUE 'ED DUE TO NO MATCHING ITEM CLUSTER ID= '.     MAKUT032
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT032
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT032
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT032
           05  FILLER                  PIC X(38)                        MAKUT032
                   VALUE 'NUMBER OF SKU STORE INVENTORY RECORDS '.      MAKUT032
           05  FILLER                  PIC X(38)                        MAKUT032
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT032
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT032
                                                                        MAKUT032
       01  WS-ORG-CLUSTR-RECORD.                                                
           05  WS-ORG-CLUSTR-STORE     PIC X(4).                                
           05  WS-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT032
       01  WS-ITEM-CLUSTR-RECORD.                                               
21120      05  WS-ITEM-CLUSTR-SKU      PIC X(15).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                                
      * FIELDS FOR THE SKU STORE INVENTORY EXTRACT FILE                 MAKUT032
           COPY MARD015.                                                MAKUT032
                                                                        MAKUT032
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT032
           COPY MARD016.                                                MAKUT032
                                                                        MAKUT032
                                                                        MAKUT032
       EJECT                                                            MAKUT032
       PROCEDURE DIVISION.                                              MAKUT032
                                                                        MAKUT032
       0000-MAINLINE.                                                   MAKUT032
                                                                        MAKUT032
           PERFORM 1000-INITIALIZE                                      MAKUT032
              THRU 1000-EXIT.                                                   
                                                                        MAKUT032
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT032
              THRU 2000-EXIT                                                    
              UNTIL PS-SKU-STORE-INV-EOF.                                       
                                                                        MAKUT032
           PERFORM 3000-CLOSE                                           MAKUT032
              THRU 3000-EXIT.                                                   
                                                                        MAKUT032
           DISPLAY 'MAKUT032 - SUCCESSFUL END'.                         MAKUT032
           MOVE ZERO TO RETURN-CODE.                                    MAKUT032
           GOBACK.                                                      MAKUT032
                                                                        MAKUT032
       0000-EXIT.                                                       MAKUT032
           EXIT.                                                                
                                                                        MAKUT032
                                                                        MAKUT032
       1000-INITIALIZE.                                                 MAKUT032
                                                                        MAKUT032
           OPEN INPUT  SKU-STORE-INVENTORY-FILE                         MAKUT032
                       ORG-CLUSTR-FILE                                  MAKUT032
                       ITEM-CLUSTR-FILE                                 MAKUT032
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT032
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-SKU-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                        MAKUT032
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT032
           READ ORG-CLUSTR-FILE                                                 
               INTO WS-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT032
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF WS-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND WS-ORG-CLUSTR-STORE > ZERO                                     
               MOVE WS-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE WS-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT032
                                                                        MAKUT032
       2000-CREATE-OH-OO-FILE.                                          MAKUT032
                                                                        MAKUT032
      * THE INPUT FILE IS SORTED BY SKU/STORE -                         MAKUT032
      * IT MAY CONTAIN THE SAME SKU FOR MULITPLE STORES -               MAKUT032
      * SO MULTIPLE INPUT RECORDS COULD BE BYPASSED                     MAKUT032
      * IF THE SKU IS NOT IN THE ITEM-CLUSTR-FILE                               
                                                                                
21120 * NOTE:  MA015-ITEM-NAME MAY CONTAIN CHARACTER DATA WITH                  
21120 *        A LENGTH OF 14 OR 15.  SKU'S THAT WERE ONCE                      
21120 *        INTELLIGENT SKUS WILL HAVE A LENGTH OF 14.  SKU'S                
21120 *        THAT WERE CREATED AS NON-INTELLIGENT WILL HAVE A                 
21120 *        LENGTH OF 15.  PROGRAMS MAKUT010 AND MAKUT013                    
21120 *        PADS THIS EXTRA POSITION WITH A SPACE.  THE SQL,                 
21120 *        WHICH CREATES THE CLUSTER ID FILE, ALSO PADS THIS                
21120 *        EXTRA POSITION WITH A SPACE.                                     
                                                                                
21120      IF MA015-ITEM-NAME(1:15) = WS-ITEM-CLUSTR-SKU                        
21120        OR MA015-ITEM-NAME(1:15) < WS-ITEM-CLUSTR-SKU                      
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                        MAKUT032
21120      IF MA015-ITEM-NAME(1:15) = WS-ITEM-CLUSTR-SKU                        
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE WS-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
21120          IF MA015-ITEM-NAME(1:15) NOT =                                   
                                      PV-PREV-ITEM-CLUSTR-SKU                   
21120              MOVE MA015-ITEM-NAME(1:15)                                   
                                   TO PV-PREV-ITEM-CLUSTR-SKU                   
                                      INF-LM-ITEM-NAME                          
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
                   DISPLAY LOG-MESSAGE UPON SYSOUT                              
               END-IF                                                           
               ADD +1 TO PV-NO-ITEM-COUNT                                       
           END-IF.                                                              
                                                                                
           PERFORM 9998-READ-SKU-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE SKU-STORE-INVENTORY-FILE                                       
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-SKU-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT032
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT032
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT032
                                                                        MAKUT032
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT032
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT032
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT032
                                                                        MAKUT032
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT032
                                                                        MAKUT032
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT032
           READ ITEM-CLUSTR-FILE                                                
               INTO WS-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO WS-ITEM-CLUSTR-SKU                    
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT032
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT032
21120      IF MA015-ITEM-NAME(1:15) = WS-ITEM-CLUSTR-SKU                        
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT032
21120      IF MA015-ITEM-NAME(1:15) < WS-ITEM-CLUSTR-SKU                        
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT032
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-SKU-STORE-INV-FILE.                                            
                                                                        MAKUT032
           READ SKU-STORE-INVENTORY-FILE                                MAKUT032
               INTO MA015-ROLLUP-INVENTORY                              MAKUT032
                   AT END                                               MAKUT032
                       SET PS-SKU-STORE-INV-EOF TO TRUE                 MAKUT032
                   NOT AT END                                           MAKUT032
                       ADD +1 TO PV-INV-READ-COUNT                      MAKUT032
           END-READ.                                                            
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT032
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT032
                                                                        MAKUT032
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT032
                                                                        MAKUT032
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT032
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
