       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.         SVKUT062.                                    00020002
       AUTHOR.             DANIEL OKYERE.                               00030002
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040002
       DATE-WRITTEN.       SEPTEMBER 2004.                              00050002
       DATE-COMPILED.                                                   00060002
                                                                        00070002
      ******************************************************************00080002
      * THIS PROGRAM WILL MATCH STORED VALUE ISSUANCES FROM SAFEWAY'S  *00090002
      * RECONCILIATION FILE TO STORED VALUE ISSUANCES FOR THE SAFEWAY  *00100002
      * LOCATION THAT HAVE NOT YET BEEN RECONCILED.                    *00101002
      *                                                                *00110002
      * INPUT FILES:   SV-EXTRACT-FILE           DDNAME = INP01        *00120002
      *                RECONCILIATION-FILE       DDNAME = INP02        *00121002
      *                                                                *00130002
      * OUTPUT FILES:  POSTING-FILE              DDNAME = OUT01        *00140002
      *                REPORT-FILE               DDNAME = OUT02        *00141002
      *                UNSETTLED-FILE            DDNAME = OUT03        *00142002
      *                                                                *00150002
      ******************************************************************00160002
                                                                        00170002
      ******************************************************************00180002
       ENVIRONMENT DIVISION.                                            00190002
      ******************************************************************00200002
                                                                        00210002
       CONFIGURATION SECTION.                                           00220002
                                                                        00230002
       SOURCE-COMPUTER.    IBM-3090.                                    00240002
       OBJECT-COMPUTER.    IBM-3090.                                    00250002
                                                                        00260002
       INPUT-OUTPUT SECTION.                                            00270002
                                                                        00280002
       FILE-CONTROL.                                                    00290002
                                                                        00300002
           SELECT SV-EXTRACT-FILE      ASSIGN TO INP01.                 00310002
           SELECT RECONCILIATION-FILE  ASSIGN TO INP02.                 00311002
           SELECT POSTING-FILE         ASSIGN TO OUT01.                 00320002
           SELECT REPORT-FILE          ASSIGN TO OUT02.                 00321002
           SELECT UNSETTLED-FILE       ASSIGN TO OUT03.                 00322002
                                                                        00330002
      ******************************************************************00340002
       DATA DIVISION.                                                   00350002
      ******************************************************************00360002
                                                                        00370002
       FILE SECTION.                                                    00380002
                                                                        00390002
       FD  SV-EXTRACT-FILE                                              00400002
           RECORDING MODE IS F                                          00410002
           BLOCK CONTAINS 0 RECORDS                                     00420002
           LABEL RECORDS ARE OMITTED.                                   00430002
                                                                        00440002
       01  SV-EXTRACT-RECORD            PIC  X(110).                    00450002
                                                                        00460002
       FD  RECONCILIATION-FILE                                          00470002
           RECORDING MODE IS F                                          00480002
           BLOCK CONTAINS 0 RECORDS                                     00490002
           LABEL RECORDS ARE OMITTED.                                   00500002
                                                                        00510002
       01  RECONCILIATION-RECORD        PIC  X(90).                     00520002
                                                                        00530002
       FD  POSTING-FILE                                                 00531002
           RECORDING MODE IS F                                          00532002
           BLOCK CONTAINS 0 RECORDS                                     00533002
           LABEL RECORDS ARE OMITTED.                                   00534002
                                                                        00535002
       01  POSTING-RECORD               PIC  X(110).                    00536002
                                                                        00537002
       FD  REPORT-FILE                                                  00538002
           RECORDING MODE IS F                                          00539002
           BLOCK CONTAINS 0 RECORDS                                     00539102
           LABEL RECORDS ARE OMITTED.                                   00539202
                                                                        00539302
       01  REPORT-RECORD                PIC  X(110).                    00539402
                                                                        00539502
       FD  UNSETTLED-FILE                                               00539602
           RECORDING MODE IS F                                          00539702
           BLOCK CONTAINS 0 RECORDS                                     00539802
           LABEL RECORDS ARE OMITTED.                                   00539902
                                                                        00540002
       01  UNSETTLED-RECORD             PIC  X(90).                     00540102
                                                                        00540202
       WORKING-STORAGE SECTION.                                         00541002
                                                                        00550002
      ******************************************************************00560002
      *    PC-PROGRAM CONSTANTS                                        *00570002
      ******************************************************************00580002
       01  PC-PROGRAM-CONSTANTS.                                        00590002
           05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'SVKUT062'. 00600002
                                                                        00620002
      ******************************************************************00630002
      *    PV-PROGRAM VARIABLES                                        *00640002
      ******************************************************************00650002
       01  PV-PROGRAM-VARIABLES.                                        00660002
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.    00670006
           05  PV-OPERATION-MSG-1           PIC  X(30) VALUE SPACES.    00680006
TKM660     05  PV-SAFEWAY-CARD-NBR          PIC S9(12) COMP-3 VALUE +0. 00680116
                                                                        00681006
       01  PV-TRANSACTION-DATE.                                         00682006
           05  PV-YEAR                      PIC  X(04) VALUE SPACES.    00683006
           05  FILLER                       PIC  X(01) VALUE SPACES.    00683106
           05  PV-MONTH                     PIC  X(02) VALUE SPACES.    00684006
           05  FILLER                       PIC  X(01) VALUE SPACES.    00685006
           05  PV-DAY                       PIC  X(02) VALUE SPACES.    00686006
                                                                        00690002
       01  PV-REFORMATTED-DATE.                                         00691007
           05  PV-REFORMATTED-YEAR          PIC  X(04) VALUE SPACES.    00692006
           05  PV-REFORMATTED-MONTH         PIC  X(02) VALUE SPACES.    00693006
           05  PV-REFORMATTED-DAY           PIC  X(02) VALUE SPACES.    00694006
                                                                        00695006
      ******************************************************************00700002
      *    PS-PROGRAM SWITCHES                                         *00710002
      ******************************************************************00720002
       01  PS-PROGRAM-SWITCHES.                                         00730002
           05  PS-END-OF-SV-EXTRACT-FILE-SW PIC  X(01) VALUE 'N'.       00740007
               88  PS-END-OF-SV-EXTRACT-FILE           VALUE 'Y'.       00750007
                                                                        00760002
           05  PS-END-OF-RECON-FILE-SW      PIC  X(01) VALUE 'N'.       00762007
               88  PS-END-OF-RECONCILIATION-FILE       VALUE 'Y'.       00763007
                                                                        00764003
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00840002
                                                        COMP.           00850002
           88  AC-DATASET-DATA-ERROR                    VALUE +4008.    00860002
                                                                        00870002
      ******************************************************************00880003
      *    DAILY EXTRACT RECORD LAYOUT                                 *00890003
      ******************************************************************00900003
           COPY SVRD022.                                                00910003
                                                                        00920003
      ******************************************************************00930002
      *    RECONCILIATION FILE LAYOUT                                  *00940002
      ******************************************************************00950002
           COPY SVRD053.                                                00960002
                                                                        00970002
      ******************************************************************00971003
      *  RECONCILIATION DETAIL FILE LAYOUT                             *00972003
      ******************************************************************00974003
           COPY SVRD054.                                                00975003
                                                                        00976003
      ******************************************************************01080002
      * PROCEDURE DIVISION                                             *01090002
      ******************************************************************01100002
       PROCEDURE DIVISION.                                              01110002
                                                                        01120002
       0000-MAINLINE-PROCESSING.                                        01130002
                                                                        01140002
           PERFORM 1000-INITIALIZE-PROGRAM.                             01150002
                                                                        01160002
           PERFORM 2000-MATCH-INPUT-FILES                               01170003
             UNTIL PS-END-OF-SV-EXTRACT-FILE                            01180003
                OR PS-END-OF-RECONCILIATION-FILE.                       01181010
                                                                        01190002
           IF PS-END-OF-SV-EXTRACT-FILE                                 01191011
               PERFORM 2400-WRITE-RECONCILIATION-REC                    01192013
                 UNTIL PS-END-OF-RECONCILIATION-FILE                    01192113
           END-IF.                                                      01193011
                                                                        01194011
           IF PS-END-OF-RECONCILIATION-FILE                             01195011
               PERFORM 2200-WRITE-EXTRACT-RECORD                        01196013
                 UNTIL PS-END-OF-SV-EXTRACT-FILE                        01196113
           END-IF.                                                      01197011
                                                                        01198011
           CLOSE SV-EXTRACT-FILE                                        01200004
                 RECONCILIATION-FILE                                    01210004
                 POSTING-FILE                                           01211004
                 REPORT-FILE                                            01212004
                 UNSETTLED-FILE.                                        01213004
                                                                        01220002
           GOBACK.                                                      01221009
                                                                        01222009
      ******************************************************************01230002
      * 1000-INITIALIZE-PROGRAM : OPEN INPUT AND OUTPUT FILES AND      *01240002
      * PERFORM AN INITIAL READ OF THE INPUT FILE.                     *01250002
      ******************************************************************01260002
       1000-INITIALIZE-PROGRAM.                                         01270002
                                                                        01280002
           OPEN INPUT  SV-EXTRACT-FILE                                  01290003
                       RECONCILIATION-FILE                              01291003
                OUTPUT POSTING-FILE                                     01300003
                       REPORT-FILE                                      01301003
                       UNSETTLED-FILE.                                  01302003
                                                                        01310002
           PERFORM 7000-READ-SV-EXTRACT-FILE.                           01320003
                                                                        01330002
           PERFORM 8000-READ-RECONCILIATION-FILE.                       01340003
                                                                        01410002
      ******************************************************************01420002
      * 2000-MATCH-INPUT-FILES                                         *01430003
      *    PROCESS THE INPUT FILE AND WRITE THE OUTPUT RECORDS         *01440002
      ******************************************************************01450002
       2000-MATCH-INPUT-FILES.                                          01460003
                                                                        01470002
           IF SV022-CRD-NBR = SV053-GIFT-CARD-NBR                       01480004
               SET SV054-RF-VERIFIED TO TRUE                            01490004
               MOVE SV022-TRN-AUTH-TMST      TO SV054-TRN-AUTH-TMST     01500004
TKM660         MOVE SV022-CRD-NBR            TO PV-SAFEWAY-CARD-NBR     01501016
               PERFORM 3000-MOVE-RECONCILIATION-DATA                    01510006
               WRITE POSTING-RECORD FROM SV054-DETAIL-TRANS             01591004
               WRITE REPORT-RECORD FROM SV054-DETAIL-TRANS              01592004
               PERFORM 7000-READ-SV-EXTRACT-FILE                        01593004
               PERFORM 8000-READ-RECONCILIATION-FILE                    01594004
           ELSE                                                         01600004
               IF SV022-CRD-NBR < SV053-GIFT-CARD-NBR                   01610004
                   PERFORM 2200-WRITE-EXTRACT-RECORD                    01610416
               ELSE                                                     01611404
                   IF SV022-CRD-NBR > SV053-GIFT-CARD-NBR               01612004
TKM660                 IF SV053-GIFT-CARD-NBR = PV-SAFEWAY-CARD-NBR     01612116
TKM660                     PERFORM 8000-READ-RECONCILIATION-FILE        01612217
TKM660                 ELSE                                             01612416
                           PERFORM 2400-WRITE-RECONCILIATION-REC        01612516
TKM660                 END-IF                                           01612616
                   END-IF                                               01612716
               END-IF                                                   01620004
           END-IF.                                                      01630004
                                                                        01780002
      ******************************************************************01781013
      * 2200-WRITE-EXTRACT-RECORD                                      *01782013
      ******************************************************************01783013
       2200-WRITE-EXTRACT-RECORD.                                       01784013
                                                                        01785013
           INITIALIZE SV054-DETAIL-TRANS.                               01786013
           MOVE SV022-CRD-NBR       TO SV054-GIFT-CARD-NBR.             01787013
           MOVE SV022-POS-DTE       TO PV-TRANSACTION-DATE.             01788013
           MOVE PV-YEAR             TO PV-REFORMATTED-YEAR.             01789013
           MOVE PV-MONTH            TO PV-REFORMATTED-MONTH.            01789113
           MOVE PV-DAY              TO PV-REFORMATTED-DAY.              01789213
           MOVE PV-REFORMATTED-DATE TO SV054-TRANSACTION-DATE.          01789313
           MOVE SV022-APP-AUTH-AMT  TO SV054-TRANSACTION-AMT.           01789413
           MOVE SV022-ST-CDE        TO SV054-SAFEWAY-STATE-CDE.         01789513
TKM660     MOVE SV022-TRN-AUTH-TMST TO SV054-TRN-AUTH-TMST.             01789623
                                                                        01789718
TKM660** NEED TO CHECK FOR REVERSALS. **********                        01789818
           PERFORM 7000-READ-SV-EXTRACT-FILE                            01790220
TKM660     IF  PS-END-OF-SV-EXTRACT-FILE-SW = 'N'                       01790321
TKM660         IF SV054-GIFT-CARD-NBR = SV022-CRD-NBR                   01790421
TKM660             SET SV054-RF-REVERSAL         TO TRUE                01790621
TKM660             WRITE POSTING-RECORD FROM SV054-DETAIL-TRANS         01790721
TKM660             WRITE REPORT-RECORD  FROM SV054-DETAIL-TRANS         01790821
TKM660             INITIALIZE SV054-DETAIL-TRANS                        01790921
TKM660             MOVE SV022-CRD-NBR       TO SV054-GIFT-CARD-NBR      01791021
TKM660             MOVE SV022-POS-DTE       TO PV-TRANSACTION-DATE      01791121
TKM660             MOVE PV-YEAR             TO PV-REFORMATTED-YEAR      01791221
TKM660             MOVE PV-MONTH            TO PV-REFORMATTED-MONTH     01791321
TKM660             MOVE PV-DAY              TO PV-REFORMATTED-DAY       01791421
TKM660             MOVE PV-REFORMATTED-DATE TO SV054-TRANSACTION-DATE   01791521
TKM660             MOVE SV022-APP-AUTH-AMT  TO SV054-TRANSACTION-AMT    01791621
TKM660             MOVE SV022-ST-CDE        TO SV054-SAFEWAY-STATE-CDE  01791721
TKM660             MOVE SV022-TRN-AUTH-TMST TO SV054-TRN-AUTH-TMST      01791822
TKM660             SET SV054-RF-REVERSAL    TO TRUE                     01791921
TKM660             WRITE POSTING-RECORD FROM SV054-DETAIL-TRANS         01792021
TKM660             WRITE REPORT-RECORD  FROM SV054-DETAIL-TRANS         01792121
TKM660             IF PS-END-OF-SV-EXTRACT-FILE                         01792221
TKM660                 CONTINUE                                         01792321
TKM660             ELSE                                                 01792421
TKM660                 PERFORM 7000-READ-SV-EXTRACT-FILE                01792521
TKM660             END-IF                                               01792621
TKM660         ELSE                                                     01792721
                   SET SV054-RF-MISSING-SAFEWAY TO TRUE                 01792821
                   WRITE REPORT-RECORD FROM SV054-DETAIL-TRANS          01792921
TKM660         END-IF                                                   01793021
TKM660     ELSE                                                         01793121
               SET SV054-RF-MISSING-SAFEWAY TO TRUE                     01793221
               WRITE REPORT-RECORD FROM SV054-DETAIL-TRANS              01793321
TKM660     END-IF.                                                      01793421
                                                                        01793521
      ******************************************************************01793621
      * 2400-WRITE-RECONCILIATION-REC                                  *01793721
      ******************************************************************01793821
       2400-WRITE-RECONCILIATION-REC.                                   01793921
                                                                        01794021
           SET SV054-RF-MISSING-SV TO TRUE.                             01794121
           PERFORM 3000-MOVE-RECONCILIATION-DATA.                       01794221
           WRITE REPORT-RECORD FROM SV054-DETAIL-TRANS.                 01794321
           WRITE UNSETTLED-RECORD FROM SV053-DETAIL-TRANS.              01794421
           PERFORM 8000-READ-RECONCILIATION-FILE.                       01794521
                                                                        01794621
      ******************************************************************01795020
      * 3000-MOVE-RECONCILIATION-DATA                                  *01800004
      * MOVE THE INPUT DATA VARIABLES TO THE OUTPUT DATA VARIABLES.    *01801008
      ******************************************************************01810004
       3000-MOVE-RECONCILIATION-DATA.                                   01820004
                                                                        01821004
           MOVE SV053-GIFT-CARD-NBR      TO SV054-GIFT-CARD-NBR.        01830004
           MOVE SV053-TRANSACTION-DATE   TO SV054-TRANSACTION-DATE.     01840004
           MOVE SV053-TRANSACTION-TIME   TO SV054-TRANSACTION-TIME.     01850004
           MOVE SV053-TRANSACTION-NBR    TO SV054-TRANSACTION-NBR.      01860004
           MOVE SV053-TRANSACTION-AMT    TO SV054-TRANSACTION-AMT.      01870004
           MOVE SV053-SAFEWAY-IDENTIFIER TO SV054-SAFEWAY-IDENTIFIER.   01880004
           MOVE SV053-SAFEWAY-STATE-CDE  TO SV054-SAFEWAY-STATE-CDE.    01890004
           MOVE SV053-SAFEWAY-ZIP-CDE    TO SV054-SAFEWAY-ZIP-CDE.      01900004
           MOVE SV053-APPROVAL-CDE       TO SV054-APPROVAL-CDE.         01910004
                                                                        01920004
      ******************************************************************01951002
      * 7000-READ-SV-EXTRACT-FILE                                      *01960003
      *    READ THE INPUT FILE AND SET THE END OF FILE SWITCH WHEN     *01970002
      *    THERE IS NO MORE RECORDS TO READ.                           *01980002
      ******************************************************************01990002
       7000-READ-SV-EXTRACT-FILE.                                       02000003
                                                                        02010002
           READ SV-EXTRACT-FILE INTO SV022-KMCACT-EXTRACT-RECORD        02020003
               AT END                                                   02030002
                   SET PS-END-OF-SV-EXTRACT-FILE TO TRUE.               02040003
                                                                        02050002
      ******************************************************************02051003
      * 8000-READ-RECONCILIATION-FILE                                  *02052003
      *    READ THE INPUT FILE AND SET THE END OF FILE SWITCH WHEN     *02053003
      *    THERE IS NO MORE RECORDS TO READ.                           *02054003
      ******************************************************************02055003
       8000-READ-RECONCILIATION-FILE.                                   02056003
                                                                        02057003
           READ RECONCILIATION-FILE INTO SV053-DETAIL-TRANS             02058005
               AT END                                                   02059003
                   SET PS-END-OF-RECONCILIATION-FILE TO TRUE.           02059103
                                                                        02059203
