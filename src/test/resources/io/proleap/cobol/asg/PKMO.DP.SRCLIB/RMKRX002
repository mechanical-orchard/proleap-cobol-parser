/* REXX */                                                                      
/* RMKRX002 - Create summary XREF copy request records for all report */        
/*      with dynamic distribution requirements in the target Infopac  */        
/*      region.                                                       */        
/* ----------------------------------------------------------------- */         
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX002 - '                                                        
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM                                                                     
"EXECIO * DISKR INP01  (STEM SUMXREF. FINIS)"                                   
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP01 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
"EXECIO * DISKR INP02  (STEM ADDIPAC. FINIS)"                                   
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP02 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
Do i = 1 to addipac.0                                                           
  If substr(addipac.i,1,1) = '*' then iterate                                   
  ipacrpt  = substr(addipac.i,3,8)                                              
  ipacrpt2 = substr(addipac.i,15,10)                                            
  ipactype = substr(addipac.i,30,7)                                             
  ipacrptg = substr(addipac.i,45,4)                                             
  ipacheir = substr(addipac.i,50,30)                                            
  Do j = 1 to sumxref.0                                                         
    temp7 = substr(sumxref.j,1,7)                                               
    If substr(sumxref.j,1,7) = ipactype then                                    
    Do                                                                          
      sumkey = substr(sumxref.j,10,4)                                           
      sumuid = substr(sumxref.j,20,7)                                           
      summbs = ipactype||sumkey                                                 
      summbs = substr(summbs,1,15)                                              
      If ipacheir ^= ' ' then                                                   
        sumheir = translate(ipacheir,sumkey,'1234')                             
        Else sumheir = ' '                                                      
      outrec = '  '||ipacrpt||'    '||ipacrpt2||'     '                         
      outrec = outrec||summbs||ipacrptg||' 'sumheir                             
      outrec = substr(outrec,1,79)                                              
      outrec = outrec||sumuid                                                   
      CALL WRITE_COPY_REQ_RECORD                                                
    End /* compare match */                                                     
  End  /* iterate j */                                                          
End  /* iterate i */                                                            
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKW OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* WRITE_COPY_REQ_RECORD                                             */         
/*                                                                   */         
/* Write a summary XREF copy request record.                         */         
/* ----------------------------------------------------------------- */         
WRITE_COPY_REQ_RECORD:                                                          
    OUT01_RECORD = LEFT(OUTREC,90)                                              
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
