       IDENTIFICATION DIVISION.                                         00010020
       PROGRAM-ID.    XMKRP033                                          00020020
       AUTHOR.        BOB MCASEY.                                       00030020
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040020
       DATE-WRITTEN.  01/10/2003.                                       00050020
       DATE-COMPILED.                                                   00060020
                                                                        00070020
      ******************************************************************00080020
      *          DOMAIN LOCATION DATA INTEGRITY                         00090020
      *                                                                 00100027
      *     XMKRP033 - LOCATION DATA INTEGRITY REPORT                   00110020
      *                                                                 00120020
      * THIS PROGRAM READS AN EXTRACT OF RMS DATA AND COMPARES TO AN    00130020
      * EXTRACT OF DATA FROM THE XM DOMAIN TABLES.                      00140020
      * ANY DIFFERENCES ARE WRITTEN TO THE XM LOCATION DATA INTEGRITY   00150020
      * ERRORS REPORT.                                                  00160020
      *                                                                 00170020
      * THE INPUT FILES ARE FORMATTED TO THE HO025-LOCATION-DATA FORMAT 00180020
      *                                                                 00190020
      * THE MESSAGE TEXT FOR THE REPORT ERROR MESSAGES ARE FOUND IN     00200020
      * COPYBOOK XMWS001.                                               00210020
      *                                                                 00220020
      * IF THERE ARE ANY ERRORS TO BE REPORTED, PROGRAM WILL END WITH   00230020
      * RETURN CODE 4095 SO THAT AN E-MAIL CAN BE SENT IN A             00240020
      * SUBSEQUENT JOB STEP.                                            00250020
      *                                                                 00260020
      ******************************************************************00270020
      *---------------------------------------------------------------* 00280020
      *    WR/PITS#     DATE        DESCRIPTION                       * 00290020
      *    --------   ----------   ---------------------------------  * 00300020
      *    WR24442    08/20/2003   INITIAL CREATION.                  * 00310020
      *    WR25285    09/19/2003   USE NEW STORE EXTRACT.  CHANGED ALL* 00320020
      *                            HORD007 TO HORD013.                * 00330020
      *    WR26600    04/29/2004   PROCESS 10 CHARACTER STORE NAME NOW* 00331020
      *                            ON THE XMT_LOC EXTRACT.            * 00332020
0407PK*    WR31384    04/24/2007   PROCESS OPEN STORE MONTH AND OPEN  * 00333020
0407PK*                            STORE YEAR ON THE XMT_LOC EXTRACT. * 00334020
      *---------------------------------------------------------------* 00340020
                                                                        00350020
                                                                        00360020
       ENVIRONMENT DIVISION.                                            00370020
       CONFIGURATION SECTION.                                           00380020
       SOURCE-COMPUTER.        IBM-3090.                                00390020
       OBJECT-COMPUTER.        IBM-3090.                                00400020
       INPUT-OUTPUT SECTION.                                            00410020
       FILE-CONTROL.                                                    00420020
                                                                        00430020
           SELECT RMS-LOC-EXTRACT-DATA-FILE                             00440020
               ASSIGN TO INP01.                                         00450020
                                                                        00460020
           SELECT XM-LOC-EXTRACT-DATA-FILE                              00470020
               ASSIGN TO INP02.                                         00480020
                                                                        00490020
           SELECT XM-LOC-DATA-INTEGRITY-RPT                             00500020
               ASSIGN TO RPT01.                                         00510020
                                                                        00520020
       DATA DIVISION.                                                   00530020
       FILE SECTION.                                                    00540020
                                                                        00550020
       FD  RMS-LOC-EXTRACT-DATA-FILE                                    00560020
           RECORDING MODE IS F                                          00570020
           LABEL RECORDS ARE STANDARD                                   00580020
           BLOCK CONTAINS 0 RECORDS.                                    00590020
                                                                        00600020
0407PK 01  RMS-EXTRACT-REC                  PIC  X(900).                00621020
                                                                        00630020
       FD  XM-LOC-EXTRACT-DATA-FILE                                     00640020
           RECORDING MODE IS F                                          00650020
           LABEL RECORDS ARE STANDARD                                   00660020
           BLOCK CONTAINS 0 RECORDS.                                    00670020
                                                                        00680020
      *01  XM-EXTRACT-REC                   PIC  X(456).                00700020
0407PK 01  XM-EXTRACT-REC                   PIC  X(463).                00701020
                                                                        00710020
       FD  XM-LOC-DATA-INTEGRITY-RPT                                    00720020
           RECORDING  MODE IS F                                         00730020
           LABEL RECORDS ARE STANDARD                                   00740020
           BLOCK CONTAINS 0 RECORDS.                                    00750020
                                                                        00760020
       01  XM-LOC-RPT-LINE                  PIC X(200).                 00770020
                                                                        00780020
                                                                        00790020
       WORKING-STORAGE SECTION.                                         00800020
                                                                        00810020
       01  PC-PROGRAM-CONSTANTS.                                        00820020
           05  PC-PGM-NAME             PIC  X(08)      VALUE 'XMKRP033'.00830020
           05  PC-MAX-DTL-LINES        PIC S9(04) COMP VALUE +70.       00840020
                                                                        00850020
       01  PV-PROGRAM-VARIABLES.                                        00860020
           05  PV-PAGE-CNT             PIC S9(05)        VALUE ZERO.    00870020
           05  PV-LINE-CNT             PIC S9(04) COMP   VALUE +71.     00880020
           05  PV-DISPLAY-CNT          PIC ZZZZ9.                       00890020
           05  PV-ADVANCE              PIC S9(03) COMP-3 VALUE ZERO.    00900020
           05  PV-NBR-ERRORS           PIC S9(04) COMP   VALUE ZERO.    00910020
           05  PV-DATE-YYMMDD.                                          00920020
               10  PV-YYMMDD-YY        PIC  X(02)        VALUE SPACE.   00930020
               10  PV-YYMMDD-MM        PIC  X(02)        VALUE SPACE.   00940020
               10  PV-YYMMDD-DD        PIC  X(02)        VALUE SPACE.   00950020
           05  PV-TIME-HHMMSS.                                          00960020
               10  PV-HHMMSS-HH        PIC  X(02)        VALUE SPACE.   00970020
               10  PV-HHMMSS-MM        PIC  X(02)        VALUE SPACE.   00980020
               10  PV-HHMMSS-SS        PIC  X(02)        VALUE SPACE.   00990020
           05  PV-MSG-NBR              PIC S9(03).                      01000020
           05  PV-XM-VALUE             PIC  X(30).                      01010020
           05  PV-RMS-VALUE            PIC  X(30).                      01020020
           05  PV-XM-EXTRACT-REC-CNT   PIC S9(04)  COMP                 01030022
                                                   VALUE ZERO.          01040020
           05  PV-RMS-EXTR-REC-CNT     PIC S9(04)  COMP                 01050020
                                                   VALUE ZERO.          01060020
           05  ABEND-CODE                    PIC S9(04) VALUE +0.       01070020
           05  PV-PARAGRAPH                  PIC  X(50).                01080020
           05  PV-MESSAGE                    PIC  X(50).                01090020
           05  PV-LOC-NBR                    PIC  9(04) VALUE 0.        01100020
           05  PV-REPLT-STR-NBR              PIC  9(04) VALUE 0.        01110020
           05  PV-DIST-NBR                   PIC  9(04) VALUE 0.        01120020
           05  PV-POS-RGST-BNK-QTY           PIC  9(03) VALUE 0.        01130020
           05  PV-FMT-CDE                    PIC  9(04) VALUE 0.        01140020
           05  PV-TOT-SLG-SQ-FTG-QTY         PIC  9(08) VALUE 0.        01150020
           05  PV-TOT-SQ-FTG-QTY             PIC  9(08) VALUE 0.        01160020
           05  PV-STP-ORD-DAYS-QTY           PIC  9(03) VALUE 0.        01170020
           05  PV-STRT-ORD-DAYS-QTY          PIC  9(03) VALUE 0.        01180020
           05  PV-MEDN-AGE-NBR               PIC  9(02)V9(02) VALUE 0.  01190020
           05  PV-EST-HH-INCM-AMT            PIC  9(11) VALUE 0.        01200020
                                                                        01210020
                                                                        01220020
       01  PS-PROGRAM-SWITCHES.                                         01230020
           05  PS-END-OF-RMS-FILE-SW PIC X   VALUE 'N'.                 01240020
               88 PS-END-OF-RMS-FILE         VALUE 'Y'.                 01250020
               88 PS-NOT-END-OF-RMS-FILE     VALUE 'N'.                 01260020
           05  PS-END-OF-XM-FILE-SW PIC X    VALUE 'N'.                 01270020
               88 PS-END-OF-XM-FILE          VALUE 'Y'.                 01280020
               88 PS-NOT-END-OF-XM-FILE      VALUE 'N'.                 01290020
                                                                        01300020
                                                                        01310020
      ******************************************************************01320020
      * STANDARD REPORT HEADING                                         01330020
      ******************************************************************01340020
                                                                        01350020
           COPY DPWS200O.                                               01360020
                                                                        01370020
       01  RPT-REPORT-HEADING.                                          01380020
           05  FILLER            PIC  X(84)    VALUE SPACES.            01390020
           05  FILLER            PIC  X(53)    VALUE                    01400020
             'XM LOCATION DATA INTEGRITY ERRORS'.                       01410020
                                                                        01420020
       01  RPT-COL-HEADING-1.                                           01430020
           05  FILLER            PIC  X(01) VALUE SPACE.                01440020
           05  FILLER            PIC  X(17) VALUE ALL '-'.              01450020
           05  FILLER            PIC  X(05) VALUE ' KEY '.              01460020
           05  FILLER            PIC  X(18) VALUE ALL '-'.              01470020
                                                                        01480020
       01  RPT-COL-HEADING-2.                                           01490020
           05  FILLER            PIC  X(01) VALUE SPACE.                01500020
           05  FILLER            PIC  X(32) VALUE SPACE.                01510020
           05  FILLER            PIC  X(10) VALUE 'LOCATION'.           01520020
           05  FILLER            PIC  X(07) VALUE 'TYPE'.               01530020
           05  FILLER            PIC  X(38) VALUE 'XM DOMAIN'.          01540020
           05  FILLER            PIC  X(39) VALUE                       01550020
              'RETEK MANAGEMENT SYSTEM - RMS'.                          01560020
           05  FILLER            PIC  X(08) VALUE 'MESSAGE'.            01570020
                                                                        01580020
       01  RPT-COL-HEADING-3.                                           01590020
           05  FILLER            PIC  X(01) VALUE SPACE.                01600020
           05  FILLER            PIC  X(42) VALUE SPACE.                01610020
           05  FILLER            PIC  X(07) VALUE 'CODE'.               01620020
           05  FILLER            PIC  X(38) VALUE 'VALUE'.              01630020
           05  FILLER            PIC  X(10) VALUE 'VALUE'.              01640020
                                                                        01650020
       01  DL-DETAIL-LINE.                                              01660020
           05  FILLER            PIC  X(01) VALUE SPACE.                01670020
           05  FILLER            PIC  X(34) VALUE SPACE.                01680020
           05  DL-LOCATION       PIC  ZZZZZ.                            01690020
           05  FILLER            PIC  X(04) VALUE SPACE.                01700020
           05  DL-TYP-CDE        PIC  X(03).                            01710020
           05  FILLER            PIC  X(03) VALUE SPACE.                01720020
           05  DL-XM-VALUE       PIC  X(35).                            01730020
           05  FILLER            PIC  X(03) VALUE SPACE.                01740020
           05  DL-RMS-VALUE      PIC  X(35).                            01750020
           05  FILLER            PIC  X(03) VALUE SPACE.                01760020
           05  DL-MESSAGE        PIC  X(75).                            01770020
                                                                        01780020
       01  RPT-TOTAL-LINE.                                              01790020
           05  FILLER            PIC  X(23) VALUE                       01800020
                                              'TOTAL NUMBER OF ERRORS '.01810020
           05  RPT-NBR-ERRORS    PIC  ZZZZZZ9.                          01820020
                                                                        01830020
       01  RPT-END-OF-REPORT-LINE.                                      01840020
           05  FILLER            PIC  X(21) VALUE                       01850020
                                                '*** END OF REPORT ***'.01860020
                                                                        01870020
      ***************************************************************** 01880020
      * RECORD LAYOUT FOR RMS LOCATION EXTRACT FILE                     01890020
      ***************************************************************** 01900020
      **   COPY HORD013.                                                01920020
0407PK     COPY HORD025.                                                01921022
                                                                        01922022
      ***************************************************************** 01930020
      * RECORD LAYOUT FOR THE XMT_LOC EXTRACT FILE.                     01940022
      * FILLER OF 01 BETWEEN COLUMNS INDICATES THAT THE PRIOR COLUMN    01950020
      * IS NULLABLE.                                                    01960020
      ***************************************************************** 01970020
0407PK     COPY XMRD016.                                                01971022
                                                                        02670020
      ***************************************************************** 02671022
      * COPYBOOK FOR THE LOCATION DATA INTEGRITY ERROR MESSAGE TABLE.   02672022
      ***************************************************************** 02673022
           COPY XMWS001.                                                02680020
                                                                        02690020
                                                                        02700020
      *----------------------------------------------------------------*02710020
       PROCEDURE DIVISION.                                              02720020
      *----------------------------------------------------------------*02730020
                                                                        02740020
           OPEN INPUT  RMS-LOC-EXTRACT-DATA-FILE                        02750020
                        XM-LOC-EXTRACT-DATA-FILE                        02760020
                OUTPUT XM-LOC-DATA-INTEGRITY-RPT.                       02770020
                                                                        02780020
           PERFORM 1000-INITIALIZE.                                     02790020
                                                                        02800020
           PERFORM 7000-READ-RMS-FILE.                                  02810020
           PERFORM 7100-READ-XM-FILE.                                   02820020
                                                                        02830020
                                                                        02840020
           PERFORM 2000-COMPARE-FILES                                   02850020
               UNTIL PS-END-OF-RMS-FILE AND                             02860020
                     PS-END-OF-XM-FILE.                                 02870020
                                                                        02880020
           PERFORM 8400-WRITE-TOTAL-LINE.                               02890020
                                                                        02900020
           CLOSE RMS-LOC-EXTRACT-DATA-FILE                              02910020
                  XM-LOC-EXTRACT-DATA-FILE                              02920020
                 XM-LOC-DATA-INTEGRITY-RPT.                             02930020
                                                                        02940020
           DISPLAY '***** ' PC-PGM-NAME ' STATISTICS *****'.            02950020
           MOVE PV-XM-EXTRACT-REC-CNT TO PV-DISPLAY-CNT.                02960022
           DISPLAY '   ** XM EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.    02970020
           MOVE PV-RMS-EXTR-REC-CNT    TO PV-DISPLAY-CNT.               02980020
           DISPLAY '   ** RMS    EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.02990020
           MOVE PV-NBR-ERRORS  TO PV-DISPLAY-CNT.                       03000020
                                                                        03010020
      *    IF THERE ARE ANY ERRORS TO BE REPORTED, ISSUE A              03020020
      *    RETURN CODE SO THAT AN E-MAIL WILL BE SENT IN A              03030020
      *    SUBSEQUENT JOB STEP.                                         03040020
                                                                        03050020
           IF PV-NBR-ERRORS > 0                                         03060020
               MOVE 4095 TO RETURN-CODE                                 03070020
           END-IF.                                                      03080020
                                                                        03090020
           DISPLAY '   ** ERRORS REPORTED: ' PV-DISPLAY-CNT.            03100020
           GOBACK.                                                      03110020
                                                                        03120020
                                                                        03130020
      ******************************************************************03140020
      * INITIALIZE AND WRITE REPORT HEADINGS                            03150020
      ******************************************************************03160020
       1000-INITIALIZE.                                                 03170020
                                                                        03180020
           ACCEPT PV-DATE-YYMMDD FROM DATE.                             03190020
           MOVE PV-YYMMDD-YY TO DP200O-RUN-YEAR.                        03200020
           MOVE PV-YYMMDD-MM TO DP200O-RUN-MONTH.                       03210020
           MOVE PV-YYMMDD-DD TO DP200O-RUN-DAY.                         03220020
                                                                        03230020
           ACCEPT PV-TIME-HHMMSS FROM TIME.                             03240020
           MOVE PV-HHMMSS-HH TO DP200O-RUN-HOUR.                        03250020
           MOVE PV-HHMMSS-MM TO DP200O-RUN-MINUTE.                      03260020
           MOVE PC-PGM-NAME  TO DP200O-PROGRAM-NAME.                    03270020
           MOVE 1            TO DP200O-REPORT-NUMBER.                   03280020
                                                                        03290020
           PERFORM 8100-WRITE-STD-RPT-HEADING.                          03300020
           PERFORM 8200-WRITE-REPORT-HEADING.                           03310020
           PERFORM 8300-WRITE-COLUMN-HEADING.                           03320020
                                                                        03330020
      ******************************************************************03340020
      * COMPARE RMS DATA TO XM DATA                                     03350020
      ******************************************************************03360020
       2000-COMPARE-FILES.                                              03370020
                                                                        03380020
           MOVE SPACES       TO PV-XM-VALUE                             03390020
                                PV-RMS-VALUE.                           03400020
                                                                        03410020
           IF PV-LOC-NBR = HO025-LOC-NBR                                03420020
      *****    LOCATION IS ON BOTH XM AND RMS.  *****                   03430020
               PERFORM 2100-COMPARE-FIELDS                              03440020
               PERFORM 7000-READ-RMS-FILE                               03450020
               PERFORM 7100-READ-XM-FILE                                03460020
           ELSE                                                         03470020
               IF PV-LOC-NBR < HO025-LOC-NBR                            03480020
      *****        LOCATION IS ON XM ONLY.      *****                   03490020
                   MOVE 26 TO PV-MSG-NBR                                03500020
                   PERFORM 3200-WRITE-REPORT-REC-XM                     03510020
                   PERFORM 7100-READ-XM-FILE                            03520020
              ELSE                                                      03530020
      *****        LOCATION IS ON RMS ONLY.     *****                   03540020
                   MOVE 27 TO PV-MSG-NBR                                03550020
                   PERFORM 3100-WRITE-REPORT-REC-RMS                    03560020
                   PERFORM 7000-READ-RMS-FILE                           03570020
              END-IF                                                    03580020
           END-IF.                                                      03590020
                                                                        03600020
      ******************************************************************03610020
      * COMPARE RMS DATA TO XM DATA FIELD BY FIELD                      03620020
      ******************************************************************03630020
      * DUE TO NULL DATA, NEED TO CHECK FOR THE POSSIBILITY THAT THE    03640020
0407PK* RMS FILE HAS ZERO OR SPACES WHEN THE XM EXTRACT HAS LOW VALUES. 03650029
      * THIS IS OK AND SHOULD NOT BE REGARDED AS AN ERROR.              03660029
      * ALSO IT IS POSSIBLE THAT FOR DATE FIELDS, XM EXTRACT WILL HAVE  03670020
0407PK* LOW VALUES WHEN RMS HAS 9999-09-09. THIS IS ALSO OK AND         03680030
      * SHOULD NOT BE REGARDED AS AN ERROR.                             03690020
      ******************************************************************03700020
       2100-COMPARE-FIELDS.                                             03710020
                                                                        03710124
           IF HO025-LOC-REPLT-STR-NBR NOT = PV-REPLT-STR-NBR            03730027
               MOVE HO025-LOC-REPLT-STR-NBR   TO PV-RMS-VALUE           03740027
               MOVE XM016-REPLT-STR-NBR       TO PV-XM-VALUE            03750027
               MOVE 1                         TO PV-MSG-NBR             03760027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03770027
           END-IF.                                                      03771027
           IF HO025-LOC-DIST-NBR NOT = PV-DIST-NBR                      03780027
               MOVE HO025-LOC-DIST-NBR  TO PV-RMS-VALUE                 03790027
               MOVE XM016-DIST-NBR      TO PV-XM-VALUE                  03800027
               MOVE 2                   TO PV-MSG-NBR                   03810027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03820027
           END-IF.                                                      03821027
           IF (HO025-LOC-TYP-CDE NOT = XM016-LOC-TYP-CDE                03830027
               AND NOT (HO025-LOC-TYP-CDE     = SPACES                  03840030
                        AND XM016-LOC-TYP-CDE = LOW-VALUES))            03850030
               MOVE HO025-LOC-TYP-CDE    TO PV-RMS-VALUE                03860027
               MOVE XM016-LOC-TYP-CDE    TO PV-XM-VALUE                 03870027
               MOVE 3                    TO PV-MSG-NBR                  03880027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03890027
           END-IF.                                                      03891027
           IF (HO025-LOC-NM      NOT = XM016-LOC-NM                     03900027
               AND NOT (HO025-LOC-NM       = SPACES                     03910030
                        AND XM016-LOC-NM   = LOW-VALUES))               03920030
               MOVE HO025-LOC-NM          TO PV-RMS-VALUE               03930027
               MOVE XM016-LOC-NM          TO PV-XM-VALUE                03940027
               MOVE 4                     TO PV-MSG-NBR                 03950027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03960027
           END-IF.                                                      03961027
           IF (HO025-LOC-5-ABBR-NM NOT = XM016-LOC-5-ABBR-NM            03970027
               AND NOT (HO025-LOC-5-ABBR-NM     = SPACES                03980030
                        AND XM016-LOC-5-ABBR-NM = LOW-VALUES))          03990030
               MOVE HO025-LOC-5-ABBR-NM   TO PV-RMS-VALUE               04000027
               MOVE XM016-LOC-5-ABBR-NM TO PV-XM-VALUE                  04010027
               MOVE 5                     TO PV-MSG-NBR                 04020027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04030027
           END-IF.                                                      04031027
           IF (HO025-LOC-LNE-1-ADDR NOT = XM016-LNE-1-ADDR              04040027
               AND NOT (HO025-LOC-LNE-1-ADDR = SPACES                   04050030
                        AND XM016-LNE-1-ADDR = LOW-VALUES))             04060030
               MOVE HO025-LOC-LNE-1-ADDR    TO PV-RMS-VALUE             04070027
               MOVE XM016-LNE-1-ADDR        TO PV-XM-VALUE              04080027
               MOVE 6                       TO PV-MSG-NBR               04090027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04100027
           END-IF.                                                      04101027
           IF (HO025-LOC-LNE-2-ADDR NOT = XM016-LNE-2-ADDR              04110027
               AND NOT (HO025-LOC-LNE-2-ADDR = SPACES                   04120030
                        AND XM016-LNE-2-ADDR = LOW-VALUES))             04130030
               MOVE HO025-LOC-LNE-2-ADDR    TO PV-RMS-VALUE             04140027
               MOVE XM016-LNE-2-ADDR        TO PV-XM-VALUE              04150027
               MOVE 7                       TO PV-MSG-NBR               04160027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04170027
           END-IF.                                                      04171027
           IF (HO025-LOC-CITY       NOT = XM016-CTY-NM                  04210027
               AND NOT (HO025-LOC-CITY     = SPACES                     04220030
                        AND XM016-CTY-NM   = LOW-VALUES))               04230030
               MOVE HO025-LOC-CITY        TO PV-RMS-VALUE               04240027
               MOVE XM016-CTY-NM          TO PV-XM-VALUE                04250027
               MOVE 8                     TO PV-MSG-NBR                 04260027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04270027
           END-IF.                                                      04271027
           IF (HO025-LOC-ST-CDE     NOT = XM016-ST-CDE                  04280027
               AND NOT (HO025-LOC-ST-CDE   = SPACES                     04290030
                        AND XM016-ST-CDE   = LOW-VALUES))               04300030
               MOVE HO025-LOC-ST-CDE      TO PV-RMS-VALUE               04310027
               MOVE XM016-ST-CDE          TO PV-XM-VALUE                04320027
               MOVE 9                     TO PV-MSG-NBR                 04330027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04340027
           END-IF.                                                      04341027
           IF (HO025-LOC-ZIP-10-CDE     NOT = XM016-ZIP-10-CDE          04350027
               AND NOT (HO025-LOC-ZIP-10-CDE = SPACES                   04360030
                        AND XM016-ZIP-10-CDE = LOW-VALUES))             04370030
               MOVE HO025-LOC-ZIP-10-CDE     TO PV-RMS-VALUE            04380027
               MOVE XM016-ZIP-10-CDE         TO PV-XM-VALUE             04390027
               MOVE 10                       TO PV-MSG-NBR              04400027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04410027
           END-IF.                                                      04411027
           IF (HO025-LOC-OPEN-DATE        NOT = XM016-OPN-DTE           04420027
               AND NOT (HO025-LOC-OPEN-DATE   = '9999-09-09'            04440031
                        AND XM016-OPN-DTE     = LOW-VALUES))            04450031
               MOVE HO025-LOC-OPEN-DATE      TO PV-RMS-VALUE            04460027
               MOVE XM016-OPN-DTE            TO PV-XM-VALUE             04470027
               MOVE 11                       TO PV-MSG-NBR              04480027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04490027
           END-IF.                                                      04491027
           IF (HO025-LOC-CLOSE-DATE      NOT = XM016-CLO-DTE            04500027
               AND NOT (HO025-LOC-CLOSE-DATE = '9999-09-09'             04520031
                        AND XM016-CLO-DTE    = LOW-VALUES))             04530031
               MOVE HO025-LOC-CLOSE-DATE    TO PV-RMS-VALUE             04540027
               MOVE XM016-CLO-DTE           TO PV-XM-VALUE              04550027
               MOVE 12                      TO PV-MSG-NBR               04560027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04570027
           END-IF.                                                      04570127
           IF (HO025-LOC-LOC-10-ABBR-NM NOT = XM016-LOC-10-ABBR-NM      04571027
               AND NOT (HO025-LOC-LOC-10-ABBR-NM = LOW-VALUES           04572027
                        AND XM016-LOC-10-ABBR-NM = SPACES))             04573027
               MOVE HO025-LOC-LOC-10-ABBR-NM TO PV-RMS-VALUE            04574027
               MOVE XM016-LOC-10-ABBR-NM TO PV-XM-VALUE                 04575027
               MOVE 13                    TO PV-MSG-NBR                 04576027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04577027
           END-IF.                                                      04578027
           IF (HO025-LOC-PHONE-NUMBER      NOT = XM016-PHN-NBR          04641028
               AND NOT (HO025-LOC-PHONE-NUMBER = SPACES                 04642032
                        AND XM016-PHN-NBR      = LOW-VALUES))           04643032
               MOVE HO025-LOC-PHONE-NUMBER    TO PV-RMS-VALUE           04644027
               MOVE XM016-PHN-NBR             TO PV-XM-VALUE            04645027
               MOVE 14                        TO PV-MSG-NBR             04646027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04647027
           END-IF.                                                      04648028
           IF (HO025-LOC-MGR-NM       NOT = XM016-MGR-NM                04650028
               AND NOT (HO025-LOC-MGR-NM  = SPACES                      04660032
                        AND XM016-MGR-NM  = LOW-VALUES))                04670032
               MOVE HO025-LOC-MGR-NM     TO PV-RMS-VALUE                04680027
               MOVE XM016-MGR-NM         TO PV-XM-VALUE                 04690027
               MOVE 15                   TO PV-MSG-NBR                  04700027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04710027
           END-IF.                                                      04711028
           IF (HO025-LOC-FINCL-CLO-DTE  NOT = XM016-FINCL-CLO-DTE       04720028
               AND NOT (HO025-LOC-FINCL-CLO-DTE = '9999-09-09'          04740032
                        AND XM016-FINCL-CLO-DTE = LOW-VALUES))          04750032
               MOVE HO025-LOC-FINCL-CLO-DTE     TO PV-RMS-VALUE         04760027
               MOVE XM016-FINCL-CLO-DTE         TO PV-XM-VALUE          04770027
               MOVE 16                          TO PV-MSG-NBR           04780027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04790027
           END-IF.                                                      04791028
           IF (HO025-LOC-MN-SLS-INCL-DTE                                04800028
               NOT = XM016-MN-SLS-INCL-DTE                              04810027
               AND NOT (HO025-LOC-MN-SLS-INCL-DTE = '9999-09-09'        04830032
                        AND XM016-MN-SLS-INCL-DTE = LOW-VALUES))        04840032
               MOVE HO025-LOC-MN-SLS-INCL-DTE    TO PV-RMS-VALUE        04850027
               MOVE XM016-MN-SLS-INCL-DTE        TO PV-XM-VALUE         04860027
               MOVE 17                           TO PV-MSG-NBR          04870027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04880027
           END-IF.                                                      04881028
           IF (HO025-LOC-YR-SLS-INCL-DTE                                04890028
                NOT = XM016-YR-SLS-INCL-DTE                             04900027
               AND NOT (HO025-LOC-YR-SLS-INCL-DTE = '9999-09-09'        04920032
                        AND XM016-YR-SLS-INCL-DTE = LOW-VALUES))        04930032
               MOVE HO025-LOC-YR-SLS-INCL-DTE    TO PV-RMS-VALUE        04940027
               MOVE XM016-YR-SLS-INCL-DTE        TO PV-XM-VALUE         04950027
               MOVE 18                           TO PV-MSG-NBR          04960027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04970027
           END-IF.                                                      04971028
           IF (HO025-LOC-OCPNC-DTE                                      04980028
                NOT = XM016-OCPNC-DTE                                   04990027
               AND NOT (HO025-LOC-OCPNC-DTE     = '9999-09-09'          05010032
                        AND XM016-OCPNC-DTE     = LOW-VALUES))          05020032
               MOVE HO025-LOC-OCPNC-DTE        TO PV-RMS-VALUE          05030027
               MOVE XM016-OCPNC-DTE            TO PV-XM-VALUE           05040027
               MOVE 19                         TO PV-MSG-NBR            05050027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05060027
           END-IF.                                                      05061028
           IF (HO025-LOC-DST-IND          NOT = XM016-DST-IND           05070028
               AND NOT (HO025-LOC-DST-IND     = SPACES                  05080032
                        AND XM016-DST-IND     = LOW-VALUES))            05090032
               MOVE HO025-LOC-DST-IND        TO PV-RMS-VALUE            05100027
               MOVE XM016-DST-IND            TO PV-XM-VALUE             05110027
               MOVE 20                       TO PV-MSG-NBR              05120027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05130027
           END-IF.                                                      05131028
           IF (HO025-LOC-E-BUS-IND        NOT = XM016-E-BUS-IND         05140028
               AND NOT (HO025-LOC-E-BUS-IND   = SPACES                  05150032
                        AND XM016-E-BUS-IND   = LOW-VALUES))            05160032
               MOVE HO025-LOC-E-BUS-IND      TO PV-RMS-VALUE            05170028
               MOVE XM016-E-BUS-IND          TO PV-XM-VALUE             05180028
               MOVE 21                       TO PV-MSG-NBR              05190028
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05200027
           END-IF.                                                      05201028
           IF (HO025-LOC-FMT-CDE          NOT = PV-FMT-CDE              05210028
               AND NOT (HO025-LOC-FMT-CDE     = SPACES                  05220032
                        AND PV-FMT-CDE        = LOW-VALUES))            05230032
               MOVE HO025-LOC-FMT-CDE        TO PV-RMS-VALUE            05240027
               MOVE PV-FMT-CDE               TO PV-XM-VALUE             05250027
               MOVE 22                       TO PV-MSG-NBR              05260027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05270027
           END-IF.                                                      05271028
           IF (HO025-LOC-STD-TM-ZN-CDE     NOT = XM016-STD-TM-ZN-CDE    05280028
               AND NOT (HO025-LOC-STD-TM-ZN-CDE   = SPACES              05290032
                        AND XM016-STD-TM-ZN-CDE   = LOW-VALUES))        05300032
               MOVE HO025-LOC-STD-TM-ZN-CDE      TO PV-RMS-VALUE        05310027
               MOVE XM016-STD-TM-ZN-CDE          TO PV-XM-VALUE         05320027
               MOVE 23                           TO PV-MSG-NBR          05330027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05340027
           END-IF.                                                      05341028
           IF (HO025-LOC-SLS-INCL-DTE-CDE NOT =                         05350028
                XM016-SLS-INCL-DTE-CDE                                  05360027
               AND NOT (HO025-LOC-SLS-INCL-DTE-CDE = SPACES             05370032
                        AND XM016-SLS-INCL-DTE-CDE = LOW-VALUES))       05380032
               MOVE HO025-LOC-SLS-INCL-DTE-CDE    TO PV-RMS-VALUE       05390027
               MOVE XM016-SLS-INCL-DTE-CDE        TO PV-XM-VALUE        05400027
               MOVE 24                            TO PV-MSG-NBR         05410027
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       05420027
           END-IF.                                                      05420128
                                                                        05420232
0407PK     IF XM016-OPNG-GP-MN-IS-NULL                                  05420332
0407PK         MOVE ZERO TO XM016-NW-STR-OPNG-GP-MN                     05420432
0407PK     END-IF.                                                      05420532
0407PK     IF HO025-LOC-OPN-STR-MN NOT = XM016-NW-STR-OPNG-GP-MN        05421028
0407PK         MOVE HO025-LOC-OPN-STR-MN    TO PV-RMS-VALUE             05424028
0407PK         MOVE XM016-NW-STR-OPNG-GP-MN TO PV-XM-VALUE              05426028
0407PK         MOVE 28                      TO PV-MSG-NBR               05426128
0407PK         PERFORM 3000-WRITE-REPORT-REC-BOTH                       05427028
           END-IF.                                                      05427128
                                                                        05427232
0407PK     IF XM016-OPNG-GP-YR-IS-NULL                                  05427632
0407PK         MOVE ZERO TO XM016-NW-STR-OPNG-GP-YR                     05427732
0407PK     END-IF.                                                      05427832
0407PK     IF HO025-LOC-OPN-STR-YR NOT = XM016-NW-STR-OPNG-GP-YR        05428028
0407PK         MOVE HO025-LOC-OPN-STR-YR    TO PV-RMS-VALUE             05429428
0407PK         MOVE XM016-NW-STR-OPNG-GP-YR TO PV-XM-VALUE              05429528
0407PK         MOVE 29                      TO PV-MSG-NBR               05429928
0407PK         PERFORM 3000-WRITE-REPORT-REC-BOTH                       05430028
           END-IF.                                                      05431027
                                                                        05440020
                                                                        05450020
      ******************************************************************05460020
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON BOTH RMS AND XM.    05470020
      ******************************************************************05480020
       3000-WRITE-REPORT-REC-BOTH.                                      05490020
                                                                        05500020
           MOVE HO025-LOC-NBR        TO DL-LOCATION.                    05510020
           MOVE HO025-LOC-TYP-CDE    TO DL-TYP-CDE.                     05520020
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    05530020
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05540020
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05550020
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05560020
                                                                        05570020
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05580020
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05590020
               PERFORM 8200-WRITE-REPORT-HEADING                        05600020
               PERFORM 8300-WRITE-COLUMN-HEADING                        05610020
           END-IF.                                                      05620020
                                                                        05630020
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          05640020
                                                                        05650020
      ******************************************************************05660020
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON RMS ONLY.           05670020
      ******************************************************************05680020
       3100-WRITE-REPORT-REC-RMS.                                       05690020
                                                                        05700020
           MOVE HO025-LOC-NBR        TO DL-LOCATION.                    05710020
           MOVE HO025-LOC-TYP-CDE    TO DL-TYP-CDE.                     05720020
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    05730020
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05740020
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05750020
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05760020
                                                                        05770020
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05780020
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05790020
               PERFORM 8200-WRITE-REPORT-HEADING                        05800020
               PERFORM 8300-WRITE-COLUMN-HEADING                        05810020
           END-IF.                                                      05820020
                                                                        05830020
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          05840020
                                                                        05850020
      ******************************************************************05860020
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON XM ONLY.            05870020
      ******************************************************************05880020
       3200-WRITE-REPORT-REC-XM.                                        05890020
                                                                        05900020
           MOVE XM016-LOC-NBR        TO DL-LOCATION.                    05910022
           MOVE XM016-LOC-TYP-CDE    TO DL-TYP-CDE.                     05920022
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    05930020
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05940020
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05950020
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05960020
                                                                        05970020
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05980020
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05990020
               PERFORM 8200-WRITE-REPORT-HEADING                        06000020
               PERFORM 8300-WRITE-COLUMN-HEADING                        06010020
           END-IF.                                                      06020020
                                                                        06030020
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          06040020
                                                                        06050020
      ******************************************************************06060020
      * READ RMS INPUT FILE                                             06070020
      ******************************************************************06080020
       7000-READ-RMS-FILE.                                              06090020
           IF PS-NOT-END-OF-RMS-FILE                                    06100020
             READ RMS-LOC-EXTRACT-DATA-FILE                             06110020
      *        INTO HORD007-RECORD                                      06120020
               INTO HO025-LOCATION-DATA                                 06130020
                 AT END SET PS-END-OF-RMS-FILE TO TRUE                  06140020
                 MOVE 9999 TO HO025-LOC-NBR                             06150020
           END-IF.                                                      06160020
           IF PS-NOT-END-OF-RMS-FILE                                    06170020
              ADD +1 TO PV-RMS-EXTR-REC-CNT                             06180020
           END-IF.                                                      06190020
                                                                        06200020
      ******************************************************************06210020
      * READ XM INPUT FILE                                              06220020
      ******************************************************************06230020
       7100-READ-XM-FILE.                                               06240020
           IF PS-NOT-END-OF-XM-FILE                                     06250020
             READ XM-LOC-EXTRACT-DATA-FILE                              06260020
               INTO XM016-LOC-UNLOAD                                    06270022
                 AT END SET PS-END-OF-XM-FILE TO TRUE                   06280020
                 MOVE 9999 TO PV-LOC-NBR                                06290020
           END-IF.                                                      06300020
           IF PS-NOT-END-OF-XM-FILE                                     06310020
              ADD +1 TO PV-XM-EXTRACT-REC-CNT                           06320022
              PERFORM 7110-LOAD-XM-WORK-FIELDS                          06330020
           END-IF.                                                      06340020
                                                                        06350020
       7110-LOAD-XM-WORK-FIELDS.                                        06360020
           MOVE XM016-LOC-NBR              TO PV-LOC-NBR.               06370022
           MOVE XM016-REPLT-STR-NBR        TO PV-REPLT-STR-NBR.         06380022
           MOVE XM016-DIST-NBR             TO PV-DIST-NBR.              06390022
           MOVE XM016-POS-RGST-BNK-QTY     TO PV-POS-RGST-BNK-QTY.      06400022
           MOVE XM016-FMT-CDE              TO PV-FMT-CDE.               06410022
           MOVE XM016-TOT-SLG-SQ-FTG-QTY TO PV-TOT-SLG-SQ-FTG-QTY.      06420022
           MOVE XM016-TOT-SQ-FTG-QTY       TO PV-TOT-SQ-FTG-QTY.        06430022
           MOVE XM016-STP-ORD-DAYS-QTY     TO PV-STP-ORD-DAYS-QTY.      06440022
           MOVE XM016-STRT-ORD-DAYS-QTY    TO PV-STRT-ORD-DAYS-QTY.     06450022
           MOVE XM016-MEDN-AGE-NBR         TO PV-MEDN-AGE-NBR.          06460022
           MOVE XM016-EST-HH-INCM-AMT      TO PV-EST-HH-INCM-AMT.       06470022
                                                                        06471022
      ******************************************************************06480020
      * WRITE REPORT DETAIL LINE                                        06490020
      ******************************************************************06500020
       8000-WRITE-RPT-DETAIL-LINE.                                      06510020
                                                                        06520020
           WRITE XM-LOC-RPT-LINE FROM DL-DETAIL-LINE                    06530020
               AFTER ADVANCING PV-ADVANCE.                              06540020
           ADD PV-ADVANCE TO PV-LINE-CNT.                               06550020
           MOVE +1        TO PV-ADVANCE.                                06560020
           ADD +1 TO PV-NBR-ERRORS.                                     06570020
                                                                        06580020
                                                                        06590020
      ******************************************************************06600020
      * PRINT THE KOHLS STANDARD REPORT HEADING.                        06610020
      ******************************************************************06620020
       8100-WRITE-STD-RPT-HEADING.                                      06630020
                                                                        06640020
           ADD 1            TO PV-PAGE-CNT.                             06650020
           MOVE PV-PAGE-CNT TO DP200O-PAGE-NUMBER.                      06660020
           WRITE XM-LOC-RPT-LINE FROM DP200O-STANDRD-HEADER-200-COLS    06670020
               AFTER ADVANCING PAGE.                                    06680020
           MOVE +1          TO PV-LINE-CNT.                             06690020
                                                                        06700020
                                                                        06710020
      ******************************************************************06720020
      * REPORT HEADING                                                  06730020
      ******************************************************************06740020
       8200-WRITE-REPORT-HEADING.                                       06750020
                                                                        06760020
           WRITE XM-LOC-RPT-LINE FROM RPT-REPORT-HEADING                06770020
               AFTER ADVANCING 1 LINES.                                 06780020
           ADD +1 TO PV-LINE-CNT.                                       06790020
                                                                        06800020
                                                                        06810020
      ******************************************************************06820020
      * REPORT COLUMN HEADING                                           06830020
      ******************************************************************06840020
       8300-WRITE-COLUMN-HEADING.                                       06850020
                                                                        06860020
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-1                 06870020
               AFTER ADVANCING 2 LINES.                                 06880020
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-2                 06890020
               AFTER ADVANCING 1 LINES.                                 06900020
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-3                 06910020
               AFTER ADVANCING 1 LINES.                                 06920020
           ADD  +4 TO PV-LINE-CNT.                                      06930020
           MOVE +2 TO PV-ADVANCE.                                       06940020
                                                                        06950020
                                                                        06960020
      ******************************************************************06970020
      * REPORT TOTAL LINE                                               06980020
      ******************************************************************06990020
       8400-WRITE-TOTAL-LINE.                                           07000020
                                                                        07010020
           MOVE PV-NBR-ERRORS TO RPT-NBR-ERRORS.                        07020020
                                                                        07030020
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            07040020
               PERFORM 8100-WRITE-STD-RPT-HEADING                       07050020
               PERFORM 8200-WRITE-REPORT-HEADING                        07060020
               PERFORM 8300-WRITE-COLUMN-HEADING                        07070020
           END-IF.                                                      07080020
                                                                        07090020
           WRITE XM-LOC-RPT-LINE FROM RPT-TOTAL-LINE                    07100020
               AFTER ADVANCING 2 LINES.                                 07110020
           ADD +2 TO PV-LINE-CNT.                                       07120020
                                                                        07130020
           WRITE XM-LOC-RPT-LINE FROM RPT-END-OF-REPORT-LINE            07140020
               AFTER ADVANCING 2 LINES.                                 07150020
                                                                        07160020
