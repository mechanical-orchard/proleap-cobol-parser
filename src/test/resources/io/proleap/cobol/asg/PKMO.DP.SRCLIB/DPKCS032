       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKCS032.                                                 
       AUTHOR.        M. FURLICK.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-15-92.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                *        
      *    Z032 - "NEW COPY" MNEMONIC ARCHITECTURE INFORMATION.        *        
      *                                                                *        
      *    THIS IS A SCREENLESS PROGRAM THAT UPDATES THE ARCHITECTURE  *        
      *    VSAM FILES FROM THE DB2 TABLES FOR A SPECIFIC MNEMONIC.     *        
      *    IT IS INVOKED FROM DPKCS029 (MNEMONIC CRITERIA SPEC SCREEN) *        
      *    WHICH PASSES THE MNEMONIC TO BE UPDATED.  BOTH "ADDS" AND   *        
      *    "UPDATES" ARE PERFORMED; NO "DELETES" ARE PERFORMED.        *        
      *    ("DELETES" ARE PERFORMED, ALONG WITH "ADDS" AND "UPDATES"   *        
      *    IN THE MASS "NEW COPY" MNEMONIC ARCHITECTURE PROGRAM,       *        
      *    DPKCS033.)                                                  *        
      *                                                                *        
      *                                                                *        
      *    04/08/96 -  ADD LOGIC FOR CICS24X7 PROCESSING.              *        
      *                ARCHITECTURE CHANGES TO ALLOW CICS TO BE UP     *        
      *                24HRS/DAY, 7DAYS/WEEK.                          *        
      *                ALSO ADDED LOGIC FOR FUNCTION KEY PROJECT.      *        
      *                1) WHEN UPDATING THE TMNEMONIC KSDS VSAM FILE   *        
      *                   FROM THE TCICSROUTE DB2 TABLE, ALSO MOVE THE *        
      *                   VALUE OF THE NEW FK-DISPN-IND FIELD.         *        
      *                2) ADD NEW CICSARCH AUDIT FILE, DPCAUDIT.       *        
      *                   (SEE COPYBOOK DPRD520).                      *        
      *                **********************************************  *        
      *                NOTE: ON AN ABEND, THERE IS NOT A FACILITY TO   *        
      *                ROLLBACK THE VSAM FILES; ONLY THE DB2 TABLES    *        
      *                ARE PRESENTLY RESTORED ON AN ABEND CONDITION.   *        
      *                **********************************************  *        
      *                NOTE: PARAGRAPH NUMBERING IN PROGRAMS DPKCS032  *        
      *                AND DPKCS033 VARY SOMEWHAT FROM STANDARDS, IN   *        
      *                ORDER TO ACCOMODATE THE PROCESSING SIMILARITES  *        
      *                BETWEEN THE TWO PROGRAMS.  THIS WAS DONE WITH   *        
      *                EASE OF FUTURE MAINTAINABILITY IN MIND.         *        
      *                ANY FUTURE MODIFICATIONS MADE TO THE "UPDATE"   *        
      *                PART OF 1100-PROCESS-REQUEST LOGIC, IN EITHER   *        
      *                ONE OF THESE PROGRAMS, WILL MOST LIKELY BE      *        
      *                NEEDED TO THE OTHER PROGRAM AS WELL.            *        
      *                **********************************************  *        
      *                BY: CHERI MASTEL   TKMA235                      *        
      ******************************************************************        
      *  09/08/98  JJR  CORRECTED THE REBUILDING OF MENUITEMS AND               
      *                 HANDLING OF DELETES.                                    
      ******************************************************************        
      *  02/25/99  JJR  CHANGED PC-DPCAUDIT-MENUITEM-LEN FROM +118              
      *                                                    TO +144.             
      *                 CHANGED PC-DPCAUDIT-MENUITEM-LEN FROM +908              
      *                                                    TO +934.             
      ******************************************************************        
      *  09/09/99  JJR  ADD ACCURATE LOCATION REQUIRED INDICATOR.               
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.        
           05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072          
                                                           COMP SYNC.           
           05  PC-CHAR-MAX                    PIC S9(04)   VALUE +30            
                                                           COMP SYNC.           
           05  PC-MAX-RETRIES                 PIC 9(02)    VALUE  3.            
           05  PC-TSYMSG-NUMBERS.                                               
               10 PC-TSYMSG-00148             PIC 9(05)    VALUE 00148.         
           05  PC-MNEMONIC-FILE               PIC X(8)     VALUE                
                                                           'DPMNEMIC'.          
           05  PC-MENUITEM-FILE               PIC X(8)     VALUE                
                                                           'DPMNUITM'.          
           05  PC-DPCAUDIT-FILE               PIC X(8)     VALUE                
                                                           'DPCAUDIT'.          
           05  PC-CURRENT-PROGRAM-NAME        PIC X(8)     VALUE                
                                                           'DPKCS032'.          
           05  PC-MENU-TRANID                 PIC X(4)     VALUE                
                                                           'Z007'.              
           05  PC-DPCAUDIT-MENUITEM-LEN       PIC S9(4)    VALUE +0144.         
           05  PC-DPCAUDIT-MNEMONIC-LEN       PIC S9(4)    VALUE +0934.         
           05  PC-VSAM-ACTION.                                                  
               10  PC-VSAM-ACTION-A-ADD       PIC X(1)     VALUE 'A'.           
               10  PC-VSAM-ACTION-U-UPDATE    PIC X(1)     VALUE 'U'.           
               10  PC-VSAM-ACTION-R-REPLACE   PIC X(1)     VALUE 'R'.           
               10  PC-VSAM-ACTION-D-DELETE    PIC X(1)     VALUE 'D'.           
           05  PC-VSAM-REC-TYPE.                                                
               10  PC-VSAM-REC-TYPE-MNEMONIC  PIC X(8)     VALUE                
                                                           'MNEMONIC'.          
               10  PC-VSAM-REC-TYPE-MENUITEM  PIC X(8)     VALUE                
                                                           'MENUITEM'.          
               10  PC-VSAM-REC-TYPE-MENUNAME  PIC X(8)     VALUE                
                                                           'MENUNAME'.          
           05  PC-UPD-VSAM-IND.                                                 
               10  PC-N-NOT-NEEDED            PIC X(1)     VALUE 'N'.           
               10  PC-Y-UPDATE-REQUESTED      PIC X(1)     VALUE 'Y'.           
               10  PC-D-DELETE-REQUESTED      PIC X(1)     VALUE 'D'.           
                                                                                
           05  PC-DB2-PKG-CDE.                                                  
               10  PC-P-PROD-TBLS-ONLY        PIC X(1)     VALUE 'P'.           
               10  PC-A-ARCHIVE-TBLS-IF-EXIST PIC X(1)     VALUE 'A'.           
                                                                                
           05  PC-ZERO-TMSTP.                                                   
               10  PC-ZERO-TMESTP-DATE        PIC X(11)    VALUE                
                                                          '0000-00-00-'.        
               10  PC-ZERO-TMESTP-TIME        PIC X(15)    VALUE                
                                                      '00:00:00:000000'.        
                                                                                
      *        *********************************************************        
      *        * SQLCODE RETURN VALUE INTERPRETATION                            
      *        *********************************************************        
           05  SQL-RETURN-CODE             PIC S9(9)       COMP-4.              
               88  SQL-SUCCESSFUL                          VALUE +0.            
               88  SQL-ROW-FOUND                           VALUE +0.            
               88  SQL-ROW-NOT-FOUND                       VALUE +100.          
               88  SQL-WARNING                             VALUE +100           
                                                                 +558           
                                                                 +560.          
               88  SQL-DUPLICATE-ROW                       VALUE -803.          
               88  SQL-MORE-THAN-1-ROW                     VALUE -811.          
               88  SQL-NOT-AVAILABLE                       VALUE -911.          
               88  SQL-DB2-DOWN                            VALUE -923.          
               88  SQL-NULL-VALUE                          VALUE -305.          
               88  SQL-DEPENDENT-ROW                       VALUE -532.          
                                                                                
       01  PM-PROGRAM-MESSAGES.                                                 
           05  PM-PROGRAM-MESSAGE-1           PIC X(78)    VALUE                
           'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
           05  PS-MNEMONIC-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-MNEMONIC-ERROR                       VALUE 'Y'.           
               88  PS-NO-MNEMONIC-ERROR                    VALUE 'N'.           
           05  PS-VSAM-MNEMONIC-FOUND-SW      PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MNEMONIC-FOUND                  VALUE 'Y'.           
               88  PS-VSAM-MNEMONIC-NOT-FOUND              VALUE 'N'.           
           05  PS-VSAM-MENUITEM-FOUND-SW      PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MENUITEM-FOUND                  VALUE 'Y'.           
               88  PS-VSAM-MENUITEM-NOT-FOUND              VALUE 'N'.           
           05  PS-VSAM-MNEMONIC-UPDT-SW       PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MNEMONIC-UPDT-OK                VALUE 'Y'.           
               88  PS-VSAM-MNEMONIC-UPDT-ERR               VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-NAV-MNEMONIC                PIC X(08)    VALUE SPACES.        
           05  PV-TRANS-ID-CODE               PIC X(04)    VALUE SPACES.        
           05  PV-DESCRIPTION                 PIC X(30)    VALUE SPACES.        
           05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.        
           05  PV-MENUITEM-KEY                PIC X(12)    VALUE SPACES.        
           05  PV-RETRY-COUNTER               PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SQL-SUB                     PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SUB                         PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-RESP                        PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-NULL-IND                    PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-AID-SEQUENCE-NMBRX.                                           
               10  PV-AID-SEQUENCE-NMBR       PIC S9(2).                        
                                                                                
           05  PV-TITLE-TABLE.                                                  
               10  PV-TITLE-CHAR              PIC X(1)                          
                                              OCCURS 30 TIMES.                  
           05  PV-CHAR-COUNT                  PIC S9(4)    COMP.                
           05  PV-STRING-POINTER              PIC S9(4)    COMP.                
           05  PV-DPCAUDIT-REC-LEN            PIC S9(4)    COMP.                
           05  PV-DPCAUDIT-RBA                PIC S9(9)    COMP SYNC.           
           05  PV-CURRENT-TMESTP-FOR-TRAN     PIC X(26).                        
           05  PV-ABEND-INFO.                                                   
               10  PV-ABEND-MNEMONIC          PIC X(8)     VALUE SPACE.         
               10  FILLER                     PIC X(2)     VALUE ', '.          
               10  PV-ABEND-TIMESTAMP         PIC X(26)    VALUE SPACE.         
                                                                                
                                                                                
      ******************************************************************        
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *        
      ******************************************************************        
           COPY DPWS006.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY DPWS055.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    APPLICATION SPECIFIC COMMAREA                                        
      *****************************************************************         
               10  DP032-SPECIFIC-COMMAREA.                                     
                   15  DP032-APPL-CURSOR-POSITION PIC S9(04) COMP.              
                                                                                
                   15  DP032-MNEMONIC             PIC X(8).                     
                                                                                
                   15  FILLER                     PIC X(2855).                  
                                                                                
                                                                                
      *****************************************************************         
      *    MNEMONIC VSAM RECORD LAYOUT.                                         
      *****************************************************************         
           COPY DPRD503.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    MENUITEM VSAM RECORD LAYOUT.                                         
      *****************************************************************         
           COPY DPRD504.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    DPCAUDIT - CICSARCH AUDIT LOG RECORD LAYOUT.                         
      *****************************************************************         
           COPY DPRD520.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    ABEND PROCESSING COPYBOOK.                                           
      *****************************************************************         
           COPY DPWS013.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    STANDARD ATTENTION IDENTIFIER LIST                                   
      *****************************************************************         
           COPY DFHAID.                                                         
                                                                                
                                                                                
      *****************************************************************         
      *    FUNCTION KEYS COPYBOOK.                                              
      *****************************************************************         
           COPY DPWS016.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 ROUTE TABLE.                                                     
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TCICSRTE                                                
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 MNEMONIC TABLE                                                   
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMNMNC                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 MENU ITEM TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMNUITM                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATIONS TABLE                                             
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    CICSROUTE CURSOR                                                     
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE ROUTE-CURSOR CURSOR FOR                                 
                SELECT  AID_SEQUENCE_NMBR                                       
                      , DEST_MNEMONIC_CODE                                      
                      , ACTION_CODE                                             
                      , ROUTE_STACK_INDCT                                       
                      , FUNC_KEY_LBL_NAME                                       
                      , IN_APLCTN_COMM_CDE                                      
                      , FUNC_KY_DISPN_IND                                       
                FROM    TCICSROUTE                                              
                WHERE   MNEMONIC_CODE   =  :DP032-MNEMONIC                      
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    MENUITEM CURSOR FOR PROCESSING ALL THE MNEMONIC ITEMS LISTED         
      *    ON THE **MENU** ENTERED FOR THIS REQUEST.                            
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE MENUITEM-CURSOR CURSOR FOR                              
                SELECT  A.MENU_NAME                                             
                      , A.SEQUENCE_NMBR                                         
                      , A.ITEM_MNEMONIC_CODE                                    
                      , A.DESC                                                  
                      , A.IN_APLCTN_COMM_CDE                                    
                      , B.TRANS_ID_CODE                                         
                      , B.AVAIL_INDCT                                           
                      , B.ACRT_LOC_REQR_IND                                     
                      , B.CMAREA_FRMT_CODE                                      
                FROM    TMENUITEM A,                                            
                        TMNEMONIC B                                             
                WHERE   A.MENU_NAME     =  :TMNEMONIC-MNEMONIC-CODE             
                  AND   B.MNEMONIC_CODE =  A.ITEM_MNEMONIC_CODE                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    MENUNAME CURSOR FOR PROCESSING ALL THE MNEMONIC MENUS ON             
      *    WHICH THE **ITEM** ENTERED FOR THIS REQUEST IS LISTED.               
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE MENUNAME-CURSOR CURSOR FOR                              
                SELECT  MENU_NAME                                               
                FROM    TMENUITEM                                               
                WHERE   ITEM_MNEMONIC_CODE = :DP032-MNEMONIC                    
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
      ******************************************************************        
      *    LINKAGE                                                     *        
      ******************************************************************        
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.          *        
      ******************************************************************        
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      ******************************************************************        
      *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS *        
      *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER    *        
      *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS     *        
      *    PROGRAM WILL RETURN TO CISS.                                *        
      ******************************************************************        
           IF DP020-SRC-RETURN-W-TRAN-ID                                        
               PERFORM 1000-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
                                                                                
       1000-CONTROL-PROCESSING.                                                 
      ******************************************************************        
      *    THE ONLY VALID PROCESS PATH IS FOR INITIAL.  IF WE HAVE     *        
      *    ENTERED THIS PROGRAM FROM ANY OTHER PATH, ABEND.            *        
      ******************************************************************        
           MOVE LENGTH OF DP503-PANEL-TITLE                                     
                                     TO  PC-CHAR-MAX.                           
           MOVE SPACES               TO  DP503-MNEMONIC-RECORD.                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE DP032-SPECIFIC-COMMAREA                           
                   MOVE DP055-MNEMONIC                                          
                                     TO  DP032-MNEMONIC                         
                   PERFORM 1100-PROCESS-REQUEST                                 
                   MOVE DP016-PF3    TO  DP020-SRC-AID                          
                   PERFORM DP015-1000-START-NAVIGATOR                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND          TO TRUE                       
                   SET DP013-NO-ROLLBACK          TO TRUE                       
                                                                                
                   MOVE '1000-CONTROL-PROCESSING' TO                            
                        DP013-PARAGRAPH                                         
                                                                                
                   MOVE PM-PROGRAM-MESSAGE-1      TO                            
                        DP013-MESSAGE-TEXT(1)                                   
                                                                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       1100-PROCESS-REQUEST.                                                    
      ******************************************************************        
      *    BEGIN THE UPDATE PROCESS.                                   *        
      ******************************************************************        
           PERFORM 9100-SELECT-MNEMONIC-ROW.                                    
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               PERFORM 8110-POPULATE-AUDIT-FIELDS-1A                            
               PERFORM 9200-OPEN-ROUTE-CURSOR                                   
               PERFORM 5000-CONTROL-MNEMONIC-UPDATE                             
               IF  PS-VSAM-MNEMONIC-UPDT-OK                                     
                   IF  TMNEMONIC-TRANS-ID-CODE EQUAL PC-MENU-TRANID             
                       PERFORM 6000-CONTROL-MENUITEM-UPDATE                     
                   END-IF                                                       
                   PERFORM 7000-CONTROL-MENUNAME-UPDATE                         
                   IF  PS-ERROR                                                 
                       SET DP055-ERROR   TO  TRUE                               
                   ELSE                                                         
                       SET DP055-UPDATED TO  TRUE                               
                   END-IF                                                       
               ELSE                                                             
                   SET PS-ERROR          TO  TRUE                               
                   SET DP013-CICS-ABEND  TO  TRUE                               
                   SET DP013-XCTL-DISPLAY-RESTART                               
                                         TO  TRUE                               
                   MOVE '1100-PROCESS-REQUEST'                                  
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'PS-VSAM-MNEMONIC-UPDT-ERR OCCURRED'                    
                                         TO  DP013-MESSAGE-TEXT (1)             
                   SET DP055-ERROR       TO  TRUE                               
                   SET DP020-MSG-FATAL   TO  TRUE                               
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               MOVE '1100-PROCESS-REQUEST'                                      
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MNEMONIC NOT FOUND FOR PROCESSING'                         
                                     TO  DP013-MESSAGE-TEXT (1)                 
               SET DP055-ERROR       TO  TRUE                                   
               SET DP020-MSG-FATAL   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
               SET DP055-NOT-FOUND   TO  TRUE                                   
           END-IF.                                                              
                                                                                
                                                                                
       5000-CONTROL-MNEMONIC-UPDATE.                                            
      ******************************************************************        
      *    1) READ THE MNEMONIC VSAM FILE FOR THE REQUESTED MNEMONIC.  *        
      *       - IF RECORD EXISTS, IT WILL BE UPDATED (REWRITE).        *        
      *       - IF RECORD DOES NOT EXIST, IT WILL BE ADDED (WRITE).    *        
      *    2) POPULATE APPROPRIATE FIELDS IN THE MNEMONIC VSAM RECORD  *        
      *       WITH VALUES FROM THE MNEMONIC DB2 TABLE ROWS ALREADY     *        
      *       SELECTED.                                                *        
      *    3) READ THE ROUTE CURSOR AND POPULATE FUNCTION KEY FIELDS   *        
      *       IN THE MNEMONIC VSAM RECORD WITH VALUES FROM THE ROUTE   *        
      *       CURSOR.                                                  *        
      ******************************************************************        
           PERFORM 9400-READ-MNEMONIC-VSAM.                                     
           PERFORM 8120-POPULATE-AUDIT-FIELDS-2A.                               
                                                                                
           IF TMNEMONIC-UPD-VSAM-IND = 'D'                                      
               MOVE 'N' TO TMNEMONIC-AVAIL-INDCT.                               
                                                                                
           INITIALIZE DP503-FUNCTION-KEY-ARRAY.                                 
           MOVE TMNEMONIC-TRANS-ID-CODE                                         
                                     TO  DP503-TRAN-ID.                         
           MOVE TMNEMONIC-GLBL-ACCS-IND                                         
                                     TO  DP503-GLOBAL-ACCESS-IND.               
           MOVE TMNEMONIC-AVAIL-INDCT                                           
                                     TO  DP503-TRAN-AVAILABLE-SW.               
           MOVE TMNEMONIC-CMAREA-FRMT-CODE                                      
                                     TO  DP503-COMMAREA-FORMAT.                 
           MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                                    
                                     TO  DP503-APPL-COMMAREA-LENGTH.            
           MOVE TMNEMONIC-SAVE-CMAREA-INDCT                                     
                                     TO  DP503-SAVE-COMMAREA-SW.                
           MOVE TMNEMONIC-DB2-PKG-CDE                                           
                                     TO  DP503-DB2-PKG-IND.                     
           MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                     
                                     TO  DP503-ACRT-LOC-REQR-IND.               
           MOVE TMNEMONIC-PANEL-TITLE-NAME                                      
                                     TO  PV-TITLE-TABLE.                        
           PERFORM                                                              
               VARYING PV-CHAR-COUNT                                            
               FROM PC-CHAR-MAX BY -1                                           
               UNTIL PV-TITLE-CHAR (PV-CHAR-COUNT) NOT EQUAL SPACE              
                 OR  PV-CHAR-COUNT EQUAL ZERO                                   
           END-PERFORM.                                                         
                                                                                
           COMPUTE PV-STRING-POINTER =                                          
               ((LENGTH OF DP503-PANEL-TITLE - PV-CHAR-COUNT) / 2) + 1.         
           MOVE SPACES TO DP503-PANEL-TITLE.                                    
           STRING TMNEMONIC-PANEL-TITLE-NAME DELIMITED BY SIZE                  
                  INTO    DP503-PANEL-TITLE                                     
                  POINTER PV-STRING-POINTER                                     
           END-STRING.                                                          
                                                                                
           PERFORM 9210-FETCH-ROUTE-CURSOR.                                     
           PERFORM 5100-POPULATE-VSAM-FKEY-TBL                                  
                   UNTIL SQLCODE NOT EQUAL ZERO.                                
           PERFORM 9221-CLOSE-ROUTE-CURSOR.                                     
                                                                                
           IF  PS-VSAM-MNEMONIC-FOUND                                           
               PERFORM 9410-REWRITE-MNEMONIC-VSAM                               
           ELSE                                                                 
               PERFORM 9420-WRITE-MNEMONIC-VSAM                                 
           END-IF.                                                              
           IF  PS-VSAM-MNEMONIC-UPDT-OK                                         
               IF TMNEMONIC-UPD-VSAM-IND NOT = 'D'                              
                   PERFORM 9120-UPDATE-MNEMONIC-ROW                             
               ELSE                                                             
                   PERFORM 9121-UPDATE-MNEMONIC-ROW                             
               END-IF                                                           
               PERFORM 8130-POPULATE-AUDIT-FIELDS-3                             
               PERFORM 9600-WRITE-AUDIT-TRAIL-LOG.                              
                                                                                
                                                                                
       5100-POPULATE-VSAM-FKEY-TBL.                                             
      ******************************************************************        
      *    FOR EVERY ROW RETURNED FROM THE TCICSROUTE CURSOR, UPDATE   *        
      *    THE CORRESPONDING ENTRY IN THE VSAM FILE'S FUNCTION KEY TBL.*        
      *    WHEN ALL TCICSROUTE ROWS ARE PROCESSED, THE VSAM RECORD     *        
      *    IS FULLY POPULATED AND READY TO BE WRITTEN OUT.             *        
      ******************************************************************        
                                                                                
           MOVE TCICSROUTE-AID-SEQUENCE-NMBR                                    
                                     TO  PV-AID-SEQUENCE-NMBRX.                 
           SET DP503-FK-INDEX        TO  PV-AID-SEQUENCE-NMBR.                  
                                                                                
           MOVE TCICSROUTE-ACTION-CODE                                          
                                     TO  DP503-FK-ACTION                        
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-DEST-MNEMONIC-CODE                                   
                                     TO  DP503-FK-MNEMONIC                      
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-FUNC-KEY-LBL-NAME                                    
                                     TO  DP503-FK-LABEL                         
                                             (DP503-FK-INDEX).                  
           IF (TCICSROUTE-DEST-MNEMONIC-CODE NOT EQUAL SPACE                    
           AND TCICSROUTE-FUNC-KEY-LBL-NAME EQUAL SPACE)                        
               MOVE TCICSROUTE-DEST-MNEMONIC-CODE                               
                                     TO  DP503-FK-LABEL                         
                                             (DP503-FK-INDEX)                   
           END-IF.                                                              
                                                                                
           MOVE TCICSROUTE-ROUTE-STACK-INDCT                                    
                                     TO  DP503-FK-STACK-IND                     
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-IN-APLCTN-COMM-CDE                                   
                                     TO  DP503-FK-INT-APPL-FORMAT-IND           
                                             (DP503-FK-INDEX).                  
                                                                                
           MOVE TCICSROUTE-FUNC-KY-DISPN-IND                                    
                                     TO  DP503-FK-DISPN-IND                     
                                             (DP503-FK-INDEX).                  
                                                                                
           IF  DP503-FK-MNEMONIC (DP503-FK-INDEX) EQUAL                         
               DP503-FK-LABEL (DP503-FK-INDEX)                                  
               SET DP503-FK-NOT-ALT-LABEL (DP503-FK-INDEX)                      
                                     TO  TRUE                                   
           ELSE                                                                 
               SET DP503-FK-ALT-LABEL (DP503-FK-INDEX)                          
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
      *    *************************************************************        
      *    * GET THE TRANID FROM THE TMNEMONIC DB2 TABLE,                       
      *    * WHICH CORRESPONDS TO THIS ROW FROM THE TCICSROUTE CURSOR.          
      *    * NOTE THAT IF THE TRANID IS NOT FOUND ON THE TMNEMONIC              
      *    * DB2 TABLE, SPACES ARE MOVED FOR THE TRANID.                        
      *    *************************************************************        
           INITIALIZE PV-TRANS-ID-CODE                                          
                      PV-DESCRIPTION.                                           
           PERFORM 9110-SELECT-TRANID-FROM-TMNEM.                               
           MOVE PV-TRANS-ID-CODE     TO  DP503-FK-TRAN-ID                       
                                             (DP503-FK-INDEX).                  
                                                                                
      *    *************************************************************        
      *    * GET NEXT                                                           
      *    *************************************************************        
           PERFORM 9210-FETCH-ROUTE-CURSOR.                                     
                                                                                
                                                                                
       6000-CONTROL-MENUITEM-UPDATE.                                            
      ******************************************************************        
      *    THE MNEMONIC ENTERED FOR THIS REQUEST IS A *MENU*.          *        
      *    FIND ALL THE MNEMONIC ITEMS LISTED ON THIS MENU AND         *        
      *    UPDATE THE APPROPRIATE INFORMATION ON THE MENU ITEM VSAM    *        
      *    DATASET.                                                    *        
      ******************************************************************        
           PERFORM 6100-LOOP-TO-READ-AND-LOG.                                   
                                                                                
           PERFORM 9510-DELETE-MENUITEM-VSAM-GNRC.                              
                                                                                
           PERFORM 9300-OPEN-MENUITEM-CURSOR.                                   
                                                                                
           PERFORM UNTIL SQLCODE NOT EQUAL ZERO                                 
                                                                                
               PERFORM 9310-FETCH-MENUITEM-CURSOR                               
                                                                                
               IF  SQLCODE EQUAL ZERO                                           
                   PERFORM 9520-WRITE-MENUITEM-VSAM                             
               END-IF                                                           
                                                                                
           END-PERFORM.                                                         
           PERFORM 9311-CLOSE-MENUITEM-CURSOR.                                  
                                                                                
       6100-LOOP-TO-READ-AND-LOG.                                               
      ******************************************************************        
      *    READ THE MENUITEM VSAM FILE.                                *        
      *    READ EACH RECORD FROM THE MENUITEM VSAM FILE FOR THIS       *        
      *    MNEMONIC.                                                   *        
      *    FOR EACH RECORD READ, WRITE A RECORD TO THE DPCAUDIT FILE   *        
      *    TO LOG THE IMAGE "BEFORE" COMMENCING WITH UPDATES.          *        
      ******************************************************************        
           MOVE DP032-MNEMONIC       TO  DP504-MENU-NAME.                       
           MOVE ZERO                 TO  DP504-MENU-ITEM-ORDER-NUMBER.          
                                                                                
           PERFORM                                                              
               UNTIL PV-RESP NOT EQUAL DFHRESP(NORMAL)                          
                OR   DP504-MENU-NAME NOT EQUAL DP032-MNEMONIC                   
                                                                                
               PERFORM 9500-READ-MENUITEM-VSAM                                  
                                                                                
               IF (PV-RESP EQUAL DFHRESP(NORMAL)                                
               AND DP504-MENU-NAME = DP032-MNEMONIC)                            
                   PERFORM 8110-POPULATE-AUDIT-FIELDS-1B                        
                   PERFORM 8120-POPULATE-AUDIT-FIELDS-2B                        
                   PERFORM 8130-POPULATE-AUDIT-FIELDS-3                         
                   PERFORM 9600-WRITE-AUDIT-TRAIL-LOG                           
               END-IF                                                           
                                                                                
               ADD 1  TO  DP504-MENU-ITEM-ORDER-NUMBER                          
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
       7000-CONTROL-MENUNAME-UPDATE.                                            
      ******************************************************************        
      *    THE MNEMONIC ENTERED FOR THIS REQUEST IS AN *ITEM*.         *        
      *    FIND ALL THE MENUS ON WHICH THIS MNEMONIC IS LISTED AND     *        
      *    UPDATE THE APPROPRIATE INFORMATION ON THE MENU ITEM VSAM    *        
      *    DATASET.                                                    *        
      ******************************************************************        
           PERFORM 9320-OPEN-MENUNAME-CURSOR.                                   
           PERFORM 7100-LOOP-FOR-MENUNAME-UPDATE                                
               UNTIL SQLCODE NOT EQUAL ZERO.                                    
           PERFORM 9321-CLOSE-MENUNAME-CURSOR.                                  
                                                                                
                                                                                
       7100-LOOP-FOR-MENUNAME-UPDATE.                                           
      ******************************************************************        
      *    CONTROL THE FETCH, VSAM READ AND UPDATE.                    *        
      ******************************************************************        
           PERFORM 9330-FETCH-MENUNAME-CURSOR.                                  
           IF  SQLCODE EQUAL ZERO                                               
               PERFORM 7200-LOOP2-FOR-MENUNAME-UPDATE                           
           END-IF.                                                              
                                                                                
                                                                                
       7200-LOOP2-FOR-MENUNAME-UPDATE.                                          
      ******************************************************************        
      *    UPDATE THE MENUITEM VSAM DATASET,                           *        
      *    FOR EACH MENU TO WHICH THIS MENUITEM BELONGS.               *        
      ******************************************************************        
           INITIALIZE DP504-MENU-ITEM-RECORD.                                   
           MOVE TMENUITEM-MENU-NAME  TO  DP504-MENU-NAME.                       
                                                                                
           PERFORM                                                              
               UNTIL PV-RESP NOT EQUAL DFHRESP(NORMAL)                          
                OR   DP504-MENU-NAME NOT EQUAL TMENUITEM-MENU-NAME              
                                                                                
               PERFORM 9501-READ-MENUITEM-VSAM-UPDT                             
                                                                                
               IF (PV-RESP EQUAL DFHRESP(NORMAL)                                
               AND DP504-MENU-ITEM-MNEMONIC = TMNEMONIC-MNEMONIC-CODE)          
                                                                                
                   PERFORM 8110-POPULATE-AUDIT-FIELDS-1B                        
                   PERFORM 8120-POPULATE-AUDIT-FIELDS-2C                        
                   PERFORM 8130-POPULATE-AUDIT-FIELDS-3                         
                   PERFORM 9600-WRITE-AUDIT-TRAIL-LOG                           
                                                                                
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                     TO  DP504-MENU-ITEM-TRAN-ID                
                   MOVE TMNEMONIC-AVAIL-INDCT                                   
                                     TO  DP504-MENU-ITEM-AVAIL-IND              
                   MOVE TMNEMONIC-ACRT-LOC-REQR-IND                             
                                     TO  DP504-ACRT-LOC-REQR-IND                
                   MOVE TMNEMONIC-CMAREA-FRMT-CODE                              
                                     TO  DP504-MENU-ITEM-COMMAREA-FRMT          
                   PERFORM 9530-UPDATE-MENUITEM-VSAM                            
                   MOVE SPACES       TO  DP504-MENU-NAME                        
               ELSE                                                             
                   PERFORM 9540-UNLOCK-MENUITEM-VSAM                            
               END-IF                                                           
                                                                                
               ADD 1                 TO  DP504-MENU-ITEM-ORDER-NUMBER           
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
       8110-POPULATE-AUDIT-FIELDS-1A.                                           
      ******************************************************************        
      *    BEGIN CONSTRUCTING A RECORD TO BE WRITTEN AS AN AUDIT TRAIL.*        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           INITIALIZE DP520-DPCAUDIT-RECORD.                                    
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP520-PGM.                          
           MOVE DP020-USERID             TO DP520-REQUESTOR.                    
           MOVE DP032-MNEMONIC           TO DP520-REQUEST.                      
           MOVE TMNEMONIC-LAST-UPDATE-TMESTP                                    
                                         TO DP520-MNEMONIC-PRIOR-TMESTP.        
                                                                                
                                                                                
       8110-POPULATE-AUDIT-FIELDS-1B.                                           
      ******************************************************************        
      *    BEGIN CONSTRUCTING A RECORD TO BE WRITTEN AS AN AUDIT TRAIL.*        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           INITIALIZE DP520-DPCAUDIT-RECORD.                                    
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP520-PGM.                          
           MOVE DP020-USERID             TO DP520-REQUESTOR.                    
           MOVE DP032-MNEMONIC           TO DP520-REQUEST.                      
           MOVE SPACES                   TO DP520-MNEMONIC-PRIOR-TMESTP.        
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2A.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP503-MNEMONIC-RECORD    TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MNEMONIC                                       
                                         TO DP520-VSAM-REC-TYPE.                
           IF  PS-VSAM-MNEMONIC-FOUND                                           
               MOVE PC-VSAM-ACTION-U-UPDATE                                     
                                         TO DP520-VSAM-ACTION                   
           ELSE                                                                 
               MOVE PC-VSAM-ACTION-A-ADD TO DP520-VSAM-ACTION                   
           END-IF.                                                              
           MOVE PC-DPCAUDIT-MNEMONIC-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2B.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP504-MENU-ITEM-RECORD   TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MENUITEM                                       
                                         TO DP520-VSAM-REC-TYPE.                
      *    ALL ITEM RECORDS BEING LOGGED NOW WILL SOON BE DELETED AND           
      *    REPLACED:                                                            
           MOVE PC-VSAM-ACTION-R-REPLACE TO DP520-VSAM-ACTION.                  
           MOVE PC-DPCAUDIT-MENUITEM-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2C.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP504-MENU-ITEM-RECORD   TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MENUNAME                                       
                                         TO DP520-VSAM-REC-TYPE.                
      *    THIS PARTICULAR MENU RECORD ON MENUITEM IS NOW BEING UPDATED:        
           MOVE PC-VSAM-ACTION-U-UPDATE  TO DP520-VSAM-ACTION.                  
           MOVE PC-DPCAUDIT-MENUITEM-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8130-POPULATE-AUDIT-FIELDS-3.                                            
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE PV-CURRENT-TMESTP-FOR-TRAN                                      
                                         TO DP520-CURRENT-TMESTP.               
                                                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
      *    ALL FILE ACCESS PARAGRAPHS IN THIS PROGRAM BEGIN WITH                
      *    NINE-THOUSAND:                                                       
      *    1) DB2 TABLE ACCESS TMNEMONIC  - 91XX                                
      *    2) DB2 TABLE ACCESS TCICSROUTE - 92XX                                
      *    3) DB2 TABLE ACCESS TMENUITEM  - 93XX                                
      *    4) VSAM FILE ACCESS MNEMONIC   - 94XX                                
      *    5) VSAM FILE ACCESS MENUITEM   - 95XX                                
      *    6) VSAM FILE ACCESS DPCAUDIT   - 96XX                                
      ******************************************************************        
      ******************************************************************        
                                                                                
                                                                                
       9100-SELECT-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    GET THE MNEMONIC ROW WHICH IS DRIVING THE UPDATES TO THE    *        
      *    VSAM FILE(S).                                               *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                           MNEMONIC_CODE                                        
                         , TRANS_ID_CODE                                        
                         , AVAIL_INDCT                                          
                         , CMAREA_FRMT_CODE                                     
                         , SAVE_CMAREA_INDCT                                    
                         , CMAREA_LNGTH_MSRMT                                   
                         , PANEL_TITLE_NAME                                     
                         , GLBL_ACCS_IND                                        
                         , LAST_UPDATE_TMESTP                                   
                         , DB2_PKG_CDE                                          
                         , UPD_VSAM_IND                                         
                         , ACRT_LOC_REQR_IND                                    
                    INTO                                                        
                           :TMNEMONIC-MNEMONIC-CODE                             
                         , :TMNEMONIC-TRANS-ID-CODE                             
                         , :TMNEMONIC-AVAIL-INDCT                               
                         , :TMNEMONIC-CMAREA-FRMT-CODE                          
                         , :TMNEMONIC-SAVE-CMAREA-INDCT                         
                         , :TMNEMONIC-CMAREA-LNGTH-MSRMT                        
                         , :TMNEMONIC-PANEL-TITLE-NAME                          
                         , :TMNEMONIC-GLBL-ACCS-IND                             
                         , :TMNEMONIC-LAST-UPDATE-TMESTP                        
                         , :TMNEMONIC-DB2-PKG-CDE                               
                         , :TMNEMONIC-UPD-VSAM-IND                              
                         , :TMNEMONIC-ACRT-LOC-REQR-IND                         
                    FROM                                                        
                           TMNEMONIC                                            
                    WHERE                                                       
                           MNEMONIC_CODE  =  :DP032-MNEMONIC                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9100-SELECT-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'SELECT FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP032-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
                        SET DP055-NOT-FOUND                                     
                                     TO  TRUE                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9100-SELECT-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC SELECT'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9110-SELECT-TRANID-FROM-TMNEM.                                           
      ******************************************************************        
      *    READ THE MNEMONIC TABLE TO OBTAIN THE TRAN ID.              *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                         TRANS_ID_CODE                                          
                      ,  PANEL_TITLE_NAME                                       
                    INTO                                                        
                        :PV-TRANS-ID-CODE                                       
                      , :PV-DESCRIPTION                                         
                    FROM                                                        
                         TMNEMONIC                                              
                    WHERE                                                       
                         MNEMONIC_CODE = :TCICSROUTE-DEST-MNEMONIC-CODE         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9110-SELECT-TRANID-FROM-TMNEM'                    
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'SELECT FAILED FOR: '                              
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE TCICSROUTE-DEST-MNEMONIC-CODE                      
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9110-SELECT-TRANID-FROM-TMNEM'                             
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR TMNEMONIC SELECT'                 
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9120-UPDATE-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, FOR THE INFORMATION WHICH HAS   *        
      *    BEEN SUCCESSFULLY COPIED TO THE MNEMONIC VSAM FILE.         *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET  UPD_VSAM_IND       = :PC-N-NOT-NEEDED,                  
                        LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP                 
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMNEMONIC-MNEMONIC-CODE           
                        AND                                                     
                        LAST_UPDATE_TMESTP =                                    
                                          :TMNEMONIC-LAST-UPDATE-TMESTP)        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9120-UPDATE-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP032-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9120-UPDATE-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC UPDATE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-XCTL-DISPLAY-RESTART                                 
                                       TO  TRUE                                 
                 MOVE '9120-UPDATE-MNEMONIC-ROW'                                
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE TMNEMONIC-LAST-UPDATE-TMESTP                              
                                       TO  PV-ABEND-TIMESTAMP                   
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9121-UPDATE-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, FOR THE INFORMATION WHICH HAS   *        
      *    BEEN SUCCESSFULLY COPIED TO THE MNEMONIC VSAM FILE.         *        
      *    (FOR DELETE SITUATIONS ONLY)                                *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET  AVAIL_INDCT        = 'N',                               
                        LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP                 
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMNEMONIC-MNEMONIC-CODE           
                        AND                                                     
                        LAST_UPDATE_TMESTP =                                    
                                          :TMNEMONIC-LAST-UPDATE-TMESTP)        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9121-UPDATE-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP032-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9121-UPDATE-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC UPDATE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-XCTL-DISPLAY-RESTART                                 
                                       TO  TRUE                                 
                 MOVE '9121-UPDATE-MNEMONIC-ROW'                                
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE TMNEMONIC-LAST-UPDATE-TMESTP                              
                                       TO  PV-ABEND-TIMESTAMP                   
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9130-SELECT-TMNEMONIC-TMESTP.                                            
      ******************************************************************        
      *    SELECT THE UPDATED TMNEMONIC ROW, TO GET THE TMESTP.        *        
      *    THIS TIMESTAMP WILL BE USED AS A MEANS TO TIE TOGETHER:     *        
      *    1) OTHER RELATED DB2 TABLE UPDATES FOR THIS TRANSACTION.    *        
      *    2) THE RECORD WRITTEN TO THE DPCAUDIT LOG.                  *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    SELECT LAST_UPDATE_TMESTP                                   
                    INTO   :TMNEMONIC-LAST-UPDATE-TMESTP                        
                    FROM   TMNEMONIC                                            
                    WHERE  MNEMONIC_CODE  =  :DP032-MNEMONIC                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9130-SELECT-TMNEMONIC-TMESTP'                     
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'SELECT FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP032-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = ZERO                                                   
               MOVE TMNEMONIC-LAST-UPDATE-TMESTP  TO                            
                                              PV-CURRENT-TMESTP-FOR-TRAN        
           ELSE                                                                 
               MOVE PC-ZERO-TMSTP         TO  PV-CURRENT-TMESTP-FOR-TRAN        
           END-IF.                                                              
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9130-SELECT-TMNEMONIC-TMESTP'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC SELECT'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9200-OPEN-ROUTE-CURSOR.                                                  
      ******************************************************************        
      *    OPEN THE CICS ROUTE TABLE CURSOR.                           *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         ROUTE-CURSOR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9200-OPEN-ROUTE-CURSOR'                           
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9200-OPEN-ROUTE-CURSOR'                                    
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9210-FETCH-ROUTE-CURSOR.                                                 
      ******************************************************************        
      *    FETCH ONE ROW AT A TIME FROM THE ROUTE CURSOR               *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         ROUTE-CURSOR                                           
                    INTO                                                        
                        :TCICSROUTE-AID-SEQUENCE-NMBR                           
                      , :TCICSROUTE-DEST-MNEMONIC-CODE                          
                      , :TCICSROUTE-ACTION-CODE                                 
                      , :TCICSROUTE-ROUTE-STACK-INDCT                           
                      , :TCICSROUTE-FUNC-KEY-LBL-NAME                           
                      , :TCICSROUTE-IN-APLCTN-COMM-CDE                          
                      , :TCICSROUTE-FUNC-KY-DISPN-IND                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9210-FETCH-ROUTE-CURSOR'                          
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9210-FETCH-ROUTE-CURSOR'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9221-CLOSE-ROUTE-CURSOR.                                                 
      ******************************************************************        
      *    CLOSE CICS ROUTE TABLE CURSOR.                              *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         ROUTE-CURSOR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9221-CLOSE-ROUTE-CURSOR'                          
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9221-CLOSE-ROUTE-CURSOR'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9300-OPEN-MENUITEM-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE MENU ITEM CURSOR FOR PROCESSING.                   *        
      *    (CONTAINS COLUMNS FROM MENUITEM AND TMNEMONIC)              *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    OPEN  MENUITEM-CURSOR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9300-OPEN-MENUITEM-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9300-OPEN-MENUITEM-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9310-FETCH-MENUITEM-CURSOR.                                              
      ******************************************************************        
      *    READ THE MENUITEM/MNEMONIC TABLES TO OBTAIN THE COLUMNS     *        
      *    THAT MAY HAVE BEEN UPDATED.                                 *        
      ******************************************************************        
           INITIALIZE DP504-MENU-ITEM-RECORD.                                   
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    FETCH MENUITEM-CURSOR                                       
                    INTO                                                        
                         :DP504-MENU-NAME                                       
                       , :DP504-MENU-ITEM-ORDER-NUMBER                          
                       , :DP504-MENU-ITEM-MNEMONIC                              
                       , :DP504-MENU-ITEM-DESCRIPTION:PV-NULL-IND               
                       , :DP504-INT-APPL-FORMAT-IND                             
                       , :DP504-MENU-ITEM-TRAN-ID                               
                       , :DP504-MENU-ITEM-AVAIL-IND                             
                       , :DP504-ACRT-LOC-REQR-IND                               
                       , :DP504-MENU-ITEM-COMMAREA-FRMT                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9310-FETCH-MENUITEM-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9310-FETCH-MENUITEM-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               MOVE DP504-MENU-ITEM-MNEMONIC                                    
                                     TO  TCICSROUTE-DEST-MNEMONIC-CODE          
               PERFORM 9110-SELECT-TRANID-FROM-TMNEM                            
               MOVE PV-TRANS-ID-CODE TO  DP504-MENU-ITEM-TRAN-ID                
                                                                                
               IF  DP504-MENU-ITEM-DESCRIPTION GREATER THAN SPACES              
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE PV-DESCRIPTION                                          
                                     TO  DP504-MENU-ITEM-DESCRIPTION            
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       9311-CLOSE-MENUITEM-CURSOR.                                              
      ******************************************************************        
      *    CLOSE MENU ITEM CURSOR.                                     *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    CLOSE MENUITEM-CURSOR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9311-CLOSE-MENUITEM-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9311-CLOSE-MENUITEM-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9320-OPEN-MENUNAME-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE MENU NAME CURSOR.                                  *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         MENUNAME-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9320-OPEN-MENUNAME-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9320-OPEN-MENUNAME-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9330-FETCH-MENUNAME-CURSOR.                                              
      ******************************************************************        
      *    FETCH ONE ROW AT A TIME FROM THE MENUNAME CURSOR.           *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         MENUNAME-CURSOR                                        
                    INTO                                                        
                        :TMENUITEM-MENU-NAME                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9330-FETCH-MENUNAME-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9330-FETCH-MENUNAME-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9321-CLOSE-MENUNAME-CURSOR.                                              
      ******************************************************************        
      *    CLOSE MENU NAME CURSOR.                                     *        
      ******************************************************************        
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         MENUNAME-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        MOVE '9321-CLOSE-MENUNAME-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-XCTL-DISPLAY-RESTART                          
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE '9321-CLOSE-MENUNAME-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9400-READ-MNEMONIC-VSAM.                                                 
      ******************************************************************        
      *    READ THE MNEMONIC VSAM FILE.                                *        
      ******************************************************************        
           INITIALIZE PS-VSAM-MNEMONIC-FOUND-SW.                                
                                                                                
           MOVE DP032-MNEMONIC       TO  DP503-MNEMONIC.                        
                                                                                
           EXEC CICS READ                                                       
                INTO  (DP503-MNEMONIC-RECORD)                                   
                FILE  (PC-MNEMONIC-FILE)                                        
                RIDFLD(DP503-MNEMONIC)                                          
                RESP  (PV-RESP)                                                 
                UPDATE                                                          
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-FOUND TO TRUE                               
           ELSE                                                                 
               IF  PV-RESP EQUAL DFHRESP(NOTFND)                                
                   SET PS-VSAM-MNEMONIC-NOT-FOUND TO TRUE                       
               ELSE                                                             
                   SET PS-ERROR          TO  TRUE                               
                   SET DP013-CICS-ABEND  TO  TRUE                               
                   SET DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                   MOVE '9400-READ-MNEMONIC-VSAM'                               
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM READ FOR:'          
                                         TO  DP013-MESSAGE-TEXT (1)             
                   MOVE DP032-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)             
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       9410-REWRITE-MNEMONIC-VSAM.                                              
      ******************************************************************        
      *    REWRITE THE UPDATED MNEMONIC RECORD.                        *        
      ******************************************************************        
           EXEC CICS REWRITE                                                    
                FROM(DP503-MNEMONIC-RECORD)                                     
                FILE(PC-MNEMONIC-FILE)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-UPDT-OK TO TRUE                             
           ELSE                                                                 
               SET PS-VSAM-MNEMONIC-UPDT-ERR TO TRUE                            
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9410-REWRITE-MNEMONIC-VSAM'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM REWRITE FOR:'           
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP032-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9420-WRITE-MNEMONIC-VSAM.                                                
      ******************************************************************        
      *    WRITE THE NEW MNEMONIC RECORD.                              *        
      ******************************************************************        
           MOVE DP032-MNEMONIC       TO  DP503-MNEMONIC.                        
                                                                                
           EXEC CICS WRITE                                                      
                FROM(DP503-MNEMONIC-RECORD)                                     
                FILE(PC-MNEMONIC-FILE)                                          
                RIDFLD(DP503-MNEMONIC)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-UPDT-OK  TO TRUE                            
           ELSE                                                                 
               SET PS-VSAM-MNEMONIC-UPDT-ERR TO TRUE                            
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9420-WRITE-MNEMONIC-VSAM'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM WRITE FOR:'             
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP032-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9500-READ-MENUITEM-VSAM.                                                 
      ******************************************************************        
      *    PERFORM A FULLKEY READ ON THE VSAM MENUITEM FILE.           *        
      *    READ EACH MENUITEM RECORD, LISTED ON THE MENU               *        
      *    BEING PROCESSED.                                            *        
      ******************************************************************        
           EXEC CICS READ                                                       
                INTO     (DP504-MENU-ITEM-RECORD)                               
                FILE     (PC-MENUITEM-FILE)                                     
                GTEQ                                                            
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                RESP     (PV-RESP)                                              
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9500-READ-MENUITEM-VSAM'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE                                                             
               'BAD RETURN CODE ON READ OF MENUITEM VSAM FOR:'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9501-READ-MENUITEM-VSAM-UPDT.                                            
      ******************************************************************        
      *    PERFORM A FULLKEY READ ON THE VSAM MENUITEM FILE.           *        
      *    READ A SPECIFIC MENU RECORD, TO WHICH THIS MENUITEM BELONGS.*        
      *    THIS RECORD IS HELD FOR EXCLUSIVE USE BY THE "READ... UPDATE"        
      *    AND AN "UNLOCK" COMMAND WILL BE ISSUED LATER.               *        
      ******************************************************************        
           EXEC CICS READ                                                       
                INTO     (DP504-MENU-ITEM-RECORD)                               
                FILE     (PC-MENUITEM-FILE)                                     
                GTEQ                                                            
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                RESP     (PV-RESP)                                              
                UPDATE                                                          
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9501-READ-MENUITEM-VSAM-UPDT'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE                                                             
               'BAD RETURN CODE READING MENUITEM VSAM FOR:'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9510-DELETE-MENUITEM-VSAM-GNRC.                                          
      ******************************************************************        
      *    DELETE THE EXISTING MENU ITEM VSAM RECORDS.                 *        
      *    DELETE ALL RECORDS FROM THE MENUITEM VSAM FILE FOR THIS     *        
      *    MNEMONIC... PARTIAL KEY, THE 1ST 8 BYTES, ARE USED.         *        
      ******************************************************************        
           MOVE DP032-MNEMONIC       TO  DP504-MENU-NAME.                       
           MOVE ZERO                 TO  DP504-MENU-ITEM-ORDER-NUMBER.          
                                                                                
           EXEC CICS DELETE                                                     
                FILE     (PC-MENUITEM-FILE)                                     
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                KEYLENGTH(8)                                                    
                GENERIC                                                         
                RESP     (PV-RESP)                                              
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9510-DELETE-MENUITEM-VSAM-GNRC'                            
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MENUITEM VSAM DELETE'                 
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9520-WRITE-MENUITEM-VSAM.                                                
      ******************************************************************        
      *    WRITE THE UPDATED MENUITEM VSAM RECORD.                     *        
      ******************************************************************        
           EXEC CICS WRITE                                                      
                FROM  (DP504-MENU-ITEM-RECORD)                                  
                RIDFLD(DP504-MENU-ITEM-KEY)                                     
                FILE  (PC-MENUITEM-FILE)                                        
                RESP  (PV-RESP)                                                 
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9520-WRITE-MENUITEM-VSAM'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MENUITEM VSAM WRITE FOR:'             
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9530-UPDATE-MENUITEM-VSAM.                                               
      ******************************************************************        
      *    UPDATE THE MENUITEM VSAM DATASET.                           *        
      ******************************************************************        
           EXEC CICS REWRITE                                                    
                FROM(DP504-MENU-ITEM-RECORD)                                    
                FILE(PC-MENUITEM-FILE)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9530-UPDATE-MENUITEM-VSAM'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE REWRITING MENUITEM VSAM FOR:'              
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-MNEMONIC                                    
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9540-UNLOCK-MENUITEM-VSAM.                                               
      ******************************************************************        
      *    UNLOCK MENUITEM VSAM FILE.                                  *        
      *    RELEASE THE RECORD HELD EXCLUSIVELY BY THE "READ... UPDATE".*        
      ******************************************************************        
           EXEC CICS UNLOCK                                                     
                FILE(PC-MENUITEM-FILE)                                          
           END-EXEC.                                                            
                                                                                
                                                                                
       9600-WRITE-AUDIT-TRAIL-LOG.                                              
      ******************************************************************        
      *    WRITE A RECORD TO THE CICSARCH AUDIT TRAIL LOG: DPCAUDIT.   *        
      *    THIS IS AN ESDS VSAM FILE WITH VARIABLE LENGTH RECORDS.     *        
      ******************************************************************        
           EXEC CICS WRITE                                                      
                FROM   (DP520-DPCAUDIT-RECORD)                                  
                FILE   (PC-DPCAUDIT-FILE)                                       
                LENGTH (PV-DPCAUDIT-REC-LEN)                                    
                RIDFLD (PV-DPCAUDIT-RBA)                                        
                RBA                                                             
                RESP   (PV-RESP)                                                
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR          TO  TRUE                                   
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-XCTL-DISPLAY-RESTART                                   
                                     TO  TRUE                                   
               MOVE '9600-WRITE-AUDIT-TRAIL-LOG'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE WRITING CICSARCH AUDIT LOG FOR:'           
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP032-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    RETURN TO CICS MODULE                                       *        
      ******************************************************************        
           COPY DPPD003.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    ABEND PROCESSOR MODULE                                      *        
      ******************************************************************        
           COPY DPPD013.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK COMMUNICATION AREA MODULE                             *        
      ******************************************************************        
           COPY DPPD015.                                                        
