000100*----------------------------------------------------------------*00010000
000200 IDENTIFICATION DIVISION.                                         00020000
000300*----------------------------------------------------------------*00030000
000400 PROGRAM-ID.    EPKUT200.                                         00040000
000500 AUTHOR.        CHUCK HERO.                                       00050000
000600 DATE-WRITTEN.  15 NOVEMBER 2007.                                 00060000
000700 DATE-COMPILED.                                                   00070000
000800                                                                  00080000
000900*----------------------------------------------------------------*00090000
001000* ** CREATE OFFERS FULL FILE REFRESH STORE LIST CONTROL FILE **  *00100000
001100*              **   NEXT GENERATION STORE   **                   *00110000
001200*----------------------------------------------------------------*00120000
001300*  THIS PROGRAM CREATES A BATCH CONTROL FILE WITH STORE NUMBERS,  00130000
001400*  AND THE ASSOCIATED STORE GROUP TYPE CODES AND STORE GROUP IDS. 00140000
001500*  THIS FILE WILL BE USED LATER BY THE EXTRACT PROGRAM (EPKUT201) 00150000
001600*  TO EXTRACT ALL THE OFFERS FOR THAT STORE.                      00160000
001700*                                                                 00170000
001800*  THE PROGRAM READS A BATCH CONTROL DATE CARD, AND SELECTS THOSE 00180000
001900*  STORE NUMBERS IN THE STORE FILE REFRESH TABLE EPT_STR_FIL_RFRSH00190000
002000*  WHICH ARE DESIGNATED TO RECEIVE A FULL FILE REFRESH FOR OFFERS.00200000
002100*                                                                 00210001
002200*  IT ALSO USES AN ADVANCE DAYS CONTROL CARD TO DETERMINE WHETHER 00220000
002300*  TO INCLUDE NEW STORES WHICH WILL BE OPENED WITHIN THE DATE     00230000
002400*  RANGE.                                                         00240000
002500*----------------------------------------------------------------*00250000
002600* HISTORY LOG:                                                    00260000
002700*----------------------------------------------------------------*00270000
002800* DATE:        15 NOVEMBER 2007                                   00280000
002900* WR/IR#:      WR# 33714                                          00290000
003000* PROGRAMMER:  CHUCK HERO                                         00300000
003100* DESCRIPTION: INITIAL PROGRAM CREATION                           00310000
003200                                                                  00320000
003300*----------------------------------------------------------------*00330000
003400* DATE:                                                           00340000
003500* WR/IR#:                                                         00350000
003600* PROGRAMMER:                                                     00360000
003700* DESCRIPTION:                                                    00370000
003800*                                                                 00380000
003900*----------------------------------------------------------------*00390000
004000 ENVIRONMENT DIVISION.                                            00400000
004100*----------------------------------------------------------------*00410000
004200 CONFIGURATION SECTION.                                           00420000
004300 SOURCE-COMPUTER. IBM-3090.                                       00430000
004400 OBJECT-COMPUTER. IBM-3090.                                       00440000
004500                                                                  00450000
004600 INPUT-OUTPUT SECTION.                                            00460000
004700                                                                  00470000
004800 FILE-CONTROL.                                                    00480000
004900                                                                  00490000
005000     SELECT CONTROL-CARD-BATCH-DATE           ASSIGN TO INP01.    00500001
005100                                                                  00510000
005200     SELECT CONTROL-CARD-ADV-DAYS             ASSIGN TO INP02.    00500001
005300                                                                  00510000
005400     SELECT REFRESH-STORE-CARD-FILE           ASSIGN TO OUT01.    00520000
005500                                                                  00530002
005600*----------------------------------------------------------------*00540000
005700 DATA DIVISION.                                                   00550000
005800*----------------------------------------------------------------*00560000
005900 FILE SECTION.                                                    00570000
006000 FD  CONTROL-CARD-BATCH-DATE                                      00580001
006100     RECORDING MODE IS F                                          00590000
006200     BLOCK CONTAINS 0 RECORDS                                     00600000
006300     LABEL RECORDS ARE STANDARD.                                  00610000
006400                                                                  00620000
006500 01  CONTROL-BATCH-DATE-RECORD        PIC X(080).                 00630001
006600                                                                  00640000
006700 FD  CONTROL-CARD-ADV-DAYS                                        00580001
006800     RECORDING MODE IS F                                          00590000
006900     BLOCK CONTAINS 0 RECORDS                                     00600000
007000     LABEL RECORDS ARE STANDARD.                                  00610000
007100                                                                  00620000
007200 01  CONTROL-ADV-DAYS-RECORD          PIC X(080).                 00630001
007300                                                                  00640000
007400 FD  REFRESH-STORE-CARD-FILE                                      00650001
007500     RECORDING MODE IS F                                          00660000
007600     LABEL RECORDS ARE STANDARD                                   00670000
007700     BLOCK CONTAINS 0 RECORDS.                                    00680000
007800                                                                  00690000
007900 01  REFRESH-STORE-CARD-RECORD        PIC X(080).                 00700001
008000                                                                  00710000
008100                                                                  00720000
008200*----------------------------------------------------------------*00730000
008300                                                                  00740000
008400 WORKING-STORAGE SECTION.                                         00750000
008500                                                                  00760000
008600*----------------------------------------------------------------*00770000
008700 01 FILLER                            PIC  X(27) VALUE            00910000
008800        'WORKING STORAGE STARTS HERE'.                            00920000
008900                                                                  00810000
009000*----------------------------------------------------------------*00770000
009100* COPYBOOK FOR INPUT BATCH DATE CONTROL CARD EPCC257             *00780000
009200*----------------------------------------------------------------*00790000
009300     COPY EPRD257.                                                00800000
009400                                                                  00810000
009500*----------------------------------------------------------------*00770000
009600* COPYBOOK FOR INPUT NEW STORE ADVANCE DAYS CONTROL CARD EPCC258.*00780000
009700*----------------------------------------------------------------*00790000
009800     COPY EPRD258.                                                00800000
009900                                                                  00810000
010000*----------------------------------------------------------------*00820000
010100* COPYBOOK FOR OUTPUT BATCH REFRESH STORES FILE                  *00830000
010200*----------------------------------------------------------------*00840000
010300     COPY EPRD250.                                                00850000
010400                                                                  00860000
010500*----------------------------------------------------------------*01060000
010600* PC- PROGRAM CONSTANTS                                          *01070000
010700*----------------------------------------------------------------*01080000
010800 01  PC-PROGRAM-CONSTANTS.                                        01090000
010900     05  FILLER                       PIC  X(23) VALUE            00910000
011000        'COPYBOOK DATA ENDS HERE'.                                00920000
011100     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'EPKUT200'.01100000
011200     05  PC-MAX-RETRIES               PIC  9(01) VALUE 5.         01110000
011300     05  PC-NO-REFRESHES-FOUND        PIC  X(49) VALUE            01130000
011400             'NO STORES IDENTIFIED TO RECEIVE AN OFFERS REFRESH'. 01140000
011500                                                                  01150000
011600*-----------------------------------------------------------              
011700* PC- PROGRAM COUNTERS                                                    
011800*-----------------------------------------------------------              
011900 01  PC-PROGRAM-COUNTERS.                                                 
012000     05  PC-CNT-READ-RECS             PIC 9(09) VALUE 0.                  
012100     05  PC-CNT-REFRESH-STRS          PIC 9(09) VALUE 0.                  
012200     05  PC-CNT-REFRESH-CSR-FETCHES   PIC 9(09) VALUE 0.                  
012300     05  PC-CNT-OUT-RECS              PIC 9(09) VALUE 0.                  
012400     05  PC-CNT-ALL-RECS              PIC 9(09) VALUE 0.                  
012500                                                                          
012600*----------------------------------------------------------------*01160000
012700* PS- PROGRAM SWITCHES                                           *01170000
012800*----------------------------------------------------------------*01180000
012900 01  PS-PROGRAM-SWITCHES.                                         01190000
013000     05  PS-BATCH-DTE-CNTL-EOF-SW     PIC  X(01) VALUE 'N'.       01200002
013100         88  PS-EOF-BATCH-DTE-CNTL-FILE          VALUE 'Y'.       01210002
013200         88  PS-NOT-EOF-BATCH-DTE-CNTL-FILE      VALUE 'N'.       01220002
013000     05  PS-ADV-DAYS-CNTL-EOF-SW      PIC  X(01) VALUE 'N'.       01200002
013100         88  PS-EOF-ADV-DAYS-CNTL-FILE           VALUE 'Y'.       01210002
013200         88  PS-NOT-EOF-ADV-DAYS-CNTL-FILE       VALUE 'N'.       01220002
013300     05  PS-END-OF-REFRESH-CSR-SW     PIC  X(01) VALUE 'N'.       01260002
013400         88  PS-END-OF-REFRESH-CSR               VALUE 'Y'.       01270002
013500         88  PS-NOT-END-OF-REFRESH-CSR           VALUE 'N'.       01280002
013900     05  PS-DATE-ROUTINE-SW             PIC X(01) VALUE 'N'.              
014000         88  PS-DATE-FOUND                        VALUE 'Y'.              
014100         88  PS-DATE-NOT-FOUND                    VALUE 'N'.              
014200                                                                  01320002
014300*--------------------------------------------------------------*  01330002
014400* PV- PROGRAM VARIABLES                                           01340002
014500*--------------------------------------------------------------*  01350002
014600 01  PV-PROGRAM-VARIABLES.                                        01360002
014700     05  PV-SQL-SUB                   PIC S9(04) VALUE 0  COMP.   01400002
014800     05  PV-SAVE-STR-NBR              PIC  9(05) VALUE 0.         01440002
015200     05  PV-DAYS-AHEAD                PIC  9(05) VALUE 0.                 
015300     05  PV-SAVE-ADDED-DAYS           PIC  X(05) VALUE SPACES.            
015400     05  PV-BATCH-DATE                PIC  X(10) VALUE SPACES.    01510002
015500     05  PV-NEW-OPEN-DATE             PIC  X(10) VALUE SPACES.    01510002
015700                                                                  02040002
015800*--------------------------------------------------------------*  02050002
015900* DATE ROUTINE COPY MEMBERS                                       02060002
016000*--------------------------------------------------------------*  02070002
016100     COPY DPWS500.                                                02080002
016200                                                                  02090002
016300*--------------------------------------------------------------*  02100002
016400* VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         02110002
016500*--------------------------------------------------------------*  02120002
016600     COPY DPWS004.                                                02130002
016700                                                                  02140002
016800*--------------------------------------------------------------*  02150002
016900* DB2 ABEND COPYBOOK                                              02160002
017000*--------------------------------------------------------------*  02170002
017100     COPY DPWS900.                                                02180002
017200                                                                  02190002
017300*--------------------------------------------------------------*  02200002
017400* DB2 MESSAGE AREA                                                02210002
017500*--------------------------------------------------------------*  02220002
017600     COPY SQLCA2.                                                 02230002
017700                                                                  02240002
017800*----------------------------------------------------------------*00870000
017900* PV- PROGRAM STATUS                                             *00880000
018000*----------------------------------------------------------------*00890000
018100 01  PV-PROGRAM-STATUS.                                           00900000
018200     05  PV-PARAGRAPH-NAME            PIC  X(40) VALUE SPACES.    00930000
018300     05  PV-DB2-OPERATION             PIC  X(50) VALUE SPACES.    00940000
018400     05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACES.    00950000
018500     05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACES.    00960000
018600 01  FILLER                          REDEFINES                    00970000
018700     PV-PROGRAM-STATUS                PIC  X(170).                00980000
018800                                                                  00990000
018900 01  AC-ABEND-CODE                    PIC S9(04) VALUE +0 COMP.   01000000
019000     88  AC-4003-CONTROL-CARD-ERROR              VALUE +4003.     01010000
019100     88  AC-4008-CONTROL-CARD-EMPTY              VALUE +4008.     01010000
019200     88  AC-4013-DB2-ERROR                       VALUE +4013.     01020000
019300     88  AC-4019-CALENDAR-ROUTINE-ERROR          VALUE +4019.     01030000
019400                                                                  01050000
019500*--------------------------------------------------------------*  02400002
019600* DB2 COMMUNICATIONS AREA                                         02410002
019700*--------------------------------------------------------------*  02420002
019800     EXEC SQL                                                     02430002
019900       INCLUDE SQLCA                                              02440002
020000     END-EXEC.                                                    02450002
020100                                                                  02460002
020200*--------------------------------------------------------------*  02470002
020300* DB2 TABLE EPT00010(EPT_STR_FIL_RFRSH) STORE REFRESH TABLE       02480002
020400*--------------------------------------------------------------*  02490002
020500     EXEC SQL                                                     02500002
020600       INCLUDE EPT00010                                           02510002
020700     END-EXEC.                                                    02520002
020800                                                                  02530002
020900*--------------------------------------------------------------*  02540002
021000* DB2 TABLE XST00001(XST_LOC) - LOCATION TABLE                    02550002
021100*--------------------------------------------------------------*  02560002
021200     EXEC SQL                                                     02570002
021300       INCLUDE XST00001                                           02580002
021400     END-EXEC.                                                    02590002
021500                                                                  02600002
021600*--------------------------------------------------------------*  02680002
021700* DB2 TABLE XST00023(XST_STR_GP_MBSHP) - STORE GROUP              02690002
021800*                                        MEMBERSHIP TABLE         02700002
021900*--------------------------------------------------------------*  02710002
022000     EXEC SQL                                                     02720002
022100       INCLUDE XST00023                                           02730002
022200     END-EXEC.                                                    02740002
022300                                                                  02750002
022400*--------------------------------------------------------------*  02610002
022500* DB2 TABLE XST00025(XST_LOC_ATTR) - LOCATION ATTRIBUTE TABLE     02620002
022600*--------------------------------------------------------------*  02630002
022700     EXEC SQL                                                     02640002
022800       INCLUDE XST00025                                           02650002
022900     END-EXEC.                                                    02660002
023000                                                                  02670002
023100*-----------------------------------------------------------              
023200* OFFER-REFRESH-CSR WILL FIND ALL THE STORES THAT NEED                    
023300* A FULL FILE REFRESH.                                                    
023400*-----------------------------------------------------------              
023500     EXEC SQL                                                             
023600       DECLARE OFFER-REFRESH-CSR CURSOR FOR                               
023700            SELECT  RFRSH.STR_NBR                                         
023800                   ,MBSHP.STR_GP_TYP_CDE                                  
023900                   ,MBSHP.STR_GP_ID                                       
024000              FROM  EPT_STR_FIL_RFRSH RFRSH                               
024100                   ,XST_LOC           LOC                                 
024200                   ,XST_LOC_ATTR      ATTR                                
024300                   ,XST_STR_GP_MBSHP  MBSHP                               
024400             WHERE  RFRSH.STR_NBR       =  LOC.LOC_NBR                    
024500               AND  LOC.LOC_NBR         =  ATTR.LOC_NBR                   
024600               AND  LOC.LOC_NBR         =  MBSHP.STR_NBR                  
024700               AND  RFRSH.RFRSH_TYP_CDE =  'OM'                           
024800               AND (RFRSH.RFRSH_DTE     IS NULL                           
024900                OR  RFRSH.RFRSH_DTE     =  :PV-BATCH-DATE)                
025000               AND  LOC.LOC_TYP_CDE     =  'DS'                           
025100               AND (ATTR.POS_CAP_LVL_ID =  5                              
025200                OR  ATTR.POS_CAP_LVL_ID >  11)                            
025300               AND ((LOC.OPN_DTE        >  :PV-BATCH-DATE                 
025400               AND (LOC.OPN_DTE         <= :PV-NEW-OPEN-DATE              
025500               AND  LOC.CLO_DTE         >= :PV-NEW-OPEN-DATE))            
025600                OR (LOC.OPN_DTE         <= :PV-BATCH-DATE                 
025700               AND  LOC.CLO_DTE         >= :PV-BATCH-DATE))               
025800               AND  MBSHP.EFF_BEG_DTE   <= :PV-BATCH-DATE                 
025900               AND (MBSHP.EFF_END_DTE   >= :PV-BATCH-DATE                 
026000                OR  MBSHP.EFF_END_DTE   IS NULL)                          
026100             ORDER BY RFRSH.STR_NBR                                       
026200                     ,MBSHP.STR_GP_TYP_CDE                                
026300                     ,MBSHP.STR_GP_ID                                     
026400           WITH UR                                                        
026500     END-EXEC.                                                            
026600                                                                          
026700 LINKAGE SECTION.                                                         
026800                                                                          
026900*----------------------------------------------------------               
027000 PROCEDURE DIVISION.                                                      
027100*----------------------------------------------------------               
027200 0000-MAINLINE.                                                           
027300                                                                          
027400     PERFORM 1000-INITIALIZATION.                                         
027500                                                                          
027600     PERFORM 2000-PROCESS-REFRESH-CURSOR                                  
027700             UNTIL PS-END-OF-REFRESH-CSR.                                 
027800                                                                          
027900     PERFORM 7300-CLOSE-OFFER-REFRESH-CSR.                                
028000                                                                          
028100     PERFORM 9999-WRAPUP.                                                 
028200                                                                          
028300     GOBACK.                                                              
028400                                                                          
028500*0000-EXIT.                                                               
028600*----------------------------------------------------------               
028700* 1000-INITIALIZATION.                                                    
028800*     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS              
028900*     FILES, READS INPUT FILES, AND DOES AN INITIAL FETCH                 
029000*     OF THE MERCHANDISE CURSOR.                                          
029100*----------------------------------------------------------               
029200 1000-INITIALIZATION.                                                     
029300     MOVE '1000-INITIALIZATION       ' TO PV-PARAGRAPH-NAME.              
029400                                                                          
026800     DISPLAY '****************************************'.                  
026900     DISPLAY '**          EPKUT200 STARTED          **'.                  
027000     DISPLAY '****************************************'.                  
030900                                                                          
029500     INITIALIZE  EP250-CC-CONTROL-CARD                                    
029600                 EP257-CC-CONTROL-CARD.                                   
029700     INITIALIZE  PS-PROGRAM-SWITCHES                                      
029800                 PC-PROGRAM-COUNTERS                                      
029900                 PV-SAVE-STR-NBR.                                         
030000                                                                          
030100     OPEN INPUT  CONTROL-CARD-BATCH-DATE                                  
030200                 CONTROL-CARD-ADV-DAYS                                    
030300          OUTPUT REFRESH-STORE-CARD-FILE.                                 
030400                                                                          
030500     SET  PS-NOT-EOF-BATCH-DTE-CNTL-FILE TO TRUE.                 01220002
030500     SET  PS-NOT-EOF-ADV-DAYS-CNTL-FILE  TO TRUE.                 01220002
030600                                                                          
030700** READ BATCH DATE CONTROL CARD                                           
030800     PERFORM 8000-READ-BATCH-DATE-CNTL-CARD.                              
030900                                                                          
027100     DISPLAY '** INPUT BATCH DATE: ' EP257-CC-CRNT-DTE                    
027200             '            **'.                                            
027500     DISPLAY '****************************************'.                  
030900                                                                          
031800     PERFORM 1100-VALIDATE-BATCH-DATE.                                    
031900                                                                          
033100     MOVE EP257-CC-CRNT-DTE      TO PV-BATCH-DATE.                        
033200                                                                          
032000** READ NEW STORE DAYS IN ADVANCE CONTROL CARD                            
032100     PERFORM 8100-READ-ADV-DAYS-CNTL-CARD.                                
032200                                                                          
027100     DISPLAY '** INPUT ADVANCE DAYS: ' EP258-DAYS-TILL-STORE-OPN          
027200             '            **'.                                            
027500     DISPLAY '****************************************'.                  
033000                                                                          
033300*    OBTAIN DATE FOR NEW STORE OPEN CRITERION                             
033400     IF  EP258-DAYS-TILL-STORE-OPN NUMERIC                                
033400         MOVE EP258-DAYS-TILL-STORE-OPN TO PV-DAYS-AHEAD                  
033500         MOVE PV-DAYS-AHEAD         TO PV-SAVE-ADDED-DAYS                 
033600         PERFORM 1200-GET-DATE                                            
033400     ELSE                                                                 
032500         MOVE '1000-INITIALIZE           ' TO PV-PARAGRAPH-NAME           
032600         MOVE 'ADVANCE DAYS VALUE INVALID' TO PV-OPERATION-MSG-1          
036700         SET   AC-4003-CONTROL-CARD-ERROR  TO TRUE                        
032800         PERFORM 9800-ABEND                                               
033400     END-IF.                                                              
033700                                                                          
033800*    SET HARD-CODED TEXT VALUES FOR OUTFILE                               
033900     MOVE  ' STORE NBR: '     TO  EP250-CC-STORE-NAME.                    
034000     MOVE  ' STR GP TYP CD: ' TO  EP250-CC-STORE-GRP-TYP.                 
034100     MOVE  ' STR GP ID: '     TO  EP250-CC-STORE-GRP-ID.                  
034200                                                                          
034300     PERFORM 7100-OPEN-OFFER-REFRESH-CSR.                                 
034400     PERFORM 7200-FETCH-OFFER-REFRESH-CSR.                                
034500                                                                          
034600     IF PS-END-OF-REFRESH-CSR                                             
034700        DISPLAY PC-NO-REFRESHES-FOUND                                     
034800        DISPLAY 'NO REFRESH OF OFFERS DATA TODAY'                         
035200     END-IF.                                                              
035300                                                                          
035400*1000-EXIT.                                                               
035500******************************************************************        
035600* 1100-VALIDATE-BATCH-DATE.                                               
035700*     VALIDATE THE DATE INPUT BY CALLING THE KOHLS CALENDAR               
035800*     ROUTINE AND CHECKING TO SEE WHETHER IT THROWS AN ERROR.             
035900******************************************************************        
036000 1100-VALIDATE-BATCH-DATE.                                                
036100                                                                          
036200     IF  EP257-CC-CRNT-DTE  = SPACES                                      
036300         DISPLAY 'INVALID CONTROL CARD'                                   
036400         DISPLAY 'BATCH DATE: ' EP257-CC-CRNT-DTE                         
036500         MOVE '1100-VALIDATE-BATCH-DATE' TO PV-PARAGRAPH-NAME             
036600         MOVE 'CONTROL CARD IS INVALID'  TO PV-OPERATION-MSG-1            
036700         SET  AC-4003-CONTROL-CARD-ERROR TO TRUE                          
036800         PERFORM 9800-ABEND                                               
036900     END-IF.                                                              
037000                                                                          
031800     PERFORM 6000-VALIDATE-DATE-INPUT.                                    
037000                                                                          
035400*1100-EXIT.                                                               
039000******************************************************************        
039100* 1200-GET-DATE.                                                          
039200*     ADDS THE NUMBER OF DAYS FROM CONTROL CARD 2 TO                      
039300*     TODAY'S DATE.                                                       
039400******************************************************************        
039500 1200-GET-DATE.                                                           
039600                                                                          
040400     PERFORM 6100-DATE-ROUTINE.                                           
039600                                                                          
040500     IF PS-DATE-FOUND                                                     
040600        MOVE DPG55-DB2-ISO-DATE-FMT TO PV-NEW-OPEN-DATE                   
040700        DISPLAY 'NEW DATE:' PV-NEW-OPEN-DATE                              
040800     END-IF.                                                              
040900                                                                          
041000*1200-EXIT.                                                               
041100******************************************************************        
041200* 2000-PROCESS-REFRESH-CURSOR.                                    03440002
041300*      PROCESS THE REFRESH STORE LIST CURSOR.                     03450002
041400******************************************************************        
041500 2000-PROCESS-REFRESH-CURSOR.                                     03440002
041600     MOVE '2000-PROCESS-REFRESH-CURSOR' TO PV-PARAGRAPH-NAME.     03490002
041700                                                                  03500002
041800     IF   EPT00010-STR-NBR  NOT EQUAL TO PV-SAVE-STR-NBR          03500002
041900          ADD +1 TO   PC-CNT-REFRESH-STRS                                 
042000          MOVE EPT00010-STR-NBR   TO  PV-SAVE-STR-NBR             03500002
042100     END-IF.                                                      03500002
042200     MOVE EPT00010-STR-NBR        TO  EP250-CC-STR-NBR            03500002
042300     MOVE XST00023-STR-GP-TYP-CDE TO  EP250-CC-STR-GP-TYP-CDE     03500002
042400     MOVE XST00023-STR-GP-ID      TO  EP250-CC-STR-GRP-ID         03500002
042500                                                                  03500002
042600     PERFORM 8200-WRITE-STR-RFRSH-CNTL-REC.                               
042700                                                                          
042800     PERFORM 7200-FETCH-OFFER-REFRESH-CSR.                                
042900                                                                          
043000*2000-EXIT.                                                               
035500******************************************************************        
035600* 6000-VALIDATE-DATE-INPUT.                                               
035700*     VALIDATE THE DATE INPUT BY CALLING THE KOHLS CALENDAR               
035800*     ROUTINE AND CHECKING TO SEE WHETHER IT THROWS AN ERROR.             
035900******************************************************************        
036000 6000-VALIDATE-DATE-INPUT.                                                
036100                                                                          
037100     INITIALIZE DPG51 DPG52 DPG53                                         
037200                DPG54 DPG55 DPG56.                                        
037300                                                                          
037400     SET  DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                             
037500     SET  DPG52-LK-DTE-ISO-FMT       TO TRUE.                             
037700     MOVE EP257-CC-CRNT-DTE          TO DPG52-LK-DATE-INPUT.              
037800                                                                          
043700     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
043800                                             DPG54 DPG55 DPG56.           
043900     EVALUATE TRUE                                                        
044000         WHEN DPG54-NO-ERROR                                              
044100             DISPLAY ' '                                                  
044200             DISPLAY 'INPUT BATCH DATE OK > ' EP257-CC-CRNT-DTE           
044600         WHEN OTHER                                                       
044700             DISPLAY ' '                                                  
044800             DISPLAY '** ABEND ** IN ' PC-PROGRAM-NAME                    
044900             DISPLAY '** INVALID BATCH DATE PASSED IN CC '                
045000             DISPLAY '** 6000-VALIDATE-DATE-INPUT'                        
045100             DISPLAY '** ERROR MESSAGE = '                                
045200                               DPG54-ERROR-MESSAGE                        
045300             DISPLAY '** CALENDAR TYPE = ' DPG51-CALENDAR-TYPE            
045300             DISPLAY '** DATE FORMAT = ' DPG52-LK-DATE-FORMAT-CODE        
045300             DISPLAY '** INPUT DATE  = ' DPG52-LK-DATE-INPUT              
045900             MOVE '6000-VALIDATE-DATE-INPUT'                              
046000                                  TO PV-PARAGRAPH-NAME                    
046100             MOVE 'ERROR VALIDATING CONTROL CARD INPUT DATE'              
046200                                  TO PV-OPERATION-MSG-1                   
046300             MOVE DPG54-ERROR-MESSAGE  TO PV-OPERATION-MSG-2              
046400             SET AC-4019-CALENDAR-ROUTINE-ERROR TO TRUE                   
046500             PERFORM 9800-ABEND                                           
046600     END-EVALUATE.                                                        
046700                                                                          
046800*6000-EXIT.                                                               
043100******************************************************************        
043200* 6100-DATE-ROUTINE.                                                      
043300*     CALLS KOHL'S DATE ROUTINE.                                          
043400******************************************************************        
043500 6100-DATE-ROUTINE.                                                       
043600                                                                          
039700     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
043600                                                                          
039800     MOVE PV-BATCH-DATE                TO DPG52-LK-DATE-INPUT.            
039900     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                           
040000     SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                           
040100     SET  DPG51-INCREMENT-DATE         TO TRUE.                           
040200     MOVE PV-SAVE-ADDED-DAYS           TO DPG51-INCR-DECR-DAYS-X.         
040300     SET PS-DATE-NOT-FOUND TO TRUE.                                       
040300                                                                          
043700     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
043800                                             DPG54 DPG55 DPG56.           
040300                                                                          
043900     EVALUATE TRUE                                                        
044000         WHEN DPG54-NO-ERROR                                              
044100             DISPLAY ' '                                                  
044200             DISPLAY 'RETRIEVING NEW CALCULATED DATE'                     
044300             DISPLAY 'DPKUT500 DATE RETRIEVED:'                           
044400                DPG55-DB2-ISO-DATE-FMT                                    
044500             SET PS-DATE-FOUND TO TRUE                                    
044600         WHEN OTHER                                                       
044700             DISPLAY ' '                                                  
044800             DISPLAY '** ABEND ** IN ' PC-PROGRAM-NAME                    
044900             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
045000                       ' TO GET NEW CALCULATED DATE'                      
045100             DISPLAY '** ERROR MESSAGE = '                                
045200                               DPG54-ERROR-MESSAGE                        
045300             DISPLAY '** CALENDAR TYPE = ' DPG51-CALENDAR-TYPE            
045300             DISPLAY '** DATE FORMAT = ' DPG52-LK-DATE-FORMAT-CODE        
045300             DISPLAY '** INPUT DATE  = ' DPG52-LK-DATE-INPUT              
045300                      ', DECREMENT FLAG = '                               
045300                                  DPG51-INCREMENT-DECREMENT-FLAG          
045300                      ', DECREMENT DAYS = ' DPG51-INCR-DECR-DAYS-X        
045900             MOVE '6100-DATE-ROUTINE'  TO PV-PARAGRAPH-NAME               
046100             MOVE 'ERROR CALCULATING END DATE'                            
046200                                       TO PV-OPERATION-MSG-1              
046300             MOVE DPG54-ERROR-MESSAGE  TO PV-OPERATION-MSG-2              
046400             SET  AC-4019-CALENDAR-ROUTINE-ERROR TO TRUE                  
046500             PERFORM 9800-ABEND                                           
046600     END-EVALUATE.                                                        
046700                                                                          
046800*6100-EXIT.                                                               
046900******************************************************************        
047000* 7100-OPEN-OFFER-REFRESH-CSR.                                    03440002
047100*       - OPEN THE REFRESH STORE LIST CURSOR.  THE OPEN WILL BE   03450002
047200*         ATTEMPTED UP TO PC-MAX-RETRIES TIMES.                   03460002
047300******************************************************************        
047400 7100-OPEN-OFFER-REFRESH-CSR.                                     03480002
047500     MOVE '7100-OPEN-OFFER-REFRESH-CSR' TO PV-PARAGRAPH-NAME.     03490002
047600                                                                  03500002
047700     PERFORM                                                      03510002
047800         WITH TEST AFTER                                          03520002
047900         VARYING PV-SQL-SUB                                       03530002
048000         FROM 1 BY 1                                              03540002
048100         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                 03550002
048200                   OR  SQLCODE = 0)                               03560002
048300                                                                  03570002
048400         EXEC SQL                                                 03580002
048500           OPEN OFFER-REFRESH-CSR                                 03590002
048600         END-EXEC                                                 03600002
048700                                                                  03610002
048800         EVALUATE TRUE                                            03620002
048900           WHEN SQLCODE = -904                                    03630002
049000           WHEN SQLCODE = -911                                    03640002
049100           WHEN SQLCODE = -913                                    03650002
049200             CONTINUE                                             03660002
049300                                                                  03670002
049400           WHEN SQLCODE = 0                                       03680002
049500             SET PS-NOT-END-OF-REFRESH-CSR TO TRUE                03690002
049600                                                                  03700002
049700           WHEN SQLWARN0 NOT EQUAL SPACE                          03710002
049800           WHEN SQLCODE NOT EQUAL ZERO                            03720002
049900             MOVE 'ERROR ON OPEN OF REFRESH CURSOR'               03730002
050000                                TO PV-DB2-OPERATION               03740002
050100             PERFORM 9900-SQL-ABEND-ROUTINE                       03750002
050200                                                                  03760002
050300         END-EVALUATE                                             03770002
050400     END-PERFORM.                                                 03780002
050500                                                                  03790002
050600     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         03800002
050700       MOVE 'OPEN RETRIES EXCEEDED'   TO  PV-DB2-OPERATION        03810002
050800       PERFORM 9900-SQL-ABEND-ROUTINE                             03820002
050900     END-IF.                                                      03830002
051000                                                                  03840002
051100*7100-EXIT.                                                       03850002
051200***********************************************************               
051300* 7200-FETCH-OFFER-REFRESH-CSR.                                   03870002
051400*     - FETCH THE OFFERS REFRESH STORE LIST RECORDS.              03880002
051500***********************************************************               
051600 7200-FETCH-OFFER-REFRESH-CSR.                                    03990002
051700     MOVE '7200-FETCH-OFFER-REFRESH-CSR' TO PV-PARAGRAPH-NAME.    04000002
051800                                                                  04010002
051900     PERFORM                                                              
052000         WITH TEST AFTER                                                  
052100         VARYING PV-SQL-SUB                                               
052200         FROM 1 BY 1                                                      
052300         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
052400                   OR  SQLCODE = 0                                        
052500                   OR  SQLCODE = +100)                                    
052600                                                                          
052700           EXEC SQL                                                       
052800              FETCH OFFER-REFRESH-CSR                                     
052900              INTO  :EPT00010-STR-NBR                                     
053000                   ,:XST00023-STR-GP-TYP-CDE                              
053100                   ,:XST00023-STR-GP-ID                                   
053200           END-EXEC                                                       
053300                                                                          
053400          EVALUATE TRUE                                                   
053500             WHEN SQLCODE = -904                                          
053600             WHEN SQLCODE = -911                                          
053700             WHEN SQLCODE = -913                                          
053800                  CONTINUE                                                
053900             WHEN SQLCODE = ZERO                                          
054000                  ADD 1 TO PC-CNT-REFRESH-CSR-FETCHES                     
054100             WHEN SQLCODE = +100                                          
054200                  SET PS-END-OF-REFRESH-CSR TO TRUE                       
054300             WHEN SQLWARN0 NOT EQUAL SPACE                                
054400             WHEN SQLCODE NOT EQUAL ZERO                                  
054500                  MOVE 'ERROR ON FETCHING REFRESH CURSOR'                 
054600                                     TO PV-DB2-OPERATION                  
054700                  DISPLAY 'SQLCODE= ' SQLCODE                             
054800                  PERFORM 9900-SQL-ABEND-ROUTINE                          
054900          END-EVALUATE                                                    
055000     END-PERFORM.                                                         
055100                                                                          
055200     IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
055300         MOVE 'FETCH RETRIES EXCEEDED'                                    
055400                               TO  PV-DB2-OPERATION                       
055500         PERFORM 9900-SQL-ABEND-ROUTINE                                   
055600     END-IF.                                                              
055700                                                                  04370002
055800*7200-EXIT.                                                       04380002
055900***********************************************************               
056000* 7300-CLOSE-REFRESH-CURSOR.                                      04410002
056100*       - CLOSE THE REFRESH STORE LIST CURSOR.  THE CLOSE WILL    04420002
056200*         BE ATTEMPTED UP TO PC-MAX-RETRIES TIMES.                04430002
056300***********************************************************               
056400 7300-CLOSE-OFFER-REFRESH-CSR.                                    04450002
056500     MOVE '7300-CLOSE-OFFER-REFRESH-CSR' TO PV-PARAGRAPH-NAME.    04460002
056600                                                                  04470002
056700     PERFORM                                                      04480002
056800         WITH TEST AFTER                                          04490002
056900         VARYING PV-SQL-SUB                                       04500002
057000         FROM 1 BY 1                                              04510002
057100         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                 04520002
057200                   OR  SQLCODE = 0)                               04530002
057300                                                                  04540002
057400         EXEC SQL                                                 04550002
057500           CLOSE OFFER-REFRESH-CSR                                04560002
057600         END-EXEC                                                 04570002
057700                                                                  04580002
057800         EVALUATE TRUE                                            04590002
057900           WHEN SQLCODE = -904                                    04600002
058000           WHEN SQLCODE = -911                                    04610002
058100           WHEN SQLCODE = -913                                    04620002
058200             CONTINUE                                             04630002
058300                                                                  04640002
058400           WHEN SQLCODE = 0                                       04650002
058500             CONTINUE                                             04660002
058600                                                                  04670002
058700           WHEN SQLWARN0 NOT EQUAL SPACE                          04680002
058800           WHEN SQLCODE NOT EQUAL ZERO                            04690002
058900             MOVE 'ERROR ON CLOSE OF REFRESH CURSOR'              04700002
059000                                TO PV-DB2-OPERATION               04710002
059100             PERFORM 9900-SQL-ABEND-ROUTINE                       04720002
059200         END-EVALUATE                                             04730002
059300     END-PERFORM.                                                 04740002
059400                                                                  04750002
059500     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         04760002
059600       MOVE 'CLOSE RETRIES EXCEEDED'  TO  PV-DB2-OPERATION        04770002
059700       PERFORM 9900-SQL-ABEND-ROUTINE                             04780002
059800     END-IF.                                                      04790002
059900                                                                  04800002
060000*7300-EXIT.                                                       04810002
060100***********************************************************               
060200* 8000-READ-BATCH-DATE-CNTL-CARD                                          
060300*     READ INPUT BATCH DATE CONTROL CARD - TODAY'S DATE                   
060400***********************************************************               
060500 8000-READ-BATCH-DATE-CNTL-CARD.                                          
060600                                                                          
060700     READ CONTROL-CARD-BATCH-DATE INTO EP257-CC-CONTROL-CARD              
060800        AT END SET PS-EOF-BATCH-DTE-CNTL-FILE TO TRUE                     
060900            NOT AT END ADD 1 TO PC-CNT-READ-RECS                          
061000     END-READ.                                                            
061100                                                                          
031000** ABEND IF BATCH DATE CONTROL CARD IS EMPTY                              
031100     IF PS-EOF-BATCH-DTE-CNTL-FILE                                        
031200        MOVE '8000-READ-BATCH-DATE-CNTL-CARD' TO PV-PARAGRAPH-NAME        
031300        MOVE 'BATCH DATE CARD IS EMPTY  '    TO PV-OPERATION-MSG-1        
031400        SET   AC-4008-CONTROL-CARD-EMPTY     TO TRUE                      
031500        PERFORM 9800-ABEND                                                
031600     END-IF.                                                              
031700                                                                          
061200*8000-EXIT.                                                               
061300***********************************************************               
061400* 8100-READ-ADV-DAYS-CNTL-CARD                                            
061500*     READ NEW STORE LEAD DAYS CONTROL CARD.                              
061600*     THIS GETS THE NUMBER OF DAYS FOR WHICH A NEW                        
061700*     STORE SHOULD BE ADDED TO THE DISTRIBUTION LIST.                     
061800***********************************************************               
061900 8100-READ-ADV-DAYS-CNTL-CARD.                                            
062000                                                                          
062100     READ CONTROL-CARD-ADV-DAYS INTO EP258-ADVDAYS-TO-SND-RECORD          
062200        AT END SET PS-EOF-ADV-DAYS-CNTL-FILE TO TRUE                      
062300            NOT AT END ADD 1 TO PC-CNT-READ-RECS                          
062400     END-READ.                                                            
062500                                                                          
032300** ABEND IF NEW STORE DAYS IN ADVANCE CONTROL CARD IS EMPTY               
032400     IF  PS-EOF-ADV-DAYS-CNTL-FILE                                        
032500         MOVE '8100-READ-ADV-DAYS-CNTL-CARD' TO PV-PARAGRAPH-NAME         
032600         MOVE 'ADVANCE DAYS CARD IS EMPTY'   TO PV-OPERATION-MSG-1        
032700         SET  AC-4008-CONTROL-CARD-EMPTY     TO TRUE                      
032800         PERFORM 9800-ABEND                                               
032900     END-IF.                                                              
033000                                                                          
062600*8100-EXIT.                                                               
062700***********************************************************               
062800* 8200-WRITE-STR-RFRSH-CNTL-REC.                                          
062900*     WRITES THE FULL FILE EXTRACT STORE GROUP DRIVER RECORD              
063000*     OUT TO THE DRIVER FILE.                                             
063100***********************************************************               
063200 8200-WRITE-STR-RFRSH-CNTL-REC.                                           
063300                                                                          
063400     WRITE REFRESH-STORE-CARD-RECORD                                      
063500         FROM EP250-CC-CONTROL-CARD.                                      
063600                                                                          
063700     ADD 1 TO PC-CNT-OUT-RECS                                             
063800              PC-CNT-ALL-RECS.                                            
063900                                                                          
064000*8200-EXIT.                                                               
064100***********************************************************               
064200* 9800-ABEND.                                                             
064300*     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                          
064400***********************************************************               
064500 9800-ABEND.                                                              
064600                                                                          
064700     DISPLAY '** ABEND           **'.                                     
064800     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
064900     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
065000     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
065100     DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
065200     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
065300                                                                          
065400*9800-EXIT.                                                               
065500***********************************************************               
065600* 9900-SQL-ABEND-ROUTINE.                                                 
065700*     SQL ABEND ROUTINE.                                                  
065800***********************************************************               
065900 9900-SQL-ABEND-ROUTINE.                                                  
066000                                                                          
066100     SET AC-4013-DB2-ERROR TO TRUE.                                       
066200     DISPLAY '** ABEND     **'.                                           
066300     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
066400     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
066500     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
066600                                                                          
066700     COPY DPPD004.                                                        
066800     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
066900                                                                          
067000*9900-EXIT.                                                               
067100***********************************************************               
067200* 9999-WRAPUP.                                                            
067300*     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
067400*     ANY DATA FROM THE PROGRAM.                                          
067500***********************************************************               
067600 9999-WRAPUP.                                                             
067700                                                                          
067800     CLOSE CONTROL-CARD-BATCH-DATE                                        
067900           CONTROL-CARD-ADV-DAYS                                          
068000           REFRESH-STORE-CARD-FILE.                                       
068100                                                                          
068200     DISPLAY ' '.                                                         
068300     DISPLAY '*********************************************'.             
068400     DISPLAY 'PROGRAM NAME: ' PC-PROGRAM-NAME.                            
068500     DISPLAY '*********************************************'.             
068600     DISPLAY 'NUMBER OF STORES NEEDING REFRESH: '                         
068700                                  PC-CNT-REFRESH-STRS.                    
068800     DISPLAY 'NUMBER OF STORE / STORE GROUP RECS FETCHED: '               
068900                                  PC-CNT-REFRESH-CSR-FETCHES.             
069000     DISPLAY 'NUMBER OF STORE GRP DRIVER RECS WRITTEN: '                  
069100                                  PC-CNT-OUT-RECS.                        
069200     DISPLAY 'ALL RECS WRITTEN: ' PC-CNT-ALL-RECS.                        
069300     DISPLAY '*********************************************'.             
069400                                                                          
069500*9999-EXIT.                                                               
069600**********************  END OF PROGRAM  **********************            
