       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP010.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050013
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070013
      **       VSM ITEM MESSAGE - NUMBER 095 - UPC ADD                  00080013
      ******************************************************************00090013
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO ADD A NEW VENDOR UPC TO00100020
      ***     TABLE XST_UPC.                                            00110013
      ******************************************************************00120013
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00130013
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00140018
      ******************************************************************00150013
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.  IBM-3090.                                      00180000
       OBJECT-COMPUTER.  IBM-3090.                                      00190000
       DATA DIVISION.                                                   00200000
                                                                        00210005
       WORKING-STORAGE SECTION.                                         00220000
      ******************************************************************00230000
      **PROGRAM SWITCHES                                                00240000
      ******************************************************************00250000
       01  PS-PROGRAM-SWITCHES.                                         00260000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00270003
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00280003
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00290003
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00300015
               88  PS-ACTION-REVERSE                  VALUE 01.         00310015
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00320015
               88  PS-ACTION-NOTHING                  VALUE 02.         00330015
      ******************************************************************00340000
      **PROGRAM VARIABLES                                               00350000
      ******************************************************************00360000
       01  PV-PROGRAM-VARIABLES.                                        00370003
           05  PV-PARAGRAPH-NAME            PIC X(20)  VALUE SPACES.    00380013
           05  PV-TIME-EVALUATE-INSERT.                                 00390003
               10  PV-TIME-HOUR-IN          PIC 9(02) VALUE ZEROS.      00400013
               10  PV-TIME-MINUTE-IN        PIC 9(02) VALUE ZEROS.      00410013
               10  PV-TIME-SECOND-IN        PIC 9(02) VALUE ZEROS.      00420013
               10  PV-TIME-MICROSEC-IN      PIC 9(06) VALUE ZEROS.      00430013
           05  PV-TIME-EVALUATE-UPDATE.                                 00440003
               10  PV-TIME-HOUR-UP          PIC 9(02) VALUE ZEROS.      00450013
               10  PV-TIME-MINUTE-UP        PIC 9(02) VALUE ZEROS.      00460013
               10  PV-TIME-SECOND-UP        PIC 9(02) VALUE ZEROS.      00470013
               10  PV-TIME-MICROSEC-UP      PIC 9(06) VALUE ZEROS.      00480013
      ******************************************************************00490005
      **PROGRAM CALCULATIONS                                            00500005
      ******************************************************************00510005
       01  PC-PROGRAM-CALCULATIONS.                                     00520005
           05  PC-10-DIGIT                  PIC 9(10)  VALUE ZEROS.     00530013
           05  PC-18-DIGIT                  PIC S9(18) COMP VALUE ZEROS.00540013
           05  PC-09-DIGIT                  PIC S9(09) COMP VALUE ZEROS.00550013
      ******************************************************************00560003
      **TIMESTAMP VARIABLES                                             00570003
      ******************************************************************00580003
       01  SD-TIMESTAMP                     PIC X(26) VALUE SPACES.     00590013
       01  FILLER REDEFINES SD-TIMESTAMP.                               00600003
           05  SD-CURR-DTE                  PIC X(10).                  00610013
           05  FILLER                       PIC X(01).                  00620013
           05  SD-HOUR                      PIC X(02).                  00630013
           05  FILLER                       PIC X(01).                  00640013
           05  SD-MINUTE                    PIC X(02).                  00650013
           05  FILLER                       PIC X(01).                  00660013
           05  SD-SECOND                    PIC X(02).                  00670013
           05  FILLER                       PIC X(01).                  00680013
           05  SD-MICROSEC                  PIC X(06).                  00690013
      ******************************************************************00700003
      **PROGRAM COUNTERS                                                00710003
      ******************************************************************00720003
       01  PC-PROGRAM-COUNTERS.                                         00730003
           05  PC-RETRY-COUNTER             PIC 9(01) VALUE ZEROS.      00740013
           05  PC-RETRY-COUNTER-2           PIC 9(01) VALUE ZEROS.      00750013
           05  PC-RETRY-MAX                 PIC 9(01) VALUE 5.          00760013
      ******************************************************************00770000
      **COPYBOOKS                                                       00780000
      ******************************************************************00790000
      ***VSM COPYBOOK                                                   00800005
           COPY IMRD008.                                                00810021
      ***ABEND COPYBOOK                                                 00820005
           COPY DPWS004.                                                00830021
      ***DB2 MESSAGE AREA                                               00840000
           COPY SQLCA2.                                                 00850021
      ***DATE ROUTINE COPYBOOK                                          00860003
           COPY DPWS500.                                                00870021
      ******************************************************************00880000
      **TABLE INCLUSIONS                                                00890000
      ******************************************************************00900000
      ***DB2 COMMUNICATIONS AREA                                        00910000
           EXEC SQL                                                     00920000
                INCLUDE SQLCA                                           00930000
           END-EXEC.                                                    00940000
      ***XST_UPC TABLE                                                  00950000
           EXEC SQL                                                     00960000
               INCLUDE XST00012                                         00970000
           END-EXEC.                                                    00980000
                                                                        00990000
       LINKAGE SECTION.                                                 01000000
      ******************************************************************01010000
      **LINKAGE VARIABLES                                               01020000
      ******************************************************************01030000
       01  LV-IMRD008-RECORD               PIC X(450).                  01040005
       01  LV-RETURN-CODE                  PIC S9(04).                  01050005
       01  LV-ACTION-IND                   PIC 9(02).                   01060015
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   01070015
                                                                        01080000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      01090005
                                LV-RETURN-CODE,                         01100015
                                LV-ACTION-IND,                          01110015
                                LV-CURRENT-TIMESTAMP.                   01120015
      ******************************************************************01130000
       0000-MAINLINE.                                                   01140000
                                                                        01150000
            PERFORM 1000-INITIALIZATION.                                01160000
            PERFORM 2000-PROCESS-MESSAGE                                01170013
                    UNTIL PS-PROCESS-COMPLETE.                          01180005
            PERFORM 9999-WRAPUP.                                        01190000
                                                                        01200000
       EJECT.                                                           01210005
      ***************************************************************** 01220000
      **INITIALIZATION AREA                                             01230000
      ***************************************************************** 01240000
       1000-INITIALIZATION.                                             01250000
                                                                        01260000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01270005
                                                                        01280004
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01290003
           INITIALIZE PC-PROGRAM-COUNTERS.                              01300013
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01310005
                                                                        01320003
      ***SET UP TIMESTAMP IN CASE DATE COMPARISON IS NEEDED             01330003
           MOVE IM008-CRE-TMST             TO SD-TIMESTAMP.             01340003
           MOVE SD-HOUR                    TO PV-TIME-HOUR-IN.          01350003
           MOVE SD-MINUTE                  TO PV-TIME-MINUTE-IN.        01360003
           MOVE SD-SECOND                  TO PV-TIME-SECOND-IN.        01370003
           MOVE SD-MICROSEC                TO PV-TIME-MICROSEC-IN.      01380003
           INITIALIZE SD-TIMESTAMP.                                     01390003
                                                                        01400000
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01410005
      *     COMP FIELDS.                                                01420005
           MOVE IM008-ITEM-KEY-UPC-ID      TO PC-10-DIGIT.              01430005
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01440005
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01450005
           MOVE PC-09-DIGIT                TO XST00012-UPC-ID.          01460005
                                                                        01470005
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01480005
      *     COMP FIELDS.                                                01490005
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01500005
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01510005
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01520005
           MOVE PC-09-DIGIT                TO XST00012-INTR-UPC-ID.     01530005
                                                                        01540006
           MOVE IM008-ITEM-KEY-UPC-NBR     TO XST00012-UPC-NBR.         01550000
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00012-LAST-UPD-TMST.   01560018
121402     MOVE IM008-CRE-TMST            TO XST00012-SOR-LAST-MSG-TMST.01570018
                                                                        01580000
       EJECT.                                                           01590005
      ***************************************************************** 01600000
      **INSERTS NEW UPC INTO XST_UPC TABLE                              01610020
      ***************************************************************** 01620000
       2000-PROCESS-MESSAGE.                                            01630013
                                                                        01640000
           MOVE '2000-PROCESS-MESSAGE'          TO PV-PARAGRAPH-NAME.   01650013
                                                                        01660004
           EXEC SQL                                                     01670005
              INSERT INTO XST_UPC ( UPC_ID                              01680005
                                  , INTR_UPC_ID                         01690005
                                  , UPC_TYP_CDE                         01700005
                                  , UPC_NBR                             01710005
                                  , LAST_UPD_TMST                       01720018
121402                            , SOR_LAST_MSG_TMST                   01730019
121402                            , LAST_ACTN_CDE)                      01740019
              VALUES ( :XST00012-UPC-ID                                 01750005
                     , :XST00012-INTR-UPC-ID                            01760005
                     , 'V'                                              01770020
                     , :XST00012-UPC-NBR                                01780005
                     , :XST00012-LAST-UPD-TMST                          01790018
121402               , :XST00012-SOR-LAST-MSG-TMST                      01800019
121402               , 'A')                                             01810019
           END-EXEC.                                                    01820005
                                                                        01830005
           EVALUATE TRUE                                                01840005
      ***SUCCESSFUL PROCESSING                                          01850020
              WHEN SQLCODE = ZERO                                       01860005
                   SET PS-PROCESS-COMPLETE TO TRUE                      01870005
      ***REFERENTIAL INTEGRITY ISSUE                                    01880020
              WHEN SQLCODE = -530                                       01890005
              WHEN SQLCODE = -532                                       01900020
                   SET PS-PROCESS-COMPLETE      TO TRUE                 01910005
      ***ROW ALREADY EXISTS ON TABLE                                    01920020
              WHEN SQLCODE = -803                                       01930005
                   PERFORM 2100-SELECT-DATA                             01940005
      ***ROW/TABLE LOCK                                                 01950020
              WHEN SQLCODE = -911                                       01960005
              WHEN SQLCODE = -913                                       01970005
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 01980005
                       SET PS-PROCESS-COMPLETE  TO TRUE                 01990005
                   ELSE                                                 02000005
                       ADD 1                    TO PC-RETRY-COUNTER     02010005
                   END-IF                                               02020005
              WHEN OTHER                                                02030005
                   SET PS-PROCESS-COMPLETE      TO TRUE                 02040005
           END-EVALUATE.                                                02050005
                                                                        02060003
       EJECT.                                                           02070005
      ******************************************************************02080003
      *SELECT TIMESTAMP FOR EXISTING RECORD FROM TABLE FOR COMPARISON   02090003
      ******************************************************************02100003
       2100-SELECT-DATA.                                                02110003
                                                                        02120003
           MOVE '2100-SELECT-DATA'             TO PV-PARAGRAPH-NAME     02130005
                                                                        02140003
           EXEC SQL                                                     02150003
                SELECT LAST_UPD_TMST                                    02160003
                INTO  :SD-TIMESTAMP                                     02170003
                FROM   XST_UPC                                          02180003
                WHERE  UPC_ID = :XST00012-UPC-ID                        02190003
           END-EXEC.                                                    02200003
                                                                        02210003
           EVALUATE TRUE                                                02220003
      ***SUCCESSFUL PROCESSING                                          02230020
              WHEN SQLCODE = ZEROS                                      02240003
                   PERFORM 2150-DATE-COMPARE                            02250003
              WHEN OTHER                                                02260003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02270003
           END-EVALUATE.                                                02280003
                                                                        02290003
       EJECT.                                                           02300005
      ******************************************************************02310003
      **COMPARE DATE OF EXISTING ROW TO MESSAGE AND UPDATE IF MESSAGE   02320003
      **        HAS A NEWER TIMESTAMP.                                  02330003
      ******************************************************************02340003
       2150-DATE-COMPARE.                                               02350003
                                                                        02360003
           MOVE '2150-DATE-COMPARE'      TO PV-PARAGRAPH-NAME.          02370006
                                                                        02380003
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02390003
                                                                        02400003
           SET DPG53-LK-CMPR-DTE-ISO-FMT   TO TRUE.                     02410019
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     02420019
           SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                     02430019
           MOVE XST00012-SOR-LAST-MSG-TMST TO DPG52-LK-DATE-INPUT.      02440019
           MOVE SD-TIMESTAMP               TO DPG53-LK-CMPR-DATE-INPUT. 02450019
                                                                        02460003
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                02470003
                                                   DPG52                02480003
                                                   DPG53                02490003
                                                   DPG54                02500003
                                                   DPG55                02510003
                                                   DPG56.               02520003
                                                                        02530008
           EVALUATE TRUE                                                02540003
              WHEN DPG55-DAYS-DIFFERENCE = 0                            02550003
                   MOVE SD-HOUR          TO PV-TIME-HOUR-UP             02560005
                   MOVE SD-MINUTE        TO PV-TIME-MINUTE-UP           02570005
                   MOVE SD-SECOND        TO PV-TIME-SECOND-UP           02580005
                   MOVE SD-MICROSEC      TO PV-TIME-MICROSEC-UP         02590005
                   EVALUATE TRUE                                        02600003
                      WHEN PV-TIME-EVALUATE-INSERT >                    02610003
                                                PV-TIME-EVALUATE-UPDATE 02620005
                           PERFORM 2200-UPDATE-DATA                     02630003
                                   UNTIL PS-PROCESS-COMPLETE            02640003
                      WHEN OTHER                                        02650003
                           SET PS-ACTION-NOTHING   TO TRUE              02660015
                           SET PS-PROCESS-COMPLETE TO TRUE              02670003
                   END-EVALUATE                                         02680003
      ***DATA ON TABLE IS NEWER                                         02690020
              WHEN DPG55-DAYS-DIFFERENCE < ZERO                         02700003
                   SET PS-PROCESS-COMPLETE         TO TRUE              02710005
      ***DATA ON MESSAGE IS NEWER                                       02720020
              WHEN DPG55-DAYS-DIFFERENCE > ZERO                         02730003
                   PERFORM 2200-UPDATE-DATA                             02740003
                           UNTIL PS-PROCESS-COMPLETE                    02750003
           END-EVALUATE.                                                02760003
                                                                        02770003
       EJECT.                                                           02780005
      ******************************************************************02790003
      **MESSAGE HAS NEWER TIMESTAMP SO UPDATE DATA ON TABLE             02800003
      ******************************************************************02810003
       2200-UPDATE-DATA.                                                02820003
                                                                        02830003
           MOVE '2200-UPDATE-DATA'             TO PV-PARAGRAPH-NAME     02840010
                                                                        02850003
           EXEC SQL                                                     02860003
              UPDATE XST_UPC                                            02870003
                  SET   INTR_UPC_ID   = :XST00012-INTR-UPC-ID           02880003
                      , UPC_TYP_CDE   = 'V'                             02890020
                      , UPC_NBR       = :XST00012-UPC-NBR               02900003
                      , LAST_UPD_TMST = :XST00012-LAST-UPD-TMST         02910003
121402                , SOR_LAST_MSG_TMST = :XST00012-SOR-LAST-MSG-TMST 02920018
121402                , LAST_ACTN_CDE = 'U'                             02930019
                  WHERE UPC_ID        = :XST00012-UPC-ID                02940003
           END-EXEC.                                                    02950003
                                                                        02960003
           EVALUATE TRUE                                                02970003
      ***SUCCESSFUL PROCESSING                                          02980020
              WHEN SQLCODE = ZEROS                                      02990003
                   SET PS-ACTION-REVERSE       TO TRUE                  03000015
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03010003
      ***ROW/TABLE LOCK                                                 03020020
              WHEN SQLCODE = -911                                       03030003
              WHEN SQLCODE = -913                                       03040003
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               03050003
                       SET PS-PROCESS-COMPLETE TO TRUE                  03060003
                   ELSE                                                 03070003
                       ADD +1                  TO PC-RETRY-COUNTER-2    03080003
                   END-IF                                               03090003
              WHEN OTHER                                                03100003
                   SET PS-PROCESS-COMPLETE TO TRUE                      03110003
           END-EVALUATE.                                                03120003
                                                                        03130003
       EJECT.                                                           03140005
      ******************************************************************03150000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             03160000
      ******************************************************************03170000
       9999-WRAPUP.                                                     03180000
                                                                        03190000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              03200017
                                                                        03210000
           MOVE SQLCODE               TO LV-RETURN-CODE.                03220017
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 03230015
                                                                        03240015
           EXIT PROGRAM.                                                03250000
                                                                        03260000
       EJECT.                                                           03270005
