00001 *-----------------------*                                         06/20/94
00002  IDENTIFICATION DIVISION.                                         DPKCS043
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    DPKCS043.                                         DPKCS043
00005  AUTHOR.        PAT DOYLE - OMNI RESOURCES.                       DPKCS043
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          DPKCS043
00007  DATE-WRITTEN.  03-06-93.                                         DPKCS043
00008  DATE-COMPILED.                                                   DPKCS043
00009                                                                   DPKCS043
00010 *---------------------------------------------------------------* DPKCS043
00011 *    Z043 - OBJECT LOCK INQUIRY PROGRAM.                        * DPKCS043
00012 *                                                               * DPKCS043
00013 *    THIS PROGRAM DISPLAYS OBJECT LOCKS FOR ANY ITEMS THAT ARE  * DPKCS043
00014 *    BEING HELD ON THE OBJECT LOCK TABLE.                       * DPKCS043
00015 *                                                               * DPKCS043
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023      *         
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE  *         
262023*  MAKING THE CALL DYNAMIC VS STATIC                            *         
00016 *---------------------------------------------------------------* DPKCS043
00017                                                                   DPKCS043
00018  ENVIRONMENT DIVISION.                                            DPKCS043
00019                                                                   DPKCS043
00020  CONFIGURATION SECTION.                                           DPKCS043
00021                                                                   DPKCS043
00022  SPECIAL-NAMES.                                                   DPKCS043
00023                                                                   DPKCS043
00024      CLASS SPECIAL-CHARACTER IS '›' '.' '<' '(' '+' '|' '&'       DPKCS043
00025                                 '!' '$' '*' ')' ';' '^' '-'       DPKCS043
00026                                 '/' ',' '%' '_' '>' '?' ':'       DPKCS043
00027                                 '#' '@' '''' '=' '"'.             DPKCS043
00028                                                                   DPKCS043
00029                                                                   DPKCS043
00030  DATA DIVISION.                                                   DPKCS043
00031                                                                   DPKCS043
00032  WORKING-STORAGE SECTION.                                         DPKCS043
00033                                                                   DPKCS043
00034  01  PC-PROGRAM-CONSTANTS.                                        DPKCS043
00035      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         DPKCS043
00036                                                     'DP043A  '.   DPKCS043
00037      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         DPKCS043
00038                                                     'DPKM043 '.   DPKCS043
00039      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         DPKCS043
00040                                                     'DPKCS043'.   DPKCS043
00041      05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 1.      DPKCS043
00042      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +15     DPKCS043
00043                                                     COMP SYNC.    DPKCS043
00044      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      DPKCS043
00045                                                     COMP SYNC.    DPKCS043
00046      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         DPKCS043
00047                                                     +1000000      DPKCS043
00048                                                     COMP-3.       DPKCS043
00049      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      DPKCS043
00050                                                     COMP SYNC.    DPKCS043
00051      05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3      DPKCS043
00052                                                      COMP-3.      DPKCS043
00053      05  PC-OBJLOK-BASE-DD       PIC  X(08)  VALUE 'DPOBJLOK'.    DPKCS043
00054      05  PC-OBJLOK-AIX-TASK-DD   PIC  X(08)  VALUE 'DPOBJLK1'.    DPKCS043
00055      05  PC-OBJLOK-AIX-NETN-DD   PIC  X(08)  VALUE 'DPOBJLK2'.    DPKCS043
00056      05  PC-NAVIGATOR-TRAN-ID    PIC  X(04)  VALUE 'Z008'.        DPKCS043
00057      05  PC-TSYMSG-NUMBERS.                                       DPKCS043
00058          10  PC-TSYMSG-00008            PIC 9(5)    VALUE 00008.  DPKCS043
00059          10  PC-TSYMSG-00050            PIC 9(5)    VALUE 00050.  DPKCS043
00060          10  PC-TSYMSG-00069            PIC 9(5)    VALUE 00069.  DPKCS043
00061          10  PC-TSYMSG-00070            PIC 9(5)    VALUE 00070.  DPKCS043
00062          10  PC-TSYMSG-00101            PIC 9(5)    VALUE 00101.  DPKCS043
00063          10  PC-TSYMSG-00148            PIC 9(5)    VALUE 00148.  DPKCS043
00064          10  PC-TSYMSG-00279            PIC 9(5)    VALUE 00279.  DPKCS043
00065          10  PC-TSYMSG-00302            PIC 9(5)    VALUE 00302.  DPKCS043
00066                                                                   DPKCS043
00067  01  PS-PROGRAM-SUBSCRIPTS.                                       DPKCS043
00068      05  PS-SUB                         PIC S9(04)   VALUE +0     DPKCS043
00069                                                      COMP SYNC.   DPKCS043
00070                                                                   DPKCS043
00071  01  PS-PROGRAM-SWITCHES.                                         DPKCS043
00072      05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.   DPKCS043
00073          88  PS-NO-ERROR                             VALUE 'Y'.   DPKCS043
00074          88  PS-ERROR                                VALUE 'N'.   DPKCS043
00075      05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.   DPKCS043
00076          88  PS-COMMAREA-VALID                       VALUE 'Y'.   DPKCS043
00077          88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.   DPKCS043
00078      05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.   DPKCS043
00079          88  PS-LIST-END                             VALUE 'Y'.   DPKCS043
00080          88  PS-NO-LIST-END                          VALUE 'N'.   DPKCS043
00081      EJECT                                                        DPKCS043
00082  01  PV-PROGRAM-VARIABLES.                                        DPKCS043
00083      05  PV-REMAINDER                   PIC S9(04)   VALUE +0     DPKCS043
00084                                                      COMP SYNC.   DPKCS043
00085      05  PV-READ-COUNT                  PIC S9(09)   VALUE +0     DPKCS043
00086                                                      COMP-3.      DPKCS043
00087      05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0     DPKCS043
00088                                                      COMP-3.      DPKCS043
00089      05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0     DPKCS043
00090                                                      COMP-3.      DPKCS043
00091      05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0     DPKCS043
00092                                                      COMP SYNC.   DPKCS043
00093      05  PV-CICS-RESP                   PIC S9(04)   VALUE +0     DPKCS043
00094                                                      COMP SYNC.   DPKCS043
00095      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0     DPKCS043
00096                                                      COMP SYNC.   DPKCS043
00097      05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0     DPKCS043
00098                                                      COMP SYNC.   DPKCS043
00099      05  PV-KEYLENGTH                   PIC S9(04)   VALUE +0     DPKCS043
00100                                                      COMP SYNC.   DPKCS043
00101      05  PV-LENGTH                      PIC S9(04)   VALUE +0     DPKCS043
00102                                                      COMP SYNC.   DPKCS043
00103      05  PV-RIDFLD                      PIC  X(32)  VALUE SPACE.  DPKCS043
00104                                                                   DPKCS043
00105      05  PV-CONVERT-OBJECT-ID.                                    DPKCS043
00106          10  PV-OBJECT-ID-BYTE          OCCURS 30 TIMES           DPKCS043
00107                                         INDEXED BY PV-ID-INDEX    DPKCS043
00108                                         PIC X(01).                DPKCS043
00109                                                                   DPKCS043
00110  01  PV-DB2-CRITERIA-AREA.                                        DPKCS043
00111      15  PV-DB2-MESSAGE-NUMBER          PIC S9(05)  VALUE ZERO    DPKCS043
00112                                                     COMP-3.       DPKCS043
00113                                                                   DPKCS043
00114  EJECT                                                            DPKCS043
00115 *----------------------------------------------------------------*DPKCS043
00116 *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *DPKCS043
00117 *----------------------------------------------------------------*DPKCS043
00118      COPY DPWS006.                                                DPKCS043
00119                                                                   DPKCS043
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
00125                                                                   DPKCS043
00126 *---------------------------------------------------------------* DPKCS043
00127 *    STANDARD COMMAREA.                                         * DPKCS043
00128 *---------------------------------------------------------------* DPKCS043
00129                                                                   DPKCS043
00130      COPY DPWS020.                                                DPKCS043
00131      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                DPKCS043
00132                                                                   DPKCS043
00133 *---------------------------------------------------------------* DPKCS043
00134 *    INTER-APPLICATION COMMAREA.                                * DPKCS043
00135 *---------------------------------------------------------------* DPKCS043
00136                                                                   DPKCS043
00137          10  INTER-APPL-COMM-AREA       PIC X(200).               DPKCS043
00138 *---------------------------------------------------------------* DPKCS043
00139 *    SPECIFIC COMMMAREA FOR DPKCS043.                           * DPKCS043
00140 *---------------------------------------------------------------* DPKCS043
00141          10  ASC-SPECIFIC-COMMAREA.                               DPKCS043
00142              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                DPKCS043
00143                  20  ASC-ITEM-ARRAY.                              DPKCS043
00144                                                                   DPKCS043
00145                      25  ASC-ITEM-ENTRIES OCCURS 15 TIMES.        DPKCS043
00146                          30  ASC-LIST-ITEM-NUMBER    PIC S9(09)   DPKCS043
00147                                                      COMP-3.      DPKCS043
00148                          30  ASC-TYPE                PIC X(02).   DPKCS043
00149                          30  ASC-OBJECT-ID           PIC X(30).   DPKCS043
00150                          30  ASC-USERID              PIC X(08).   DPKCS043
00151                          30  ASC-MNEMONIC            PIC X(08).   DPKCS043
00152                          30  ASC-TIME                PIC X(19).   DPKCS043
00153                                                                   DPKCS043
00154              15  ASC-BLK-SUB                         PIC S9(04)   DPKCS043
00155                                                          COMP     DPKCS043
00156                                                          SYNC.    DPKCS043
00157              15  ASC-ITEM-SUB                        PIC S9(04)   DPKCS043
00158                                                          COMP     DPKCS043
00159                                                          SYNC.    DPKCS043
00160              15  ASC-SEL-SUB                         PIC S9(04)   DPKCS043
00161                                                          COMP.    DPKCS043
00162              15  ASC-SEL-ITEM                        PIC S9(04)   DPKCS043
00163                                                          COMP.    DPKCS043
00164              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   DPKCS043
00165                                                          COMP-3.  DPKCS043
00166              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   DPKCS043
00167                                                          COMP-3.  DPKCS043
00168              15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)   DPKCS043
00169                                                          COMP-3.  DPKCS043
00170              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   DPKCS043
00171                                                          COMP-3.  DPKCS043
00172              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   DPKCS043
00173                                                          COMP-3.  DPKCS043
00174              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   DPKCS043
00175                                                          COMP-3.  DPKCS043
00176              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   DPKCS043
00177                                                          COMP     DPKCS043
00178                                                          SYNC.    DPKCS043
00179              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   DPKCS043
00180                                                          COMP-3.  DPKCS043
00181              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   DPKCS043
00182                                                          COMP-3.  DPKCS043
00183              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  DPKCS043
00184                  88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'. DPKCS043
00185                  88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'. DPKCS043
00186              15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).  DPKCS043
00187                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. DPKCS043
00188              15  ASC-DESC-LINE-IND                   PIC  X(01).  DPKCS043
00189                  88  ASC-DESC-LINE-DONE                VALUE 'Y'. DPKCS043
00190              15  ASC-CORR-LINE-IND                   PIC  X(01).  DPKCS043
00191                  88  ASC-CORR-LINE-DONE                VALUE 'Y'. DPKCS043
00192              15  ASC-LIST-QUEUE-NAME.                             DPKCS043
00193                  20  ASC-LIST-TERM-ID                PIC  X(04).  DPKCS043
00194                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   DPKCS043
00195                                                          COMP-3.  DPKCS043
00196                  20  ASC-LIST-SEQ                    PIC  9(01).  DPKCS043
00197              15  ASC-SEL-QUEUE-NAME.                              DPKCS043
00198                  20  ASC-SEL-TERM-ID                 PIC  X(04).  DPKCS043
00199                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   DPKCS043
00200                                                           COMP-3. DPKCS043
00201                  20  ASC-SEL-SEQ                     PIC  9(01).  DPKCS043
00202              15  ASC-DTL-QUEUE-NAME.                              DPKCS043
00203                  20  ASC-DTL-TERM-ID                 PIC  X(04).  DPKCS043
00204                  20  ASC-DTL-TASK-NUMBER             PIC S9(05)   DPKCS043
00205                                                          COMP-3.  DPKCS043
00206                  20  ASC-DTL-SEQ                     PIC  9(01).  DPKCS043
00207              15  ASC-START-ITEM-REQUEST.                          DPKCS043
00208                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  DPKCS043
00209              15  ASC-NAVIGATOR-SYSID                 PIC  X(04).  DPKCS043
00210                                                                   DPKCS043
00211      EJECT                                                        DPKCS043
00212 *---------------------------------------------------------------* DPKCS043
00213 *    ABEND PROCESSING COPYBOOK.                                 * DPKCS043
00214 *---------------------------------------------------------------* DPKCS043
00215                                                                   DPKCS043
00216      COPY DPWS013.                                                DPKCS043
00217                                                                   DPKCS043
00218      EJECT                                                        DPKCS043
00219 *---------------------------------------------------------------* DPKCS043
00220 *    ATTRIBUTE SETTINGS COPYBOOK.                               * DPKCS043
00221 *---------------------------------------------------------------* DPKCS043
00222                                                                   DPKCS043
00223      COPY DPWS015.                                                DPKCS043
00224                                                                   DPKCS043
00225 *---------------------------------------------------------------* DPKCS043
00226 *    FUNCTION KEYS COPYBOOK.                                    * DPKCS043
00227 *---------------------------------------------------------------* DPKCS043
00228                                                                   DPKCS043
00229      COPY DPWS016.                                                DPKCS043
00230                                                                   DPKCS043
00231 *---------------------------------------------------------------* DPKCS043
00232 *    NUMERIC EDIT LAYOUT.                                       * DPKCS043
00233 *---------------------------------------------------------------* DPKCS043
00234                                                                   DPKCS043
00235      COPY DPWS010I.                                               DPKCS043
00236                                                                   DPKCS043
00237 *---------------------------------------------------------------* DPKCS043
00238 *    MAP LAYOUT                                                 * DPKCS043
00239 *---------------------------------------------------------------* DPKCS043
00240                                                                   DPKCS043
00241      COPY DPKM043.                                                DPKCS043
00242                                                                   DPKCS043
00243  01  MR-OCCURS-LAYOUT          REDEFINES  DP043AO.                DPKCS043
00244      05  FILLER                PIC X(42).                         DPKCS043
00245      05  MR-SCROLL-AREA OCCURS 15 TIMES.                          DPKCS043
00246          10  MR-TYPEL          PIC S9(04) COMP.                   DPKCS043
00247          10  MR-TYPEA          PIC  X(01).                        DPKCS043
00248          10  MR-TYPEC          PIC  X(01).                        DPKCS043
00249          10  MR-TYPEH          PIC  X(01).                        DPKCS043
00250          10  MR-TYPE           PIC  X(02).                        DPKCS043
00251          10  MR-OBIDL          PIC S9(04) COMP.                   DPKCS043
00252          10  MR-OBIDA          PIC  X(01).                        DPKCS043
00253          10  MR-OBIDC          PIC  X(01).                        DPKCS043
00254          10  MR-OBIDH          PIC  X(01).                        DPKCS043
00255          10  MR-OBJECT-ID      PIC  X(30).                        DPKCS043
00256          10  MR-USERL          PIC S9(04) COMP.                   DPKCS043
00257          10  MR-USERA          PIC  X(01).                        DPKCS043
00258          10  MR-USERC          PIC  X(01).                        DPKCS043
00259          10  MR-USERH          PIC  X(01).                        DPKCS043
00260          10  MR-USERID         PIC  X(08).                        DPKCS043
00261          10  MR-MNEML          PIC S9(04) COMP.                   DPKCS043
00262          10  MR-MNEMA          PIC  X(01).                        DPKCS043
00263          10  MR-MNEMC          PIC  X(01).                        DPKCS043
00264          10  MR-MNEMH          PIC  X(01).                        DPKCS043
00265          10  MR-MNEMONIC       PIC  X(08).                        DPKCS043
00266          10  MR-TIMEL          PIC S9(04) COMP.                   DPKCS043
00267          10  MR-TIMEA          PIC  X(01).                        DPKCS043
00268          10  MR-TIMEC          PIC  X(01).                        DPKCS043
00269          10  MR-TIMEH          PIC  X(01).                        DPKCS043
00270          10  MR-TIME           PIC  X(19).                        DPKCS043
00271                                                                   DPKCS043
00272  EJECT                                                            DPKCS043
00273 *-------------------------------------------------------*         DPKCS043
00274 * THE RECORD LAYOUT FOR THE OBJECT LOCKING VSAM FILE    *         DPKCS043
00275 *-------------------------------------------------------*         DPKCS043
00276      COPY DPRD509.                                                DPKCS043
00277  EJECT                                                            DPKCS043
00278  LINKAGE SECTION.                                                 DPKCS043
00279                                                                   DPKCS043
00280  01  DFHCOMMAREA.                                                 DPKCS043
00281      05  FILLER                         OCCURS  1 TO 4072 TIMES   DPKCS043
00282                                         DEPENDING ON EIBCALEN.    DPKCS043
00283          10  FILLER                     PIC X.                    DPKCS043
00284                                                                   DPKCS043
00285  EJECT                                                            DPKCS043
00286  PROCEDURE DIVISION.                                              DPKCS043
00287                                                                   DPKCS043
00288  0000-MAIN-MODULE.                                                DPKCS043
00289                                                                   DPKCS043
00290      INITIALIZE DP030-CICS-API-FIELDS.                            DPKCS043
00291      MOVE +1                      TO DP030-NUMBER-OF-MAPS.        DPKCS043
00292      MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.           DPKCS043
00293      MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).          DPKCS043
00294      SET DP030-RECEIVE-APPL-MAP   TO TRUE.                        DPKCS043
00295      MOVE LENGTH OF DP043AI       TO DP030-MAP-LENGTH (1).        DPKCS043
00296      MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).      DPKCS043
00297      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS043
00298                                                                   DPKCS043
00299      PERFORM 1000-CONTROL-PROCESSING.                             DPKCS043
00300                                                                   DPKCS043
00301      MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).      DPKCS043
00302      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS043
00303                                                                   DPKCS043
00304 *----------------------------------------------------------------*DPKCS043
00305 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *DPKCS043
00306 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *DPKCS043
00307 *----------------------------------------------------------------*DPKCS043
00308                                                                   DPKCS043
00309  0001-CALL-CICS-ARCH-API.                                         DPKCS043
00310                                                                   DPKCS043
00311      CALL DP930-CICS-ARCH-API USING DFHEIBLK                      DPKCS043
00312                                     DFHCOMMAREA                   DPKCS043
00313                                     DP030-CICS-API-FIELDS         DPKCS043
00314                                     DP020-STANDARD-COMMAREA       DPKCS043
00315                                     DP043AI.                      DPKCS043
00316                                                                   DPKCS043
00317      IF  DP030-RC-CALL-SUCCESSFUL                                 DPKCS043
00318          CONTINUE                                                 DPKCS043
00319      ELSE                                                         DPKCS043
00320          SET DP013-NO-ROLLBACK TO TRUE                            DPKCS043
00321          SET DP013-XCTL-DISPLAY-RESTART TO TRUE                   DPKCS043
00322          SET DP013-CICS-ABEND TO TRUE                             DPKCS043
00323          MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH        DPKCS043
00324          MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
00325 -             'N NEXT LINE'    TO DP013-MESSAGE-TEXT (2)                  
00326          MOVE DP030-RETURN-CODE                                   DPKCS043
00327                                TO DP013-MESSAGE-TEXT (3)          DPKCS043
00328          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
00329      END-IF.                                                      DPKCS043
00330                                                                   DPKCS043
00331 *----------------------------------------------------------------*DPKCS043
00332 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *DPKCS043
00333 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *DPKCS043
00334 *                                                                *DPKCS043
00335 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *DPKCS043
00336 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *DPKCS043
00337 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *DPKCS043
00338 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *DPKCS043
00339 *----------------------------------------------------------------*DPKCS043
00340                                                                   DPKCS043
00341  1000-CONTROL-PROCESSING.                                         DPKCS043
00342                                                                   DPKCS043
00343      EVALUATE TRUE                                                DPKCS043
00344          WHEN DP020-NEXT-ACT-INITIAL                              DPKCS043
00345              INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS043
00346              MOVE EIBTRMID          TO ASC-DTL-TERM-ID            DPKCS043
00347              MOVE DP020-SRC-TASK-NUMBER                           DPKCS043
00348                                     TO ASC-DTL-TASK-NUMBER        DPKCS043
00349              MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                DPKCS043
00350              PERFORM 1200-GET-SYSID                               DPKCS043
00351              PERFORM 1100-PROCESS-INTER-APPL-COMM                 DPKCS043
00352              IF  PS-COMMAREA-NOT-VALID                            DPKCS043
00353                  SET DP020-NEXT-ACT-APPL-ERROR                    DPKCS043
00354                                TO  TRUE                           DPKCS043
00355              ELSE                                                 DPKCS043
00356                  PERFORM 2050-DELETE-DETAIL-QUEUE                 DPKCS043
00357                  PERFORM 4000-BUILD-INITIAL-PANEL                 DPKCS043
00358              END-IF                                               DPKCS043
00359                                                                   DPKCS043
00360          WHEN DP020-NEXT-ACT-READ-MAP                             DPKCS043
00361              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        DPKCS043
00362                                TO PV-TOP-ITEM                     DPKCS043
00363              PERFORM 2000-PROCESS-PANEL                           DPKCS043
00364                                                                   DPKCS043
00365          WHEN DP020-NEXT-ACT-RETURN                               DPKCS043
00366              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        DPKCS043
00367                                TO PV-TOP-ITEM                     DPKCS043
00368              MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL        DPKCS043
00369              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS043
00370                                                                   DPKCS043
00371          WHEN OTHER                                               DPKCS043
00372              SET DP013-LOGIC-ABEND TO TRUE                        DPKCS043
00373              SET DP013-NO-ROLLBACK TO TRUE                        DPKCS043
00374              MOVE '1000-CONTROL-PROCESSING'                       DPKCS043
00375                                TO  DP013-PARAGRAPH                DPKCS043
00376              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    DPKCS043
00377                                TO  DP013-MESSAGE-TEXT(1)          DPKCS043
00378              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS043
00379      END-EVALUATE.                                                DPKCS043
00380                                                                   DPKCS043
00381  EJECT                                                            DPKCS043
00382 *----------------------------------------------------------------*DPKCS043
00383 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *DPKCS043
00384 * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *DPKCS043
00385 *----------------------------------------------------------------*DPKCS043
00386                                                                   DPKCS043
00387  1100-PROCESS-INTER-APPL-COMM.                                    DPKCS043
00388                                                                   DPKCS043
00389      SET PS-COMMAREA-VALID     TO TRUE.                           DPKCS043
00390      MOVE SPACE                TO ASC-START-ITEM-REQUEST.         DPKCS043
00391                                                                   DPKCS043
00392                                                                   DPKCS043
00393 *----------------------------------------------------------------*DPKCS043
00394 * GET THE SYSID OF THE NAVIGATOR.  THIS IS USED FOR READING THE  *DPKCS043
00395 * OBJECT LOCKING VSAM FILES WHICH IS OWNED BY THE SAME REGION    *DPKCS043
00396 * THAT THE NAVIGATOR IS OWNED BY.                                *DPKCS043
00397 *----------------------------------------------------------------*DPKCS043
00398  1200-GET-SYSID.                                                  DPKCS043
00399      EXEC CICS                                                    DPKCS043
00400          INQUIRE                                                  DPKCS043
00401              TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                  DPKCS043
00402              REMOTESYSTEM (ASC-NAVIGATOR-SYSID)                   DPKCS043
00403      END-EXEC.                                                    DPKCS043
00404 *----------------------------------------------------------------*DPKCS043
00405 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *DPKCS043
00406 *    ON WHAT FUNCTION KEY WAS SELECTED.                          *DPKCS043
00407 *----------------------------------------------------------------*DPKCS043
00408                                                                   DPKCS043
00409  2000-PROCESS-PANEL.                                              DPKCS043
00410                                                                   DPKCS043
00411      EVALUATE TRUE                                                DPKCS043
00412          WHEN DP020-SRC-AID = DP016-CLEAR                         DPKCS043
00413              MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                DPKCS043
00414              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS043
00415              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS043
00416                                                                   DPKCS043
00417          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    DPKCS043
00418              PERFORM 2050-DELETE-DETAIL-QUEUE                     DPKCS043
00419              PERFORM 4000-BUILD-INITIAL-PANEL                     DPKCS043
00420              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS043
00421                                                                   DPKCS043
00422          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  DPKCS043
00423              MOVE +1 TO PV-TOP-ITEM                               DPKCS043
00424              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS043
00425              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS043
00426                                                                   DPKCS043
00427          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   DPKCS043
00428              PERFORM 2110-BROWSE-BACKWARD                         DPKCS043
00429              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS043
00430              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS043
00431                                                                   DPKCS043
00432          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   DPKCS043
00433              PERFORM 2120-BROWSE-FORWARD                          DPKCS043
00434              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS043
00435              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS043
00436                                                                   DPKCS043
00437          WHEN DP020-SRC-AID = DP016-ENTER                         DPKCS043
00438              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   DPKCS043
00439              IF  PS-ERROR                                         DPKCS043
00440                  CONTINUE                                         DPKCS043
00441              ELSE                                                 DPKCS043
00442                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             DPKCS043
00443                  PERFORM 3200-RESET-ATTRIBUTES                    DPKCS043
00444              END-IF                                               DPKCS043
00445                                                                   DPKCS043
00446          WHEN OTHER                                               DPKCS043
00447              SET DP013-NO-ROLLBACK TO TRUE                        DPKCS043
00448              SET DP013-XCTL-DISPLAY-RESTART TO TRUE               DPKCS043
00449              SET DP013-CICS-ABEND TO TRUE                         DPKCS043
00450              MOVE '2100-CHECK-FUNCTION-KEY' TO DP013-PARAGRAPH    DPKCS043
00451              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      DPKCS043
00452                                    TO DP013-MESSAGE-TEXT (1)      DPKCS043
00453              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS043
00454                                                                   DPKCS043
00455      END-EVALUATE.                                                DPKCS043
00456  EJECT                                                            DPKCS043
00457 *----------------------------------------------------------------*DPKCS043
00458 *    DELETE THE TEMPORARY STORAGE QUEUE.                         *DPKCS043
00459 *----------------------------------------------------------------*DPKCS043
00460                                                                   DPKCS043
00461  2050-DELETE-DETAIL-QUEUE.                                        DPKCS043
00462                                                                   DPKCS043
00463      EXEC CICS                                                    DPKCS043
00464          DELETEQ TS                                               DPKCS043
00465              QUEUE (ASC-DTL-QUEUE-NAME)                           DPKCS043
00466              RESP  (PV-CICS-RESP)                                 DPKCS043
00467      END-EXEC.                                                    DPKCS043
00468                                                                   DPKCS043
00469      IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                       DPKCS043
00470         (PV-CICS-RESP = DFHRESP(QIDERR))                          DPKCS043
00471          CONTINUE                                                 DPKCS043
00472      ELSE                                                         DPKCS043
00473          SET DP013-CICS-ABEND TO TRUE                             DPKCS043
00474          SET DP013-NO-ROLLBACK TO TRUE                            DPKCS043
00475                                                                   DPKCS043
00476          MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH       DPKCS043
00477          MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'  DPKCS043
00478                         TO DP013-MESSAGE-TEXT (1)                 DPKCS043
00479          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
00480      END-IF.                                                      DPKCS043
00481                                                                   DPKCS043
00482      MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                        DPKCS043
00483  EJECT                                                            DPKCS043
00484 *----------------------------------------------------------------*DPKCS043
00485 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *DPKCS043
00486 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *DPKCS043
00487 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *DPKCS043
00488 *    MESSAGE.                                                    *DPKCS043
00489 *----------------------------------------------------------------*DPKCS043
00490                                                                   DPKCS043
00491  2110-BROWSE-BACKWARD.                                            DPKCS043
00492                                                                   DPKCS043
00493      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    DPKCS043
00494          GIVING PV-TOP-ITEM.                                      DPKCS043
00495                                                                   DPKCS043
00496      IF  PV-TOP-ITEM < +1                                         DPKCS043
00497          MOVE +1              TO PV-TOP-ITEM                      DPKCS043
00498 *        -- TOP OF LIST --                                        DPKCS043
00499          MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                 DPKCS043
00500          SET DP020-MSG-INFORMATIONAL TO TRUE                      DPKCS043
00501      END-IF.                                                      DPKCS043
00502                                                                   DPKCS043
00503  EJECT                                                            DPKCS043
00504                                                                   DPKCS043
00505 *----------------------------------------------------------------*DPKCS043
00506 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *DPKCS043
00507 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *DPKCS043
00508 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *DPKCS043
00509 *----------------------------------------------------------------*DPKCS043
00510                                                                   DPKCS043
00511  2120-BROWSE-FORWARD.                                             DPKCS043
00512                                                                   DPKCS043
00513      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              DPKCS043
00514          GIVING PV-TOP-ITEM.                                      DPKCS043
00515      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   DPKCS043
00516          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            DPKCS043
00517                                    TO PV-TOP-ITEM                 DPKCS043
00518 *        -- BOTTOM OF LIST --                                     DPKCS043
00519          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            DPKCS043
00520          SET DP020-MSG-INFORMATIONAL                              DPKCS043
00521                                    TO TRUE                        DPKCS043
00522      ELSE                                                         DPKCS043
00523          COMPUTE PV-BOTTOM-ITEM =                                 DPKCS043
00524              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 DPKCS043
00525          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            DPKCS043
00526 *            -- BOTTOM OF LIST --                                 DPKCS043
00527              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            DPKCS043
00528              SET DP020-MSG-INFORMATIONAL                          DPKCS043
00529                                    TO TRUE                        DPKCS043
00530          END-IF                                                   DPKCS043
00531      END-IF.                                                      DPKCS043
00532  EJECT                                                            DPKCS043
00533 *----------------------------------------------------------------*DPKCS043
00534 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *DPKCS043
00535 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *DPKCS043
00536 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *DPKCS043
00537 *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *DPKCS043
00538 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *DPKCS043
00539 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *DPKCS043
00540 *----------------------------------------------------------------*DPKCS043
00541                                                                   DPKCS043
00542  2130-CHECK-FOR-JUMP-BROWSE.                                      DPKCS043
00543                                                                   DPKCS043
00544      IF ASTRTITL > ZERO                                           DPKCS043
00545          MOVE ASTRTITI             TO ASC-START-ITEM-REQUEST      DPKCS043
00546      ELSE                                                         DPKCS043
00547          IF ASTRTITF = DP015-ERASE-EOF                            DPKCS043
00548              INITIALIZE ASC-START-ITEM-REQUEST                    DPKCS043
00549          END-IF                                                   DPKCS043
00550      END-IF.                                                      DPKCS043
00551                                                                   DPKCS043
00552      SET PS-NO-ERROR               TO TRUE.                       DPKCS043
00553      IF (ASC-START-ITEM-REQUEST = SPACE)                          DPKCS043
00554          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            DPKCS043
00555                                    TO PV-TOP-ITEM                 DPKCS043
00556      ELSE                                                         DPKCS043
00557          MOVE ASC-START-ITEM-REQUEST                              DPKCS043
00558                                    TO DP010I-UNEDITED-FIELD       DPKCS043
00559          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       DPKCS043
00560          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     DPKCS043
00561          SET DP010I-NEGATIVE-NOT-ALLOWED                          DPKCS043
00562                                    TO TRUE                        DPKCS043
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
00564                                                                   DPKCS043
00565          IF  NOT DP010I-ERROR-DETECTED                            DPKCS043
00566              MOVE DP010I-NUMERIC-FIELD                            DPKCS043
00567                                    TO  ASC-START-ITEM-REQUEST-N   DPKCS043
00568              IF  ASC-START-ITEM-REQUEST-N > ZERO                  DPKCS043
00569                  IF  ASC-START-ITEM-REQUEST-N >                   DPKCS043
00570                                           ASC-NUMBER-OF-ITEMS-READDPKCS043
00571                      MOVE SPACE TO ASC-START-ITEM-REQUEST         DPKCS043
00572                      COMPUTE PV-TOP-ITEM =                        DPKCS043
00573                              ASC-NUMBER-OF-ITEMS-READ             DPKCS043
00574                              - PC-ITEMS-PER-PANEL + 1             DPKCS043
00575                      IF  PV-TOP-ITEM < +1                         DPKCS043
00576                          MOVE +1   TO PV-TOP-ITEM                 DPKCS043
00577                          MOVE PC-TSYMSG-00070                     DPKCS043
00578                                    TO DP020-MSG-NUMBER            DPKCS043
00579                          SET DP020-MSG-INFORMATIONAL              DPKCS043
00580                                    TO TRUE                        DPKCS043
00581                      END-IF                                       DPKCS043
00582                  ELSE                                             DPKCS043
00583                      MOVE ASC-START-ITEM-REQUEST-N                DPKCS043
00584                                    TO PV-TOP-ITEM                 DPKCS043
00585                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      DPKCS043
00586                      COMPUTE PV-BOTTOM-ITEM =                     DPKCS043
00587                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     DPKCS043
00588                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READDPKCS043
00589 *                        -- BOTTOM OF LIST --                     DPKCS043
00590                          MOVE PC-TSYMSG-00070                     DPKCS043
00591                                    TO DP020-MSG-NUMBER            DPKCS043
00592                          SET DP020-MSG-INFORMATIONAL              DPKCS043
00593                                    TO TRUE                        DPKCS043
00594                      END-IF                                       DPKCS043
00595                  END-IF                                           DPKCS043
00596              ELSE                                                 DPKCS043
00597 *                --- VALUE OUT OF RANGE ----                      DPKCS043
00598                  MOVE PC-TSYMSG-00101                             DPKCS043
00599                                    TO DP020-MSG-NUMBER            DPKCS043
00600                  MOVE -1           TO ASTRTITL                    DPKCS043
00601                  SET DP030-SET-CURSOR-APPL-1                      DPKCS043
00602                                    TO TRUE                        DPKCS043
00603                  MOVE DP015-REVERSE                               DPKCS043
00604                                    TO ASTRTITH                    DPKCS043
00605                  MOVE DP015-RED    TO ASTRTITC                    DPKCS043
00606                  MOVE DP015-UNP-NUM-BRT-MDT                       DPKCS043
00607                                    TO ASTRTITA                    DPKCS043
00608                  SET DP020-MSG-FATAL                              DPKCS043
00609                                    TO TRUE                        DPKCS043
00610                  SET PS-ERROR      TO TRUE                        DPKCS043
00611              END-IF                                               DPKCS043
00612                                                                   DPKCS043
00613          ELSE                                                     DPKCS043
00614              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            DPKCS043
00615              MOVE -1               TO ASTRTITL                    DPKCS043
00616              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  DPKCS043
00617              MOVE DP015-REVERSE    TO ASTRTITH                    DPKCS043
00618              MOVE DP015-RED        TO ASTRTITC                    DPKCS043
00619              MOVE DP015-UNP-NUM-BRT-MDT                           DPKCS043
00620                                    TO ASTRTITA                    DPKCS043
00621              SET DP020-MSG-FATAL   TO TRUE                        DPKCS043
00622              SET PS-ERROR          TO TRUE                        DPKCS043
00623          END-IF                                                   DPKCS043
00624      END-IF.                                                      DPKCS043
00625  EJECT                                                            DPKCS043
00626                                                                   DPKCS043
00627 *----------------------------------------------------------------*DPKCS043
00628 *    RESET ATTRIBUTES.                                           *DPKCS043
00629 *----------------------------------------------------------------*DPKCS043
00630                                                                   DPKCS043
00631  3200-RESET-ATTRIBUTES.                                           DPKCS043
00632                                                                   DPKCS043
00633      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   DPKCS043
00634      MOVE DP015-GREEN              TO ASTRTITC.                   DPKCS043
00635      MOVE DP015-UNDERLINE          TO ASTRTITH.                   DPKCS043
00636      MOVE -1                       TO ASTRTITL.                   DPKCS043
00637      SET DP030-SET-CURSOR-APPL-1   TO TRUE.                       DPKCS043
00638                                                                   DPKCS043
00639  EJECT                                                            DPKCS043
00640                                                                   DPKCS043
00641 *----------------------------------------------------------------*DPKCS043
00642 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *DPKCS043
00643 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *DPKCS043
00644 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *DPKCS043
00645 *    DISPLAY THE PANEL TO USER.                                  *DPKCS043
00646 *----------------------------------------------------------------*DPKCS043
00647                                                                   DPKCS043
00648  4000-BUILD-INITIAL-PANEL.                                        DPKCS043
00649                                                                   DPKCS043
00650      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            DPKCS043
00651                                  ASC-BLOCK-ENTRIES (2).           DPKCS043
00652      MOVE +1                 TO  ASC-BLK-SUB.                     DPKCS043
00653      SET ASC-ITEMS-LEFT-ON-DB                                     DPKCS043
00654                              TO  TRUE.                            DPKCS043
00655                                                                   DPKCS043
00656      MOVE +0                 TO  ASC-ITEM-SUB                     DPKCS043
00657                                  ASC-NUMBER-OF-ITEMS-READ         DPKCS043
00658                                  ASC-NUMBER-OF-SELECTED-ITEMS     DPKCS043
00659                                  ASC-HIGHEST-BLOCK-WRITTEN        DPKCS043
00660                                  ASC-START-OF-SELECTED-RANGE      DPKCS043
00661                                  ASC-END-OF-SELECTED-RANGE        DPKCS043
00662                                  ASC-ITEM-AT-TOP-OF-PANEL.        DPKCS043
00663                                                                   DPKCS043
00664      MOVE SPACE              TO  ASC-DESC-LINE-IND                DPKCS043
00665                                  ASC-CORR-LINE-IND.               DPKCS043
00666                                                                   DPKCS043
00667      PERFORM 4010-START-BROWSE.                                   DPKCS043
00668                                                                   DPKCS043
00669      IF  PV-CICS-RESP = DFHRESP (NOTFND)                          DPKCS043
00670          CONTINUE                                                 DPKCS043
00671      ELSE                                                         DPKCS043
00672          PERFORM 4300-GET-OBJECT-LOCK-ITEMS                       DPKCS043
00673          PERFORM 4020-END-BROWSE                                  DPKCS043
00674      END-IF.                                                      DPKCS043
00675                                                                   DPKCS043
00676      MOVE +1                 TO  PV-TOP-ITEM.                     DPKCS043
00677      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          DPKCS043
00678          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    DPKCS043
00679          SET DP020-MSG-FATAL   TO  TRUE                           DPKCS043
00680          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               DPKCS043
00681      ELSE                                                         DPKCS043
00682          MOVE -1             TO ASTRTITL                          DPKCS043
00683          SET DP030-SET-CURSOR-APPL-1 TO TRUE                      DPKCS043
00684          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     DPKCS043
00685      END-IF.                                                      DPKCS043
00686  EJECT                                                            DPKCS043
00687 *----------------------------------------------------------------*DPKCS043
00688 * OPEN THE CURSOR.                                               *DPKCS043
00689 *----------------------------------------------------------------*DPKCS043
00690                                                                   DPKCS043
00691  4010-START-BROWSE.                                               DPKCS043
00692                                                                   DPKCS043
00693      MOVE LENGTH OF PV-RIDFLD TO PV-KEYLENGTH.                    DPKCS043
00694      IF  ASC-NAVIGATOR-SYSID = SPACE                              DPKCS043
00695          EXEC CICS                                                DPKCS043
00696              STARTBR                                              DPKCS043
00697                  FILE      (PC-OBJLOK-BASE-DD)                    DPKCS043
00698                  RIDFLD    (PV-RIDFLD)                            DPKCS043
00699                  KEYLENGTH (PV-KEYLENGTH)                         DPKCS043
00700                  GTEQ                                             DPKCS043
00701                  RESP (PV-CICS-RESP)                              DPKCS043
00702          END-EXEC                                                 DPKCS043
00703      ELSE                                                         DPKCS043
00704          EXEC CICS                                                DPKCS043
00705              STARTBR                                              DPKCS043
00706                  FILE      (PC-OBJLOK-BASE-DD)                    DPKCS043
00707                  RIDFLD    (PV-RIDFLD)                            DPKCS043
00708                  KEYLENGTH (PV-KEYLENGTH)                         DPKCS043
00709                  GTEQ                                             DPKCS043
00710                  SYSID     (ASC-NAVIGATOR-SYSID)                  DPKCS043
00711                  RESP      (PV-CICS-RESP)                         DPKCS043
00712          END-EXEC                                                 DPKCS043
00713                                                                   DPKCS043
00714      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          DPKCS043
00715                      OR DFHRESP (NOTFND)                          DPKCS043
00716          CONTINUE                                                 DPKCS043
00717      ELSE                                                         DPKCS043
00718          MOVE '4010-START-BROWSE'                                 DPKCS043
00719                            TO  DP013-PARAGRAPH                    DPKCS043
00720          MOVE 'START A BROWSE OF ALL RECS ON THE OBJECT LOCK FILE'DPKCS043
00721                            TO  DP013-MESSAGE-TEXT (1)             DPKCS043
00722          MOVE ASC-NAVIGATOR-SYSID                                 DPKCS043
00723                            TO  DP013-MESSAGE-TEXT (2)             DPKCS043
00724          SET DP013-CICS-ABEND                                     DPKCS043
00725                            TO  TRUE                               DPKCS043
00726          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
00727      END-IF.                                                      DPKCS043
00728                                                                   DPKCS043
00729  EJECT                                                            DPKCS043
00730 *----------------------------------------------------------------*DPKCS043
00731 * CLOSE THE CURSOR.                                              *DPKCS043
00732 *----------------------------------------------------------------*DPKCS043
00733                                                                   DPKCS043
00734  4020-END-BROWSE.                                                 DPKCS043
00735      IF  ASC-NAVIGATOR-SYSID = SPACE                              DPKCS043
00736          EXEC CICS                                                DPKCS043
00737              ENDBR                                                DPKCS043
00738                  FILE (PC-OBJLOK-BASE-DD)                         DPKCS043
00739                  RESP (PV-CICS-RESP)                              DPKCS043
00740          END-EXEC                                                 DPKCS043
00741      ELSE                                                         DPKCS043
00742          EXEC CICS                                                DPKCS043
00743              ENDBR                                                DPKCS043
00744                  FILE  (PC-OBJLOK-BASE-DD)                        DPKCS043
00745                  SYSID (ASC-NAVIGATOR-SYSID)                      DPKCS043
00746                  RESP  (PV-CICS-RESP)                             DPKCS043
00747          END-EXEC                                                 DPKCS043
00748      END-IF.                                                      DPKCS043
00749                                                                   DPKCS043
00750      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          DPKCS043
00751          CONTINUE                                                 DPKCS043
00752      ELSE                                                         DPKCS043
00753          MOVE '4020-END-BROWSE'                                   DPKCS043
00754                            TO  DP013-PARAGRAPH                    DPKCS043
00755          MOVE 'END A BROWSE OF ALL RECS ON THE OBJECT LOCK FILE'  DPKCS043
00756                            TO  DP013-MESSAGE-TEXT (1)             DPKCS043
00757          MOVE ASC-NAVIGATOR-SYSID                                 DPKCS043
00758                            TO  DP013-MESSAGE-TEXT (2)             DPKCS043
00759          SET DP013-CICS-ABEND                                     DPKCS043
00760                            TO  TRUE                               DPKCS043
00761          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
00762      END-IF.                                                      DPKCS043
00763                                                                   DPKCS043
00764  EJECT                                                            DPKCS043
00765 *----------------------------------------------------------------*DPKCS043
00766 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *DPKCS043
00767 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *DPKCS043
00768 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *DPKCS043
00769 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *DPKCS043
00770 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *DPKCS043
00771 *                                                                *DPKCS043
00772 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *DPKCS043
00773 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *DPKCS043
00774 *    UPON INITIALIZATION.                                        *DPKCS043
00775 *----------------------------------------------------------------*DPKCS043
00776                                                                   DPKCS043
00777  4300-GET-OBJECT-LOCK-ITEMS.                                      DPKCS043
00778                                                                   DPKCS043
00779      PERFORM                                                      DPKCS043
00780          VARYING PV-READ-COUNT                                    DPKCS043
00781          FROM 1 BY 1                                              DPKCS043
00782          UNTIL PV-CICS-RESP = DFHRESP (ENDFILE)                   DPKCS043
00783                                                                   DPKCS043
00784          PERFORM 4310-READNEXT                                    DPKCS043
00785                                                                   DPKCS043
00786          IF  PV-CICS-RESP = DFHRESP (NORMAL)                      DPKCS043
00787              ADD +1 TO ASC-NUMBER-OF-ITEMS-READ                   DPKCS043
00788              PERFORM 4320-GET-SUB                                 DPKCS043
00789              MOVE DP509-TYP-COD      TO ASC-TYPE                  DPKCS043
00790                                      (ASC-BLK-SUB  ASC-ITEM-SUB)  DPKCS043
00791              PERFORM 4330-CONVERT-OBJECT-ID                       DPKCS043
00792              MOVE PV-CONVERT-OBJECT-ID                            DPKCS043
00793                                      TO ASC-OBJECT-ID             DPKCS043
00794                                      (ASC-BLK-SUB  ASC-ITEM-SUB)  DPKCS043
00795              MOVE DP509-USER-ID     TO ASC-USERID                 DPKCS043
00796                                      (ASC-BLK-SUB  ASC-ITEM-SUB)  DPKCS043
00797              MOVE DP509-OBJ-MNMNC-CDE                             DPKCS043
00798                                      TO ASC-MNEMONIC              DPKCS043
00799                                      (ASC-BLK-SUB  ASC-ITEM-SUB)  DPKCS043
00800              MOVE DP509-LOCK-TMST   TO ASC-TIME                   DPKCS043
00801                                      (ASC-BLK-SUB  ASC-ITEM-SUB)  DPKCS043
00802          END-IF                                                   DPKCS043
00803      END-PERFORM.                                                 DPKCS043
00804                                                                   DPKCS043
00805      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          DPKCS043
00806          PERFORM 4320-GET-SUB                                     DPKCS043
00807          MOVE SPACES               TO ASC-TYPE                    DPKCS043
00808                                    (ASC-BLK-SUB  ASC-ITEM-SUB)    DPKCS043
00809          MOVE                                                     DPKCS043
00810          '** NO LOCKS FOUND **'    TO ASC-OBJECT-ID               DPKCS043
00811                                    (ASC-BLK-SUB  ASC-ITEM-SUB)    DPKCS043
00812          MOVE SPACES               TO ASC-USERID                  DPKCS043
00813                                    (ASC-BLK-SUB  ASC-ITEM-SUB)    DPKCS043
00814          MOVE SPACES               TO ASC-MNEMONIC                DPKCS043
00815                                    (ASC-BLK-SUB  ASC-ITEM-SUB)    DPKCS043
00816          MOVE SPACES               TO ASC-TIME                    DPKCS043
00817                                    (ASC-BLK-SUB  ASC-ITEM-SUB)    DPKCS043
00818      END-IF.                                                      DPKCS043
00819                                                                   DPKCS043
00820      MOVE +1                       TO  ASC-BLK-SUB.               DPKCS043
00821      PERFORM 5100-WRITE-TEMP-STORAGE.                             DPKCS043
00822      ADD +1                        TO  ASC-BLK-SUB.               DPKCS043
00823      PERFORM 5100-WRITE-TEMP-STORAGE.                             DPKCS043
00824                                                                   DPKCS043
00825      MOVE 1                        TO  ASC-BLK-SUB                DPKCS043
00826                                        PV-BLOCK-NUMBER.           DPKCS043
00827      PERFORM 4410-READ-TEMP-STORAGE.                              DPKCS043
00828      IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                 DPKCS043
00829          MOVE 2                    TO  ASC-BLK-SUB                DPKCS043
00830                                        PV-BLOCK-NUMBER            DPKCS043
00831          PERFORM 4410-READ-TEMP-STORAGE                           DPKCS043
00832      END-IF.                                                      DPKCS043
00833  EJECT                                                            DPKCS043
00834 *----------------------------------------------------------------*DPKCS043
00835 *    FETCH THE TEXT CURSOR.                                      *DPKCS043
00836 *----------------------------------------------------------------*DPKCS043
00837                                                                   DPKCS043
00838  4310-READNEXT.                                                   DPKCS043
00839      MOVE LENGTH OF PV-RIDFLD TO PV-KEYLENGTH.                    DPKCS043
00840      MOVE LENGTH OF DP509-OBJECT-LOCKING-RCD                      DPKCS043
00841                               TO PV-LENGTH.                       DPKCS043
00842      IF  ASC-NAVIGATOR-SYSID = SPACE                              DPKCS043
00843          EXEC CICS                                                DPKCS043
00844              READNEXT                                             DPKCS043
00845                  FILE      (PC-OBJLOK-BASE-DD)                    DPKCS043
00846                  RIDFLD    (PV-RIDFLD)                            DPKCS043
00847                  KEYLENGTH (PV-KEYLENGTH)                         DPKCS043
00848                  INTO      (DP509-OBJECT-LOCKING-RCD)             DPKCS043
00849                  LENGTH    (PV-LENGTH)                            DPKCS043
00850                  RESP      (PV-CICS-RESP)                         DPKCS043
00851          END-EXEC                                                 DPKCS043
00852      ELSE                                                         DPKCS043
00853          EXEC CICS                                                DPKCS043
00854              READNEXT                                             DPKCS043
00855                  FILE      (PC-OBJLOK-BASE-DD)                    DPKCS043
00856                  RIDFLD    (PV-RIDFLD)                            DPKCS043
00857                  KEYLENGTH (PV-KEYLENGTH)                         DPKCS043
00858                  INTO      (DP509-OBJECT-LOCKING-RCD)             DPKCS043
00859                  LENGTH    (PV-LENGTH)                            DPKCS043
00860                  SYSID     (ASC-NAVIGATOR-SYSID)                  DPKCS043
00861                  RESP      (PV-CICS-RESP)                         DPKCS043
00862          END-EXEC                                                 DPKCS043
00863                                                                   DPKCS043
00864      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          DPKCS043
00865                      OR DFHRESP (ENDFILE)                         DPKCS043
00866          CONTINUE                                                 DPKCS043
00867      ELSE                                                         DPKCS043
00868          MOVE '4310-READNEXT'                                     DPKCS043
00869                            TO  DP013-PARAGRAPH                    DPKCS043
00870          MOVE 'ATTEMPTING TO READ THE NEXT RECORD ON THE'         DPKCS043
00871                            TO  DP013-MESSAGE-TEXT (1)             DPKCS043
00872          MOVE 'OBJECT LOCK VSAM FILE'                             DPKCS043
00873                            TO  DP013-MESSAGE-TEXT (2)             DPKCS043
00874          MOVE ASC-NAVIGATOR-SYSID                                 DPKCS043
00875                            TO  DP013-MESSAGE-TEXT (3)             DPKCS043
00876          SET DP013-CICS-ABEND                                     DPKCS043
00877                            TO  TRUE                               DPKCS043
00878          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
00879      END-IF.                                                      DPKCS043
00880                                                                   DPKCS043
00881  EJECT                                                            DPKCS043
00882 *----------------------------------------------------------------*DPKCS043
00883 *    GET THE CORRECT SUBSCRIPTS FOR PLACING THE LOCK LINE.       *DPKCS043
00884 *----------------------------------------------------------------*DPKCS043
00885                                                                   DPKCS043
00886  4320-GET-SUB.                                                    DPKCS043
00887                                                                   DPKCS043
00888      IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        DPKCS043
00889          ADD +1            TO  ASC-ITEM-SUB                       DPKCS043
00890      ELSE                                                         DPKCS043
00891          ADD +1            TO  ASC-BLK-SUB                        DPKCS043
00892          MOVE +1           TO  ASC-ITEM-SUB                       DPKCS043
00893                                                                   DPKCS043
00894          IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 DPKCS043
00895              MOVE +1       TO  ASC-BLK-SUB                        DPKCS043
00896              PERFORM 5100-WRITE-TEMP-STORAGE                      DPKCS043
00897              MOVE ASC-BLOCK-ENTRIES                               DPKCS043
00898                        (PC-MAX-BLOCKS-IN-ARRAY)                   DPKCS043
00899              TO   ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                  DPKCS043
00900              ADD +1        TO  ASC-BLK-SUB                        DPKCS043
00901              INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           DPKCS043
00902          END-IF                                                   DPKCS043
00903      END-IF.                                                      DPKCS043
00904                                                                   DPKCS043
00905      ADD +1            TO ASC-NUMBER-OF-ITEMS-READ.               DPKCS043
00906      MOVE ASC-NUMBER-OF-ITEMS-READ TO ASC-LIST-ITEM-NUMBER        DPKCS043
00907                                       (ASC-BLK-SUB                DPKCS043
00908                                        ASC-ITEM-SUB).             DPKCS043
00909                                                                   DPKCS043
00910 *----------------------------------------------------------------*DPKCS043
00911 *    CONVERT OBJECT ID TO DISPLAYABLE FORMAT IF THERE ARE NON-   *DPKCS043
00912 *    DISPLAY CHARACTERS IN IT.                                   *DPKCS043
00913 *----------------------------------------------------------------*DPKCS043
00914                                                                   DPKCS043
00915  4330-CONVERT-OBJECT-ID.                                          DPKCS043
00916                                                                   DPKCS043
00917      MOVE DP509-OBJ-ID  TO PV-CONVERT-OBJECT-ID.                  DPKCS043
00918                                                                   DPKCS043
00919      PERFORM VARYING PV-ID-INDEX FROM 1 BY 1                      DPKCS043
00920                      UNTIL PV-ID-INDEX > 30                       DPKCS043
00921          IF PV-OBJECT-ID-BYTE (PV-ID-INDEX) IS NUMERIC OR         DPKCS043
00922             PV-OBJECT-ID-BYTE (PV-ID-INDEX) IS ALPHABETIC OR      DPKCS043
00923             PV-OBJECT-ID-BYTE (PV-ID-INDEX) IS SPECIAL-CHARACTER  DPKCS043
00924              CONTINUE                                             DPKCS043
00925          ELSE                                                     DPKCS043
00926              MOVE '.' TO PV-OBJECT-ID-BYTE (PV-ID-INDEX)          DPKCS043
00927          END-IF                                                   DPKCS043
00928      END-PERFORM.                                                 DPKCS043
00929                                                                   DPKCS043
00930 *----------------------------------------------------------------*DPKCS043
00931 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *DPKCS043
00932 *----------------------------------------------------------------*DPKCS043
00933                                                                   DPKCS043
00934  4400-MOVE-COMMAREA-TO-SCREEN.                                    DPKCS043
00935                                                                   DPKCS043
00936      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             DPKCS043
00937          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          DPKCS043
00938          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          DPKCS043
00939              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             DPKCS043
00940                  GIVING PV-BLOCK-NUMBER                           DPKCS043
00941                  REMAINDER PV-REMAINDER                           DPKCS043
00942              IF  PV-REMAINDER > ZERO                              DPKCS043
00943                  ADD +1 TO PV-BLOCK-NUMBER                        DPKCS043
00944              END-IF                                               DPKCS043
00945              MOVE +1    TO ASC-BLK-SUB                            DPKCS043
00946              PERFORM 4410-READ-TEMP-STORAGE                       DPKCS043
00947              ADD +1     TO ASC-BLK-SUB                            DPKCS043
00948                            PV-BLOCK-NUMBER                        DPKCS043
00949              PERFORM 4410-READ-TEMP-STORAGE                       DPKCS043
00950      END-IF.                                                      DPKCS043
00951      MOVE +1                       TO ASC-BLK-SUB.                DPKCS043
00952      COMPUTE ASC-ITEM-SUB =                                       DPKCS043
00953          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              DPKCS043
00954          + PC-ITEMS-PER-PANEL).                                   DPKCS043
00955                                                                   DPKCS043
00956      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        DPKCS043
00957          ADD +1   TO ASC-BLK-SUB                                  DPKCS043
00958          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            DPKCS043
00959      END-IF.                                                      DPKCS043
00960                                                                   DPKCS043
00961      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           DPKCS043
00962          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                DPKCS043
00963      PERFORM 4420-FORMAT-LIST-LINES                               DPKCS043
00964          VARYING PS-SUB                                           DPKCS043
00965          FROM PC-ITEMS-PER-PANEL BY -1                            DPKCS043
00966          UNTIL PS-SUB < +1.                                       DPKCS043
00967                                                                   DPKCS043
00968      COMPUTE ASC-ITEMS-DISPLAYED =                                DPKCS043
00969          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          DPKCS043
00970      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL    DPKCS043
00971                                       ASTRTITO.                   DPKCS043
00972      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    DPKCS043
00973      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    DPKCS043
00974                                                                   DPKCS043
00975      IF  DP020-MSG-NUMBER-X = SPACE                               DPKCS043
00976          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  DPKCS043
00977 *            --- MORE ITEMS TO DISPLAY ----                       DPKCS043
00978              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            DPKCS043
00979              SET DP020-MSG-INFORMATIONAL TO TRUE                  DPKCS043
00980          END-IF                                                   DPKCS043
00981      END-IF.                                                      DPKCS043
00982                                                                   DPKCS043
00983 *----------------------------------------------------------------*DPKCS043
00984 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *DPKCS043
00985 *----------------------------------------------------------------*DPKCS043
00986                                                                   DPKCS043
00987  4410-READ-TEMP-STORAGE.                                          DPKCS043
00988                                                                   DPKCS043
00989      EXEC CICS                                                    DPKCS043
00990          READQ TS                                                 DPKCS043
00991              QUEUE (ASC-DTL-QUEUE-NAME)                           DPKCS043
00992              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               DPKCS043
00993              ITEM  (PV-BLOCK-NUMBER)                              DPKCS043
00994              RESP  (PV-CICS-RESP)                                 DPKCS043
00995      END-EXEC.                                                    DPKCS043
00996                                                                   DPKCS043
00997      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       DPKCS043
00998        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     DPKCS043
00999          CONTINUE                                                 DPKCS043
01000      ELSE                                                         DPKCS043
01001          SET DP013-CICS-ABEND TO  TRUE                            DPKCS043
01002          SET DP013-NO-ROLLBACK TO  TRUE                           DPKCS043
01003          MOVE '4410-READ-TEMP-STORAGE'                            DPKCS043
01004                                TO  DP013-PARAGRAPH                DPKCS043
01005          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      DPKCS043
01006                                TO  DP013-MESSAGE-TEXT(1)          DPKCS043
01007          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
01008      END-IF.                                                      DPKCS043
01009                                                                   DPKCS043
01010      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         DPKCS043
01011          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               DPKCS043
01012      END-IF.                                                      DPKCS043
01013  EJECT                                                            DPKCS043
01014 *----------------------------------------------------------------*DPKCS043
01015 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *DPKCS043
01016 *----------------------------------------------------------------*DPKCS043
01017                                                                   DPKCS043
01018  4420-FORMAT-LIST-LINES.                                          DPKCS043
01019                                                                   DPKCS043
01020      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      DPKCS043
01021          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                DPKCS043
01022          MOVE SPACE              TO MR-TYPE      (PS-SUB)         DPKCS043
01023          MOVE SPACE              TO MR-OBJECT-ID (PS-SUB)         DPKCS043
01024          MOVE SPACE              TO MR-USERID    (PS-SUB)         DPKCS043
01025          MOVE SPACE              TO MR-MNEMONIC  (PS-SUB)         DPKCS043
01026          MOVE SPACE              TO MR-TIME      (PS-SUB)         DPKCS043
01027      ELSE                                                         DPKCS043
01028          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           DPKCS043
01029              MOVE ASC-TYPE      (ASC-BLK-SUB ASC-ITEM-SUB)        DPKCS043
01030                                  TO MR-TYPE       (PS-SUB)        DPKCS043
01031              MOVE ASC-OBJECT-ID (ASC-BLK-SUB ASC-ITEM-SUB)        DPKCS043
01032                                  TO MR-OBJECT-ID  (PS-SUB)        DPKCS043
01033              MOVE ASC-USERID    (ASC-BLK-SUB ASC-ITEM-SUB)        DPKCS043
01034                                  TO MR-USERID     (PS-SUB)        DPKCS043
01035              MOVE ASC-MNEMONIC  (ASC-BLK-SUB ASC-ITEM-SUB)        DPKCS043
01036                                  TO MR-MNEMONIC   (PS-SUB)        DPKCS043
01037              MOVE ASC-TIME      (ASC-BLK-SUB ASC-ITEM-SUB)        DPKCS043
01038                                  TO MR-TIME       (PS-SUB)        DPKCS043
01039          END-IF                                                   DPKCS043
01040      END-IF.                                                      DPKCS043
01041      SUBTRACT +1 FROM ASC-ITEM-SUB.                               DPKCS043
01042      IF  ASC-ITEM-SUB < + 1                                       DPKCS043
01043          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  DPKCS043
01044          MOVE +1                 TO ASC-BLK-SUB                   DPKCS043
01045      END-IF.                                                      DPKCS043
01046                                                                   DPKCS043
01047 *----------------------------------------------------------------*DPKCS043
01048 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *DPKCS043
01049 *----------------------------------------------------------------*DPKCS043
01050                                                                   DPKCS043
01051  5100-WRITE-TEMP-STORAGE.                                         DPKCS043
01052                                                                   DPKCS043
01053      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     DPKCS043
01054                                TO  PV-LIST-ITEM-NUMBER.           DPKCS043
01055      COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                           DPKCS043
01056          ASC-HIGHEST-BLOCK-WRITTEN + 1.                           DPKCS043
01057                                                                   DPKCS043
01058      EXEC CICS                                                    DPKCS043
01059          WRITEQ TS                                                DPKCS043
01060              QUEUE (ASC-DTL-QUEUE-NAME)                           DPKCS043
01061              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               DPKCS043
01062              ITEM  (PV-HIGHEST-BLOCK-WRITTEN)                     DPKCS043
01063              RESP  (PV-CICS-RESP)                                 DPKCS043
01064      END-EXEC.                                                    DPKCS043
01065                                                                   DPKCS043
01066      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      DPKCS043
01067          SET DP013-CICS-ABEND                                     DPKCS043
01068                        TO  TRUE                                   DPKCS043
01069          SET DP013-NO-ROLLBACK                                    DPKCS043
01070                        TO  TRUE                                   DPKCS043
01071          MOVE '5100-WRITE-TEMP-STORAGE'                           DPKCS043
01072                        TO  DP013-PARAGRAPH                        DPKCS043
01073          MOVE 'ATTEMPTING TO WRITE TEMP STORAGE LIST BLOCK'       DPKCS043
01074                        TO  DP013-MESSAGE-TEXT(1)                  DPKCS043
01075          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS043
01076      END-IF.                                                      DPKCS043
01077      ADD +1            TO  ASC-HIGHEST-BLOCK-WRITTEN.             DPKCS043
01078                                                                   DPKCS043
01079  EJECT                                                            DPKCS043
01080 *----------------------------------------------------------------*DPKCS043
01081 *    ABEND PROCESSOR MODULE                                      *DPKCS043
01082 *----------------------------------------------------------------*DPKCS043
01083                                                                   DPKCS043
01084      COPY DPPD013.                                                DPKCS043
