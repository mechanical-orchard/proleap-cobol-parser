       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT312.                                                 
       AUTHOR.        MARK A FRAMNESS.                                          
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  APRIL 2011.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT312 - PHYSICAL INVENTORY STORE DIMENSION                           
      * CREATE DATA FOR MWT_PHY_SITE_INV_DIM                                    
      *                                                                         
      * THIS PROGRAM WILL CREATE A FILE TO BE LOADED TO TERADATA STAGE          
      * VIA INFORMATICA.                                                        
      *                                                                         
      * CHANGE LOG:  MARIBETH ZAMORA    WR33304 (HILO)                          
      *              NEW                                                        
      *              LUKE SCHULTE       WR33304 (HILO)                          
      *              ADD ACTL & PLND INV DATES TO EXTRACT.                      
      *                                                                         
      *              MARK FRAMNESS       WR39512 MDSE EDW                       
      *              COPY FROM MWKUT701 - NO CHANGES                            
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  330 CHARACTERS.                                     
       01  INPUT-RECORD                PIC X(330).                              
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  218 CHARACTERS.                                     
       01  OUTPUT-RECORD               PIC X(218).                              
                                                                                
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUT312'.          
           05  PC-BD                   PIC  X(02)    VALUE 'BD'.                
           05  PC-EX                   PIC  X(02)    VALUE 'EX'.                
           05  PC-CL                   PIC  X(02)    VALUE 'CL'.                
           05  PC-PC                   PIC  X(02)    VALUE 'PC'.                
           05  PC-FB                   PIC  X(02)    VALUE 'FB'.                
           05  PC-LS                   PIC  X(02)    VALUE 'LS'.                
           05  PC-Y                    PIC  X(01)    VALUE 'Y'.                 
           05  PC-N                    PIC  X(01)    VALUE 'N'.                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)    VALUE ZEROES               
                                                     COMP SYNC.                 
           88 ABEND-4013                             VALUE +4013.               
                                                                                
       01  PV-SQL-ERROR.                                                        
           05  PV-PARAGRAPH-NAME        PIC X(30)    VALUE SPACES.              
           05  PV-DB2-OPER-ATTEMPTED    PIC X(30)    VALUE SPACES.              
                                                                                
       01  PV-COUNTS.                                                           
           05  PV-IN-COUNT              PIC 9(09)    VALUE ZEROES.              
           05  PV-OUT-COUNT             PIC 9(09)    VALUE ZEROES.              
                                                                                
       01  PV-INPUT-SW                 PIC  X(01)    VALUE 'N'.                 
           88  NOT-END-INPUT                         VALUE 'N'.                 
           88  END-INPUT                             VALUE 'Y'.                 
                                                                                
       01  PV-TEMP.                                                             
           05  PV-PHY-SITE-ID          PIC S9(05)    VALUE 0.                   
           05  PV-INV-CYC-ID           PIC S9(09)    VALUE 0.                   
                                                                                
      ****************************************************************          
      *  COPY BOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE                     
      ****************************************************************          
           COPY DPWS004.                                                        
           COPY SQLCA2.                                                         
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR INPUT FILE (PI)                                          
      ****************************************************************          
           COPY INRD901.                                                        
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR OUTPUT FILE                                              
      ****************************************************************          
           COPY INRD902.                                                        
                                                                                
      ****************************************************************          
      *  SQLCA                                                                  
      ****************************************************************          
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  INT_INV_STAT_CDE TABLE LAYOUT                                          
      ****************************************************************          
           EXEC SQL                                                             
              INCLUDE INT00001                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  INT_STAT_TYP_ASSOC TABLE LAYOUT                                        
      ****************************************************************          
           EXEC SQL                                                             
              INCLUDE INT00005                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-SECTION.                                                   
      ******************************************************************        
                                                                                
           PERFORM 1000-BEG-OF-JOB.                                             
                                                                                
           PERFORM 3000-MAIN-PROCESS                                            
             UNTIL END-INPUT.                                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       1000-BEG-OF-JOB.                                                         
      ******************************************************************        
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '** PROGRAM :       ' PC-PROGRAM-NAME                        
           DISPLAY '************************************************'.          
                                                                                
           OPEN INPUT  INPUT-FILE                                               
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESS.                                                       
      *****************************************************************         
                                                                                
           PERFORM 6000-FORMAT-OUTPUT-RECORD.                                   
           PERFORM 7000-WRITE-OUTPUT-RECORD.                                    
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       4000-READ-INPUT-FILE.                                                    
      *****************************************************************         
                                                                                
           READ INPUT-FILE                                                      
               INTO  IN901-INVPAR-COMMON-EXTRACT                                
               AT END                                                           
                   SET END-INPUT     TO TRUE                                    
           END-READ.                                                            
                                                                                
           IF NOT-END-INPUT                                                     
              ADD +1 TO PV-IN-COUNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
       6000-FORMAT-OUTPUT-RECORD.                                               
      *****************************************************************         
                                                                                
           INITIALIZE IN902-STORE-INV-RECORD.                                   
                                                                                
           MOVE IN901-LOC-NBR          TO PV-PHY-SITE-ID                        
           MOVE PV-PHY-SITE-ID         TO IN902-PHY-SITE-ID                     
                                                                                
           MOVE IN901-INV-ID           TO PV-INV-CYC-ID                         
           MOVE PV-INV-CYC-ID          TO IN902-INV-CYC-ID                      
                                                                                
           MOVE IN901-INV-NM-TEXT      TO IN902-INV-CYC-NM                      
           MOVE IN901-SCHD-PHY-CNT-CDE TO IN902-CNT-METH-CDE                    
           MOVE IN901-FINCL-INV-STAT-CDE TO IN902-FIN-BK-STAT-CDE               
           MOVE IN901-UNIT-BK-DTE      TO IN902-UNT-BK-DTE                      
           MOVE IN901-FIN-BK-DTE       TO IN902-FIN-BK-DTE                      
           MOVE IN901-LOC-INV-STAT-CDE TO IN902-LOC-DLT-STAT-CDE                
           MOVE IN901-OPN-FSCL-MN-DTE  TO IN902-OPN-FMN-END-DTE.                
                                                                                
           IF  IN901-FINCL-INV-STAT-CDE = PC-BD                                 
               MOVE PC-Y               TO IN902-FIN-BKG-DLAYD-IND               
           ELSE                                                                 
               MOVE PC-N               TO IN902-FIN-BKG-DLAYD-IND               
           END-IF                                                               
                                                                                
           IF  IN901-FINCL-INV-STAT-CDE = PC-EX                                 
               MOVE PC-Y               TO IN902-STR-DLT-IND                     
           ELSE                                                                 
               MOVE PC-N               TO IN902-STR-DLT-IND                     
           END-IF                                                               
                                                                                
           IF  IN901-SCHD-PHY-CNT-CDE   = PC-CL                                 
               MOVE PC-Y               TO IN902-CLOD-STR-IND                    
           ELSE                                                                 
               MOVE PC-N               TO IN902-CLOD-STR-IND                    
           END-IF                                                               
                                                                                
           IF  IN901-ACTV-IND           = PC-Y                                  
               MOVE IN901-ACTV-IND     TO IN902-ACTV-IND                        
           ELSE                                                                 
               MOVE PC-N               TO IN902-ACTV-IND                        
           END-IF                                                               
                                                                                
           IF  IN901-SCN-INV-IND        = PC-Y                                  
               MOVE IN901-SCN-INV-IND  TO IN902-SCND-IND                        
           ELSE                                                                 
               MOVE PC-N               TO IN902-SCND-IND                        
           END-IF                                                               
                                                                                
           IF   IN901-MFSTNG-IND        = PC-Y                                  
                MOVE IN901-MFSTNG-IND  TO IN902-LOC-MFSTNG-IND                  
           ELSE                                                                 
                MOVE PC-N              TO IN902-LOC-MFSTNG-IND                  
           END-IF.                                                              
                                                                                
           PERFORM 6100-GET-CNT-METH-DESC                                       
           PERFORM 6200-GET-FIN-BK-STAT-DESC                                    
           PERFORM 6300-GET-LOC-DLT-STAT-DESC.                                  
                                                                                
           MOVE IN901-SHTG-ACTL-INV-DTE TO IN902-ACTL-INV-DTE.                  
           MOVE IN901-PLND-INV-DTE      TO IN902-PLND-INV-DTE.                  
      *****************************************************************         
       6100-GET-CNT-METH-DESC.                                                  
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
                SELECT  B.INV_STAT_CDE_DESC                                     
                  INTO  :INT00001-INV-STAT-CDE-DESC                             
                  FROM  INT_STAT_TYP_ASSOC  A                                   
                     ,  INT_INV_STAT_CDE B                                      
                 WHERE  A.INV_TYP_CDE    =  'PC'                                
                   AND  A.INV_STAT_CDE   =  :IN902-CNT-METH-CDE                 
                   AND  A.INV_STAT_CDE   =  B.INV_STAT_CDE                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                 MOVE  INT00001-INV-STAT-CDE-DESC                       NBR     
                                    TO  IN902-CNT-METH-DESC             NBR     
               WHEN SQLCODE = +100                                              
                 MOVE  SPACES       TO  IN902-CNT-METH-DESC             NBR     
               WHEN OTHER                                                       
                 MOVE '6100-GET-CNT-METH-DESC        '                          
                                    TO  PV-PARAGRAPH-NAME                       
                 MOVE 'SELECT STAT_TYP_ASSOC/STAT_CDE'                          
                                    TO  PV-DB2-OPER-ATTEMPTED                   
                 PERFORM 9999-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
       6200-GET-FIN-BK-STAT-DESC.                                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
                SELECT  B.INV_STAT_CDE_DESC                                     
                  INTO  :INT00001-INV-STAT-CDE-DESC                             
                  FROM  INT_STAT_TYP_ASSOC  A                                   
                     ,  INT_INV_STAT_CDE B                                      
                 WHERE  A.INV_TYP_CDE    =  'FB'                                
                   AND  A.INV_STAT_CDE   =  :IN902-FIN-BK-STAT-CDE              
                   AND  A.INV_STAT_CDE   =  B.INV_STAT_CDE                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                 MOVE  INT00001-INV-STAT-CDE-DESC                       NBR     
                                    TO  IN902-FIN-BK-STAT-DESC          NBR     
               WHEN SQLCODE = +100                                              
                 MOVE  SPACES       TO  IN902-FIN-BK-STAT-DESC          NBR     
               WHEN OTHER                                                       
                 MOVE '6200-GET-FIN-BK-STAT-DESC     '                          
                                    TO  PV-PARAGRAPH-NAME                       
                 MOVE 'SELECT STAT_TYP_ASSOC/STAT_CDE'                          
                                    TO  PV-DB2-OPER-ATTEMPTED                   
                 PERFORM 9999-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
       6300-GET-LOC-DLT-STAT-DESC.                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
                SELECT  B.INV_STAT_CDE_DESC                                     
                  INTO  :INT00001-INV-STAT-CDE-DESC                             
                  FROM  INT_STAT_TYP_ASSOC  A                                   
                     ,  INT_INV_STAT_CDE B                                      
                 WHERE  A.INV_TYP_CDE    =  'LS'                                
                   AND  A.INV_STAT_CDE   =  :IN902-LOC-DLT-STAT-CDE             
                   AND  A.INV_STAT_CDE   =  B.INV_STAT_CDE                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                 MOVE  INT00001-INV-STAT-CDE-DESC                       NBR     
                                    TO  IN902-LOC-DLT-STAT-DESC         NBR     
               WHEN SQLCODE = +100                                              
                 MOVE  SPACES       TO  IN902-LOC-DLT-STAT-DESC         NBR     
               WHEN OTHER                                                       
                 MOVE '6300-GET-LOC-DLT-STAT-DESC    '                          
                                    TO  PV-PARAGRAPH-NAME                       
                 MOVE 'SELECT STAT_TYP_ASSOC/STAT_CDE'                          
                                    TO  PV-DB2-OPER-ATTEMPTED                   
                 PERFORM 9999-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
       7000-WRITE-OUTPUT-RECORD.                                                
      *****************************************************************         
                                                                                
           WRITE OUTPUT-RECORD         FROM IN902-STORE-INV-RECORD              
           ADD +1 TO PV-OUT-COUNT.                                              
                                                                                
                                                                                
      *****************************************************************         
       9000-END-OF-JOB.                                                         
      *****************************************************************         
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '========== INKUT312 PROGRAM SUMMARY =========='.            
           DISPLAY '  INPUT  RECS READ        : ' PV-IN-COUNT                   
           DISPLAY '  OUTPUT RECS WRITTEN     : ' PV-OUT-COUNT                  
           DISPLAY '************************************************'.          
                                                                                
                                                                                
      *****************************************************************         
       9999-SQL-ERROR.                                                          
      ******************************************************************        
      *    SQL ABEND ROUTINE                                                    
      ******************************************************************        
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME                       
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
      ******************************************************************        
