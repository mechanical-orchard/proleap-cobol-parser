000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.           ETKUT150.                                          
000300 AUTHOR.               JON FIEDLER.                                       
000400 INSTALLATION.         KOHLS DEPARTMENT STORES.                           
000500 DATE-WRITTEN          12/10/2003.                                        
000600 DATE-COMPILED.                                                           
000700                                                                          
000800******************************************************************        
000900*PURPOSE: EXTRACT DOCUMENT TRACKING INFO FROM ETADMIN   ORACLE            
001000*         TABLES.                                                         
001100*                                                                         
001200*  DATE  05/01/2013                                                       
001300*  CHANGE MADE:                                                           
001400*  UPDATED PROGRAM TO CHANGE SCHEMA TO ETADMIN FROM TRADELINK.            
001500*  CHANGED TEST PASSWORD FOR 'ETTEAM' ORACLE CONNECT.                     
001600*  Hint : To test this program, uncomment ORACLE Prod connect.            
001700*                                                                         
001800*  MADE BY: BASKARAN SRINIVASAN     CHG#:  CHG0059608                     
      *                                                                         
      *  DATE  08/28/2013                                                       
      *  CHANGE MADE:                                                           
      *  SINCE ORACLE DATABASE CONNECTION WAS FAILING, RICK TULLOCH             
      *  SUGGESTED SOME CHANGES TO ORACLE SIGNON CONNECTION. REMOVED            
      *  EXISTING WAY TO CONNECT TO ORACLE.                                     
      *  MADE BY: BASKARAN SRINIVASAN     CHG#:  CHG0061411                     
001900******************************************************************        
002000                                                                          
002100 ENVIRONMENT DIVISION.                                                    
002200 CONFIGURATION SECTION.                                                   
002300 SOURCE-COMPUTER. IBM-3090.                                               
002400 OBJECT-COMPUTER. IBM-3090.                                               
002500                                                                          
002600 INPUT-OUTPUT SECTION.                                                    
002700 FILE-CONTROL.                                                            
002800                                                                          
           SELECT ORACLE-LOGIN-FILE          ASSIGN TO ORALOGON.                
002900     SELECT OUT-FILE                   ASSIGN TO OUT01.                   
003000                                                                          
003100 DATA DIVISION.                                                           
003200 FILE SECTION.                                                            
003300                                                                          
       FD  ORACLE-LOGIN-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ORACLE-LOGIN-REC.                                     
       01  ORACLE-LOGIN-REC.                                                    
           05  FILLER                     PIC X(80).                            
                                                                                
003400 FD  OUT-FILE                                                             
003500     LABEL RECORDS ARE STANDARD                                           
003600     RECORDING MODE IS F                                                  
003700     BLOCK CONTAINS 0 RECORDS.                                            
003800 01  OUT-RECORD               PIC X(185).                                 
003900                                                                          
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  PC-PROGRAM-CONSTANTS.                                                
004300     05  PC-PROGRAM-NAME            PIC X(08)       VALUE                 
004400                                                     'ETKUT150'.          
004500     05  PC-MESSAGES.                                                     
004600         10  PC-SQL-ABEND-MESSAGE   PIC X(23)       VALUE                 
004700         '** ABEND -> SQLERROR = '.                                       
004800         10  PC-PROGRAM-MESSAGE     PIC X(23)       VALUE                 
004900         '** PROGRAM        ** = '.                                       
005000         10  PC-PARAGRAPH-MESSAGE   PIC X(23)       VALUE                 
005100         '** PARAGRAPH-NAME ** = '.                                       
005200         10  PC-DB2-OPER-MESSAGE    PIC X(23)       VALUE                 
005300         '** DB2 OPERATION  ** = '.                                       
           05  PC-AUTO-LOGON              PIC X(01)       VALUE '/'.            
005400                                                                          
005500 01 PV-PROGRAM-VARIABLES.                                                 
005600     05  PV-ABEND-PARAGRAPH         PIC X(40)       VALUE SPACE.          
005700     05  PV-ABEND-CODE  COMP SYNC   PIC S9(04)      VALUE +0000.          
           05  PV-ORACLE-USERID-LEN       PIC S9(04)      VALUE ZEROS.          
           05  PV-ORACLE-PASSWORD-LEN     PIC S9(04)      VALUE ZEROS.          
                                                                                
005800                                                                          
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID        PIC X(40).                               
               88 CC-AUTO-LOGON                   VALUE '/'.                    
           05  CC-ORACLE-PASSWORD      PIC X(40).                               
                                                                                
005900 01 PV-OUTPUT-REC.                                                        
006000     05  PV-DOC-TRK-DTE-TIME        PIC X(14) VALUE SPACE.                
006100     05  PV-SENDER-QUAL-ID.                                               
006200         10 PV-SENDER-QUAL                  PIC X(02) VALUE SPACE.        
006300         10 PV-SENDER-ID                    PIC X(15) VALUE SPACE.        
006400     05  PV-RECEIVER-QUAL-ID.                                             
006500         10 PV-RECEIVER-QUAL                PIC X(02) VALUE SPACE.        
006600         10 PV-RECEIVER-ID                  PIC X(15) VALUE SPACE.        
006700     05  PV-CONTROL-ID                      PIC X(09) VALUE SPACE.        
006800                                                                          
006900 01  PS-PROGRAM-SWITCHES.                                                 
007000     05  PS-ROWS-LEFT-SW            PIC X(01)       VALUE 'Y'.            
007100         88  PS-NO-MORE-ROWS                        VALUE 'N'.            
007200         88  PS-MORE-ROWS                           VALUE 'Y'.            
           05  PS-ORACLE-LOGIN-EOF-SW     PIC X(01)       VALUE 'N'.            
               88  ORACLE-LOGIN-EOF                       VALUE 'Y'.            
                                                                                
007300                                                                          
007400 01  PS-PROGRAM-ACCUMUALTORS.                                             
007500     05  PA-ROWS-FETCHED            PIC 9(07)       VALUE 0.              
007600     05  PA-RECORDS-WRITTEN         PIC 9(07)       VALUE 0.              
007700                                                                          
007800 01  WS-MSG-TEXT                    PIC X(512).                           
007900 01  WS-MAX-SIZE                    PIC S9(09)      COMP                  
008000                                                    VALUE 512.            
008100 01  WS-MSG-LENGTH                  PIC S9(09)      COMP.                 
008200                                                                          
008300*----------------------------------------------------------------*        
008400*    ORACLE DECLARE SECTION                                               
008500*----------------------------------------------------------------*        
008600     EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
008700                                                                          
008800 01  USERNAME                       PIC X(10)       VARYING.              
008900 01  PASSWD                         PIC X(10)       VARYING.              
009000 01  PC-ENT01-TYPE-CDE              PIC X(02)       VALUE '01'.           
009100 01  PV-ORACLE-ID                   PIC X(1)        VALUE '/'.            
009200 01  PV-ITMCLT-CLUSTR               PIC X(40)       VALUE SPACES.         
009300                                                                          
009400******************************************************************        
009500* COBOL DECLARATION FOR TABLE AUDIT_ENTRY - ETT00001                      
009600******************************************************************        
009700     EXEC SQL                                                             
009800         INCLUDE ETT00001                                                 
009900     END-EXEC.                                                            
010000                                                                          
010100******************************************************************        
010200* COBOL DECLARATION FOR TABLE EDI_INTERCHANGE - ETT00002                  
010300******************************************************************        
010400     EXEC SQL                                                             
010500         INCLUDE ETT00002                                                 
010600     END-EXEC.                                                            
010700                                                                          
010800******************************************************************        
010900* COBOL DECLARATION FOR TABLE STREAMAUDITLINK - ETT00003                  
011000******************************************************************        
011100     EXEC SQL                                                             
011200         INCLUDE ETT00003                                                 
011300     END-EXEC.                                                            
011400                                                                          
011500******************************************************************        
011600* COBOL DECLARATION FOR TABLE STREAMDESCR - ETT00004                      
011700******************************************************************        
011800     EXEC SQL                                                             
011900         INCLUDE ETT00004                                                 
012000     END-EXEC.                                                            
012100                                                                          
012200******************************************************************        
012300* COBOL DECLARATION FOR TABLE TRACK - ETT00005                            
012400******************************************************************        
012500     EXEC SQL                                                             
012600         INCLUDE ETT00005                                                 
012700     END-EXEC.                                                            
012800                                                                          
012900     EXEC SQL END DECLARE SECTION END-EXEC.                               
013000                                                                          
013100******************************************************************        
013200* ORA COMMUNICATION AREA                                                  
013300******************************************************************        
013400  01       SQLCA.                                                         
013500           05  SQLCAID               PIC X(8).                            
013600           05  SQLCABC               PIC S9(9) COMPUTATIONAL.             
013700           05  SQLCODE               PIC S9(9) COMPUTATIONAL.             
013800           05  SQLERRM.                                                   
013900               49 SQLERRML           PIC S9(4) COMPUTATIONAL.             
014000               49 SQLERRMC           PIC X(70).                           
014100           05  SQLERRP               PIC X(8).                            
014200           05  SQLERRD OCCURS 6 TIMES                                     
014300                                     PIC S9(9) COMPUTATIONAL.             
014400           05  SQLWARN.                                                   
014500               10 SQLWARN0           PIC X(1).                            
014600               10 SQLWARN1           PIC X(1).                            
014700               10 SQLWARN2           PIC X(1).                            
014800               10 SQLWARN3           PIC X(1).                            
014900               10 SQLWARN4           PIC X(1).                            
015000               10 SQLWARN5           PIC X(1).                            
015100               10 SQLWARN6           PIC X(1).                            
015200               10 SQLWARN7           PIC X(1).                            
015300           05  SQLEXT                PIC X(8).                            
015400*****************************************************************         
015500* ORA COMMUNICATION AREA2                                                 
015600*****************************************************************         
015700     COPY SQLCA2.                                                         
015800                                                                          
015900 PROCEDURE DIVISION.                                                      
016000******************************************************************        
016100* MAIN PROCEDURE                                                          
016200******************************************************************        
016300 0000-MAINLINE-LOGIC.                                                     
016400                                                                          
016500     PERFORM 1000-INITIALIZATION.                                         
016600                                                                          
016700     PERFORM 1100-OPEN-CURSOR.                                            
016800                                                                          
016900     PERFORM 2000-PROCESS                                                 
017000        UNTIL PS-NO-MORE-ROWS.                                            
017100                                                                          
017200     PERFORM 9900-EOJ-LOGIC.                                              
017300                                                                          
017400                                                                          
017500     GOBACK.                                                              
017600     EJECT                                                                
017700******************************************************************        
017800* THIS PROCEDURE LOGONS TO ORACLE, DECLARES THE ORACLE CURSORS,           
017900* AND INITIALIZES STARTING VARIABLES.                                     
018000******************************************************************        
018100 1000-INITIALIZATION.                                                     
           OPEN INPUT   ORACLE-LOGIN-FILE                                       
018200     OPEN OUTPUT  OUT-FILE.                                               
018300                                                                          
018400                                                                          
018500****************************************************************          
018600* LOGON                                                        *          
018700****************************************************************          
022600                                                                          
      * NEW CODE TO CONNECT TO ORACLE                                           
           PERFORM 1000-CONNECT-TO-ORACLE.                                      
                                                                                
023300     EXEC SQL                                                             
023400      ALTER SESSION                                                       
023500            SET NLS_DATE_FORMAT = 'YYYY-MM-DD.HH24.MI.SS'                 
023600     END-EXEC.                                                            
023700                                                                          
023800                                                                          
023900*****************************************************************         
024000*    DOCUMENT TRACKING CURSOR -                                           
024100*****************************************************************         
024200                                                                          
024300     EXEC SQL                                                             
024400         DECLARE DOCTRACK_CSR CURSOR FOR                                  
024500             SELECT TO_CHAR (NEW_TIME (TO_DATE                            
024600             ('1970-01-01-00.00.00','YYYY-MM-DD-HH24.MI.SS') +            
024700             AUD_TIME/86400000, 'GMT', 'CDT'),                            
024800             'YYYY-MM-DDHH24MI')                                          
024900                   ,EDIIC_SENDERID                                        
025000                   ,EDIIC_RECEIVER_ID                                     
025100                   ,LPAD (EDIIC_CONTROLID, 9, '0')                        
025200             FROM ETADMIN.AUDIT_ENTRY                                     
025300                , ETADMIN.STREAMAUDITLINK                                 
025400                , ETADMIN.STREAMDESCR                                     
025500                , ETADMIN.TRACK                                           
025600                , ETADMIN.EDI_INTERCHANGE                                 
025700             WHERE SAL_STREAMDESCR_ID = DESCR_ID                          
025800             AND SAL_AUDIT_ID = AUD_ID                                    
025900             AND DESCR_ID = TRK_DESCR_ID                                  
026000             AND TRK_ID = EDIIC_TRK_ID                                    
026100             AND AUD_STATUS = 100                                         
026200             AND ((AUD_MSGSUBTYPE = 'File Processing'                     
026300             AND EDIIC_RECEIVER_ID IN                                     
026400                             ('1214147844480', '124147037000'))           
026500             OR (AUD_MSGSUBTYPE IN                                        
026600                             ( 'FTP Outbound', 'AS2 Outbound')))          
026700             AND TO_DATE                                                  
026800             ('1970-01-01-00.00.00','YYYY-MM-DD-HH24.MI.SS') +            
026900             AUD_TIME/86400000                                            
027000             >= (                                                         
027100                  SELECT SYSDATE - 2 DAYS                                 
027200                  FROM DUAL)                                              
027300             ORDER BY 1                                                   
027400     END-EXEC.                                                            
027500                                                                          
027600 EJECT                                                                    
      *----------------------------------------------------------------*        
      *    LOGON TO ORACLE                                             *        
      *    - READ THE CONTROL CARD TO GET THE CREDENTIALS AND LOGON    *        
      *    - ORACLE                                                    *        
      *----------------------------------------------------------------*        
       1000-CONNECT-TO-ORACLE.                                                  
                                                                                
           MOVE '1000-CONNECT-TO-ORACLE'   TO PV-ABEND-PARAGRAPH.               
                                                                                
           READ ORACLE-LOGIN-FILE INTO CC-ORACLE-CONNECTION                     
              AT END                                                            
                 SET ORACLE-LOGIN-EOF TO TRUE.                                  
                                                                                
           IF CC-AUTO-LOGON                                                     
              DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                 
              EXEC SQL                                                          
                   CONNECT :PC-AUTO-LOGON                                       
              END-EXEC                                                          
           ELSE                                                                 
              INITIALIZE PV-ORACLE-USERID-LEN                                   
                         PV-ORACLE-PASSWORD-LEN                                 
              INSPECT CC-ORACLE-USERID                                          
                      TALLYING PV-ORACLE-USERID-LEN                             
                      FOR CHARACTERS BEFORE INITIAL SPACE                       
              INSPECT CC-ORACLE-PASSWORD                                        
                      TALLYING PV-ORACLE-PASSWORD-LEN                           
                      FOR CHARACTERS BEFORE INITIAL SPACE                       
              MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                 
                      USERNAME-ARR                                              
              MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                         
              MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
                      PASSWD-ARR                                                
              MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                         
              MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
                   PASSWD-ARR                                                   
              MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                         
              MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                 
                   USERNAME-ARR                                                 
              MOVE PV-ORACLE-USERID-LEN  TO USERNAME-LEN                        
              MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
                   PASSWD-ARR                                                   
              MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
              DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER '            
                   USERNAME-ARR                                                 
              EXEC SQL                                                          
                   CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
              END-EXEC                                                          
              INITIALIZE CC-ORACLE-PASSWORD                                     
                         PASSWD                                                 
           END-IF                                                               
                                                                                
           IF (SQLCODE NOT = 0)                                                 
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
027700 1100-OPEN-CURSOR.                                                        
027800                                                                          
027900     MOVE '1100-OPEN-CURSOR' TO PV-ABEND-PARAGRAPH.                       
028000                                                                          
028100     EXEC SQL                                                             
028200         OPEN DOCTRACK_CSR                                                
028300     END-EXEC.                                                            
028400                                                                          
028500     EVALUATE TRUE                                                        
028600       WHEN SQLCODE = ZEROS                                               
028700         DISPLAY 'SUCCESSFUL OPEN OF CURSOR'                              
028800         DISPLAY ' '                                                      
028900         CONTINUE                                                         
029000       WHEN SQLCODE = 1403                                                
029100         SET PS-NO-MORE-ROWS TO TRUE                                      
029200         CONTINUE                                                         
029300       WHEN OTHER                                                         
029400         DISPLAY PV-ABEND-PARAGRAPH                                       
029500         PERFORM 9999-SQL-ERROR                                           
029600     END-EVALUATE.                                                        
029700                                                                          
029800 EJECT                                                                    
029900 1200-FETCH.                                                              
030000                                                                          
030100     MOVE '1200-FETCH' TO PV-ABEND-PARAGRAPH.                             
030200                                                                          
030300     EXEC SQL                                                             
030400         FETCH DOCTRACK_CSR                                               
030500         INTO  :PV-DOC-TRK-DTE-TIME                                       
030600             , :EDIIC-SENDERID                                            
030700             , :EDIIC-RECEIVER-ID                                         
030800             , :EDIIC-CONTROLID                                           
030900     END-EXEC.                                                            
031000                                                                          
031100     EVALUATE TRUE                                                        
031200       WHEN SQLCODE = ZEROS                                               
031300         DISPLAY 'SUCCESSFUL FETCH'                                       
031400         ADD 1 TO PA-ROWS-FETCHED                                         
031500       WHEN SQLCODE = 1403                                                
031600         SET PS-NO-MORE-ROWS TO TRUE                                      
031700       WHEN OTHER                                                         
031800         DISPLAY PV-ABEND-PARAGRAPH                                       
031900         PERFORM 9999-SQL-ERROR                                           
032000     END-EVALUATE.                                                        
032100                                                                          
032200 EJECT                                                                    
032300 1300-CLOSE-CURSOR.                                                       
032400                                                                          
032500     MOVE '1300-CLOSE-CURSOR' TO PV-ABEND-PARAGRAPH.                      
032600                                                                          
032700     EXEC SQL                                                             
032800         CLOSE DOCTRACK_CSR                                               
032900     END-EXEC.                                                            
033000                                                                          
033100     EVALUATE TRUE                                                        
033200       WHEN SQLCODE = ZEROS                                               
033300         DISPLAY 'SUCCESSFUL CLOSE OF CSR'                                
033400         CONTINUE                                                         
033500       WHEN OTHER                                                         
033600         DISPLAY PV-ABEND-PARAGRAPH                                       
033700         PERFORM 9999-SQL-ERROR                                           
033800     END-EVALUATE.                                                        
033900                                                                          
034000 EJECT                                                                    
034100******************************************************************        
034200*    2000-PROCESS                                                         
034300*      PURPOSE: READ AND PROCESS THE DOC TRACKING CURSOR                  
034400******************************************************************        
034500 2000-PROCESS.                                                            
034600     PERFORM 1200-FETCH.                                                  
034700     IF PS-MORE-ROWS                                                      
034800         ADD 1 TO PA-RECORDS-WRITTEN                                      
034900         MOVE EDIIC-SENDERID TO PV-SENDER-QUAL-ID                         
035000         MOVE EDIIC-RECEIVER-ID TO PV-RECEIVER-QUAL-ID                    
035100         MOVE EDIIC-CONTROLID TO PV-CONTROL-ID                            
035200         WRITE OUT-RECORD FROM PV-OUTPUT-REC                              
035300     END-IF.                                                              
035400                                                                          
035500******************************************************************        
035600*  THIS PROCEDURE PROCESSES THE END OF JOB LOGIC.                         
035700******************************************************************        
035800 9900-EOJ-LOGIC.                                                          
035900     PERFORM 1300-CLOSE-CURSOR.                                           
036000     CLOSE  OUT-FILE.                                                     
036100     DISPLAY 'ROWS FETCHED    ' PA-ROWS-FETCHED.                          
036200     DISPLAY 'RECORDS WRITTEN ' PA-RECORDS-WRITTEN.                       
036300                                                                          
036400******************************************************************        
036500*   9999-ABEND.                                                           
036600*      ERROR ABEND ROUTINE                                                
036700******************************************************************        
036800 9999-SQL-ERROR.                                                          
036900                                                                          
037000     MOVE SQLCODE    TO SQLCODE2.                                         
037100     MOVE SQLERRD(3) TO SQLERRD3.                                         
037200     DISPLAY '** ABEND **       ** = SQLERROR'.                           
037300     DISPLAY '** PROGRAM        ** = ETKUT150'.                           
037400     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
037500     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
037600     DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
037700     DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
037800     DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
037900                                                                          
038000     MOVE SPACES TO WS-MSG-TEXT.                                          
038100*    GET FULL TEXT OF ERROR MESSAGE                                       
038200                                                                          
038300     CALL 'SQLGLM' USING WS-MSG-TEXT,                                     
038400                         WS-MAX-SIZE,                                     
038500                         WS-MSG-LENGTH.                                   
038600     DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.                       
038700                                                                          
038800     MOVE +4013 TO PV-ABEND-CODE.                                         
038900     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
039000                                                                          
039100*---------------------------------------------------------------*         
039200*    END OF SOURCE CODE FOR ETKUT150                            *         
039300*---------------------------------------------------------------*         
