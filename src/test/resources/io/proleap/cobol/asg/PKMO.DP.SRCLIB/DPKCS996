00001 ******************************************************************06/23/95
00002  IDENTIFICATION DIVISION.                                         DPKCS996
00003 ******************************************************************   LV001
00004                                                                   DPKCS996
00005  PROGRAM-ID.             DPKCS996.                                DPKCS996
00006  AUTHOR.                 TECH SUPPORT.                            DPKCS996
00007  INSTALLATION.           KOHLS.                                   DPKCS996
00008  DATE-WRITTEN            OCTOBER, 1993.                           DPKCS996
00009  DATE-COMPILED.                                                   DPKCS996
00010                                                                   DPKCS996
00011 ******************************************************************DPKCS996
00012 * THIS PROGRAM DISPLAYS MAP DPKM996 TO TEST USER SECURITY.        DPKCS996
00013 * CICSAUTH IS CALLED USING PARAMENTERS SUPPLIED BY THE USER.      DPKCS996
00014 * THE CICSAUTH RETURN CODE AND ALLOW/DENY SWITCH IS DISPLAYED.    DPKCS996
00015 ******************************************************************DPKCS996
00016                                                                   DPKCS996
00017 ******************************************************************DPKCS996
00018  ENVIRONMENT DIVISION.                                            DPKCS996
00019 ******************************************************************DPKCS996
00020                                                                   DPKCS996
00021 ******************************************************************DPKCS996
00022  DATA DIVISION.                                                   DPKCS996
00023 ******************************************************************DPKCS996
00024                                                                   DPKCS996
00025  WORKING-STORAGE SECTION.                                         DPKCS996
00026 *                                                                 DPKCS996
00027  01  PC-PROGRAM-CONSTANTS.                                        DPKCS996
00028      05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'DP996A  '. DPKCS996
00029      05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'DPKM996 '. DPKCS996
00030      05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'DPKCS996'. DPKCS996
00031      05  PC-TSYMSG-NUMBERS.                                       DPKCS996
00032          10  PC-TSYMSG-00005       PIC  9(05)  VALUE  00005.      DPKCS996
00033          10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.      DPKCS996
00034          10  PC-TSYMSG-00148       PIC  9(05)  VALUE  00148.      DPKCS996
00035      05  PC-ACCESS-MESSAGES.                                      DPKCS996
00036          10  PC-ACCESS-ALLOWED-MSG PIC  X(09)  VALUE 'Y ALLOWED'. DPKCS996
00037          10  PC-ACCESS-DENIED-MSG  PIC  X(09)  VALUE 'N DENIED '. DPKCS996
00038 *                                                                 DPKCS996
00039  01  PS-PROGRAM-SWITCHES.                                         DPKCS996
00040      05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        DPKCS996
00041          88  PS-ERROR                          VALUE  'Y'.        DPKCS996
00042          88  PS-NO-ERROR                       VALUE  'N'.        DPKCS996
00043                                                                   DPKCS996
00044 ******************************************************************DPKCS996
00045 *----------------------------------------------------------------*DPKCS996
00046 *    MAP LAYOUT                                                  *DPKCS996
00047 *----------------------------------------------------------------*DPKCS996
00048                                                                   DPKCS996
00049      COPY DPKM996.                                                DPKCS996
00050      EJECT                                                        DPKCS996
00051 *----------------------------------------------------------------*DPKCS996
00052 *    COPYBOOK FOR THE CICS AUTH ROUTINE                          *DPKCS996
00053 *----------------------------------------------------------------*DPKCS996
00054                                                                   DPKCS996
00055      COPY DPWS008.                                                DPKCS996
00056                                                                   DPKCS996
00057                                                                   DPKCS996
00058  EJECT                                                            DPKCS996
00059 *----------------------------------------------------------------*DPKCS996
00060 *    ABEND PROCESSING COPYBOOK.                                  *DPKCS996
00061 *----------------------------------------------------------------*DPKCS996
00062                                                                   DPKCS996
00063      COPY DPWS013.                                                DPKCS996
00064                                                                   DPKCS996
00065                                                                   DPKCS996
00066  EJECT                                                            DPKCS996
00067 *----------------------------------------------------------------*DPKCS996
00068 *    ATTRIBUTE SETTINGS COPYBOOK.                                *DPKCS996
00069 *----------------------------------------------------------------*DPKCS996
00070                                                                   DPKCS996
00071      COPY DPWS015.                                                DPKCS996
00072                                                                   DPKCS996
00073  EJECT                                                            DPKCS996
00074 *----------------------------------------------------------------*DPKCS996
00075 *    FUNCTION KEYS COPYBOOK                                      *DPKCS996
00076 *----------------------------------------------------------------*DPKCS996
00077                                                                   DPKCS996
00078      COPY DPWS016.                                                DPKCS996
00079  EJECT                                                            DPKCS996
00080 ***************************************************************** DPKCS996
00081 *    STANDARD COMMAREA.                                           DPKCS996
00082 ***************************************************************** DPKCS996
00083                                                                   DPKCS996
00084      COPY DPWS020.                                                DPKCS996
00085      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                DPKCS996
00086                                                                   DPKCS996
00087 ***************************************************************** DPKCS996
00088 *    INTER-APPLICATION COMMAREA.                                  DPKCS996
00089 ***************************************************************** DPKCS996
00090                                                                   DPKCS996
00091          10  INTER-APPL-COMM-AREA PIC X(200).                     DPKCS996
00092                                                                   DPKCS996
00093 ***************************************************************** DPKCS996
00094 *    SPECIFIC COMMMAREA FOR DPKCS996.                             DPKCS996
00095 ***************************************************************** DPKCS996
00096          10  ASC-SPECIFIC-COMMAREA.                               DPKCS996
00097              15  ASC-RES-TYPE                PIC  X(01).          DPKCS996
00098                  88  ASC-RES-TYPE-TRAN       VALUE 'T'.           DPKCS996
00099                  88  ASC-RES-TYPE-MENU       VALUE 'M'.           DPKCS996
00100                  88  ASC-RES-TYPE-FUNC       VALUE 'F'.           DPKCS996
00101                  88  ASC-RES-TYPE-STOR       VALUE 'S'.           DPKCS996
00102                  88  ASC-RES-TYPE-DEPT       VALUE 'D'.           DPKCS996
00103              15  ASC-RES-NAME                PIC  X(15).          DPKCS996
00104              15  ASC-ACC-TYPE                PIC  X(01).          DPKCS996
00105                  88  ASC-ACC-TYPE-READ       VALUE 'R'.           DPKCS996
00106                  88  ASC-ACC-TYPE-ADD        VALUE 'A'.           DPKCS996
00107                  88  ASC-ACC-TYPE-UPD        VALUE 'U'.           DPKCS996
00108                  88  ASC-ACC-TYPE-DEL        VALUE 'D'.           DPKCS996
00109              15  ASC-ERR-CODE                PIC  X(04).          DPKCS996
00110              15  ASC-ACC-SWITCH              PIC  X(09).          DPKCS996
00111          10  FILLER                          PIC  X(2835).        DPKCS996
                                                                                
      *****************************************************************         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *****************************************************************         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
00118                                                                   DPKCS996
00119  LINKAGE SECTION.                                                 DPKCS996
00120                                                                   DPKCS996
00121  01  DFHCOMMAREA.                                                 DPKCS996
00122      05  FILLER                         OCCURS  1 TO 4072 TIMES   DPKCS996
00123                                         DEPENDING ON EIBCALEN.    DPKCS996
00124          10  FILLER                     PIC X.                    DPKCS996
00125                                                                   DPKCS996
00126                                                                   DPKCS996
00127  PROCEDURE DIVISION.                                              DPKCS996
00128 ***************************************************************** DPKCS996
00129 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** DPKCS996
00130 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** DPKCS996
00131 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** DPKCS996
00132 **                                                             ** DPKCS996
00133 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** DPKCS996
00134 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** DPKCS996
00135 ***************************************************************** DPKCS996
00136  0000-MAIN-MODULE.                                                DPKCS996
00137      INITIALIZE DP030-CICS-API-FIELDS.                            DPKCS996
00138      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       DPKCS996
00139      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          DPKCS996
00140      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         DPKCS996
00141      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       DPKCS996
00142      MOVE LENGTH OF DP996AI        TO DP030-MAP-LENGTH (1).       DPKCS996
00143      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     DPKCS996
00144      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS996
00145 *                                                                 DPKCS996
00146      PERFORM 1000-CONTROL-PROCESSING                              DPKCS996
00147 *                                                                 DPKCS996
00148      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     DPKCS996
00149      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS996
00150                                                                   DPKCS996
00151  0001-CALL-CICS-ARCH-API.                                         DPKCS996
00152      CALL DP930-CICS-ARCH-API USING DFHEIBLK                      DPKCS996
00153                                     DFHCOMMAREA                   DPKCS996
00154                                     DP030-CICS-API-FIELDS         DPKCS996
00155                                     DP020-STANDARD-COMMAREA       DPKCS996
00156                                     DP996AI.                      DPKCS996
00157 *                                                                 DPKCS996
00158      IF  DP030-RC-CALL-SUCCESSFUL                                 DPKCS996
00159          CONTINUE                                                 DPKCS996
00160      ELSE                                                         DPKCS996
00161          SET DP013-NO-ROLLBACK                                    DPKCS996
00162              DP013-XCTL-DISPLAY-RESTART                           DPKCS996
00163              DP013-CICS-ABEND      TO TRUE                        DPKCS996
00164          MOVE 'BEFORE 0000-MAIN-MODULE'                           DPKCS996
00165                                    TO DP013-PARAGRAPH             DPKCS996
00166          MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
00167 -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00168          MOVE DP030-RETURN-CODE                                   DPKCS996
00169                                    TO DP013-MESSAGE-TEXT (3)      DPKCS996
00170          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS996
00171      END-IF.                                                      DPKCS996
00172  EJECT                                                            DPKCS996
00173 *----------------------------------------------------------------*DPKCS996
00174 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *DPKCS996
00175 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *DPKCS996
00176 *----------------------------------------------------------------*DPKCS996
00177                                                                   DPKCS996
00178  1000-CONTROL-PROCESSING.                                         DPKCS996
00179                                                                   DPKCS996
00180      EVALUATE TRUE                                                DPKCS996
00181          WHEN DP020-NEXT-ACT-INITIAL                              DPKCS996
00182              INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS996
00183              PERFORM 4000-BUILD-INITIAL-PANEL                     DPKCS996
00184                                                                   DPKCS996
00185          WHEN DP020-NEXT-ACT-READ-MAP                             DPKCS996
00186              PERFORM 2000-FKEY-PROCESSING                         DPKCS996
00187                                                                   DPKCS996
00188          WHEN DP020-NEXT-ACT-RETURN                               DPKCS996
00189              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS996
00190              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   DPKCS996
00191                                                                   DPKCS996
00192          WHEN OTHER                                               DPKCS996
00193              SET DP013-LOGIC-ABEND                                DPKCS996
00194                  DP013-NO-ROLLBACK TO TRUE                        DPKCS996
00195              MOVE '1000-CONTROL-PROCESSING'                       DPKCS996
00196                                    TO  DP013-PARAGRAPH            DPKCS996
00197              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   DPKCS996
00198                                    TO  DP013-MESSAGE-TEXT(1)      DPKCS996
00199              MOVE DP020-NEXT-APPL-ACTIVITY                        DPKCS996
00200                                    TO DP013-MESSAGE-TEXT(2)       DPKCS996
00201              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS996
00202      END-EVALUATE.                                                DPKCS996
00203                                                                   DPKCS996
00204 *----------------------------------------------------------------*DPKCS996
00205 * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*DPKCS996
00206 * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*DPKCS996
00207 * EDITS.                                                         *DPKCS996
00208 *----------------------------------------------------------------*DPKCS996
00209                                                                   DPKCS996
00210  2000-FKEY-PROCESSING.                                            DPKCS996
00211                                                                   DPKCS996
00212      EVALUATE TRUE                                                DPKCS996
00213                                                                   DPKCS996
00214          WHEN DP020-SRC-AID = DP016-CLEAR                         DPKCS996
00215              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   DPKCS996
00216              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS996
00217                                                                   DPKCS996
00218          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     DPKCS996
00219              INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS996
00220              PERFORM 4000-BUILD-INITIAL-PANEL                     DPKCS996
00221              PERFORM 3200-RESET-ATTRIBUTES                        DPKCS996
00222                                                                   DPKCS996
00223          WHEN OTHER                                               DPKCS996
00224              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 DPKCS996
00225              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   DPKCS996
00226              IF  PS-NO-ERROR                                      DPKCS996
00227                  SET DP030-CONTINUE-GO TO TRUE                    DPKCS996
00228                  PERFORM 2100-CHECK-FUNCTION-KEY                  DPKCS996
00229              ELSE                                                 DPKCS996
00230                  SET DP030-OVERRIDE-GO TO TRUE                    DPKCS996
00231              END-IF                                               DPKCS996
00232      END-EVALUATE.                                                DPKCS996
00233 ******************************************************************DPKCS996
00234 ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **DPKCS996
00235 ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **DPKCS996
00236 ** FROM THE CICS ARCHITECTURE API.                              **DPKCS996
00237 ******************************************************************DPKCS996
00238  2100-CHECK-FUNCTION-KEY.                                         DPKCS996
00239      EVALUATE TRUE                                                DPKCS996
00240          WHEN DP020-SRC-AID = DP016-ENTER                         DPKCS996
00241              CONTINUE                                             DPKCS996
00242                                                                   DPKCS996
00243          WHEN OTHER                                               DPKCS996
00244              SET DP013-NO-ROLLBACK                                DPKCS996
00245                  DP013-XCTL-DISPLAY-RESTART                       DPKCS996
00246                  DP013-CICS-ABEND  TO TRUE                        DPKCS996
00247              MOVE '2100-CHECK-FUNCTION-KEY'                       DPKCS996
00248                                    TO DP013-PARAGRAPH             DPKCS996
00249              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      DPKCS996
00250                                    TO DP013-MESSAGE-TEXT (1)      DPKCS996
00251              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS996
00252      END-EVALUATE.                                                DPKCS996
00253  EJECT                                                            DPKCS996
00254 *----------------------------------------------------------------*DPKCS996
00255 * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*DPKCS996
00256 * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *DPKCS996
00257 * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *DPKCS996
00258 *----------------------------------------------------------------*DPKCS996
00259                                                                   DPKCS996
00260  2200-MOVE-SCREEN-TO-COMMAREA.                                    DPKCS996
00261      IF  ARESTYPL > ZERO                                          DPKCS996
00262          MOVE ARESTYPI             TO ASC-RES-TYPE                DPKCS996
00263      ELSE                                                         DPKCS996
00264          IF  ARESTYPF = DP015-ERASE-EOF                           DPKCS996
00265              MOVE SPACE            TO ASC-RES-TYPE                DPKCS996
00266          END-IF                                                   DPKCS996
00267      END-IF.                                                      DPKCS996
00268                                                                   DPKCS996
00269      IF  ARESNAML > ZERO                                          DPKCS996
00270          MOVE ARESNAMI             TO ASC-RES-NAME                DPKCS996
00271      ELSE                                                         DPKCS996
00272          IF  ARESNAMF = DP015-ERASE-EOF                           DPKCS996
00273              MOVE SPACE            TO ASC-RES-NAME                DPKCS996
00274          END-IF                                                   DPKCS996
00275      END-IF.                                                      DPKCS996
00276                                                                   DPKCS996
00277      IF  ACCTYPL > ZERO                                           DPKCS996
00278          MOVE ACCTYPI              TO ASC-ACC-TYPE                DPKCS996
00279      ELSE                                                         DPKCS996
00280          IF  ACCTYPF  = DP015-ERASE-EOF                           DPKCS996
00281              MOVE SPACE            TO ASC-ACC-TYPE                DPKCS996
00282          END-IF                                                   DPKCS996
00283      END-IF.                                                      DPKCS996
00284                                                                   DPKCS996
00285 *----------------------------------------------------------------*DPKCS996
00286 * ENSURE THE DATA IS CORRECT.                                    *DPKCS996
00287 *                                                                *DPKCS996
00288 *                                                                *DPKCS996
00289 *                                                                *DPKCS996
00290 *----------------------------------------------------------------*DPKCS996
00291                                                                   DPKCS996
00292  3000-EDIT-DATA-IN-COMMAREA.                                      DPKCS996
00293      PERFORM 3200-RESET-ATTRIBUTES.                               DPKCS996
00294      EVALUATE TRUE                                                DPKCS996
00295          WHEN ASC-RES-TYPE-TRAN                                   DPKCS996
00296          WHEN ASC-RES-TYPE-MENU                                   DPKCS996
00297          WHEN ASC-RES-TYPE-FUNC                                   DPKCS996
00298          WHEN ASC-RES-TYPE-STOR                                   DPKCS996
00299          WHEN ASC-RES-TYPE-DEPT                                   DPKCS996
00300              MOVE ASC-RES-TYPE TO DP008-RESOURCE-TYPE             DPKCS996
00301          WHEN OTHER                                               DPKCS996
00302              SET PS-ERROR                                         DPKCS996
00303                  DP020-MSG-FATAL   TO TRUE                        DPKCS996
00304              MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER            DPKCS996
00305              MOVE DP015-UNP-ALP-BRT-OFF                           DPKCS996
00306                                    TO ARESTYPA                    DPKCS996
00307              MOVE DP015-RED        TO ARESTYPC                    DPKCS996
00308              MOVE DP015-REVERSE    TO ARESTYPH                    DPKCS996
00309              MOVE -1               TO ARESTYPL                    DPKCS996
00310              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  DPKCS996
00311      END-EVALUATE.                                                DPKCS996
00312      IF ASC-RES-NAME > SPACES                                     DPKCS996
00313          MOVE ASC-RES-NAME TO DP008-RESOURCE-NAME                 DPKCS996
00314      ELSE                                                         DPKCS996
00315          SET PS-ERROR                                             DPKCS996
00316              DP020-MSG-FATAL   TO TRUE                            DPKCS996
00317          MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                DPKCS996
00318          MOVE DP015-UNP-ALP-BRT-OFF                               DPKCS996
00319                                TO ARESNAMA                        DPKCS996
00320          MOVE DP015-RED        TO ARESNAMC                        DPKCS996
00321          MOVE DP015-REVERSE    TO ARESNAMH                        DPKCS996
00322          MOVE -1               TO ARESNAML                        DPKCS996
00323          SET DP030-SET-CURSOR-APPL-1 TO TRUE                      DPKCS996
00324      END-IF.                                                      DPKCS996
00325      EVALUATE TRUE                                                DPKCS996
00326          WHEN ASC-ACC-TYPE-READ                                   DPKCS996
00327          WHEN ASC-ACC-TYPE-ADD                                    DPKCS996
00328          WHEN ASC-ACC-TYPE-UPD                                    DPKCS996
00329          WHEN ASC-ACC-TYPE-DEL                                    DPKCS996
00330              MOVE ASC-ACC-TYPE TO DP008-ACCESS-TYPE               DPKCS996
00331          WHEN OTHER                                               DPKCS996
00332              SET PS-ERROR                                         DPKCS996
00333                  DP020-MSG-FATAL   TO TRUE                        DPKCS996
00334              MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER            DPKCS996
00335              MOVE DP015-UNP-ALP-BRT-OFF                           DPKCS996
00336                                    TO ACCTYPA                     DPKCS996
00337              MOVE DP015-RED        TO ACCTYPC                     DPKCS996
00338              MOVE DP015-REVERSE    TO ACCTYPH                     DPKCS996
00339              MOVE -1               TO ACCTYPL                     DPKCS996
00340              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  DPKCS996
00341      END-EVALUATE.                                                DPKCS996
00342 *                                                                 DPKCS996
00343      IF  PS-NO-ERROR                                              DPKCS996
00344          MOVE -1                   TO ARESTYPL                    DPKCS996
00345          SET DP030-SET-CURSOR-APPL-1                              DPKCS996
00346                                    TO TRUE                        DPKCS996
00347          PERFORM 5000-CHECK-CICSAUTH                              DPKCS996
00348      END-IF.                                                      DPKCS996
00349 ******************************************************************DPKCS996
00350 ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **DPKCS996
00351 ** MAP.                                                         **DPKCS996
00352 ******************************************************************DPKCS996
00353  3200-RESET-ATTRIBUTES.                                           DPKCS996
00354      MOVE DP015-UNP-ALP-NOR-OFF    TO ARESTYPA                    DPKCS996
00355                                       ARESNAMA                    DPKCS996
00356                                       ACCTYPA.                    DPKCS996
00357      MOVE DP015-GREEN              TO ARESTYPC                    DPKCS996
00358                                       ARESNAMC                    DPKCS996
00359                                       ACCTYPC.                    DPKCS996
00360      MOVE DP015-UNDERLINE          TO ARESTYPH                    DPKCS996
00361                                       ARESNAMH                    DPKCS996
00362                                       ACCTYPH.                    DPKCS996
00363 *                                                                 DPKCS996
00364 *----------------------------------------------------------------*DPKCS996
00365 *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *DPKCS996
00366 *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *DPKCS996
00367 *  IT HERE.                                                      *DPKCS996
00368 *----------------------------------------------------------------*DPKCS996
00369  4000-BUILD-INITIAL-PANEL.                                        DPKCS996
00370      SET DP030-SET-CURSOR-APPL-1   TO TRUE                        DPKCS996
00371      MOVE -1                       TO ARESTYPL.                   DPKCS996
00372 *                                                                 DPKCS996
00373 *----------------------------------------------------------------*DPKCS996
00374 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *DPKCS996
00375 *----------------------------------------------------------------*DPKCS996
00376  4400-MOVE-COMMAREA-TO-SCREEN.                                    DPKCS996
00377      MOVE ASC-RES-TYPE             TO ARESTYPO.                   DPKCS996
00378      MOVE ASC-RES-NAME             TO ARESNAMO.                   DPKCS996
00379      MOVE ASC-ACC-TYPE             TO ACCTYPO.                    DPKCS996
00380  EJECT                                                            DPKCS996
00381 *----------------------------------------------------------------*DPKCS996
00382 *    CALL CICSAUTH TO CHECK RACF SECUURITY.                      *DPKCS996
00383 *----------------------------------------------------------------*DPKCS996
00384  5000-CHECK-CICSAUTH.                                             DPKCS996
00385      EXEC CICS                                                    DPKCS996
00386          LINK PROGRAM  (DP008-SECURITY-SUBROUTINE)                DPKCS996
00387               COMMAREA (DP008-SECURITY-COMMAREA)                  DPKCS996
00388               LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)           DPKCS996
00389      END-EXEC.                                                    DPKCS996
00390      MOVE DP008-ERROR-CODE          TO AERRCODO.                  DPKCS996
00391      IF DP008-ACCESS-ALLOWED                                      DPKCS996
00392          MOVE PC-ACCESS-ALLOWED-MSG TO ACCSSWO                    DPKCS996
00393      ELSE                                                         DPKCS996
00394          MOVE PC-ACCESS-DENIED-MSG  TO ACCSSWO                    DPKCS996
00395      END-IF.                                                      DPKCS996
00396  EJECT                                                            DPKCS996
00397 *----------------------------------------------------------------*DPKCS996
00398 *    ABEND PROCESSOR MODULE                                      *DPKCS996
00399 *----------------------------------------------------------------*DPKCS996
00400                                                                   DPKCS996
00401      COPY DPPD013.                                                DPKCS996
