000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.             ARKUT063.                                        
000600 AUTHOR.                 LUKE SCHULTE.                                    
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            03-08-2005.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*   THIS PROGRAM WILL READ THRU THE ART_EMP_DISC FOR ALL ASSOC            
001300*   THAT HAVE AN 'R' STATUS AND THEN LOOK TO SEE IF THEY HAVE             
001300*   BEEN TERMINATED ON THE WORKDAY FILE. WORKDAY FILE IS LOADED           
001300*   INTO AN ARRAY.                                                        
001400******************************************************************        
001410*  FD CONVER 2011/09/14 RCW CHANGE TO WRITE FD NON MONS AS OUTPUT         
001420******************************************************************        
EDDCHG*                                                                         
EDDCHG* REQUIREMENT   : EMPLOYEE DISCOUNT DEVELOPMENT PROJECT                   
EDDCHG* INCIDENT ID   : INC2168500                                              
EDDCHG* AUTHOR        : COGNIZANT                                               
EDDCHG* DATE  CHANGED : 2012-09-27                                              
EDDCHG* CHANGES MADE  : CURRENTLY THE CODE EXTRACTS ONLY DETAILS OF             
EDDCHG*                 'TERMINATED' EMPLOYEES, NOW CHANGES ARE MADE TO         
EDDCHG*                 EXTRACT DETAILS OF 'DELETED' EMPLOYEES ALSO.            
EDDCHG*                 CHANGES ARE TAGGED 'EDDCHG'.                            
EDDCHG*                                                                         
P10237******************************************************************        
P10237* REQUIREMENT   : EMPLOYEE DISCOUNT CHANGES COSA SECURITY PROJECT         
P10237* WR/IR#        : CHG0125054                                              
P10237* AUTHOR        : COGNIZANT                                               
P10237* DATE  CHANGED : 2015-08-27                                              
P10237* CHANGES MADE  : MODIFIED THE CODE TO DE-TOKENIZE THE ACCONT             
P10237*                 NUMBER IN THE INPUT FILE TO SEARCH PRIN                 
EDDCHG******************************************************************        
      * REQUIREMENT   : WORKDAY ASSOCIATE DISC CHANGES                          
      * WR/IR#        : CHG0168613                                              
      * AUTHOR        : CHERI PARMLEY                                           
      * DATE  CHANGED : 2016-11-23                                              
      * CHANGES MADE  : MODIFIED THE CODE TO REMOVE THE XST_ASSOC AND           
      *                 THE XST_ASSOC_EMPT AND REPLACE WITH THE NEW             
      *                 WORKDAY FILE LOOKING FOR ASSOCIATES THAT HAVE           
      *                 BEEN TERMINATED.                                        
001500                                                                          
001600******************************************************************        
0623RM* REQUIREMENT   : RESEARCHING LIFETIME DISCOUNT ISSUES                    
0623RM* WR/IR#        : CHG0290038                                              
0623RM* AUTHOR        : BOB MCASEY                                              
0623RM* DATE  CHANGED : 2023-06-21                                              
0623RM* CHANGES MADE  : ADDED COUNTER FOR UPDATES AND A DISPLAY OF              
0623RM*                 UPDATED EMPLOYEE ID                                     
001600******************************************************************        
0623RM* REQUIREMENT   : LIFETIME DISCOUNT NOT APPLYING CORRECTLY                
0623RM* WR/IR#        : CHG0290218                                              
0623RM* AUTHOR        : BOB MCASEY                                              
0623RM* DATE  CHANGED : 2023-06-28                                              
0623RM* CHANGES MADE  : ADDED NEW LOGIC TO RE-PROCESS LTD INPUT RECORDS         
0623RM*                 TO CORRECTLY UPDATE TABLE RECORDS THAT ARE              
0623RM*                 DISC-STAT-CDE OF 'N' TO 'L'.                            
0623RM******************************************************************        
0623RM* REQUIREMENT   : LIFETIME DISCOUNT NOT APPLYING CORRECTLY                
0623RM* WR/IR#        : CHG0290291                                              
0623RM* AUTHOR        : BOB MCASEY                                              
0623RM* DATE  CHANGED : 2023-06-30                                              
0623RM* CHANGES MADE  : ADDED 8210-UPDATE-ART-EMP-DISC TO UPDATE ALL RECS       
0623RM*                 FOR AN EMP-ID DURING THE SECONDARY PROCESSING           
0723RM******************************************************************        
0723RM* REQUIREMENT   : LIFETIME DISCOUNT NOT APPLYING CORRECTLY                
0723RM* WR/IR#        : CHG0290513                                              
0723RM* AUTHOR        : BOB MCASEY                                              
0723RM* DATE  CHANGED : 2023-06-30                                              
0723RM* CHANGES MADE  : IN 8210-UPDATE-ART-EMP-DISC REMOVE THE CHECK            
0723RM*                 FOR LAST UPDATE DATE <> CURRENT DATE                    
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002710                                                                          
002720     SELECT CONTROL-CARD-1                                                
002730         ASSIGN TO INP01.                                                 
002740                                                                          
002750     SELECT CONTROL-CARD-2                                                
002760         ASSIGN TO INP02.                                                 
002800                                                                          
002750     SELECT WD-EMP-ACCT-FILE                                              
002760         ASSIGN TO INP03.                                                 
                                                                                
002900     SELECT EMPLOYEE-DSCNT-TERMS-FILE                                     
003000         ASSIGN TO OUT01.                                                 
003400                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
003910                                                                          
003920 FD CONTROL-CARD-1                                                        
003930     RECORDING MODE IS F                                                  
003940     BLOCK CONTAINS 0 RECORDS                                             
003950     LABEL RECORDS ARE STANDARD.                                          
003960                                                                          
003970 01 CONTROL-CARD-1-REC.                                                   
003980    05 CC-ACCT-PRE                    PIC X(3).                           
003990    05 FILLER                         PIC X(1).                           
003991    05 CC-FD-PRIN                     PIC X(4).                           
003992    05 FILLER                         PIC X(72).                          
003993                                                                          
003994 FD CONTROL-CARD-2                                                        
003995     RECORDING MODE IS F                                                  
003996     BLOCK CONTAINS 0 RECORDS                                             
003997     LABEL RECORDS ARE STANDARD.                                          
003998                                                                          
003999 01 CONTROL-CARD-2-REC.                                                   
004000    05 CC-PROD-TEST-CNTL              PIC X(4).                           
004001    05 FILLER                         PIC X(76).                          
004010                                                                          
003920 FD WD-EMP-ACCT-FILE                                                      
003930     RECORDING MODE IS F                                                  
003940     BLOCK CONTAINS 0 RECORDS                                             
003950     LABEL RECORDS ARE STANDARD.                                          
003960                                                                          
003970 01 WD-EMP-ACCT-REC                  PIC X(200).                          
                                                                                
004100 FD EMPLOYEE-DSCNT-TERMS-FILE                                             
004200     RECORDING MODE IS F                                                  
004300     BLOCK CONTAINS 0 RECORDS                                             
004400     LABEL RECORDS ARE STANDARD.                                          
004500                                                                          
004600 01 EMPLOYEE-DISC-UPDATE-REC       PIC X(80).                             
005400                                                                          
005500 WORKING-STORAGE SECTION.                                                 
005600* KOHL'S STANDARD PAGE HEADER LAYOUT                                      
005700     COPY DPWS132O.                                                       
005710                                                                          
005720* KOHL'S CALENDAR ROUTINE                                                 
005730     COPY DPWS500.                                                        
005740******************************************************************        
005750*                        ABEND CODES                                      
005760******************************************************************        
005770     COPY ARWS052.                                                        
005780                                                                          
005790******************************************************************        
005800*                        SWITCHES                                         
005900******************************************************************        
006000 01 PS-PROGRAM-SWITCHES.                                                  
006400    05 PS-ASSOC-EMP-DTE-SW            PIC X      VALUE SPACES.            
006500       88 ASSOC-EMP-DTE-FOUND                    VALUE 'Y'.               
006600       88 ASSOC-EMP-DTE-NOT-FOUND                VALUE 'N'.               
009700    05 PS-ASSOC-FOUND-SW              PIC X      VALUE SPACES.            
009800       88 PS-ASSOC-FOUND                         VALUE 'Y'.               
009900       88 PS-NO-ASSOC-FOUND                      VALUE 'N'.               
010000    05 PS-EOF-WORKDAY-SW              PIC X      VALUE 'N'.               
010100       88 PS-EOF-WORKDAY                         VALUE 'Y'.               
010200       88 PS-NOT-EOF-WORKDAY                     VALUE 'N'.               
006610    05 PS-EOF-CONTROL-CARD-1-SW       PIC X      VALUE 'N'.               
006620       88 PS-EOF-CONTROL-CARD-1                  VALUE 'Y'.               
006630       88 PS-NOT-EOF-CONTROL-CARD-1              VALUE 'N'.               
006640    05 PS-PROCESSING-SW               PIC X      VALUE SPACES.            
006650       88 PS-CONTINUE-PROCESSING                 VALUE 'Y'.               
006660       88 PS-STOP-PROCESSING                     VALUE 'N'.               
006700    05 PS-SUCCESSFUL-UPDATE-SW        PIC X      VALUE 'N'.               
006800       88 PS-SUCCESSFUL-UPDATE                   VALUE 'Y'.               
006900       88 PS-UNSUCCESSFUL-UPDATE                 VALUE 'N'.               
006910    05 PS-SQL-STATUS-FLAG             PIC X      VALUE 'N'.               
006920       88 PS-SQL-STATUS-OK                       VALUE 'Y'.               
006930       88 PS-SQL-STATUS-NOT-OK                   VALUE 'N'.               
EDDCHG    05 PS-DEL-REC-SW                  PIC X      VALUE 'N'.               
EDDCHG       88 PS-DEL-REC                             VALUE 'Y'.               
EDDCHG       88 PS-NO-DEL-REC                          VALUE 'N'.               
KKTEST    05 PS-LIFTIME-DISC-SW             PIC X      VALUE 'N'.               
KKTEST       88 PS-LIF-DEC                             VALUE 'Y'.               
KKTEST       88 PS-NO-LIF-DEC                          VALUE 'N'.               
0623RM    05 PS-N-DISC-STAT-NOT-FOUND-SW    PIC X(01)  VALUE 'N'.               
0623RM       88 PS-N-DISC-STAT-NOT-FOUND               VALUE 'Y'.               
007000******************************************************************        
007100*                        CONSTANTS                                        
007200******************************************************************        
007300 01 PC-PROGRAM-CONSTANTS.                                                 
007510    05 PC-PAGE-SIZE                   PIC S9(4)  VALUE +61                
007520                                                 COMP.                    
007600    05 PC-SUCCESSFUL-SQLCODE          PIC S9(9)  VALUE ZEROES             
007700                                                 COMP.                    
007800    05 PC-ROW-NOT-FOUND-SQLCODE       PIC S9(9)  VALUE +100               
007900                                                 COMP.                    
008000    05 PC-DUPLICATE-ROW-SQLCODE       PIC S9(9)  VALUE -803               
008100                                                 COMP.                    
008200    05 PC-PROGRAM-NAME                PIC X(8)   VALUE 'ARKUT063'.        
KKTEST    05 PC-TERM-DISC-STAT-N            PIC X(1)   VALUE 'N'.               
KKTEST    05 PC-TERM-DISC-STAT-L            PIC X(1)   VALUE 'L'.               
008220                                                                          
008230******************************************************************        
008240*                        VARIABLES                                        
008250******************************************************************        
008260 01 PV-PROGRAM-VARIABLES.                                                 
008261    05 TEST-COUNT                     PIC S9(4)  VALUE ZEROES             
008262                                                 COMP.                    
008263    05 PV-IDX-MAX                     PIC S9(4)  VALUE ZEROES             
008264                                                 COMP.                    
014200    05 PV-IDX2-MAX                    PIC S9(6)  VALUE ZEROES             
014300                                                 COMP.                    
014200    05 PV-SSN-TABLE-COUNT             PIC S9(9)  VALUE ZEROES             
014300                                                 COMP.                    
008270    05 PV-TOTAL-READS                 PIC S9(9)  VALUE ZEROES             
008280                                                 COMP.                    
008290    05 PV-TOTAL-TERMS                 PIC S9(9)  VALUE ZEROES             
008300                                                 COMP.                    
0623RM    05 PV-DISC-SEL-CTR                PIC S9(9)  VALUE ZEROES             
0623RM                                                 COMP.                    
0623RM    05 PV-TOTAL-UPDATES               PIC S9(9)  VALUE ZEROES             
0623RM                                                 COMP.                    
0623RM    05 PV-SQLERRD3                    PIC S9(09)    VALUE ZERO            
0623RM                                                COMP-3.                   
008320    05 PV-CURR-ACCT-PRE               PIC X(3)   VALUE SPACES.            
008321    05 PV-CURR-DTE                    PIC X(10)  VALUE SPACES.            
008322    05 PV-CURR-TMST                   PIC X(26)  VALUE SPACES.            
008323    05 PV-DB2-TABLE-NAME              PIC X(30)  VALUE SPACES.            
008324    05 PV-DISPLAY-VALUE               PIC 9(10)  VALUE ZEROES.            
P10237    05 PV-ACCT-NBR-ZERO               PIC S9(10) VALUE ZEROES             
P10237                                                 COMP-3.                  
008325    05 PV-KEY-VALUE-NAME              PIC X(30)  VALUE SPACES.            
008326    05 PV-KEY-VALUE                   PIC X(30)  VALUE SPACES.            
008327    05 PV-OPERATION-MSG               PIC X(30)  VALUE SPACES.            
008328    05 PV-PARAGRAPH-NAME              PIC X(30)  VALUE SPACES.            
008329    05 PV-PGM-BEG-TMST                PIC X(26)  VALUE SPACES.            
008330    05 PV-PGM-END-TMST                PIC X(26)  VALUE SPACES.            
008331    05 PV-PREV-ACCT-PRE               PIC X(3)   VALUE SPACES.            
008340    05 PV-SRCH-PRIN                   PIC X(4)   VALUE SPACES.            
P10237    05 PV-ACCT-NBR-12.                                                    
P10237       10 PV-ACCT-NBR-10              PIC X(10)  VALUE SPACES.            
P10237       10 FILLER                      PIC X(02)  VALUE '00'.              
008340    05 PV-EMP-ID                      PIC X(7)   VALUE SPACES.            
009310                                                                          
009320 01 PT-ACCT-PRIN-TABLE.                                                   
009330    05 PT-ACCT-PRIN-ARRAY   OCCURS 1 TO 200 TIMES                         
009340                             DEPENDING ON PV-IDX-MAX                      
009350                             ASCENDING KEY IS PT-ACCT-PRE                 
009360                             INDEXED BY PV-IDX.                           
009370       10 PT-ACCT-PRE                 PIC  X(3).                          
009380       10 PT-FD-PRIN                  PIC  X(4).                          
009390                                                                          
019100 01 PT-EMP-TABLE.                                                         
019200    05 PT-EMP-ARRAY         OCCURS 1 TO 200000 TIMES                      
019300                             DEPENDING ON PV-IDX2-MAX                     
019400                             ASCENDING KEY IS PT-EMP-ID                   
019500                             INDEXED BY PV-IDX2.                          
019600       10 PT-EMP-ID                  PIC  X(07).                          
019600       10 PT-SSN-NBR                 PIC  X(09).                          
019600       10 PT-EMP-NM                  PIC  X(88).                          
019600       10 PT-EMP-STAT-CDE            PIC  X(01).                          
019600       10 PT-EMP-TYP-CDE             PIC  X(01).                          
019600       10 PT-LOC-NBR                 PIC  9(04).                          
019600       10 PT-DISC-DTE                PIC  X(10).                          
019600       10 PT-TERM-DTE                PIC  X(10).                          
019600       10 PT-LIFETIME-DISC-ACTV      PIC  X(01).                          
                                                                                
009500*                        COPYBOOKS                                        
009600******************************************************************        
009700                                                                          
009800******************************************************************        
009900* EMPLOYEE DISCOUNT UPDATE RECORD LAYOUT                                  
010000******************************************************************        
010100     COPY ARRD068.                                                        
010200                                                                          
009800******************************************************************        
009900* WORKDAY EMPLOYEE ACCOUNT RECORD LAYOUT                                  
010000******************************************************************        
010100     COPY ARRD100.                                                        
010200                                                                          
P10237******************************************************************        
P10237* LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *        
P10237* MODULE                                                         *        
P10237******************************************************************        
P10237     COPY PTYCCSIP.                                                       
P10237                                                                          
P10237******************************************************************        
P10237* LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *        
P10237* MODULE                                                         *        
P10237******************************************************************        
P10237     COPY DPWS031.                                                        
P10237                                                                          
010300******************************************************************        
010400* DPWS004 VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         
010500******************************************************************        
010600     COPY DPWS004.                                                        
010700                                                                          
010800******************************************************************        
010900* DB2 MESSAGE AREA                                                        
011000******************************************************************        
011100     COPY SQLCA2.                                                         
011200                                                                          
011300******************************************************************        
011400*                     SQL DECLARATIONS                                    
011500******************************************************************        
011600                                                                          
011700******************************************************************        
011800*  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                        
011900******************************************************************        
012000                                                                          
012100     EXEC SQL                                                             
012200         INCLUDE SQLCA                                                    
012300     END-EXEC.                                                            
013800******************************************************************        
013810*                     ART_EMP_DISC                                        
013820******************************************************************        
013830     EXEC SQL                                                             
013840         INCLUDE ART00001                                                 
013850     END-EXEC.                                                            
013860                                                                          
013870******************************************************************        
013880*                     HOST VARIABLES                                      
013890******************************************************************        
013900 01 HOST-VARIABLES.                                                       
014000    05 HV-NBR-OF-ACCTS   PIC S9(4) VALUE ZEROES COMP.                     
014010    05 LAST-EMP-DTE-NI   PIC S9(4) VALUE ZEROES COMP.                     
014100                                                                          
014200*EMPLOYEE DISCOUNT CURSOR                                                 
014300     EXEC SQL                                                             
014400         DECLARE EMP_DISC_CRSR CURSOR FOR                                 
014500            SELECT                                                        
014600                ACCT_NBR                                                  
014700               ,EMP_ID                                                    
014800               ,DISC_STAT_CDE                                             
014900               ,LAST_UPD_DTE                                              
015000            FROM ART_EMP_DISC                                             
P10237            WHERE ACCT_NBR <> :PV-ACCT-NBR-ZERO                           
015110              AND DISC_STAT_CDE = 'R'                                     
015200            WITH UR                                                       
015300     END-EXEC.                                                            
015400******************************************************************        
015500 PROCEDURE DIVISION.                                                      
015600******************************************************************        
015700                                                                          
015800*--------------*                                                          
015900 0000-MAINLINE.                                                           
016000*--------------*                                                          
016100                                                                          
016200     PERFORM Z050-INITIALIZE.                                             
016300                                                                          
016400     SET PS-CONTINUE-PROCESSING TO TRUE.                                  
016500                                                                          
P10237     PERFORM 5900-TOKENIZE-ZERO-ACCT.                                     
P10237                                                                          
016600     PERFORM 8000-OPEN-EMP-DISC-CRSR.                                     
016700     PERFORM 8025-FETCH-EMP-DISC-CRSR.                                    
016800                                                                          
016900     PERFORM 2000-PROCESS-EMP-DISC-TERMS                                  
017000        UNTIL PS-STOP-PROCESSING.                                         
017100                                                                          
017200     PERFORM 8050-CLOSE-EMP-DISC-CRSR.                                    
017300                                                                          
0623RM*-- GO BACK THRU THE INPUT FILE ARRAY SELECTING ONLY 'Y'                  
0623RM*-- AND UPDATE THE DISCOUNT STAT CODE TO 'L'                              
0623RM     PERFORM 3000-PROCESS-LIFETIME-RECS.                                  
                                                                                
017400     PERFORM Z100-END-PROC.                                               
017500                                                                          
017600     STOP RUN.                                                            
017700                                                                          
017800*-------------------------------*                                         
017900 2000-PROCESS-EMP-DISC-TERMS.                                             
018000*-------------------------------*                                         
018100                                                                          
018300     MOVE ART00001-EMP-ID            TO PV-EMP-ID.                        
                                                                                
034800     SET PS-NO-ASSOC-FOUND TO TRUE.                                       
047800     SEARCH ALL PT-EMP-ARRAY                                              
047900        AT END SET PS-NO-ASSOC-FOUND  TO TRUE                             
048000        WHEN PT-EMP-ID  (PV-IDX2) = PV-EMP-ID                             
048100             SET PS-ASSOC-FOUND TO TRUE                                   
048200     END-SEARCH.                                                          
034700                                                                          
018600     IF PS-ASSOC-FOUND                                                    
018700        IF PT-TERM-DTE (PV-IDX2)  <= PV-CURR-DTE                          
018818            ADD 1                   TO PV-TOTAL-TERMS                     
018819            PERFORM 5700-DETERMINE-NOTIFY-FD                              
KKTEST         IF PT-LIFETIME-DISC-ACTV(PV-IDX2) = "Y"                          
KKTEST            SET PS-LIF-DEC TO TRUE                                        
KKTEST         END-IF                                                           
KKTEST           IF PS-LIF-DEC                                                  
KKTEST            MOVE PC-TERM-DISC-STAT-L     TO ART00001-DISC-STAT-CDE        
KKTEST           ELSE                                                           
KKTEST            MOVE PC-TERM-DISC-STAT-N     TO ART00001-DISC-STAT-CDE        
KKTEST           END-IF                                                         
                  MOVE PT-EMP-STAT-CDE(PV-IDX2) TO ART00001-EMP-STAT-CDE        
                  MOVE PT-TERM-DTE(PV-IDX2)     TO ART00001-TERM-DTE            
018821            PERFORM 8200-UPDATE-ART-EMP-DISC                              
018828        END-IF                                                            
018832     END-IF.                                                              
018833     INITIALIZE PS-LIFTIME-DISC-SW                                        
018834     PERFORM 8025-FETCH-EMP-DISC-CRSR.                                    
018835                                                                          
0623RM*-------------------------------*                                         
0623RM 3000-PROCESS-LIFETIME-RECS.                                              
0623RM*-------------------------------*                                         
**TEMP     DISPLAY '3000-PROCESS-LIFETIME-RECS'.                                
0623RM                                                                          
0623RM     PERFORM VARYING PV-IDX2 FROM 1 BY 1                                  
0623RM             UNTIL  PV-IDX2 > PV-IDX2-MAX                                 
0623RM        IF PT-LIFETIME-DISC-ACTV(PV-IDX2) = 'Y'                           
0623RM             PERFORM 3100-LIFETIME-UPDATE                                 
0623RM        END-IF                                                            
0623RM     END-PERFORM.                                                         
0623RM                                                                          
0623RM*-------------------------------*                                         
0623RM 3100-LIFETIME-UPDATE.                                                    
0623RM*-------------------------------*                                         
**TEMP     DISPLAY '3100-LIFETIME-UPDATE ' PT-EMP-ID (PV-IDX2).                 
0623RM                                                                          
0623RM     PERFORM 3150-SELECT-ART-EMP-DISC.                                    
0623RM*-- CONTINUE PROCESSING IF THE EMP-STAT-CDE IS 'N'                        
0623RM     IF PS-N-DISC-STAT-NOT-FOUND                                          
0612RM        CONTINUE                                                          
0623RM     ELSE                                                                 
0623RM        IF PT-TERM-DTE (PV-IDX2)  <= PV-CURR-DTE                          
0623RM           ADD 1                         TO PV-TOTAL-TERMS                
0623RM           PERFORM 5700-DETERMINE-NOTIFY-FD                               
0623RM           SET PS-LIF-DEC                TO TRUE                          
0623RM           MOVE PC-TERM-DISC-STAT-L      TO ART00001-DISC-STAT-CDE        
0623RM           MOVE PT-EMP-STAT-CDE(PV-IDX2) TO ART00001-EMP-STAT-CDE         
0623RM           MOVE PT-TERM-DTE(PV-IDX2)     TO ART00001-TERM-DTE             
0623RM*          PERFORM 8200-UPDATE-ART-EMP-DISC                               
0623RM           PERFORM 8210-UPDATE-ART-EMP-DISC                               
0623RM        END-IF                                                            
0623RM     END-IF.                                                              
0623RM     INITIALIZE PS-LIFTIME-DISC-SW.                                       
0623RM                                                                          
0623RM 3150-SELECT-ART-EMP-DISC.                                                
0623RM                                                                          
**TEMP     DISPLAY '3150-SELECT-ART-EMP-DISC'.                                  
0623RM     MOVE 'N' TO PS-N-DISC-STAT-NOT-FOUND-SW.                             
0623RM     MOVE PT-EMP-ID (PV-IDX2) TO PV-EMP-ID.                               
0623RM                                                                          
0623RM     EXEC SQL                                                             
0623RM        SELECT                                                            
0623RM            ACCT_NBR                                                      
0623RM           ,EMP_ID                                                        
0623RM           ,DISC_STAT_CDE                                                 
0623RM           ,LAST_UPD_DTE                                                  
0623RM      INTO :ART00001-ACCT-NBR                                             
0623RM          ,:ART00001-EMP-ID                                               
0623RM          ,:ART00001-DISC-STAT-CDE                                        
0623RM          ,:ART00001-LAST-UPD-DTE                                         
0623RM        FROM ART_EMP_DISC                                                 
0623RM        WHERE EMP_ID      = :PV-EMP-ID                                    
0623RM          AND DISC_STAT_CDE = 'N'                                         
0623RM          ORDER BY LAST_UPD_DTE DESC                                      
0623RM          FETCH FIRST ROW ONLY                                            
0623RM        WITH UR                                                           
0623RM     END-EXEC.                                                            
0623RM                                                                          
0623RM     EVALUATE TRUE                                                        
0623RM        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
0623RM           ADD 1 TO PV-DISC-SEL-CTR                                       
0623RM           DISPLAY 'EMP ID        ' PV-EMP-ID                             
0623RM           DISPLAY 'ACCT #        ' ART00001-ACCT-NBR                     
0623RM           DISPLAY 'DISC STAT CDE ' ART00001-DISC-STAT-CDE                
0623RM           DISPLAY 'LAST UPDT DTE ' ART00001-LAST-UPD-DTE                 
0623RM        WHEN SQLCODE = PC-ROW-NOT-FOUND-SQLCODE                           
0623RM           SET PS-N-DISC-STAT-NOT-FOUND TO TRUE                           
**TEMP           DISPLAY PV-EMP-ID ' REC ALREADY PROCESSED'                     
0623RM        WHEN OTHER                                                        
0623RM           MOVE '3150-SELECT-ART-EMP-DISC'                                
0623RM                                         TO PV-PARAGRAPH-NAME             
0623RM           MOVE 'ART00001-ACCT-NBR'      TO PV-KEY-VALUE-NAME             
0623RM           MOVE ART00001-ACCT-NBR        TO PV-DISPLAY-VALUE              
0623RM           MOVE PV-DISPLAY-VALUE         TO PV-KEY-VALUE                  
0623RM           MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME             
0623RM           MOVE 'SELECT ERROR'           TO PV-OPERATION-MSG              
0623RM           SET ABEND-4013-DB2-ERROR      TO TRUE                          
0623RM           PERFORM Z999-ABEND                                             
0623RM     END-EVALUATE.                                                        
                                                                                
048700*-----------------------------*                                           
048800 5500-READ-WORKDAY-ASSOC.                                                 
048900*-----------------------------*                                           
           READ  WD-EMP-ACCT-FILE INTO AR100-WD-EMPLOYEE-REC                    
040600        AT END                                                            
040700           SET PS-EOF-WORKDAY TO TRUE                                     
040800     END-READ.                                                            
079700     IF PS-NOT-EOF-WORKDAY                                                
079900        MOVE AR100-EMP-ID       TO PT-EMP-ID (PV-IDX2)                    
079900        MOVE AR100-SSN-NBR      TO PT-SSN-NBR (PV-IDX2)                   
              MOVE AR100-EMP-NM       TO PT-EMP-NM  (PV-IDX2)                   
              MOVE AR100-EMP-STAT-CDE TO PT-EMP-STAT-CDE(PV-IDX2)               
              MOVE AR100-EMP-TYP-CDE  TO PT-EMP-TYP-CDE (PV-IDX2)               
              MOVE AR100-LOC-NBR      TO PT-LOC-NBR(PV-IDX2)                    
              MOVE AR100-DISC-DTE     TO PT-DISC-DTE(PV-IDX2)                   
              MOVE AR100-TERM-DTE     TO PT-TERM-DTE(PV-IDX2)                   
              MOVE AR100-LIFETIME-DISC-ACTV                                     
                                      TO PT-LIFETIME-DISC-ACTV(PV-IDX2)         
              ADD 1 TO PV-SSN-TABLE-COUNT                                       
080100        SET PV-IDX2 UP BY 1                                               
080200     END-IF.                                                              
080300                                                                          
018836*----------------------*                                                  
018837 5700-DETERMINE-NOTIFY-FD.                                                
018838*----------------------*                                                  
018839                                                                          
018840     EXEC SQL                                                             
018841        SELECT COUNT(*)                                                   
018842          INTO :HV-NBR-OF-ACCTS                                           
018843        FROM ART_EMP_DISC                                                 
018844        WHERE ACCT_NBR = :ART00001-ACCT-NBR                               
018845          AND DISC_STAT_CDE IN ('R', 'L')                                 
018846          WITH UR                                                         
018847     END-EXEC.                                                            
018848                                                                          
018852     EVALUATE TRUE                                                        
018853        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
018854           IF HV-NBR-OF-ACCTS < 2                                         
018855              PERFORM 5800-WRITE-EMP-DISC-TERM-REC                        
018856           END-IF                                                         
018858        WHEN OTHER                                                        
018859           MOVE '5700-DETERMINE-NOTIFY-FD'                                
018860                                         TO PV-PARAGRAPH-NAME             
018861           MOVE 'ART00001-ACCT-NBR'      TO PV-KEY-VALUE-NAME             
018862           MOVE ART00001-ACCT-NBR        TO PV-DISPLAY-VALUE              
018863           MOVE PV-DISPLAY-VALUE         TO PV-KEY-VALUE                  
018866           MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME             
018867           MOVE 'SELECT COUNT ERROR'     TO PV-OPERATION-MSG              
018868           SET ABEND-4013-DB2-ERROR      TO TRUE                          
018869           PERFORM Z999-ABEND                                             
018870     END-EVALUATE.                                                        
018871                                                                          
018872*----------------------*                                                  
018873 5800-WRITE-EMP-DISC-TERM-REC.                                            
018874*----------------------*                                                  
018875                                                                          
018876*    CHECK IF NEED TO LOOK UP NEW PRIN                                    
018877     MOVE ART00001-ACCT-NBR      TO PV-DISPLAY-VALUE.                     
P10237     PERFORM 5950-DETOKENIZE-ACCT-NBR.                                    
018879     IF PV-CURR-ACCT-PRE NOT = PV-PREV-ACCT-PRE                           
018880        PERFORM 5850-FIND-PRIN                                            
018881        MOVE PV-CURR-ACCT-PRE    TO PV-PREV-ACCT-PRE                      
018882     END-IF.                                                              
018883*    FORMAT BATCH NON-MON                                                 
018884     STRING '639305'             DELIMITED BY SIZE                        
018885            PV-DISPLAY-VALUE     DELIMITED BY SIZE                        
018886            INTO AR068-ACCT-NBR.                                          
KKTEST     IF PS-LIF-DEC                                                        
KKTEST      MOVE PC-TERM-DISC-STAT-L    TO AR068-DISC-STAT-CDE                  
KKTEST     ELSE                                                                 
KKTEST      MOVE PC-TERM-DISC-STAT-N    TO AR068-DISC-STAT-CDE                  
KKTEST     END-IF                                                               
018888      MOVE PV-SRCH-PRIN           TO AR068-KOHLS-SORT-PRIN                
018889                                                                          
018890      WRITE EMPLOYEE-DISC-UPDATE-REC                                      
018900        FROM AR068-EMP-DISC-UPDATE-REC.                                   
018901                                                                          
018902*-----------------------------*                                           
018910 5850-FIND-PRIN.                                                          
018920*-----------------------------*                                           
018930                                                                          
018940     IF CC-PROD-TEST-CNTL = 'PROD'                                        
018950        SEARCH ALL PT-ACCT-PRIN-ARRAY                                     
018960           AT END MOVE ZEROES            TO PV-SRCH-PRIN                  
018970           WHEN PT-ACCT-PRE (PV-IDX) = PV-CURR-ACCT-PRE                   
018980                MOVE PT-FD-PRIN (PV-IDX) TO PV-SRCH-PRIN                  
018990        END-SEARCH                                                        
018991     ELSE                                                                 
018992        MOVE ZEROES                   TO PV-SRCH-PRIN                     
018993     END-IF.                                                              
020200                                                                          
P10237*-----------------------*                                                 
P10237 5900-TOKENIZE-ZERO-ACCT.                                                 
P10237*-----------------------*                                                 
P10237                                                                          
P10237     INITIALIZE CSI-PARAMETERS                                            
P10237                DP031-PROTEG-PGM-PARAMETERS                               
P10237                PV-ACCT-NBR-12                                            
P10237                PV-ACCT-NBR-ZERO.                                         
P10237                                                                          
P10237     SET CSI-ENCRYPT-FUNCTION        TO TRUE.                             
P10237     SET DP031-ACCTNBR               TO TRUE.                             
P10237     MOVE 'ACCT-NBR'                 TO DP031-FIELD.                      
P10237     MOVE PV-ACCT-NBR-12             TO DP031-CLEAR-TEXT.                 
P10237     MOVE 12                         TO DP031-INPUT-LEN.                  
P10237                                                                          
P10237     PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
P10237                                                                          
P10237     IF CSI-RETURN-CODE NOT = ZERO                                        
P10237        DISPLAY 'ACCOUNT NBR      : ' PV-ACCT-NBR-ZERO                    
P10237        PERFORM DP031-9999-ABEND                                          
P10237     ELSE                                                                 
P10237        MOVE DP031-CIPHER-TEXT      TO PV-ACCT-NBR-ZERO                   
P10237     END-IF.                                                              
P10237                                                                          
P10237*-----------------------------*                                           
P10237 5950-DETOKENIZE-ACCT-NBR.                                                
P10237*-----------------------------*                                           
P10237                                                                          
P10237     INITIALIZE CSI-PARAMETERS                                            
P10237                DP031-PROTEG-PGM-PARAMETERS                               
P10237                PV-ACCT-NBR-12.                                           
P10237                                                                          
P10237     MOVE ART00001-ACCT-NBR          TO PV-ACCT-NBR-10.                   
P10237                                                                          
P10237     SET CSI-DECRYPT-FUNCTION        TO TRUE.                             
P10237     SET DP031-ACCTNBR               TO TRUE.                             
P10237     MOVE 'ACCT-NBR'                 TO DP031-FIELD.                      
P10237     MOVE PV-ACCT-NBR-12             TO DP031-CIPHER-TEXT.                
P10237     MOVE 12                         TO DP031-INPUT-LEN.                  
P10237                                                                          
P10237     PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
P10237                                                                          
P10237     IF CSI-RETURN-CODE NOT = ZERO                                        
P10237        DISPLAY 'ACCOUNT NBR      : ' PV-DISPLAY-VALUE                    
P10237        PERFORM DP031-9999-ABEND                                          
P10237     ELSE                                                                 
P10237        MOVE DP031-CLEAR-TEXT(1:3)  TO PV-CURR-ACCT-PRE                   
P10237     END-IF.                                                              
P10237                                                                          
020300*-----------------------------*                                           
020400 8000-OPEN-EMP-DISC-CRSR.                                                 
020500*-----------------------------*                                           
020600     EXEC SQL                                                             
020700         OPEN EMP_DISC_CRSR                                               
020800     END-EXEC.                                                            
020900                                                                          
021000     EVALUATE TRUE                                                        
021100        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
021200           CONTINUE                                                       
021300        WHEN OTHER                                                        
021400           MOVE '8000-OPEN-EMP-DISC-CRSR'                                 
021500                                         TO PV-PARAGRAPH-NAME             
021600           MOVE SPACES                   TO PV-KEY-VALUE-NAME             
021700           MOVE SPACES                   TO PV-KEY-VALUE                  
021800           MOVE 'EMP_DISC CRSR'          TO PV-DB2-TABLE-NAME             
021900           MOVE 'CURSOR OPEN ERROR'      TO PV-OPERATION-MSG              
022000           SET ABEND-4013-DB2-ERROR      TO TRUE                          
022100           PERFORM Z999-ABEND                                             
022200     END-EVALUATE.                                                        
022300                                                                          
022400*-----------------------------*                                           
022500 8025-FETCH-EMP-DISC-CRSR.                                                
022600*-----------------------------*                                           
022700     EXEC SQL                                                             
022800         FETCH EMP_DISC_CRSR                                              
022900          INTO :ART00001-ACCT-NBR                                         
023000              ,:ART00001-EMP-ID                                           
023100              ,:ART00001-DISC-STAT-CDE                                    
023200              ,:ART00001-LAST-UPD-DTE                                     
023300     END-EXEC.                                                            
023400                                                                          
023500     EVALUATE TRUE                                                        
023600        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
023700           ADD 1                  TO PV-TOTAL-READS                       
023800        WHEN SQLCODE = PC-ROW-NOT-FOUND-SQLCODE                           
023900           SET PS-STOP-PROCESSING TO TRUE                                 
024000        WHEN OTHER                                                        
024100           MOVE '8025-FETCH-EMP-DISC-CRSR'                                
024200                                          TO PV-PARAGRAPH-NAME            
024300           MOVE 'ART00001-ACCT-NBR'       TO PV-KEY-VALUE-NAME            
024400           MOVE ART00001-ACCT-NBR         TO PV-DISPLAY-VALUE             
024500           MOVE PV-DISPLAY-VALUE          TO PV-KEY-VALUE                 
024600           MOVE 'EMP_DISC CRSR'           TO PV-DB2-TABLE-NAME            
024700           MOVE 'CURSOR FETCH ERROR'      TO PV-OPERATION-MSG             
024800           SET ABEND-4013-DB2-ERROR       TO TRUE                         
024900           PERFORM Z999-ABEND                                             
025000     END-EVALUATE.                                                        
025100                                                                          
025200*-----------------------------*                                           
025300 8050-CLOSE-EMP-DISC-CRSR.                                                
025400*-----------------------------*                                           
025500     EXEC SQL                                                             
025600         CLOSE EMP_DISC_CRSR                                              
025700     END-EXEC.                                                            
025800                                                                          
025810     EVALUATE TRUE                                                        
025820        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
025830           CONTINUE                                                       
025840        WHEN OTHER                                                        
025850           MOVE '8050-CLOSE-EMP-DISC-CRSR'                                
025860                                          TO PV-PARAGRAPH-NAME            
025870           MOVE SPACES                    TO PV-KEY-VALUE-NAME            
025871           MOVE SPACES                    TO PV-KEY-VALUE                 
025872           MOVE 'EMP_DISC CRSR'           TO PV-DB2-TABLE-NAME            
025873           MOVE 'CURSOR CLOSE ERROR'      TO PV-OPERATION-MSG             
025874           SET ABEND-4013-DB2-ERROR       TO TRUE                         
025875           PERFORM Z999-ABEND                                             
025876     END-EVALUATE.                                                        
025877                                                                          
026128                                                                          
026129******************************************************************        
026130* 8200 - UPDATE THE DISCOUNT CODE TO NONE ON ART_EMP_DISC                 
026131******************************************************************        
026132 8200-UPDATE-ART-EMP-DISC.                                                
026133     EXEC SQL                                                             
026134         UPDATE ART_EMP_DISC                                              
026135          SET DISC_STAT_CDE = :ART00001-DISC-STAT-CDE                     
026136             ,LAST_UPD_DTE  = CURRENT DATE                                
                   ,EMP_STAT_CDE  = :ART00001-EMP-STAT-CDE                      
                   ,TERM_DTE      = :ART00001-TERM-DTE                          
026137          WHERE ACCT_NBR = :ART00001-ACCT-NBR                             
026138            AND EMP_ID   = :ART00001-EMP-ID                               
026139     END-EXEC.                                                            
026140                                                                          
026141     EVALUATE TRUE                                                        
026142        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
026143           SET PS-SUCCESSFUL-UPDATE      TO TRUE                          
0623RM           ADD 1 TO PV-TOTAL-UPDATES                                      
0623RM           DISPLAY '8200-UPDATE EMP-ID ' ART00001-EMP-ID                  
0623RM                   ' LTD IND ' ART00001-DISC-STAT-CDE                     
026145*       WHEN SQLCODE = PC-ROW-NOT-FOUND-SQLCODE                           
026146*          SET PS-UNSUCCESSFUL-UPDATE    TO TRUE                          
026147        WHEN OTHER                                                        
026148           MOVE '8200-UPDATE-ART-EMP-DISC' TO PV-PARAGRAPH-NAME           
026149           MOVE 'ACCT_NBR,EMP_ID'        TO PV-KEY-VALUE-NAME             
026150                                                                          
026151           MOVE ART00001-ACCT-NBR        TO PV-DISPLAY-VALUE              
026152           MOVE SPACES                   TO PV-KEY-VALUE                  
026153           STRING                                                         
026154              PV-DISPLAY-VALUE   DELIMITED BY SIZE                        
026155              ','                DELIMITED BY SIZE                        
026156              ART00001-EMP-ID    DELIMITED BY SIZE                        
026157              INTO PV-KEY-VALUE                                           
026158           END-STRING                                                     
026159                                                                          
026160           MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME             
026161           MOVE 'UPDATE ERROR'           TO PV-OPERATION-MSG              
026162           SET ABEND-4013-DB2-ERROR      TO TRUE                          
026163           PERFORM Z999-ABEND                                             
026164        END-EVALUATE.                                                     
026165                                                                          
026166******************************************************************        
0623RM 8210-UPDATE-ART-EMP-DISC.                                                
0623RM******************************************************************        
0623RM     EXEC SQL                                                             
0623RM         UPDATE ART_EMP_DISC                                              
0623RM          SET DISC_STAT_CDE = :ART00001-DISC-STAT-CDE                     
0623RM             ,LAST_UPD_DTE  = CURRENT DATE                                
0623RM             ,EMP_STAT_CDE  = :ART00001-EMP-STAT-CDE                      
0623RM             ,TERM_DTE      = :ART00001-TERM-DTE                          
0623RM          WHERE EMP_ID   = :ART00001-EMP-ID                               
0623RM     END-EXEC.                                                            
0623RM                                                                          
0623RM     EVALUATE TRUE                                                        
0623RM        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
0623RM           SET PS-SUCCESSFUL-UPDATE      TO TRUE                          
0623RM*          ADD 1 TO PV-TOTAL-UPDATES                                      
0623RM           MOVE SQLERRD(3) TO PV-SQLERRD3                                 
**TEMP           DISPLAY '# OF UPDATES FOR ' ART00001-EMP-ID ' '                
**TEMP                   PV-SQLERRD3                                            
0623RM           ADD PV-SQLERRD3 TO PV-TOTAL-UPDATES                            
0623RM           DISPLAY '8210-UPDATE EMP-ID ' ART00001-EMP-ID                  
0623RM                   ' LTD IND ' ART00001-DISC-STAT-CDE                     
0623RM        WHEN OTHER                                                        
0623RM           MOVE '8210-UPDATE-ART-EMP-DISC' TO PV-PARAGRAPH-NAME           
0623RM           MOVE 'EMP_ID'                   TO PV-KEY-VALUE-NAME           
0623RM           MOVE ART00001-EMP-ID            TO PV-KEY-VALUE                
0623RM           MOVE 'ART_EMP_DISC'             TO PV-DB2-TABLE-NAME           
0623RM           MOVE 'UPDATE ERROR'             TO PV-OPERATION-MSG            
0623RM           SET ABEND-4013-DB2-ERROR        TO TRUE                        
0623RM           PERFORM Z999-ABEND                                             
0623RM        END-EVALUATE.                                                     
0623RM                                                                          
      ******************************************************************        
      * 8600 - ACCEPT THE CURRENT DATE FROM DB2                                 
026168******************************************************************        
026169 8600-ACCEPT-CURR-TMST.                                                   
026170                                                                          
026171     EXEC SQL                                                             
026172         SET :PV-CURR-TMST = CURRENT TIMESTAMP                            
026173     END-EXEC                                                             
026174                                                                          
026175     EVALUATE TRUE                                                        
026176        WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                              
026177           MOVE PV-CURR-TMST               TO PV-CURR-DTE                 
026178        WHEN SQLCODE NOT = PC-SUCCESSFUL-SQLCODE                          
026179        WHEN SQLWARN0 = 'W'                                               
026180           MOVE '8600-ACCEPT-CURR-TMST' TO PV-PARAGRAPH-NAME              
026181           MOVE 'CURRENT TIMESTAMP'      TO PV-KEY-VALUE-NAME             
026182           MOVE PV-CURR-TMST             TO PV-KEY-VALUE                  
026183           MOVE 'N/A'                    TO PV-DB2-TABLE-NAME             
026184           MOVE 'TMST RETRIEVAL ERROR'   TO PV-OPERATION-MSG              
026185           SET ABEND-4013-DB2-ERROR      TO TRUE                          
026186           PERFORM Z999-ABEND                                             
026187     END-EVALUATE.                                                        
026188                                                                          
026189******************************************************************        
026190* Z050 - INITIALIZE                                                       
026191******************************************************************        
026192 Z050-INITIALIZE.                                                         
026193                                                                          
026194     OPEN INPUT CONTROL-CARD-1                                            
026195                CONTROL-CARD-2                                            
                      WD-EMP-ACCT-FILE.                                         
026196     OPEN OUTPUT EMPLOYEE-DSCNT-TERMS-FILE.                               
026200*                EMPLOYEE-NON-DISC-FILE.                                  
P10237                                                                          
P10237     MOVE PC-PROGRAM-NAME    TO DP031-PROGRAM-NAME.                       
026300                                                                          
026400     PERFORM 8600-ACCEPT-CURR-TMST.                                       
026500     MOVE PV-CURR-TMST      TO PV-PGM-BEG-TMST.                           
026600                                                                          
026700     MOVE ZEROES            TO ART00001-ACCT-NBR.                         
026701                                                                          
026710     SET PV-IDX              TO 1.                                        
026720     PERFORM Z060-READ-CONTROL-CARD-1                                     
026730        UNTIL PS-EOF-CONTROL-CARD-1.                                      
026740                                                                          
026750     SET PV-IDX-MAX TO PV-IDX.                                            
026760     SUBTRACT 1              FROM PV-IDX-MAX.                             
026770                                                                          
026780     READ CONTROL-CARD-2.                                                 
026800                                                                          
           SET PV-IDX2             TO 1.                                        
078200     PERFORM 5500-READ-WORKDAY-ASSOC                                      
078300        UNTIL PS-EOF-WORKDAY.                                             
                                                                                
078500     SET PV-IDX2-MAX TO PV-IDX2.                                          
078600     SUBTRACT 1              FROM PV-IDX2-MAX.                            
                                                                                
026810****************************************************************          
026820* Z060 - LOAD THE ACCT PRE - FD PRIN ARRAY                                
026830****************************************************************          
026840 Z060-READ-CONTROL-CARD-1.                                                
026850     READ CONTROL-CARD-1                                                  
026860        AT END SET PS-EOF-CONTROL-CARD-1 TO TRUE.                         
026870                                                                          
026880     IF PS-NOT-EOF-CONTROL-CARD-1                                         
026890     AND CC-ACCT-PRE (1:1) NOT = '*'                                      
026891        MOVE CC-ACCT-PRE   TO PT-ACCT-PRE (PV-IDX)                        
026892        MOVE CC-FD-PRIN    TO PT-FD-PRIN  (PV-IDX)                        
026893        SET PV-IDX UP BY 1                                                
026894     END-IF.                                                              
026895                                                                          
026900******************************************************************        
027000* Z100 - PREFORMS TASK TERMINATION PROCESSING                             
027100******************************************************************        
027200 Z100-END-PROC.                                                           
027300                                                                          
027400     CLOSE EMPLOYEE-DSCNT-TERMS-FILE                                      
027500*          EMPLOYEE-NON-DISC-FILE.                                        
                 WD-EMP-ACCT-FILE.                                              
027600                                                                          
027700     PERFORM 8600-ACCEPT-CURR-TMST.                                       
027800     MOVE PV-CURR-TMST      TO PV-PGM-END-TMST.                           
027900                                                                          
028000     DISPLAY ' '.                                                         
028100     DISPLAY '**********************'.                                    
028200     DISPLAY '   ARKUT063 CONTROLS  '.                                    
028300     DISPLAY '**********************'.                                    
028400     DISPLAY 'BEG DATE/TMST = ' PV-PGM-BEG-TMST.                          
028500     DISPLAY 'END DATE/TMST = ' PV-PGM-END-TMST.                          
028600     DISPLAY '======================'.                                    
028700     DISPLAY ' '.                                                         
028720     DISPLAY 'RECORDS READ                            : '                 
028720              PV-SSN-TABLE-COUNT.                                         
028710     DISPLAY 'CURSOR COUNT                            : '                 
028710             PV-TOTAL-READS.                                              
0623RM     DISPLAY 'L RECS ON FILE REPROCESSED AFTER CURSOR : '                 
0623RM              PV-DISC-SEL-CTR.                                            
0623RM     DISPLAY 'EMP UPDATES                             : '                 
0623RM              PV-TOTAL-UPDATES.                                           
028720     DISPLAY 'RECORDS WRITTEN                         : '                 
028720              PV-TOTAL-TERMS.                                             
028730                                                                          
028740******************************************************************        
028750* Z999 - ABEND ROUTINE                                                    
028760******************************************************************        
028770 Z999-ABEND.                                                              
028780                                                                          
028790     IF ABEND-4013-DB2-ERROR                                              
028800                                                                          
028900        PERFORM D999-DISPLAY-SQLCA                                        
029000                                                                          
029100        MOVE SQLCODE    TO SQLCODE2                                       
029200        MOVE SQLERRD(3) TO SQLERRD3                                       
029300                                                                          
029400        DISPLAY '** ABEND           ** = SQLABEND'                        
029500        DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME                
029600        DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME              
029700        DISPLAY '** KEY VALUE NAME  ** = ' PV-KEY-VALUE-NAME              
029800        DISPLAY '** KEY VALUE       ** = ' PV-KEY-VALUE                   
029900        DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME              
030000        DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG               
030100        DISPLAY '** SQLCODE         ** = ' SQLCODE2                       
030200        DISPLAY '** ROWS PROCESSED  ** = ' SQLERRD3                       
030300        DISPLAY '** SQLERRM         ** = ' SQLERRM                        
030400        DISPLAY '** SQLERRMC        ** = ' SQLERRMC                       
030500     ELSE                                                                 
030600        DISPLAY '** ABEND           ** = ABEND (NON-SQL)'                 
030700        DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME                
030800        DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME              
030900        DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG               
031000        DISPLAY '** KEY VALUE NAME  ** = ' PV-KEY-VALUE-NAME              
031100        DISPLAY '** KEY VALUE       ** = ' PV-KEY-VALUE                   
031200     END-IF.                                                              
031300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
031400                                                                          
031500******************************************************************        
031600* D999 - DISPLAY SQL INFORMATION IN READABLE FORMAT                       
031700******************************************************************        
031800 D999-DISPLAY-SQLCA.                                                      
031900******************************************************************        
032000*  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-           
032100*  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                
032200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
032300*  FORMAT.                                                                
032400******************************************************************        
032500                                                                          
032600        COPY DPPD004.                                                     
P10237                                                                          
P10237******************************************************************        
P10237*  COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE               
P10237*  PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                          
P10237******************************************************************        
P10237        COPY DPPD031.                                                     
P10237                                                                          
P10237******************************************************************        
