       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUP002                                                  
       AUTHOR.        SUE BARTELT.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/07/05.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WILL POPULATE NEW ROWS ON THE 13 WEEK PERCENT     *        
      * TABLE.                                                         *        
      ******************************************************************        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR           DESCRIPTION OF CHANGES                
      * ------- ---------- -------------  -----------------------------         
      * W28582  09-21-2006 J SELWITSCHKA  ADD UPDATE FUNCTIONALITY.             
      *                                                                         
      * 04/14/2020 RENAMED FROM BVKUP002 TO MLKUP002                            
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IMB-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BV-FILE                                                       
               ASSIGN TO INP01.                                                 
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BV-FILE                                                              
           LABEL RECORDS ARE OMITTED                                            
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  BV-FILE-REC                       PIC X(42).                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                        PIC S9(4) VALUE ZEROS              
                                                       COMP SYNC.               
           88  AC-OTHER-ERROR                          VALUE +4003.             
           88  AC-SQL-ERROR                            VALUE +4013.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-BV-RECORDS-SW         PIC X(01) VALUE 'Y'.               
               88  PS-MORE-BV-RECORDS                  VALUE 'Y'.               
               88  PS-NO-MORE-BV-RECORDS               VALUE 'N'.               
           05  PS-CNTL-DATE-FILE-SW          PIC X     VALUE 'N'.               
               88  PS-END-OF-CNTL-DTE-FILE             VALUE 'Y'.               
                                                                                
       01  ABEND-CODE-SQL              PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05 PC-PGM-NAME                   PIC X(8)  VALUE 'MLKUP002'.         
           05 PC-MAX-RETRIES                PIC S9(03) VALUE 3 COMP-3.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-MISC.                                                          
              10 PV-RETRY-COUNTER            PIC S9(04) COMP.                   
              10 PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACES.           
              10 PV-DB2-OPERATION-ATTEMPTED  PIC X(30)  VALUE SPACES.           
              10 PV-DB2-DATE-EXISTS          PIC X(01).                         
           05 PV-DB2-COUNT                   PIC S9(09) VALUE +0                
                                                         COMP-3.                
           05 PV-TOT-BRANDS                  PIC S9(09) VALUE +0                
                                                         COMP-3.                
           05 PV-BRAND-COUNT                 PIC S9(09) VALUE +0                
                                                         COMP-3.                
           05 PV-NULL                        PIC S9(04) VALUE +0 COMP.          
           05 PV-DISPLAY-AMOUNT       PIC ZZZZZZZZZZZZZZ9  VALUE ZEROS.         
                                                                                
           05 PV-BRAND-TABLE-STRING          PIC X(100).                        
           05 PV-BRAND-TABLE-INFO.                                              
             10 PV-BRAND-TABLE OCCURS  100 TIMES.                               
                15  PV-BT-LBL-SEQ-NBR        PIC  S9(4) COMP.                   
                15  PV-BT-PADJ-DA-COST-AMT   PIC  S9(11)V9(2) COMP-3.           
                15  PV-BT-MKDN-BYR-RTL-AMT   PIC  S9(11)V9(2) COMP-3.           
                15  PV-BT-MKDN-STR-RTL-AMT   PIC  S9(11)V9(2) COMP-3.           
                15  PV-BT-RCPT-GRO-COST-AMT  PIC  S9(11)V9(2) COMP-3.           
                15  PV-BT-PADJ-RTV-COST-AMT  PIC  S9(11)V9(2) COMP-3.           
                15  PV-BT-SLS-REG-RTL-AMT    PIC  S9(11)V9(2) COMP-3.           
      ******************************************************************        
      *  DB2 SQL ERROR MESSAGE FORMAT AREA                                      
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * BRAND PRORATION PERCENT TABLE                                           
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE BVT00003                                                   
           END-EXEC.                                                            
      *****************************************************************         
      *    DB2 AREA FOR COMMUNICATIONS                                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE                                              
           PERFORM 7000-READ-BV-FILE.                                           
                                                                                
           PERFORM 3000-INSERT-PCT-TABLE                                        
               UNTIL PS-NO-MORE-BV-RECORDS.                                     
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS.                                       
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *  PERFORM THE NECESSARY INITIALIZATION STEPS                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
           OPEN INPUT BV-FILE.                                                  
                                                                                
           MOVE ZERO TO PV-TOT-BRANDS                                           
                        PV-BRAND-COUNT.                                         
                                                                                
                                                                                
      ****************************************************************          
      * INSERT A NEW ROW ONTO THE BRAND PRORATION PERCENT TABLE                 
      ****************************************************************          
       3000-INSERT-PCT-TABLE.                                                   
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = 100))                                    
                  EXEC SQL                                                      
                      INSERT INTO BVT_BRND_PRRTN_PCT                            
                         ( FSCL_WK_END_DTE                                      
                          ,DEPT_NBR                                             
                          ,ENT_ID                                               
                          ,LBL_SEQ_NBR                                          
                          ,MKDN_BYR_RTL_PCT                                     
                          ,MKDN_STR_RTL_PCT                                     
                          ,RCPT_GRO_COST_PCT                                    
                          ,PADJ_DA_COST_PCT                                     
                          ,PADJ_RTV_COST_PCT                                    
                          ,SLS_REG_RTL_PCT                                      
                         )                                                      
                         VALUES (                                               
                          :BVT00003-FSCL-WK-END-DTE                             
                         ,:BVT00003-DEPT-NBR                                    
                         ,:BVT00003-ENT-ID                                      
                         ,:BVT00003-LBL-SEQ-NBR                                 
                         ,:BVT00003-MKDN-BYR-RTL-PCT                            
                         ,:BVT00003-MKDN-STR-RTL-PCT                            
                         ,:BVT00003-RCPT-GRO-COST-PCT                           
                         ,:BVT00003-PADJ-DA-COST-PCT                            
                         ,:BVT00003-PADJ-RTV-COST-PCT                           
                         ,:BVT00003-SLS-REG-RTL-PCT                             
                         )                                                      
                  END-EXEC                                                      
                                                                                
                  EVALUATE TRUE                                                 
                      WHEN SQLCODE = 0                                          
                           ADD +1 TO  PV-BRAND-COUNT                            
                           PERFORM 7000-READ-BV-FILE                            
                      WHEN SQLCODE = -904                                       
                      WHEN SQLCODE = -913                                       
                           CONTINUE                                             
W28582                WHEN SQLCODE = -803                                       
W28582                     PERFORM 3100-UPDATE-PCT-TABLE                        
                      WHEN SQLWARN0 NOT = SPACES                                
                      WHEN SQLCODE < +0                                         
                      WHEN SQLCODE > +0                                         
                           MOVE '3000-INSERT-PCT-TABLE'                         
                                 TO PV-PARAGRAPH-NAME                           
                           DISPLAY 'INSERTING ONTO THE % TABLE'                 
                                   ' DEPT: '                                    
                                    BVT00003-DEPT-NBR                           
                                   ' VENDOR: '                                  
                                    BVT00003-ENT-ID                             
                                   ' BRAND: '                                   
                                    BVT00003-LBL-SEQ-NBR                        
                           PERFORM 9999-SQL-ABEND                               
                  END-EVALUATE                                                  
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
              DISPLAY 'RETRIES EXCEEDED FOR % INSERT'                           
           END-IF.                                                              
W28582****************************************************************          
W28582* UPDATE A ROW ON THE BRAND PRORATION PERCENT TABLE                       
W28582****************************************************************          
W28582 3100-UPDATE-PCT-TABLE.                                                   
W28582     PERFORM                                                              
W28582         WITH TEST AFTER                                                  
W28582         VARYING PV-RETRY-COUNTER                                         
W28582         FROM 1 BY 1                                                      
W28582         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
W28582                   OR (SQLCODE = ZERO)                                    
W28582                   OR (SQLCODE = 100))                                    
W28582            EXEC SQL                                                      
W28582               UPDATE BVT_BRND_PRRTN_PCT                                  
W28582               SET MKDN_STR_RTL_PCT  = :BVT00003-MKDN-STR-RTL-PCT         
W28582                  ,MKDN_BYR_RTL_PCT  = :BVT00003-MKDN-BYR-RTL-PCT         
W28582                  ,RCPT_GRO_COST_PCT = :BVT00003-RCPT-GRO-COST-PCT        
W28582                  ,PADJ_DA_COST_PCT  = :BVT00003-PADJ-DA-COST-PCT         
W28582                  ,PADJ_RTV_COST_PCT = :BVT00003-PADJ-RTV-COST-PCT        
W28582                  ,SLS_REG_RTL_PCT   = :BVT00003-SLS-REG-RTL-PCT          
W28582              WHERE FSCL_WK_END_DTE = :BVT00003-FSCL-WK-END-DTE           
W28582               AND DEPT_NBR         = :BVT00003-DEPT-NBR                  
W28582               AND ENT_ID           = :BVT00003-ENT-ID                    
W28582               AND LBL_SEQ_NBR      = :BVT00003-LBL-SEQ-NBR               
W28582            END-EXEC                                                      
W28582                                                                          
W28582            EVALUATE TRUE                                                 
W28582                WHEN SQLCODE = 0                                          
W28582                     ADD +1 TO  PV-BRAND-COUNT                            
W28582                     PERFORM 7000-READ-BV-FILE                            
W28582                WHEN SQLCODE = -904                                       
W28582                WHEN SQLCODE = -913                                       
W28582                     CONTINUE                                             
W28582                WHEN SQLWARN0 NOT = SPACES                                
W28582                WHEN SQLCODE < +0                                         
W28582                WHEN SQLCODE > +0                                         
W28582                     MOVE '3100-UPDATE-PCT-TABLE'                         
W28582                           TO PV-PARAGRAPH-NAME                           
W28582                     DISPLAY 'UPDATING THE % TABLE'                       
W28582                             ' DEPT: '                                    
W28582                              BVT00003-DEPT-NBR                           
W28582                             ' VENDOR: '                                  
W28582                              BVT00003-ENT-ID                             
W28582                             ' BRAND: '                                   
W28582                              BVT00003-LBL-SEQ-NBR                        
W28582                     PERFORM 9999-SQL-ABEND                               
W28582            END-EVALUATE                                                  
W28582     END-PERFORM.                                                         
W28582                                                                          
W28582     IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
W28582        DISPLAY 'RETRIES EXCEEDED FOR % UPDATE'                           
W28582     END-IF.                                                              
      ******************************************************************        
      * 7000-READ-BV-FILE.                                                      
      ******************************************************************        
       7000-READ-BV-FILE.                                                       
           READ BV-FILE INTO DCLBVT00003                                        
               AT END SET PS-NO-MORE-BV-RECORDS TO TRUE.                        
                                                                                
      /*****************************************************************        
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.                
      ******************************************************************        
       9000-END-OF-JOB-TASKS.                                                   
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '** RECORD COUNT SUMMARY **'.                                
                                                                                
           MOVE PV-BRAND-COUNT          TO PV-DISPLAY-AMOUNT.                   
           DISPLAY '* WRITE BVT BRND PRRTN PCT  RECORDS: '                      
                                           PV-DISPLAY-AMOUNT.                   
           DISPLAY ' '.                                                         
           DISPLAY '******************************************'.                
           DISPLAY '*  END OF MLKUP002                        '.                
           DISPLAY '******************************************'.                
                                                                                
           CLOSE BV-FILE.                                                       
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           SET AC-SQL-ERROR           TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                          
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
