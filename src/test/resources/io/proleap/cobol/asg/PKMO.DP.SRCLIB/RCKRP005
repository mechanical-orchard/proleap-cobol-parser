00001 ***************************************************************** 01/25/95
00002  IDENTIFICATION DIVISION.                                         RCKRP005
00003 *****************************************************************    LV001
00004                                                                   RCKRP005
00005  PROGRAM-ID.             RCKRP005.                                RCKRP005
00006  AUTHOR.                 LARRY SCHLEICHER.                        RCKRP005
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 RCKRP005
00008  DATE-WRITTEN            09-28-94.                                RCKRP005
00009  DATE-COMPILED.                                                   RCKRP005
00010                                                                   RCKRP005
00011 ***************************************************************** RCKRP005
00012 *     ASN TRANSCRIPTION                                         * RCKRP005
00013 *                                                               * RCKRP005
00014 *  THIS PROGRAM WILL READ AN ASN EXTRACT FILE AND USE THE DATA  * RCKRP005
00015 *  TO CREATE RECORDS IN THE TPKSLIP AND TPKITEM TABLES.         * RCKRP005
00016 *                                                               * RCKRP005
00017 ***************************************************************** RCKRP005
00018 *                                                               * RCKRP005
00019 *  CHANGED MM/DD/YY BY XXXXXXXXXXX                              * RCKRP005
00020 *   WR# XXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX * RCKRP005
00021 *                                                               * RCKRP005
00022 *                                                               * RCKRP005
00023 ***************************************************************** RCKRP005
00024  ENVIRONMENT DIVISION.                                            RCKRP005
00025 ***************************************************************** RCKRP005
00026                                                                   RCKRP005
00027  CONFIGURATION SECTION.                                           RCKRP005
00028                                                                   RCKRP005
00029  SOURCE-COMPUTER.        IBM-3090.                                RCKRP005
00030  OBJECT-COMPUTER.        IBM-3090.                                RCKRP005
00031                                                                   RCKRP005
00032  INPUT-OUTPUT SECTION.                                            RCKRP005
00033                                                                   RCKRP005
00034  FILE-CONTROL.                                                    RCKRP005
00035                                                                   RCKRP005
00036      SELECT ASN-RECEIVING-RPT-FILE                                RCKRP005
00037          ASSIGN TO INP01.                                         RCKRP005
00038                                                                   RCKRP005
00039      SELECT RECV-REPORT-FILE                                      RCKRP005
00040          ASSIGN TO RPT01.                                         RCKRP005
00041                                                                   RCKRP005
00042                                                                   RCKRP005
00043 ******************************************************************RCKRP005
00044  DATA DIVISION.                                                   RCKRP005
00045 ******************************************************************RCKRP005
00046                                                                   RCKRP005
00047  FILE SECTION.                                                    RCKRP005
00048                                                                   RCKRP005
00049  FD  ASN-RECEIVING-RPT-FILE                                       RCKRP005
00050      RECORDING MODE IS F                                          RCKRP005
00051      LABEL RECORDS ARE STANDARD                                   RCKRP005
00052      BLOCK CONTAINS 0 RECORDS.                                    RCKRP005
00053                                                                   RCKRP005
00054  01  ASN-RECEIVING-RPT-RECORD         PIC  X(200).                RCKRP005
00055                                                                   RCKRP005
00056  FD  RECV-REPORT-FILE                                             RCKRP005
00057      RECORDING MODE IS F                                          RCKRP005
00058      BLOCK CONTAINS 0  RECORDS.                                   RCKRP005
00059  01  RECV-REPORT-RECORD              PIC  X(132).                 RCKRP005
00060                                                                   RCKRP005
00061  WORKING-STORAGE SECTION.                                         RCKRP005
00062                                                                   RCKRP005
00063  01  FILLER.                                                      RCKRP005
00064      05  FILLER                      PIC  X(36)    VALUE          RCKRP005
00065          ' ***** WORKING STORAGE PGM=RCKRP005 '.                  RCKRP005
00066      05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.   RCKRP005
00067      05  FILLER                      PIC  X(10)    VALUE          RCKRP005
00068          ' LOCATION='.                                            RCKRP005
00069      05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.   RCKRP005
00070      05  FILLER                      PIC  X(06)    VALUE          RCKRP005
00071          ' *****'.                                                RCKRP005
00072  EJECT                                                            RCKRP005
00073  01  PROGRAM-SWITCHES.                                            RCKRP005
00074      05  PS-NO-MORE-RECV-RECS-IND    PIC  X(01)    VALUE 'Y'.     RCKRP005
00075          88  PS-MORE-RECV-RECS                     VALUE 'Y'.     RCKRP005
00076          88  PS-NO-MORE-RECV-RECS                  VALUE 'N'.     RCKRP005
00077                                                                   RCKRP005
00078  01  PC-CONSTANTS.                                                RCKRP005
00079      05  PC-2                        PIC S9(03)    VALUE +2       RCKRP005
00080                                                    COMP-3.        RCKRP005
00081      05  PC-4                        PIC S9(03)    VALUE +4       RCKRP005
00082                                                    COMP-3.        RCKRP005
00083      05  PC-60                       PIC S9(03)    VALUE +60      RCKRP005
00084                                                    COMP-3.        RCKRP005
00085      05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          RCKRP005
00086                                                    'RCKRP005'.    RCKRP005
00087      05  PC-REPORT-NBR-1             PIC  9(01)    VALUE 1.       RCKRP005
00088      05  PC-UNKNOWN                  PIC  X(07)    VALUE          RCKRP005
00089                                                    'UNKNOWN'.     RCKRP005
00090      05  PC-MEN-FALLS                PIC  X(15)    VALUE          RCKRP005
00091          'MENOMONEE FALLS'.                                       RCKRP005
00092      05  PC-FINDLAY                  PIC  X(15)    VALUE          RCKRP005
00093          'FINDLAY        '.                                       RCKRP005
00094  EJECT                                                            RCKRP005
00095                                                                   RCKRP005
00096  01  PV-ACCUMULATOR-FIELDS.                                       RCKRP005
00097      05  PV-PAGE-CNT                 PIC S9(04)    VALUE ZERO     RCKRP005
00098                                                    COMP-3.        RCKRP005
00099      05  PV-LINE-CNT                 PIC S9(03)    VALUE +61      RCKRP005
00100                                                    COMP-3.        RCKRP005
00101  EJECT                                                            RCKRP005
00102                                                                   RCKRP005
00103  01  PV-PREV-FIELDS.                                              RCKRP005
00104      05  PV-PREV-DIST-CNTR           PIC  X(03)    VALUE SPACES.  RCKRP005
00105      05  PV-PREV-SCAC                PIC  X(04)    VALUE SPACES.  RCKRP005
00106      05  PV-PREV-BOL-NBR             PIC  X(16)    VALUE SPACES.  RCKRP005
00107      05  PV-PREV-PRO-NBR             PIC  X(30)    VALUE SPACES.  RCKRP005
00108      05  PV-PREV-EDI-VEND-NAME       PIC  X(25)    VALUE SPACES.  RCKRP005
00109      05  PV-PREV-PO-NBR              PIC  X(09)    VALUE SPACES.  RCKRP005
00110      05  PV-PREV-SHIP-DATE           PIC  X(08)    VALUE SPACES.  RCKRP005
00111                                                                   RCKRP005
00112  01  PV-MISC-FIELDS.                                              RCKRP005
00113      05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO     RCKRP005
00114                                                    COMP SYNC.     RCKRP005
00115      05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.  RCKRP005
029800     05  PV-CKPT-JOB-NAME            PIC X(08)   VALUE SPACES.            
00116                                                                   RCKRP005
00117      05  PV-CURR-DATE-YYMMDD.                                     RCKRP005
00118          10  PV-CURR-DATE-YY         PIC  9(02)    VALUE ZERO.    RCKRP005
00119          10  PV-CURR-DATE-MM         PIC  9(02)    VALUE ZERO.    RCKRP005
00120          10  PV-CURR-DATE-DD         PIC  9(02)    VALUE ZERO.    RCKRP005
00121                                                                   RCKRP005
00122      05  PV-CURR-TIME.                                            RCKRP005
00123          10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.    RCKRP005
00124          10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.    RCKRP005
00125          10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.    RCKRP005
00126                                                                   RCKRP005
00127      05  PV-HOLD-CURR-DATE-MMDDYY    PIC  9(06)    VALUE ZERO.    RCKRP005
00128      05  PV-HOLD-CURR-DATE-MMDDYY-X  REDEFINES                    RCKRP005
00129          PV-HOLD-CURR-DATE-MMDDYY.                                RCKRP005
00130          10  PV-HOLD-CURR-DATE-MM    PIC  9(02).                  RCKRP005
00131          10  PV-HOLD-CURR-DATE-DD    PIC  9(02).                  RCKRP005
00132          10  PV-HOLD-CURR-DATE-YY    PIC  9(02).                  RCKRP005
00133                                                                   RCKRP005
00134      05  PV-DATE-SHIPPED.                                         RCKRP005
00135          10  PV-DATE-SHIPPED-MM      PIC  9(02).                  RCKRP005
00136          10  PV-DATE-SHIPPED-DD      PIC  9(02).                  RCKRP005
00137          10  PV-DATE-SHIPPED-CCYY    PIC  9(04).                  RCKRP005
00138          10  FILLER REDEFINES PV-DATE-SHIPPED-CCYY.               RCKRP005
00139              15  PV-DATE-SHIPPED-CC      PIC  9(02).              RCKRP005
00140              15  PV-DATE-SHIPPED-YY      PIC  9(02).              RCKRP005
00141                                                                   RCKRP005
00142  EJECT                                                            RCKRP005
00143 ******************************************************************RCKRP005
00144 *  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.               RCKRP005
00145 ******************************************************************RCKRP005
00146      COPY DPWS132O.                                               RCKRP005
00147                                                                   RCKRP005
00148  01  HEADING-LINES.                                               RCKRP005
00149      05  HEADING-LINE-0.                                          RCKRP005
00150          10  FILLER                  PIC  X(56)    VALUE SPACES.  RCKRP005
00151          10  FILLER                  PIC  X(21)    VALUE          RCKRP005
00152              'EDI RECEIVING SUMMARY'.                             RCKRP005
00153          10  FILLER                  PIC  X(55)    VALUE SPACES.  RCKRP005
00154                                                                   RCKRP005
00149      05  HEADING-LINE-0-IMPORT.                                   RCKRP005
00150          10  FILLER                  PIC  X(55)    VALUE SPACES.  RCKRP005
00151          10  FILLER                  PIC  X(24)    VALUE          RCKRP005
00152              'IMPORT RECEIVING SUMMARY'.                          RCKRP005
00153          10  FILLER                  PIC  X(53)    VALUE SPACES.  RCKRP005
00154                                                                   RCKRP005
00155      05  HEADING-LINE-1.                                          RCKRP005
00156          10  FILLER                  PIC X(14)     VALUE          RCKRP005
00157              'DIST CENTER:  '.                                    RCKRP005
00158          10  HL1-DC-UNKN.                                         RCKRP005
00159              15  HL1-DC-UNKNOWN      PIC X(07)   VALUE SPACES.    RCKRP005
00160              15  FILLER              PIC X(13)   VALUE SPACES.    RCKRP005
00161          10  HL1-DC-ID REDEFINES HL1-DC-UNKN.                     RCKRP005
00162              15  HL1-DC-NBR          PIC X(04).                   RCKRP005
00163              15  HL1-DC-NAME         PIC X(16).                   RCKRP005
00164          10  FILLER                  PIC X(98)   VALUE SPACES.    RCKRP005
00165                                                                   RCKRP005
00166                                                                   RCKRP005
00167  01  MISCELLANEOUS-LINES.                                         RCKRP005
00168      05  END-OF-REPORT-LINE.                                      RCKRP005
00169          10  FILLER                  PIC  X(44)    VALUE SPACES.  RCKRP005
00170          10  FILLER                  PIC  X(45)    VALUE          RCKRP005
00171              '*******   E N D   O F   R E P O R T   *******'.     RCKRP005
00172          10  FILLER                  PIC  X(43)    VALUE SPACES.  RCKRP005
00173                                                                   RCKRP005
00174                                                                   RCKRP005
00175  01  DETAIL-HEADING-LINES.                                        RCKRP005
00176      05  DETAIL-HEADING-LINE-1.                                   RCKRP005
00177          10  FILLER                  PIC  X(42)    VALUE          RCKRP005
00178              '    SCAC    BOL                 PRO NUMBER'.        RCKRP005
00179          10  FILLER                  PIC  X(24)    VALUE SPACES.  RCKRP005
00180          10  FILLER                  PIC  X(47)    VALUE          RCKRP005
00181              'VENDOR                       PO       SHIP DATE'.   RCKRP005
00182          10  FILLER                  PIC  X(13)    VALUE          RCKRP005
00183              '   ASN   CALC'.                                     RCKRP005
00184          10  FILLER                  PIC  X(06)    VALUE SPACES.  RCKRP005
00185                                                                   RCKRP005
00186  01  DETAIL-LINES.                                                RCKRP005
00187      05  DETAIL-LINE1.                                            RCKRP005
00188          10  FILLER                  PIC  X(04)    VALUE SPACES.  RCKRP005
00189          10  DL1-SCAC                PIC  X(04)    VALUE SPACES.  RCKRP005
00190          10  FILLER                  PIC  X(04)    VALUE SPACES.  RCKRP005
00191          10  DL1-BOL                 PIC  X(16)    VALUE SPACES.  RCKRP005
00192          10  FILLER                  PIC  X(04)    VALUE SPACES.  RCKRP005
00193          10  DL1-PRO                 PIC  X(30)    VALUE SPACES.  RCKRP005
00194          10  FILLER                  PIC  X(04)    VALUE SPACES.  RCKRP005
00195          10  DL1-VENDOR-NAME         PIC  X(25)    VALUE SPACES.  RCKRP005
00196          10  FILLER                  PIC  X(04)    VALUE SPACES.  RCKRP005
00197          10  DL1-PO-NUMBER           PIC  9(08)    VALUE ZEROES.  RCKRP005
00198          10  FILLER                  PIC  X(01)    VALUE SPACES.  RCKRP005
00199          10  DL1-SHIP-DATE.                                       RCKRP005
00200              15  DL1-SHIP-DATE-MM    PIC  X(02)    VALUE SPACES.  RCKRP005
00201              15  FILLER              PIC  X(01)    VALUE '/'.     RCKRP005
00202              15  DL1-SHIP-DATE-DD    PIC  X(02)    VALUE SPACES.  RCKRP005
00203              15  FILLER              PIC  X(01)    VALUE '/'.     RCKRP005
00204              15  DL1-SHIP-DATE-YY    PIC  X(02)    VALUE SPACES.  RCKRP005
00205          10  FILLER                  PIC  X(02)    VALUE SPACES.  RCKRP005
00206          10  DL1-ASN-CARTONS         PIC  9(05)    VALUE ZEROES.  RCKRP005
00207          10  FILLER                  PIC  X(02)    VALUE SPACES.  RCKRP005
00208          10  DL1-CALC-CARTONS        PIC  9(05)    VALUE ZEROES.  RCKRP005
00209          10  FILLER                  PIC  X(06)    VALUE SPACES.  RCKRP005
00210                                                                   RCKRP005
00211 ******************************************************************RCKRP005
00212 *    ASN RECEIVING REPORT LAYOUT                                  RCKRP005
00213 ******************************************************************RCKRP005
00214      COPY RCRD004.                                                RCKRP005
00215  EJECT                                                            RCKRP005
00216 ***************************************************************** RCKRP005
00217 * PROCEDURE DIVISION.                                           * RCKRP005
00218 ***************************************************************** RCKRP005
00219  PROCEDURE DIVISION.                                              RCKRP005
00220                                                                   RCKRP005
00221  0000-MAINLINE-PROCESSING.                                        RCKRP005
00222                                                                   RCKRP005
00223      PERFORM 1000-INITIALIZATION.                                 RCKRP005
00224                                                                   RCKRP005
00225      PERFORM 2000-PROCESS-RECV-RPT-RECS                           RCKRP005
00226        UNTIL PS-NO-MORE-RECV-RECS.                                RCKRP005
00227                                                                   RCKRP005
00228      WRITE RECV-REPORT-RECORD                                     RCKRP005
00229        FROM END-OF-REPORT-LINE                                    RCKRP005
00230          AFTER ADVANCING 2 LINES.                                 RCKRP005
00231                                                                   RCKRP005
00232      CLOSE ASN-RECEIVING-RPT-FILE                                 RCKRP005
00233            RECV-REPORT-FILE.                                      RCKRP005
00234                                                                   RCKRP005
00235      STOP RUN.                                                    RCKRP005
00236                                                                   RCKRP005
00237  EJECT                                                            RCKRP005
00238 ******************************************************************RCKRP005
00239 ** READ THE DB2 CONTROL TABLE AND READ THE FIRST ASN EXTRACT REC *RCKRP005
00240 ******************************************************************RCKRP005
00241  1000-INITIALIZATION.                                             RCKRP005
00242                                                                   RCKRP005
00243      OPEN INPUT  ASN-RECEIVING-RPT-FILE                           RCKRP005
00244           OUTPUT RECV-REPORT-FILE.                                RCKRP005
00245                                                                   RCKRP005
00246      ACCEPT PV-CURR-DATE-YYMMDD FROM DATE.                        RCKRP005
00247      MOVE PV-CURR-DATE-YY TO PV-HOLD-CURR-DATE-YY.                RCKRP005
00248      MOVE PV-CURR-DATE-MM TO PV-HOLD-CURR-DATE-MM.                RCKRP005
00249      MOVE PV-CURR-DATE-DD TO PV-HOLD-CURR-DATE-DD.                RCKRP005
00250                                                                   RCKRP005
00251      ACCEPT PV-CURR-TIME FROM TIME.                               RCKRP005
00252                                                                   RCKRP005
00253      MOVE PV-CURR-DATE-MM        TO DP132O-RUN-MONTH.             RCKRP005
00254      MOVE PV-CURR-DATE-DD        TO DP132O-RUN-DAY.               RCKRP005
00255      MOVE PV-CURR-DATE-YY        TO DP132O-RUN-YEAR.              RCKRP005
00256                                                                   RCKRP005
00257      MOVE PV-CURR-TIME-HH        TO DP132O-RUN-HOUR.              RCKRP005
00258      MOVE PV-CURR-TIME-MM        TO DP132O-RUN-MINUTE.            RCKRP005
00259      MOVE PC-CURRENT-PROGRAM-NAME                                 RCKRP005
00260                                  TO DP132O-PROGRAM-NAME.          RCKRP005
           ACCEPT PV-CKPT-JOB-NAME FROM SYSIN.                                  
00261      PERFORM 7000-READ-RECEIVING-RPT.                             RCKRP005
00262                                                                   RCKRP005
00263  EJECT                                                            RCKRP005
00264 ******************************************************************RCKRP005
00265 *  PROCESS THE RECEIVING REPORT FILE.                            *RCKRP005
00266 *                                                                *RCKRP005
00267 ******************************************************************RCKRP005
00268  2000-PROCESS-RECV-RPT-RECS.                                      RCKRP005
00269                                                                   RCKRP005
00270      PERFORM 2100-FORMAT-DETAIL-LINES.                            RCKRP005
00271                                                                   RCKRP005
00272      PERFORM 8400-WRITE-DETAIL-LINES.                             RCKRP005
00273      PERFORM 8500-SAVE-PREV-RECORD.                               RCKRP005
00274                                                                   RCKRP005
00275      PERFORM 7000-READ-RECEIVING-RPT.                             RCKRP005
00276                                                                   RCKRP005
00277  2100-FORMAT-DETAIL-LINES.                                        RCKRP005
00278 ******************************************************************RCKRP005
00279 *  THIS PARAGRAPH FORMATS THE DETAIL LINES FOR AN RECV REPORT.   *RCKRP005
00280 ******************************************************************RCKRP005
00281      MOVE RC004-CARRIER-SCAC-CODE    TO DL1-SCAC                  RCKRP005
00282      MOVE RC004-BILL-OF-LADING-NBR   TO DL1-BOL                   RCKRP005
00283      MOVE RC004-PRO-NBR              TO DL1-PRO                   RCKRP005
00284      MOVE RC004-EDI-VEND-NAME        TO DL1-VENDOR-NAME           RCKRP005
00285      MOVE RC004-PO-NBR               TO DL1-PO-NUMBER             RCKRP005
00286 *    MOVE RC004-TRAILER-NBR          TO                           RCKRP005
00287 *    MOVE RC004-CONSOLIDATION-IND                                 RCKRP005
00288      MOVE RC004-DATE-SHIPPED         TO PV-DATE-SHIPPED.          RCKRP005
00289      MOVE PV-DATE-SHIPPED-MM         TO DL1-SHIP-DATE-MM.         RCKRP005
00290      MOVE PV-DATE-SHIPPED-DD         TO DL1-SHIP-DATE-DD.         RCKRP005
00291      MOVE PV-DATE-SHIPPED-YY         TO DL1-SHIP-DATE-YY.         RCKRP005
00292      MOVE RC004-ASN-CARTONS          TO DL1-ASN-CARTONS.          RCKRP005
00293      MOVE RC004-CALC-CARTONS         TO DL1-CALC-CARTONS.         RCKRP005
00294                                                                   RCKRP005
00295                                                                   RCKRP005
00296  EJECT                                                            RCKRP005
00297 ******************************************************************RCKRP005
00298 *  READ THE ASN EXTRACT RECORDS.                                 *RCKRP005
00299 ******************************************************************RCKRP005
00300  7000-READ-RECEIVING-RPT.                                         RCKRP005
00301                                                                   RCKRP005
00302      MOVE '7000'                  TO PGM-LOCATION.                RCKRP005
00303      MOVE '7000-READ-RECEIVING-RPT   '                            RCKRP005
00304                                   TO PV-ABEND-PARAGRAPH.          RCKRP005
00305                                                                   RCKRP005
00306      READ ASN-RECEIVING-RPT-FILE                                  RCKRP005
00307        INTO RC004-ASN-RECEIVING-RECORD                            RCKRP005
00308          AT END                                                   RCKRP005
00309            SET PS-NO-MORE-RECV-RECS TO TRUE.                      RCKRP005
00310                                                                   RCKRP005
00311  EJECT                                                            RCKRP005
00312  8000-WRITE-REPORT-HEADERS.                                       RCKRP005
00313 ******************************************************************RCKRP005
00314 *  PRINT THE HEADING FOR THE FIRST PAGE OF AN ASN.  SUBSEQUENT   *RCKRP005
00315 *  PAGES FOR THE SAME ASN DO NOT REPEAT ALL OF THE HEADING INFO. *RCKRP005
00316 ******************************************************************RCKRP005
00317      IF RC004-DIST-CNTR NOT =  PV-PREV-DIST-CNTR                  RCKRP005
00318          WRITE RECV-REPORT-RECORD                                 RCKRP005
00319            FROM END-OF-REPORT-LINE                                RCKRP005
00320              AFTER ADVANCING 2 LINES                              RCKRP005
00321      END-IF.                                                      RCKRP005
00322                                                                   RCKRP005
00323      PERFORM 8200-WRITE-TOP-LINE.                                 RCKRP005
00324      MOVE RC004-DIST-CNTR            TO HL1-DC-NBR.               RCKRP005
00325      MOVE SPACES                     TO HL1-DC-NAME.              RCKRP005
00326      IF RC004-DIST-CNTR = HIGH-VALUES                             RCKRP005
00327          MOVE PC-UNKNOWN             TO HL1-DC-UNKNOWN            RCKRP005
00328      ELSE                                                         RCKRP005
00329          MOVE RC004-DIST-CNTR        TO HL1-DC-NBR                RCKRP005
00330      END-IF.                                                      RCKRP005
00331                                                                   RCKRP005
166410     IF PV-CKPT-JOB-NAME = 'RCK020  '                                     
00332          WRITE RECV-REPORT-RECORD                                 RCKRP005
00333            FROM HEADING-LINE-0-IMPORT                             RCKRP005
00334            AFTER ADVANCING 1 LINE                                 RCKRP005
           ELSE                                                                 
00332          WRITE RECV-REPORT-RECORD                                 RCKRP005
00333            FROM HEADING-LINE-0                                    RCKRP005
00334            AFTER ADVANCING 1 LINE                                 RCKRP005
           END-IF.                                                              
00335                                                                   RCKRP005
00336      WRITE RECV-REPORT-RECORD                                     RCKRP005
00337        FROM HEADING-LINE-1                                        RCKRP005
00338          AFTER ADVANCING 2 LINES.                                 RCKRP005
00339                                                                   RCKRP005
00340      MOVE PC-4                    TO PV-LINE-CNT.                 RCKRP005
00341                                                                   RCKRP005
00342      PERFORM 8300-WRITE-DETAIL-HEADERS.                           RCKRP005
00343                                                                   RCKRP005
00344                                                                   RCKRP005
00345  8200-WRITE-TOP-LINE.                                             RCKRP005
00346 ******************************************************************RCKRP005
00347 *     WRITES HEADER LINES 0 AND 1 WHEN MORE THAN ONE PAGE IS     *RCKRP005
00348 *     NEEDED FOR AN ASN NUMBER.                                  *RCKRP005
00349 ******************************************************************RCKRP005
00350      ADD 1                        TO PV-PAGE-CNT.                 RCKRP005
00351                                                                   RCKRP005
00352      MOVE PV-PAGE-CNT             TO DP132O-PAGE-NUMBER.          RCKRP005
00353                                                                   RCKRP005
00354      MOVE PC-REPORT-NBR-1        TO DP132O-REPORT-NUMBER.         RCKRP005
00355      WRITE RECV-REPORT-RECORD                                     RCKRP005
00356        FROM DP132O-STANDRD-HEADER-132-COLS                        RCKRP005
00357          AFTER ADVANCING PAGE.                                    RCKRP005
00358                                                                   RCKRP005
00359                                                                   RCKRP005
00360  8300-WRITE-DETAIL-HEADERS.                                       RCKRP005
00361 ******************************************************************RCKRP005
00362 *     WRITES THE DETAIL LINE HEADERS                             *RCKRP005
00363 ******************************************************************RCKRP005
00364      WRITE RECV-REPORT-RECORD                                     RCKRP005
00365        FROM DETAIL-HEADING-LINE-1                                 RCKRP005
00366          AFTER ADVANCING 2 LINES.                                 RCKRP005
00367                                                                   RCKRP005
00368      ADD PC-2                     TO PV-LINE-CNT.                 RCKRP005
00369                                                                   RCKRP005
00370  8400-WRITE-DETAIL-LINES.                                         RCKRP005
00371 ******************************************************************RCKRP005
00372 *     WRITES THE DETAIL LINE INFORMATION.                        *RCKRP005
00373 ******************************************************************RCKRP005
00374      IF RC004-DIST-CNTR NOT =  PV-PREV-DIST-CNTR OR               RCKRP005
00375         RC004-CARRIER-SCAC-CODE NOT = PV-PREV-SCAC                RCKRP005
00376          PERFORM 8000-WRITE-REPORT-HEADERS                        RCKRP005
00377      ELSE                                                         RCKRP005
00378          IF  PV-LINE-CNT > PC-60                                  RCKRP005
00379               PERFORM 8000-WRITE-REPORT-HEADERS                   RCKRP005
00380          END-IF                                                   RCKRP005
00381      END-IF.                                                      RCKRP005
00382                                                                   RCKRP005
00383      WRITE RECV-REPORT-RECORD                                     RCKRP005
00384        FROM DETAIL-LINE1                                          RCKRP005
00385          AFTER ADVANCING 2 LINES.                                 RCKRP005
00386                                                                   RCKRP005
00387      ADD PC-2                        TO PV-LINE-CNT.              RCKRP005
00388                                                                   RCKRP005
00389  8500-SAVE-PREV-RECORD.                                           RCKRP005
00390      MOVE RC004-DIST-CNTR            TO PV-PREV-DIST-CNTR.        RCKRP005
00391      MOVE RC004-CARRIER-SCAC-CODE    TO PV-PREV-SCAC.             RCKRP005
00392      MOVE RC004-BILL-OF-LADING-NBR   TO PV-PREV-BOL-NBR.          RCKRP005
00393      MOVE RC004-PRO-NBR              TO PV-PREV-PRO-NBR.          RCKRP005
00394      MOVE RC004-EDI-VEND-NAME        TO PV-PREV-EDI-VEND-NAME.    RCKRP005
00395      MOVE RC004-PO-NBR               TO PV-PREV-PO-NBR.           RCKRP005
00396      MOVE RC004-DATE-SHIPPED         TO PV-PREV-SHIP-DATE.        RCKRP005
00397 /                                                                 RCKRP005
