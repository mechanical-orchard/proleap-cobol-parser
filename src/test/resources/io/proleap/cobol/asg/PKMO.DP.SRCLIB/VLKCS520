000100******************************************************************04/11/95
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************   LV002
000400                                                                          
000500 PROGRAM-ID.                VLKCS520.                                     
000600 AUTHOR.                    MICHAEL WALTERS.                              
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              12-16-96.                                     
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  CICS PROGRAM - VLKCS520 - V520                                *        
001300*                                                                *        
001400*       VL SYSTEM TICKET OFFICE MAINTENANCE SCREEN               *        
001500*                                                                *        
001600*  THIS SCREEN WILL ADD/UPDATE TICKET OFFICES FOR THE TICKET     *        
001600*  SYSTEM.                                                       *        
001700******************************************************************        
263627*---------------------------------------------------------------*         
263627* CHANGED 11/05/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*---------------------------------------------------------------*         
CICS  * CHANGED 11/14/2022    BY JIM HUNTER      WR: CHG0278236                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *****************************************************************         
001800 ENVIRONMENT DIVISION.                                                    
001900                                                                          
002000 DATA DIVISION.                                                           
002100                                                                          
002200 WORKING-STORAGE SECTION.                                                 
002300                                                                          
002400 01  DK-DB2-KEYS.                                                         
002500     15  DK-TKT-OFF-ID                  PIC  X(06) VALUE SPACE.           
009500     05  DK-OPER-UNT-ID                 PIC S9(04)  COMP VALUE +0.        
009500     05  DK-FACILITY-ID                 PIC S9(04)  COMP VALUE +0.        
009500     05  DK-OPER-UNT-ID-OLD             PIC S9(04)  COMP VALUE +0.        
009500     05  DK-FACILITY-ID-OLD             PIC S9(04)  COMP VALUE +0.        
           05  DK-STORE-NMBR                  PIC X(04)   VALUE SPACES.         
003200                                                                          
003300 01  PC-PROGRAM-CONSTANTS.                                                
004700     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
004800                                                    'VL520A  '.           
004900     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
005000                                                    'VLKM520 '.           
005100     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
005200                                                    'VLKCS520'.           
005700     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
006700     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
006800                                                    COMP-3.               
006900     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
007000     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
007100     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
007200     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
007300     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
007400*                                                                         
007500     05  PC-TSYMSG-NUMBERS.                                               
007600         10  PC-TSYMSG-00004            PIC  9(05)  VALUE 00004.          
007600         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
007700         10  PC-TSYMSG-00007            PIC  9(05)  VALUE 00007.          
007800         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
007900         10  PC-TSYMSG-00034            PIC  9(05)  VALUE 00034.          
008000         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
007900         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
008100         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
008200         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
008300         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
008400         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
008500         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
008600         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
007900         10  PC-TSYMSG-00059            PIC  9(05)  VALUE 00059.          
008700         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
008800         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
008900         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
008900         10  PC-TSYMSG-00115            PIC  9(05)  VALUE 00115.          
008900         10  PC-TSYMSG-00116            PIC  9(05)  VALUE 00116.          
007700         10  PC-TSYMSG-00117            PIC  9(05)  VALUE 00117.          
009000         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
009000         10  PC-TSYMSG-00349            PIC  9(05)  VALUE 00349.          
009000         10  PC-TSYMSG-00815            PIC  9(05)  VALUE 00815.          
009100         10  PC-TSYMSG-01004            PIC  9(05)  VALUE 01004.          
009100         10  PC-TSYMSG-01339            PIC  9(05)  VALUE 01339.          
009200         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
009300*                                                                         
009400 01  PS-PROGRAM-SWITCHES.                                                 
009500     05  PS-END-OF-CURSOR-SW            PIC  X(01)  VALUE 'Y'.            
009600         88  PS-END-OF-CURSOR                       VALUE 'Y'.            
009700         88  PS-NOT-END-OF-CURSOR                   VALUE 'N'.            
009500     05  PS-TKT-OFFICE-IND              PIC  X(01)  VALUE 'Y'.            
009600         88  PS-TKT-OFFICE-INCLUDED                 VALUE 'Y'.            
009700         88  PS-TKT-OFFICE-NOT-INCLUDED             VALUE 'N'.            
009500     05  PS-SHIP-DEL-CURSOR-SW          PIC  X(01)  VALUE 'Y'.            
009600         88  PS-EOC-SHIP-DEL                        VALUE 'Y'.            
009700         88  PS-NOT-EOC-SHIP-DEL                    VALUE 'N'.            
009500     05  PS-SEL-RECORD-SW               PIC  X(01)  VALUE 'Y'.            
009600         88  PS-SEL-RECORD-FOUND                    VALUE 'Y'.            
009700         88  PS-SEL-RECORD-NOT-FOUND                VALUE 'N'.            
009800     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
009900         88  PS-NO-ERROR                            VALUE 'Y'.            
010000         88  PS-ERROR                               VALUE 'N'.            
010100     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
010200         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
010300         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
010400*                                                                         
010500 01  PV-PROGRAM-VARIABLES.                                                
012700                                                                          
012900     05  PV-DATE-ISO.                                                     
013000         10  PV-DATE-CC                 PIC  X(02).                       
013100         10  PV-DATE-YY                 PIC  X(02).                       
013200         10  PV-DATE-DASH1              PIC  X VALUE '-'.                 
013300         10  PV-DATE-MM                 PIC  X(02).                       
013400         10  PV-DATE-DASH2              PIC  X VALUE '-'.                 
013500         10  PV-DATE-DD                 PIC  X(02).                       
013600     05  PV-DATE-6BYTE.                                                   
013700         10  PV-DATE-YY                 PIC  X(02).                       
013800         10  PV-DATE-MM                 PIC  X(02).                       
013900         10  PV-DATE-DD                 PIC  X(02).                       
014000     05  PV-DATE-USA.                                                     
014100         10  PV-DATE-MM                 PIC  X(02).                       
014200         10  PV-DATE-SLASH1             PIC  X VALUE '/'.                 
014300         10  PV-DATE-DD                 PIC  X(02).                       
014400         10  PV-DATE-SLASH2             PIC  X VALUE '/'.                 
014500         10  PV-DATE-CC                 PIC  X(02).                       
014600         10  PV-DATE-YY                 PIC  X(02).                       
014700                                                                          
014800     05  PV-TIMESTAMP           PIC X(26).                                
014900     05  PV-TIMESTAMP-SPLIT.                                              
015000         10  PV-TMST-DATE       PIC X(10).                                
015000         10  FILLER             PIC X(04).                                
015100         10  PV-MINUTE          PIC X(02).                                
015200         10  FILLER             PIC X(02).                                
015300         10  PV-SECOND          PIC X(01).                                
015400         10  FILLER             PIC X(01).                                
015500         10  PV-NANOSEC-1-3     PIC X(03).                                
015600         10  PV-NANOSEC-4-6     PIC X(03).                                
015700                                                                          
015800     05  PV-RANDOM-KEY          PIC 9(09).                                
015900     05  FILLER REDEFINES PV-RANDOM-KEY.                                  
016000         10  PV-RND-NANOSEC-4-6 PIC X(03).                                
016100         10  PV-RND-NANOSEC-1-3 PIC X(03).                                
016200         10  PV-RND-MINUTE      PIC X(02).                                
016300         10  PV-RND-SECOND      PIC X(01).                                
016400                                                                          
014800     05  PV-DISPLAY-ABEND-MESSAGE       PIC  X(79) VALUE SPACES.          
014900     05  PV-DISPLAY-COMP1               PIC  9(09) VALUE ZERO.            
014900     05  PV-DISPLAY-COMP2               PIC  9(09) VALUE ZERO.            
015200     05  PV-WORK-DATE.                                                    
015300         10  PV-WORK-CC                 PIC XX.                           
015400         10  PV-WORK-YY                 PIC XX.                           
015500         10  FILLER                     PIC X.                            
015600         10  PV-DISPLAY-DATE.                                             
015700             15 PV-WORK-MM              PIC XX.                           
015800             15 FILLER                  PIC X.                            
015900             15 PV-WORK-DD              PIC XX.                           
016000     05  PV-STATUS-CODE                 PIC X.                            
016200     05  PV-READ-COUNT                  PIC S9(04)  VALUE +0              
016300                                                    COMP SYNC.            
016400     05  PV-SUB                         PIC S9(04)  VALUE +0              
016500                                                    COMP SYNC.            
016600     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
016700                                                    COMP SYNC.            
017000     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
017100                                                    COMP-3.               
017200     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
017300                                                    COMP-3.               
017400     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
017500                                                    COMP-3.               
017600     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
017700                                                    COMP-3.               
017800     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
017900                                                    COMP-3.               
018000     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
018100                                                    COMP-3.               
018200     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
018300                                                    COMP SYNC.            
018400     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
018500                                                    COMP SYNC.            
018600     05  PV-ITEM                        PIC S9(04)  VALUE +0              
018700                                                    COMP SYNC.            
018600     05  PV-SHIP-ITEM                   PIC S9(04)  VALUE +0              
018700                                                    COMP SYNC.            
018600     05  PV-COMM-ITEM                   PIC S9(04)  VALUE +0              
018700                                                    COMP SYNC.            
018600     05  PV-SEL-ITEM                    PIC S9(04)  VALUE +0              
018700                                                    COMP SYNC.            
019000     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
019100                                                    COMP SYNC.            
021000     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
021100                                                    COMP SYNC.            
021200     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
021300                                                    COMP SYNC.            
023500                                                                          
023600     EJECT                                                                
023700*---------------------------------------------------------------*         
023800*    MAP LAYOUT                                                 *         
023900*---------------------------------------------------------------*         
024000                                                                          
024100     COPY VLKM520.                                                        
024200*                                                                         
025800                                                                          
025900     EJECT                                                                
026000*---------------------------------------------------------------*         
026100*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
026200*---------------------------------------------------------------*         
026300                                                                          
026400     COPY DPWS015.                                                        
026500                                                                          
026600*---------------------------------------------------------------*         
026700*    FUNCTION KEYS COPYBOOK.                                    *         
026800*---------------------------------------------------------------*         
026900                                                                          
027000     COPY DPWS016.                                                        
027100                                                                          
027200*---------------------------------------------------------------*         
027300*    NUMERIC EDIT LAYOUT.                                       *         
027400*---------------------------------------------------------------*         
027500                                                                          
027600     COPY DPWS010I.                                                       
                                                                                
023300*----------------------------------------------------------------*        
023400*    COPYBOOK FOR LOCATION CALL MODULE                                    
023500*-----------------------------------------------------------------        
023600     COPY XLWS071.                                                        
023700                                                                          
027200*---------------------------------------------------------------*         
027300*    WORKING STORAGE LAYOUT FOR THE TS QUEUE (FROM THE TICKET   *         
027300*    FORMAT MAINTENANCE SCREEN                                  *         
027400*---------------------------------------------------------------*         
027500                                                                          
027600     COPY VLWS014.                                                        
027700 EJECT                                                                    
028400*---------------------------------------------------------------*         
028500*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
028600*---------------------------------------------------------------*         
028700                                                                          
028800     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
028900 EJECT                                                                    
029000*---------------------------------------------------------------*         
029100*    STANDARD COMMAREA.                                         *         
029200*---------------------------------------------------------------*         
029300                                                                          
029400     COPY DPWS020.                                                        
029500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
029600                                                                          
029700*---------------------------------------------------------------*         
029800* INTER-APPLICATION COMMAREA.                                   *         
029900*   VLWS013 - IS THE INPUT LAYOUT WHEN COMING FROM THE TICKET   *         
030000*             FORMAT MAINTENANCE SCREEN.                        *         
030300*---------------------------------------------------------------*         
030400                                                                          
030500         10  INTER-APPL-COMM-AREA       PIC X(200).                       
030600     COPY VLWS013.                                                        
030700*---------------------------------------------------------------*         
030800*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
030900*---------------------------------------------------------------*         
031000         10  ASC-SPECIFIC-COMMAREA.                                       
031100             15  ASC-SCREEN-FIELDS.                                       
032800                 20  ASC-TKT-OFF-ID                  PIC  X(06).          
030000                 20  ASC-OPER-UNT-ID                 PIC X(04).   RCKCS2  
030100                 20  ASC-OPER-UNT-ID-N                                RCKC
030200                     REDEFINES ASC-OPER-UNT-ID       PIC 9(04).   RCKCS2  
030300                 20  ASC-FACILITY-ID                 PIC X(04).   RCKCS2  
030400                 20  ASC-FACILITY-ID-N                                RCKC
030500                     REDEFINES ASC-FACILITY-ID       PIC 9(04).   RCKCS2  
                       20  ASC-TKT-OFF-ADR-ID              PIC  X(08).          
031100             15  ASC-SCREEN-FIELDS-OLD.                                   
032800                 20  ASC-TKT-OFF-ID-OLD              PIC  X(06).          
030000                 20  ASC-OPER-UNT-ID-OLD             PIC X(04).   RCKCS2  
030100                 20  ASC-OPER-UNT-ID-OLD-N                            RCKC
030200                     REDEFINES ASC-OPER-UNT-ID-OLD   PIC 9(04).   RCKCS2  
030300                 20  ASC-FACILITY-ID-OLD             PIC X(04).   RCKCS2  
030400                 20  ASC-FACILITY-ID-OLD-N                            RCKC
030500                     REDEFINES ASC-FACILITY-ID-OLD   PIC 9(04).   RCKCS2  
                       20  ASC-TKT-OFF-ADR-ID-OLD          PIC  X(08).          
                   20  ASC-FACILITY-NAME                   PIC  X(10).          
040800*            15  ASC-CURRENT-DATE                    PIC  X(10).          
00142              15  ASC-SEL-SUB                         PIC S9(04)      CL*41
00143                                                          COMP.       CL*41
00144              15  ASC-SEL-ITEM                        PIC S9(04)      CL*41
00145                                                          COMP.       CL*41
041500             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
041600                                                          COMP-3.         
041700             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
041800                                                          COMP-3.         
041900             15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(09)           
041800                                                          COMP-3.         
043700             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
043800                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
043900                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
044500             15  ASC-SEL-QUEUE-NAME                  PIC  X(08).          
013800             15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
014000                 88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
014000                 88  ASC-DONT-USE-SELECTION-QUEUE      VALUE 'N'.         
045700             15  ASC-ACTION-COMPLETE-SW              PIC X(01).           
044900                 88  ASC-ACTION-PENDING              VALUE ' '.           
044900                 88  ASC-DELETE-COMPLETE             VALUE 'D'.           
044900                 88  ASC-ADD-COMPLETE                VALUE 'A'.           
043400             15  ASC-FORMAT-INDICATOR                PIC X(01).           
                       88  ASC-TICKET-LIST-FORMAT      VALUE  '1' '2'           
                                                              '3'.              
                       88  ASC-CREATE-TKT-OFF-FORMAT   VALUE  '1'.              
                       88  ASC-UPDATE-TKT-OFF-FORMAT   VALUE  '2'.              
                       88  ASC-DELETE-TKT-OFF-FORMAT   VALUE  '3'.              
045900     EJECT                                                                
046000*---------------------------------------------------------------*         
046100*    ABEND PROCESSING COPYBOOK.                                 *         
046200*---------------------------------------------------------------*         
046300                                                                          
046400     COPY DPWS013.                                                        
046500                                                                          
046600 EJECT                                                                    
046700*---------------------------------------------------------------*         
046800*    DB2 AREA FOR THE TICKET FORMAT TABLE                       *         
046900*---------------------------------------------------------------*         
047000     EXEC SQL                                                             
047100          INCLUDE TTKTFMT                                                 
047200     END-EXEC.                                                            
046700*---------------------------------------------------------------*         
046800*    DB2 AREA FOR THE PRINT STOCK TABLE                         *         
046900*---------------------------------------------------------------*         
047000     EXEC SQL                                                             
047100          INCLUDE TPRTSTK                                                 
047200     END-EXEC.                                                            
047300*---------------------------------------------------------------*         
047400*    DB2 AREA FOR COMMUNICATIONS                                *         
047500*---------------------------------------------------------------*         
047600     EXEC SQL                                                             
047700          INCLUDE SQLCA                                                   
047800     END-EXEC.                                                            
047900                                                                          
052100                                                                          
052200*---------------------------------------------------------------*         
052300*    TICKET FORMAT CURSOR TO FETCH ALL THE POSSIBLE TICKET      *         
052300*    FORMATS FOR INSERTING NEW TICKET OFFICES.                  *         
052400*---------------------------------------------------------------*         
052500                                                                          
052600     EXEC SQL                                                             
052700          DECLARE TKT_FORMAT_CURSOR CURSOR FOR                          86
                   SELECT                                                       
                         TKT_FMT_ID                                             
                        ,TKT_OFF_DESC                                           
                        ,TKT_FMT_DESC                                           
                        ,TYP_CDE                                                
                        ,OPT_BLK_TAGS_IND                                       
                        ,PRT_SIZE_IND                                           
                        ,PRT_COLOR_IND                                          
                        ,PRT_GP_PR_IND                                          
                        ,CMPANON_OP_UNT_ID                                      
                        ,CMPANON_FCLTY_ID                                       
                        ,CMPANON_TKT_ID                                         
                        ,PAPER_TYP_ID                                           
                        ,OPT_BLK_LBLS_IND                                       
                        ,STKR_NBR_REQ_IND                                       
                        ,TKT_FMT_NBR                                            
                   FROM TTKTFMT                                                 
                   GROUP BY                                                     
                         TKT_FMT_ID                                             
                        ,TKT_OFF_DESC                                           
                        ,TKT_FMT_DESC                                           
                        ,TYP_CDE                                                
                        ,OPT_BLK_TAGS_IND                                       
                        ,PRT_SIZE_IND                                           
                        ,PRT_COLOR_IND                                          
                        ,PRT_GP_PR_IND                                          
                        ,CMPANON_OP_UNT_ID                                      
                        ,CMPANON_FCLTY_ID                                       
                        ,CMPANON_TKT_ID                                         
                        ,PAPER_TYP_ID                                           
                        ,OPT_BLK_LBLS_IND                                       
                        ,STKR_NBR_REQ_IND                                       
                        ,TKT_FMT_NBR                                            
056500     END-EXEC.                                                            
056600                                                                          
047900                                                                          
056700 EJECT                                                                    
056800 LINKAGE SECTION.                                                         
056900                                                                          
057000 01  DFHCOMMAREA.                                                         
057100     05  FILLER                         OCCURS  1 TO 4072 TIMES           
057200                                        DEPENDING ON EIBCALEN.            
057300         10  FILLER                     PIC X.                            
057400                                                                          
057500 EJECT                                                                    
057600 PROCEDURE DIVISION.                                                      
057700******************************************************************        
057800**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
057900**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
058000**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
058100******************************************************************        
058200 0000-MAIN-MODULE.                                                        
058300     INITIALIZE DP030-CICS-API-FIELDS.                                    
058400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
058500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
058600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
058700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
058800     MOVE LENGTH OF VL520AI        TO DP030-MAP-LENGTH (1).               
058900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
059000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
059100*                                                                         
059200     PERFORM 1000-CONTROL-PROCESSING                                      
059300*                                                                         
059400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
059500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
059600                                                                          
059700******************************************************************        
059800**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
059900**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
060000******************************************************************        
060100 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
060300                                    DFHCOMMAREA                           
060400                                    DP030-CICS-API-FIELDS                 
060500                                    DP020-STANDARD-COMMAREA               
060600                                    VL520AI.                              
060700*                                                                         
060800     IF  DP030-RC-CALL-SUCCESSFUL                                         
060900         CONTINUE                                                         
061000     ELSE                                                                 
061100         SET DP013-NO-ROLLBACK                                            
061200             DP013-XCTL-DISPLAY-RESTART                                   
061300             DP013-CICS-ABEND      TO TRUE                                
061400         MOVE '0001-CALL-CICS-ARCH-API'                                   
061500                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
061800         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
061900         PERFORM DP013-0000-PROCESS-ABEND                                 
062000     END-IF.                                                              
062100*----------------------------------------------------------------*        
062200* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
062300* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
062400*                                                                *        
062900*----------------------------------------------------------------*        
063000                                                                          
063100 1000-CONTROL-PROCESSING.                                                 
063200                                                                          
063300     EVALUATE TRUE                                                        
063400         WHEN DP020-NEXT-ACT-INITIAL                                      
063500             INITIALIZE ASC-SPECIFIC-COMMAREA                             
064400             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
064500***-----$TEMPLATE NOTE$-------------------------------------------        
064600*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
064700*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
064800*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
064900***---------------------------------------------------------------        
065000             IF  PS-COMMAREA-NOT-VALID                                    
065100***              MOVE PC-TSYMSG-99901                                     
065200***                                TO DP020-MSG-NUMBER                    
065300***              SET DP020-MSG-FATAL                                      
065400***                                TO TRUE                                
065500                 SET DP020-NEXT-ACT-APPL-ERROR                            
065600                                   TO TRUE                                
065700             ELSE                                                         
065800                 PERFORM 4000-BUILD-INITIAL-PANEL                         
037200                 PERFORM 3000-EDIT-DATA-IN-COMMAREA                       
037200                 PERFORM 3200-RESET-ATTRIBUTES                            
065900             END-IF                                                       
066000                                                                          
066100         WHEN DP020-NEXT-ACT-READ-MAP                                     
066400             PERFORM 2000-PROCESS-PANEL                                   
066500                                                                          
066600         WHEN DP020-NEXT-ACT-RETURN                                       
067000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
067100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
067200                                                                          
067300         WHEN OTHER                                                       
067400             SET DP013-LOGIC-ABEND TO TRUE                                
067500             SET DP013-NO-ROLLBACK TO TRUE                                
067600             MOVE '1000-CONTROL-PROCESSING'                               
067700                                   TO DP013-PARAGRAPH                     
067800             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
067900                                   TO DP013-MESSAGE-TEXT(1)               
068000             PERFORM DP013-0000-PROCESS-ABEND                             
068100     END-EVALUATE.                                                        
068200                                                                          
068300 EJECT                                                                    
068400*----------------------------------------------------------------*        
068500* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
068600* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
068700*----------------------------------------------------------------*        
068800                                                                          
068900 1100-PROCESS-INTER-APPL-COMM.                                            
069100     SET PS-COMMAREA-VALID         TO TRUE.                               
069000                                                                          
           MOVE DP020-INT-APPL-FORMAT-IND                                       
043400                                   TO ASC-FORMAT-INDICATOR.               
           EVALUATE TRUE                                                        
           WHEN ASC-UPDATE-TKT-OFF-FORMAT                                       
           WHEN ASC-DELETE-TKT-OFF-FORMAT                                       
014000         SET ASC-USE-SELECTION-QUEUE   TO TRUE                            
               MOVE VL013-SEL-QUEUE-NAME     TO ASC-SEL-QUEUE-NAME              
069600         MOVE VL013-NUMBER-OF-TS-ITEMS TO ASC-NUMBER-OF-TS-ITEMS          
00420          PERFORM 1120-PREPARE-LIST-ENTRY                                *4
                                                                                
           WHEN ASC-CREATE-TKT-OFF-FORMAT                                       
014000         SET ASC-DONT-USE-SELECTION-QUEUE                                 
                                             TO TRUE                            
00566          MOVE SPACES                   TO ASC-TKT-OFF-ID          DPKCS036
00566                                           ASC-OPER-UNT-ID         DPKCS036
00566                                           ASC-FACILITY-ID         DPKCS036
00566                                           ASC-TKT-OFF-ADR-ID      DPKCS036
               MOVE ASC-SCREEN-FIELDS        TO ASC-SCREEN-FIELDS-OLD           
           WHEN OTHER                                                           
069100         SET PS-COMMAREA-NOT-VALID     TO TRUE                            
           END-EVALUATE.                                                        
00577                                                                   DPKCS036
00454                                                                      CL**8
00455 *----------------------------------------------------------------*   CL*61
00456 *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *   CL*61
00457 *----------------------------------------------------------------*   CL*61
00458                                                                      CL*61
00459  1120-PREPARE-LIST-ENTRY.                                            CL**5
00460                                                                      CL**8
00461      MOVE VL014-MAX-ITEMS TO ASC-SEL-SUB.                            CL**5
00462      MOVE ZERO            TO ASC-SEL-ITEM.                           CL**5
00463      PERFORM 2140-GET-NEXT-TICKET-OFFICE.                            CL*38
070100 EJECT                                                                    
070200*----------------------------------------------------------------*        
070300* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
070400* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
070500* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
070600* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
070700* FUNCTION KEYS CAN GET THIS FAR.                                *        
070800*----------------------------------------------------------------*        
070900                                                                          
071000 2000-PROCESS-PANEL.                                                      
071100     EVALUATE TRUE                                                        
071200         WHEN DP020-SRC-AID = DP016-CLEAR                                 
071400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
071500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
071600                                                                          
071700         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
071900             PERFORM 4000-BUILD-INITIAL-PANEL                             
037200             PERFORM 3200-RESET-ATTRIBUTES                                
                   IF ASC-DELETE-TKT-OFF-FORMAT                                 
00746                  MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER            CL**5
00747                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL**5
                   END-IF                                                       
                                                                                
00742              IF ASC-DELETE-COMPLETE                                  CL**5
00746                  MOVE PC-TSYMSG-00116 TO DP020-MSG-NUMBER            CL**5
00747                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL**5
                   END-IF                                                       
036700                                                                          
036700                                                                          
036800         WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
036900             IF  ASC-USE-SELECTION-QUEUE                                  
037000                 PERFORM 2140-GET-NEXT-TICKET-OFFICE                      
037100                 PERFORM 4000-BUILD-INITIAL-PANEL                         
037200                 PERFORM 3200-RESET-ATTRIBUTES                            
037300             ELSE                                                         
037400*                --- BOTTOM OF LIST ---                                   
037500                 MOVE PC-TSYMSG-00070                                     
037600                                   TO DP020-MSG-NUMBER                    
037700                 SET DP020-MSG-INFORMATIONAL                              
037800                                   TO TRUE                                
037900             END-IF                                                       
038000                                                                          
038100         WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
038200             IF  ASC-USE-SELECTION-QUEUE                                  
038300                 PERFORM 2150-GET-PREV-TICKET-OFFICE                      
038400                 PERFORM 4000-BUILD-INITIAL-PANEL                         
038500                 PERFORM 3200-RESET-ATTRIBUTES                            
038600             ELSE                                                         
038700*                --- TOP OF LIST ---                                      
038800                 MOVE PC-TSYMSG-00069                                     
038900                                   TO DP020-MSG-NUMBER                    
039000                 SET DP020-MSG-INFORMATIONAL                              
039100                                   TO TRUE                                
039200             END-IF                                                       
039300                                                                          
071600                                                                          
072100         WHEN OTHER                                                       
072200             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
072300             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
072400             IF  PS-NO-ERROR                                              
072500                 SET DP030-CONTINUE-GO                                    
072600                                 TO TRUE                                  
072700                 PERFORM 2100-CHECK-FUNCTION-KEY                          
072800             ELSE                                                         
072900                 SET DP030-OVERRIDE-GO                                    
073000                                 TO TRUE                                  
073100             END-IF                                                       
073200     END-EVALUATE.                                                        
073300 EJECT                                                                    
076100*----------------------------------------------------------------*        
076200* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
076300* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
076400* CICS ARCHITECTURE API.                                         *        
076500*----------------------------------------------------------------*        
076600 2100-CHECK-FUNCTION-KEY.                                                 
076700     EVALUATE TRUE                                                        
076800                                                                          
      *        WHEN "ADD" KEY IS PRESSED                                        
077900         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                  PERFORM 7000-ADD-TICKET-FORMAT                                
                  MOVE PC-TSYMSG-00034        TO DP020-MSG-NUMBER               
                  SET DP020-MSG-INFORMATIONAL TO TRUE                           
                                                                                
      *        WHEN "UPDATE" KEY IS PRESSED                                     
077900         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
                  PERFORM 7200-UPDATE-TICKET-OFFICE                             
                  MOVE PC-TSYMSG-00059        TO DP020-MSG-NUMBER               
                  SET DP020-MSG-INFORMATIONAL TO TRUE                           
00737             PERFORM 9100-READ-IN-SEL-QUEUE                           CL*49
                  MOVE ASC-TKT-OFF-ADR-ID                                       
                      TO VL014-TKT-OFF-ADR-ID (ASC-SEL-SUB)                     
00742             SET VL014-UPD (ASC-SEL-SUB) TO TRUE                      CL**5
00743             PERFORM 9101-REWRITE-SEL-QUEUE                                
                                                                                
      *        WHEN "DELETE" KEY IS PRESSED                                     
077900         WHEN DP020-FK-CONFIRM(DP020-SRC-AID)                             
                  IF ASC-DELETE-COMPLETE                                        
                      CONTINUE                                                  
                  ELSE                                                          
                      PERFORM 7100-DELETE-TICKET-OFFICE                         
00737                 PERFORM 9100-READ-IN-SEL-QUEUE                       CL*49
00742                 SET VL014-DEL (ASC-SEL-SUB) TO TRUE                  CL**5
00743                 PERFORM 9101-REWRITE-SEL-QUEUE                            
                  END-IF                                                        
                  MOVE PC-TSYMSG-00115        TO DP020-MSG-NUMBER               
                  SET DP020-MSG-INFORMATIONAL                                   
                      ASC-DELETE-COMPLETE     TO TRUE                           
                                                                                
                                                                                
077900         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
078000             MOVE +1               TO PV-TOP-ITEM                         
078100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
078200                                                                          
079000                                                                          
079100         WHEN DP020-SRC-AID = DP016-ENTER                                 
079300             IF  PS-ERROR                                                 
079400                 CONTINUE                                                 
079500             ELSE                                                         
079600                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
079700             END-IF                                                       
079800                                                                          
079900         WHEN OTHER                                                       
080000             SET DP013-NO-ROLLBACK                                        
080100                 DP013-XCTL-DISPLAY-RESTART                               
080200                 DP013-CICS-ABEND  TO TRUE                                
080300             MOVE '2100-CHECK-FUNCTION-KEY'                               
080400                                   TO DP013-PARAGRAPH                     
080500             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
080600                                   TO DP013-MESSAGE-TEXT (1)              
080700             PERFORM DP013-0000-PROCESS-ABEND                             
080800     END-EVALUATE.                                                        
080900*                                                                         
00724 *----------------------------------------------------------------*   CL*61
00725 *    GET THE NEXT TICKET FORMAT IF A LIST OF TICKET FORMATS IS   *   CL*61
00726 *    BEING PROCESSED                                             *   CL*61
00727 *----------------------------------------------------------------*   CL*61
00728                                                                      CL*61
00729  2140-GET-NEXT-TICKET-OFFICE.                                        CL**5
00730                                                                      CL**8
00731      ADD +1                       TO ASC-SEL-SUB.                    CL**5
00732      IF  ASC-SEL-SUB > VL014-MAX-ITEMS                               CL**5
00733          ADD +1                   TO ASC-SEL-ITEM                    CL**5
00734          MOVE +1                  TO ASC-SEL-SUB                     CL**5
00735      END-IF.                                                         CL**5
00736      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                   CL**5
00737          PERFORM 9100-READ-IN-SEL-QUEUE                              CL*49
00738          IF  VL014-ITEM (ASC-SEL-SUB) > ZERO                         CL**5
00739              MOVE VL014-TKT-OFF-ID (ASC-SEL-SUB)                     CL**5
00566                                        TO ASC-TKT-OFF-ID          DPKCS036
00739              MOVE VL014-OPER-UNT-ID (ASC-SEL-SUB)                    CL**5
00566                                        TO ASC-OPER-UNT-ID         DPKCS036
00739              MOVE VL014-FACILITY-ID (ASC-SEL-SUB)                    CL**5
00566                                        TO ASC-FACILITY-ID         DPKCS036
00739              MOVE VL014-TKT-OFF-ADR-ID (ASC-SEL-SUB)                 CL**5
00566                                        TO ASC-TKT-OFF-ADR-ID      DPKCS036
                   MOVE ASC-SCREEN-FIELDS    TO ASC-SCREEN-FIELDS-OLD           
                                                                                
                   IF ASC-DELETE-TKT-OFF-FORMAT                                 
00746                  MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER            CL**5
00747                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL**5
                   END-IF                                                       
                                                                                
                   IF VL014-ACTION-SW (ASC-SEL-SUB) = SPACES                    
00742                  SET VL014-INQ (ASC-SEL-SUB) TO TRUE                 CL**5
00743                  PERFORM 9101-REWRITE-SEL-QUEUE                      CL*49
                       SET ASC-ACTION-PENDING      TO TRUE                      
                   ELSE                                                         
00742                  IF VL014-DEL (ASC-SEL-SUB)                          CL**5
                           SET ASC-DELETE-COMPLETE TO TRUE                      
00746                      MOVE PC-TSYMSG-00116 TO DP020-MSG-NUMBER        CL**5
00747                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL**5
                       END-IF                                              CL**5
                   END-IF                                                       
00742              SET VL014-INQ (ASC-SEL-SUB) TO TRUE                     CL**5
00743              PERFORM 9101-REWRITE-SEL-QUEUE                          CL*49
00744          ELSE                                                        CL**5
00745              SUBTRACT +1 FROM ASC-SEL-SUB                            CL**5
00746              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                CL**5
00747              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL**5
00749          END-IF                                                      CL**5
00750      ELSE                                                            CL**5
00751          SUBTRACT +1 FROM ASC-SEL-ITEM                               CL**5
00752          MOVE VL014-MAX-ITEMS     TO ASC-SEL-SUB                     CL**5
00753          MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                CL**5
00754          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL**5
00756      END-IF.                                                         CL**5
00757                                                                      CL**8
046400******************************************************************        
046500** GET THE TICKET FORMAT (KEY) OF THE PREVIOUS SELECTED         **        
046600** OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.          **        
046700******************************************************************        
046800 2150-GET-PREV-TICKET-OFFICE.                                             
046900     SUBTRACT +1 FROM ASC-SEL-SUB.                                        
047000     IF  ASC-SEL-SUB < +1                                                 
047100         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
047200         MOVE VL014-MAX-ITEMS            TO ASC-SEL-SUB                   
047300     END-IF.                                                              
047400*                                                                         
047500     IF  ASC-SEL-ITEM > ZERO                                              
047600         PERFORM 9100-READ-IN-SEL-QUEUE                                   
00739          MOVE VL014-TKT-OFF-ID (ASC-SEL-SUB)                         CL**5
00566                                          TO ASC-TKT-OFF-ID        DPKCS036
00739          MOVE VL014-OPER-UNT-ID (ASC-SEL-SUB)                        CL**5
00566                                          TO ASC-OPER-UNT-ID       DPKCS036
00739          MOVE VL014-FACILITY-ID (ASC-SEL-SUB)                        CL**5
00566                                          TO ASC-FACILITY-ID       DPKCS036
00739          MOVE VL014-TKT-OFF-ADR-ID (ASC-SEL-SUB)                     CL**5
00566                                          TO ASC-TKT-OFF-ADR-ID    DPKCS036
               MOVE ASC-SCREEN-FIELDS          TO ASC-SCREEN-FIELDS-OLD         
                                                                                
               IF ASC-DELETE-TKT-OFF-FORMAT                                     
00746              MOVE PC-TSYMSG-00004        TO DP020-MSG-NUMBER         CL**5
00747              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL**5
               END-IF                                                           
                                                                                
00742          IF VL014-DEL (ASC-SEL-SUB)                                  CL**5
                   SET ASC-DELETE-COMPLETE     TO TRUE                          
00746              MOVE PC-TSYMSG-00116        TO DP020-MSG-NUMBER         CL**5
00747              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL**5
               ELSE                                                             
                   SET ASC-ACTION-PENDING      TO TRUE                          
               END-IF                                                           
047900     ELSE                                                                 
048000         MOVE +1                   TO ASC-SEL-ITEM                        
048100                                      ASC-SEL-SUB                         
048200*        --- TOP OF LIST ---                                              
048300         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
048400         SET DP020-MSG-INFORMATIONAL                                      
048500                                   TO TRUE                                
048600     END-IF.                                                              
                                                                                
094100 EJECT                                                                    
094200******************************************************************        
094300** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
094400** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
094500******************************************************************        
094600 2200-MOVE-SCREEN-TO-COMMAREA.                                            
094700                                                                          
095300     IF  AOPERIDL > ZERO                                                  
095400         MOVE AOPERIDI         TO ASC-OPER-UNT-ID                         
095500     ELSE                                                                 
095600         IF  AOPERIDF = DP015-ERASE-EOF                                   
095700             INITIALIZE ASC-OPER-UNT-ID                                   
095800         END-IF                                                           
095900     END-IF.                                                              
096000*                                                                         
095300     IF  AFCLTYL > ZERO                                                   
095400         MOVE AFCLTYI         TO ASC-FACILITY-ID                          
095500     ELSE                                                                 
095600         IF  AFCLTYF = DP015-ERASE-EOF                                    
095700             INITIALIZE ASC-FACILITY-ID                                   
095800         END-IF                                                           
095900     END-IF.                                                              
096000*                                                                         
096100     IF  ATKTOFFL > ZERO                                                  
096200         MOVE ATKTOFFI         TO ASC-TKT-OFF-ID                          
096300     ELSE                                                                 
096400         IF  ATKTOFFF = DP015-ERASE-EOF                                   
096500             INITIALIZE ASC-TKT-OFF-ID                                    
096600         END-IF                                                           
096700     END-IF.                                                              
096000*                                                                         
096100     IF  ADESTL > ZERO                                                    
096200         MOVE ADESTI           TO ASC-TKT-OFF-ADR-ID                      
096300     ELSE                                                                 
096400         IF  ADESTF = DP015-ERASE-EOF                                     
096500             INITIALIZE ASC-TKT-OFF-ADR-ID                                
096600         END-IF                                                           
096700     END-IF.                                                              
100800******************************************************************        
100900** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
101000** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
101100******************************************************************        
101200 3000-EDIT-DATA-IN-COMMAREA.                                              
101300     SET PS-NO-ERROR                     TO TRUE.                         
101400     PERFORM 3200-RESET-ATTRIBUTES.                                       
101500                                                                          
                                                                                
094900     IF  ASC-OPER-UNT-ID  > SPACE                                         
095000         MOVE ASC-OPER-UNT-ID  TO  DP010I-UNEDITED-FIELD                  
095100         MOVE LENGTH OF ASC-OPER-UNT-ID                                   
095200             TO DP010I-MAXIMUM-DIGITS                                     
095300         MOVE ZERO  TO  DP010I-MAXIMUM-DECIMALS                           
095400         SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
095600         IF  DP010I-ERROR-DETECTED                                        
095700             SET PS-ERROR                                                 
095800                 DP020-MSG-FATAL   TO TRUE                                
095900             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
096000             MOVE DP015-UNP-ALP-BRT-OFF                                   
096100                                   TO AOPERIDA                            
096200             MOVE DP015-RED        TO AOPERIDC                            
096300             MOVE DP015-REVERSE    TO AOPERIDH                            
096400             MOVE -1               TO AOPERIDL                            
096500             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
096600         ELSE                                                             
096700             MOVE DP010I-NUMERIC-FIELD                                    
096800                                   TO ASC-OPER-UNT-ID-N                   
096900                                      DK-OPER-UNT-ID                      
097000                                      AOPERIDO                            
097100             MOVE ASC-OPER-UNT-ID  TO DK-STORE-NMBR                       
097200             PERFORM 3400-VALIDATE-OPER-UNT-ID                            
                   IF XL071-NO-ERRORS AND XL071-LOC-TYP-CDE = 'DC'              
                       MOVE XL071-LOC-10-ABBR-NM                                
                                             TO ASC-FACILITY-NAME               
                   END-IF                                                       
097300             IF  XL071-SQLCODE-N = +100                                   
097400                 SET DP020-MSG-FATAL                                      
097500                     PS-ERROR          TO TRUE                            
097600                MOVE PC-TSYMSG-01004  TO DP020-MSG-NUMBER                 
097700                 MOVE DP015-UNP-ALP-BRT-OFF                               
097800                                       TO AOPERIDA                        
097900                 MOVE DP015-RED        TO AOPERIDC                        
098000                 MOVE DP015-REVERSE    TO AOPERIDH                        
098100                 MOVE -1               TO AOPERIDL                        
098200                 MOVE SPACES           TO ASC-FACILITY-NAME               
098300                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
098400             END-IF                                                       
098500         END-IF                                                           
098600     ELSE                                                                 
098700         SET PS-ERROR                                                     
098800             DP020-MSG-FATAL   TO TRUE                                    
098900         MOVE PC-TSYMSG-00815  TO DP020-MSG-NUMBER                        
099000         MOVE DP015-UNP-ALP-BRT-OFF                                       
099100                               TO AOPERIDA                                
099200         MOVE DP015-RED        TO AOPERIDC                                
099300         MOVE DP015-REVERSE    TO AOPERIDH                                
099400         MOVE -1               TO AOPERIDL                                
099500         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
099600     END-IF.                                                              
099700                                                                          
099800     IF  ASC-FACILITY-ID  > SPACE                                         
099900         MOVE ASC-FACILITY-ID  TO  DP010I-UNEDITED-FIELD                  
100000         MOVE LENGTH OF ASC-FACILITY-ID                                   
100100             TO DP010I-MAXIMUM-DIGITS                                     
100200         MOVE ZERO  TO  DP010I-MAXIMUM-DECIMALS                           
100300         SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
100500         IF  DP010I-ERROR-DETECTED                                        
100600             SET PS-ERROR                                                 
100700                 DP020-MSG-FATAL   TO TRUE                                
100800             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
100900             MOVE DP015-UNP-ALP-BRT-OFF                                   
101000                                   TO AFCLTYA                             
101100             MOVE DP015-RED        TO AFCLTYC                             
101200             MOVE DP015-REVERSE    TO AFCLTYH                             
101300             MOVE -1               TO AFCLTYL                             
101400             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
101500         ELSE                                                             
101600             MOVE DP010I-NUMERIC-FIELD                                    
101700                                   TO ASC-FACILITY-ID-N                   
101800                                      DK-FACILITY-ID                      
101900                                      AFCLTYO                             
                                                                                
102100             IF  ASC-FACILITY-ID-N NOT = 1                                
102200                 SET DP020-MSG-FATAL                                      
102300                     PS-ERROR          TO TRUE                            
102400                 MOVE PC-TSYMSG-01004  TO                                 
102500                                       DP020-MSG-NUMBER                   
102600                 MOVE DP015-UNP-ALP-BRT-OFF                               
102700                                       TO AFCLTYA                         
102800                 MOVE DP015-RED        TO AFCLTYC                         
102900                 MOVE DP015-REVERSE    TO AFCLTYH                         
103000                 MOVE -1               TO AFCLTYL                         
103100                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
103200             END-IF                                                       
103300         END-IF                                                           
103400     ELSE                                                                 
103500         SET PS-ERROR                                                     
103600             DP020-MSG-FATAL   TO TRUE                                    
103700         MOVE PC-TSYMSG-00815  TO DP020-MSG-NUMBER                        
103800         MOVE DP015-UNP-ALP-BRT-OFF                                       
103900                               TO AFCLTYA                                 
104000         MOVE DP015-RED        TO AFCLTYC                                 
104100         MOVE DP015-REVERSE    TO AFCLTYH                                 
104200         MOVE -1               TO AFCLTYL                                 
104300         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
104400     END-IF.                                                              
177703                                                                          
176900     IF ASC-TKT-OFF-ADR-ID = SPACES                                       
177000         MOVE PC-TSYMSG-00007            TO DP020-MSG-NUMBER              
177100         MOVE -1                         TO ADESTL                        
177300         MOVE DP015-REVERSE              TO ADESTH                        
177400         MOVE DP015-RED                  TO ADESTC                        
177600         MOVE DP015-UNP-ALP-BRT-OFF      TO ADESTA                        
177200         SET DP030-SET-CURSOR-APPL-1                                      
177700             DP020-MSG-FATAL                                              
177701             PS-ERROR                    TO TRUE                          
177702     END-IF.                                                              
                                                                                
176900     IF ASC-TKT-OFF-ID = SPACES                                           
177000         MOVE PC-TSYMSG-00007            TO DP020-MSG-NUMBER              
177100         MOVE -1                         TO ATKTOFFL                      
177300         MOVE DP015-REVERSE              TO ATKTOFFH                      
177400         MOVE DP015-RED                  TO ATKTOFFC                      
177600         MOVE DP015-UNP-ALP-BRT-OFF      TO ATKTOFFA                      
177200         SET DP030-SET-CURSOR-APPL-1                                      
177700             DP020-MSG-FATAL                                              
177701             PS-ERROR                    TO TRUE                          
           ELSE                                                                 
      *        IF THIS IS AN ADD, MAKE SURE THE TICKET OFFICE DOESN'T EXIST     
               IF  ASC-CREATE-TKT-OFF-FORMAT                                    
                   PERFORM 3100-VALIDATE-TKT-OFF-ID                             
                   IF SQLCODE = +100                                            
                       CONTINUE                                                 
                   ELSE                                                         
177000                 MOVE PC-TSYMSG-00117    TO DP020-MSG-NUMBER              
177100                 MOVE -1                 TO ATKTOFFL                      
177300                 MOVE DP015-REVERSE      TO ATKTOFFH                      
177400                 MOVE DP015-RED          TO ATKTOFFC                      
177600                 MOVE DP015-UNP-ALP-BRT-OFF                               
177600                                         TO ATKTOFFA                      
177200                 SET DP030-SET-CURSOR-APPL-1                              
177700                     DP020-MSG-FATAL                                      
177701                     PS-ERROR             TO TRUE                         
                   END-IF                                                       
               END-IF                                                           
177702     END-IF.                                                              
                                                                                
139000     EJECT                                                                
125400*----------------------------------------------------------------*        
125500*  VALIDATE THE TICKET OFFICE ID TO MAKE SURE IT DOES NOT ALREADY*        
125600*  EXIST                                                         *        
125600*  ADDED OPER_UNT_ID TO SELECT  01/09/01  VML                    *        
125700*----------------------------------------------------------------*        
125800 3100-VALIDATE-TKT-OFF-ID.                                                
125900                                                                          
126000     PERFORM                                                              
126100         WITH TEST AFTER                                                  
126200         VARYING PV-SQL-SUB                                               
126300         FROM 1 BY 1                                                      
126400         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
126500            OR   SQLCODE = +0                                             
126500            OR   SQLCODE = -811                                           
126600            OR   SQLCODE = +100)                                          
126700                                                                          
126800         EXEC SQL                                                         
126900            SELECT TKT_OFF_ID                                             
127000              INTO :TKTFMT-TKT-OFF-ID                                     
127100              FROM TTKTFMT                                                
127200             WHERE TKT_OFF_ID   = :ASC-TKT-OFF-ID                         
127200               AND OPER_UNT_ID  = :DK-OPER-UNT-ID                         
128500         END-EXEC                                                         
128600                                                                          
128700         EVALUATE TRUE                                                    
129100             WHEN SQLCODE = +100                                          
129100             WHEN SQLCODE = -811                                          
129300             WHEN SQLCODE = +0                                            
129400                 CONTINUE                                                 
129700             WHEN OTHER                                                   
129800                  MOVE '3100-VALIDATE-TKT-OFF-ID'                         
129900                                   TO DP013-PARAGRAPH                     
130000                  MOVE 'SELECT TICKET OFFICE ID'                          
130100                                   TO DP013-MESSAGE-TEXT (1)              
130200                  MOVE SQLCA       TO DP013-SQLCA                         
130300                  MOVE 'TTKTFMT '  TO DP013-DB2-TABLE-NAME (1)            
130400                  SET DP013-DB2-ABEND                                     
130500                      DP013-XCTL-DISPLAY-RESTART                          
130600                                   TO TRUE                                
130700                  PERFORM DP013-0000-PROCESS-ABEND                        
130800         END-EVALUATE                                                     
130900     END-PERFORM.                                                         
131000                                                                          
131100     IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
131200         MOVE '3100-VALIDATE-TKT-OFF-ID'                                  
131300                               TO  DP013-PARAGRAPH                        
131400         MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
131500                               TO  DP013-MESSAGE-TEXT (1)                 
131800         MOVE SQLCA            TO  DP013-SQLCA                            
131900         MOVE 'TTKTFMT '       TO DP013-DB2-TABLE-NAME (1)                
132000         SET DP013-DB2-ABEND                                              
132100             DP013-XCTL-DISPLAY-RESTART                                   
132200                               TO  TRUE                                   
132300         PERFORM DP013-0000-PROCESS-ABEND                                 
132400     END-IF.                                                              
132500     EJECT                                                                
265100******************************************************************        
265200** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
265300******************************************************************        
265400                                                                          
265500 3200-RESET-ATTRIBUTES.                                                   
           EVALUATE TRUE                                                        
                                                                                
      *    WHEN IN "DELETE MODE" - PROTECT ALL FIELDS                           
           WHEN ASC-DELETE-TKT-OFF-FORMAT                                       
265600         MOVE DP015-ASK-NOR-OFF    TO AOPERIDA                            
                                            AFCLTYA                             
                                            ATKTOFFA                            
                                            ADESTA                              
266200         MOVE DP015-BLUE           TO AOPERIDC                            
                                            AFCLTYC                             
                                            ATKTOFFC                            
                                            ADESTC                              
266800         MOVE DP015-HL-OFF         TO AOPERIDH                            
                                            AFCLTYH                             
                                            ATKTOFFH                            
                                            ADESTH                              
01152 *                                                                 DPKCS036
                                                                                
      *    WHEN IN "UPDATE MODE" - UNPROTECT ALL FIELDS                 OW      
           WHEN ASC-UPDATE-TKT-OFF-FORMAT                                       
265600         MOVE DP015-ASK-NOR-OFF       TO ATKTOFFA                         
265600                                         AOPERIDA                         
                                               AFCLTYA                          
               MOVE DP015-UNP-ALP-NOR-OFF   TO ADESTA                           
                                                                                
               MOVE DP015-BLUE              TO ATKTOFFC                         
                                               AOPERIDC                         
                                               AFCLTYC                          
               MOVE DP015-GREEN             TO ADESTC                           
                                                                                
               MOVE DP015-HL-OFF            TO ATKTOFFH                         
                                               AOPERIDH                         
                                               AFCLTYH                          
               MOVE DP015-UNDERLINE         TO ADESTH                           
                                                                                
      *    WHEN IN "CREATE MODE" - ALLOW UPDATE TO ALL FIELDS                   
           WHEN ASC-CREATE-TKT-OFF-FORMAT                                       
265600         MOVE DP015-UNP-ALP-NOR-OFF   TO ATKTOFFA                         
265600                                         AOPERIDA                         
                                               AFCLTYA                          
                                               ADESTA                           
                                                                                
               MOVE DP015-GREEN             TO ATKTOFFC                         
                                               AOPERIDC                         
                                               AFCLTYC                          
                                               ADESTC                           
                                                                                
               MOVE DP015-UNDERLINE         TO ATKTOFFH                         
                                               AOPERIDH                         
                                               AFCLTYH                          
                                               ADESTH                           
           END-EVALUATE.                                                        
139000     EJECT                                                                
125400*----------------------------------------------------------------*        
125500*  VALIDATE THE OPERATING UNIT ID TO MAKE SURE THAT IT IS A      *        
125600*  VALID DISTRIBUTION CENTER.                                    *        
125700*----------------------------------------------------------------*        
125800 3400-VALIDATE-OPER-UNT-ID.                                               
                                                                                
               INITIALIZE XL071-COMMAREA-REC.                                   
               MOVE DK-STORE-NMBR  TO XL071-LOCATION-NUMBER.                    
                                                                                
               CALL XL071-LOCATION-MODULE USING DFHEIBLK                        
                                                XL071-COMMAREA                  
                                                XL071-COMMAREA-REC.             
                                                                                
                                                                                
               EVALUATE TRUE                                                    
                                                                                
                   WHEN XL071-NO-ERRORS                                         
                       CONTINUE                                                 
                                                                                
                   WHEN XL071-LOC-NOT-FOUND                                     
                       MOVE SPACES       TO ASC-FACILITY-NAME                   
                                                                                
                   WHEN XL071-BAD-DB2-RETURN                                    
                        MOVE '3400-VALIDATE-OPER-UNT-ID'                        
                                         TO DP013-PARAGRAPH                     
                        MOVE 'CALL MODULE XLKUT071'                             
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        MOVE 'XLT_LOC '  TO DP013-DB2-TABLE-NAME (1)            
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE.                                                    
132500     EJECT                                                                
267368 EJECT                                                                    
267369*----------------------------------------------------------------*        
267370*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
267380*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
267390*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
267400*    DISPLAY THE PANEL TO USER.                                  *        
267500*----------------------------------------------------------------*        
267600                                                                          
267700 4000-BUILD-INITIAL-PANEL.                                                
267800                                                                          
270800     MOVE -1               TO AOPERIDL.                                   
270900     SET DP030-SET-CURSOR-APPL-1                                          
271000                           TO TRUE.                                       
271100     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
271300 EJECT                                                                    
347700*----------------------------------------------------------------*        
347800* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
347900*----------------------------------------------------------------*        
348000                                                                          
348100 4400-MOVE-COMMAREA-TO-SCREEN.                                            
352900                                                                          
031200     MOVE  ASC-TKT-OFF-ID          TO ATKTOFFO.                           
031200     MOVE  ASC-OPER-UNT-ID         TO AOPERIDO.                           
031200     MOVE  ASC-FACILITY-ID         TO AFCLTYO.                            
031200     MOVE  ASC-FACILITY-NAME       TO ANAMEO.                             
031200     MOVE  ASC-TKT-OFF-ADR-ID      TO ADESTO.                             
02104  EJECT                                                            DPKCS036
271400*--------------------------------------------------------------------*    
271500*  ADD ALL THE TICKET FORMAT INFORMATION TO THE TICKET FORMAT TABLES *    
271600*--------------------------------------------------------------------*    
271700                                                                          
271800 7000-ADD-TICKET-FORMAT.                                                  
                                                                                
           INITIALIZE DCLTTKTFMT.                                               
                                                                                
      *    MOVE TICKET OFFICE KEY VALUES TO THE HOST VARIABLES                  
           MOVE ASC-OPER-UNT-ID-N       TO TKTFMT-OPER-UNT-ID.                  
           MOVE ASC-FACILITY-ID-N       TO TKTFMT-FCLTY-ID.                     
           MOVE ASC-TKT-OFF-ID          TO TKTFMT-TKT-OFF-ID.                   
           MOVE ASC-TKT-OFF-ADR-ID      TO TKTFMT-TKT-OFF-ADR-ID.               
                                                                                
      *    FOR EACH TICKET FORMAT, SELECT THE REST OF THE HOST VARIABLES FROM   
      *    THE TICKET FORMAT TABLE, AND INSERT A ROW WITH THE NEW TICKET        
      *    OFFICE                                                               
           PERFORM 7020-OPEN-CURSOR.                                            
           PERFORM 7010-FETCH-CURSOR.                                           
                                                                                
           PERFORM UNTIL PS-END-OF-CURSOR                                       
               PERFORM 7040-INSERT-TTKTFMT                                      
               PERFORM 7010-FETCH-CURSOR                                        
           END-PERFORM.                                                         
                                                                                
           PERFORM 7030-CLOSE-CURSOR.                                           
                                                                                
343100*----------------------------------------------------------------*        
343200*    FETCH THE MESSAGE CURSOR.                                   *        
343300*----------------------------------------------------------------*        
343400                                                                          
343500 7010-FETCH-CURSOR.                                                       
343600                                                                          
343700     EXEC SQL                                                             
343800          FETCH TKT_FORMAT_CURSOR                                         
343900          INTO                                                            
                       :TKTFMT-TKT-FMT-ID                                       
                     , :TKTFMT-TKT-OFF-DESC                                     
                     , :TKTFMT-TKT-FMT-DESC                                     
                     , :TKTFMT-TYP-CDE                                          
                     , :TKTFMT-OPT-BLK-TAGS-IND                                 
                     , :TKTFMT-PRT-SIZE-IND                                     
                     , :TKTFMT-PRT-COLOR-IND                                    
                     , :TKTFMT-PRT-GP-PR-IND                                    
                     , :TKTFMT-CMPANON-OP-UNT-ID                                
                     , :TKTFMT-CMPANON-FCLTY-ID                                 
                     , :TKTFMT-CMPANON-TKT-ID                                   
                     , :TKTFMT-PAPER-TYP-ID                                     
                     , :TKTFMT-OPT-BLK-LBLS-IND                                 
                     , :TKTFMT-STKR-NBR-REQ-IND                                 
                     , :TKTFMT-TKT-FMT-NBR                                      
345300     END-EXEC.                                                            
345400                                                                          
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345700         WHEN SQLCODE = +100                                              
345800              CONTINUE                                                    
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7010-FETCH-CURSOR'                                    
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'FETCH TKT_FORMAT_CURSOR'                              
346400                               TO DP013-MESSAGE-TEXT (1)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
347000                                                                          
347100     IF  SQLCODE EQUAL +100                                               
347300         SET PS-END-OF-CURSOR TO TRUE                                     
347400     END-IF.                                                              
                                                                                
343000 EJECT                                                                    
343100*----------------------------------------------------------------*        
343200*    OPEN THE LIST DETAIL CUSOR FOR SHIPPING UPDATE              *        
343300*----------------------------------------------------------------*        
343400                                                                          
343500 7020-OPEN-CURSOR.                                                        
343700     EXEC SQL                                                             
343800          OPEN TKT_FORMAT_CURSOR                                          
345300     END-EXEC.                                                            
345400                                                                          
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345800              SET PS-NOT-END-OF-CURSOR TO TRUE                            
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7020-OPEN-CURSOR'                                     
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'OPEN TKT_FORMAT_CURSOR'                               
346400                               TO DP013-MESSAGE-TEXT (1)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
                                                                                
343100*----------------------------------------------------------------*        
343200*   CLOSE THE LIST DETAIL CUSOR FOR SHIPPING UPDATE              *        
343300*----------------------------------------------------------------*        
343400                                                                          
343500 7030-CLOSE-CURSOR.                                                       
343700     EXEC SQL                                                             
343800         CLOSE TKT_FORMAT_CURSOR                                          
345300     END-EXEC.                                                            
345400                                                                          
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345800              CONTINUE                                                    
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7030-CLOSE-CURSOR'                                    
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'CLOSE TKT_FORMAT_CURSOR'                              
346400                               TO DP013-MESSAGE-TEXT (1)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
347500                                                                          
271400*----------------------------------------------------------------*        
271500* INSERT THE TICKET FORMAT ROW TO DB2.                           *        
271600*----------------------------------------------------------------*        
271700                                                                          
271800 7040-INSERT-TTKTFMT.                                                     
                                                                                
           EXEC SQL                                                             
               INSERT INTO TTKTFMT                                              
                   ( OPER_UNT_ID                                                
                    ,FCLTY_ID                                                   
                    ,TKT_OFF_ID                                                 
                    ,TKT_FMT_ID                                                 
                    ,TKT_OFF_DESC                                               
                    ,TKT_FMT_DESC                                               
                    ,TYP_CDE                                                    
                    ,OPT_BLK_TAGS_IND                                           
                    ,PRT_SIZE_IND                                               
                    ,PRT_COLOR_IND                                              
                    ,PRT_GP_PR_IND                                              
                    ,CMPANON_OP_UNT_ID                                          
                    ,CMPANON_FCLTY_ID                                           
                    ,CMPANON_TKT_ID                                             
                    ,PAPER_TYP_ID                                               
                    ,OPT_BLK_LBLS_IND                                           
                    ,STKR_NBR_REQ_IND                                           
                    ,TKT_OFF_ADR_ID                                             
                    ,TKT_FMT_NBR                                                
                   )                                                            
               VALUES                                                           
                   (                                                            
                     :TKTFMT-OPER-UNT-ID                                        
                   , :TKTFMT-FCLTY-ID                                           
                   , :TKTFMT-TKT-OFF-ID                                         
                   , :TKTFMT-TKT-FMT-ID                                         
                   , :TKTFMT-TKT-OFF-DESC                                       
                   , :TKTFMT-TKT-FMT-DESC                                       
                   , :TKTFMT-TYP-CDE                                            
                   , :TKTFMT-OPT-BLK-TAGS-IND                                   
                   , :TKTFMT-PRT-SIZE-IND                                       
                   , :TKTFMT-PRT-COLOR-IND                                      
                   , :TKTFMT-PRT-GP-PR-IND                                      
                   , :TKTFMT-CMPANON-OP-UNT-ID                                  
                   , :TKTFMT-CMPANON-FCLTY-ID                                   
                   , :TKTFMT-CMPANON-TKT-ID                                     
                   , :TKTFMT-PAPER-TYP-ID                                       
                   , :TKTFMT-OPT-BLK-LBLS-IND                                   
                   , :TKTFMT-STKR-NBR-REQ-IND                                   
                   , :TKTFMT-TKT-OFF-ADR-ID                                     
                   , :TKTFMT-TKT-FMT-NBR                                        
                   )                                                            
           END-EXEC.                                                            
                                                                                
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345800              CONTINUE                                                    
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7040-INSERT-TTKTFMT'                                  
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'INSERT TICKET FORMAT ROW'                             
346400                               TO DP013-MESSAGE-TEXT (1)                  
                    MOVE TKTFMT-OPER-UNT-ID                                     
014900                               TO PV-DISPLAY-COMP1                        
                    MOVE TKTFMT-OPER-UNT-ID                                     
014900                               TO PV-DISPLAY-COMP2                        
                    STRING PV-DISPLAY-COMP1   DELIMITED BY SIZE                 
                    ,      ' - '              DELIMITED BY SIZE                 
                    ,      PV-DISPLAY-COMP2   DELIMITED BY SIZE                 
                    ,      ' - '              DELIMITED BY SIZE                 
                    ,      TKTFMT-TKT-OFF-ID  DELIMITED BY SIZE                 
                    ,      ' - '              DELIMITED BY SIZE                 
                    ,      TKTFMT-TKT-FMT-ID  DELIMITED BY SIZE                 
                                   INTO DP013-MESSAGE-TEXT (2)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
347000                                                                          
271400*----------------------------------------------------------------*        
271500* DELETE ALL THE TICKET FORMATS ASSOCIATED TO THE TICKET OFFICE  *        
271600*----------------------------------------------------------------*        
271700                                                                          
271800 7100-DELETE-TICKET-OFFICE.                                               
                                                                                
           MOVE ASC-OPER-UNT-ID-OLD-N TO DK-OPER-UNT-ID-OLD.                    
           MOVE ASC-FACILITY-ID-OLD-N TO DK-FACILITY-ID-OLD.                    
                                                                                
           EXEC SQL                                                             
               DELETE FROM TTKTFMT                                              
               WHERE                                                            
                     OPER_UNT_ID    = :DK-OPER-UNT-ID-OLD                       
                 AND FCLTY_ID       = :DK-FACILITY-ID-OLD                       
                 AND TKT_OFF_ID     = :ASC-TKT-OFF-ID-OLD                       
           END-EXEC.                                                            
 22973                                                                          
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345600         WHEN SQLCODE = +100                                              
345800              CONTINUE                                                    
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7100-DELETE-TICKET-OFFICE'                            
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'DELETE TKT OFFICE FROM TKT FORMAT TABLE'              
346400                               TO DP013-MESSAGE-TEXT (1)                  
                    STRING 'KEY VALUES: '        DELIMITED BY SIZE              
                    ,      ASC-OPER-UNT-ID-OLD   DELIMITED BY SIZE              
                    ,      ' - '                 DELIMITED BY SIZE              
                    ,      ASC-FACILITY-ID-OLD   DELIMITED BY SIZE              
                    ,      ' - '                 DELIMITED BY SIZE              
                    ,      ASC-TKT-OFF-ID-OLD    DELIMITED BY SIZE              
                    ,      ' - '                 DELIMITED BY SIZE              
                                   INTO DP013-MESSAGE-TEXT (2)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
347000                                                                          
284700                                                                          
271400*----------------------------------------------------------------*        
271500* UPDATE EACH ROW ON THE TICKET FORMAT TABLE WITH THE NEW TICKET *        
271500* OFFICE VALUES.                                                 *        
271600*----------------------------------------------------------------*        
271700*                                                                         
271800 7200-UPDATE-TICKET-OFFICE.                                               
                                                                                
           MOVE ASC-OPER-UNT-ID-OLD-N  TO DK-OPER-UNT-ID-OLD                    
           MOVE ASC-FACILITY-ID-OLD-N  TO DK-FACILITY-ID-OLD                    
                                                                                
           EXEC SQL                                                             
               UPDATE TTKTFMT                                                   
               SET                                                              
                   TKT_OFF_ADR_ID = :ASC-TKT-OFF-ADR-ID                         
               WHERE                                                            
                     OPER_UNT_ID    = :DK-OPER-UNT-ID-OLD                       
                 AND FCLTY_ID       = :DK-FACILITY-ID-OLD                       
                 AND TKT_OFF_ID     = :ASC-TKT-OFF-ID-OLD                       
           END-EXEC.                                                            
 22973                                                                          
345500     EVALUATE TRUE                                                        
345600         WHEN SQLCODE = ZERO                                              
345800              CONTINUE                                                    
345900         WHEN SQLWARN0 NOT = SPACE                                        
346000         WHEN SQLCODE NOT = ZERO                                          
346100              MOVE '7200-UPDATE-TICKET-OFFICE'                            
346200                               TO DP013-PARAGRAPH                         
346300              MOVE 'UPDATE TKT OFFICE INFO ON TKT FORMAT TABLE'           
346400                               TO DP013-MESSAGE-TEXT (1)                  
                    STRING 'KEY VALUES: '        DELIMITED BY SIZE              
                    ,      ASC-OPER-UNT-ID-OLD   DELIMITED BY SIZE              
                    ,      ' - '                 DELIMITED BY SIZE              
                    ,      ASC-FACILITY-ID-OLD   DELIMITED BY SIZE              
                    ,      ' - '                 DELIMITED BY SIZE              
                    ,      ASC-TKT-OFF-ID-OLD    DELIMITED BY SIZE              
                                   INTO DP013-MESSAGE-TEXT (2)                  
                    STRING 'NEW VALUES: '        DELIMITED BY SIZE              
                    ,      ASC-TKT-OFF-ADR-ID    DELIMITED BY SIZE              
                                   INTO DP013-MESSAGE-TEXT (3)                  
346500              MOVE SQLCA       TO DP013-SQLCA                             
346600              SET DP013-DB2-ABEND                                         
346700                               TO TRUE                                    
346800              PERFORM DP013-0000-PROCESS-ABEND                            
346900     END-EVALUATE.                                                        
347000                                                                          
370300 EJECT                                                                    
370400*----------------------------------------------------------------*        
370500*    READ THE SELECTION QUEUE CREATED IN VLKCS506                *        
370600*----------------------------------------------------------------*        
370700                                                                          
370800 9100-READ-IN-SEL-QUEUE.                                                  
370900                                                                          
371000     EXEC CICS                                                            
371100          READQ TS                                                        
371200              QUEUE (ASC-SEL-QUEUE-NAME)                                  
371300              INTO  (VL014-TKT-FMT-SEL-REC)                               
371400              ITEM  (ASC-SEL-ITEM)                                        
371500              RESP  (PV-CICS-RESP)                                        
371600     END-EXEC.                                                            
371700*                                                                         
371800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
371900         CONTINUE                                                         
372000     ELSE                                                                 
372100         SET DP013-CICS-ABEND      TO  TRUE                               
372200         SET DP013-NO-ROLLBACK     TO  TRUE                               
372300         MOVE '9100-READ-IN-SEL-QUEUE'                                    
372400                                   TO  DP013-PARAGRAPH                    
372500         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
372600                                   TO  DP013-MESSAGE-TEXT(1)              
372700         PERFORM DP013-0000-PROCESS-ABEND                                 
372800     END-IF.                                                              
370400*----------------------------------------------------------------*        
370500*  REWRITE THE SELECTION QUEUE CREATED IN VLKCS506               *        
370600*----------------------------------------------------------------*        
370700                                                                          
370800 9101-REWRITE-SEL-QUEUE.                                                  
370900                                                                          
371000     EXEC CICS                                                            
371100        WRITEQ TS                                                         
371200              QUEUE (ASC-SEL-QUEUE-NAME)                                  
371300              FROM  (VL014-TKT-FMT-SEL-REC)                               
371400              ITEM  (ASC-SEL-ITEM)                                        
371300              REWRITE                                                     
371500              RESP  (PV-CICS-RESP)                                        
371600     END-EXEC.                                                            
371700*                                                                         
371800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
371900         CONTINUE                                                         
372000     ELSE                                                                 
372100         SET DP013-CICS-ABEND      TO  TRUE                               
372200         SET DP013-NO-ROLLBACK     TO  TRUE                               
372300         MOVE '9101-REWRITE-SEL-QUEUE'                                    
372400                                   TO  DP013-PARAGRAPH                    
372500         MOVE 'ATTEMPTING TO REWRITE THE SEL QUEUE'                       
372600                                   TO  DP013-MESSAGE-TEXT(1)              
372700         PERFORM DP013-0000-PROCESS-ABEND                                 
372800     END-IF.                                                              
372900 EJECT                                                                    
442562                                                                          
373000*----------------------------------------------------------------*        
373100*    ABEND PROCESSOR MODULE                                      *        
373200*----------------------------------------------------------------*        
373300                                                                          
373400     COPY DPPD013.                                                        
