      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT090.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT090 -                                                           
      *      OPEN RUN DATE FILE AND READ IT IN.  LOG ON TO ORACLE.     *        
      *      ADD ONE DAY TO THE RUN DATE FOR THE START DATE OF THE     *        
      *      PROMO THAT WILL BE USED.  TRUNCATE THE PMTT_ACCM_OH_QTY TO*        
      *      MAKE SURE IT IS REALLY EMPTY.  THEN USING THE START DATE  *        
      *      FIND ALL PROMOS THAT START ON THAT DAY.  FOR EACH PROMO   *        
      *      FIND ALL THE PROMO ITEMS TIED TO IT.  FOR EACH PROMO      *        
      *      ITEM CALL PMKUT091 TO FIND THE ONHAND QTY. INSERT THE QTY *        
      *      AND SKU INTO THE TEMP TABLE, PMTT_ACCM_OH_QTY.  WHEN THE  *        
      *      PROMO ITEM IS DIFFERET SUM UP THE TEMP TABLE AND INSERT   *        
      *      TOTAL INTO THE ACTL_PREP COLUMN FOUND ON THE PROMO_ITM    *        
      *      TABLE.  TRUNC THE TEMP TABLE SO IT IS READY FOR THE NEXT  *        
      *      PROMO ITEM.  NEXT RUN THIS SAME PROCESS FOR THE END DATE. *        
      *      USE THE DATE FOUND FOR THE START DATE.  CLOSE ALL OPEN    *        
      *      FILES AND DISPLAY COUNT TOTAL IN THE OUTPUT FILE.         *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *                                                                *        
      ******************************************************************        
      *    11/11/2007 - AMANDA JEFFORDS -  INC000000152176             *        
      *    IN THE EXCLNCSR ADD DEPT_NBR IN THE SELECT.                 *        
      *                                                                *        
      *    11/11/2007 - AMANDA JEFFORDS -  INC000000152176             *        
      *    IN C230 MOVE RUN DATE BACK INTO PM801-RUN-DATE              *        
TA3304*                                                                *        
TA3304*    06/03/2010 - THASLIM ARIFF - PKE000000003304                *        
TA3304*    INCLUDED THE COMMIT PARA BEFORE THE STOP RUN STATEMENT AND  *        
TA3304*    ALSO INCLUDED THE COMMIT COUNTER                            *        
      *                                                                *        
      *    03/06/2014 - COGNIZANT     - INC0592171                     *        
      *    INCREASED THE INTERNAL TABLE SIZE IN COPYBOOK PMRD801       *        
      *    FROM 50000 TO 99999                                         *        
      *                                                                *        
      *    03/07/2014 - COGNIZANT     - INC0592171                     *        
      *    CHANGED THE PIC CLAUSE OF PM801-INDEX FROM PIC S9(04)       *        
      *    TO PIC 9(05) IN COPYBOOK PMRD801.                           *        
      *    CHANGED THE PIC CLAUSE OF PV-INDEX FROM PIC S9(04)          *        
      *    TO PIC S9(09) IN THE PROGRAM.                               *        
      *                                                                *        
161213*    02/10/2017 - COGNIZANT     -  CHG0161213                   *         
161213*    MODIFIED CODE TO REMOVE UNWANTED COMMENTS WHICH WILL       *         
161213*    IMPROVE CPU UTILIZATION.                                   *         
      *                                                                *        
      *    07/30/2018 - COGNIZANT     -  CHG0204126                    *        
      *    ORACLE LOGON STANDARDS PROGRAM CHANGE                       *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT RUN-DATE-FILE                                                 
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
               ASSIGN                       TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RUN-DATE-FILE                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  RUN-DATE-LINE                    PIC  X(80).                         
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-RUN-DATE-FILE.                                                    
           05  CC-RUN-DATE                  PIC X(08).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW         PIC X(1) VALUE 'N'.              
006530         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
           05  PS-ORA-SRC-CTL-DONE-SW          PIC X(1) VALUE 'N'.              
               88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
           05  PS-RUN-DATE-FILE-SW             PIC X(1) VALUE 'N'.              
               88  PS-RUN-DATE-FILE-END                 VALUE 'Y'.              
               88  PS-RUN-DATE-FILE-START               VALUE 'N'.              
           05  PS-END-PROMO-STRT-CSR-SWITCH    PIC X(1) VALUE 'N'.              
               88  PS-END-OF-PROMO-STRT-CSR             VALUE 'Y'.              
               88  PS-NOT-END-OF-PROMO-STRT-CSR         VALUE 'N'.              
           05  PS-END-OF-PROMO-END-CSR-SWITCH  PIC X(1) VALUE 'N'.              
               88  PS-END-OF-PROMO-END-CSR              VALUE 'Y'.              
               88  PS-NOT-END-OF-PROMO-ENT-CSR          VALUE 'N'.              
           05  PS-END-OF-ITM-CSR-SWITCH        PIC X(1) VALUE 'N'.              
               88  PS-END-OF-ITM-CSR                    VALUE 'Y'.              
               88  PS-NOT-END-OF-ITM-CSR                VALUE 'N'.              
           05  PS-END-OF-EXCLN-CSR-SWITCH      PIC X(1) VALUE 'N'.              
               88  PS-END-OF-EXCLN-CSR                  VALUE 'Y'.              
               88  PS-NOT-END-OF-EXCLN-CSR              VALUE 'N'.              
           05  PS-OH-QTY-ROWS-SWITCH           PIC X(1) VALUE 'N'.              
               88  PS-OH-QTY-ROWS                       VALUE 'Y'.              
               88  PS-NO-OH-QTY-ROWS                    VALUE 'N'.              
                                                                                
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID             PIC X(40).                          
               88 CC-AUTO-LOGON                         VALUE '/'.              
           05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AUTO-LOGON                PIC X(01)   VALUE '/'.              
           05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT090'.          
           05  PC-PMKUT091-PGM              PIC  X(08)  VALUE                   
                                                           'PMKUT091'.          
           05  PC-ONE                       PIC  9(02)  VALUE 01.               
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROES.           
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SKU-NBR-OUT               PIC  9(08)  VALUE ZEROES.           
           05  PV-ONHAND-QTY-OUT            PIC S9(07)V VALUE ZEROES.           
           05  PV-INDEX                     PIC S9(09)  VALUE +0                
                                                        COMP SYNC.              
           05  PV-PROMO-DTE                 PIC  X(08)  VALUE SPACES.           
           05  PV-PROMO-STRT-STRT-TMST      PIC  X(18)  VALUE SPACES.           
           05  PV-PROMO-STRT-END-TMST       PIC  X(18)  VALUE SPACES.           
           05  PV-PROMO-END-STRT-TMST       PIC  X(18)  VALUE SPACES.           
           05  PV-PROMO-END-END-TMST        PIC  X(18)  VALUE SPACES.           
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-PROMO-ID                  PIC  X(10)  VALUE SPACES.           
           05  PV-TOTAL-ITM-OH-QTY          PIC S9(15)  VALUE ZEROES.           
                                                                                
       01  PV-PREV-ITM-VARIABLES.                                               
           05  PV-PREV-PROMO-ITM-ID         PIC  9(10)  VALUE ZEROES.           
           05  PV-PREV-MDSE-HIER-CDE        PIC  X(03)  VALUE SPACES.           
           05  PV-PREV-DEPT-NBR             PIC  9(04)  VALUE ZEROES.           
           05  PV-PREV-MAJ-CL-NBR           PIC  9(04)  VALUE ZEROES.           
           05  PV-PREV-SUB-CL-NBR           PIC  9(04)  VALUE ZEROES.           
           05  PV-PREV-VS-ID                PIC  9(09)  VALUE ZEROES.           
           05  PV-PREV-SKU-NBR              PIC  9(08)  VALUE ZEROES.           
           05  PV-PREV-MDSE-SELN-LNE-ID     PIC  9(10)  VALUE ZEROES.           
                                                                                
       01  PV-ITM-VARIABLES.                                                    
           05  PV-PROMO-ITM-ID              PIC  9(10)  VALUE ZEROES.           
           05  PV-MDSE-HIER-CDE             PIC  X(03)  VALUE SPACES.           
           05  PV-DEPT-NBR                  PIC  9(04)  VALUE ZEROES.           
           05  PV-MAJ-CL-NBR                PIC  9(04)  VALUE ZEROES.           
           05  PV-SUB-CL-NBR                PIC  9(04)  VALUE ZEROES.           
           05  PV-VS-ID                     PIC  9(09)  VALUE ZEROES.           
           05  PV-SKU-NBR                   PIC  9(08)  VALUE ZEROES.           
           05  PV-MDSE-SELN-LNE-ID          PIC  9(10)  VALUE ZEROES.           
                                                                                
           05  PV-DEPT-NBR-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
           05  PV-MAJ-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
           05  PV-SUB-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
           05  PV-VS-ID-NULL                PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
           05  PV-SKU-NBR-NULL              PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
           05  PV-MDSE-SELN-LNE-ID-NULL     PIC S9(04)  COMP                    
                                                           VALUE ZEROES.        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-UPDATED              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-PROMO-STRT-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-PROMO-END-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-ITM-CNT                   PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-EXCLN-CNT                 PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-STRT-ITM-CNT              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-END-ITM-CNT               PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
161213     05  PA-KEEP-TRACK                PIC 9(09) VALUE ZEROES.             
161213     05  PA-RECS-WRITTEN              PIC 9(09) VALUE ZEROES.             
TA3304     05  PA-COMMIT-CNT                PIC S9(11)  COMP-3                  
TA3304                                                  VALUE ZEROES.           
                                                                                
      ******************************************************************        
      *    THE LAYOUT FOR THE SKU ONHAND QTY.                                   
      *    PROGRAM PMKUT091 PARAMETER LIST.                                     
      ******************************************************************        
           COPY PMRD801.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      *****************************************************************         
      *    ORACLE AREA FOR PROMOS STARTING TOMORROW                   *         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE STRTPROMOCSR                                            
                CURSOR FOR                                                      
                    SELECT                                                      
                         PROMO_ID                                               
                    FROM PMT_PROMO                                              
                    WHERE (DFLT_STRT_TMST BETWEEN                               
                         TO_DATE(:PV-PROMO-STRT-STRT-TMST,                      
                                            'YYYYMMDD HH24:MI:SS')              
                         AND TO_DATE(:PV-PROMO-STRT-END-TMST,                   
                                            'YYYYMMDD HH24:MI:SS'))             
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    ORACLE AREA FOR PROMOS ENDING TODAY                        *         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE ENDPROMOCSR                                             
                CURSOR FOR                                                      
                    SELECT                                                      
                         PROMO_ID                                               
                    FROM PMT_PROMO                                              
                    WHERE (DFLT_END_TMST BETWEEN                                
                         TO_DATE(:PV-PROMO-END-STRT-TMST,                       
                                            'YYYYMMDD HH24:MI:SS')              
                         AND TO_DATE(:PV-PROMO-END-END-TMST,                    
                                            'YYYYMMDD HH24:MI:SS'))             
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    ORACLE AREA FOR ITEM CURSOR                                *         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE ITMCSR                                                  
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.PROMO_ITM_ID                                         
                        ,B.MDSE_HIER_CDE                                        
                        ,C.DEPT_NBR                                             
                        ,C.MAJ_CL_NBR                                           
                        ,C.SUB_CL_NBR                                           
                        ,C.VS_ID                                                
                        ,C.SKU_NBR                                              
                        ,C.MDSE_SELN_LNE_ID                                     
                    FROM                                                        
                         PMT_PROMO_ITM A                                        
                        ,PMT_MDSE_SELN B                                        
                        ,PMT_MDSE_SELN_LNE C                                    
                    WHERE                                                       
                            A.PROMO_ID = :PV-PROMO-ID                           
                        AND A.PROMO_ITM_ID = B.PROMO_ITM_ID                     
                        AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                     
                    ORDER BY                                                    
                        A.PROMO_ITM_ID                                          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    ORACLE AREA FOR EXCLUSION CSR                              *         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE EXCLNCSR                                                
                CURSOR FOR                                                      
                    SELECT                                                      
                         D.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,D.MAJ_CL_NBR                                           
                        ,D.SUB_CL_NBR                                           
                        ,D.VS_ID                                                
                        ,D.SKU_NBR                                              
                        ,D.MDSE_SELN_LNE_ID                                     
                    FROM                                                        
                         PMT_PROMO_ITM A                                        
                        ,PMT_MDSE_SELN B                                        
                        ,PMT_MDSE_SELN_LNE C                                    
                        ,PMT_MDSE_SELN_LNE_EXCLN D                              
                    WHERE                                                       
                            A.PROMO_ID = :PV-PROMO-ID                           
                        AND A.PROMO_ITM_ID = :PV-PROMO-ITM-ID                   
                        AND A.PROMO_ITM_ID = B.PROMO_ITM_ID                     
                        AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                     
                        AND C.MDSE_SELN_LNE_ID = D.MDSE_SELN_LNE_ID             
                    ORDER BY                                                    
                        A.PROMO_ITM_ID                                          
           END-EXEC.                                                            
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CC AND READ IT                                    *         
      *  - SETUP ORACLE                                               *         
      *  - ADD 1 DAY TO THE CC DATE                                   *         
      *  - MAKE SURE PMTT_ACCM_OH_QTY IS AN EMPTY TABLE.  DON'T WANT  *         
      *    THE ON HAND QTY GETTING SKEWED BECAUSE OF LEFTOVER DATA.   *         
      *  - PROCESS THE PROMOS THAT ARE 1 DAY AHEAD OF THE CC          *         
      *  - PROCESS THE PROMOS THAT END THE DAY OF THE CC              *         
      *  - CLOSE CC                                                   *         
      *  - DISPLAY TOTALS                                             *         
      * ==> PERFORMED FROM:  START                                    *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-RUN-DATE-FILE-START TO TRUE.                                  
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-RUN-DATE-FILE.                                     
                                                                                
      *    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM B300-ORACLE-SETUP.                                           
                                                                                
           PERFORM D100-FIND-TOMORROW.                                          
           PERFORM D105-CONCAT-DATES.                                           
                                                                                
           PERFORM D140-TRUNC-TEMP-TBL.                                         
                                                                                
           PERFORM C100-OPEN-PROMO-STRT-CSR.                                    
           PERFORM C200-FETCH-PROMO-STRT-CSR.                                   
           PERFORM C300-PROCESS-PROMO-STRT-CSR                                  
               UNTIL PS-END-OF-PROMO-STRT-CSR                                   
           PERFORM C400-CLOSE-PROMO-STRT-CSR.                                   
                                                                                
           MOVE PA-ITM-CNT TO PA-STRT-ITM-CNT.                                  
           MOVE ZEROES TO PA-ITM-CNT.                                           
           INITIALIZE PV-PREV-ITM-VARIABLES.                                    
                                                                                
           PERFORM C110-OPEN-PROMO-END-CSR.                                     
           PERFORM C210-FETCH-PROMO-END-CSR.                                    
           PERFORM C310-PROCESS-PROMO-END-CSR                                   
               UNTIL PS-END-OF-PROMO-END-CSR                                    
           PERFORM C410-CLOSE-PROMO-END-CSR.                                    
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
TA3304     PERFORM D150-COMMIT.                                                 
TA3304                                                                          
           DISPLAY 'PROMO START CSR FETCHES FROM ORACLE    = '          .       
                                                   PA-PROMO-STRT-CNT.           
           DISPLAY 'PROMO END CSR FETCHES FROM ORACLE      = '          .       
                                                   PA-PROMO-END-CNT.            
           DISPLAY 'STRT PROMO ITM CSR FETCHES FROM ORACLE = '                  
                                                   PA-STRT-ITM-CNT.             
           DISPLAY 'END PROMO ITM CSR FETCHES FROM ORACLE  = '                  
                                                   PA-END-ITM-CNT.              
           DISPLAY 'NUMBER OF EXCLUSIONS FOUND             = '                  
                                                   PA-EXCLN-CNT.                
           DISPLAY 'RECORDS UPDATED ON PROMO_ITM TBLE      = '                  
                                                   PA-RECS-UPDATED.             
TA3304     DISPLAY 'NUMBER OF COMMITS                      = '                  
TA3304                                             PA-COMMIT-CNT.               
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * A300-PROCESS-INSERT                                           *         
      * ==> PROCESSES:                                                *         
      *  - LOOP THROUGH THE TEMP ARRAY AND INSERT INTO TEMP TABLE     *         
      *  - COMMIT WHAT WAS JUST INSERTED INTO THE TEMP TABLE          *         
      * ==> PERFORMED FROM:  A100-MAINLINE, C320-PROCESS-ITM-CSR      *         
      *****************************************************************         
       A300-PROCESS-INSERT.                                                     
                                                                                
           MOVE PC-ONE TO PV-INDEX.                                             
           PERFORM D110-INSERT-INTO-TEMP-TBL                                    
             UNTIL PV-INDEX > PM801-INDEX.                                      
           PERFORM D150-COMMIT.                                                 
                                                                                
      *****************************************************************         
      * A400-PROCESS-TEMP-TBL                                         *         
      * ==> PROCESSES:                                                *         
      *  - ADD ALL THE ITM_OH_QTY IN THE PMTT_ACCM_OH_QTY TABLE       *         
      *  - UPDATE THE ACTL_PREP_UNT_QTY ON THE PMT_PROMO_ITM_TBLE     *         
      *    WITH THE SUM FROM THE PMTT_ACCM_OH_QTY TABLE               *         
      *  - DO A COMMIT FOR THE UPDATE TO PMT_PROMO_ITM TABLE          *         
      *  - IF THERE ARE ANY ITEMS ON THE PMTT_ACCM_OH_QTY THEN        *         
      *    TRUNC ALL ITEMS FROM THE TABLE                             *         
      *  - DO A COMMIT FOR THE TRUNC                                  *         
      * ==> PERFORMED FROM:  A100-MAINLINE, C320-PROCESS-ITM-CSR      *         
      *****************************************************************         
       A400-PROCESS-TEMP-TBL.                                                   
                                                                                
           SET PS-NOT-END-OF-EXCLN-CSR TO TRUE.                                 
           PERFORM C130-OPEN-EXCLN-CSR.                                         
           PERFORM C230-FETCH-EXCLN-CSR.                                        
           PERFORM C330-PROCESS-EXCLN-CSR                                       
               UNTIL PS-END-OF-EXCLN-CSR.                                       
           PERFORM C430-CLOSE-EXCLN-CSR.                                        
                                                                                
           PERFORM D120-SUMM-TEMP-TBL.                                          
           IF PS-OH-QTY-ROWS                                                    
               IF PS-NOT-END-OF-PROMO-STRT-CSR                                  
                   PERFORM D130-UPDATE-ACT-PROMO-ITM-TBL                        
               ELSE                                                             
                   PERFORM D135-UPDATE-EOH-PROMO-ITM-TBL                        
               END-IF                                                           
               PERFORM D150-COMMIT                                              
           END-IF.                                                              
           PERFORM D140-TRUNC-TEMP-TBL.                                         
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD                                      *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  RUN-DATE-FILE.                                           
                                                                                
      *****************************************************************         
      * B200-READ-RUN-DATE-FILE.                                      *         
      * ==> PROCESSES:                                                *         
      *   - READ IN THE CONTROL CARD                                  *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       B200-READ-RUN-DATE-FILE.                                                 
                                                                                
           READ RUN-DATE-FILE INTO CC-RUN-DATE-FILE                             
              AT END                                                            
                 SET PS-RUN-DATE-FILE-END TO TRUE                               
           END-READ.                                                            
                                                                                
           IF PS-RUN-DATE-FILE-END                                              
               MOVE 'NO RUN DATE FILE TO PROCESS'                               
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               MOVE CC-RUN-DATE TO PM801-RUN-DATE                               
               DISPLAY 'CC-RUN-DATE = ' CC-RUN-DATE                             
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-RUN-DATE-FILE.                                      *         
      * ==> PROCESSES:                                                *         
      *   - READ ORCALE CONTROL CARD                                  *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       B300-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM B310-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000*        PERFORM 9900-ORACLE-ABEND                                        
               PERFORM Z100-ABEND                                               
021200     END-IF.                                                              
                                                                                
      *****************************************************************         
      *  B310-READ-ORACLE-LOGON.                                      *         
      * ==> PROCESSES:                                                *         
      *   - READ ORCALE CONTROL CARD                                  *         
      * ==> PERFORMED FROM:  B300-ORACLE-SETUP                        *         
      *****************************************************************         
       B310-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD FILE                                *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  RUN-DATE-FILE.                                                
                                                                                
      *****************************************************************         
      * C100-OPEN-PROMO-STRT-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE PROMO START CSR                                   *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C100-OPEN-PROMO-STRT-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN STRTPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C100-OPEN-PROMO-STRT-CSR      '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON STRTPROMOCSR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-OPEN-PROMO-END-CSR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE PROMO END CSR                                     *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C110-OPEN-PROMO-END-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN ENDPROMOCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C110-OPEN-PROMO-END-CSR      '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON ENDPROMOCSR '                            
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-OPEN-ITM-CSR.                                            *         
      * ==> PROCESSES:                                                *         
      *   - OPEN THE ITEM CURSOR                                      *         
      * ==> PERFORMED FROM:  C200-FETCH-PROMO-STRT-CSR                *         
      *      C210-FETCH-PROMO-END-CSR                                 *         
      *****************************************************************         
       C120-OPEN-ITM-CSR.                                                       
                                                                                
           EXEC SQL                                                             
                 OPEN ITMCSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C120-OPEN-ITM-CSR      '                               
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON ITMCSR '                                 
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C130-OPEN-EXCLN-CSR.                                          *         
      * ==> PROCESSES:                                                *         
      *   - OPEN THE EXCLN CURSOR                                     *         
      * ==> PERFORMED FROM:  A400                                     *         
      *****************************************************************         
       C130-OPEN-EXCLN-CSR.                                                     
                                                                                
           EXEC SQL                                                             
                 OPEN EXCLNCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C130-OPEN-EXCLN-CSR '                                  
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON EXCLNCSR'                                
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-FETCH-PROMO-STRT-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  -FETCH THE STRTPROMOCSR CURSOR.                              *         
      *  -IF DATA IS FOUND FIND THE ITEMS ASSOCIATED WITH THE PROMO   *         
      * ==> SQLCODE:                                                  *         
      *  -1403  = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C300-PROCESS-PROMO-STRT-CSR*         
      *****************************************************************         
       C200-FETCH-PROMO-STRT-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH STRTPROMOCSR                                               
                INTO  :PV-PROMO-ID                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   ADD PC-ONE                  TO PA-PROMO-STRT-CNT             
                   SET PS-NOT-END-OF-ITM-CSR   TO TRUE                          
                   MOVE ZERO                   TO PA-ITM-CNT                    
                   PERFORM C120-OPEN-ITM-CSR                                    
                   PERFORM C220-FETCH-ITM-CSR                                   
                   PERFORM C320-PROCESS-ITM-CSR                                 
                       UNTIL PS-END-OF-ITM-CSR                                  
                   PERFORM C420-CLOSE-ITM-CSR                                   
                                                                                
                   IF PA-ITM-CNT > 0                                            
                       PERFORM A400-PROCESS-TEMP-TBL                            
                   END-IF                                                       
                   ADD PA-ITM-CNT              TO PA-STRT-ITM-CNT               
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' PROMO START COUNT FROM FETCH = '                   
                                                    PA-PROMO-STRT-CNT           
                   IF PA-PROMO-STRT-CNT = ZEROES                                
                       DISPLAY 'NO PROMOTIONS START TOMORROW - ACTUAL'          
                       DISPLAY ' PREP UNIT CALCULATION DID NOT RUN.'            
                   ELSE                                                         
                       DISPLAY 'END OF PROMO CSR'                               
                   END-IF                                                       
                   SET PS-END-OF-PROMO-STRT-CSR TO TRUE                         
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   DISPLAY 'PROMO START COUNT FROM FETCH = '                    
                                                    PA-PROMO-STRT-CNT           
                   MOVE 'C200-FETCH-PROMO-STRT-CSR        '                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON STRTPROMOCSR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C210-FETCH-PROMO-END-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  -FETCH THE ENDPROMOCSR CURSOR.                               *         
      *  -IF DATA IS FOUND FIND THE ITEMS ASSOCIATED WITH THE PROMO   *         
      * ==> SQLCODE:                                                  *         
      *  -1403  = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C310-PROCESS-PROMO-END-CSR *         
      *****************************************************************         
       C210-FETCH-PROMO-END-CSR.                                                
                                                                                
           DISPLAY ' C210 START DATE = ' PV-PROMO-END-STRT-TMST.                
           DISPLAY ' C210 END DATE   = ' PV-PROMO-END-END-TMST.                 
                                                                                
                                                                                
           EXEC SQL                                                             
               FETCH ENDPROMOCSR                                                
                INTO  :PV-PROMO-ID                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   ADD PC-ONE                  TO PA-PROMO-END-CNT              
                   SET PS-NOT-END-OF-ITM-CSR   TO TRUE                          
                   MOVE ZERO                   TO PA-ITM-CNT                    
                   PERFORM C120-OPEN-ITM-CSR                                    
                   PERFORM C220-FETCH-ITM-CSR                                   
                   PERFORM C320-PROCESS-ITM-CSR                                 
                       UNTIL PS-END-OF-ITM-CSR                                  
                   PERFORM C420-CLOSE-ITM-CSR                                   
                                                                                
                   IF PA-ITM-CNT > 0                                            
                       PERFORM A400-PROCESS-TEMP-TBL                            
                   END-IF                                                       
                   ADD PA-ITM-CNT              TO PA-END-ITM-CNT                
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PROMO END COUNT FROM FETCH = '                      
                                                     PA-PROMO-END-CNT           
                   IF PA-PROMO-END-CNT = ZEROES                                 
                       DISPLAY 'NO PROMOTIONS ENDED TODAY - ENDING ON'          
                       DISPLAY ' HAND QUANTITY CALCULATION DID NOT RUN.'        
                   ELSE                                                         
                       DISPLAY 'END OF PROMO END CSR'                           
                   END-IF                                                       
                   SET PS-END-OF-PROMO-END-CSR TO TRUE                          
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   DISPLAY 'PROMO END COUNT FROM FETCH = '                      
                                                     PA-PROMO-END-CNT           
                   MOVE 'C210-FETCH-PROMO-END-CSR        '                      
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON ENDPROMOCSR   '                         
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C220-FETCH-ITM-CSR.                                           *         
      * ==> PROCESSES:                                                *         
      *  -FETCH THE ITMPROMOCSR CURSOR.                               *         
      * ==> SQLCODE:                                                  *         
      *  -1403  = NO DATA WAS FOUND                                   *         
      * ==> PERFORMED FROM:  C200-FETCH-PROMO-STRT-CSR,               *         
      *       C210-FETCH-PROMO-END-CSR                                *         
      *****************************************************************         
       C220-FETCH-ITM-CSR.                                                      
                                                                                
           EXEC SQL                                                             
               FETCH ITMCSR                                                     
                INTO  :PV-PROMO-ITM-ID                                          
                     ,:PM801-HIER-LVL-CDE                                       
                     ,:PM801-DEPT-NBR         :PV-DEPT-NBR-NULL                 
                     ,:PM801-MCL-NBR          :PV-MAJ-CL-NBR-NULL               
                     ,:PM801-SCL-NBR          :PV-SUB-CL-NBR-NULL               
                     ,:PM801-VS-ID            :PV-VS-ID-NULL                    
                     ,:PM801-SKU-NBR          :PV-SKU-NBR-NULL                  
                     ,:PV-MDSE-SELN-LNE-ID    :PV-MDSE-SELN-LNE-ID-NULL         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE CC-RUN-DATE TO PM801-RUN-DATE                           
                   ADD PC-ONE TO PA-ITM-CNT                                     
                   IF PA-ITM-CNT = PC-ONE                                       
                       MOVE PV-PROMO-ITM-ID  TO PV-PREV-PROMO-ITM-ID            
                   END-IF                                                       
                                                                                
161213             ADD 1 TO PA-RECS-WRITTEN                                     
161213             ADD 1 TO PA-KEEP-TRACK                                       
161213            IF PA-KEEP-TRACK = 500000                                     
161213             MOVE 0 TO PA-KEEP-TRACK                                      
161213             DISPLAY 'PROCESSING ' PA-RECS-WRITTEN ' PROMO ITEMS'         
161213            END-IF                                                        
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' ITEM COUNT FROM FETCH = ' PA-ITM-CNT               
                   DISPLAY ' END OF ITEM CSR'                                   
                   SET PS-END-OF-ITM-CSR TO TRUE                                
                                                                                
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-ITM-ID = ' PV-PROMO-ITM-ID                   
                   DISPLAY ' ITEM COUNT FROM FETCH = ' PA-ITM-CNT               
                   MOVE 'C220-FETCH-ITM-CSR        '                            
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON ITMCSR   '                              
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C230-FETCH-EXCLN-CSR                                          *         
      * ==> PROCESSES:                                                *         
      *  -FETCH THE ITMPROMOCSR CURSOR.                               *         
      * ==> SQLCODE:                                                  *         
      *  -1403  = NO DATA WAS FOUND                                   *         
      * ==> PERFORMED FROM:  C200-FETCH-PROMO-STRT-CSR,               *         
      *       C210-FETCH-PROMO-END-CSR                                *         
      *****************************************************************         
       C230-FETCH-EXCLN-CSR.                                                    
                                                                                
           EXEC SQL                                                             
               FETCH EXCLNCSR                                                   
                INTO  :PM801-HIER-LVL-CDE                                       
                     ,:PM801-DEPT-NBR         :PV-DEPT-NBR-NULL                 
                     ,:PM801-MCL-NBR          :PV-MAJ-CL-NBR-NULL               
                     ,:PM801-SCL-NBR          :PV-SUB-CL-NBR-NULL               
                     ,:PM801-VS-ID            :PV-VS-ID-NULL                    
                     ,:PM801-SKU-NBR          :PV-SKU-NBR-NULL                  
                     ,:PV-MDSE-SELN-LNE-ID    :PV-MDSE-SELN-LNE-ID-NULL         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
AJ1107             MOVE CC-RUN-DATE TO PM801-RUN-DATE                           
                   ADD PC-ONE TO PA-EXCLN-CNT                                   
                                                                                
               WHEN SQLCODE = 1403                                              
      *            DISPLAY ' EXCLN COUNT FROM FETCH = ' PA-EXCLN-CNT            
      *            DISPLAY ' END OF EXCLNCSR'                                   
                   SET PS-END-OF-EXCLN-CSR TO TRUE                              
                                                                                
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-ITM-ID = ' PV-PROMO-ITM-ID                   
                   DISPLAY ' EXCLN COUNT FROM FETCH = ' PA-EXCLN-CNT            
                   MOVE 'C230-FETCH-EXCLN-CSR    '                              
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON EXCLNCSR '                              
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C300-PROCESS-PROMO-STRT-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - RESET PROMO SELECT VARIABLES                               *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT.                 *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C300-PROCESS-PROMO-STRT-CSR.                                             
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C200-FETCH-PROMO-STRT-CSR.                                   
                                                                                
      *****************************************************************         
      * C310-PROCESS-PROMO-END-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - RESET PROMO SELECT VARIABLES                               *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT.                 *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C310-PROCESS-PROMO-END-CSR.                                              
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C210-FETCH-PROMO-END-CSR.                                    
                                                                                
      *****************************************************************         
      * C320-PROCESS-ITM-CSR.                                         *         
      * ==> PROCESSES:                                                *         
      *  - IF THE NEXT PROMO ITM DOES NOT MATCH THE PREV PROMO ITM    *         
      *    PROCESS TEMP TABLE                                         *         
      *  - CALL PRG MMKUT091 TO GET THE SKU'S AND THERE ON HAND QTY   *         
      *    FOR EACH CURRENT PROMO ITEM                                *         
      *  - INSERT THE ARRAY BROUGHT BACK FROM PMKUT091 INTO           *         
      *    PMTT_ACCM_OH_QTY                                           *         
      *  - REINITALIZE THE VARAIBLES IN PMRD801 AND MOVE THE CURRENT  *         
      *    VARIABLES TO THE PREV VARIABLES                            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT.                 *         
      * ==> PERFORMED FROM: C200-FETCH-PROMO-STRT-CSR,                *         
      *       C210-FETCH-PROMO-END-CSR                                *         
      *****************************************************************         
       C320-PROCESS-ITM-CSR.                                                    
                                                                                
           IF NOT(PV-PROMO-ITM-ID = PV-PREV-PROMO-ITM-ID)                       
               PERFORM A400-PROCESS-TEMP-TBL                                    
           END-IF.                                                              
                                                                                
           SET PM801-GET-ONHAND-QTY TO TRUE.                                    
           CALL PC-PMKUT091-PGM USING PM801-PARAMETERS.                         
           IF  PM801-UNSUCCESSFUL-CALL                                          
               MOVE PM801-ERROR-MESSAGE   TO PV-OPERATION-MSG                   
               MOVE PM801-SQLCODE         TO PV-SQL                             
               MOVE PM801-SQLCODE         TO SQLCODE                            
               MOVE 'TSKXREF'             TO PV-TABLE-NAME                      
               MOVE 'C220-FETCH-ITM-CSR' TO PV-PARAGRAPH-NAME                   
               PERFORM Z100-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM A300-PROCESS-INSERT.                                         
                                                                                
           INITIALIZE PM801-IO-PARMS                                            
                      PM801-GET-QTY-OUTPUT-PARMS.                               
                                                                                
           MOVE PV-PROMO-ITM-ID      TO  PV-PREV-PROMO-ITM-ID.                  
           MOVE PV-MDSE-HIER-CDE     TO  PV-PREV-MDSE-HIER-CDE.                 
           MOVE PV-DEPT-NBR          TO  PV-PREV-DEPT-NBR.                      
           MOVE PV-MAJ-CL-NBR        TO  PV-PREV-MAJ-CL-NBR.                    
           MOVE PV-SUB-CL-NBR        TO  PV-PREV-SUB-CL-NBR.                    
           MOVE PV-VS-ID             TO  PV-PREV-VS-ID.                         
           MOVE PV-SKU-NBR           TO  PV-PREV-SKU-NBR.                       
           MOVE PV-MDSE-SELN-LNE-ID TO  PV-PREV-MDSE-SELN-LNE-ID.               
                                                                                
           PERFORM C220-FETCH-ITM-CSR.                                          
                                                                                
      *****************************************************************         
      * C330-PROCESS-EXCLN-CSR.                                       *         
      * ==> PROCESSES:                                                *         
      * ==> PERFORMED FROM: A400                                      *         
      *****************************************************************         
       C330-PROCESS-EXCLN-CSR.                                                  
                                                                                
           SET PM801-GET-ONHAND-QTY TO TRUE.                                    
           CALL PC-PMKUT091-PGM USING PM801-PARAMETERS.                         
           IF  PM801-UNSUCCESSFUL-CALL                                          
               MOVE PM801-ERROR-MESSAGE      TO PV-OPERATION-MSG                
               MOVE PM801-SQLCODE            TO PV-SQL                          
               MOVE PM801-SQLCODE            TO SQLCODE                         
               MOVE 'TSKXREF'                TO PV-TABLE-NAME                   
               MOVE 'C330-PROCESS-EXCLN-CSR' TO PV-PARAGRAPH-NAME               
               PERFORM Z100-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE PC-ONE TO PV-INDEX.                                             
           PERFORM D115-DELETE-INTO-TEMP-TBL                                    
             UNTIL PV-INDEX > PM801-INDEX.                                      
           PERFORM D150-COMMIT.                                                 
                                                                                
           INITIALIZE PM801-IO-PARMS                                            
                      PM801-GET-QTY-OUTPUT-PARMS.                               
                                                                                
           PERFORM C230-FETCH-EXCLN-CSR.                                        
                                                                                
      *****************************************************************         
      * C400-CLOSE-PROMO-STRT-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE STRTPROMOCSR                                     *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C400-CLOSE-PROMO-STRT-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE STRTPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C400-CLOSE-PROMO-STRT-CSR      '                       
                                    TO PV-PARAGRAPH-NAME                        
                   MOVE 'CLOSE ERROR ON STRTPROMOCSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C410-CLOSE-PROMO-END-CSR.                                    *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE ENDPROMOCSR                                     *          
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      ****************************************************************          
       C410-CLOSE-PROMO-END-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE ENDPROMOCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C410-CLOSE-PROMO-END-CSR      '                        
                                    TO PV-PARAGRAPH-NAME                        
                   MOVE 'CLOSE ERROR ON ENDPROMOCSR     '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C420-CLOSE-ITM-CSR.                                           *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE ITMPROMOCSR                                      *         
      * ==> PERFORMED FROM: C200-FETCH-PROMO-STRT-CSR,                *         
      *       C210-FETCH-PROMO-END-CSR                                *         
      *****************************************************************         
       C420-CLOSE-ITM-CSR.                                                      
                                                                                
           EXEC SQL                                                             
                CLOSE ITMCSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C420-CLOSE-ITM-CSR      '                              
                                    TO PV-PARAGRAPH-NAME                        
                   MOVE 'CLOSE ERROR ON ITMCSR     '                            
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C430-CLOSE-EXCLN-CSR.                                         *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE ITMPROMOCSR                                      *         
      * ==> PERFORMED FROM: C200-FETCH-PROMO-STRT-CSR,                *         
      *       C210-FETCH-PROMO-END-CSR                                *         
      *****************************************************************         
       C430-CLOSE-EXCLN-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                CLOSE EXCLNCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C430-CLOSE-EXCLN-CSR    '                              
                                    TO PV-PARAGRAPH-NAME                        
                   MOVE 'CLOSE ERROR ON EXCLNCSR   '                            
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-FIND-TOMORROW                                            *         
      * ==> PROCESSES:                                                *         
      *   - SINCE NO TABLE IS ACTUAL USED WE USE THE DUAL TABLE.      *         
      *   - ADD ONE DAY TO THE RUN DATE.                              *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       D100-FIND-TOMORROW.                                                      
           EXEC SQL                                                             
               SELECT                                                           
                   TO_CHAR(TO_DATE(:CC-RUN-DATE) + :PC-ONE, 'YYYYMMDD')         
               INTO :PV-PROMO-DTE                                               
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                 CONTINUE                                                       
                                                                                
               WHEN OTHER                                                       
                 MOVE SQLCODE               TO PV-SQL                           
                 MOVE 'DUAL'                TO PV-TABLE-NAME                    
                 MOVE 'D100-FIND-TOMORROW'  TO PV-PARAGRAPH-NAME                
                 PERFORM Z100-ABEND                                             
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D105-CONCAT-DATES                                             *         
      * ==> PROCESSES:                                                *         
      *   - ADD A TIME STAMP TO THE START AND END DATE                *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       D105-CONCAT-DATES.                                                       
                                                                                
           STRING PV-PROMO-DTE DELIMITED BY SPACE                               
                  PC-SPACE DELIMITED BY SIZE                                    
                  PC-BEG-TMST   DELIMITED BY SIZE                               
                  INTO PV-PROMO-STRT-STRT-TMST.                                 
                                                                                
           STRING PV-PROMO-DTE DELIMITED BY SPACE                               
                  PC-SPACE DELIMITED BY SIZE                                    
                  PC-END-TMST   DELIMITED BY SIZE                               
                  INTO PV-PROMO-STRT-END-TMST.                                  
                                                                                
           STRING CC-RUN-DATE DELIMITED BY SPACE                                
                  PC-SPACE DELIMITED BY SIZE                                    
                  PC-BEG-TMST   DELIMITED BY SIZE                               
                  INTO PV-PROMO-END-STRT-TMST.                                  
                                                                                
           STRING CC-RUN-DATE DELIMITED BY SPACE                                
                  PC-SPACE DELIMITED BY SIZE                                    
                  PC-END-TMST   DELIMITED BY SIZE                               
                  INTO PV-PROMO-END-END-TMST.                                   
                                                                                
           DISPLAY 'PV-PROMO-STRT-STRT-TMST = ' PV-PROMO-STRT-STRT-TMST.        
           DISPLAY 'PV-PROMO-STRT-END-TMST  = ' PV-PROMO-STRT-END-TMST.         
           DISPLAY 'PV-PROMO-END-STRT-TMST  = ' PV-PROMO-END-STRT-TMST.         
           DISPLAY 'PV-PROMO-END-END-TMST   = ' PV-PROMO-END-END-TMST.          
                                                                                
      *****************************************************************         
      * D110-INSERT-INTO-TEMP-TBL                                     *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE ARRAY VALUES BROUGHT BACK FROM PMKUT091 TO      *         
      *    PMTT_ACCM_OH_QTY                                           *         
      * ==> PERFORMED FROM:  C320-PROCESS-ITM-CSR                     *         
      *****************************************************************         
       D110-INSERT-INTO-TEMP-TBL.                                               
                                                                                
           MOVE PM801-SKU-NBR-OUT(PV-INDEX) TO PV-SKU-NBR-OUT.                  
           MOVE PM801-ONHAND-QTY(PV-INDEX) TO PV-ONHAND-QTY-OUT.                
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMTT_ACCM_OH_QTY                                     
                   (SKU_NBR                                                     
                   ,MDSE_SELN_ID                                                
                   ,ITM_OH_QTY)                                                 
               VALUES                                                           
                   (:PV-SKU-NBR-OUT                                             
                   ,:PV-MDSE-SELN-LNE-ID                                        
                   ,:PV-ONHAND-QTY-OUT)                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 ADD PC-ONE TO PV-INDEX                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                  TO PV-SQL                          
               MOVE 'PMTT_ACCM_OH_QTY'       TO PV-TABLE-NAME                   
               MOVE 'D110-INSERT-INTO-TEMP-TBL' TO PV-PARAGRAPH-NAME            
               STRING 'INSERT ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D115-DELETE-INTO-TEMP-TBL                                     *         
      * ==> PROCESSES:                                                *         
      *  - DELETE THE ARRAY VALUES BROUGHT BACK FROM PMKUT091 TO      *         
      *    PMTT_ACCM_OH_QTY                                           *         
      * ==> PERFORMED FROM:  C330-PROCESS-EXCLN-CSR                   *         
      *****************************************************************         
       D115-DELETE-INTO-TEMP-TBL.                                               
                                                                                
           MOVE PM801-SKU-NBR-OUT(PV-INDEX) TO PV-SKU-NBR-OUT.                  
                                                                                
           EXEC SQL                                                             
               DELETE                                                           
               FROM                                                             
                    PMTT_ACCM_OH_QTY                                            
               WHERE                                                            
                    SKU_NBR = :PV-SKU-NBR-OUT                                   
                AND MDSE_SELN_ID = :PV-MDSE-SELN-LNE-ID                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
             WHEN SQLCODE = 1403                                                
                 ADD PC-ONE TO PV-INDEX                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                  TO PV-SQL                          
               MOVE 'PMTT_ACCM_OH_QTY'       TO PV-TABLE-NAME                   
               MOVE 'D115-DELETE-INTO-TEMP-TBL' TO PV-PARAGRAPH-NAME            
               STRING 'INSERT ERROR - SQLCODE= '                                
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D120-SUMM-TEMP-TBL                                            *         
      * ==> PROCESSES:                                                *         
      *  - SUM ALL THE ITM_OH_QTY FROM PMTT_ACCM_OH_QTY               *         
      *  - THIS TABLE HAS ALL THE SKU'S ASSOCIATED TO ONE PROMO ITEM  *         
      * ==> SQLCODE:                                                  *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL - THERE ARE NO RECORDS *         
      *     IN PMTT_ACCM_OH_QTY WHICH MEANS THAT THERE ARE NO SKUS    *         
      *     ASSOCIATED WITH THE PROMO ITM.                            *         
      * ==> PERFORMED FROM:  C320-PROCESS-ITM-CSR                     *         
      *****************************************************************         
       D120-SUMM-TEMP-TBL.                                                      
                                                                                
           EXEC SQL                                                             
               SELECT DISTINCT                                                  
                   SUM(ITM_OH_QTY)                                              
               INTO                                                             
                   PV-TOTAL-ITM-OH-QTY                                          
               FROM                                                             
                   (SELECT DISTINCT                                             
                        SKU_NBR,                                                
                        ITM_OH_QTY                                              
                    FROM                                                        
                        PMTT_ACCM_OH_QTY) VT1                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 SET PS-OH-QTY-ROWS TO TRUE                                     
                                                                                
             WHEN SQLCODE = -1405                                               
                 SET PS-NO-OH-QTY-ROWS TO TRUE                                  
                 MOVE ZEROES TO PV-TOTAL-ITM-OH-QTY                             
                 DISPLAY ' THERE ARE NO RECORDS IN PMTT_ACCM_OH_QTY'            
                 DISPLAY ' FOR PROMO ITM ' PV-PREV-PROMO-ITM-ID                 
                 DISPLAY ' THEREFORE PMT_PROMO_ITM CAN NOT BE UPDATED'          
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                  TO PV-SQL                          
               MOVE 'PMTT_ACCM_OH_QTY'       TO PV-TABLE-NAME                   
               MOVE 'D120-SUMM-TEMP-TBL'     TO PV-PARAGRAPH-NAME               
               STRING 'SUM ON HAND QTY - SQLCODE= '                             
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D130-UPDATE-ACT-PROMO-ITM-TBL                                 *         
      * ==> PROCESSES:                                                *         
      *  - UPDATE THE ACTL_PREP_UNT_QTY WITH THE SUM TOTAL FROM       *         
      *    PMTT_ACCM_OH_QTY                                           *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       D130-UPDATE-ACT-PROMO-ITM-TBL.                                           
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PMT_PROMO_ITM                                                
               SET                                                              
                   ACTL_PREP_UNT_QTY = :PV-TOTAL-ITM-OH-QTY                     
               WHERE                                                            
                   PROMO_ITM_ID = :PV-PREV-PROMO-ITM-ID                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 ADD PC-ONE TO PA-RECS-UPDATED                                  
                                                                                
             WHEN SQLCODE = 1403                                                
                 DISPLAY ' NO DATA TO INSERT INTO PMT_PROMO_ITM'                
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                         TO PV-SQL                   
               MOVE 'PMTT_ACCM_OH_QTY'              TO PV-TABLE-NAME            
               MOVE 'D130-UPDATE-ACT-PROMO-ITM-TBL' TO PV-PARAGRAPH-NAME        
               STRING 'SUM ON HAND QTY - SQLCODE= '                             
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D135-UPDATE-EOH-PROMO-ITM-TBL                                 *         
      * ==> PROCESSES:                                                *         
      *  - UPDATE THE ACTL_PREP_UNT_QTY WITH THE SUM TOTAL FROM       *         
      *    PMTT_ACCM_OH_QTY                                           *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       D135-UPDATE-EOH-PROMO-ITM-TBL.                                           
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PMT_PROMO_ITM                                                
               SET                                                              
                   ACTL_EOH_INV_QTY = :PV-TOTAL-ITM-OH-QTY                      
               WHERE                                                            
                   PROMO_ITM_ID = :PV-PREV-PROMO-ITM-ID                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 ADD PC-ONE TO PA-RECS-UPDATED                                  
                                                                                
             WHEN SQLCODE = 1403                                                
                 DISPLAY ' NO DATA TO INSERT INTO PMT_PROMO_ITM'                
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                         TO PV-SQL                   
               MOVE 'PMTT_ACCM_OH_QTY'              TO PV-TABLE-NAME            
               MOVE 'D135-UPDATE-EOH-PROMO-ITM-TBL' TO PV-PARAGRAPH-NAME        
               STRING 'SUM ON HAND QTY - SQLCODE= '                             
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D140-TRUNC-TEMP-TBL                                           *         
      * ==> PROCESSES:                                                *         
      *  - TRUNC EVERYTHING FROM PMTT_ACCM_OH_QTY TO PREPARE FOR THE *          
      *    NEXT PROMO ITM.                                            *         
      * ==> SQLCODE:                                                  *         
      *  - 1403 = NO DATA FOUND                                       *         
      * ==> PERFORMED FROM:  A100-MAINLINE, C320-PROCESS-ITM-CSR      *         
      *****************************************************************         
       D140-TRUNC-TEMP-TBL.                                                     
                                                                                
           EXEC SQL EXECUTE                                                     
             BEGIN                                                              
               PMSP_TRUNC_TABLE('K_PM01.PMTT_ACCM_OH_QTY');                     
             END;                                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                 CONTINUE                                                       
                                                                                
               WHEN OTHER                                                       
                 MOVE SQLCODE                  TO PV-SQL                        
                 MOVE 'PMTT_ACCM_OH_QTY'       TO PV-TABLE-NAME                 
                 MOVE 'D140-TRUNC-TEMP-TBL'    TO PV-PARAGRAPH-NAME             
                 STRING 'TRUNC TEMP TBL - SQLCODE= '                            
                         PV-SQL DELIMITED BY SIZE INTO                          
                         PV-OPERATION-MSG                                       
                 END-STRING                                                     
                 PERFORM Z100-ABEND                                             
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D150-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> PERFORMED FROM:  A100-MAINLINE, C320-PROCESS-ITM-CSR      *         
      *****************************************************************         
       D150-COMMIT.                                                             
                                                                                
067100     EXEC SQL                                                             
067200         COMMIT                                                           
067300     END-EXEC.                                                            
067400                                                                          
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
TA3304         ADD 1 TO PA-COMMIT-CNT                                           
067600                                                                          
                                                                                
               WHEN OTHER                                                       
067800           MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG           
067900           MOVE 'D150-COMMIT'               TO PV-PARAGRAPH-NAME          
068000           PERFORM Z910-ABEND                                             
           END-EVALUATE.                                                        
068200                                                                          
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
      *    COPY PMPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
