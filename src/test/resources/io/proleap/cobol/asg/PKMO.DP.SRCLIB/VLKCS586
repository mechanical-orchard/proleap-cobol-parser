000100******************************************************************04/11/95
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************   LV002
000400                                                                          
000500 PROGRAM-ID.                VLKCS586.                                     
000600 AUTHOR.                    MICHAEL WALTERS.                              
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              06-03-96.                                     
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  CICS PROGRAM - VLKCS586 - W086                                *        
001300*                                                                *        
001400*       PO SYSTEM PRETICKET INQUIRE LIST SCREEN                  *        
001500*                                                                *        
001600*  THIS SCREEN WILL LIST THE PRETICKET REQUESTS ACCORDING TO THE *        
001700*  CRITERIA FROM VLKCS585.                                                
001800*  THE REQUESTS WILL BE LISTED IN THE FOLLOWING ORDER:                    
001900*        DEPARTMENT NUMBER            ASCENDING                           
002000*        VENDOR NAME                  ASCENDING                           
002100*        PRINT DATE                   ASCENDING                           
002200*        REQUEST DATE                 ASCENDING                           
      *---------------------------------------------------------------*         
      * DATE  11/04/2014, CHERI PARMLEY                               *         
      *   CHG0094517 EXPANDED PO# FROM 7 TO 8 DIGITS                  *         
      *---------------------------------------------------------------*         
263627*---------------------------------------------------------------*         
263627* CHANGED 11/05/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*---------------------------------------------------------------*         
CICS  * CHANGED 11/14/2022    BY JIM HUNTER      WR: CHG0278236                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *****************************************************************         
002400 ENVIRONMENT DIVISION.                                                    
002500                                                                          
002600 DATA DIVISION.                                                           
002700                                                                          
002800 WORKING-STORAGE SECTION.                                                 
002900                                                                          
003000 01  DK-DB2-KEYS.                                                         
003100     10  DK-PO-NUMBER                   PIC S9(09) VALUE +0               
003200                                                   COMP.                  
003300     10  DK-DEPT-NBR                    PIC  X(03) VALUE SPACE.           
003300     10  DK-DATE-LIMIT                  PIC  X(10) VALUE SPACE.           
003400     10  DK-ENT-ID                      PIC S9(09) VALUE +0               
003500                                                   COMP.                  
000940     10  DK-DMA-NBR                   PIC X(02)  VALUE SPACES.            
000950     10  DK-GMA-NBR                   PIC X(02)  VALUE SPACES.            
003600                                                                          
003700 01  PC-PROGRAM-CONSTANTS.                                                
022203     05  PC-DPT-MDSELV-CDE         PIC  X(03)  VALUE 'DPT'.               
022204     05  PC-OPERCO-NBR             PIC  X(02)  VALUE '03'.                
022208     05  PC-PRETICKET-TYP-CDE           PIC S9(04)  COMP                  
022209                                                    VALUE +1.             
022212     05  PC-SERVICE-AGRMT-CDE           PIC  X(01)  VALUE '4'.            
022213     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
022214                                                    'VL586A  '.           
022215     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
022216                                                    'VLKM586 '.           
022217     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
022218                                                    'VLKCS586'.           
022219     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
022220     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
022221     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
022222     05  PC-PO-INFO-SCREEN              PIC  X(01)  VALUE '2'.            
022223     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
022224                                                    COMP SYNC.            
022225     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
022226                                                    COMP SYNC.            
022227     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
022228                                                    +1000000              
022229                                                    COMP-3.               
022230     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
022231                                                    COMP SYNC.            
022232     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
022233                                                    COMP-3.               
022234     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
022235     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
022236     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
022237     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
022238     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
022239*                                                                         
022240     05  PC-TSYMSG-NUMBERS.                                               
022241         10  PC-TSYMSG-NEW              PIC  9(05)  VALUE 00005.          
022242         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
022243         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
022244         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
022245         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
022246         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
022247         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
022248         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
022249         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
022250         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
022251         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
022252         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
022253         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
022254         10  PC-TSYMSG-00083            PIC  9(05)  VALUE 00083.          
022255         10  PC-TSYMSG-00117            PIC  9(05)  VALUE 00117.          
022256         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
022257         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
022258         10  PC-TSYMSG-01321            PIC  9(05)  VALUE 01321.          
022259         10  PC-TSYMSG-01322            PIC  9(05)  VALUE 01322.          
022260         10  PC-TSYMSG-01350            PIC  9(05)  VALUE 01350.          
022261         10  PC-TSYMSG-01352            PIC  9(05)  VALUE 01352.          
022262         10  PC-TSYMSG-01391            PIC  9(05)  VALUE 01391.          
022263         10  PC-TSYMSG-01359            PIC  9(05)  VALUE 01359.          
022264         10  PC-TSYMSG-01418            PIC  9(05)  VALUE 01418.          
022264         10  PC-TSYMSG-01420            PIC  9(05)  VALUE 01420.          
022265         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
022266*                                                                         
022267 01  PS-PROGRAM-SWITCHES.                                                 
022268     05  PS-AUTH-DEPT-SW                PIC  X(01)  VALUE 'Y'.            
022269         88  PS-AUTH-FOR-DEPT                       VALUE 'Y'.            
022270         88  PS-NOT-AUTH-FOR-DEPT                   VALUE 'N'.            
022271     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
022272         88  PS-NO-ERROR                            VALUE 'Y'.            
022273         88  PS-ERROR                               VALUE 'N'.            
022274     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
022275         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
022276         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
022277*                                                                         
022278 01  PV-PROGRAM-VARIABLES.                                                
022279     05  PV-TEMP-PO-NBR                 PIC S9(09) COMP.                  
022279     05  PV-SAVE-DEPT-NBR               PIC  X(03) VALUE SPACES.          
022279     05  PV-DB2-COUNT                   PIC S9(09) COMP.                  
022280     05  PV-SHIP-DATE-COUNT             PIC S9(09) COMP.                  
022281     05  PV-DISPLAY-ABEND-MESSAGE       PIC  X(79) VALUE SPACES.          
022282     05  PV-DISPLAY-COMP1               PIC  9(09) VALUE ZERO.            
022283     05  PV-DISPLAY-COMP2               PIC  9(09) VALUE ZERO.            
022284     05  PV-DISPLAY-COMP3               PIC  9(09) VALUE ZERO.            
022285     05  PV-WORK-DATE.                                                    
022286         10  PV-WORK-CC                 PIC XX.                           
022287         10  PV-WORK-YY                 PIC XX.                           
022288         10  FILLER                     PIC X.                            
022289         10  PV-DISPLAY-DATE.                                             
022290             15 PV-WORK-MM              PIC XX.                           
022291             15 FILLER                  PIC X.                            
022292             15 PV-WORK-DD              PIC XX.                           
022293     05  PV-REQUEST-TICKET-TOTAL        PIC S9(09)  VALUE +0              
022294                                                    COMP SYNC.            
022295     05  PV-SUB                         PIC S9(04)  VALUE +0              
022296                                                    COMP SYNC.            
022297     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
022298                                                    COMP SYNC.            
022299     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
022300                                                    COMP-3.               
022301     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
022302                                                    COMP-3.               
022303     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
022304                                                    COMP-3.               
022305     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
022306                                                    COMP-3.               
022307     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
022308                                                    COMP-3.               
022309     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
022310                                                    COMP-3.               
022311     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
022312                                                    COMP-3.               
022313     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
022314                                                    COMP SYNC.            
022315     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
022316                                                    COMP SYNC.            
022317     05  PV-ITEM                        PIC S9(04)  VALUE +0              
022318                                                    COMP SYNC.            
022319     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
022320                                                    COMP SYNC.            
022321     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
022322                                                    COMP SYNC.            
022323     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
022324                                                    COMP SYNC.            
022325     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
022326                                                    COMP SYNC.            
022327     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
022328                                                    COMP SYNC.            
022329     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
022330                                                    COMP SYNC.            
022331     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
022332                                                    COMP SYNC.            
022333     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
022334                                                    COMP SYNC.            
022335     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
022336                                                    COMP SYNC.            
022337     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
022338                                                    COMP SYNC.            
022339     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
022340                                                    COMP SYNC.            
022341     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
022342                                                    COMP SYNC.            
022343     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
022344                                                    COMP SYNC.            
022345******************************************************************        
022346**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
022347**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
022348**  APPLICATION-SPECIFIC COMMAREA.                              **        
022349******************************************************************        
022350 01  LQW-LIST-QUEUE-WORK-AREA.                                            
022351     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
022352     05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES                   
022353                                        INDEXED BY LQW-IDX.               
022354         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
022355         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
022356         10  LQW-SELECTED-SW            PIC  X(01).                       
022357             88  LQW-SELECT                         VALUE 'S'.            
022358             88  LQW-DESELECT                       VALUE ' '.            
022359         10  LQW-ACTION                 PIC  X(03).                       
022360         10  LQW-PO-NUMBER              PIC  9(09).                       
022361         10  LQW-RQST-NBR               PIC S9(09) COMP.                  
022362         10  LQW-RQST-TYPE              PIC  X(01).                       
022363         10  LQW-RQST-STAT              PIC  X(01).                       
022364         10  LQW-VENDOR-NAME            PIC  X(15).                       
022365         10  LQW-ENT-ID                 PIC S9(09) COMP.                  
022366         10  LQW-DEPT-NBR               PIC  X(03).                       
022367         10  LQW-RQST-DATE              PIC  X(10).                       
022368         10  LQW-PRINT-DATE             PIC  X(10).                       
022369         10  LQW-SHIP-DATE              PIC  X(10).                       
022370         10  LQW-RQSTD-TICKETS          PIC S9(07) COMP.                  
022371                                                                          
022372     EJECT                                                                
022373*---------------------------------------------------------------*         
022374*  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
022375*---------------------------------------------------------------*         
022376                                                                          
022377     COPY VLWS006.                                                        
022378                                                                          
022379     EJECT                                                                
022380*---------------------------------------------------------------*         
022381*    MAP LAYOUT                                                 *         
022382*---------------------------------------------------------------*         
022383                                                                          
022384     COPY VLKM586.                                                        
022385*                                                                         
022386 01  MR-OCCURS-LAYOUT                   REDEFINES  VL586AO.               
022387     05  FILLER                         PIC X(42).                        
022388     05  MR-SCROLL-AREA                 OCCURS 13 TIMES.                  
022389         10  MR-SELECTL                 PIC S9(04) COMP.                  
022390         10  MR-SELECTA                 PIC  X(01).                       
022391         10  MR-SELECTC                 PIC  X(01).                       
022392         10  MR-SELECTH                 PIC  X(01).                       
022393         10  MR-SELECTI                 PIC  X(01).                       
022394                                                                          
022395         10  MR-ACTIONL                 PIC S9(04) COMP.                  
022396         10  MR-ACTIONA                 PIC  X(01).                       
022397         10  MR-ACTIONC                 PIC  X(01).                       
022398         10  MR-ACTIONH                 PIC  X(01).                       
022399         10  MR-ACTION                  PIC  X(03).                       
022400                                                                          
022401         10  FILLER                     PIC  X(05).                     86
      *CLP 11/04/14                                                             
022402         10  MR-PO-NUMBER.                                                
022403             15  MR-PO-NUMBER-N         PIC  9(08).                       
022404                                                                          
022405         10  FILLER                     PIC  X(05).                     86
022500         10  MR-RQST-TYPE               PIC  X(01).                       
022600                                                                          
022700         10  MR-RQST-STATL              PIC S9(04) COMP.                  
022800         10  MR-RQST-STATA              PIC  X(01).                       
022900         10  MR-RQST-STATC              PIC  X(01).                       
023000         10  MR-RQST-STATH              PIC  X(01).                       
023100         10  MR-RQST-STAT               PIC  X(01).                       
023200                                                                          
023300         10  FILLER                     PIC  X(05).                     86
023400         10  MR-VENDOR-NAME             PIC  X(15).                       
023500                                                                          
023600         10  FILLER                     PIC  X(05).                     86
023700         10  MR-DPT-NBR                 PIC  X(03).                       
023800                                                                          
023900         10  FILLER                     PIC  X(05).                     86
024000         10  MR-RQST-DATE               PIC  X(05).                       
024100                                                                          
024200         10  FILLER                     PIC  X(05).                     86
024300         10  MR-PRINT-DATE              PIC  X(05).                       
024400                                                                          
024500         10  FILLER                     PIC  X(05).                     86
024600         10  MR-SHIP-DATE               PIC  X(05).                       
024700                                                                          
024800         10  FILLER                     PIC  X(05).                     86
024900         10  MR-RQSTD-TICKETS-X.                                          
025000             15  MR-RQSTD-TICKETS       PIC  Z,ZZZ,ZZ9.                   
025100     EJECT                                                                
025200*---------------------------------------------------------------*         
025300*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
025400*---------------------------------------------------------------*         
025500                                                                          
025600     COPY DPWS015.                                                        
025700                                                                          
025800*---------------------------------------------------------------*         
025900*    FUNCTION KEYS COPYBOOK.                                    *         
026000*---------------------------------------------------------------*         
026100                                                                          
026200     COPY DPWS016.                                                        
026300                                                                          
026400*---------------------------------------------------------------*         
026500*    COPYBOOK FOR THE CALL TO RACF SECURITY SUBROUTINE          *         
026600*---------------------------------------------------------------*         
026700                                                                          
026800     COPY DPWS008.                                                        
026801                                                                          
026802*---------------------------------------------------------------*         
026803*    NUMERIC EDIT LAYOUT.                                       *         
026804*---------------------------------------------------------------*         
026805                                                                          
026806     COPY DPWS010I.                                                       
026900 EJECT                                                                    
027000*---------------------------------------------------------------*         
027100*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
027200*---------------------------------------------------------------*         
027300                                                                          
027400     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
027500 EJECT                                                                    
027600*---------------------------------------------------------------*         
027700*    STANDARD COMMAREA.                                         *         
027800*---------------------------------------------------------------*         
027900                                                                          
028000     COPY DPWS020.                                                        
028100     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
028200                                                                          
028300*---------------------------------------------------------------*         
028400* INTER-APPLICATION COMMAREA.                                   *         
028500*   DPWS054 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    *         
028600*             SPEC SCREEN.                                      *         
028700*   DPWS056 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
028800*             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    *         
028900*---------------------------------------------------------------*         
029000                                                                          
029100         10  INTER-APPL-COMM-AREA       PIC X(200).                       
029200     COPY VLWS005.                                                        
029300*---------------------------------------------------------------*         
029400*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
029500*---------------------------------------------------------------*         
029600         10  ASC-SPECIFIC-COMMAREA.                                       
029700             15  ASC-LIST-KEYS.                                           
029800                 20  ASC-KEY-DEPT-NBR                PIC  X(03).          
029900                 20  ASC-KEY-PO-NUMBER               PIC S9(09)           
030000                                                     COMP.              86
030100                 20  ASC-KEY-ENT-ID                  PIC S9(09)           
030200                                                     COMP.              86
030300                 20  ASC-KEY-VENDOR-NAME             PIC  X(30).          
030300                 20  ASC-KEY-DATE-LIMIT              PIC  X(10).          
030300                 20  ASC-CURRENT-DATE                PIC  X(10).          
030400             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
030500                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
030600                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
030700                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
030800                                                                          
030900                 20  ASC-ITEM-ARRAY.                                      
031000                     25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.                
031100                         30  ASC-LIST-ITEM-NUMBER                         
031200                                                     PIC S9(09)           
031300                                                     COMP-3.              
031400                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
031500                             88  ASC-VALID-SELECT    VALUE ' '            
031600                                                           'S'            
031700                                                           'B'            
031800                                                           'E'.           
031900                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
032000                             88  ASC-TEMP-SELECT     VALUE 'S'.           
032100                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
032200                             88  ASC-TEMP-END        VALUE 'E'.           
032300                         30  ASC-SELECTED-SW         PIC  X(01).          
032400                             88  ASC-NO-SELECT       VALUE ' '.           
032500                             88  ASC-SELECT          VALUE 'S'.           
032600                             88  ASC-BEGIN           VALUE 'B'.           
032700                             88  ASC-END             VALUE 'E'.           
032800                         30  ASC-ACTION              PIC  X(03).          
032900                         30  ASC-PO-NUMBER           PIC  9(09).          
033000                         30  ASC-RQST-NBR            PIC S9(09)           
033100                                                     COMP.                
033200                         30  ASC-RQST-TYPE           PIC  X(01).          
033300                         30  ASC-RQST-STAT           PIC  X(01).          
033400                             88 ASC-ADD-REQ-STAT                          
033500                                VALUE 'I', 'P', 'S', 'C'.                 
033600                             88 ASC-UPD-REQ-STAT                          
033700                                VALUE 'A', 'R', 'H'.                      
033800                             88 ASC-DEL-REQ-STAT                          
033900                                VALUE 'A', 'R', 'H', 'P', 'S'.            
034000                         30  ASC-VENDOR-NAME         PIC  X(15).          
034100                         30  ASC-ENT-ID              PIC S9(09)           
034200                                                         COMP.            
034300                         30  ASC-DEPT-NBR            PIC  X(03).          
034400                         30  ASC-RQST-DATE           PIC  X(10).          
034500                         30  ASC-PRINT-DATE          PIC  X(10).          
034600                         30  ASC-SHIP-DATE           PIC  X(10).          
034700                         30  ASC-RQSTD-TICKETS       PIC S9(07)           
034800                                                         COMP.            
034900                                                                          
035000             15  ASC-BLK-SUB                         PIC S9(04)           
035100                                                          COMP            
035200                                                          SYNC.           
035300             15  ASC-ITEM-SUB                        PIC S9(04)           
035400                                                          COMP            
035500                                                          SYNC.           
035600             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
035700                                                          COMP-3.         
035800             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
035900                                                          COMP-3.         
036000             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
036100                                                          COMP-3.         
036200             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
036300                                                          COMP-3.         
036400             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
036500                                                          COMP-3.         
036600             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
036700                                                          COMP            
036800                                                          SYNC.           
036900             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
037000                                                          COMP-3.         
037100             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
037200                                                          COMP-3.         
037300             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
037400                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
037500                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
037600             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
037700                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
037800                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
037900             15  ASC-LIST-QUEUE-NAME.                                     
038000                 20  ASC-LIST-TERM-ID                PIC  X(04).          
038100                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
038200                                                          COMP-3.         
038300                 20  ASC-LIST-SEQ                    PIC  9(01).          
038400             15  ASC-SEL-QUEUE-NAME.                                      
038500                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
038600                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
038700                                                          COMP-3.         
038800                 20  ASC-SEL-SEQ                     PIC  9(01).          
038900                                                                          
039000             15  ASC-CURSOR-SW                       PIC  X(01).          
039100                 88  ASC-NO-CURSOR                   VALUE ' '.           
039200                 88  ASC-PO-CURSOR                   VALUE 'P'.           
039300                 88  ASC-DPT-CURSOR                  VALUE 'D'.           
039400                 88  ASC-VND-CURSOR                  VALUE 'V'.           
039410                 88  ASC-VLVND-CURSOR                VALUE 'L'.           
039500                 88  ASC-DPTVND-CURSOR               VALUE 'B'.           
039600             15  ASC-START-ITEM-REQUEST.                                  
039700                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
039800     EJECT                                                                
039900*---------------------------------------------------------------*         
040000*    ABEND PROCESSING COPYBOOK.                                 *         
040100*---------------------------------------------------------------*         
040200                                                                          
040300     COPY DPWS013.                                                        
040400                                                                          
040500 EJECT                                                                    
040600*---------------------------------------------------------------*         
040700*    DB2 AREA FOR TMESSAGE                                      *         
040800*---------------------------------------------------------------*         
040900     EXEC SQL                                                             
041000          INCLUDE TSYMSG                                                  
041100     END-EXEC.                                                            
041200*---------------------------------------------------------------*         
041300*    DB2 AREA FOR COMMUNICATIONS                                *         
041400*---------------------------------------------------------------*         
041500     EXEC SQL                                                             
041600          INCLUDE SQLCA                                                   
041700     END-EXEC.                                                            
041800                                                                          
041900*---------------------------------------------------------------*         
042000*    DB2 AREA THE PURCHASE ORDER TABLE                          *         
042100*---------------------------------------------------------------*         
042200     EXEC SQL                                                             
042300          INCLUDE TPURCHS                                                 
042400     END-EXEC.                                                            
042401                                                                          
042402*---------------------------------------------------------------*         
042403*    DB2 AREA THE PURCHASE ORDER LINE TABLE                     *         
042404*---------------------------------------------------------------*         
042405     EXEC SQL                                                             
042406          INCLUDE TPURITM                                                 
042407     END-EXEC.                                                            
042401                                                                          
042402*---------------------------------------------------------------*         
042403*    DB2 AREA THE PLNSHP                                        *         
042404*---------------------------------------------------------------*         
042405     EXEC SQL                                                             
042406          INCLUDE TPLNSHP                                                 
042407     END-EXEC.                                                            
042408                                                                          
042409*---------------------------------------------------------------*         
042410*    DB2 AREA THE PURCHASE ORDER AGREEMENT TABLE                *         
042411*---------------------------------------------------------------*         
042412     EXEC SQL                                                             
042413          INCLUDE TPOAGMT                                                 
042414     END-EXEC.                                                            
042415                                                                          
042416*---------------------------------------------------------------*         
042417*    DB2 AREA THE VENDOR AGREEMENT TABLE                        *         
042418*---------------------------------------------------------------*         
042419     EXEC SQL                                                             
042420          INCLUDE TVNDAGR                                                 
042421     END-EXEC.                                                            
042422                                                                          
042423*---------------------------------------------------------------*         
042424*    DB2 AREA THE PRETICKET REQUEST TABLE                       *         
042425*---------------------------------------------------------------*         
042426     EXEC SQL                                                             
042427          INCLUDE TPTKREQ                                                 
042428     END-EXEC.                                                            
042500                                                                          
042600*---------------------------------------------------------------*         
042700*    DB2 AREA THE PRETICKET REQEUST LINE TABLE                  *         
042800*---------------------------------------------------------------*         
042900     EXEC SQL                                                             
043000          INCLUDE TPTKLNE                                                 
043100     END-EXEC.                                                            
043101                                                                          
043102*---------------------------------------------------------------*         
043103*    DB2 AREA FOR THE MERCHANDISE AREA TABLE                    *         
043104*---------------------------------------------------------------*         
043105     EXEC SQL                                                             
043106          INCLUDE TMDSEAR                                                 
043107     END-EXEC.                                                            
043200                                                                          
043201*---------------------------------------------------------------*         
043202*    DB2 AREA FOR THE WORKER TABLE                              *         
043203*---------------------------------------------------------------*         
043204     EXEC SQL                                                             
043205          INCLUDE TWORKER                                                 
043206     END-EXEC.                                                            
043207                                                                          
043208*---------------------------------------------------------------*         
043209*    DB2 AREA FOR THE RESPONSIBILITY AREA TABLE                 *         
043210*---------------------------------------------------------------*         
043211     EXEC SQL                                                             
043212          INCLUDE TRSPBAR                                                 
043213     END-EXEC.                                                            
043214                                                                          
043300*---------------------------------------------------------------*         
043400*    DB2 AREA THE PRETICKET REQUEST SHIPMENT ASSOCIATION TABLE  *         
043500*---------------------------------------------------------------*         
043600     EXEC SQL                                                             
043700          INCLUDE TREQSHP                                                 
043800     END-EXEC.                                                            
043900                                                                          
044000*---------------------------------------------------------------*         
044100*    DB2 AREA THE RQUEST SHIPMENT TABLE                         *         
044200*---------------------------------------------------------------*         
044300     EXEC SQL                                                             
044400          INCLUDE TPTKSHP                                                 
044500     END-EXEC.                                                            
044600                                                                          
044700*---------------------------------------------------------------*         
044800*    DB2 AREA THE ENTERPRISE NAME TABLE                         *         
044900*---------------------------------------------------------------*         
045000     EXEC SQL                                                             
045100          INCLUDE TENTNME                                                 
045200     END-EXEC.                                                            
045300                                                                          
045400*---------------------------------------------------------------*         
045500*    REQUEST CURSOR WHEN THE DEPTARTMENT NUMBER IS SUPPLIED     *         
      *****************************************************************         
      *    THE FOLLOWING WHEN CLAUSE:                                 *         
      *           AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')        *         
      *           OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT               *         
      *           AND  A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )             *         
      *                                                               *         
      *    WILL LIMIT THE LIST TO SHOWING THE FOLLOWING REQUESTS:    *          
      *           1 - ALL THE ACTIVE REQUESTS (A,H,R,I,P)             *         
      *           2 - ALL THE CANCELLED ORDERS THAT WERE CREATED SINCE*         
      *               THE DATE LIMIT, BUT WERE NOT PRINTED            *         
      *           3 - ALL THE CANCELLED AND SHIPPED ORDERS THAT WERE  *         
      *               PRINTED SINCE THE DATE LIMIT                    *         
045600*---------------------------------------------------------------*         
045800     EXEC SQL                                                             
045900          DECLARE DPT_RQST_CURSOR CURSOR                                86
046000          FOR SELECT                                                      
046100                A.PO_REQ_NBR                                              
046200              , A.PO_NBR                                                  
046300              , A.ADDR_SEQ_NBR                                            
046400              , A.REQ_TYP_CDE                                             
046500              , A.REQ_STAT_CDE                                            
046600              , D.ENT_NAME_DESC                                           
046700              , A.ENT_ID                                                  
046800              , A.DEPT_NBR                                                
046900              , A.REQ_CREN_DTE                                            
047000              , A.ACTL_PRT_DTE                                            
047100          FROM                                                            
047200                TPTKREQ A                                               86
047300              , TENTNME D                                               86
047400          WHERE                                                           
047500                A.DEPT_NBR   = :DK-DEPT-NBR                               
047600            AND A.ENT_ID     = D.ENT_ID                                   
047700            AND D.TYPE_CDE   = '01'                                       
                  AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')                  
                  OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT                         
                  OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT)                        
047800          GROUP BY                                                        
047900                A.PO_REQ_NBR                                              
048000              , A.PO_NBR                                                  
048100              , A.ADDR_SEQ_NBR                                            
048200              , A.REQ_TYP_CDE                                             
048300              , A.REQ_STAT_CDE                                            
048400              , D.ENT_NAME_DESC                                           
048500              , A.ENT_ID                                                  
048600              , A.DEPT_NBR                                                
048700              , A.REQ_CREN_DTE                                            
048800              , A.ACTL_PRT_DTE                                            
048900          ORDER BY                                                        
049000               A.DEPT_NBR                                                 
049100             , D.ENT_NAME_DESC                                            
049200             , A.ACTL_PRT_DTE                                             
049300             , A.REQ_CREN_DTE                                             
049400     END-EXEC.                                                            
049800                                                                          
049801*---------------------------------------------------------------*         
049802*    REQUEST CURSOR WHEN THE VENDOR IS SUPPLIED                 *         
049803*---------------------------------------------------------------*         
      *    THE FOLLOWING WHEN CLAUSE:                                 *         
      *           AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')        *         
      *           OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT               *         
      *           OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )             *         
      *                                                               *         
      *    WILL LIMIT THE LIST TO SHOWING THE FOLLOWING REQUESTS:    *          
      *           1 - ALL THE ACTIVE REQUESTS (A,H,R,I,P)             *         
      *           2 - ALL THE CANCELLED ORDERS THAT WERE CREATED SINCE*         
      *               THE DATE LIMIT, BUT WERE NOT PRINTED            *         
      *           3 - ALL THE CANCELLED AND SHIPPED ORDERS THAT WERE  *         
      *               PRINTED SINCE THE DATE LIMIT                    *         
045600*---------------------------------------------------------------*         
049900     EXEC SQL                                                             
050000          DECLARE VND_RQST_CURSOR CURSOR                                86
050100          FOR SELECT                                                      
050200                A.PO_REQ_NBR                                              
050300              , A.PO_NBR                                                  
050400              , A.ADDR_SEQ_NBR                                            
050500              , A.REQ_TYP_CDE                                             
050600              , A.REQ_STAT_CDE                                            
050700              , D.ENT_NAME_DESC                                           
050800              , A.ENT_ID                                                  
050900              , A.DEPT_NBR                                                
051000              , A.REQ_CREN_DTE                                            
051100              , A.ACTL_PRT_DTE                                            
051200          FROM                                                            
051300                TPTKREQ A                                               86
051400              , TENTNME D                                               86
051500          WHERE                                                           
051600                A.ENT_ID     = :DK-ENT-ID                                 
051700            AND A.ENT_ID     = D.ENT_ID                                   
189402            AND D.TYPE_CDE   = '01'                                       
                  AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')                  
                  OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT                         
                  OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )                       
189403          GROUP BY                                                        
189404                A.PO_REQ_NBR                                              
189405              , A.PO_NBR                                                  
189406              , A.ADDR_SEQ_NBR                                            
189407              , A.REQ_TYP_CDE                                             
189408              , A.REQ_STAT_CDE                                            
189409              , D.ENT_NAME_DESC                                           
189410              , A.ENT_ID                                                  
189411              , A.DEPT_NBR                                                
189412              , A.REQ_CREN_DTE                                            
189413              , A.ACTL_PRT_DTE                                            
189414          ORDER BY                                                        
189415               A.DEPT_NBR                                                 
049200             , A.ACTL_PRT_DTE                                             
049300             , A.REQ_CREN_DTE                                             
189416     END-EXEC.                                                            
189457                                                                          
189458*---------------------------------------------------------------*         
189459*    REQUEST CURSOR WHEN THE PO IS SUPPLIED                     *         
189460*---------------------------------------------------------------*         
      *    THE FOLLOWING WHEN CLAUSE:                                 *         
      *           AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')        *         
      *           OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT               *         
      *           OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )             *         
      *                                                               *         
      *    WILL LIMIT THE LIST TO SHOWING THE FOLLOWING REQUESTS:    *          
      *           1 - ALL THE ACTIVE REQUESTS (A,H,R,I,P)             *         
      *           2 - ALL THE CANCELLED ORDERS THAT WERE CREATED SINCE*         
      *               THE DATE LIMIT, BUT WERE NOT PRINTED            *         
      *           3 - ALL THE CANCELED AND SHIPPED ORDERS THAT WERE  *          
      *               PRINTED SINCE THE DATE LIMIT                    *         
045600*---------------------------------------------------------------*         
189461     EXEC SQL                                                             
189462          DECLARE PO_RQST_CURSOR CURSOR                                 86
189463          FOR SELECT                                                      
189464                A.PO_REQ_NBR                                              
189465              , A.PO_NBR                                                  
189466              , A.ADDR_SEQ_NBR                                            
189467              , A.REQ_TYP_CDE                                             
189468              , A.REQ_STAT_CDE                                            
189469              , D.ENT_NAME_DESC                                           
189470              , A.ENT_ID                                                  
189471              , A.DEPT_NBR                                                
189472              , A.REQ_CREN_DTE                                            
189473              , A.ACTL_PRT_DTE                                            
189474          FROM                                                            
189475                TPTKREQ A                                               86
189476              , TENTNME D                                               86
189477          WHERE                                                           
189478                A.PO_NBR     = :DK-PO-NUMBER                              
189479            AND A.ENT_ID     = D.ENT_ID                                   
189480            AND D.TYPE_CDE   = '01'                                       
189481          GROUP BY                                                        
189482                A.PO_REQ_NBR                                              
189483              , A.PO_NBR                                                  
189484              , A.ADDR_SEQ_NBR                                            
189485              , A.REQ_TYP_CDE                                             
189486              , A.REQ_STAT_CDE                                            
189487              , D.ENT_NAME_DESC                                           
189488              , A.ENT_ID                                                  
189489              , A.DEPT_NBR                                                
189490              , A.REQ_CREN_DTE                                            
189491              , A.ACTL_PRT_DTE                                            
189492          ORDER BY                                                        
189493                A.ACTL_PRT_DTE, A.REQ_CREN_DTE                            
189494     END-EXEC.                                                            
189495                                                                          
189496*---------------------------------------------------------------*         
189497*    REQUEST CURSOR WHEN BOTH THE DPT AND VENDOR ARE SUPPLIED   *         
189498*---------------------------------------------------------------*         
      *    THE FOLLOWING WHEN CLAUSE:                                 *         
      *           AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')        *         
      *           OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT               *         
      *           OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )             *         
      *                                                               *         
      *    WILL LIMIT THE LIST TO SHOWING THE FOLLOWING REQUESTS:    *          
      *           1 - ALL THE ACTIVE REQUESTS (A,H,R,I,P)             *         
      *           2 - ALL THE CANCELLED ORDERS THAT WERE CREATED SINCE*         
      *               THE DATE LIMIT, BUT WERE NOT PRINTED            *         
      *           3 - ALL THE CANCELLED AND SHIPPED ORDERS THAT WERE  *         
      *               PRINTED SINCE THE DATE LIMIT                    *         
045600*---------------------------------------------------------------*         
189499     EXEC SQL                                                             
189500          DECLARE DPTVND_RQST_CURSOR CURSOR                             86
189501          FOR SELECT                                                      
189502                A.PO_REQ_NBR                                              
189503              , A.PO_NBR                                                  
189504              , A.ADDR_SEQ_NBR                                            
189505              , A.REQ_TYP_CDE                                             
189506              , A.REQ_STAT_CDE                                            
189507              , D.ENT_NAME_DESC                                           
189508              , A.ENT_ID                                                  
189509              , A.DEPT_NBR                                                
189510              , A.REQ_CREN_DTE                                            
189511              , A.ACTL_PRT_DTE                                            
189512          FROM                                                            
189513                TPTKREQ A                                               86
189514              , TENTNME D                                               86
189515          WHERE                                                           
189516                A.DEPT_NBR   = :DK-DEPT-NBR                               
189517            AND A.ENT_ID     = :DK-ENT-ID                                 
189518            AND A.ENT_ID     = D.ENT_ID                                   
189519            AND D.TYPE_CDE   = '01'                                       
                  AND (A.REQ_STAT_CDE IN ('A','H','R','I','P')                  
                  OR   A.REQ_CREN_DTE >= :DK-DATE-LIMIT                         
                  OR   A.ACTL_PRT_DTE >= :DK-DATE-LIMIT )                       
189520          GROUP BY                                                        
189521                A.PO_REQ_NBR                                              
189522              , A.PO_NBR                                                  
189523              , A.ADDR_SEQ_NBR                                            
189524              , A.REQ_TYP_CDE                                             
189525              , A.REQ_STAT_CDE                                            
189526              , D.ENT_NAME_DESC                                           
189527              , A.ENT_ID                                                  
189528              , A.DEPT_NBR                                                
189529              , A.REQ_CREN_DTE                                            
189530              , A.ACTL_PRT_DTE                                            
189492          ORDER BY                                                        
189493                A.ACTL_PRT_DTE, A.REQ_CREN_DTE                            
189533     END-EXEC.                                                            
189534                                                                          
189535                                                                          
189536 EJECT                                                                    
189537 LINKAGE SECTION.                                                         
189538                                                                          
189539 01  DFHCOMMAREA.                                                         
189540     05  FILLER                         OCCURS  1 TO 4072 TIMES           
189541                                        DEPENDING ON EIBCALEN.            
189542         10  FILLER                     PIC X.                            
189543                                                                          
189544 EJECT                                                                    
189545 PROCEDURE DIVISION.                                                      
189546******************************************************************        
189547**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
189548**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
189549**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
189550******************************************************************        
189551 0000-MAIN-MODULE.                                                        
189552     INITIALIZE DP030-CICS-API-FIELDS.                                    
189553     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
189554     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
189555     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
189556     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
189557     MOVE LENGTH OF VL586AI        TO DP030-MAP-LENGTH (1).               
189558     MOVE PC-CURRENT-PROGRAM-NAME  TO DP013-PROGRAM.                      
189559     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
189560     PERFORM 0001-CALL-CICS-ARCH-API.                                     
189561*                                                                         
189562     PERFORM 1000-CONTROL-PROCESSING                                      
189563*                                                                         
189564     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
189565     PERFORM 0001-CALL-CICS-ARCH-API.                                     
189566                                                                          
189567******************************************************************        
189568**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
189569**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
189570******************************************************************        
189571 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
189573                                    DFHCOMMAREA                           
189574                                    DP030-CICS-API-FIELDS                 
189575                                    DP020-STANDARD-COMMAREA               
189576                                    VL586AI.                              
189577*                                                                         
189578     IF  DP030-RC-CALL-SUCCESSFUL                                         
189579         CONTINUE                                                         
189580     ELSE                                                                 
189581         SET DP013-NO-ROLLBACK                                            
189582             DP013-XCTL-DISPLAY-RESTART                                   
189583             DP013-CICS-ABEND      TO TRUE                                
189584         MOVE '0001-CALL-CICS-ARCH-API'                                   
189585                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
189588         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
189589         PERFORM DP013-0000-PROCESS-ABEND                                 
189590     END-IF.                                                              
189591*----------------------------------------------------------------*        
189592* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
189593* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
189594*                                                                *        
189595* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
189596*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
189597*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
189598*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
189599*----------------------------------------------------------------*        
189600                                                                          
189601 1000-CONTROL-PROCESSING.                                                 
189602                                                                          
189603     EVALUATE TRUE                                                        
189604         WHEN DP020-NEXT-ACT-INITIAL                                      
189605             INITIALIZE ASC-SPECIFIC-COMMAREA                             
189606             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
189607                                      ASC-SEL-TERM-ID                     
189608             MOVE DP020-SRC-TASK-NUMBER                                   
189609                                   TO ASC-LIST-TASK-NUMBER                
189610                                      ASC-SEL-TASK-NUMBER                 
189611             MOVE PC-LIST-QUEUE-SEQ                                       
189612                                   TO ASC-LIST-SEQ                        
189613             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
189614             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
189615***-----$TEMPLATE NOTE$-------------------------------------------        
189616*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
189617*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
189618*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
189619***---------------------------------------------------------------        
189620             IF  PS-COMMAREA-NOT-VALID                                    
189621***              MOVE PC-TSYMSG-99901                                     
189622***                                TO DP020-MSG-NUMBER                    
189623***              SET DP020-MSG-FATAL                                      
189624***                                TO TRUE                                
189625                 SET DP020-NEXT-ACT-APPL-ERROR                            
189626                                   TO TRUE                                
189627             ELSE                                                         
189628                 PERFORM 4000-BUILD-INITIAL-PANEL                         
189629             END-IF                                                       
189630                                                                          
189631         WHEN DP020-NEXT-ACT-READ-MAP                                     
189632             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
189633                                   TO PV-TOP-ITEM                         
189634             PERFORM 2000-PROCESS-PANEL                                   
189635                                                                          
189636         WHEN DP020-NEXT-ACT-RETURN                                       
189637             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
189638                                   TO PV-TOP-ITEM                         
189639             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
189640             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
189641             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
189642             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189643                                                                          
189644         WHEN OTHER                                                       
189645             SET DP013-LOGIC-ABEND TO TRUE                                
189646             SET DP013-NO-ROLLBACK TO TRUE                                
189647             MOVE '1000-CONTROL-PROCESSING'                               
189648                                   TO DP013-PARAGRAPH                     
189649             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
189650                                   TO DP013-MESSAGE-TEXT(1)               
189651             PERFORM DP013-0000-PROCESS-ABEND                             
189652     END-EVALUATE.                                                        
189653                                                                          
189654 EJECT                                                                    
189655*----------------------------------------------------------------*        
189656* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
189657* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
189658*----------------------------------------------------------------*        
189659                                                                          
189660 1100-PROCESS-INTER-APPL-COMM.                                            
189661                                                                          
189662     SET PS-COMMAREA-VALID          TO TRUE.                              
189663     MOVE SPACE                     TO ASC-START-ITEM-REQUEST.            
189664     IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                  
189665     OR  DP020-INT-APPL-FORMAT-IND = PC-PO-INFO-SCREEN                    
189666         MOVE VL005-PO-NUMBER       TO ASC-KEY-PO-NUMBER                  
189667                                       DK-PO-NUMBER                       
189668         MOVE VL005-VENDOR-NAME     TO ASC-KEY-VENDOR-NAME                
189669         MOVE VL005-ENT-ID          TO ASC-KEY-ENT-ID                     
189670                                       DK-ENT-ID                          
189671         MOVE VL005-DEPT-NBR        TO ASC-KEY-DEPT-NBR                   
189672                                       DK-DEPT-NBR                        
189671         MOVE VL005-DEPT-NBR        TO ASC-KEY-DEPT-NBR                   
189672                                       DK-DEPT-NBR                        
               MOVE VL005-SHIP-BY-DATE    TO ASC-KEY-DATE-LIMIT                 
189672                                       DK-DATE-LIMIT                      
189673     END-IF.                                                              
189674                                                                          
189675 EJECT                                                                    
189676*----------------------------------------------------------------*        
189677* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
189678* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
189679* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
189680* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
189681* FUNCTION KEYS CAN GET THIS FAR.                                *        
189682*----------------------------------------------------------------*        
189683                                                                          
189684 2000-PROCESS-PANEL.                                                      
189685     EVALUATE TRUE                                                        
189686         WHEN DP020-SRC-AID = DP016-CLEAR                                 
189687             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
189688             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
189689             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189690                                                                          
189691         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
189692             MOVE ASC-KEY-DEPT-NBR  TO DK-DEPT-NBR                        
189693             MOVE ASC-KEY-PO-NUMBER TO DK-PO-NUMBER                       
189694             MOVE ASC-KEY-ENT-ID    TO DK-ENT-ID                        86
189694             MOVE ASC-KEY-DATE-LIMIT                                    86
                                          TO DK-DATE-LIMIT                    86
189695             PERFORM 2050-DELETE-LIST-QUEUE                               
189696             PERFORM 4000-BUILD-INITIAL-PANEL                             
189697                                                                          
189698         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
189699             PERFORM 2140-DESELECT-ALL-ITEMS                              
189700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189701                                                                          
189702         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
189703             PERFORM 2150-SELECT-ALL-ITEMS                                
189704             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189705                                                                          
189706         WHEN OTHER                                                       
189707             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
189708             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
189709             IF  PS-NO-ERROR                                              
189710                 SET DP030-CONTINUE-GO                                    
189711                                 TO TRUE                                  
189712                 PERFORM 2100-CHECK-FUNCTION-KEY                          
189713             ELSE                                                         
189714                 SET DP030-OVERRIDE-GO                                    
189715                                 TO TRUE                                  
189716             END-IF                                                       
189717     END-EVALUATE.                                                        
189718 EJECT                                                                    
189719*----------------------------------------------------------------*        
189720*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
189721*----------------------------------------------------------------*        
189722 2050-DELETE-LIST-QUEUE.                                                  
189723                                                                          
189724     EXEC CICS                                                            
189725         DELETEQ TS                                                       
189726             QUEUE (ASC-LIST-QUEUE-NAME)                                  
189727             RESP  (PV-CICS-RESP)                                         
189728     END-EXEC.                                                            
189729                                                                          
189730     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
189731        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
189732         CONTINUE                                                         
189733     ELSE                                                                 
189734         SET DP013-CICS-ABEND      TO TRUE                                
189735         SET DP013-NO-ROLLBACK     TO TRUE                                
189736                                                                          
189737         MOVE '2050-DELETE-LIST-QUEUE'                                    
189738                                   TO DP013-PARAGRAPH                     
189739         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
189740                                   TO DP013-MESSAGE-TEXT (1)              
189741         PERFORM DP013-0000-PROCESS-ABEND                                 
189742     END-IF.                                                              
189743                                                                          
189744     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
189745 EJECT                                                                    
189746*----------------------------------------------------------------*        
189747* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
189748* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
189749* CICS ARCHITECTURE API.                                         *        
189750*----------------------------------------------------------------*        
189751 2100-CHECK-FUNCTION-KEY.                                                 
189752     EVALUATE TRUE                                                        
189753*        WHEN THE UPDATE IS PRESSED                                       
189754         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
189755             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
189756                 IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                     
189757                     INITIALIZE VL005-INTER-APPL-COMMAREA                 
189758                     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                  36
189759                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
189760                     PERFORM 5000-PREPARE-SEL-QUEUE                     36
189761                     PERFORM 3601-CHECK-FOR-COMPLETE-RQST                 
189762                 ELSE                                                     
189763                     SET DP030-OVERRIDE-GO                                
189764                                       TO TRUE                            
189765                     MOVE PC-TSYMSG-00047                                 
189766                                       TO DP020-MSG-NUMBER                
189767                     SET DP020-MSG-FATAL                                  
189768                                       TO TRUE                            
189769                 END-IF                                                   
189770             ELSE                                                         
189771                 SET DP030-OVERRIDE-GO                                    
189772                                   TO TRUE                                
189773                 MOVE PC-TSYMSG-00057                                     
189774                                   TO DP020-MSG-NUMBER                    
189775                 SET DP020-MSG-WARNING                                    
189776                                   TO TRUE                                
189777             END-IF                                                       
189778                                                                          
189779*        WHEN THE DELETE IS PRESSED                                       
189780         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
189781             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
189782                 IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                     
189783                     INITIALIZE VL005-INTER-APPL-COMMAREA                 
189784                     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                  36
189785                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
189790                     PERFORM 5000-PREPARE-SEL-QUEUE                     36
189791                 ELSE                                                     
189792                     SET DP030-OVERRIDE-GO                                
189793                                       TO TRUE                            
189794                     MOVE PC-TSYMSG-00047                                 
189795                                       TO DP020-MSG-NUMBER                
189796                     SET DP020-MSG-FATAL                                  
189797                                       TO TRUE                            
189798                 END-IF                                                   
189799             ELSE                                                         
189800                 SET DP030-OVERRIDE-GO                                    
189801                                   TO TRUE                                
189802                 MOVE PC-TSYMSG-00057                                     
189803                                   TO DP020-MSG-NUMBER                    
189804                 SET DP020-MSG-WARNING                                    
189805                                   TO TRUE                                
189806             END-IF                                                       
189807                                                                          
189808*        WHEN THE DETAIL IS PRESSED                                       
189809         WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
189810             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
189811                 IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                     
189812                     INITIALIZE VL005-INTER-APPL-COMMAREA                 
189813                     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                  36
189814                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
189815                     PERFORM 5000-PREPARE-SEL-QUEUE                     36
189816                 ELSE                                                     
189817                     SET DP030-OVERRIDE-GO                                
189818                                       TO TRUE                            
189819                     MOVE PC-TSYMSG-00047                                 
189820                                       TO DP020-MSG-NUMBER                
189821                     SET DP020-MSG-FATAL                                  
189822                                       TO TRUE                            
189823                 END-IF                                                   
189824             ELSE                                                         
189825                 SET DP030-OVERRIDE-GO                                    
189826                                   TO TRUE                                
189827                 MOVE PC-TSYMSG-00057                                     
189828                                   TO DP020-MSG-NUMBER                    
189829                 SET DP020-MSG-WARNING                                    
189830                                   TO TRUE                                
189831             END-IF                                                       
189832                                                                          
189833*        WHEN THE ADD KEY IS PRESSED                                      
189834         WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
189835             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
189836                 IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                     
189837                     INITIALIZE VL005-INTER-APPL-COMMAREA                 
189838                     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                  36
189839                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
189840                     SET VL005-RESUBMIT-REQ TO TRUE                       
189841                     PERFORM 5000-PREPARE-SEL-QUEUE                     36
189842                                                                          
189843                 ELSE                                                     
189844                     SET DP030-OVERRIDE-GO                                
189845                                       TO TRUE                            
189846                     MOVE PC-TSYMSG-00047                                 
189847                                       TO DP020-MSG-NUMBER                
189848                     SET DP020-MSG-FATAL                                  
189849                                       TO TRUE                            
189850                 END-IF                                                   
189851             ELSE                                                         
189852                 SET DP030-OVERRIDE-GO                                    
189853                                   TO TRUE                                
189854                 MOVE PC-TSYMSG-00057                                     
189855                                   TO DP020-MSG-NUMBER                    
189856                 SET DP020-MSG-WARNING                                    
189857                                   TO TRUE                                
189858             END-IF                                                       
189859*        WHEN THE QUEMAINT IS PRESSED                                     
189860         WHEN DP020-FK-LOCAL-FUNC-05 (DP020-SRC-AID)                      
189861            CONTINUE                                                      
189862                                                                          
189863         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
189864             MOVE +1               TO PV-TOP-ITEM                         
189865             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189866                                                                          
189867         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
189868             PERFORM 2110-BROWSE-BACKWARD                                 
189869             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189870                                                                          
189871         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
189872             PERFORM 2120-BROWSE-FORWARD                                  
189873             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
189874                                                                          
189875         WHEN DP020-SRC-AID = DP016-ENTER                                 
189876             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
189877             IF  PS-ERROR                                                 
189878                 CONTINUE                                                 
189879             ELSE                                                         
189880                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
189881             END-IF                                                       
189882                                                                          
189883         WHEN OTHER                                                       
189884             SET DP013-NO-ROLLBACK                                        
189885                 DP013-XCTL-DISPLAY-RESTART                               
189886                 DP013-CICS-ABEND  TO TRUE                                
189887             MOVE '2100-CHECK-FUNCTION-KEY'                               
189888                                   TO DP013-PARAGRAPH                     
189889             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
189890                                   TO DP013-MESSAGE-TEXT (1)              
189891             PERFORM DP013-0000-PROCESS-ABEND                             
189892     END-EVALUATE.                                                        
189893*                                                                         
189894*----------------------------------------------------------------*        
189895*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
189896*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
189897*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
189898*    MESSAGE.                                                    *        
189899*----------------------------------------------------------------*        
189900                                                                          
189901 2110-BROWSE-BACKWARD.                                                    
189902     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
189903         GIVING PV-TOP-ITEM.                                              
189904*                                                                         
189905     IF  ((PV-TOP-ITEM < +1)                                              
189906       OR (PV-TOP-ITEM = +1))                                             
189907         MOVE +1                   TO PV-TOP-ITEM                         
189908*        -- TOP OF LIST --                                                
189909         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
189910         SET DP020-MSG-INFORMATIONAL TO TRUE                              
189911     END-IF.                                                              
189912                                                                          
189913 EJECT                                                                    
189914*----------------------------------------------------------------*        
189915*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
189916*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
189917*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
189918*----------------------------------------------------------------*        
189919                                                                          
189920 2120-BROWSE-FORWARD.                                                     
189921                                                                          
189922     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
189923         GIVING PV-TOP-ITEM.                                              
189924     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
189925         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
189926                                   TO PV-TOP-ITEM                         
189927*        -- BOTTOM OF LIST --                                             
189928         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
189929         SET DP020-MSG-INFORMATIONAL                                      
189930                                   TO TRUE                                
189931     ELSE                                                                 
189932         COMPUTE PV-BOTTOM-ITEM =                                         
189933             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
189934         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
189935*            -- BOTTOM OF LIST --                                         
189936             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
189937             SET DP020-MSG-INFORMATIONAL                                  
189938                                   TO TRUE                                
189939         END-IF                                                           
189940     END-IF.                                                              
189941 EJECT                                                                    
189942*----------------------------------------------------------------*        
189943*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
189944*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
189945*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
189946*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
189947*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
189948*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
189949*----------------------------------------------------------------*        
189950                                                                          
189951 2130-CHECK-FOR-JUMP-BROWSE.                                              
189952     SET PS-NO-ERROR               TO TRUE.                               
189953     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
189954         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
189955                                   TO PV-TOP-ITEM                         
189956     ELSE                                                                 
189957         MOVE ASC-START-ITEM-REQUEST                                      
189958                                   TO DP010I-UNEDITED-FIELD               
189959         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
189960         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
189961         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
189962                                   TO TRUE                                
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
189964                                                                          
189965         IF  NOT DP010I-ERROR-DETECTED                                    
189966             MOVE DP010I-NUMERIC-FIELD                                    
189967                                   TO  ASC-START-ITEM-REQUEST-N           
189968             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
189969                 IF  ASC-START-ITEM-REQUEST-N >                           
189970                                          ASC-NUMBER-OF-ITEMS-READ        
189971                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
189972                     COMPUTE PV-TOP-ITEM =                                
189973                             ASC-NUMBER-OF-ITEMS-READ                     
189974                             - PC-ITEMS-PER-PANEL + 1                     
189975                     IF  PV-TOP-ITEM < +1                                 
189976                         MOVE +1   TO PV-TOP-ITEM                         
189977                         MOVE PC-TSYMSG-00070                             
189978                                   TO DP020-MSG-NUMBER                    
189979                         SET DP020-MSG-INFORMATIONAL                      
189980                                   TO TRUE                                
189981                     END-IF                                               
189982                 ELSE                                                     
189983                     MOVE ASC-START-ITEM-REQUEST-N                        
189984                                   TO PV-TOP-ITEM                         
189985                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
189986                     COMPUTE PV-BOTTOM-ITEM =                             
189987                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
189988                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
189989*                        -- BOTTOM OF LIST --                             
189990                         MOVE PC-TSYMSG-00070                             
189991                                   TO DP020-MSG-NUMBER                    
189992                         SET DP020-MSG-INFORMATIONAL                      
189993                                   TO TRUE                                
189994                     END-IF                                               
189995                 END-IF                                                   
189996             ELSE                                                         
189997*                --- VALUE OUT OF RANGE ----                              
189998                 MOVE PC-TSYMSG-00101                                     
189999                                   TO DP020-MSG-NUMBER                    
190000                 MOVE -1           TO ASTRTITL                            
190001                 SET DP030-SET-CURSOR-APPL-1                              
190002                                   TO TRUE                                
190003                 MOVE DP015-REVERSE                                       
190004                                   TO ASTRTITH                            
190005                 MOVE DP015-RED    TO ASTRTITC                            
190006                 MOVE DP015-UNP-NUM-BRT-MDT                               
190007                                   TO ASTRTITA                            
190008                 SET DP020-MSG-FATAL                                      
190009                                   TO TRUE                                
190010                 SET PS-ERROR      TO TRUE                                
190011             END-IF                                                       
190012                                                                          
190013         ELSE                                                             
190014             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
190015             MOVE -1               TO ASTRTITL                            
190016             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
190017             MOVE DP015-REVERSE    TO ASTRTITH                            
190018             MOVE DP015-RED        TO ASTRTITC                            
190019             MOVE DP015-UNP-NUM-BRT-MDT                                   
190020                                   TO ASTRTITA                            
190021             SET DP020-MSG-FATAL   TO TRUE                                
190022             SET PS-ERROR          TO TRUE                                
190023         END-IF                                                           
190024     END-IF.                                                              
190025 EJECT                                                                    
190026*----------------------------------------------------------------*        
190027*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
190028*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
190029*----------------------------------------------------------------*        
190030 2140-DESELECT-ALL-ITEMS.                                                 
190031     MOVE +1                       TO ASC-BLK-SUB.                        
190032                                                                          
190033     PERFORM 2141-CLEAR-SELECTED-INDS                                     
190034         VARYING ASC-ITEM-SUB                                             
190035         FROM 1 BY 1                                                      
190036         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190037                                                                          
190038     ADD +1                        TO ASC-BLK-SUB.                        
190039                                                                          
190040     PERFORM 2141-CLEAR-SELECTED-INDS                                     
190041         VARYING ASC-ITEM-SUB                                             
190042         FROM 1 BY 1                                                      
190043         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190044                                                                          
190045     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
190046                                   TO PV-LIST-ITEM-NUMBER.                
190047                                                                          
190048     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
190049         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
190050                                                                          
190051     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
190052        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
190053         MOVE +1                   TO ASC-BLK-SUB                         
190054                                                                          
190055         PERFORM 5100-WRITE-TEMP-STORAGE                                  
190056                                                                          
190057         MOVE +1                   TO PV-BLOCK-NUMBER                     
190058                                                                          
190059         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
190060             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
190061                                                                          
190062         PERFORM                                                          
190063             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
190064                                                                          
190065             PERFORM 4410-READ-TEMP-STORAGE                               
190066                                                                          
190067             PERFORM 2141-CLEAR-SELECTED-INDS                             
190068                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
190069                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190070                                                                          
190071             PERFORM 5100-WRITE-TEMP-STORAGE                              
190072                                                                          
190073             ADD +1                TO PV-BLOCK-NUMBER                     
190074                                                                          
190075         END-PERFORM                                                      
190076                                                                          
190077         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
190078             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
190079                                                                          
190080         PERFORM                                                          
190081             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
190082                   ASC-HIGHEST-BLOCK-WRITTEN                              
190083                                                                          
190084             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
190085                                   TO PV-BLOCK-NUMBER                     
190086                                                                          
190087             PERFORM 4410-READ-TEMP-STORAGE                               
190088                                                                          
190089             PERFORM 2141-CLEAR-SELECTED-INDS                             
190090                 VARYING ASC-ITEM-SUB                                     
190091                 FROM 1 BY 1                                              
190092                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190093                                                                          
190094             PERFORM 5100-WRITE-TEMP-STORAGE                              
190095                                                                          
190096             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
190097                                                                          
190098         END-PERFORM                                                      
190099                                                                          
190100         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
190101                                   TO PV-BLOCK-NUMBER                     
190102                                                                          
190103         PERFORM 4410-READ-TEMP-STORAGE                                   
190104     END-IF.                                                              
190105                                                                          
190106                                                                          
190107     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
190108     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
190109     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
190110                                                                          
190111     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
190112                                                                          
190113 EJECT                                                                    
190114*----------------------------------------------------------------*        
190115*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
190116*----------------------------------------------------------------*        
190117                                                                          
190118 2141-CLEAR-SELECTED-INDS.                                                
190119                                                                          
190120     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
190121             NOT EQUAL ZERO                                               
190122         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
190123             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
190124                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
190125                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
190126                                   TO TRUE                                
190127         END-IF                                                           
190128     END-IF.                                                              
190129                                                                          
190130 EJECT                                                                    
190131*----------------------------------------------------------------*        
190132*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
190133*  FOR ALL ITEMS.                                                *        
190134*----------------------------------------------------------------*        
190135                                                                          
190136 2150-SELECT-ALL-ITEMS.                                                   
190137                                                                          
190138     MOVE +1                       TO ASC-BLK-SUB.                        
190139                                                                          
190140     PERFORM 2151-SET-SELECTED-INDS                                       
190141         VARYING ASC-ITEM-SUB                                             
190142         FROM 1 BY 1                                                      
190143         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190144                                                                          
190145     ADD +1                        TO ASC-BLK-SUB.                        
190146                                                                          
190147     PERFORM 2151-SET-SELECTED-INDS                                       
190148         VARYING ASC-ITEM-SUB                                             
190149         FROM 1 BY 1                                                      
190150         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190151                                                                          
190152     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
190153                                   TO PV-LIST-ITEM-NUMBER.                
190154                                                                          
190155     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
190156         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
190157                                                                          
190158     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
190159        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
190160         MOVE +1                   TO ASC-BLK-SUB                         
190161                                                                          
190162         PERFORM 5100-WRITE-TEMP-STORAGE                                  
190163                                                                          
190164         MOVE +1                   TO PV-BLOCK-NUMBER                     
190165                                                                          
190166         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
190167             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
190168                                                                          
190169         PERFORM                                                          
190170             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
190171                                                                          
190172             PERFORM 4410-READ-TEMP-STORAGE                               
190173                                                                          
190174             PERFORM 2151-SET-SELECTED-INDS                               
190175                 VARYING ASC-ITEM-SUB                                     
190176                 FROM 1 BY 1                                              
190177                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190178                                                                          
190179             PERFORM 5100-WRITE-TEMP-STORAGE                              
190180                                                                          
190181             ADD +1                TO PV-BLOCK-NUMBER                     
190182                                                                          
190183         END-PERFORM                                                      
190184                                                                          
190185         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
190186             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
190187                                                                          
190188         PERFORM                                                          
190189             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
190190                   ASC-HIGHEST-BLOCK-WRITTEN                              
190191                                                                          
190192             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
190193                                   TO PV-BLOCK-NUMBER                     
190194                                                                          
190195             PERFORM 4410-READ-TEMP-STORAGE                               
190196                                                                          
190197             PERFORM 2151-SET-SELECTED-INDS                               
190198                 VARYING ASC-ITEM-SUB                                     
190199                 FROM 1 BY 1                                              
190200                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190201                                                                          
190202             PERFORM 5100-WRITE-TEMP-STORAGE                              
190203                                                                          
190204             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
190205                                                                          
190206         END-PERFORM                                                      
190207                                                                          
190208         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
190209                                   TO PV-BLOCK-NUMBER                     
190210                                                                          
190211         PERFORM 4410-READ-TEMP-STORAGE                                   
190212     END-IF.                                                              
190213                                                                          
190214                                                                          
190215     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
190216                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
190217                                                                          
190218     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
190219                                                                          
190220 EJECT                                                                    
190221*----------------------------------------------------------------*        
190222*    MARK EACH ITEM AS BEING SELECTED.                           *        
190223*----------------------------------------------------------------*        
190224                                                                          
190225 2151-SET-SELECTED-INDS.                                                  
190226                                                                          
190227     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
190228             NOT EQUAL ZERO                                               
190229         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
190230             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
190231                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
190232                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
190233                               TO  TRUE                                   
190234         END-IF                                                           
190235     END-IF.                                                              
190236******************************************************************        
190237** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
190238** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
190239******************************************************************        
190240 2200-MOVE-SCREEN-TO-COMMAREA.                                            
190241                                                                          
190242     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
190243     MOVE +1                       TO PV-SUB.                             
190244*                                                                         
190245     PERFORM 3100-SET-SUBSCRIPTS.                                         
190246     IF  ASTRTITL > ZERO                                                  
190247         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
190248     ELSE                                                                 
190249         IF  ASTRTITF = DP015-ERASE-EOF                                   
190250             INITIALIZE ASC-START-ITEM-REQUEST                            
190251         END-IF                                                           
190252     END-IF.                                                              
190253     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
190254         VARYING PV-SUB                                                   
190255         FROM 1 BY 1                                                      
190256         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
190257******************************************************************        
190258** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
190259******************************************************************        
190260 2210-MOVE-LINES-TO-COMMAREA.                                             
190261     IF  MR-SELECTL (PV-SUB) > ZERO                                       
190262         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
190263         MOVE MR-SELECTI (PV-SUB)                                         
190264                                   TO ASC-TEMP-SELECTED-SW                
190265                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
190266     ELSE                                                                 
190267         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
190268                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
190269                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
190270                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
190271         END-IF                                                           
190272     END-IF.                                                              
190273     ADD +1                        TO ASC-ITEM-SUB.                       
190274     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
190275         ADD +1                    TO ASC-BLK-SUB                         
190276         MOVE +1                   TO ASC-ITEM-SUB                        
190277     END-IF.                                                              
190278******************************************************************        
190279** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
190280** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
190281******************************************************************        
190282 3000-EDIT-DATA-IN-COMMAREA.                                              
190283     SET PS-NO-ERROR               TO TRUE.                               
190284     PERFORM 3200-RESET-ATTRIBUTES.                                       
190285     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
190286**                                                                        
190287* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
190288* EDITTING IS DONE FROM THE BOTTOM UP.                                    
190289**                                                                        
190290     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
190291         ASC-ITEMS-DISPLAYED - 1.                                         
190292     PERFORM 3100-SET-SUBSCRIPTS.                                         
190293     PERFORM 3300-EDIT-SELECTIONS                                         
190294         VARYING PV-SUB                                                   
190295         FROM PV-SUB BY -1                                                
190296         UNTIL PV-SUB < +1                                                
190297*                                                                         
190298     IF  PS-NO-ERROR                                                      
190299         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
190300     END-IF.                                                              
190301*                                                                         
190302     IF  ((PS-NO-ERROR)                                                   
190303       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
190304       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
190305             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
190306     END-IF.                                                              
190307 EJECT                                                                    
190308*----------------------------------------------------------------*        
190309*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
190310*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
190311*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
190312*                                                                *        
190313*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
190314*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
190315*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
190316*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
190317*----------------------------------------------------------------*        
190318                                                                          
190319 3100-SET-SUBSCRIPTS.                                                     
190320                                                                          
190321     COMPUTE ASC-ITEM-SUB =                                               
190322         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
190323                                                                          
190324     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
190325         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
190326                                                                          
190327         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
190328     ELSE                                                                 
190329         MOVE +1                     TO ASC-BLK-SUB                       
190330     END-IF.                                                              
190331******************************************************************        
190332** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
190333******************************************************************        
190334                                                                          
190335 3200-RESET-ATTRIBUTES.                                                   
190336     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
190337     MOVE DP015-GREEN              TO ASTRTITC.                           
190338     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
190339*                                                                         
190340     PERFORM                                                              
190341         VARYING PV-SUB                                                   
190342         FROM 1 BY 1                                                      
190343         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
190344             MOVE DP015-UNP-ALP-NOR-OFF                                   
190345                                   TO MR-SELECTA (PV-SUB)                 
190346             MOVE DP015-GREEN      TO MR-SELECTC (PV-SUB)                 
190347             MOVE DP015-BLUE       TO MR-RQST-STATC (PV-SUB)              
190348             MOVE DP015-UNDERLINE  TO MR-SELECTH (PV-SUB)                 
190349             MOVE DP015-HL-OFF     TO MR-RQST-STATH (PV-SUB)              
190350     END-PERFORM.                                                         
190351******************************************************************        
190352** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
190353** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
190354** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
190355** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
190356******************************************************************        
190357 3300-EDIT-SELECTIONS.                                                    
190358     EVALUATE TRUE                                                        
190359         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
190360             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
190361                 CONTINUE                                                 
190362         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
190363             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
190364                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
190365                 MOVE ASC-TEMP-SELECTED-SW                                
190366                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
190367                                TO ASC-SELECTED-SW                        
190368                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
190369             END-IF                                                       
190370         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
190371                 = SPACE                                                  
190372             EVALUATE TRUE                                                
190373                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
190374                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
190375                     MOVE SPACE TO ASC-SELECTED-SW                        
190376                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
190377                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
190378                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
190379                     MOVE SPACE TO ASC-SELECTED-SW                        
190380                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
190381                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
190382                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
190383                     MOVE SPACE TO ASC-SELECTED-SW                        
190384                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
190385             END-EVALUATE                                                 
190386         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
190387             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
190388                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
190389             END-IF                                                       
190390             PERFORM 3310-EDIT-RANGE                                      
190391         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
190392             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
190393                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
190394             END-IF                                                       
190395             PERFORM 3310-EDIT-RANGE                                      
190396         WHEN OTHER                                                       
190397             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
190398             SET DP020-MSG-FATAL    TO TRUE                               
190399             MOVE -1                TO MR-SELECTL (PV-SUB)                
190400             SET DP030-SET-CURSOR-APPL-1                                  
190401                                    TO TRUE                               
190402             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
190403             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
190404             SET PS-ERROR TO TRUE                                         
190405             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
190406                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
190407                 MOVE SPACE         TO ASC-SELECTED-SW                    
190408                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
190409             END-IF                                                       
190410     END-EVALUATE.                                                        
190411                                                                          
190412     IF ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                             
190413*        IF UPDATE KEY WAS PRESSED                                        
190414         IF DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                        
190415            IF ASC-UPD-REQ-STAT (ASC-BLK-SUB ASC-ITEM-SUB)                
190416                CONTINUE                                                  
190417            ELSE                                                          
190418                SET  DP020-MSG-FATAL                                      
190419                     PS-ERROR      TO TRUE                                
190420                MOVE PC-TSYMSG-01391                                      
190421                                   TO DP020-MSG-NUMBER                    
190422                MOVE -1            TO MR-SELECTL (PV-SUB)                 
190423                SET DP030-SET-CURSOR-APPL-1                               
190424                                   TO TRUE                                
190425                MOVE DP015-RED     TO MR-RQST-STATC (PV-SUB)              
190426                MOVE DP015-REVERSE TO MR-RQST-STATH (PV-SUB)              
190427            END-IF                                                        
190428         END-IF                                                           
190445                                                                          
190446*        IF ADD KEY WAS PRESSED (RESUBMIT)                                
190447         IF DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                        
190448            IF ASC-ADD-REQ-STAT (ASC-BLK-SUB ASC-ITEM-SUB)                
190449                MOVE ASC-PO-NUMBER (ASC-BLK-SUB ASC-ITEM-SUB)             
190450                     TO DK-PO-NUMBER                                      
190451                PERFORM 3600-CHECK-FOR-ACTIVE-RQST                        
190452                IF SQLCODE = +100                                         
190453                    MOVE ASC-ENT-ID (ASC-BLK-SUB ASC-ITEM-SUB)            
190454                         TO DK-ENT-ID                                     
190455                    MOVE ASC-DEPT-NBR (ASC-BLK-SUB ASC-ITEM-SUB)          
190456                         TO DK-DEPT-NBR                                   
190457                    PERFORM 3700-PO-EDIT-CHECKS                           
190458                ELSE                                                      
190459                    SET  DP020-MSG-FATAL                                  
190460                         PS-ERROR      TO TRUE                            
190461                    MOVE PC-TSYMSG-00117                                  
190462                                       TO DP020-MSG-NUMBER                
190463                    MOVE -1            TO MR-SELECTL (PV-SUB)             
190464                    SET DP030-SET-CURSOR-APPL-1                           
190465                                       TO TRUE                            
190466                END-IF                                                    
190467            ELSE                                                          
190468                SET  DP020-MSG-FATAL                                      
190469                     PS-ERROR      TO TRUE                                
190470                MOVE PC-TSYMSG-01350                                      
190471                                   TO DP020-MSG-NUMBER                    
190472                MOVE -1            TO MR-SELECTL (PV-SUB)                 
190473                SET DP030-SET-CURSOR-APPL-1                               
190474                                   TO TRUE                                
190475                MOVE DP015-RED     TO MR-RQST-STATC (PV-SUB)              
190476                MOVE DP015-REVERSE TO MR-RQST-STATH (PV-SUB)              
190477            END-IF                                                        
190478         END-IF                                                           
190479     END-IF.                                                              
190480                                                                          
190481                                                                          
190482     SUBTRACT +1                   FROM PV-ITEM-IN-USE.                   
190483     SUBTRACT +1                   FROM ASC-ITEM-SUB.                     
190484     IF  ASC-ITEM-SUB < 1                                                 
190485         SUBTRACT +1               FROM ASC-BLK-SUB                       
190486         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
190487     END-IF.                                                              
190488******************************************************************        
190489** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
190490** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
190491** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
190492** THE ENDING.                                                  **        
190493******************************************************************        
190494 3310-EDIT-RANGE.                                                         
190495     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
190496         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
190497             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
190498                                   TO ASC-SELECTED-SW                     
190499                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
190500             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
190501         ELSE                                                             
190502             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
190503                 CONTINUE                                                 
190504             ELSE                                                         
190505*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
190506                 MOVE PC-TSYMSG-00054                                     
190507                                   TO DP020-MSG-NUMBER                    
190508                 SET DP020-MSG-FATAL                                      
190509                                   TO TRUE                                
190510                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
190511                 SET DP030-SET-CURSOR-APPL-1                              
190512                                   TO TRUE                                
190513                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
190514                 MOVE DP015-UNDERLINE                                     
190515                                   TO MR-SELECTH (PV-SUB)                 
190516                 SET PS-ERROR      TO TRUE                                
190517             END-IF                                                       
190518         END-IF                                                           
190519     ELSE                                                                 
190520         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
190521             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
190522                                   TO ASC-SELECTED-SW                     
190523                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
190524             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
190525         ELSE                                                             
190526             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
190527                 CONTINUE                                                 
190528             ELSE                                                         
190529*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
190530                 MOVE PC-TSYMSG-00055                                     
190531                                   TO DP020-MSG-NUMBER                    
190532                 SET DP020-MSG-FATAL                                      
190533                                   TO TRUE                                
190534                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
190535                 SET DP030-SET-CURSOR-APPL-1                              
190536                                   TO TRUE                                
190537                 SET PS-ERROR      TO TRUE                                
190538             END-IF                                                       
190539         END-IF                                                           
190540     END-IF.                                                              
190541*                                                                         
190542******************************************************************        
190543** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
190544** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
190545******************************************************************        
190546 3400-CHECK-RANGE-SEQUENCE.                                               
190547     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
190548       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
190549       AND  ASC-START-OF-SELECTED-RANGE >                                 
190550                                         ASC-END-OF-SELECTED-RANGE        
190551*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
190552         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
190553         SET DP020-MSG-FATAL TO TRUE                                      
190554         SET PS-ERROR TO TRUE                                             
190555         PERFORM 3410-POSITION-AT-ERROR                                   
190556     END-IF.                                                              
190557******************************************************************        
190558** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
190559** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
190560** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
190561******************************************************************        
190562 3410-POSITION-AT-ERROR.                                                  
190563     SET DP030-SET-CURSOR-APPL-1                                          
190564                                   TO TRUE.                               
190565     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
190566                    - PV-TOP-ITEM + 1.                                    
190567     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
190568         CONTINUE                                                         
190569     ELSE                                                                 
190570         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
190571         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
190572         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
190573     END-IF.                                                              
190574*                                                                         
190575     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
190576                    - PV-TOP-ITEM + 1.                                    
190577     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
190578         CONTINUE                                                         
190579     ELSE                                                                 
190580         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
190581         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
190582         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
190583     END-IF.                                                              
190584 EJECT                                                                    
190585*----------------------------------------------------------------*        
190586*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
190587*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
190588*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
190589*----------------------------------------------------------------*        
190590                                                                          
190591 3500-MARK-RANGE-AS-SELECTED.                                             
190592     MOVE +1                       TO  ASC-BLK-SUB.                       
190593     PERFORM 3510-MARK-SELECTED-INDS                                      
190594         VARYING ASC-ITEM-SUB                                             
190595         FROM 1 BY 1                                                      
190596         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190597                                                                          
190598     ADD +1                        TO ASC-BLK-SUB.                        
190599     PERFORM 3510-MARK-SELECTED-INDS                                      
190600         VARYING ASC-ITEM-SUB                                             
190601         FROM 1 BY 1                                                      
190602         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
190603                                                                          
190604     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
190605                                   TO  PV-LIST-ITEM-NUMBER.               
190606     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
190607         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
190608               PC-ITEMS-PER-PANEL).                                       
190609                                                                          
190610     MOVE ASC-START-OF-SELECTED-RANGE                                     
190611                                   TO PV-START-OF-SELECTED-RANGE.         
190612     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
190613         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
190614               PC-ITEMS-PER-PANEL).                                       
190615                                                                          
190616     MOVE ASC-END-OF-SELECTED-RANGE                                       
190617                                   TO  PV-END-OF-SELECTED-RANGE.          
190618     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
190619         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
190620               PC-ITEMS-PER-PANEL).                                       
190621     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
190622         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
190623                                                                          
190624     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
190625        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
190626         MOVE +1                   TO ASC-BLK-SUB                         
190627         PERFORM 5100-WRITE-TEMP-STORAGE                                  
190628         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
190629             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
190630                                                                          
190631         PERFORM                                                          
190632             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
190633                   PV-PREV-BLOCK-IN-ARRAY                                 
190634             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
190635                                   TO PV-BLOCK-NUMBER                     
190636             PERFORM 4410-READ-TEMP-STORAGE                               
190637             PERFORM 3510-MARK-SELECTED-INDS                              
190638                 VARYING ASC-ITEM-SUB                                     
190639                 FROM 1 BY 1                                              
190640                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190641             PERFORM 5100-WRITE-TEMP-STORAGE                              
190642             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
190643         END-PERFORM                                                      
190644                                                                          
190645         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
190646             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
190647         PERFORM                                                          
190648             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
190649                   PV-LAST-BLOCK-IN-RANGE                                 
190650             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
190651                                   TO PV-BLOCK-NUMBER                     
190652             PERFORM 4410-READ-TEMP-STORAGE                               
190653             PERFORM 3510-MARK-SELECTED-INDS                              
190654                 VARYING ASC-ITEM-SUB                                     
190655                 FROM 1 BY 1                                              
190656                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
190657             PERFORM 5100-WRITE-TEMP-STORAGE                              
190658             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
190659         END-PERFORM                                                      
190660                                                                          
190661         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
190662                                   TO PV-BLOCK-NUMBER                     
190663         PERFORM 4410-READ-TEMP-STORAGE                                   
190664     END-IF.                                                              
190665                                                                          
190666     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
190667                                      ASC-END-OF-SELECTED-RANGE.          
190668                                                                          
190669 EJECT                                                                    
190670*----------------------------------------------------------------*        
190671* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
190672*----------------------------------------------------------------*        
190673                                                                          
190674 3510-MARK-SELECTED-INDS.                                                 
190675                                                                          
190676     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
190677                                ASC-ITEM-SUB) >                           
190678         ASC-START-OF-SELECTED-RANGE)                                     
190679     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
190680                                ASC-ITEM-SUB) =                           
190681         ASC-START-OF-SELECTED-RANGE)                                     
190682                                                                          
190683         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
190684                                    ASC-ITEM-SUB) <                       
190685             ASC-END-OF-SELECTED-RANGE)                                   
190686         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
190687                                    ASC-ITEM-SUB) =                       
190688             ASC-END-OF-SELECTED-RANGE)                                   
190689                                                                          
190690             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
190691                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
190692                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
190693                               TO TRUE                                    
190694                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
190695                               TO TRUE                                    
190696                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
190697             END-IF                                                       
190698         END-IF                                                           
190699     END-IF.                                                              
190700                                                                          
190701*----------------------------------------------------------------*        
190702*    GET THE TICKET QUANTITY FOR A PO/REQUEST                    *        
190703*----------------------------------------------------------------*        
190704                                                                          
190705 3600-CHECK-FOR-ACTIVE-RQST.                                              
190706                                                                          
190707     EXEC SQL                                                             
190708         SELECT                                                           
190709              PO_NBR                                                      
190710         INTO                                                             
190711              :PV-TEMP-PO-NBR                                             
190712         FROM                                                             
190713              TPTKREQ                                                     
190714         WHERE                                                            
190715              PO_NBR     = :DK-PO-NUMBER                                  
190716         AND  REQ_STAT_CDE IN ('A','R','H')                               
190717     END-EXEC                                                             
190718                                                                          
190719     EVALUATE TRUE                                                        
190720         WHEN SQLCODE = ZERO                                              
190721         WHEN SQLCODE = +100                                              
190722              CONTINUE                                                    
190723         WHEN SQLWARN0 NOT = SPACE                                        
190724         WHEN SQLCODE NOT = ZERO                                          
190725              MOVE '3600-CHECK-FOR-ACTIVE-RQST'                           
190726                               TO DP013-PARAGRAPH                         
190727              MOVE 'SELECT REQUEST FROM PRETICKET REQUEST TABLE'          
190728                               TO DP013-MESSAGE-TEXT (1)                  
190729              MOVE DK-PO-NUMBER                                           
190730                               TO DP013-MESSAGE-TEXT (2)                  
190731              MOVE SQLCA       TO DP013-SQLCA                             
190732              SET DP013-DB2-ABEND                                         
190733                               TO TRUE                                    
190734              PERFORM DP013-0000-PROCESS-ABEND                            
190735     END-EVALUATE.                                                        
190736                                                                          
190737                                                                          
190738                                                                          
190739 3601-CHECK-FOR-COMPLETE-RQST.                                            
190740                                                                          
190741     EXEC SQL                                                             
190742         SELECT                                                           
190743              PO_NBR                                                      
190744         INTO                                                             
190745              :PV-TEMP-PO-NBR                                             
190746         FROM                                                             
190747              TPTKREQ                                                     
190748         WHERE                                                            
190749              PO_NBR     = :DK-PO-NUMBER                                  
190750         AND  REQ_STAT_CDE IN ('I','P','S','C')                           
190751     END-EXEC                                                             
190752                                                                          
190753     EVALUATE TRUE                                                        
190754         WHEN SQLCODE = -811                                              
190755         WHEN SQLCODE = ZERO                                              
190756             SET VL005-UPDATE-RESUB TO TRUE                               
190757         WHEN SQLCODE = +100                                              
190758             SET VL005-UPDATE-REG   TO TRUE                               
190759                                                                          
190760         WHEN SQLWARN0 NOT = SPACE                                        
190761         WHEN SQLCODE NOT = ZERO                                          
190762              MOVE '3600-CHECK-FOR-ACTIVE-RQST'                           
190763                               TO DP013-PARAGRAPH                         
190764              MOVE 'SELECT REQUEST FROM PRETICKET REQUEST TABLE'          
190765                               TO DP013-MESSAGE-TEXT (1)                  
190766              MOVE DK-PO-NUMBER                                           
190767                               TO DP013-MESSAGE-TEXT (2)                  
190768              MOVE SQLCA       TO DP013-SQLCA                             
190769              SET DP013-DB2-ABEND                                         
190770                               TO TRUE                                    
190771              PERFORM DP013-0000-PROCESS-ABEND                            
190772     END-EVALUATE.                                                        
190773                                                                          
190774                                                                          
190775                                                                          
190776********************************************************************      
190777* THIS PARAGRAPH PERFORMS THE EDIT CHECKS NEEDED ON THE PURCHSE   **      
190778* ORDER WHEN CREATING A NEW PRETICKET REQUEST.                    **      
190779********************************************************************      
190780 3700-PO-EDIT-CHECKS.                                                     
190781                                                                          
190782     EXEC SQL                                                             
190783         SELECT                                                           
190784              A.STATUS_CDE                                                
                   ,B.CAN_IFNOT_SHIP_DTE                                        
190785         INTO                                                             
190786              :PURCHS-STATUS-CDE                                          
190786            , :PLNSHP-CAN-IFNOT-SHIP-DTE                                  
190787         FROM                                                             
190788              TPURCHS A                                                   
190788            , TPLNSHP B                                                   
190789         WHERE                                                            
190790              A.PO_NBR     = :DK-PO-NUMBER                                
190790         AND  B.PO_NBR     = A.PO_NBR                                     
190791         AND  A.VER_STATUS_CDE = 'A'                                      
190791         AND  B.VER_STATUS_CDE = 'A'                                      
190792     END-EXEC.                                                            
190793                                                                          
190794     EVALUATE TRUE                                                        
190795         WHEN SQLCODE = ZERO                                              
190796             IF PURCHS-STATUS-CDE = 'CA'                                  
190797* CANNOT ADD REQUEST -- PO HAS BEEN CANCELLED                             
190798                  SET  DP020-MSG-FATAL                                    
190799                       PS-ERROR      TO TRUE                              
190800                  MOVE PC-TSYMSG-01418                                    
190801                                     TO DP020-MSG-NUMBER                  
190802                  MOVE -1            TO MR-SELECTL (PV-SUB)               
190803                  SET DP030-SET-CURSOR-APPL-1                             
190804                                     TO TRUE                              
190805             ELSE                                                         
190796                 IF PLNSHP-CAN-IFNOT-SHIP-DTE < ASC-CURRENT-DATE          
190797* CANNOT ADD REQUEST -- PO HAS BEEN CANCELLED                             
190798                      SET  DP020-MSG-FATAL                                
190799                           PS-ERROR      TO TRUE                          
190800                      MOVE PC-TSYMSG-01420                                
190801                                         TO DP020-MSG-NUMBER              
190802                      MOVE -1            TO MR-SELECTL (PV-SUB)           
190803                      SET DP030-SET-CURSOR-APPL-1                         
190804                                         TO TRUE                          
190805                 ELSE                                                     
190806* THE PO EXISTS, NOW CHECK IT IF IS A PRETICKETING PO                     
190807                     PERFORM 3710-VALIDATE-PO-PRETICKET                   
190808                 END-IF                                                   
190808             END-IF                                                       
190809                                                                          
190810* THE PO DOES NOT EXIST                                                   
190811         WHEN SQLCODE = +100                                              
190812             SET  DP020-MSG-FATAL                                         
190813                  PS-ERROR      TO TRUE                                   
190814             MOVE PC-TSYMSG-00083                                         
190815                                TO DP020-MSG-NUMBER                       
190816             MOVE -1            TO MR-SELECTL (PV-SUB)                    
190817             SET DP030-SET-CURSOR-APPL-1                                  
190818                                TO TRUE                                   
190819                                                                          
190820         WHEN SQLWARN0 NOT = SPACE                                        
190821         WHEN SQLCODE NOT = ZERO                                          
190822              MOVE '3700-PO-EDIT-CHECKS'                                  
190823                               TO DP013-PARAGRAPH                         
190824              MOVE 'SELECT PURCHASE ORDER TABLE'                          
190825                               TO DP013-MESSAGE-TEXT (1)                  
190826              MOVE DK-PO-NUMBER                                           
190827                               TO DP013-MESSAGE-TEXT (2)                  
190828             MOVE 'TPURCHS '   TO DP013-DB2-TABLE-NAME (1)                
190829              MOVE SQLCA       TO DP013-SQLCA                             
190830              SET DP013-DB2-ABEND                                         
190831                               TO TRUE                                    
190832              PERFORM DP013-0000-PROCESS-ABEND                            
190840     END-EVALUATE.                                                        
190841                                                                          
190842******************************************************************        
190843** VALIDATES THAT THE PO IS A PRETICKET PO                      **        
190844******************************************************************        
190845 3710-VALIDATE-PO-PRETICKET.                                              
190846                                                                          
190847     EXEC SQL                                                             
190848          SELECT A.PO_NBR                                                 
190849          INTO                                                            
190850                :POAGMT-PO-NBR                                            
190851          FROM TPOAGMT A                                                  
190852          WHERE A.PO_NBR         = :DK-PO-NUMBER                          
190853            AND A.AGRMT_TYP_ID   = :PC-SERVICE-AGRMT-CDE                  
190854            AND A.TYP_CDE        = :PC-PRETICKET-TYP-CDE                  
190855            AND A.VER_STATUS_CDE = 'A'                                    
190856     END-EXEC.                                                            
190857                                                                          
190858     EVALUATE TRUE                                                        
190859         WHEN SQLCODE = ZERO                                              
190860             PERFORM 3720-CHECK-VENDOR-FILE                               
190861                                                                          
190862* IF THE PO ISN'T A PRETICKET PO, A REQUEST CANNOT BE ADDED               
190863         WHEN SQLCODE = +100                                              
190864             SET  DP020-MSG-FATAL                                         
190865                  PS-ERROR      TO TRUE                                   
190866             MOVE PC-TSYMSG-01352                                         
190867                                TO DP020-MSG-NUMBER                       
190868             MOVE -1            TO MR-SELECTL (PV-SUB)                    
190869             SET DP030-SET-CURSOR-APPL-1                                  
190870                                TO TRUE                                   
190871                                                                          
190872         WHEN SQLWARN0 NOT = SPACES                                       
190873         WHEN SQLCODE < ZERO                                              
190874         WHEN SQLCODE > ZERO                                              
190875             MOVE '3710-VALIDATE-PO-PRETICKET'                            
190876                               TO DP013-PARAGRAPH                         
190877             MOVE 'SELECT PURCHASE ORDER AGREEMENT'                       
190878                               TO DP013-MESSAGE-TEXT (1)                  
190879             MOVE DK-PO-NUMBER TO DP013-MESSAGE-TEXT (2)                  
190880             MOVE SQLCA        TO DP013-SQLCA                             
190881             MOVE 'TPOAGMT '   TO DP013-DB2-TABLE-NAME (1)                
190882             SET DP013-DB2-ABEND                                          
190883                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
190884             PERFORM DP013-0000-PROCESS-ABEND                             
190885     END-EVALUATE.                                                        
190886                                                                          
190887******************************************************************        
190888** VALIDATES THAT THE VENDOR IS A PRETICKETING VENDOR            *        
190889******************************************************************        
190890 3720-CHECK-VENDOR-FILE.                                                  
190891                                                                          
190892     EXEC SQL                                                             
190893          SELECT A.TKT_MKR_IND                                            
190894          INTO                                                            
190895                :VNDAGR-TKT-MKR-IND                                       
190896          FROM TVNDAGR A                                                  
190897          WHERE A.ENT_ID         = :DK-ENT-ID                             
190898            AND A.DEPT_NBR       = :DK-DEPT-NBR                           
190899            AND A.EFFECTIVE_DATE <= CURRENT DATE                          
190900            AND A.EXPIRATION_DATE > CURRENT DATE                          
190901            AND A.AGRMT_TYP_ID   = :PC-SERVICE-AGRMT-CDE                  
190902            AND A.TYP_CDE        = :PC-PRETICKET-TYP-CDE                  
190903     END-EXEC.                                                            
190904                                                                          
190905     EVALUATE TRUE                                                        
190906         WHEN SQLCODE = ZERO                                              
190907* THE VENDOR IS A PRETICKET VENDOR - IF THE TICKET MAKER IND IS           
190908* A "V", THE VENDOR MAKES THE TICKETS, AND A REQUEST CANNOT BE CREATED    
190909             IF VNDAGR-TKT-MKR-IND = 'V'                                  
190910                 SET  DP020-MSG-FATAL                                     
190911                      PS-ERROR      TO TRUE                               
190912                 MOVE PC-TSYMSG-01322                                     
190913                                    TO DP020-MSG-NUMBER                   
190914                 MOVE -1            TO MR-SELECTL (PV-SUB)                
190915                 SET DP030-SET-CURSOR-APPL-1                              
190916                                    TO TRUE                               
190917                                                                          
190918             END-IF                                                       
190919                                                                          
190920* IF THE VND ISNT A PRETICKET VND,A REQUEST CANNOT BE ADDED               
190921         WHEN SQLCODE = +100                                              
190922                 SET  DP020-MSG-FATAL                                     
190923                      PS-ERROR      TO TRUE                               
190924                 MOVE PC-TSYMSG-01321                                     
190925                                    TO DP020-MSG-NUMBER                   
190926                 MOVE -1            TO MR-SELECTL (PV-SUB)                
190927                 SET DP030-SET-CURSOR-APPL-1                              
190928                                    TO TRUE                               
190929                                                                          
190930         WHEN SQLWARN0 NOT = SPACES                                       
190931         WHEN SQLCODE < ZERO                                              
190932         WHEN SQLCODE > ZERO                                              
190933             MOVE '3720-CHECK-VENDOR-FILE'                                
190934                               TO DP013-PARAGRAPH                         
190935             MOVE 'SELECT VENDOR AGREEMENTS'                              
190936                               TO DP013-MESSAGE-TEXT (1)                  
190937             MOVE DK-ENT-ID    TO DP013-MESSAGE-TEXT (2)                  
190938             MOVE DK-DEPT-NBR  TO DP013-MESSAGE-TEXT (3)                  
190939             MOVE SQLCA        TO DP013-SQLCA                             
190940             MOVE 'TVNDAGR '   TO DP013-DB2-TABLE-NAME (1)                
190941             SET DP013-DB2-ABEND                                          
190942                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
190943             PERFORM DP013-0000-PROCESS-ABEND                             
190944     END-EVALUATE.                                                        
190945                                                                          
190946                                                                          
190947 EJECT                                                                    
190948*----------------------------------------------------------------*        
190949*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
190950*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
190951*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
190952*    DISPLAY THE PANEL TO USER.                                  *        
190953*----------------------------------------------------------------*        
190954                                                                          
190955 4000-BUILD-INITIAL-PANEL.                                                
190956                                                                          
190957     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
190958                                 ASC-BLOCK-ENTRIES (2).                   
190959     MOVE +1                 TO ASC-BLK-SUB.                              
190960     SET ASC-ITEMS-LEFT-ON-DB                                             
190961                             TO TRUE.                                     
190962                                                                          
190963     MOVE +0                 TO ASC-ITEM-SUB                              
190964                                ASC-NUMBER-OF-ITEMS-READ                  
190965                                ASC-NUMBER-OF-SELECTED-ITEMS              
190966                                ASC-HIGHEST-BLOCK-WRITTEN                 
190967                                ASC-START-OF-SELECTED-RANGE               
190968                                ASC-END-OF-SELECTED-RANGE                 
190969                                ASC-ITEM-AT-TOP-OF-PANEL.                 
190970     PERFORM 4050-GET-CURRENT-DATE.                                       
190970     PERFORM 4100-PREPARE-CURSOR.                                         
190971                                                                          
190972     PERFORM 4010-OPEN-CURSOR.                                            
190973                                                                          
190974     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
190975                                                                          
190976     PERFORM 4020-CLOSE-CURSOR.                                           
190977                                                                          
190978     MOVE +1                   TO  PV-TOP-ITEM.                           
190979     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
190980         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
190981         SET DP020-MSG-FATAL   TO  TRUE                                   
190982         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
190983     ELSE                                                                 
190984         MOVE -1               TO MR-SELECTL (1)                          
190985         SET DP030-SET-CURSOR-APPL-1                                      
190986                               TO TRUE                                    
190987         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
190988     END-IF.                                                              
190989 EJECT                                                                    
190990*----------------------------------------------------------------*        
190991* OPEN THE TMESSAGE CURSOR.                                      *        
190992*   IN THIS PARTICULAR APPLICATION ONE OF 4 DIFFERENT CURSORS    *        
190993*   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
190994*   USER.                                                        *        
190995*----------------------------------------------------------------*        
190996                                                                          
190997 4010-OPEN-CURSOR.                                                        
190998     PERFORM                                                              
190999         WITH TEST AFTER                                                  
191000         VARYING PV-RETRY-COUNTER                                         
191001         FROM 1 BY 1                                                      
191002         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
191003                   OR (SQLCODE = ZERO))                                   
191004         EVALUATE TRUE                                                    
191005             WHEN ASC-PO-CURSOR                                           
191006                 MOVE 'CURSOR:  PO_RQST_CURSOR'                           
191007                                   TO DP013-MESSAGE-TEXT (2)              
191008                 EXEC SQL                                                 
191009                      OPEN PO_RQST_CURSOR                                 
191010                 END-EXEC                                                 
191011                                                                          
191012             WHEN ASC-DPT-CURSOR                                          
191013                 MOVE 'CURSOR:  DPT_RQST_CURSOR'                          
191014                                   TO DP013-MESSAGE-TEXT (2)              
191015                 EXEC SQL                                                 
191016                      OPEN DPT_RQST_CURSOR                                
191100                 END-EXEC                                                 
191200                                                                          
191300             WHEN ASC-VND-CURSOR                                          
191802             WHEN ASC-VLVND-CURSOR                                        
191400                 MOVE 'CURSOR:  VND_RQST_CURSOR'                          
191500                                   TO DP013-MESSAGE-TEXT (2)              
191600                 EXEC SQL                                                 
191700                      OPEN VND_RQST_CURSOR                                
191800                 END-EXEC                                                 
191801                                                                          
191900                                                                          
192000             WHEN ASC-DPTVND-CURSOR                                       
192100                 MOVE 'CURSOR:  DPTVND_RQST_CURSOR'                       
192200                                   TO DP013-MESSAGE-TEXT (2)              
192300                 EXEC SQL                                                 
192400                      OPEN DPTVND_RQST_CURSOR                             
192500                 END-EXEC                                                 
192600         END-EVALUATE                                                     
192700                                                                          
192800         EVALUATE TRUE                                                    
192900             WHEN SQLCODE = ZERO                                          
193000             WHEN SQLCODE = -904                                          
193100             WHEN SQLCODE = -913                                          
193200                 CONTINUE                                                 
193300             WHEN SQLWARN0 NOT = SPACES                                   
193400             WHEN SQLCODE < ZERO                                          
193500             WHEN SQLCODE > ZERO                                          
193600                 MOVE '4010-OPEN-CURSOR'                                  
193700                                   TO DP013-PARAGRAPH                     
193800                 MOVE 'OPEN PRETICKET REQUEST CURSOR - '                  
193900                                   TO DP013-MESSAGE-TEXT (1)              
194000                 MOVE SQLCA        TO DP013-SQLCA                         
194100                 MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)            
194200                 MOVE 'TPTKLNE '   TO DP013-DB2-TABLE-NAME (2)            
194300                 SET DP013-DB2-ABEND                                      
194400                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
194500                 PERFORM DP013-0000-PROCESS-ABEND                         
194600         END-EVALUATE                                                     
194700     END-PERFORM.                                                         
194800*                                                                         
194900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
195000         MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
195100         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
195200                                   TO DP013-MESSAGE-TEXT (1)              
195300         MOVE SQLCA                TO DP013-SQLCA                         
195400         MOVE 'TPTKREQ '           TO DP013-DB2-TABLE-NAME (1)            
195500         MOVE 'TPTKLNE '           TO DP013-DB2-TABLE-NAME (2)            
195600         SET DP013-DB2-ABEND                                              
195700             DP013-XCTL-DISPLAY-RESTART                                   
195800                                   TO TRUE                                
195900         PERFORM DP013-0000-PROCESS-ABEND                                 
196000     END-IF.                                                              
196100                                                                          
196200 EJECT                                                                    
196300*----------------------------------------------------------------*        
196400* CLOSE THE TMESSAGE CURSOR.                                     *        
196500*   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
196600*   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
196700*   USER.                                                        *        
196800*----------------------------------------------------------------*        
196900                                                                          
197000 4020-CLOSE-CURSOR.                                                       
197100     EVALUATE TRUE                                                        
197200         WHEN ASC-PO-CURSOR                                               
197300             EXEC SQL                                                     
197400                  CLOSE PO_RQST_CURSOR                                    
197500             END-EXEC                                                     
197600                                                                          
197700         WHEN ASC-DPT-CURSOR                                              
197800             EXEC SQL                                                     
197900                  CLOSE DPT_RQST_CURSOR                                   
198000             END-EXEC                                                     
198100                                                                          
198200         WHEN ASC-VND-CURSOR                                              
198502         WHEN ASC-VLVND-CURSOR                                            
198300             EXEC SQL                                                     
198400                  CLOSE VND_RQST_CURSOR                                   
198500             END-EXEC                                                     
198501                                                                          
198600                                                                          
198700         WHEN ASC-DPTVND-CURSOR                                           
198800             EXEC SQL                                                     
198900                  CLOSE DPTVND_RQST_CURSOR                                
199000             END-EXEC                                                     
199100     END-EVALUATE.                                                        
199200*                                                                         
199300     EVALUATE TRUE                                                        
199400         WHEN SQLCODE = ZERO                                              
199500             CONTINUE                                                     
199600         WHEN SQLWARN0 NOT = SPACES                                       
199700         WHEN SQLCODE < ZERO                                              
199800         WHEN SQLCODE > ZERO                                              
199900             MOVE '4020-CLOSE-CURSOR'                                     
200000                               TO DP013-PARAGRAPH                         
200100             MOVE 'CLOSE SYSTEM MESSAGE CURSOR '                          
200200                               TO DP013-MESSAGE-TEXT (1)                  
200300             MOVE SQLCA        TO DP013-SQLCA                             
200400             MOVE 'TSYMSG  '   TO DP013-DB2-TABLE-NAME (1)                
200500             SET DP013-DB2-ABEND                                          
200600                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
200700             PERFORM DP013-0000-PROCESS-ABEND                             
200800     END-EVALUATE.                                                        
200900                                                                          
196200 EJECT                                                                    
196300*----------------------------------------------------------------*        
196400* GET THE CURRENT DATE .                                         *        
196800*----------------------------------------------------------------*        
196900                                                                          
197000 4050-GET-CURRENT-DATE.                                                   
197100     EXEC SQL                                                             
197200         SET :ASC-CURRENT-DATE = CURRENT DATE                             
197700     END-EXEC.                                                            
199200*                                                                         
199300     EVALUATE TRUE                                                        
199400         WHEN SQLCODE = ZERO                                              
199500             CONTINUE                                                     
199800         WHEN OTHER                                                       
199900             MOVE '4050-GET-CURRENT-DATE'                                 
200000                               TO DP013-PARAGRAPH                         
200300             MOVE SQLCA        TO DP013-SQLCA                             
200400             MOVE 'TSYMSG  '   TO DP013-DB2-TABLE-NAME (1)                
200500             SET DP013-DB2-ABEND                                          
200600                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
200700             PERFORM DP013-0000-PROCESS-ABEND                             
200800     END-EVALUATE.                                                        
200900                                                                          
201000 EJECT                                                                    
201100*----------------------------------------------------------------*        
201200*    SET UP THE HOST VARIABLES.                                  *        
201300*----------------------------------------------------------------*        
201400                                                                          
201500 4100-PREPARE-CURSOR.                                                     
201600                                                                          
201700     SET ASC-NO-CURSOR         TO TRUE.                                   
201800                                                                          
201900     EVALUATE TRUE                                                        
202000         WHEN ASC-KEY-PO-NUMBER GREATER THAN ZERO                       86
202100             SET ASC-PO-CURSOR     TO TRUE                                
202200                                                                        86
202300         WHEN ASC-KEY-DEPT-NBR GREATER THAN SPACES                        
202400             IF  ASC-KEY-VENDOR-NAME GREATER THAN SPACES                  
202500                 SET ASC-DPTVND-CURSOR                                    
202600                                   TO TRUE                                
202700             ELSE                                                         
202800                 SET ASC-DPT-CURSOR                                       
202900                                   TO TRUE                                
203000             END-IF                                                       
203100                                                                          
203200         WHEN ASC-KEY-VENDOR-NAME GREATER THAN SPACES                     
203300             PERFORM 4200-RACF-ACCESS                                     
203400             IF DP008-ACCESS-ALLOWED                                      
203500                 SET ASC-VLVND-CURSOR TO TRUE                             
203501             ELSE                                                         
203502                 SET ASC-VND-CURSOR   TO TRUE                             
203503             END-IF                                                       
203504     END-EVALUATE.                                                        
203505                                                                          
203506*----------------------------------------------------------------*        
203507*    DETERMINE IF USER HAS RACF ACCESS TO UPDATE THE FUNCTION             
203508*----------------------------------------------------------------*        
203509 4200-RACF-ACCESS.                                                        
203510                                                                          
203520      MOVE 'F'                           TO DP008-RESOURCE-TYPE           
203530      MOVE 'VLPTKUPD'                    TO DP008-RESOURCE-NAME           
203540      SET DP008-ACCESS-TYPE-READ         TO TRUE                          
203550                                                                          
203560      EXEC CICS                                                           
203570         LINK PROGRAM (DP008-SECURITY-SUBROUTINE)                         
203580              COMMAREA (DP008-SECURITY-COMMAREA)                          
203590              LENGTH (DP008-SECURITY-COMMAREA-LENGTH)                     
203600      END-EXEC                                                            
203700                                                                          
203800      IF DP008-CALL-SUCCESSFUL                                            
203900         CONTINUE                                                         
204000     ELSE                                                                 
204100         MOVE '4200-RACF-ACCESS'                                          
204200                               TO DP013-PARAGRAPH                         
204300          MOVE 'UNABLE TO ACCESS CICS'                                    
204400                               TO DP013-MESSAGE-TEXT (1)                  
204500          MOVE SQLCA                 TO DP013-SQLCA                       
204600          SET DP013-DB2-ABEND                                             
204700              DP013-XCTL-DISPLAY-RESTART                                  
204800                               TO TRUE                                    
204900          PERFORM DP013-0000-PROCESS-ABEND                                
204901      END-IF.                                                             
204902 EJECT                                                                    
204903*----------------------------------------------------------------*        
204904*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
204905*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
204906*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
204907*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
204908*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
204909*                                                                *        
204910*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
204911*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
204912*    UPON INITIALIZATION.                                        *        
204913*----------------------------------------------------------------*        
204914                                                                          
204915 4300-READ-ITEMS-FROM-DB.                                                 
204916                                                                          
205000     PERFORM                                                              
205100         VARYING PV-READ-COUNT                                            
205200         FROM 1 BY 1                                                      
205300         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
205400            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
205500                                                                          
               SET PS-NOT-AUTH-FOR-DEPT TO TRUE                                 
                                                                                
 0             PERFORM 4310-FETCH                                               
205600                 UNTIL PS-AUTH-FOR-DEPT                                   
                          OR ASC-NO-ITEMS-LEFT-ON-DB                            
205700                                                                          
205800         IF  ASC-ITEMS-LEFT-ON-DB                                         
205900             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
206000                 ADD +1            TO ASC-ITEM-SUB                        
206100             ELSE                                                         
206200                 ADD +1            TO ASC-BLK-SUB                         
206300                 MOVE +1           TO ASC-ITEM-SUB                        
206400                                                                          
206500                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
206600                     MOVE +1       TO ASC-BLK-SUB                         
206700                     SET ASC-BLOCK-MODIFIED (1)                           
206800                                   TO TRUE                                
206900                     PERFORM 5100-WRITE-TEMP-STORAGE                      
207000                     MOVE ASC-BLOCK-ENTRIES                               
207100                               (PC-MAX-BLOCKS-IN-ARRAY)                   
207200                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
207300                     ADD +1        TO ASC-BLK-SUB                         
207400                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
207500                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
207600                                   TO TRUE                                
207700                 END-IF                                                   
207800             END-IF                                                       
207900             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
208000             MOVE ASC-NUMBER-OF-ITEMS-READ                                
208100                                   TO ASC-LIST-ITEM-NUMBER                
208200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
208300             MOVE PTKREQ-PO-NBR    TO ASC-PO-NUMBER                       
208400                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
208500             MOVE PTKREQ-PO-REQ-NBR                                       
208600                                   TO ASC-RQST-NBR                        
208700                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
208800             MOVE PTKREQ-REQ-TYP-CDE                                      
208900                                   TO ASC-RQST-TYPE                       
209000                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
209100             MOVE PTKREQ-REQ-STAT-CDE                                     
209200                                   TO ASC-RQST-STAT                       
209300                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
209400             MOVE ENTNME-ENT-NAME-DESC                                    
209500                                   TO ASC-VENDOR-NAME                     
209600                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
209700             MOVE PTKREQ-ENT-ID    TO ASC-ENT-ID                          
209800                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
209900             MOVE PTKREQ-DEPT-NBR  TO ASC-DEPT-NBR                        
210000                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
210100             MOVE PTKREQ-REQ-CREN-DTE                                     
210200                                   TO ASC-RQST-DATE                       
210300                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
210400             MOVE PTKREQ-ACTL-PRT-DTE                                     
210500                                   TO ASC-PRINT-DATE                      
210600                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
210700                                                                          
210800             PERFORM 4320-GET-TICKET-TOTAL                                
210900                                                                          
211000             IF PV-REQUEST-TICKET-TOTAL > 9999999                         
289600                 MOVE '4400-READ-ITEMS-FROM-DB'                           
289700                               TO DP013-PARAGRAPH                         
289800                 MOVE 'TICKET QUANTITY TOO LARGE FOR FIELDS'              
289900                               TO DP013-MESSAGE-TEXT (1)                  
290000                 MOVE PTKREQ-PO-NBR                                       
290100                               TO PV-DISPLAY-COMP1                        
290101                 MOVE PV-REQUEST-TICKET-TOTAL                             
290102                               TO PV-DISPLAY-COMP2                        
290103                 STRING 'PO: '          DELIMITED BY SIZE                 
290104                        PV-DISPLAY-COMP1                                  
290105                                        DELIMITED BY SIZE                 
290106                        ' - TICKET QUANTITY: '                            
290107                                        DELIMITED BY SIZE                 
290108                        PV-DISPLAY-COMP2                                  
290109                                        DELIMITED BY SIZE                 
290110                               INTO PV-DISPLAY-ABEND-MESSAGE              
290111                 MOVE PV-DISPLAY-ABEND-MESSAGE                            
290112                               TO DP013-MESSAGE-TEXT (2)                  
290113                 MOVE PV-REQUEST-TICKET-TOTAL                             
290114                               TO DP013-MESSAGE-TEXT (3)                  
290200                 SET DP013-LOGIC-ABEND                                    
290300                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
290400                 PERFORM DP013-0000-PROCESS-ABEND                         
290500             ELSE                                                         
290600                 MOVE PV-REQUEST-TICKET-TOTAL                             
290700                                   TO ASC-RQSTD-TICKETS                   
290800                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
290900             END-IF                                                       
291000                                                                          
291100             PERFORM 4330-GET-SHIP-DATE                                   
291200                                                                          
291300         END-IF                                                           
291400     END-PERFORM.                                                         
291500                                                                          
291600     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
291700         MOVE +1               TO ASC-BLK-SUB                             
291800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
291900         ADD +1                TO ASC-BLK-SUB                             
292000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
292100                                                                          
292200         MOVE 1                TO ASC-BLK-SUB                             
292300                                  PV-BLOCK-NUMBER                         
292400         PERFORM 4410-READ-TEMP-STORAGE                                   
292500         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
292600             MOVE 2            TO ASC-BLK-SUB                             
292700                                  PV-BLOCK-NUMBER                         
292800             PERFORM 4410-READ-TEMP-STORAGE                               
292900         END-IF                                                           
293000     END-IF.                                                              
293100 EJECT                                                                    
293200*----------------------------------------------------------------*        
293300*    FETCH THE MESSAGE CURSOR.                                   *        
293400*----------------------------------------------------------------*        
293500                                                                          
293600 4310-FETCH.                                                              
293700                                                                          
293800     EVALUATE TRUE                                                        
293900         WHEN ASC-PO-CURSOR                                               
294000             EXEC SQL                                                     
294100                  FETCH PO_RQST_CURSOR                                    
294200                  INTO                                                    
294300                    :PTKREQ-PO-REQ-NBR                                    
294400                   ,:PTKREQ-PO-NBR                                        
294500                   ,:PTKREQ-ADDR-SEQ-NBR                                  
294600                   ,:PTKREQ-REQ-TYP-CDE                                   
294700                   ,:PTKREQ-REQ-STAT-CDE                                  
294800                   ,:ENTNME-ENT-NAME-DESC                                 
294900                   ,:PTKREQ-ENT-ID                                        
295000                   ,:PTKREQ-DEPT-NBR                                      
295100                   ,:PTKREQ-REQ-CREN-DTE                                  
295200                   ,:PTKREQ-ACTL-PRT-DTE                                  
295300             END-EXEC                                                     
295400                                                                          
295500         WHEN ASC-DPT-CURSOR                                              
295600             EXEC SQL                                                     
295700                  FETCH DPT_RQST_CURSOR                                   
295800                  INTO                                                    
295900                    :PTKREQ-PO-REQ-NBR                                    
296000                   ,:PTKREQ-PO-NBR                                        
296100                   ,:PTKREQ-ADDR-SEQ-NBR                                  
296200                   ,:PTKREQ-REQ-TYP-CDE                                   
296300                   ,:PTKREQ-REQ-STAT-CDE                                  
296400                   ,:ENTNME-ENT-NAME-DESC                                 
296500                   ,:PTKREQ-ENT-ID                                        
296600                   ,:PTKREQ-DEPT-NBR                                      
296700                   ,:PTKREQ-REQ-CREN-DTE                                  
296800                   ,:PTKREQ-ACTL-PRT-DTE                                  
296900             END-EXEC                                                     
297000                                                                          
297100         WHEN ASC-VND-CURSOR                                              
298502         WHEN ASC-VLVND-CURSOR                                            
297200             EXEC SQL                                                     
297300                  FETCH VND_RQST_CURSOR                                   
297400                  INTO                                                    
297500                    :PTKREQ-PO-REQ-NBR                                    
297600                   ,:PTKREQ-PO-NBR                                        
297700                   ,:PTKREQ-ADDR-SEQ-NBR                                  
297800                   ,:PTKREQ-REQ-TYP-CDE                                   
297900                   ,:PTKREQ-REQ-STAT-CDE                                  
298000                   ,:ENTNME-ENT-NAME-DESC                                 
298100                   ,:PTKREQ-ENT-ID                                        
298200                   ,:PTKREQ-DEPT-NBR                                      
298300                   ,:PTKREQ-REQ-CREN-DTE                                  
298400                   ,:PTKREQ-ACTL-PRT-DTE                                  
298500             END-EXEC                                                     
298501                                                                          
298600                                                                          
298700         WHEN ASC-DPTVND-CURSOR                                           
298800             EXEC SQL                                                     
298900                  FETCH DPTVND_RQST_CURSOR                                
299000                  INTO                                                    
299100                    :PTKREQ-PO-REQ-NBR                                    
299200                   ,:PTKREQ-PO-NBR                                        
299300                   ,:PTKREQ-ADDR-SEQ-NBR                                  
299400                   ,:PTKREQ-REQ-TYP-CDE                                   
299500                   ,:PTKREQ-REQ-STAT-CDE                                  
299600                   ,:ENTNME-ENT-NAME-DESC                                 
299700                   ,:PTKREQ-ENT-ID                                        
299800                   ,:PTKREQ-DEPT-NBR                                      
299900                   ,:PTKREQ-REQ-CREN-DTE                                  
300000                   ,:PTKREQ-ACTL-PRT-DTE                                  
300100             END-EXEC                                                     
300200     END-EVALUATE.                                                        
300300                                                                          
300400     EVALUATE TRUE                                                        
300500         WHEN SQLCODE = ZERO                                              
                   IF ASC-VND-CURSOR                                            
                       IF PTKREQ-DEPT-NBR = PV-SAVE-DEPT-NBR                    
                           CONTINUE                                             
                       ELSE                                                     
                           MOVE PTKREQ-DEPT-NBR TO PV-SAVE-DEPT-NBR             
                           PERFORM 4500-CHECK-DEPT-AUTHORIZATION                
                       END-IF                                                   
                   ELSE                                                         
                       MOVE +1 TO PV-DB2-COUNT                                  
                   END-IF                                                       
                                                                                
                   IF PV-DB2-COUNT > +0                                         
                       SET PS-AUTH-FOR-DEPT TO TRUE                             
                   ELSE                                                         
                       SET PS-NOT-AUTH-FOR-DEPT TO TRUE                         
                   END-IF                                                       
                                                                                
300600         WHEN SQLCODE = +100                                              
300700              CONTINUE                                                    
300800         WHEN SQLWARN0 NOT = SPACE                                        
300900         WHEN SQLCODE NOT = ZERO                                          
301000              MOVE '4310-FETCH-CURSOR'                                    
301100                               TO DP013-PARAGRAPH                         
301200              MOVE 'FETCH MESSAGE CURSOR'                                 
301300                               TO DP013-MESSAGE-TEXT (1)                  
301400              MOVE SQLCA       TO DP013-SQLCA                             
301500              SET DP013-DB2-ABEND                                         
301600                               TO TRUE                                    
301700              PERFORM DP013-0000-PROCESS-ABEND                            
301800     END-EVALUATE.                                                        
301900                                                                          
302000     IF  SQLCODE EQUAL +100                                               
302100         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
302200                                   TO TRUE                                
302300     END-IF.                                                              
302400                                                                          
302500*----------------------------------------------------------------*        
302600*    GET THE TICKET QUANTITY FOR A PO/REQUEST                    *        
302700*----------------------------------------------------------------*        
302800                                                                          
302900 4320-GET-TICKET-TOTAL.                                                   
303000                                                                          
303100     EXEC SQL                                                             
303200         SELECT                                                           
303300              SUM(TKT_QTY)                                                
303400         INTO                                                             
303500              :PV-REQUEST-TICKET-TOTAL                                    
303600         FROM                                                             
303700              TPTKLNE                                                     
303800         WHERE                                                            
303900              PO_REQ_NBR = :PTKREQ-PO-REQ-NBR                             
304000     END-EXEC                                                             
304100                                                                          
304200     EVALUATE TRUE                                                        
304300         WHEN SQLCODE = ZERO                                              
304500              CONTINUE                                                    
304400         WHEN SQLCODE = -305                                              
304500              MOVE +0 TO PV-REQUEST-TICKET-TOTAL                          
304600         WHEN SQLWARN0 NOT = SPACE                                        
304700         WHEN SQLCODE NOT = ZERO                                          
304800              MOVE '4320-GET-TICKET-TOTAL'                                
304900                               TO DP013-PARAGRAPH                         
305000              MOVE 'SELECT TICKET TOTAL FROM REQUEST DETAIL'              
305100                               TO DP013-MESSAGE-TEXT (1)                  
305200              MOVE SQLCA       TO DP013-SQLCA                             
305300              SET DP013-DB2-ABEND                                         
305400                               TO TRUE                                    
305500              PERFORM DP013-0000-PROCESS-ABEND                            
305600     END-EVALUATE.                                                        
305700                                                                          
305800                                                                          
305900*----------------------------------------------------------------*        
306000*    GET THE SHIP DATE(S) FOR A PO/REQUEST                       *        
306100*----------------------------------------------------------------*        
306200                                                                          
306300 4330-GET-SHIP-DATE.                                                      
306400                                                                          
306500     EXEC SQL                                                             
306600         SELECT                                                           
306700              MIN(B.SHIPT_DTE)                                            
306800            , COUNT(DISTINCT B.SHIPT_DTE)                                 
306900         INTO                                                             
307000              :PTKSHP-SHIPT-DTE                                           
307100            , :PV-SHIP-DATE-COUNT                                         
307200         FROM                                                             
307300              TREQSHP A                                                   
307400            , TPTKSHP B                                                   
307500         WHERE                                                            
307600               A.PO_REQ_NBR = :PTKREQ-PO-REQ-NBR                          
307700           AND A.SHIPT_NBR  = B.SHIPT_NBR                                 
307800     END-EXEC                                                             
307900                                                                          
308000     EVALUATE TRUE                                                        
308100         WHEN SQLCODE = ZERO                                              
308200              MOVE PTKSHP-SHIPT-DTE TO ASC-SHIP-DATE                      
308300                                 (ASC-BLK-SUB ASC-ITEM-SUB)             86
308400              IF PV-SHIP-DATE-COUNT = 0                                   
308500                  MOVE SPACES       TO ASC-SHIP-DATE                      
308600                                 (ASC-BLK-SUB ASC-ITEM-SUB)             86
308700              ELSE                                                        
308800                  IF PV-SHIP-DATE-COUNT > 1                               
308900                      MOVE 'MULT'   TO ASC-SHIP-DATE                      
309000                                 (ASC-BLK-SUB ASC-ITEM-SUB)             86
309100                  END-IF                                                  
309200              END-IF                                                      
309300                                                                          
309400         WHEN SQLCODE = -305                                              
309500              MOVE SPACES           TO ASC-SHIP-DATE                      
309600                                 (ASC-BLK-SUB ASC-ITEM-SUB)             86
309700                                                                          
309800         WHEN SQLWARN0 NOT = SPACE                                        
309900         WHEN SQLCODE NOT = ZERO                                          
310000              MOVE '4320-GET-TICKET-TOTAL'                                
310100                               TO DP013-PARAGRAPH                         
310200              MOVE 'SELECT TICKET TOTAL FROM REQUEST DETAIL'              
310300                               TO DP013-MESSAGE-TEXT (1)                  
310400              MOVE SQLCA       TO DP013-SQLCA                             
310500              SET DP013-DB2-ABEND                                         
310600                               TO TRUE                                    
310700              PERFORM DP013-0000-PROCESS-ABEND                            
310800     END-EVALUATE.                                                        
310900                                                                          
311000 EJECT                                                                    
311100*----------------------------------------------------------------*        
311200* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
311300*----------------------------------------------------------------*        
311400                                                                          
311500 4400-MOVE-COMMAREA-TO-SCREEN.                                            
311600     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
311700         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
311800         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
311900             MOVE +1    TO ASC-BLK-SUB                                    
312000             PERFORM 5100-WRITE-TEMP-STORAGE                              
312100             ADD +1     TO ASC-BLK-SUB                                    
312200             PERFORM 5100-WRITE-TEMP-STORAGE                              
312300             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
312400                 GIVING PV-BLOCK-NUMBER                                   
312500                 REMAINDER PV-REMAINDER                                   
312600             IF  PV-REMAINDER > ZERO                                      
312700                 ADD +1 TO PV-BLOCK-NUMBER                                
312800             END-IF                                                       
312900             MOVE +1    TO ASC-BLK-SUB                                    
313000             PERFORM 4410-READ-TEMP-STORAGE                               
313100             ADD +1     TO ASC-BLK-SUB                                    
313200                           PV-BLOCK-NUMBER                                
313300             PERFORM 4410-READ-TEMP-STORAGE                               
313400     END-IF.                                                              
313500     MOVE +1            TO ASC-BLK-SUB.                                   
313600     COMPUTE ASC-ITEM-SUB =                                               
313700         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
313800         + PC-ITEMS-PER-PANEL).                                           
313900*                                                                         
314000     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
314100         ADD +1         TO ASC-BLK-SUB                                    
314200         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
314300     END-IF.                                                              
314400*                                                                         
314500     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
314600         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
314700     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
314800     PERFORM 4420-FORMAT-LIST-LINES                                       
314900         VARYING PV-SUB                                                   
315000         FROM PC-ITEMS-PER-PANEL BY -1                                    
315100         UNTIL PV-SUB < +1.                                               
315200*                                                                         
315300     COMPUTE ASC-ITEMS-DISPLAYED =                                        
315400         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
315500     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
315600                                      ASTRTITO.                           
315700     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
315800     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
315900     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
316000                                   TO ASELITO.                            
316100*                                                                         
316200     IF  DP020-MSG-NUMBER-X = SPACE                                       
316300         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
316400*            --- MORE ITEMS TO DISPLAY ----                               
316500             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
316600             SET DP020-MSG-INFORMATIONAL TO TRUE                          
316700         END-IF                                                           
316800     END-IF.                                                              
316900*----------------------------------------------------------------*        
317000*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
317100*----------------------------------------------------------------*        
317200                                                                          
317300 4410-READ-TEMP-STORAGE.                                                  
317400                                                                          
317500     EXEC CICS                                                            
317600         READQ TS                                                         
317700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
317800             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
317900             ITEM  (PV-BLOCK-NUMBER)                                      
318000             RESP  (PV-CICS-RESP)                                         
318100     END-EXEC.                                                            
318200                                                                          
318300     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
318400       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
318500         CONTINUE                                                         
318600     ELSE                                                                 
318700         SET DP013-CICS-ABEND     TO TRUE                                 
318800         SET DP013-NO-ROLLBACK    TO TRUE                                 
318900         MOVE '4410-READ-TEMP-STORAGE'                                    
319000                                  TO DP013-PARAGRAPH                      
319100         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
319200                                  TO DP013-MESSAGE-TEXT(1)                
319300         PERFORM DP013-0000-PROCESS-ABEND                                 
319400     END-IF.                                                              
319500                                                                          
319600     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
319700*                                                                         
319800     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
319900         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
320000     END-IF.                                                              
320100 EJECT                                                                    
320200*----------------------------------------------------------------*        
320300*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
320400*----------------------------------------------------------------*        
320500                                                                          
320600 4420-FORMAT-LIST-LINES.                                                  
320700     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
320800         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
320900         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
321000         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
321100                                    MR-RQST-STATH (PV-SUB)                
321200         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
321300                                    MR-ACTION (PV-SUB)                    
321400                                    MR-PO-NUMBER (PV-SUB)                 
321500                                    MR-RQST-TYPE (PV-SUB)               86
321600                                    MR-RQST-STAT (PV-SUB)               86
321700                                    MR-VENDOR-NAME (PV-SUB)             86
321800                                    MR-DPT-NBR (PV-SUB)                 86
321900                                    MR-RQST-DATE (PV-SUB)               86
322000                                    MR-PRINT-DATE (PV-SUB)              86
322100                                    MR-SHIP-DATE (PV-SUB)               86
322200                                    MR-RQSTD-TICKETS-X (PV-SUB)         86
322300     ELSE                                                                 
322400         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
322500                                                   > SPACE                
322600             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
322700                                 TO MR-SELECTI (PV-SUB)                   
322800         ELSE                                                             
322900             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
323000                                 TO MR-SELECTI (PV-SUB)                   
323100         END-IF                                                           
323200*                                                                         
323300         IF  DP030-SET-CURSOR-TRAILER                                     
323400             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
323500                 > SPACE)                                                 
323600               OR (PV-SUB = 1))                                           
323700                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
323800                MOVE -1          TO MR-SELECTL (PV-SUB)                   
323900             END-IF                                                       
324000         END-IF                                                           
324100*                                                                         
324200         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
324300             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
324400                                 TO MR-ACTION (PV-SUB)                    
324500             MOVE ASC-PO-NUMBER (ASC-BLK-SUB ASC-ITEM-SUB)                
324600                                 TO  MR-PO-NUMBER-N (PV-SUB)              
324700             MOVE ASC-RQST-TYPE (ASC-BLK-SUB ASC-ITEM-SUB)                
324800                                 TO  MR-RQST-TYPE (PV-SUB)              86
324900             MOVE ASC-RQST-STAT (ASC-BLK-SUB ASC-ITEM-SUB)                
325000                                 TO  MR-RQST-STAT (PV-SUB)              86
325100             MOVE ASC-VENDOR-NAME (ASC-BLK-SUB ASC-ITEM-SUB)              
325200                                 TO  MR-VENDOR-NAME (PV-SUB)            86
325300             MOVE ASC-DEPT-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                 
325400                                 TO  MR-DPT-NBR (PV-SUB)                86
325500             MOVE ASC-RQST-DATE (ASC-BLK-SUB ASC-ITEM-SUB)                
325600                                 TO  PV-WORK-DATE                       86
325700             MOVE PV-DISPLAY-DATE                                         
325800                                 TO  MR-RQST-DATE (PV-SUB)              86
325900                                                                          
326000             IF ASC-PRINT-DATE (ASC-BLK-SUB ASC-ITEM-SUB)                 
326100                  = '0001-01-01'                                          
326200                 MOVE SPACES TO MR-PRINT-DATE (PV-SUB)                    
326300             ELSE                                                         
326400                 MOVE ASC-PRINT-DATE (ASC-BLK-SUB ASC-ITEM-SUB)           
326500                                 TO  PV-WORK-DATE                       86
326600                 MOVE PV-DISPLAY-DATE                                     
326700                                 TO  MR-PRINT-DATE (PV-SUB)             86
326800             END-IF                                                       
326900                                                                          
327000             IF ASC-SHIP-DATE (ASC-BLK-SUB ASC-ITEM-SUB)                  
327100                 = 'MULT'                                                 
327200             OR ASC-SHIP-DATE (ASC-BLK-SUB ASC-ITEM-SUB)                  
327300                 = SPACES                                                 
327400                 MOVE ASC-SHIP-DATE (ASC-BLK-SUB ASC-ITEM-SUB)          86
327500                                 TO  MR-SHIP-DATE (PV-SUB)              86
327600             ELSE                                                         
327700                 MOVE ASC-SHIP-DATE (ASC-BLK-SUB ASC-ITEM-SUB)            
327800                                 TO  PV-WORK-DATE                       86
327900                 MOVE PV-DISPLAY-DATE                                     
328000                                 TO  MR-SHIP-DATE (PV-SUB)              86
328100             END-IF                                                       
328200                                                                          
328300             MOVE ASC-RQSTD-TICKETS (ASC-BLK-SUB ASC-ITEM-SUB)            
328400                                 TO  MR-RQSTD-TICKETS (PV-SUB)          86
328500         END-IF                                                           
328600     END-IF.                                                              
328700     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
328800     IF  ASC-ITEM-SUB < + 1                                               
328900         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
329000         MOVE +1                 TO ASC-BLK-SUB                           
329100     END-IF.                                                              
329200******************************************************************        
329300** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
329400** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
329500** ISSUE A WARNING.                                             **        
329600******************************************************************        
329700 4430-CHECK-FOR-BEGIN-AND-END.                                            
329800     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
329900         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
330000*            -- NO END-OF-RANGE SPECFIED ---                              
330100             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
330200             SET DP020-MSG-WARNING TO TRUE                                
330300             PERFORM 4431-POSITION-AT-WARNING                             
330400         END-IF                                                           
330500     ELSE                                                                 
330600         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
330700*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
330800             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
330900             SET DP020-MSG-WARNING TO TRUE                                
331000             PERFORM 4431-POSITION-AT-WARNING                             
331100         END-IF                                                           
331200     END-IF.                                                              
331300******************************************************************        
331400** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
331500** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
331600** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
331700** POSITION THE CURSOR THERE.                                   **        
331800******************************************************************        
331900 4431-POSITION-AT-WARNING.                                                
332000     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
332100                    - PV-TOP-ITEM + 1.                                    
332200     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
332300         CONTINUE                                                         
332400     ELSE                                                                 
332500         SET DP030-SET-CURSOR-APPL-1                                      
332600                                   TO TRUE                                
332700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
332800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
332900         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
333000     END-IF.                                                              
333100*                                                                         
333200     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
333300                    - PV-TOP-ITEM + 1.                                    
333400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
333500         CONTINUE                                                         
333600     ELSE                                                                 
333700         SET DP030-SET-CURSOR-APPL-1                                      
333800                                   TO TRUE                                
333900         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
334000         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
334100         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
334200     END-IF.                                                              
190703*                                                                         
190704*                                                                         
190705******************************************************************        
190706*  VALIDATE THAT THE USER IS AUTHORIZED TO DEAL WITH THE DEPT   **        
190707* THAT WAS ENTERED.                                                       
190708******************************************************************        
190709 4500-CHECK-DEPT-AUTHORIZATION.                                           
190710                                                                          
190713     EXEC SQL                                                             
190714        SELECT COUNT(*)                                                   
190716        INTO   :PV-DB2-COUNT                                              
190718        FROM                                                              
190719               TMDSEAR A                                                  
190720              ,TRSPBAR B                                                  
190721              ,TWORKER C                                                  
190722        WHERE  A.MDSEAR_DKEY = B.MDSEAR_DKEY                              
190723           AND A.MDSELV_CDE  = :PC-DPT-MDSELV-CDE                         
190724           AND A.OPERCO_NBR  = :PC-OPERCO-NBR                             
190725           AND A.DEPT_NBR    = :PTKREQ-DEPT-NBR                           
190726           AND B.WORKER_STRT_DTE <= CURRENT DATE                          
190727           AND B.WORKER_END_DTE  >= CURRENT DATE                          
190728           AND B.WORKER_NBR  = C.WORKER_NBR                               
190729           AND A.OPERCO_NBR  = C.OPERCO_NBR                               
190730           AND C.UID_NBR     = :DP020-USERID                              
190731     END-EXEC.                                                            
190732                                                                          
190733     EVALUATE TRUE                                                        
190734         WHEN SQLCODE = ZERO                                              
190736         WHEN SQLCODE = +100                                              
190737             CONTINUE                                                     
190738                                                                          
190739         WHEN SQLWARN0 NOT = SPACES                                       
190740         WHEN SQLCODE < ZERO                                              
190741         WHEN SQLCODE > ZERO                                              
190742             MOVE '4500-CHECK-DEPT-AUTHORIZATION'                         
190743                               TO DP013-PARAGRAPH                         
190744             MOVE 'SELECT DEFAULT DEPT'                                   
190745                               TO DP013-MESSAGE-TEXT (1)                  
190746             MOVE SQLCA        TO DP013-SQLCA                             
190747             MOVE 'TWORKER '   TO DP013-DB2-TABLE-NAME (1)                
190748             MOVE 'TMDSEAR '   TO DP013-DB2-TABLE-NAME (2)                
190749             MOVE 'TRSPBAR '   TO DP013-DB2-TABLE-NAME (3)                
190750             SET DP013-DB2-ABEND                                          
190751                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
190752             PERFORM DP013-0000-PROCESS-ABEND                             
190753     END-EVALUATE.                                                        
190754                                                                          
018190     IF PV-DB2-COUNT = +0                                                 
018200         PERFORM 4510-GET-DMA-GMA                                         
018210         IF SQLCODE = +100                                                
018220             CONTINUE                                                     
018230         ELSE                                                             
018240             PERFORM 4520-CHECK-DMA-AUTHORIZATION                         
018250             IF PV-DB2-COUNT = +0                                         
018260                 PERFORM 4530-CHECK-GMA-AUTHORIZATION                     
018270             END-IF                                                       
018280         END-IF                                                           
018290     END-IF.                                                              
018340                                                                          
      ****************************************************************          
      * GET THE DMA AND GMA NUMBERS FOR THIS DEPARTMENT TO CHECK IF  *          
      * THE USER HAS DMA OR GMA ACCESS                                          
      ****************************************************************          
018350 4510-GET-DMA-GMA.                                                        
018360                                                                          
018370     PERFORM                                                              
018380         WITH TEST AFTER                                                  
018390         VARYING PV-RETRY-COUNTER                                         
018400         FROM 1 BY 1                                                      
018410         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
018420             OR (SQLCODE = +0)                                            
018430             OR (SQLCODE = +100))                                         
018440                                                                          
018450         EXEC SQL                                                         
018460              SELECT GMA_NBR,                                             
018470                     DMA_NBR                                              
018480                INTO :DK-GMA-NBR,                                         
018490                     :DK-DMA-NBR                                          
018500                FROM TMDSEAR                                              
018510               WHERE DEPT_NBR    = :PTKREQ-DEPT-NBR                       
018520                 AND MDSELV_CDE  = 'DPT'                                  
018530                 AND OPERCO_NBR  = '03'                                   
018540                 AND STRT_DTE   <= CURRENT DATE                           
018550                 AND END_DTE    >= CURRENT DATE                           
018560         END-EXEC                                                         
018570                                                                          
018580         EVALUATE TRUE                                                    
018590             WHEN SQLCODE = +0                                            
018600             WHEN SQLCODE = +100                                          
018610             WHEN SQLCODE = -904                                          
018620             WHEN SQLCODE = -913                                          
018630                  CONTINUE                                                
018640                                                                          
018650             WHEN SQLWARN0 NOT = SPACES                                   
018660             WHEN SQLCODE < +0                                            
018670             WHEN SQLCODE > +0                                            
018680                  MOVE '4510-GET-DMA-GMA'                                 
018690                                  TO DP013-PARAGRAPH                      
018700                  MOVE 'UNSUCCESSFUL SELECT ON MDSEAR'                    
018710                                  TO DP013-MESSAGE-TEXT (1)               
018720                  MOVE SQLCA      TO DP013-SQLCA                          
018730                  MOVE 'TMDSEAR ' TO DP013-DB2-TABLE-NAME (1)             
018740                  SET DP013-DB2-ABEND                                     
018750                      DP013-XCTL-DISPLAY-RESTART                          
018760                                  TO TRUE                                 
018770                  PERFORM DP013-0000-PROCESS-ABEND                        
018780         END-EVALUATE                                                     
018790     END-PERFORM.                                                         
018800                                                                          
018810     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
018820         MOVE '4510-GET-DMA-GMA'                                          
018830                                  TO DP013-PARAGRAPH                      
018840         MOVE 'MAX RETRIES EXCEEDED ON SELECT ON MDSEAR'                  
018850                                  TO DP013-MESSAGE-TEXT (1)               
018860         MOVE SQLCA               TO DP013-SQLCA                          
018870         MOVE 'TMDSEAR '          TO DP013-DB2-TABLE-NAME (1)             
018880         SET DP013-DB2-ABEND                                              
018890             DP013-XCTL-DISPLAY-RESTART                                   
018900                                  TO TRUE                                 
018910         PERFORM DP013-0000-PROCESS-ABEND                                 
018920     END-IF.                                                              
018930                                                                          
      *****************************************************************         
      * CHECK IF THE USER HAS AUTHORIZATION TO THE DMA FOR THE ENTERED*         
      * DEPARTMENT                                                    *         
      *****************************************************************         
018940 4520-CHECK-DMA-AUTHORIZATION.                                            
018950                                                                          
018960     PERFORM                                                              
018970         WITH TEST AFTER                                                  
018980         VARYING PV-RETRY-COUNTER                                         
018990         FROM 1 BY 1                                                      
019000         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
019010             OR (SQLCODE = +0)                                            
019020             OR (SQLCODE = +100))                                         
019030                                                                          
019040         EXEC SQL                                                         
019050              SELECT COUNT(*)                                             
019060                INTO :PV-DB2-COUNT                                        
019070                FROM TMDSEAR A                                            
019080                    ,TRSPBAR B                                            
019090               WHERE A.MDSEAR_DKEY = B.MDSEAR_DKEY                        
019100                 AND A.DMA_NBR    = :DK-DMA-NBR                           
019110                 AND A.MDSELV_CDE  = 'DMA'                                
019120                 AND A.OPERCO_NBR  = '03'                                 
019130                 AND B.WORKER_STRT_DTE <= CURRENT DATE                    
019140                 AND B.WORKER_END_DTE  >= CURRENT DATE                    
019150                 AND B.WORKER_NBR  =                                      
019160                    (SELECT WORKER_NBR                                    
019170                       FROM TWORKER                                       
019180                      WHERE OPERCO_NBR = '03'                             
019190                        AND UID_NBR    = :DP020-USERID)                   
019200         END-EXEC                                                         
019210                                                                          
019220         EVALUATE TRUE                                                    
019230             WHEN SQLCODE = +0                                            
019240             WHEN SQLCODE = +100                                          
019250             WHEN SQLCODE = -904                                          
019260             WHEN SQLCODE = -913                                          
019270                  CONTINUE                                                
019280                                                                          
019290             WHEN SQLWARN0 NOT = SPACES                                   
019300             WHEN SQLCODE < +0                                            
019310             WHEN SQLCODE > +0                                            
019320                  MOVE '4520-CHECK-DMA-AUTHORIZATION'                     
019330                                  TO DP013-PARAGRAPH                      
019340                  MOVE 'UNSUCCESSFUL COUNT ON WORKER'                     
019350                                  TO DP013-MESSAGE-TEXT (1)               
019360                  MOVE SQLCA      TO DP013-SQLCA                          
019370                  MOVE 'TWORKER ' TO DP013-DB2-TABLE-NAME (1)             
019380                  MOVE 'TRSPBAR ' TO DP013-DB2-TABLE-NAME (2)             
019390                  SET DP013-DB2-ABEND                                     
019400                      DP013-XCTL-DISPLAY-RESTART                          
019410                                  TO TRUE                                 
019420                  PERFORM DP013-0000-PROCESS-ABEND                        
019430         END-EVALUATE                                                     
019440     END-PERFORM.                                                         
019450                                                                          
019460     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
019470         MOVE '4520-CHECK-DMA-AUTHORIZATION'                              
019480                                  TO DP013-PARAGRAPH                      
019490         MOVE 'MAX RETRIES EXCEEDED ON COUNT ON TWORKER'                  
019500                                  TO DP013-MESSAGE-TEXT (1)               
019510         MOVE SQLCA               TO DP013-SQLCA                          
019520         MOVE 'TWORKER '          TO DP013-DB2-TABLE-NAME (1)             
019530         MOVE 'TRSPBAR '          TO DP013-DB2-TABLE-NAME (2)             
019540         SET DP013-DB2-ABEND                                              
019550             DP013-XCTL-DISPLAY-RESTART                                   
019560                                  TO TRUE                                 
019570         PERFORM DP013-0000-PROCESS-ABEND                                 
019580     END-IF.                                                              
019590                                                                          
      *****************************************************************         
      * CHECK IF THE USER HAS AUTHORIZATION TO THE GMA FOR THE ENTERED*         
      * DEPARTMENT                                                    *         
      *****************************************************************         
019600 4530-CHECK-GMA-AUTHORIZATION.                                            
019610                                                                          
019620     PERFORM                                                              
019630         WITH TEST AFTER                                                  
019640         VARYING PV-RETRY-COUNTER                                         
019650         FROM 1 BY 1                                                      
019660         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
019670             OR (SQLCODE = +0)                                            
019680             OR (SQLCODE = +100))                                         
019690                                                                          
019700         EXEC SQL                                                         
019710              SELECT COUNT(*)                                             
019720                INTO :PV-DB2-COUNT                                        
019730                FROM TMDSEAR A                                            
019740                    ,TRSPBAR B                                            
019750               WHERE A.MDSEAR_DKEY = B.MDSEAR_DKEY                        
019760                 AND A.GMA_NBR    = :DK-GMA-NBR                           
019770                 AND A.MDSELV_CDE  = 'GMA'                                
019780                 AND A.OPERCO_NBR  = '03'                                 
019790                 AND B.WORKER_STRT_DTE <= CURRENT DATE                    
019800                 AND B.WORKER_END_DTE  >= CURRENT DATE                    
019810                 AND B.WORKER_NBR  =                                      
019820                    (SELECT WORKER_NBR                                    
019830                       FROM TWORKER                                       
019840                      WHERE OPERCO_NBR = '03'                             
019850                        AND UID_NBR    = :DP020-USERID)                   
019860         END-EXEC                                                         
019870                                                                          
019880         EVALUATE TRUE                                                    
019890             WHEN SQLCODE = +0                                            
019900             WHEN SQLCODE = +100                                          
019910             WHEN SQLCODE = -904                                          
019920             WHEN SQLCODE = -913                                          
019930                  CONTINUE                                                
019940                                                                          
019950             WHEN SQLWARN0 NOT = SPACES                                   
019960             WHEN SQLCODE < +0                                            
019970             WHEN SQLCODE > +0                                            
019980                  MOVE '4530-CHECK-GMA-AUTHORIZATION'                     
019990                                  TO DP013-PARAGRAPH                      
020000                  MOVE 'UNSUCCESSFUL COUNT ON WORKER'                     
020010                                  TO DP013-MESSAGE-TEXT (1)               
020020                  MOVE SQLCA      TO DP013-SQLCA                          
020030                  MOVE 'TWORKER ' TO DP013-DB2-TABLE-NAME (1)             
020040                  MOVE 'TRSPBAR ' TO DP013-DB2-TABLE-NAME (2)             
020050                  SET DP013-DB2-ABEND                                     
020060                      DP013-XCTL-DISPLAY-RESTART                          
020070                                  TO TRUE                                 
020080                  PERFORM DP013-0000-PROCESS-ABEND                        
020090         END-EVALUATE                                                     
020100     END-PERFORM.                                                         
020110                                                                          
020120     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
020130         MOVE '4530-CHECK-GMA-AUTHORIZATION'                              
020140                                  TO DP013-PARAGRAPH                      
020150         MOVE 'MAX RETRIES EXCEEDED ON COUNT ON TWORKER'                  
020160                                  TO DP013-MESSAGE-TEXT (1)               
020170         MOVE SQLCA               TO DP013-SQLCA                          
020180         MOVE 'TWORKER '          TO DP013-DB2-TABLE-NAME (1)             
020190         MOVE 'TRSPBAR '          TO DP013-DB2-TABLE-NAME (2)             
020200         SET DP013-DB2-ABEND                                              
020210             DP013-XCTL-DISPLAY-RESTART                                   
020220                                  TO TRUE                                 
020230         PERFORM DP013-0000-PROCESS-ABEND                                 
020240     END-IF.                                                              
020250                                                                          
334300 EJECT                                                                    
334400*----------------------------------------------------------------*        
334500* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
334600* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
334700* FOR THE SELECTED ITEMS QUEUE.                                  *        
334800*                                                                *        
334900* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
335000* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
335100* THE ARRAY IS FULL.                                             *        
335200*----------------------------------------------------------------*        
335300                                                                          
335400 5000-PREPARE-SEL-QUEUE.                                                  
335500                                                                          
335600     MOVE +1                   TO  ASC-BLK-SUB.                           
335700     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
335800     ADD +1                    TO  ASC-BLK-SUB.                           
335900     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
336000                                                                          
336100     MOVE ASC-SEL-QUEUE-NAME   TO  VL005-SEL-QUEUE-NAME.                  
336200     PERFORM 6010-DELETE-SEL-QUEUE.                                       
336300                                                                          
336400     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
336500                                                                          
336600     SET VL006-IDX             TO  1.                                     
336700     MOVE ZERO                 TO VL005-NUMBER-OF-TS-ITEMS.               
336800     INITIALIZE VL006-SELECTED-ITEM-ARRAY.                                
336900                                                                          
337000     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
337100             VARYING PV-ITEM                                              
337200             FROM 1 BY 1                                                  
337300             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
337400                                                                          
337500     IF  VL006-IDX > 1                                                    
337600         PERFORM 5205-WRITE-SEL-QUEUE                                     
337700         ADD +1                TO VL005-NUMBER-OF-TS-ITEMS                
337800     END-IF.                                                              
337900 EJECT                                                                    
338000*----------------------------------------------------------------*        
338100*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
338200*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
338300*----------------------------------------------------------------*        
338400                                                                          
338500 5100-WRITE-TEMP-STORAGE.                                                 
338600     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
338700                                   TO PV-LIST-ITEM-NUMBER.                
338800     COMPUTE PV-BLOCK-NUMBER =                                            
338900         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
339000                                                                          
339100     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
339200     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
339300         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
339400             PERFORM 5105-REWRITE-ASC-LISTQ                               
339500         END-IF                                                           
339600     ELSE                                                                 
339700         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
339800         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
339900         PERFORM 5110-WRITE-ASC-LISTQ                                     
340000         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
340100     END-IF.                                                              
340200                                                                          
340300     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
340400******************************************************************        
340500** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
340600** SPECIFIC COMMAREA.                                           **        
340700******************************************************************        
340800 5105-REWRITE-ASC-LISTQ.                                                  
340900     EXEC CICS                                                            
341000         WRITEQ TS                                                        
341100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
341200             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
341300             ITEM  (PV-BLOCK-NUMBER)                                      
341400             REWRITE                                                      
341500             RESP  (PV-CICS-RESP)                                         
341600     END-EXEC.                                                            
341700                                                                          
341800     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
341900         CONTINUE                                                         
342000     ELSE                                                                 
342100         SET DP013-CICS-ABEND            TO TRUE                          
342200         SET DP013-NO-ROLLBACK           TO TRUE                          
342300         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
342400         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
342500                                         TO DP013-MESSAGE-TEXT(1)         
342600         PERFORM DP013-0000-PROCESS-ABEND                                 
342700     END-IF.                                                              
342800                                                                          
342900******************************************************************        
343000** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
343100** SPECIFIC COMMAREA.                                           **        
343200******************************************************************        
343300 5110-WRITE-ASC-LISTQ.                                                    
343400     EXEC CICS                                                            
343500         WRITEQ TS                                                        
343600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
343700             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
343800             ITEM  (PV-BLOCK-NUMBER)                                      
343900             RESP  (PV-CICS-RESP)                                         
344000     END-EXEC.                                                            
344100     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
344200         SET DP013-CICS-ABEND      TO TRUE                                
344300         SET DP013-NO-ROLLBACK     TO TRUE                                
344400         MOVE '5110-WRITE-ASC-LISTQ'                                      
344500                                   TO DP013-PARAGRAPH                     
344600         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
344700                                   TO DP013-MESSAGE-TEXT(1)               
344800         PERFORM DP013-0000-PROCESS-ABEND                                 
344900     END-IF.                                                              
345000 EJECT                                                                    
345100*----------------------------------------------------------------*        
345200* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
345300* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
345400*----------------------------------------------------------------*        
345500                                                                          
345600 5200-FILL-SEL-QUEUE-RECORD.                                              
345700                                                                          
345800     PERFORM 6112-READ-LIST-QUEUE.                                        
345900                                                                          
346000     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
346100         PERFORM                                                          
346200         VARYING LQW-IDX                                                  
346300         FROM 1 BY 1                                                      
346400         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
346500           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
346600                                                                          
346700             IF  LQW-SELECT (LQW-IDX)                                     
346800                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
346900                                   TO VL006-ITEM (VL006-IDX)              
347000                 MOVE SPACE        TO VL006-ACTION-SW (VL006-IDX)         
347100                 MOVE LQW-RQST-NBR (LQW-IDX)                              
347200                                   TO VL006-PO-REQ-NBR                    
347300                                        (VL006-IDX)                       
347301                 MOVE LQW-PO-NUMBER (LQW-IDX)                             
347302                                    TO VL006-PO-NUMBER                  11
347303                                        (VL006-IDX)                       
347304                                       DK-PO-NUMBER                       
347305                 MOVE LQW-VENDOR-NAME (LQW-IDX)                           
347306                                    TO VL006-VENDOR-NAME                11
347307                                        (VL006-IDX)                       
347308                 MOVE LQW-ENT-ID (LQW-IDX)                                
347309                                    TO VL006-ENT-ID                     11
347310                                        (VL006-IDX)                       
347311                 MOVE LQW-DEPT-NBR (LQW-IDX)                              
347312                                    TO VL006-DEPT-NBR                   11
347313                                        (VL006-IDX)                       
347314                 MOVE LQW-RQST-STAT (LQW-IDX)                             
347317                     TO  VL005-PRETKT-REQ-STAT                            
347318                 SET VL006-IDX UP BY 1                                    
347319                 IF  VL006-IDX > VL006-MAX-ITEMS                          
347320                     PERFORM 5205-WRITE-SEL-QUEUE                         
347321                     ADD +1        TO VL005-NUMBER-OF-TS-ITEMS            
347322                     INITIALIZE VL006-SELECTED-ITEM-ARRAY                 
347323                     SET VL006-IDX TO  1                                  
347324                 END-IF                                                   
347330             END-IF                                                       
347340         END-PERFORM                                                      
347350     END-IF.                                                              
347360                                                                          
347370*----------------------------------------------------------------*        
347380*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
347390*----------------------------------------------------------------*        
347400                                                                          
347500 5205-WRITE-SEL-QUEUE.                                                    
347600                                                                          
347700     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
347800                                   TO VL006-SELECTED-ITEMS.               
347900                                                                          
348000     EXEC CICS                                                            
348100          WRITEQ TS                                                       
348200              QUEUE (ASC-SEL-QUEUE-NAME)                                  
348300              FROM  (VL006-LINE-SEL-REC)                                  
348400              ITEM  (PV-SEL-QUEUE-ITEM)                                   
348500              RESP  (PV-CICS-RESP)                                        
348600     END-EXEC.                                                            
348700                                                                          
348800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
348900         CONTINUE                                                         
349000     ELSE                                                                 
349100         SET DP013-CICS-ABEND      TO TRUE                                
349200         SET DP013-NO-ROLLBACK     TO TRUE                                
349300         MOVE '5205-WRITE-SEL-QUEUE'                                      
349400                                   TO DP013-PARAGRAPH                     
349500         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
349600                                   TO DP013-MESSAGE-TEXT(1)               
349700         PERFORM DP013-0000-PROCESS-ABEND                                 
349800     END-IF.                                                              
349900 EJECT                                                                    
350000******************************************************************        
350100** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
350200** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
350300** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
350400** THE USER.                                                    **        
350500******************************************************************        
350600 6000-REFRESH-PANEL-FROM-TEMP.                                            
350700     IF  ASC-UPDATE-TSQ                                                   
350800         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
350900         MOVE ZERO                 TO PV-SAVE-ITEM                        
351000                                      PV-SEL-QUEUE-ITEM                   
351100         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
351200                                   TO PV-HOLD-SELECTED-ITEMS              
351300         MOVE +1                   TO PV-ITEMS-PROCESSED                  
351400         PERFORM 6100-UPDATE-TSQ                                          
351500             UNTIL PV-ITEMS-PROCESSED >                                   
351600                                     PV-HOLD-SELECTED-ITEMS               
351700         PERFORM 6010-DELETE-SEL-QUEUE                                    
351800         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
351900                    ASC-BLOCK-ENTRIES (2)                                 
352000     END-IF.                                                              
352100                                                                          
352200*----------------------------------------------------------------*        
352300*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
352400*----------------------------------------------------------------*        
352500 6010-DELETE-SEL-QUEUE.                                                   
352600                                                                          
352700     EXEC CICS                                                            
352800         DELETEQ TS                                                       
352900             QUEUE (ASC-SEL-QUEUE-NAME)                                   
353000             RESP  (PV-CICS-RESP)                                         
353100     END-EXEC.                                                            
353200*                                                                         
353300     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
353400        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
353500         CONTINUE                                                         
353600     ELSE                                                                 
353700         SET DP013-CICS-ABEND TO TRUE                                     
353800         SET DP013-NO-ROLLBACK TO TRUE                                    
353900         MOVE '6010-DELETE-SEL-QUEUE'                                     
354000                        TO DP013-PARAGRAPH                                
354100         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
354200                        TO DP013-MESSAGE-TEXT (1)                         
354300         PERFORM DP013-0000-PROCESS-ABEND                                 
354400     END-IF.                                                              
354500                                                                          
354600*----------------------------------------------------------------*        
354700*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
354800*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
354900*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
355000*    THE TEMP QUEUE.                                             *        
355100*----------------------------------------------------------------*        
355200                                                                          
355300 6100-UPDATE-TSQ.                                                         
355400                                                                          
355500     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
355600     PERFORM 6105-READ-SEL-QUEUE.                                         
355700     PERFORM                                                              
355800         VARYING VL006-IDX                                                
355900         FROM 1 BY 1                                                      
356000         UNTIL ((PV-ITEMS-PROCESSED >                                     
356100                              PV-HOLD-SELECTED-ITEMS)                     
356200         OR (VL006-IDX GREATER THAN VL006-MAX-ITEMS))                     
356300         IF  VL006-ACTION-SW (VL006-IDX) > SPACE                          
356400             PERFORM 6110-PROCESS-ITEMS                                   
356500         END-IF                                                           
356600         ADD +1                TO PV-ITEMS-PROCESSED                      
356700     END-PERFORM.                                                         
356800     PERFORM 6111-WRITE-LIST-QUEUE.                                       
356900*----------------------------------------------------------------*        
357000*    READ THE LIST QUEUE RECORD.                                 *        
357100*----------------------------------------------------------------*        
357200                                                                          
357300 6105-READ-SEL-QUEUE.                                                     
357400                                                                          
357500     EXEC CICS                                                            
357600          READQ TS                                                        
357700              QUEUE (ASC-SEL-QUEUE-NAME)                                  
357800              INTO  (VL006-LINE-SEL-REC)                                  
357900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
358000              RESP  (PV-CICS-RESP)                                        
358100     END-EXEC.                                                            
358200*                                                                         
358300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
358400         CONTINUE                                                         
358500     ELSE                                                                 
358600         SET DP013-CICS-ABEND      TO  TRUE                               
358700         SET DP013-NO-ROLLBACK     TO  TRUE                               
358800         MOVE '6105-READ-SEL-QUEUE'                                       
358900                                   TO  DP013-PARAGRAPH                    
359000         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
359100                                   TO  DP013-MESSAGE-TEXT(1)              
359200         PERFORM DP013-0000-PROCESS-ABEND                                 
359300     END-IF.                                                              
359400 EJECT                                                                    
359500*----------------------------------------------------------------*        
359600*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
359700*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
359800*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
359900*----------------------------------------------------------------*        
360000                                                                          
360100 6110-PROCESS-ITEMS.                                                      
360200                                                                          
360300     COMPUTE PV-ITEM = 1 + ((VL006-ITEM (VL006-IDX) - 1)                  
360400                     / PC-ITEMS-PER-PANEL).                               
360500                                                                          
360600     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
360700         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
360800             PERFORM 6111-WRITE-LIST-QUEUE                                
360900         END-IF                                                           
361000         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
361100                                                                          
361200         PERFORM 6112-READ-LIST-QUEUE                                     
361300         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
361400             CONTINUE                                                     
361500         END-IF                                                           
361600     END-IF.                                                              
361700                                                                          
361800     COMPUTE PV-SUB = (VL006-ITEM (VL006-IDX)                             
361900                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
362000     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
362100     MOVE LQW-SELECTED-SW (PV-SUB)                                        
362200                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
362300     EVALUATE TRUE                                                        
362400         WHEN VL006-INQ (VL006-IDX)                                       
362500              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
362600                                                                          
362700         WHEN VL006-UPD (VL006-IDX)                                       
362800              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
362900                                                                          
363000         WHEN VL006-DEL (VL006-IDX)                                       
363100              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
363200                                                                          
363300         WHEN VL006-ERR (VL006-IDX)                                       
363400              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
363500     END-EVALUATE.                                                        
363600                                                                          
363700     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
363800                                                                          
363900*----------------------------------------------------------------*        
364000*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
364100*----------------------------------------------------------------*        
364200                                                                          
364300 6111-WRITE-LIST-QUEUE.                                                   
364400                                                                          
364500     EXEC CICS                                                            
364600         WRITEQ TS                                                        
364700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
364800             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
364900             ITEM  (PV-SAVE-ITEM)                                         
365000             REWRITE                                                      
365100             RESP  (PV-CICS-RESP)                                         
365200     END-EXEC.                                                            
365300*                                                                         
365400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
365500         CONTINUE                                                         
365600     ELSE                                                                 
365700         SET DP013-CICS-ABEND  TO  TRUE                                   
365800         SET DP013-NO-ROLLBACK TO  TRUE                                   
365900         MOVE '6111-WRITE-LIST-QUEUE'                                     
366000                               TO  DP013-PARAGRAPH                        
366100         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
366200                               TO  DP013-MESSAGE-TEXT(1)                  
366300         PERFORM DP013-0000-PROCESS-ABEND                                 
366400     END-IF.                                                              
366500                                                                          
366600 EJECT                                                                    
366700*----------------------------------------------------------------*        
366800*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
366900*----------------------------------------------------------------*        
367000                                                                          
367100 6112-READ-LIST-QUEUE.                                                    
367200                                                                          
367300     EXEC CICS                                                            
367400         READQ TS                                                         
367500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
367600             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
367700             ITEM  (PV-ITEM)                                              
367800             RESP  (PV-CICS-RESP)                                         
367900     END-EXEC.                                                            
368000*                                                                         
368100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
368200         CONTINUE                                                         
368300     ELSE                                                                 
368400         SET DP013-CICS-ABEND        TO TRUE                              
368500         SET DP013-NO-ROLLBACK       TO TRUE                              
368600         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
368700         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
368800                                     TO DP013-MESSAGE-TEXT(1)             
368900         PERFORM DP013-0000-PROCESS-ABEND                                 
369000     END-IF.                                                              
369100                                                                          
369200 EJECT                                                                    
369300*----------------------------------------------------------------*        
369400*    ABEND PROCESSOR MODULE                                      *        
369500*----------------------------------------------------------------*        
369600                                                                          
369700     COPY DPPD013.                                                        
