00001  ID  DIVISION.                                                    02/17/95
00002  PROGRAM-ID.         ETKUP009.                                    ETKUP009
00003  AUTHOR.             STEVE FLUNKER.                                  LV028
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                        CL**2
00005  DATE-WRITTEN.       FEB 1995.                                       CL**2
00006  DATE-COMPILED.                                                      CL**2
00007                                                                      CL**2
00008 ******************************************************************   CL**2
00009 *  ETKUP009 - DELETE RECORDS OFF ETK DATABASES FROM INPUT FILE       CL**2
00010 *  WORK REQUEST #  - 2026                                            CL**2
00011 ******************************************************************   CL**2
00012 *  THIS PROGRAM WILL INPUT A FILE OF ROWS TO BE                      CL**2
00013 *  DELETED FROM THE EDI TRANMISSION TABLES. ROWS WHICH ARE A         CL**2
00014 *  CERTAIN NUMBER OF DAYS OLD ARE THE ONES WHICH WILL BE DELETED.    CL**2
00015 *                                                                    CL**2
00016 *  THE INPUT FILE TO THIS PROGRAM WILL CONTAIN THE TABLE NAME        CL**2
00017 *  AND THE PRIMARY KEY OF THE ROW(S) TO BE DELETED.                  CL**2
00018 *                                                                    CL**3
00019 *  TABLES DELETED FROM:                                              CL**3
00020 *        TINTTRN                                                     CL**3
00021 *        TFGTRST                                                     CL**3
00022 *        TINTERR                                                     CL**3
00023 *        TINTNOT                                                     CL**3
00024 ******************************************************************   CL**2
00025 *                                                                    CL**2
00026  ENVIRONMENT DIVISION.                                               CL**2
00027 *                                                                    CL**2
00028  CONFIGURATION SECTION.                                              CL**2
00029 *                                                                    CL**2
00030  SOURCE-COMPUTER.        IBM-3090.                                   CL**2
00031  OBJECT-COMPUTER.        IBM-3090.                                   CL**2
00032 *                                                                    CL**2
00033  INPUT-OUTPUT SECTION.                                               CL**2
00034  FILE-CONTROL.                                                       CL**2
00035      SELECT  TO-BE-DELETED-FILE                                      CL**2
00036          ASSIGN TO INP01.                                            CL**2
00037                                                                      CL**2
00038  DATA DIVISION.                                                      CL**2
00039  FILE SECTION.                                                       CL**2
00040                                                                      CL**2
00041  FD  TO-BE-DELETED-FILE                                              CL**2
00042      LABEL RECORDS ARE STANDARD                                      CL**2
00043      RECORDING MODE IS F                                             CL**2
00044      BLOCK CONTAINS 0 RECORDS                                        CL*22
00045      RECORD CONTAINS 80 CHARACTERS                                   CL*23
00046      DATA RECORD IS TO-BE-DELETED-RECORD.                            CL*21
00047                                                                      CL*21
00048  01  TO-BE-DELETED-RECORD        PIC X(80).                          CL**2
00049                                                                      CL**2
00050                                                                      CL**2
00051  WORKING-STORAGE SECTION.                                            CL**2
00052 *                                                                    CL**2
00053  01  PC-PROGRAM-CONSTANTS.                                           CL**2
00054      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03              CL**7
00055                                               COMP-3.                CL**2
00056 *                                                                    CL**2
00057  01  WS-WORKING-STORAGE.                                             CL*10
00058      05  WS-HOLD-TMST               PIC X(26) VALUE SPACES.          CL*10
00059      05  WS-TMST                    PIC X(26) VALUE SPACES.          CL*10
00060 *                                                                    CL*10
00061  01  PV-PROGRAM-VARIABLES.                                           CL*10
00062      05  PV-PARAGRAPH-NAME          PIC X(30) VALUE SPACES.          CL**7
00063      05  PV-DB2-OPER-ATTEMPTED      PIC X(30) VALUE SPACES.          CL**2
00064      05  PV-SQL-SUB                 PIC 9(03) VALUE ZERO.            CL**2
00065 *                                                                    CL**2
00066  01  PS-PROGRAM-SUBSCRIPTS.                                          CL**2
00067      05  PS-SUB1                 PIC 9(02)    VALUE ZERO.            CL**2
00068 *                                                                    CL**2
00069  01  PS-PROGRAM-SWITCHES.                                            CL**2
00070      05  PS-END-OF-FILE          PIC X        VALUE 'N'.             CL**2
00071          88  PS-EOF                           VALUE 'Y'.             CL**2
00072 *                                                                    CL**2
00073 *****************************************************************    CL**2
00074 *****  TO BE DELETED FILE RECORD LAYOUT.                             CL**2
00075 *****************************************************************    CL**2
00076 *                                                                    CL**2
00077      COPY ETWS035.                                                   CL*28
00078      EJECT                                                           CL**2
00079 *                                                                    CL**2
00080 ******************************************************************   CL**2
00081 *  SQL ABEND WORKING STORAGE                                         CL**2
00082 ******************************************************************   CL**2
00083      COPY DPWS004.                                                   CL**2
00084      EJECT                                                           CL**2
00085 *                                                                    CL**2
00086 *****************************************************************    CL**2
00087 *****  DB2 CHECKPOINT RESTART FOR TIMED COMMIT LOGIC                 CL*10
00088 *****************************************************************    CL**2
00089 *                                                                    CL**2
00090      EXEC SQL                                                        CL**2
00091          INCLUDE TCKRSTCN                                            CL*10
00092      END-EXEC.                                                       CL**2
00093 *                                                                    CL*10
00094 *****************************************************************    CL*10
00095 *****  DB2 EDI INTERCHANGE TRANMISSION TABLE DCLGEN                  CL*10
00096 *****************************************************************    CL*10
00097 *                                                                    CL*10
00098      EXEC SQL                                                        CL*10
00099          INCLUDE TINTTRN                                             CL*10
00100      END-EXEC.                                                       CL*10
00101 *                                                                    CL**2
00102 *****************************************************************    CL**2
00103 *****  DB2 EDI INTERCHANGE TRANMISSION ERROR TABLE DCLGEN            CL**2
00104 *****************************************************************    CL**2
00105 *                                                                    CL**2
00106      EXEC SQL                                                        CL**2
00107          INCLUDE TINTERR                                             CL**3
00108      END-EXEC.                                                       CL**2
00109 *                                                                    CL**2
00110 *****************************************************************    CL**3
00111 *****  DB2 EDI INTERCHANGE TRANMISSION ERROR NOTE TABLE DCLGEN       CL**3
00112 *****************************************************************    CL**3
00113 *                                                                    CL**3
00114      EXEC SQL                                                        CL**3
00115          INCLUDE TINTNOT                                             CL**3
00116      END-EXEC.                                                       CL**3
00117 *                                                                    CL**3
00118 *****************************************************************    CL**2
00119 *****  DB2 EDI FUNCTIONAL GROUP/TRAN SET TABLE DCLGEN                CL**2
00120 *****************************************************************    CL**2
00121 *                                                                    CL**2
00122      EXEC SQL                                                        CL**2
00123          INCLUDE TFGTRST                                             CL**3
00124      END-EXEC.                                                       CL**2
00125 *                                                                    CL**2
00118 *****************************************************************    CL**2
00119 *****  DB2 EDI TANSACTION LIFE CYCLE TABLE DCLGEN                    CL**2
00120 *****************************************************************    CL**2
00121 *                                                                    CL**2
00122      EXEC SQL                                                        CL**2
00123          INCLUDE TTNSTLC                                             CL**3
00124      END-EXEC.                                                       CL**2
00125 *                                                                    CL**2
00126 *****************************************************************    CL**2
00127 * DB2 COMMUNICATIONS AREA                                            CL**2
00128 *****************************************************************    CL**2
00129                                                                      CL**2
00130      EXEC SQL                                                        CL**2
00131          INCLUDE SQLCA                                               CL**2
00132      END-EXEC.                                                       CL**2
00133 *                                                                    CL**2
00134      COPY SQLCA2.                                                    CL**2
00135                                                                      CL**2
00136  01  ABEND-CODE               PIC S9(04)  VALUE ZERO                 CL**2
00137                                           COMP SYNC.                 CL**2
00138      EJECT                                                           CL**2
00139  LINKAGE SECTION.                                                    CL**2
00140 *                                                                    CL**2
00141  PROCEDURE DIVISION.                                                 CL**2
00142 *                                                                    CL**2
00143 ******************************************************************   CL**2
00144 * MAIN-LINE LOGIC.                                                   CL**2
00145 ******************************************************************   CL**2
00146  A100-MAIN-MODULE.                                                   CL**2
00147 *                                                                    CL**5
00148      PERFORM B100-OPEN-FILES.                                        CL**2
00149 *                                                                    CL**5
00150      PERFORM B200-PROCESS-DELETE-FILE                                CL**6
00151              UNTIL PS-EOF.                                           CL**6
00152 *                                                                    CL**4
00153      PERFORM B300-CLOSE-FILES.                                       CL**5
00154      STOP RUN.                                                       CL**2
00155 *                                                                    CL**5
00156 ******************************************************************   CL**5
00157 *  OPEN ALL FILES USED BY THE PROGRAM AND LOCK DOWN TABLES.          CL**8
00158 ******************************************************************   CL**5
00159  B100-OPEN-FILES.                                                    CL**5
00160 *                                                                    CL**5
00161      OPEN INPUT TO-BE-DELETED-FILE.                                  CL**5
00162 *                                                                    CL**8
00163 * LOCK TABLES DOWN.                                                  CL**8
00164 *                                                                    CL**8
00165      EXEC SQL                                                        CL**8
00166         LOCK TABLE TINTTRN IN EXCLUSIVE MODE                         CL**8
00167      END-EXEC.                                                       CL**8
00168                                                                      CL**8
00169      EXEC SQL                                                        CL**8
00170         LOCK TABLE TFGTRST IN EXCLUSIVE MODE                         CL*25
00171      END-EXEC.                                                       CL**8
00172                                                                      CL**8
00173      EXEC SQL                                                        CL**8
00174         LOCK TABLE TINTERR IN EXCLUSIVE MODE                         CL*25
00175      END-EXEC.                                                       CL**8
00176                                                                      CL**8
00177      EXEC SQL                                                        CL**8
00178         LOCK TABLE TINTNOT IN EXCLUSIVE MODE                         CL*25
00179      END-EXEC.                                                       CL**8
00176                                                                      CL**8
00177      EXEC SQL                                                        CL**8
00178         LOCK TABLE TTNSTLC IN EXCLUSIVE MODE                         CL*25
00179      END-EXEC.                                                       CL**8
00180                                                                      CL**8
00181 ******************************************************************   CL**2
00182 *  PROCESS ALL RECORDS TO BE DELETED FROM INPUT FILE.                CL**5
00183 ******************************************************************   CL**2
00184  B200-PROCESS-DELETE-FILE.                                           CL**5
00185 *                                                                    CL**2
00186      EXEC SQL                                                        CL*11
00187        SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)          CL*11
00188        INTO :WS-HOLD-TMST                                            CL*11
00189        FROM TCKRSTCNTL                                               CL*11
00190        WHERE PLAN_NAME = 'ETKUP009'                                  CL*11
00191      END-EXEC.                                                       CL*11
00192                                                                      CL*11
00193      IF SQLCODE = +0                                                 CL*12
00194           CONTINUE                                                   CL*12
00195      ELSE MOVE 'B200-PROCESS-DELETE-FILE'                            CL*16
00196                   TO PV-PARAGRAPH-NAME                               CL*16
00197           MOVE 'FETCH COMMIT FREQUENCY'                              CL*11
00198                   TO  PV-DB2-OPER-ATTEMPTED                          CL*16
00199           PERFORM Z900-SQL-ABEND                                     CL*11
00200      END-IF.                                                         CL*11
00201 *                                                                    CL**9
00202      PERFORM                                                         CL*12
00203       UNTIL (WS-TMST > WS-HOLD-TMST) OR (PS-EOF)                     CL*18
00204 *                                                                    CL*12
00205           EXEC SQL                                                   CL*12
00206              SET :WS-TMST = CURRENT TIMESTAMP                        CL*12
00207           END-EXEC                                                   CL*12
00208           IF SQLCODE = +0                                            CL*12
00209              IF WS-TMST > WS-HOLD-TMST                               CL*12
00210                 EXEC SQL                                             CL*14
00211                    COMMIT                                            CL*14
00212                 END-EXEC                                             CL*14
00213 *                                                                    CL*14
00214                 IF SQLCODE NOT = +0                                  CL*18
00215                    MOVE 'B200-PROCESS-DELETE-FILE'                   CL*14
00216                        TO PV-PARAGRAPH-NAME                          CL*14
00217                    MOVE 'COMMIT TABLES'                              CL*14
00218                        TO  PV-DB2-OPER-ATTEMPTED                     CL*16
00219                    PERFORM Z900-SQL-ABEND                            CL*14
00220                 END-IF                                               CL*14
00221 *                                                                    CL*14
00222              END-IF                                                  CL*12
00223           ELSE                                                       CL*12
00224              MOVE 'B200-PROCESS-DELETE-FILE'                         CL*16
00225                        TO PV-PARAGRAPH-NAME                          CL*16
00226              MOVE 'CHECK TIMESTAMP FOR COMMIT'                       CL*12
00227                        TO  PV-DB2-OPER-ATTEMPTED                     CL*16
00228              PERFORM Z900-SQL-ABEND                                  CL*12
00229           END-IF                                                     CL*12
00230 *                                                                    CL*12
00231           PERFORM Z100-READ-INPUT-FILE                               CL*12
00232 *                                                                    CL*12
00233           IF NOT PS-EOF                                              CL*13
00234              IF ET035-TABLE-NAME = 'TINTTRN'                         CL*13
00235                 PERFORM C100-DELETE-TINTTRN-ROW                      CL*13
00236              END-IF                                                  CL*13
00237              IF ET035-TABLE-NAME = 'TFGTRST'                         CL*13
00238                 PERFORM C200-DELETE-TFGTRST-ROW                      CL*13
00239              END-IF                                                  CL*13
00240              IF ET035-TABLE-NAME = 'TINTERR'                         CL*13
00241                 PERFORM C300-DELETE-TINTERR-ROW                      CL*13
00242              END-IF                                                  CL*13
00243              IF ET035-TABLE-NAME = 'TINTNOT'                         CL*13
00244                 PERFORM C400-DELETE-TINTNOT-ROW                      CL*13
00245              END-IF                                                  CL*13
00243              IF ET035-TABLE-NAME = 'TTNSTLC'                         CL*13
00244                 PERFORM C500-DELETE-TTNSTLC-ROW                      CL*13
00245              END-IF                                                  CL*13
00246           END-IF                                                     CL*14
00247 *                                                                    CL*13
00248      END-PERFORM.                                                    CL*12
00249 *                                                                    CL**5
00250 ******************************************************************   CL**2
00251 *  DELETE A ROW FROM THE TINTTRN TABLE.                              CL*15
00252 ******************************************************************   CL**2
00253  C100-DELETE-TINTTRN-ROW.                                            CL*17
00254 *                                                                    CL*17
00255      PERFORM                                                         CL*15
00256          WITH TEST AFTER                                             CL*15
00257          VARYING PV-SQL-SUB                                          CL*15
00258          FROM 1 BY 1                                                 CL*15
00259          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*15
00260                    OR SQLCODE = 0                                    CL*15
00261                    OR SQLCODE = +100)                                CL*15
00262 *                                                                    CL*15
00263          EXEC SQL                                                    CL*15
00264              DELETE                                                  CL*15
00265              FROM TINTTRN                                            CL*15
00266              WHERE TP_DKEY =:ET035-TP-DKEY                           CL*15
00267                AND TP_DKEY_TMST =:ET035-TP-DKEY-TMST                 CL*15
00268          END-EXEC                                                    CL*15
00269 *                                                                    CL*15
00270          EVALUATE TRUE                                               CL*15
00271              WHEN SQLCODE = -904                                     CL*15
00272              WHEN SQLCODE = -911                                     CL*15
00273              WHEN SQLCODE = 100                                      CL*15
00274              WHEN SQLCODE = 0                                        CL*15
00275                  CONTINUE                                            CL*15
00276              WHEN SQLWARN NOT = SPACES                               CL*15
00277              WHEN SQLCODE NOT = ZERO                                 CL*15
00278                  MOVE 'C100-DELETE-TINTTRN-ROW'                      CL*15
00279                                TO  PV-PARAGRAPH-NAME                 CL*15
00280                  MOVE 'DELETE ROW FROM TINTTRN'                      CL*15
00281                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00282                  PERFORM Z900-SQL-ABEND                              CL*15
00283          END-EVALUATE                                                CL*15
00284      END-PERFORM.                                                    CL*15
00285 *                                                                    CL*15
00286      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*15
00287          MOVE 'C100-DELETE-TINTTRN-ROW'                              CL*15
00288                                TO  PV-PARAGRAPH-NAME                 CL*15
00289          MOVE 'DELETE RETRIES EXCEEDED'                              CL*15
00290                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00291          PERFORM Z900-SQL-ABEND                                      CL*15
00292      END-IF.                                                         CL*15
00293 *                                                                    CL*15
00294 ******************************************************************   CL*15
00295 *  DELETE A ROW FROM THE TFGTRST TABLE.                              CL*15
00296 ******************************************************************   CL*15
00297  C200-DELETE-TFGTRST-ROW.                                            CL*17
00298 *                                                                    CL*17
00299      PERFORM                                                         CL*15
00300          WITH TEST AFTER                                             CL*15
00301          VARYING PV-SQL-SUB                                          CL*15
00302          FROM 1 BY 1                                                 CL*15
00303          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*15
00304                    OR SQLCODE = 0                                    CL*15
00305                    OR SQLCODE = +100)                                CL*15
00306 *                                                                    CL*15
00307          EXEC SQL                                                    CL*15
00308            DELETE                                                    CL*15
00309            FROM TFGTRST                                              CL*15
00310            WHERE TP_DKEY         =:ET035-TP-DKEY                     CL*15
00311             AND TP_DKEY_TMST     =:ET035-TP-DKEY-TMST                CL*15
00312             AND FUNC_CDE_SEQ_NBR =:ET035-TFGTRST-FUNC-CDE-SEQ-NBR    CL*15
00313             AND TRN_SET_SEQ_NBR  =:ET035-TFGTRST-TRN-SET-SEQ-NBR     CL*15
00314          END-EXEC                                                    CL*15
00315 *                                                                    CL*15
00316          EVALUATE TRUE                                               CL*15
00317              WHEN SQLCODE = -904                                     CL*15
00318              WHEN SQLCODE = -911                                     CL*15
00319              WHEN SQLCODE = 100                                      CL*15
00320              WHEN SQLCODE = 0                                        CL*15
00321                  CONTINUE                                            CL*15
00322              WHEN SQLWARN NOT = SPACES                               CL*15
00323              WHEN SQLCODE NOT = ZERO                                 CL*15
00324                  MOVE 'C100-DELETE-TFGTRST-ROW'                      CL*15
00325                                TO  PV-PARAGRAPH-NAME                 CL*15
00326                  MOVE 'DELETE ROW FROM TFGTRST'                      CL*15
00327                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00328                  PERFORM Z900-SQL-ABEND                              CL*15
00329          END-EVALUATE                                                CL*15
00330      END-PERFORM.                                                    CL*15
00331 *                                                                    CL*15
00332      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*15
00333          MOVE 'C100-DELETE-TFGTRST-ROW'                              CL*15
00334                                TO  PV-PARAGRAPH-NAME                 CL*15
00335          MOVE 'DELETE RETRIES EXCEEDED'                              CL*15
00336                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00337          PERFORM Z900-SQL-ABEND                                      CL*15
00338      END-IF.                                                         CL*15
00339 *                                                                    CL*15
00340 ******************************************************************   CL*15
00341 *  DELETE ALL RELATED ROWS FROM THE TINTERR TABLE.                   CL*26
00342 ******************************************************************   CL*15
00343  C300-DELETE-TINTERR-ROW.                                            CL*27
00344 *                                                                    CL*17
00345      PERFORM                                                         CL*26
00346          WITH TEST AFTER                                             CL*26
00347          VARYING PV-SQL-SUB                                          CL*26
00348          FROM 1 BY 1                                                 CL*26
00349          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*26
00350                    OR SQLCODE = 0                                    CL*26
00351                    OR SQLCODE = +100)                                CL*26
00352 *                                                                    CL*26
00353          EXEC SQL                                                    CL*26
00354              DELETE                                                  CL*26
00355              FROM TINTERR                                            CL*26
00356              WHERE TP_DKEY =:ET035-TP-DKEY                           CL*26
00357                AND TP_DKEY_TMST =:ET035-TP-DKEY-TMST                 CL*26
00358          END-EXEC                                                    CL*26
00359 *                                                                    CL*26
00360          EVALUATE TRUE                                               CL*26
00361              WHEN SQLCODE = -904                                     CL*26
00362              WHEN SQLCODE = -911                                     CL*26
00363              WHEN SQLCODE = 100                                      CL*26
00364              WHEN SQLCODE = 0                                        CL*26
00365                  CONTINUE                                            CL*26
00366              WHEN SQLWARN NOT = SPACES                               CL*26
00367              WHEN SQLCODE NOT = ZERO                                 CL*26
00368                  MOVE 'C100-DELETE-TINTERR-ROW'                      CL*26
00369                                TO  PV-PARAGRAPH-NAME                 CL*26
00370                  MOVE 'DELETE ROW FROM TINTERR'                      CL*26
00371                                TO  PV-DB2-OPER-ATTEMPTED             CL*26
00372                  PERFORM Z900-SQL-ABEND                              CL*26
00373          END-EVALUATE                                                CL*26
00374      END-PERFORM.                                                    CL*26
00375 *                                                                    CL*26
00376      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*26
00377          MOVE 'C100-DELETE-TINTERR-ROW'                              CL*26
00378                                TO  PV-PARAGRAPH-NAME                 CL*26
00379          MOVE 'DELETE RETRIES EXCEEDED'                              CL*26
00380                                TO  PV-DB2-OPER-ATTEMPTED             CL*26
00381          PERFORM Z900-SQL-ABEND                                      CL*26
00382      END-IF.                                                         CL*26
00383 *                                                                    CL*26
00384 ******************************************************************   CL*15
00385 *  DELETE A ROW FROM THE TINTNOT TABLE.                              CL*15
00386 ******************************************************************   CL*15
00387  C400-DELETE-TINTNOT-ROW.                                            CL*17
00388 *                                                                    CL*17
00389      PERFORM                                                         CL*15
00390          WITH TEST AFTER                                             CL*15
00391          VARYING PV-SQL-SUB                                          CL*15
00392          FROM 1 BY 1                                                 CL*15
00393          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*15
00394                    OR SQLCODE = 0                                    CL*15
00395                    OR SQLCODE = +100)                                CL*15
00396 *                                                                    CL*15
00397          EXEC SQL                                                    CL*15
00398              DELETE                                                  CL*15
00399              FROM TINTNOT                                            CL*15
00400              WHERE TP_DKEY =:ET035-TP-DKEY                           CL*15
00401                AND TP_DKEY_TMST =:ET035-TP-DKEY-TMST                 CL*15
00402          END-EXEC                                                    CL*15
00403 *                                                                    CL*15
00404          EVALUATE TRUE                                               CL*15
00405              WHEN SQLCODE = -904                                     CL*15
00406              WHEN SQLCODE = -911                                     CL*15
00407              WHEN SQLCODE = 100                                      CL*15
00408              WHEN SQLCODE = 0                                        CL*15
00409                  CONTINUE                                            CL*15
00410              WHEN SQLWARN NOT = SPACES                               CL*15
00411              WHEN SQLCODE NOT = ZERO                                 CL*15
00412                  MOVE 'C100-DELETE-TINTNOT-ROW'                      CL*15
00413                                TO  PV-PARAGRAPH-NAME                 CL*15
00414                  MOVE 'DELETE ROW FROM TINTNOT'                      CL*15
00415                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00416                  PERFORM Z900-SQL-ABEND                              CL*15
00417          END-EVALUATE                                                CL*15
00418      END-PERFORM.                                                    CL*15
00419 *                                                                    CL*15
00420      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*15
00421          MOVE 'C100-DELETE-TINTNOT-ROW'                              CL*15
00422                                TO  PV-PARAGRAPH-NAME                 CL*15
00423          MOVE 'DELETE RETRIES EXCEEDED'                              CL*15
00424                                TO  PV-DB2-OPER-ATTEMPTED             CL*15
00425          PERFORM Z900-SQL-ABEND                                      CL*15
00426      END-IF.                                                         CL*15
00339 *                                                                    CL*15
00340 ******************************************************************   CL*15
00341 *  DELETE ALL RELATED ROWS FROM THE TTNSTLC TABLE.                   CL*26
00342 ******************************************************************   CL*15
00343  C500-DELETE-TTNSTLC-ROW.                                            CL*27
00344 *                                                                    CL*17
00345      PERFORM                                                         CL*26
00346          WITH TEST AFTER                                             CL*26
00347          VARYING PV-SQL-SUB                                          CL*26
00348          FROM 1 BY 1                                                 CL*26
00349          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*26
00350                    OR SQLCODE = 0                                    CL*26
00351                    OR SQLCODE = +100)                                CL*26
00352 *                                                                    CL*26
00353          EXEC SQL                                                    CL*26
00354              DELETE                                                  CL*26
00355              FROM TTNSTLC                                            CL*26
00356              WHERE TP_DKEY =:ET035-TP-DKEY                           CL*26
00357                AND TP_DKEY_TMST =:ET035-TP-DKEY-TMST                 CL*26
00358          END-EXEC                                                    CL*26
00359 *                                                                    CL*26
00360          EVALUATE TRUE                                               CL*26
00361              WHEN SQLCODE = -904                                     CL*26
00362              WHEN SQLCODE = -911                                     CL*26
00363              WHEN SQLCODE = 100                                      CL*26
00364              WHEN SQLCODE = 0                                        CL*26
00365                  CONTINUE                                            CL*26
00366              WHEN SQLWARN NOT = SPACES                               CL*26
00367              WHEN SQLCODE NOT = ZERO                                 CL*26
00368                  MOVE 'C100-DELETE-TTNSTLC-ROW'                      CL*26
00369                                TO  PV-PARAGRAPH-NAME                 CL*26
00370                  MOVE 'DELETE ROW FROM TTNSTLC'                      CL*26
00371                                TO  PV-DB2-OPER-ATTEMPTED             CL*26
00372                  PERFORM Z900-SQL-ABEND                              CL*26
00373          END-EVALUATE                                                CL*26
00374      END-PERFORM.                                                    CL*26
00375 *                                                                    CL*26
00376      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*26
00377          MOVE 'C500-DELETE-TTNSTLC-ROW'                              CL*26
00378                                TO  PV-PARAGRAPH-NAME                 CL*26
00379          MOVE 'DELETE RETRIES EXCEEDED'                              CL*26
00380                                TO  PV-DB2-OPER-ATTEMPTED             CL*26
00381          PERFORM Z900-SQL-ABEND                                      CL*26
00382      END-IF.                                                         CL*26
00427 *                                                                    CL*26
00428 ******************************************************************   CL*15
00429 *  CLOSE ALL FILES USED BY THIS PROGRAM.                             CL*15
00430 ******************************************************************   CL*15
00431  B300-CLOSE-FILES.                                                   CL*15
00432 *                                                                    CL**2
00433      CLOSE  TO-BE-DELETED-FILE.                                      CL**2
00434 *                                                                    CL**2
00435 ******************************************************************   CL**2
00436 * Z100-READ-INPUT-FILE                                               CL**2
00437 ******************************************************************   CL**2
00438  Z100-READ-INPUT-FILE.                                               CL**2
00439 *                                                                    CL*20
00440      READ TO-BE-DELETED-FILE INTO                                    CL*24
00441           ET035-TRANS-TO-BE-DELETED                                  CL*19
00442             AT END SET PS-EOF TO TRUE.                               CL*19
00443 *                                                                    CL**2
00444 ******************************************************************   CL**2
00445 *  STANDARD DB2 ERROR ABEND ROUTINE                                  CL**2
00446 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                CL**2
00447 ******************************************************************   CL**2
00448  Z900-SQL-ABEND.                                                     CL**2
00449                                                                      CL**2
00450      MOVE +4013                 TO ABEND-CODE.                       CL**2
00451      DISPLAY '**  ABEND     **'.                                     CL**2
00452      DISPLAY '**  PROGRAM   **  =  ETKUP009'.                        CL**2
00453      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**2
00454      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**2
00455                                                                      CL**2
00456 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**2
00457 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**2
00458                                                                      CL**2
00459      COPY DPPD004.                                                   CL**2
00460                                                                      CL**2
00461      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**2
