      *-----------------------*                                         00010074
       IDENTIFICATION DIVISION.                                         00020074
      *-----------------------*                                         00030074
       PROGRAM-ID.    TFKCS454.                                         00040000
       AUTHOR.        JACK KRAMER.                                      00050074
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060074
       DATE-WRITTEN.  MAY 1997.                                         00070000
       DATE-COMPILED.                                                   00080074
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*00090074
      * CONTAINS LOGIC TO OVERRIDE MENOMENEE FALLS AND FINDLAY DC'S TO *00100074
      * RTV'S. (085 BECOMES 801 AND 810 BECOMES 811).                  *00110000
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*00120074
      *----------------------------------------------------------------*00130074
      *    P454 - RESET TRANSFER.                                      *00140000
      *                                                                *00150074
      *    THIS PROGRAM ALLOWS THE USER TO RESET A TRANSFER THAT IS    *00160000
      *    STUCK IN 'RECEIVE IN PROCESS'.                              *00170000
      *----------------------------------------------------------------*00180074
                                                                        00190074
      ******************************************************************        
      * 6/02/2003 - RAY GRAF                                                    
      *  PROCESS NONINTELLIGENT SKU.                                            
      ******************************************************************        
      * 11/17/03  - RAY GRAF                                                    
      *  REMOVE UNUSED PC-DEFECTIVE-DAMAGES WORKING STORAGE FIELD               
      ******************************************************************        
080204**DATE:08/02/2004 CHANGED BY: RAY GRAF                                    
080204**     NO LONGER ACCESS DP020-LOCATION. USE DP020-LOCATION-NBR            
      *****************************************************************         
100404**DATE:10/04/2004 CHANGED BY: RAY GRAF                                    
100404**     EXPAND STORE NUMBER.                                               
      *****************************************************************         
022212**DATE:02/22/2012 CHANGED BY: CHERI PARMLEY     PRJ#3403                  
022212**     COMPILE DUE TO CHANGES TO TTFITEM AND TTFITRC TABLES               
      *****************************************************************         
CICS  * CHANGED 11/16/2022    BY JIM HUNTER      WR: CHG0278345                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
       ENVIRONMENT DIVISION.                                            00200074
                                                                        00210074
       DATA DIVISION.                                                   00220074
                                                                        00240074
       WORKING-STORAGE SECTION.                                         00250074
      *                                                                 00260074
       01  PC-PROGRAM-CONSTANTS.                                        00270074
           05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TF454A  '. 00280000
           05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TFKM454 '. 00290000
           05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TFKCS454'. 00300000
           05  PC-COMPLETED                    PIC  X(02)  VALUE 'CM'.  00310074
           05  PC-ASSUMPTIVELY-RECEIVED        PIC  X(02)  VALUE 'AR'.  00320074
           05  PC-INTERMEDIATELY-RECEIVED      PIC  X(02)  VALUE 'IR'.  00340074
           05  PC-DEFECTIVES-DAMAGES           PIC  X(02)  VALUE '01'.  00350074
           05  PC-YES                          PIC  X(01)  VALUE 'Y'.   00420074
           05  PC-MAX-RETRIES                  PIC S9(03)  VALUE 3      00510074
                                                           COMP-3.      00520074
100404*    05  PC-RTV-MEN-FLS                  PIC  X(03)  VALUE '801'.         
100404     05  PC-RTV-MEN-FLS                  PIC  9(04)  VALUE  801.          
080204     05  PC-DC-MEN-FLS                   PIC  9(05)  VALUE  085.          
100404*    05  PC-RTV-FINDLAY                  PIC  X(03)  VALUE '811'.         
100404     05  PC-RTV-FINDLAY                  PIC  9(04)  VALUE  811.          
080204     05  PC-DC-FINDLAY                   PIC  9(05)  VALUE  810.          
      *                                                                 00650074
           05  PC-TSYMSG-NUMBERS.                                       00530074
               10  PC-TSYMSG-01581       PIC  9(05)  VALUE  01581.      00640074
               10  PC-TSYMSG-01582       PIC  9(05)  VALUE  01582.      00640074
               10  PC-TSYMSG-01583       PIC  9(05)  VALUE  01583.      00640074
               10  PC-TSYMSG-01584       PIC  9(05)  VALUE  01584.      00640074
      *                                                                 00650074
       01  PS-PROGRAM-SWITCHES.                                         00660074
           05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        00670074
               88  PS-ERROR                          VALUE  'Y'.        00680074
               88  PS-NO-ERROR                       VALUE  'N'.        00690074
      *                                                                 00800074
       01  PV-PROGRAM-VARIABLES.                                        00810074
           05  PV-XFER-NBR.                                             00820072
100404*        10  PV-FROM-STORE               PIC X(03).               00830000
100404         10  PV-FROM-STORE               PIC X(04).               00830000
               10  FILLER                      PIC X(01)  VALUE '-'.    00831000
               10  PV-SEQ-NBR                  PIC X(06).               00840000
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE +0      00970074
                                                          COMP SYNC.    00980074
           05  PV-DISPLAY-XFER-DKEY-ID         PIC  9(09)  VALUE ZEROS. 00990074
           05  PV-CURRENT-TIMESTAMP           PIC X(26).                01000074
           05  PV-NULL-IND                     PIC S9(04)  VALUE ZEROS  01010074
                                                           COMP SYNC.   01020074
                                                                        01030074
       01  PM-PROGRAM-MESSAGES.                                         01040074
           05  PM-ABEND-INFO-A.                                         01050074
               10  FILLER                PIC X(15)   VALUE              01060074
                                         'DP020-SRC-AID: '.             01070074
               10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.      01080074
               10  FILLER                PIC X(19)   VALUE              01090074
                                         '. DP020-FK-ACTION: '.         01100074
               10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.      01110074
               10  FILLER                PIC X(22)   VALUE              01120074
                                         '. DP020-FK-DISPN-IND: '.      01130074
               10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.      01140074
               10  FILLER                PIC X(01)   VALUE '.'.         01150074
               10  FILLER                PIC X(19)   VALUE SPACE.       01160074
                                                                        01170074
       01  DK-DB2-KEYS.                                                 01180074
           05  DK-XFER-DKEY-ID           PIC S9(09) COMP  VALUE +0.     01220074
                                                                        01230074
      *----------------------------------------------------------------*01240074
      *    MAP LAYOUT                                                  *01250074
      *----------------------------------------------------------------*01260074
                                                                        01270074
           COPY TFKM454.                                                01280000
           EJECT                                                        01290074
      *                                                                 01300000
      *----------------------------------------------------------------*01310074
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *01320074
      *----------------------------------------------------------------*01330074
                                                                        01340074
           COPY DPWS015.                                                01350074
                                                                        01360074
      *----------------------------------------------------------------*01370074
      *    FUNCTION KEYS COPYBOOK                                      *01380074
      *----------------------------------------------------------------*01390074
                                                                        01400074
           COPY DPWS016.                                                01410074
       EJECT                                                            01420074
      ******************************************************************01430074
      ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **01440074
      ** SUB-ROUTINE (DPKUT100).                                      **01450074
      ******************************************************************01460074
           COPY DPWS010I.                                               01470074
       EJECT                                                            01480074
      ******************************************************************01490074
      ** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **01500074
      ** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **01510074
      ******************************************************************01520074
           COPY DPWS030.                                                01530074
CICS       COPY DPWS930.                                                01530074
       EJECT                                                            01540074
      ***************************************************************** 01550074
      *    STANDARD COMMAREA.                                           01560074
      ***************************************************************** 01570074
                                                                        01580074
           COPY DPWS020.                                                01590074
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                01600074
                                                                        01610074
      ***************************************************************** 01620074
      *    INTER-APPLICATION COMMAREA.                                  01630074
      ***************************************************************** 01640074
                                                                        01650074
               10  INTER-APPL-COMM-AREA PIC X(200).                     01660074
      * INTER-APPLICATION COMMAREA.                                   *         
           COPY TFWS001.                                                01670074
      *   TFWS006 - IS THE LAYOUT USED FOR RECEIVE STORE OVERRIDE     *         
           COPY TFWS006.                                                01670074
      ***************************************************************** 01680074
      *    SPECIFIC COMMMAREA FOR TFKCS454                              01690000
      ***************************************************************** 01700074
               10  ASC-SPECIFIC-COMMAREA.                               01710074
100404*            15  ASC-STORE-NBR               PIC  9(03).          01720074
100404             15  ASC-STORE-NBR               PIC  S9(04) COMP.    01720074
100404*            15  ASC-TO-STORE-NBR            PIC  9(03).          01720074
100404             15  ASC-TO-STORE-NBR            PIC  S9(04) COMP.    01720074
100404*            15  ASC-XFER-NBR                PIC  9(09).          01750074
100404             15  ASC-XFER-NBR                PIC  9(10).          01750074
                   15  FILLER REDEFINES ASC-XFER-NBR.                   01760074
                       20  ASC-XFER-FR-STR.                             01770074
100404*                    25  ASC-XFER-FR-STR-N   PIC  9(03).          01780074
100404                     25  ASC-XFER-FR-STR-N   PIC  9(04).          01780074
                       20  ASC-XFER-SEQ-NBR.                            01790074
                           25  ASC-XFER-SEQ-NBR-N  PIC  9(06).          01800074
                   15  ASC-XFER-DKEY-ID            PIC S9(09) COMP.     01830074
           EJECT                                                        01960074
      *----------------------------------------------------------------*01970074
      *    ABEND PROCESSING COPYBOOK.                                  *01980074
      *----------------------------------------------------------------*01990074
                                                                        02000074
           COPY DPWS013.                                                02010074
                                                                        02020074
      *---------------------------------------------------------------* 02030012
      *    DB2 AREA FOR TRANSFER MASTER TABLE                         * 02040012
      *---------------------------------------------------------------* 02050012
           EXEC SQL                                                     02060012
                INCLUDE TTFMSTR                                         02070012
           END-EXEC.                                                    02080012
      *---------------------------------------------------------------* 02030012
      *    DB2 AREA FOR TRANSFER ITEM TABLE                           * 02040012
      *---------------------------------------------------------------* 02050012
           EXEC SQL                                                     02060012
                INCLUDE TTFITEM                                         02070012
           END-EXEC.                                                    02080012
      *---------------------------------------------------------------* 02030012
      *    DB2 AREA FOR TRANSFER ITEM RECEIPT TABLE                   * 02040012
      *---------------------------------------------------------------* 02050012
           EXEC SQL                                                     02060012
                INCLUDE TTFITRC                                         02070012
           END-EXEC.                                                    02080012
      *---------------------------------------------------------------* 02090012
      *    DB2 AREA FOR TRANSFER RECEIPT TABLE                        * 02100012
      *---------------------------------------------------------------* 02110012
           EXEC SQL                                                     02120012
                INCLUDE TTFRCPT                                         02130012
           END-EXEC.                                                    02140012
      *---------------------------------------------------------------* 02090012
      *    DB2 AREA FOR TRANSFER STATUS TABLE                         * 02100012
      *---------------------------------------------------------------* 02110012
           EXEC SQL                                                     02120012
                INCLUDE TTFSTAT                                         02130012
           END-EXEC.                                                    02140012
      *---------------------------------------------------------------* 02090012
      *    DB2 AREA FOR TRANSFER STATUS CODE TABLE                    * 02100012
      *---------------------------------------------------------------* 02110012
           EXEC SQL                                                     02120012
                INCLUDE TTFSTCD                                         02130012
           END-EXEC.                                                    02140012
      *---------------------------------------------------------------* 02150012
      *    DB2 AREA FOR TRANSFER DISCREPANCY TABLE                    * 02160012
      *---------------------------------------------------------------* 02170012
           EXEC SQL                                                     02180012
                INCLUDE TTFDSCP                                         02190012
           END-EXEC.                                                    02200012
      *---------------------------------------------------------------* 02390074
      *    DB2 AREA FOR COMMUNICATIONS                                * 02400012
      *---------------------------------------------------------------* 02410012
           EXEC SQL                                                     02420012
                INCLUDE SQLCA                                           02430012
           END-EXEC.                                                    02440012
                                                                        02450074
       EJECT                                                            02460074
       LINKAGE SECTION.                                                 02470074
                                                                        02480074
       01  DFHCOMMAREA.                                                 02490074
           05  FILLER                         OCCURS  1 TO 4072 TIMES   02500074
                                              DEPENDING ON EIBCALEN.    02510074
               10  FILLER                     PIC X.                    02520074
                                                                        02530074
                                                                        02540074
       PROCEDURE DIVISION.                                              02550074
      ***************************************************************** 02560074
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** 02570074
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** 02580074
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** 02590074
      **                                                             ** 02600074
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** 02610074
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** 02620074
      ***************************************************************** 02630074
       0000-MAIN-MODULE.                                                02640074
           INITIALIZE DP030-CICS-API-FIELDS.                            02650074
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       02660074
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          02670074
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         02680074
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       02690074
           MOVE LENGTH OF TF454AI        TO DP030-MAP-LENGTH (1).       02700000
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     02710074
           PERFORM 0001-CALL-CICS-ARCH-API.                             02720074
      *                                                                 02730074
           PERFORM 1000-CONTROL-PROCESSING                              02740074
      *                                                                 02750074
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     02760074
           PERFORM 0001-CALL-CICS-ARCH-API.                             02770074
                                                                        02780074
       0001-CALL-CICS-ARCH-API.                                         02790074
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TF454AI.                              
      *                                                                 02850074
           IF  DP030-RC-CALL-SUCCESSFUL                                 02860074
               CONTINUE                                                 02870074
           ELSE                                                         02880074
               SET DP013-NO-ROLLBACK                                    02890074
                   DP013-XCTL-DISPLAY-RESTART                           02900074
                   DP013-CICS-ABEND      TO TRUE                        02910074
               MOVE 'BEFORE 0000-MAIN-MODULE'                           02920074
                                         TO DP013-PARAGRAPH             02930074
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                   02960074
                                         TO DP013-MESSAGE-TEXT (3)      02970074
               PERFORM DP013-0000-PROCESS-ABEND                         02980074
           END-IF.                                                      02990074
       EJECT                                                            03000074
      *----------------------------------------------------------------*03010074
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *03020074
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *03030074
      *----------------------------------------------------------------*03040074
                                                                        03050074
       1000-CONTROL-PROCESSING.                                         03060074
                                                                        03070074
           EVALUATE TRUE                                                03080074
               WHEN DP020-NEXT-ACT-INITIAL                              03090074
                   MOVE SPACES           TO ASC-SPECIFIC-COMMAREA       03100074
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                 03110000
                   PERFORM 4000-BUILD-INITIAL-PANEL                     03120074
                                                                        03130074
               WHEN DP020-NEXT-ACT-READ-MAP                             03140074
                   PERFORM 2000-FKEY-PROCESSING                         03150074
                                                                        03160074
               WHEN DP020-NEXT-ACT-RETURN                               03170074
                   IF TF006-RCV-STR-OVERRIDE                                    
                       MOVE TF006-RCV-STR-NBR   TO ASC-STORE-NBR                
                   END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 03260074
                                                                        03270074
               WHEN OTHER                                               03280074
                   SET DP013-LOGIC-ABEND                                03290074
                       DP013-NO-ROLLBACK TO TRUE                        03300074
                   MOVE '1000-CONTROL-PROCESSING'                       03310074
                                         TO  DP013-PARAGRAPH            03320074
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   03330074
                                         TO  DP013-MESSAGE-TEXT(1)      03340074
                   MOVE DP020-NEXT-APPL-ACTIVITY                        03350074
                                         TO DP013-MESSAGE-TEXT(2)       03360074
                   PERFORM DP013-0000-PROCESS-ABEND                     03370074
           END-EVALUATE.                                                03380074
      ******************************************************************03380100
      ** MOVE DATA FROM THE INTER-APPLICATION COMMAREA TO THE         **03380200
      ** APPLICATION SPECIFIC COMMAREA.                               **03380300
      ******************************************************************03380400
       1100-PROCESS-INTER-APPL-COMM.                                    03380500
           MOVE TF001-XFER-NBR          TO ASC-XFER-NBR.                03380700
           MOVE TF001-DKEY-ID                TO ASC-XFER-DKEY-ID.       03380900
080204     IF DP020-LOCATION-NBR = PC-DC-MEN-FLS                                
               MOVE PC-RTV-MEN-FLS           TO ASC-STORE-NBR                   
           ELSE                                                                 
080204         IF DP020-LOCATION-NBR = PC-DC-FINDLAY                            
                   MOVE PC-RTV-FINDLAY       TO ASC-STORE-NBR                   
               ELSE                                                             
080204             MOVE DP020-LOCATION-NBR   TO ASC-STORE-NBR                   
               END-IF                                                           
           END-IF.                                                              
           PERFORM 8200-GET-TO-STORE.                                   03381500
       EJECT                                                            03382000
                                                                        03390074
      *----------------------------------------------------------------*03400074
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*03410074
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*03420074
      * EDITS.                                                         *03430074
      *----------------------------------------------------------------*03440074
                                                                        03450074
       2000-FKEY-PROCESSING.                                            03460074
                                                                        03470074
           EVALUATE TRUE                                                03480074
                                                                        03490074
               WHEN DP020-SRC-AID = DP016-ENTER                         03701000
               WHEN DP020-SRC-AID = DP016-CLEAR                         03702000
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     03703000
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 03704000
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                 CL**7
      * ------- RECV STR -----------------------------------------------DPKCS035
                   SET DP030-CONTINUE-GO TO TRUE                           CL*11
                   INITIALIZE TF006-INTER-APPL-COMMAREA                    CL**9
                   MOVE ASC-STORE-NBR      TO TF006-RCV-STR-NBR                 
                                                                        03710074
               WHEN OTHER                                               03720074
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   03740074
                   IF  PS-NO-ERROR                                      03750074
                       SET DP030-CONTINUE-GO TO TRUE                    03760074
                       PERFORM 2100-CHECK-FUNCTION-KEY                  03770074
                   ELSE                                                 03780074
                       SET DP030-OVERRIDE-GO TO TRUE                    03790074
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             03800074
                   END-IF                                               03810074
           END-EVALUATE.                                                03820074
      ******************************************************************03830074
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **03840074
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **03850074
      ** FROM THE CICS ARCHITECTURE API.                              **03860074
      ******************************************************************03870074
       2100-CHECK-FUNCTION-KEY.                                         03880074
           EVALUATE TRUE                                                03890074
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)              03990000
      *----- RESET -----------------------------------------------------04000074
                   PERFORM 5000-RESET-TRANSFER                          04011000
                   MOVE ASC-XFER-FR-STR         TO PV-FROM-STORE        04020000
                   MOVE ASC-XFER-SEQ-NBR        TO PV-SEQ-NBR           04020100
                   MOVE PV-XFER-NBR             TO DP020-MSG-TEXT       04020400
                   MOVE PC-TSYMSG-01582         TO  DP020-MSG-NUMBER    04021000
                   SET  DP020-MSG-INFORMATIONAL TO TRUE                 04022000
                                                                        04070074
               WHEN OTHER                                               04080074
                   SET DP013-NO-ROLLBACK                                04090074
                       DP013-XCTL-DISPLAY-RESTART                       04100074
                       DP013-CICS-ABEND  TO TRUE                        04110074
                   MOVE '2100-CHECK-FUNCTION-KEY'                       04120074
                                         TO DP013-PARAGRAPH             04130074
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      04140074
                                         TO DP013-MESSAGE-TEXT (1)      04150074
                   MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:' 04160074
                                         TO DP013-MESSAGE-TEXT (2)      04170074
                   MOVE DP020-SRC-AID    TO PM-A-SRC-AID                04180074
                   MOVE DP020-FK-ACTION (DP020-SRC-AID)                 04190074
                                         TO PM-A-FK-ACTION              04200074
                   MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)              04210074
                                         TO PM-A-FK-DISPN               04220074
                   MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)      04230074
                   MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'        04240074
                                         TO DP013-MESSAGE-TEXT (4)      04250074
                   PERFORM DP013-0000-PROCESS-ABEND                     04260074
           END-EVALUATE.                                                04270074
      *----------------------------------------------------------------*05620074
      * CHECK TO SEE THAT THE RECEIPT ROW'S BEGIN AND END TIMESTAMPS   *05630000
      * ARE EQUAL.                                                     *05640000
      *----------------------------------------------------------------*05670074
                                                                        05680074
       3000-EDIT-DATA-IN-COMMAREA.                                      05690074
           SET PS-NO-ERROR               TO TRUE.                       05700000
           IF ASC-STORE-NBR NOT = ASC-TO-STORE-NBR                              
               SET PS-ERROR                                                     
                   DP030-OVERRIDE-GO                                    20010002
                   DP020-MSG-FATAL TO TRUE                                      
      *   -- CAN NOT RESET ANOTHER STORE'S TRANSFER --                          
                   MOVE PC-TSYMSG-01584 TO DP020-MSG-NUMBER                     
           END-IF.                                                              
           MOVE ASC-XFER-DKEY-ID         TO DK-XFER-DKEY-ID.            05701000
           PERFORM 3030-CHK-XFER-STATUS                                         
           IF PS-NO-ERROR                                                       
               PERFORM 3050-CHK-IN-PROCESS                                      
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  CHECK THE CURRENT STATUS OF THE TRANSFER.                              
      *----------------------------------------------------------------*        
       3030-CHK-XFER-STATUS.                                                    
           PERFORM 7050-SELECT-MAX-STAT-TMST.                                   
           PERFORM 7100-SELECT-CURRENT-STATUS.                                  
                                                                                
           EVALUATE TFSTAT-XFER-STAT-CDE                                        
               WHEN PC-COMPLETED                                                
               WHEN PC-INTERMEDIATELY-RECEIVED                                  
               WHEN PC-ASSUMPTIVELY-RECEIVED                                    
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE TFSTCD-XFER-STAT-DESC  TO  DP020-MSG-TEXT               
                   SET  DP020-MSG-FATAL                                         
                        DP030-OVERRIDE-GO                                       
                        PS-ERROR               TO  TRUE                         
                   MOVE PC-TSYMSG-01581        TO  DP020-MSG-NUMBER             
           END-EVALUATE.                                                        
      *                                                                         
      *----------------------------------------------------------------*        
      *  CHECK TO SEE IF THE TRANSFER IS IN PROCESS.                            
      *----------------------------------------------------------------*        
       3050-CHK-IN-PROCESS.                                                     
           PERFORM 7150-SELECT-MAX-RCPT-TMST.                                   
                                                                                
           IF PV-NULL-IND IS NOT NEGATIVE                                       
               PERFORM 7200-SELECT-CURRENT-RCPT                                 
               IF TFRCPT-RCV-XFER-BEG-TMST IS NOT =                             
                                               TFRCPT-RCV-XFER-END-TMST         
                   SET  DP020-MSG-FATAL                                         
                        DP030-OVERRIDE-GO                                       
                        PS-ERROR               TO  TRUE                         
                   MOVE PC-TSYMSG-01583  TO  DP020-MSG-NUMBER                   
               END-IF                                                           
           ELSE                                                                 
               SET  DP020-MSG-FATAL                                             
                    DP030-OVERRIDE-GO                                           
                    PS-ERROR               TO  TRUE                             
               MOVE PC-TSYMSG-01583  TO  DP020-MSG-NUMBER                       
           END-IF.                                                              
      *                                                                         
      *----------------------------------------------------------------*09050074
      *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *09060074
      *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *09070074
      *  IT HERE.                                                      *09080074
      *----------------------------------------------------------------*09090074
       4000-BUILD-INITIAL-PANEL.                                        09100074
                                                                        09110074
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        09260074
       EJECT                                                            09270074
      *                                                                 09980074
      *----------------------------------------------------------------*09990074
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *10000074
      *----------------------------------------------------------------*10010074
       4400-MOVE-COMMAREA-TO-SCREEN.                                    10020074
           MOVE ASC-STORE-NBR            TO ARCVSTRO.                   10030074
           MOVE ASC-TO-STORE-NBR         TO ATOSTRO.                    10030074
           MOVE ASC-XFER-FR-STR          TO PV-FROM-STORE               04020000
           MOVE ASC-XFER-SEQ-NBR         TO PV-SEQ-NBR                  04020100
           MOVE PV-XFER-NBR              TO AXFERNOO.                   04020400
       EJECT                                                            10110074
      *----------------------------------------------------------------*10930074
      *  GET THE MAX STATUS TIMESTAMP FOR THIS TRANSFER SO THE CURRENT  10940074
      *  STATUS ROW CAN BE READ.                                        10950074
      *----------------------------------------------------------------*10960074
       5000-RESET-TRANSFER.                                             10970074
           PERFORM 7450-UPDATE-MAX-RCPT-TMST.                           03470082
           PERFORM 7500-DELETE-XFER-ITM-RCPTS.                          10110200
           PERFORM 7550-DELETE-ALL-ADDED-ITMS.                          10111000
           PERFORM 7800-DELETE-DISCREPANCY.                             10120000
      *----------------------------------------------------------------*        
      *  GET THE MAX STATUS TIMESTAMP FOR THIS TRANSFER SO THE CURRENT          
      *  STATUS ROW CAN BE READ.                                                
      *----------------------------------------------------------------*        
       7050-SELECT-MAX-STAT-TMST.                                               
           INITIALIZE DCLTTFSTAT.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT                                           
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
                   OR (SQLCODE = +0))                                           
                                                                                
               EXEC SQL                                                         
                   SELECT  MAX(XFER_STAT_TMST)                                  
                     INTO  :TFSTAT-XFER-STAT-TMST                               
                               :PV-NULL-IND                                     
                     FROM  TTFSTAT                                              
                    WHERE  (XFER_DKEY_ID  =  :DK-XFER-DKEY-ID)                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '7050-SELECT-MAX-STAT-TMST'                         
                                             TO DP013-PARAGRAPH                 
                       MOVE 'SELECT MAX TIMESTAMP FOR DATALESS KEY: '           
                                             TO DP013-MESSAGE-TEXT (1)          
                       MOVE DK-XFER-DKEY-ID                                     
                                             TO PV-DISPLAY-XFER-DKEY-ID         
                       MOVE PV-DISPLAY-XFER-DKEY-ID                             
                                             TO DP013-MESSAGE-TEXT (2)          
                       MOVE SQLCA            TO DP013-SQLCA                     
                       MOVE 'TTFSTAT'        TO DP013-DB2-TABLE-NAME (1)        
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                             TO TRUE                            
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '7050-SELECT-MAX-STAT-TMST'                                 
                                             TO DP013-PARAGRAPH                 
               MOVE 'MAX RETRIES EXCEEDED FOR'                                  
                                        TO DP013-MESSAGE-TEXT (1)               
               MOVE 'SELECT MAX TIMESTAMP FOR DATALESS KEY: '                   
                                             TO DP013-MESSAGE-TEXT (2)          
               MOVE DK-XFER-DKEY-ID                                             
                                             TO PV-DISPLAY-XFER-DKEY-ID         
               MOVE PV-DISPLAY-XFER-DKEY-ID                                     
                                             TO DP013-MESSAGE-TEXT (3)          
               MOVE SQLCA               TO DP013-SQLCA                          
               MOVE 'TTFSTAT '          TO DP013-DB2-TABLE-NAME (1)             
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                        TO TRUE                                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *----------------------------------------------------------------*        
      *  GET THE CURRENT STATUS OF THIS TRANSFER.                               
      *----------------------------------------------------------------*        
       7100-SELECT-CURRENT-STATUS.                                              
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT                                           
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
                   OR (SQLCODE = +0)                                            
                   OR (SQLCODE = +100))                                         
                                                                                
               EXEC SQL                                                         
                   SELECT  A.XFER_STAT_CDE,                                     
                           B.XFER_STAT_DESC                                     
                     INTO  :TFSTAT-XFER-STAT-CDE,                               
                           :TFSTCD-XFER-STAT-DESC                               
                     FROM  TTFSTAT  A,                                          
                           TTFSTCD  B                                           
                    WHERE  (XFER_DKEY_ID     =  :DK-XFER-DKEY-ID                
                      AND   XFER_STAT_TMST   =  :TFSTAT-XFER-STAT-TMST          
                      AND   A.XFER_STAT_CDE  =  B.XFER_STAT_CDE)                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE 'INVALID'          TO TFSTCD-XFER-STAT-DESC         
                   WHEN OTHER                                                   
                       MOVE '7100-SELECT-CURRENT-STATUS'                        
                                             TO DP013-PARAGRAPH                 
                       MOVE 'SELECT CURRENT STATUS FOR DATALESS KEY/TIME        
      -                 'STAMP: '            TO DP013-MESSAGE-TEXT (1)          
                       MOVE DK-XFER-DKEY-ID                                     
                                             TO PV-DISPLAY-XFER-DKEY-ID         
                       MOVE PV-DISPLAY-XFER-DKEY-ID                             
                                             TO DP013-MESSAGE-TEXT (2)          
                       MOVE TFSTAT-XFER-STAT-TMST                               
                                             TO DP013-MESSAGE-TEXT (3)          
                       MOVE SQLCA            TO DP013-SQLCA                     
                       MOVE 'TTFSTAT'        TO DP013-DB2-TABLE-NAME (1)        
                       MOVE 'TTFSTCD'        TO DP013-DB2-TABLE-NAME (2)        
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                             TO TRUE                            
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '7100-SELECT-CURRENT-STATUS'                                
                                             TO DP013-PARAGRAPH                 
               MOVE 'SELECT CURRENT STATUS FOR DATALESS KEY/TIMESTAMP'          
                                             TO DP013-MESSAGE-TEXT (1)          
               MOVE DK-XFER-DKEY-ID                                             
                                             TO PV-DISPLAY-XFER-DKEY-ID         
               MOVE PV-DISPLAY-XFER-DKEY-ID                                     
                                             TO DP013-MESSAGE-TEXT (2)          
               MOVE TFSTAT-XFER-STAT-TMST                                       
                                             TO DP013-MESSAGE-TEXT (3)          
               MOVE SQLCA               TO DP013-SQLCA                          
               MOVE 'TTFSTAT '          TO DP013-DB2-TABLE-NAME (1)             
               MOVE 'TTFSTCD '          TO DP013-DB2-TABLE-NAME (2)             
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                        TO TRUE                                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *----------------------------------------------------------------*12340074
      *  GET THE MAX RECEIPT TIMESTAMP.                                 12350074
      *----------------------------------------------------------------*12360074
       7150-SELECT-MAX-RCPT-TMST.                                       12370074
           PERFORM                                                      12380074
               WITH TEST AFTER                                          12390074
               VARYING PV-RETRY-COUNT                                   12400074
               FROM 1 BY 1                                              12410074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 12420074
                   OR (SQLCODE = +0)                                    12430074
                   OR (SQLCODE = +100))                                 12440074
                                                                        12450074
               EXEC SQL                                                 12460074
                   SELECT  MAX(RCV_XFER_BEG_TMST)                       12470074
                     INTO  :TFRCPT-RCV-XFER-BEG-TMST                    12480074
                               :PV-NULL-IND                             12490074
                     FROM  TTFRCPT                                      12500074
                    WHERE  (XFER_DKEY_ID  =  :DK-XFER-DKEY-ID)          12510074
               END-EXEC                                                 12520074
                                                                        12530074
               EVALUATE TRUE                                            12540074
                   WHEN SQLCODE = ZERO                                  12550074
                   WHEN SQLCODE = +100                                  12560074
                   WHEN SQLCODE = -904                                  12570074
                   WHEN SQLCODE = -913                                  12580074
                       CONTINUE                                         12590074
                   WHEN OTHER                                           12600074
                       MOVE '7150-SELECT-MAX-RCPT-TMST'                 12630074
                                             TO DP013-PARAGRAPH         12640074
                       MOVE 'SELECT MAX TIMESTAMP FOR DATALESS KEY: '   12650074
                                             TO DP013-MESSAGE-TEXT (1)  12660074
                       MOVE DK-XFER-DKEY-ID                             12670074
                                             TO PV-DISPLAY-XFER-DKEY-ID 12680074
                       MOVE PV-DISPLAY-XFER-DKEY-ID                     12690074
                                             TO DP013-MESSAGE-TEXT (2)  12700074
                       MOVE SQLCA            TO DP013-SQLCA             12710074
                       MOVE 'TTFRCPT'        TO DP013-DB2-TABLE-NAME (1)12720074
                       SET DP013-DB2-ABEND                              12730074
                           DP013-XCTL-DISPLAY-RESTART                   12740074
                                             TO TRUE                    12750074
                       PERFORM DP013-0000-PROCESS-ABEND                 12760074
               END-EVALUATE                                             12770074
           END-PERFORM.                                                 12780074
                                                                        12790074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          12800074
                   MOVE '7150-SELECT-MAX-RCPT-TMST'                     12810074
                                             TO DP013-PARAGRAPH         12820074
                   MOVE 'SELECT MAX TIMESTAMP FOR DATALESS KEY: '       12830074
                                             TO DP013-MESSAGE-TEXT (1)  12840074
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 12850074
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         12860074
                                             TO DP013-MESSAGE-TEXT (2)  12870074
                   MOVE SQLCA                TO DP013-SQLCA             12880074
                   MOVE 'TTFRCPT'            TO DP013-DB2-TABLE-NAME (1)12890074
                   SET DP013-DB2-ABEND                                  12900074
                       DP013-XCTL-DISPLAY-RESTART                       12910074
                                            TO TRUE                     12920074
                   PERFORM DP013-0000-PROCESS-ABEND                     12930074
           END-IF.                                                      12940074
      *                                                                 12950074
      *----------------------------------------------------------------*12960074
      *  GET THE CURRENT RECEIPT END TIMESTAMP.                         12970074
      *----------------------------------------------------------------*12980074
       7200-SELECT-CURRENT-RCPT.                                        12990074
           PERFORM                                                      13000074
               WITH TEST AFTER                                          13010074
               VARYING PV-RETRY-COUNT                                   13020074
               FROM 1 BY 1                                              13030074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 13040074
                   OR (SQLCODE = +0)                                    13050074
                   OR (SQLCODE = +100))                                 13060074
                                                                        13070074
               EXEC SQL                                                 13080074
                   SELECT  RCV_XFER_END_TMST                            13090074
                     INTO  :TFRCPT-RCV-XFER-END-TMST                    13100074
                     FROM  TTFRCPT                                      13110074
                    WHERE  (XFER_DKEY_ID  =  :DK-XFER-DKEY-ID  AND      13120074
                       RCV_XFER_BEG_TMST  =  :TFRCPT-RCV-XFER-BEG-TMST) 13130074
               END-EXEC                                                 13140074
                                                                        13150074
               EVALUATE TRUE                                            13160074
                   WHEN SQLCODE = ZERO                                  13170074
                   WHEN SQLCODE = +100                                  13180074
                   WHEN SQLCODE = -904                                  13190074
                   WHEN SQLCODE = -913                                  13200074
                       CONTINUE                                         13210074
                   WHEN OTHER                                           13220074
                       MOVE '7200-SELECT-CURRENT-RCPT'                  13250074
                                             TO DP013-PARAGRAPH         13260074
                       MOVE 'SELECT CURRENT RECEIPT FOR DATALESS KEY/TIM13270074
      -                 'MP: '           TO DP013-MESSAGE-TEXT (1)      13280074
                       MOVE DK-XFER-DKEY-ID                             13290074
                                             TO PV-DISPLAY-XFER-DKEY-ID 13300074
                       MOVE PV-DISPLAY-XFER-DKEY-ID                     13310074
                                             TO DP013-MESSAGE-TEXT (2)  13320074
                       MOVE TFRCPT-RCV-XFER-BEG-TMST                    13330074
                                             TO DP013-MESSAGE-TEXT (3)  13340074
                       MOVE SQLCA            TO DP013-SQLCA             13350074
                       MOVE 'TTFRCPT'        TO DP013-DB2-TABLE-NAME (1)13360074
                       SET DP013-DB2-ABEND                              13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                       PERFORM DP013-0000-PROCESS-ABEND                 13400074
               END-EVALUATE                                             13410074
           END-PERFORM.                                                 13420074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13430074
                   MOVE '7200-SELECT-CURRENT-RCPT'                      13440074
                                             TO DP013-PARAGRAPH         13450074
                   MOVE 'SELECT CURRENT RECEIPT FOR DATALESS KEY/TIMESTA13460074
      -                 'MP: '               TO DP013-MESSAGE-TEXT (1)  13470074
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 13480074
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         13490074
                                             TO DP013-MESSAGE-TEXT (2)  13500074
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                        13510074
                                             TO DP013-MESSAGE-TEXT (3)  13520074
                   MOVE SQLCA                TO DP013-SQLCA             13530074
                   MOVE 'TTFRCPT'            TO DP013-DB2-TABLE-NAME (1)13540074
                   SET DP013-DB2-ABEND                                  13550074
                       DP013-XCTL-DISPLAY-RESTART                       13560074
                                            TO TRUE                     13570074
                   PERFORM DP013-0000-PROCESS-ABEND                     13580074
           END-IF.                                                      13590074
       7450-UPDATE-MAX-RCPT-TMST.                                       10260082
           PERFORM                                                      13000074
               WITH TEST AFTER                                          13010074
               VARYING PV-RETRY-COUNT                                   13020074
               FROM 1 BY 1                                              13030074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 13040074
                   OR (SQLCODE = +0))                                   13050074
                                                                        13070074
               EXEC SQL                                                 13080074
                  UPDATE  TTFRCPT                                       10280050
                     SET  RCV_XFER_END_TMST   =  CURRENT TIMESTAMP      10290050
                   WHERE  (XFER_DKEY_ID       =  :DK-XFER-DKEY-ID       10300050
                    AND   RCV_XFER_BEG_TMST = :TFRCPT-RCV-XFER-BEG-TMST)10310050
               END-EXEC                                                 10320050
                                                                        12530074
               EVALUATE TRUE                                            12540074
                 WHEN SQLCODE = ZERO                                    12550074
                 WHEN SQLCODE = -904                                    12570074
                 WHEN SQLCODE = -913                                    12580074
                       CONTINUE                                         12590074
                 WHEN OTHER                                             12600074
                   MOVE '7450-UPDATE-MAX-RCPT-TMST'                     10440082
                                             TO DP013-PARAGRAPH         10450050
                   MOVE 'UPDATE RECEIPT END TIMESTAMP FOR XFER_DKEY_ID/R10460050
      -                 'CV_XFER_BEG_TMST: ' TO DP013-MESSAGE-TEXT (1)  10470050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 10480050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         10490050
                                             TO DP013-MESSAGE-TEXT (2)  10500050
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                        10510050
                                             TO DP013-MESSAGE-TEXT (3)  10520050
                   MOVE SQLCA                TO DP013-SQLCA             10530050
                   MOVE 'TTFRCPT'            TO DP013-DB2-TABLE-NAME (1)10540050
                   SET DP013-DB2-ABEND                                  13370074
                   DP013-XCTL-DISPLAY-RESTART                           13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
               END-EVALUATE                                             13410074
           END-PERFORM.                                                 13420074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13430074
                   MOVE '7450-UPDATE-MAX-RCPT-TMST'                     10440082
                                             TO DP013-PARAGRAPH         10450050
                   MOVE 'UPDATE RECEIPT END TIMESTAMP FOR XFER_DKEY_ID/R10460050
      -                 'CV_XFER_BEG_TMST: ' TO DP013-MESSAGE-TEXT (1)  10470050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 10480050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         10490050
                                             TO DP013-MESSAGE-TEXT (2)  10500050
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                        10510050
                                             TO DP013-MESSAGE-TEXT (3)  10520050
                   MOVE SQLCA                TO DP013-SQLCA             13530074
                   MOVE 'TTFRCPT'            TO DP013-DB2-TABLE-NAME (1)13540074
                   SET DP013-DB2-ABEND                                  13550074
                       DP013-XCTL-DISPLAY-RESTART                       13560074
                                            TO TRUE                     13570074
                   PERFORM DP013-0000-PROCESS-ABEND                     13580074
           END-IF.                                                      13590074
      *                                                                 10650050
       7500-DELETE-XFER-ITM-RCPTS.                                      10660082
           PERFORM                                                      13000074
               WITH TEST AFTER                                          13010074
               VARYING PV-RETRY-COUNT                                   13020074
               FROM 1 BY 1                                              13030074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 13040074
                   OR (SQLCODE = +0)                                    13050074
                   OR (SQLCODE = +100))                                 13060074
                                                                        13070074
               EXEC SQL                                                 13080074
                  DELETE FROM TTFITRC                                   10680050
                   WHERE  (XFER_DKEY_ID    =  :DK-XFER-DKEY-ID          10690050
                    AND   XFER_RCPT_TMST  =  :TFRCPT-RCV-XFER-BEG-TMST) 10700050
               END-EXEC                                                 10320050
                                                                        12530074
               EVALUATE TRUE                                            12540074
                 WHEN SQLCODE = ZERO                                    12550074
                 WHEN SQLCODE = +100                                    12560074
                 WHEN SQLCODE = -904                                    12570074
                 WHEN SQLCODE = -913                                    12580074
                       CONTINUE                                         12590074
                 WHEN OTHER                                             12600074
                   MOVE '7500-DELETE-XFER-ITM-RCPTS'                    10790082
                                             TO DP013-PARAGRAPH         10800050
                   MOVE 'DELETE ALL RECEIPT ITEMS FOR XFER_DKEY_ID/XFER_10810050
      -                 'RCPT_TMST: '        TO DP013-MESSAGE-TEXT (1)  10820050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 10830050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         10840050
                                             TO DP013-MESSAGE-TEXT (2)  10850050
                   MOVE TFRCPT-RCV-XFER-END-TMST                        10860050
                                             TO DP013-MESSAGE-TEXT (3)  10870050
                   MOVE SQLCA                TO DP013-SQLCA             10880050
                   MOVE 'TTFITRC'            TO DP013-DB2-TABLE-NAME (1)10890050
                   MOVE SQLCA                TO DP013-SQLCA             10530050
                   SET DP013-DB2-ABEND                                  13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
               END-EVALUATE                                             13410074
           END-PERFORM.                                                 13420074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13430074
                   MOVE '7500-DELETE-XFER-ITM-RCPTS'                    10790082
                                             TO DP013-PARAGRAPH         10800050
                   MOVE 'DELETE ALL RECEIPT ITEMS FOR XFER_DKEY_ID/XFER_10810050
      -                 'RCPT_TMST: '        TO DP013-MESSAGE-TEXT (1)  10820050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 10830050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         10840050
                                             TO DP013-MESSAGE-TEXT (2)  10850050
                   MOVE TFRCPT-RCV-XFER-END-TMST                        10860050
                                             TO DP013-MESSAGE-TEXT (3)  10870050
                   MOVE SQLCA                TO DP013-SQLCA             10880050
                   MOVE 'TTFITRC'            TO DP013-DB2-TABLE-NAME (1)10890050
                   MOVE SQLCA                TO DP013-SQLCA             10530050
                   SET DP013-DB2-ABEND                                  13550074
                       DP013-XCTL-DISPLAY-RESTART                       13560074
                                            TO TRUE                     13570074
                   PERFORM DP013-0000-PROCESS-ABEND                     13580074
           END-IF.                                                      13590074
      *                                                                 11040050
       7550-DELETE-ALL-ADDED-ITMS.                                      11050082
           INITIALIZE TFITEM-XFER-QTY.                                  11060050
                                                                        11070050
           PERFORM                                                      13000074
               WITH TEST AFTER                                          13010074
               VARYING PV-RETRY-COUNT                                   13020074
               FROM 1 BY 1                                              13030074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 13040074
                   OR (SQLCODE = +0)                                    13050074
                   OR (SQLCODE = +100))                                 13060074
                                                                        13070074
               EXEC SQL                                                 13080074
                   DELETE FROM TTFITEM                                  11090050
                    WHERE  (XFER_DKEY_ID  =  :DK-XFER-DKEY-ID           11100050
                      AND   XFER_QTY      =  :TFITEM-XFER-QTY)          11110050
               END-EXEC                                                 10320050
                                                                        12530074
               EVALUATE TRUE                                            12540074
                 WHEN SQLCODE = ZERO                                    12550074
                 WHEN SQLCODE = +100                                    12560074
                 WHEN SQLCODE = -904                                    12570074
                 WHEN SQLCODE = -913                                    12580074
                       CONTINUE                                         12590074
                 WHEN OTHER                                             12600074
                   MOVE '7550-DELETE-ALL-ADDED-ITMS'                    11200082
                                             TO DP013-PARAGRAPH         11210050
                   MOVE 'DELETE ALL XFER ITEMS ADDED DURING THIS RECEIPT11220050
      -                 'FOR XFER_DKEY_ID: ' TO DP013-MESSAGE-TEXT (1)  11230050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 11240050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         11250050
                                             TO DP013-MESSAGE-TEXT (2)  11260050
                   MOVE SQLCA                TO DP013-SQLCA             11270050
                   MOVE 'TTFITEM'            TO DP013-DB2-TABLE-NAME (1)11280050
                   SET DP013-DB2-ABEND                                  13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
               END-EVALUATE                                             13410074
           END-PERFORM.                                                 13420074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13430074
                   MOVE '7550-DELETE-ALL-ADDED-ITMS'                    11200082
                                             TO DP013-PARAGRAPH         11210050
                   MOVE 'DELETE ALL XFER ITEMS ADDED DURING THIS RECEIPT11220050
      -                 'FOR XFER_DKEY_ID: ' TO DP013-MESSAGE-TEXT (1)  11230050
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 11240050
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         11250050
                                             TO DP013-MESSAGE-TEXT (2)  11260050
                   MOVE SQLCA                TO DP013-SQLCA             11270050
                   MOVE 'TTFITEM'            TO DP013-DB2-TABLE-NAME (1)11280050
                   SET DP013-DB2-ABEND                                  13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
           END-IF.                                                      13590074
      *                                                                 13090074
       7800-DELETE-DISCREPANCY.                                         13100082
           PERFORM                                                      13000074
               WITH TEST AFTER                                          13010074
               VARYING PV-RETRY-COUNT                                   13020074
               FROM 1 BY 1                                              13030074
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 13040074
                   OR (SQLCODE = +0)                                    13050074
                   OR (SQLCODE = +100))                                 13060074
                                                                        13070074
               EXEC SQL                                                 13080074
                 DELETE FROM TTFDSCP                                    13140074
                  WHERE  (XFER_DKEY_ID     =  :DK-XFER-DKEY-ID          13150076
                   AND   RCV_XFER_BEG_TMST =  :TFRCPT-RCV-XFER-BEG-TMST)13160076
               END-EXEC                                                 10320050
                                                                        12530074
               EVALUATE TRUE                                            12540074
                 WHEN SQLCODE = ZERO                                    12550074
                 WHEN SQLCODE = +100                                    12560074
                 WHEN SQLCODE = -904                                    12570074
                 WHEN SQLCODE = -913                                    12580074
                       CONTINUE                                         12590074
                 WHEN OTHER                                             12600074
                   MOVE '7800-DELETE-DISCREPANCY'                       13260082
                                             TO DP013-PARAGRAPH         13270074
                   MOVE 'DELETE DISCREPANCY FOR XFER_DKEY_ID/RCV_XFER_RC13280075
      -                 'PT_TMST: '          TO DP013-MESSAGE-TEXT (1)  13290075
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 13300074
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         13310074
                                             TO DP013-MESSAGE-TEXT (2)  13320074
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                        13330075
                                             TO DP013-MESSAGE-TEXT (3)  13340074
                   MOVE SQLCA                TO DP013-SQLCA             13350074
                   MOVE 'TTFDSCP'            TO DP013-DB2-TABLE-NAME (1)13360075
                   SET DP013-DB2-ABEND                                  13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
               END-EVALUATE                                             13410074
           END-PERFORM.                                                 13420074
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13430074
                   MOVE '7800-DELETE-DISCREPANCY'                       13260082
                                             TO DP013-PARAGRAPH         13270074
                   MOVE 'DELETE DISCREPANCY FOR XFER_DKEY_ID/RCV_XFER_RC13280075
      -                 'PT_TMST: '          TO DP013-MESSAGE-TEXT (1)  13290075
                   MOVE DK-XFER-DKEY-ID      TO PV-DISPLAY-XFER-DKEY-ID 13300074
                   MOVE PV-DISPLAY-XFER-DKEY-ID                         13310074
                                             TO DP013-MESSAGE-TEXT (2)  13320074
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                        13330075
                                             TO DP013-MESSAGE-TEXT (3)  13340074
                   MOVE SQLCA                TO DP013-SQLCA             13350074
                   MOVE 'TTFDSCP'            TO DP013-DB2-TABLE-NAME (1)13360075
                   SET DP013-DB2-ABEND                                  13370074
                       DP013-XCTL-DISPLAY-RESTART                       13380074
                                            TO TRUE                     13390074
                   PERFORM DP013-0000-PROCESS-ABEND                     13400074
           END-IF.                                                      13420074
      ******************************************************************        
      *    SELECT TTFMSTR TO GET THE 'TO' STORE NUMBER.                         
      ******************************************************************        
       8200-GET-TO-STORE.                                               17690009
                                                                        17700009
           MOVE ASC-XFER-DKEY-ID         TO  TFMSTR-XFER-DKEY-ID.               
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT                                           
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
                   OR (SQLCODE = +0))                                           
                                                                                
               EXEC SQL                                                         
                   SELECT                                                       
                          TO_STR_NBR                                            
                    INTO :TFMSTR-TO-STR-NBR                                     
                    FROM TTFMSTR                                                
                   WHERE XFER_DKEY_ID = :TFMSTR-XFER-DKEY-ID                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        MOVE TFMSTR-TO-STR-NBR   TO ASC-TO-STORE-NBR            
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN OTHER                                                   
                        MOVE '8200-GET-TO-STORE'                                
                                        TO DP013-PARAGRAPH                      
                        MOVE 'UNSUCCESSFUL SELECT'                              
                                        TO DP013-MESSAGE-TEXT (1)               
                        MOVE SQLCA      TO DP013-SQLCA                          
                        MOVE 'TTFMSTR'  TO DP013-DB2-TABLE-NAME (1)             
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                        TO TRUE                                 
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8200-GET-TO-STORE'                                         
                                        TO DP013-PARAGRAPH                      
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                        TO DP013-MESSAGE-TEXT (1)               
               MOVE SQLCA               TO DP013-SQLCA                          
               MOVE 'TTFMSTR   '        TO DP013-DB2-TABLE-NAME (1)             
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                        TO TRUE                                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           EJECT                                                        17680010
      *                                                                 11430028
                                                                        15010074
           COPY DPPD013.                                                15020074
