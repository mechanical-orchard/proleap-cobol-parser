00001  ID DIVISION.                                                     04/13/95
00002                                                                   SNKUP018
00003  PROGRAM-ID.    SNKUP018.                                            LV001
00004                                                                   SNKUP018
00005  AUTHOR.        ANNE YEKO.                                        SNKUP018
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP018
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP018
00008  DATE-COMPILED.                                                   SNKUP018
00009                                                                   SNKUP018
00010 ******************************************************************SNKUP018
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP018
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP018
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASOADR TABLE.          **SNKUP018
00014 ******************************************************************SNKUP018
00015 * CHANGED __/__/__ BY ____________                              **SNKUP018
00016 *   P_________  ________________________________________________**SNKUP018
00017 *               ________________________________________________**SNKUP018
00018 *                                                               **SNKUP018
00019 ******************************************************************SNKUP018
00020                                                                   SNKUP018
00021      EJECT                                                        SNKUP018
00022                                                                   SNKUP018
00023  ENVIRONMENT DIVISION.                                            SNKUP018
00024                                                                   SNKUP018
00025  INPUT-OUTPUT SECTION.                                            SNKUP018
00026                                                                   SNKUP018
00027  FILE-CONTROL.                                                    SNKUP018
00028                                                                   SNKUP018
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP018
00030             ASSIGN TO INP01.                                      SNKUP018
00031                                                                   SNKUP018
00032  DATA DIVISION.                                                   SNKUP018
00033                                                                   SNKUP018
00034  FILE SECTION.                                                    SNKUP018
00035                                                                   SNKUP018
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP018
00037      RECORDING MODE IS F                                          SNKUP018
00038      LABEL RECORDS ARE STANDARD                                   SNKUP018
00039      RECORD CONTAINS 05 CHARACTERS                                SNKUP018
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP018
00041                                                                   SNKUP018
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                       SNKUP018
00043                                                                   SNKUP018
00044      EJECT                                                        SNKUP018
00045  WORKING-STORAGE SECTION.                                         SNKUP018
00046                                                                   SNKUP018
00047  01  FILLER.                                                      SNKUP018
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP018
00049          ' ***** WORKING STORAGE PGM=SNKUP018 '.                  SNKUP018
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP018
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP018
00052          ' LOCATION='.                                            SNKUP018
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP018
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP018
00055          ' *****'.                                                SNKUP018
00056                                                                   SNKUP018
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP018
00058                                              COMP SYNC.           SNKUP018
00059                                                                   SNKUP018
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP018
00061                                                                   SNKUP018
00062      EJECT                                                        SNKUP018
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP018
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP018
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP018
00066                                                                   SNKUP018
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP018
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP018
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP018
00070                                                                   SNKUP018
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP018
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP018
00073                                              COMP-3.              SNKUP018
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP018
00075                                              COMP-3.              SNKUP018
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP018
00077                                              COMP-3.              SNKUP018
00078                                                                   SNKUP018
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP018
00080                                                                   SNKUP018
00081      COPY SNRD002I.                                               SNKUP018
00082      EJECT                                                        SNKUP018
00083 ******************************************************************SNKUP018
00084 *    DB2 DATABASE AREA                                            SNKUP018
00085 ******************************************************************SNKUP018
00086                                                                   SNKUP018
00087 *    ASN DETAIL ORDER NAME & ADDRESS DETAIL                       SNKUP018
00088                                                                   SNKUP018
00089      EXEC SQL                                                     SNKUP018
00090          INCLUDE TASOADR                                          SNKUP018
00091      END-EXEC.                                                    SNKUP018
00092                                                                   SNKUP018
00093      EJECT                                                        SNKUP018
00094 *    SQL COMMUNICATION AREA                                       SNKUP018
00095                                                                   SNKUP018
00096      EXEC SQL                                                     SNKUP018
00097          INCLUDE SQLCA                                            SNKUP018
00098      END-EXEC.                                                    SNKUP018
00099      EJECT                                                        SNKUP018
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP018
00101                                                                   SNKUP018
00102      COPY SQLCA2.                                                 SNKUP018
00103      EJECT                                                        SNKUP018
00104                                                                   SNKUP018
00105 ***************************************************************** SNKUP018
00106  PROCEDURE DIVISION.                                              SNKUP018
00107 ***************************************************************** SNKUP018
00108                                                                   SNKUP018
00109  A000-MAIN-DRIVER.                                                SNKUP018
00110                                                                   SNKUP018
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP018
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP018
00113                                                                   SNKUP018
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP018
00115                                                                   SNKUP018
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP018
00117                                                                   SNKUP018
00118      PERFORM B100-DELETE-ROWS                                     SNKUP018
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP018
00120                                                                   SNKUP018
00121      DISPLAY 'SNKUP018 RECORDS READ:    ' PV-RECS-READ.           SNKUP018
00122      DISPLAY 'SNKUP018 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP018
00123                                                                   SNKUP018
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP018
00125                                                                   SNKUP018
00126      GOBACK.                                                      SNKUP018
00127                                                                   SNKUP018
00128      EJECT                                                        SNKUP018
00129                                                                   SNKUP018
00130 ***************************************************************** SNKUP018
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP018
00132 ***************************************************************** SNKUP018
00133  B100-DELETE-ROWS.                                                SNKUP018
00134                                                                   SNKUP018
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP018
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP018
00137                                  TO ABEND-PARAGRAPH.              SNKUP018
00138                                                                   SNKUP018
00139      PERFORM B300-DELETE-ASOADR.                                  SNKUP018
00140      IF PV-DELETE-COUNTER > +250                                  SNKUP018
00141          PERFORM B400-COMMIT                                      SNKUP018
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP018
00143      END-IF.                                                      SNKUP018
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP018
00145                                                                   SNKUP018
00146      EJECT                                                        SNKUP018
00147                                                                   SNKUP018
00148 ***************************************************************** SNKUP018
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP018
00150 ***************************************************************** SNKUP018
00151  B200-READ-INPUT-FILE.                                            SNKUP018
00152                                                                   SNKUP018
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP018
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP018
00155                                  TO ABEND-PARAGRAPH.              SNKUP018
00156                                                                   SNKUP018
00157      READ ASNHDR-DELETE-FILE                                      SNKUP018
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP018
00159          AT END                                                   SNKUP018
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP018
00161                                                                   SNKUP018
00162      IF NOT PS-END-OF-INPUT                                       SNKUP018
00163          ADD 1                   TO PV-RECS-READ                  SNKUP018
00164      END-IF.                                                      SNKUP018
00165                                                                   SNKUP018
00166      EJECT                                                        SNKUP018
00167                                                                   SNKUP018
00168 ***************************************************************** SNKUP018
00169 *    THIS PARAGRAPH DELETES ALL ASOADR ROWS THAT ARE EQUAL TO     SNKUP018
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP018
00171 ***************************************************************** SNKUP018
00172  B300-DELETE-ASOADR.                                              SNKUP018
00173                                                                   SNKUP018
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP018
00175      MOVE 'B300-DELETE-ASOADR        '                            SNKUP018
00176                                  TO ABEND-PARAGRAPH.              SNKUP018
00177                                                                   SNKUP018
00178      EXEC SQL                                                     SNKUP018
00179          DELETE                                                   SNKUP018
00180          FROM   TASOADR                                           SNKUP018
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                   SNKUP018
00182      END-EXEC.                                                    SNKUP018
00183                                                                   SNKUP018
00184      IF SQLCODE = ZERO                                            SNKUP018
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP018
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP018
00187      ELSE                                                         SNKUP018
00188          IF SQLCODE = +100                                        SNKUP018
00189              CONTINUE                                             SNKUP018
00190          ELSE                                                     SNKUP018
00191              PERFORM Z998-SQL-ERROR                               SNKUP018
00192          END-IF                                                   SNKUP018
00193      END-IF.                                                      SNKUP018
00194                                                                   SNKUP018
00195      EJECT                                                        SNKUP018
00196                                                                   SNKUP018
00197 ***************************************************************** SNKUP018
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP018
00199 ***************************************************************** SNKUP018
00200  B400-COMMIT.                                                     SNKUP018
00201                                                                   SNKUP018
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP018
00203      MOVE 'B400-COMMIT               '                            SNKUP018
00204                                  TO ABEND-PARAGRAPH.              SNKUP018
00205                                                                   SNKUP018
00206      EXEC SQL                                                     SNKUP018
00207          COMMIT                                                   SNKUP018
00208      END-EXEC.                                                    SNKUP018
00209                                                                   SNKUP018
00210      IF SQLCODE = ZERO                                            SNKUP018
00211          CONTINUE                                                 SNKUP018
00212      ELSE                                                         SNKUP018
00213          PERFORM Z998-SQL-ERROR                                   SNKUP018
00214      END-IF.                                                      SNKUP018
00215                                                                   SNKUP018
00216      EJECT                                                        SNKUP018
00217                                                                   SNKUP018
00218 ***************************************************************** SNKUP018
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP018
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP018
00221 ***************************************************************** SNKUP018
00222  Z998-SQL-ERROR.                                                  SNKUP018
00223                                                                   SNKUP018
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP018
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP018
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP018
00227      DISPLAY '** PROGRAM          ** = SNKUP018'.                 SNKUP018
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP018
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP018
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP018
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP018
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP018
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP018
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP018
