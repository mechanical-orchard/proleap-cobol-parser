000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MLKUT471.                                                 
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  09-01-2000.                                               
000600 DATE-COMPILED.                                                           
                                                                                
000700******************************************************************        
000800*PURPOSE:                                                                 
001100* THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR            
001200* DB2 TABLE TWKDPTC.                                                      
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PROGRAMMER   DESCRIPTION                             
      * ------- ---------- ------------ --------------------------              
23565 * 23565   09-10-2003 ROD JANSSEN  DETAILED PURCH ADJUSTMENTS ENH.         
25554 * 25554   12-24-2003 PAUL KEBER   MARKDOWN REFINEMENT FOR PLANNING        
W26603* W26603  03-23-2004 STORE EXPANSION.  EXPANDED LRECL OF OUT01            
W26603*                    DUE TO INCREASE OF MLRD153.                          
W26603*                    ADDED 'WITH UR' TO DB2 SELECTS.                      
W26603*                    - RONNA MCDONALD                                     
W27511* W27511  12-28-2004 BROADCAST OPTIMIZATION SPLIT PLANNING                
W27511*                    - J LINDEMANN                                        
W28630* W28630  04-15-2005 LICENSE FEE SPLIT PLANNING                           
W28630*                    - S ASEN                                             
P18333*P18333   09-28-2012 ML PERFORMANCE TUNING FOR PEAK                       
P18333*                    REMOVED UNWANTED DISPLAYS.                           
P18333*                    -COGNIZANT                                           
001400******************************************************************        
001500                                                                          
001600 ENVIRONMENT DIVISION.                                                    
001800 CONFIGURATION SECTION.                                                   
002000 SOURCE-COMPUTER.        IBM-3090.                                        
002100 OBJECT-COMPUTER.        IBM-3090.                                        
002300 INPUT-OUTPUT SECTION.                                                    
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT DATA-INTEGRITY-CTL-FILE                                       
002700         ASSIGN TO INP01.                                                 
002800                                                                          
002900     SELECT DATA-INTEGRITY-EXTR-FILE                                      
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003300 DATA DIVISION.                                                           
003600 FILE SECTION.                                                            
003700                                                                          
003800 FD  DATA-INTEGRITY-CTL-FILE                                              
003900     LABEL RECORDS ARE STANDARD                                           
004000     RECORDING MODE IS F                                                  
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  DATA-INTEGRITY-CTL-REC      PIC X(80).                               
004300                                                                          
004400 FD  DATA-INTEGRITY-EXTR-FILE                                             
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800                                                                          
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                              
005000                                                                          
005200******************************************************************        
005300 WORKING-STORAGE SECTION.                                                 
005400******************************************************************        
005500                                                                          
005600 01  PS-PROGRAM-SWITCHES.                                                 
005700     05  PS-TWKDPTC-DONE         PIC X       VALUE 'N'.                   
005800         88  PS-TWKDPTC-IS-DONE              VALUE 'Y'.                   
005900         88  PS-TWKDPTC-IS-NOT-DONE          VALUE 'N'.                   
25554      05  PS-MKDN-TTLS-INBAL-SW   PIC X       VALUE 'N'.                   
25554          88  PS-MKDN-TTLS-INBALANCE          VALUE 'Y'.                   
006000                                                                          
006100 01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
006200     05  PA-TWKDPTC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.                  
006300     05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
23565      05  PA-WORK-AMOUNT          PIC S9(11)V99 COMP-3 VALUE ZEROS.        
23565 **** 05  PA-WORK-AMOUNT          PIC S9(9)V99 VALUE ZEROS.                
25554      05  PA-NEW-MKDN-RTL-AMT     PIC S9(11)V99 VALUE +0 COMP-3.           
006400                                                                          
006500 01  PV-PROGRAM-VARIABLES.                                                
006800     05  PV-PREV-FSCL-MN-END-DTE PIC X(10) VALUE SPACE.                   
006900     05  PV-PREV-DEPT-NBR        PIC X(03) VALUE SPACE.                   
006900     05  PV-PREV-CHNL-CDE        PIC X(01) VALUE SPACE.                   
           05  PV-STRT-DTE             PIC X(10).                               
           05  PV-END-DTE              PIC X(10).                               
008000                                                                          
008100 01  CD-COUNTER-DISPLAY.                                                  
008200     05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
008300                                                                          
008400 01 ABEND-AREAS.                                                          
008500     05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
008600                                             COMP SYNC.                   
008700         88  AC-BAD-DATA                     VALUE +4003.                 
008800         88  AC-DB2-ERROR                    VALUE +4013.                 
008900     05  AA-ABEND-LIT            PIC X(40)   VALUE                        
009000           '*****       ABEND'.                                           
009100     05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
009200             '*****   PROGRAM: MLKUT471'.                                 
009300     05  AA-PARAGRAPH-LIT.                                                
009400         10  FILLER              PIC X(17)   VALUE                        
009500             '***** PARAGRAPH: '.                                         
009600         10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
009700     05  AA-MESSAGE-LINE.                                                 
009800         10  FILLER              PIC X(06)   VALUE '*****'.               
009900         10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
010000     05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
010100             '*****    DB2 ERROR'.                                        
010200     05  AA-DB2-OPERATION-LIT.                                            
010300         10  FILLER              PIC X(17)   VALUE                        
010400             '***** OPERATION: '.                                         
010500         10  AA-DB2-OPERATION.                                            
010600             15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
010700             15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
011200                                                                          
011300 EJECT                                                                    
011400                                                                          
011500*** DATA INTEGRITY CONTROL CARD                                           
011600     COPY MLRD110.                                                        
011700                                                                          
011900*** DATA INTEGRITY TWKDPTC EXTRACT FILE                                   
012000     COPY MLRD153.                                                        
012100                                                                          
012200 EJECT                                                                    
012300                                                                          
012400******************************************************************        
012500*  DB2 ERROR MESSAGES FORMAT AREA.                                        
012600******************************************************************        
012700     COPY DPWS004.                                                        
012800                                                                          
012900******************************************************************        
013000*  DB2 ERRORS                                                             
013100******************************************************************        
013200     COPY SQLCA2.                                                         
013300                                                                          
013400******************************************************************        
013500*  DB2 COMMUNICATION AREA                                                 
013600******************************************************************        
013700     EXEC SQL                                                             
013800          INCLUDE SQLCA                                                   
013900     END-EXEC.                                                            
014000                                                                          
014100******************************************************************        
014200*  DB2 TWKDPTC TABLE LAYOUT                                               
014300******************************************************************        
014400     EXEC SQL                                                             
014500          INCLUDE TWKDPTC                                                 
014600     END-EXEC.                                                            
014700                                                                          
014800******************************************************************        
014900*  DATE ATTRIBUTE TABLE LAYOUT                                            
015000******************************************************************        
015100     EXEC SQL                                                             
015200          INCLUDE TDTATTR                                                 
015300     END-EXEC.                                                            
015400                                                                          
014800******************************************************************        
014900*  MERCHANDISE LEDGER CONTROL TABLE LAYOUT                                
015000******************************************************************        
015100     EXEC SQL                                                             
015200          INCLUDE TMLCNTL                                                 
015300     END-EXEC.                                                            
015400                                                                          
015500******************************************************************        
015600*  DB2 TWKDPTC CURSOR                                                     
015700******************************************************************        
015800     EXEC SQL                                                             
015900         DECLARE TWKDPTC_CURSOR CURSOR FOR                                
016000          SELECT B.FSCL_MN_NBR                                            
016100                ,B.FSCL_MN_END_DTE                                        
016200                ,A.FSCL_WK_NBR                                            
016300                ,A.FSCL_WK_END_DTE                                        
                      ,DEPT_NBR                                                 
                      ,CHNL_CDE                                                 
                      ,SLS_RTL_AMT                                              
                      ,MKDN_PLU_RTL_AMT                                         
                      ,MKDN_RGST_RTL_AMT                                        
                      ,MKDN_UMTCH_RTL_AMT                                       
                      ,MKDN_BYR_RTL_AMT                                         
                      ,MKDN_STR_RTL_AMT                                         
                      ,MKUP_RTL_AMT                                             
                      ,XFER_IN_RTL_AMT                                          
                      ,XFER_OUT_RTL_AMT                                         
                      ,RCPT_RTL_AMT                                             
                      ,RCPT_NET_COST_AMT                                        
                      ,PADJ_RTL_AMT                                             
                      ,PADJ_NET_COST_AMT                                        
                      ,PADJ_FRGT_COST_AMT                                       
                      ,INV_ADJ_RTL_AMT                                          
                      ,SHNK_RTL_AMT                                             
                      ,END_INV_RTL_AMT                                          
                      ,CUM_MKUP_PCT                                             
                      ,END_INV_COST_AMT                                         
                      ,SLS_COST_AMT                                             
                      ,GRO_MRGN_AMT                                             
23565                 ,PADJ_RTV_COST_AMT                                        
23565                 ,PADJ_RTV_RTL_AMT                                         
23565                 ,PADJ_DA_COST_AMT                                         
23565                 ,PADJ_DA_RTL_AMT                                          
23565                 ,PADJ_OTH_COST_AMT                                        
23565                 ,PADJ_OTH_RTL_AMT                                         
25554                 ,(MKDN_PROMO_RTL_AMT + MKDN_PRPT_RTL_AMT +                
25554                   MKDN_GLDST_RTL_AMT + MKDN_OTH_RTL_AMT +                 
25554                   MKDN_BYR_RTL_AMT + MKDN_EMP_RTL_AMT)                    
W27511                ,PADJ_BROP_COST_AMT                                       
W28630                ,LIC_FEE_COST_AMT                                         
017700            FROM TWKDPTC A                                                
017800                ,TDTATTR B                                                
017900           WHERE (A.FSCL_WK_END_DTE                                       
018000                      BETWEEN :PV-STRT-DTE AND :PV-END-DTE)               
018200             AND A.FSCL_WK_END_DTE = B.KY_DTE                             
****************   AND CHNL_CDE = 'A'                                           
018300        ORDER BY  CHNL_CDE                                                
018300                 ,DEPT_NBR                                                
018400                 ,FSCL_WK_END_DTE DESC                                    
W26603        WITH UR                                                           
018500     END-EXEC.                                                            
018600                                                                          
018700 EJECT                                                                    
018800                                                                          
018900******************************************************************        
019000 PROCEDURE DIVISION.                                                      
019100******************************************************************        
019200                                                                          
019300******************************************************************        
019400 0000-MAINLINE-MLKUT471.                                                  
019500******************************************************************        
019600*                                                                         
019700     PERFORM 1000-INITIALIZATION.                                         
019800                                                                          
019900     PERFORM 2000-MAIN-PROCESS.                                           
020000                                                                          
020100     PERFORM 8000-EOJ.                                                    
020200                                                                          
25554      IF PS-MKDN-TTLS-INBALANCE                                            
  |            MOVE 4095 TO RETURN-CODE                                         
  |        ELSE                                                                 
  |            MOVE 0 TO RETURN-CODE                                            
25554      END-IF.                                                              
020200                                                                          
020300     STOP RUN.                                                            
020400                                                                          
020500 EJECT                                                                    
020600                                                                          
020700******************************************************************        
020800 1000-INITIALIZATION.                                                     
020900******************************************************************        
021000                                                                          
021100     OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                                 
021200     OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                                
021300                                                                          
021400     PERFORM 4000-READ-CONTROL-CARD.                                      
021500                                                                          
021600     DISPLAY ' '.                                                         
021700     DISPLAY '*** MLKUT471 INPUT CONTROL CARD VALUES ***'                 
021800     DISPLAY ML110-DATA-INTEGRITY-CC-REC.                                 
021900                                                                          
022000     INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
022100     INITIALIZE PV-PROGRAM-VARIABLES.                                     
022200                                                                          
      *    DETERMINE RANGE OF DATES TO BE PROCESSED.                            
      *                                                                         
      *    THE START DATE WILL BE THE FIRST DAY OF THE FISCAL MONTH             
      *    INTO WHICH ML110-STRT-DTE FALLS.                                     
      *                                                                         
      *    THE END DATE WILL BE ML110-END-DTE OR THE MAX POSTING FSCL WK        
      *    DATE FROM TMLCNTL, WHICHEVER IS GREATER.  THE REASON THE END         
      *    DATE CANNOT BE GREATER THAN THE MAX POSTING FSCL WK DATE IS          
      *    THAT END INV RETAIL IS TAKEN FROM THE MOST RECENT WEEK               
      *    RETURNED BY THE TWKDPTC CURSOR. IF THE CURSOR RETURNS THE            
      *    FUTURE WEEK ROWS THAT EXIST FOR ON-ORDER, END INV RETAIL WILL        
      *    BE PICKED UP FROM THOSE ROWS.  END INV RTL WILL BE ZERO ON           
      *    THOSE ROWS BECAUSE WE ONLY RECALCULATE UP THROUGH THE MAX            
      *    POSTING FSCL WK DATE.                                                
                                                                                
           MOVE ML110-STRT-DTE         TO DTATTR-KY-DTE.                        
022300     PERFORM 7000-GET-FSCL-MN-BEG-DTE.                                    
           MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-STRT-DTE.                          
                                                                                
030600     PERFORM 7050-GET-MXPSTG-FSCL-WK-DTE.                                 
           IF ML110-END-DTE > MLCNTL-MXPSTG-FSCL-WK-DTE                         
               MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO PV-END-DTE                     
           ELSE                                                                 
               MOVE ML110-END-DTE             TO PV-END-DTE                     
           END-IF.                                                              
022400                                                                          
           DISPLAY 'START DATE: ' PV-STRT-DTE.                                  
           DISPLAY 'END DATE:   ' PV-END-DTE.                                   
022500 EJECT                                                                    
022600                                                                          
022700******************************************************************        
022800 2000-MAIN-PROCESS.                                                       
022900******************************************************************        
023000*                                                                         
023100     PERFORM 7100-OPEN-CURSOR-TWKDPTC.                                    
023200                                                                          
023300     PERFORM 7200-FETCH-TWKDPTC.                                          
023400                                                                          
023500     PERFORM                                                              
023600       UNTIL PS-TWKDPTC-IS-DONE                                           
023700         PERFORM 5000-WRITE-EXTRACT                                       
023800         PERFORM 7200-FETCH-TWKDPTC                                       
023900     END-PERFORM.                                                         
024000                                                                          
024100     PERFORM 7300-CLOSE-CURSOR-TWKDPTC.                                   
024200                                                                          
024300                                                                          
024400******************************************************************        
024500 4000-READ-CONTROL-CARD.                                                  
024600******************************************************************        
024700*                                                                         
024800     READ DATA-INTEGRITY-CTL-FILE                                         
024900       INTO ML110-DATA-INTEGRITY-CC-REC                                   
025000         AT END                                                           
025100             MOVE '4000-READ-CONTROL-CARD'                                
025200                                 TO AA-PARAGRAPH-NAME                     
025300             MOVE 'CONTROL CARD IS MISSING'                               
025400                                 TO AA-MESSAGE                            
025500             MOVE +4001          TO ABEND-CODE                            
025600             PERFORM 9900-COMMON-ABEND-ROUTINE                            
025700     END-READ.                                                            
025800                                                                          
025900 EJECT                                                                    
026000                                                                          
026100******************************************************************        
026200 5000-WRITE-EXTRACT.                                                      
026300******************************************************************        
026400*                                                                         
026500     MOVE DTATTR-FSCL-MN-END-DTE TO ML153-FSCL-MN-END-DTE.                
026600     MOVE DTATTR-FSCL-MN-NBR     TO ML153-FSCL-MN-NBR.                    
026700     MOVE WKDPTC-DEPT-NBR        TO ML153-DEPT-NBR.                       
026800     MOVE '00'                   TO ML153-MAJ-CL-NBR.                     
026900     MOVE '00'                   TO ML153-SUB-CL-NBR.                     
027000     MOVE ZEROS                  TO ML153-STR-NBR.                        
027000     MOVE WKDPTC-CHNL-CDE        TO ML153-CHNL-CDE.                       
027100     MOVE WKDPTC-SLS-RTL-AMT     TO ML153-SLS-RTL-AMT.                    
                                                                                
           COMPUTE PA-WORK-AMOUNT      = WKDPTC-MKDN-PLU-RTL-AMT                
                                       + WKDPTC-MKDN-RGST-RTL-AMT               
                                       + WKDPTC-MKDN-UMTCH-RTL-AMT              
                                       + WKDPTC-MKDN-BYR-RTL-AMT                
                                       + WKDPTC-MKDN-STR-RTL-AMT.               
027200     MOVE PA-WORK-AMOUNT         TO ML153-MKDN-RTL-AMT.                   
25554      IF PA-WORK-AMOUNT NOT = PA-NEW-MKDN-RTL-AMT                          
  |            DISPLAY '*** '                                                   
  |            DISPLAY '*** MARKDOWN TOTALS DO NOT MATCH FOR CHANNEL COD        
  |   -                'E: ' WKDPTC-CHNL-CDE ', DEPT: ' WKDPTC-DEPT-NBR         
  |                    ', FISCAL WEEK END DATE: ' WKDPTC-FSCL-WK-END-DTE        
  |            DISPLAY '*** ORIGINAL MARKDOWN TOTALS: ' PA-WORK-AMOUNT          
  |                    '  NEW MARKDOWN TOTALS: ' PA-NEW-MKDN-RTL-AMT            
  |            SET PS-MKDN-TTLS-INBALANCE TO TRUE                               
25554      END-IF.                                                              
                                                                                
027300     MOVE WKDPTC-MKUP-RTL-AMT    TO ML153-MKUP-RTL-AMT.                   
027400     MOVE WKDPTC-XFER-IN-RTL-AMT TO ML153-XFER-IN-RTL-AMT.                
027500     MOVE WKDPTC-XFER-OUT-RTL-AMT                                         
027600                                 TO ML153-XFER-OUT-RTL-AMT.               
027700     MOVE WKDPTC-RCPT-RTL-AMT    TO ML153-RCPT-RTL-AMT.                   
027800     MOVE WKDPTC-RCPT-NET-COST-AMT                                        
027900                                 TO ML153-RCPT-NET-COST-AMT.              
                                                                                
23565      COMPUTE PA-WORK-AMOUNT      = WKDPTC-PADJ-RTV-RTL-AMT                
  |                                    + WKDPTC-PADJ-DA-RTL-AMT                 
  |                                    + WKDPTC-PADJ-OTH-RTL-AMT.               
  |        MOVE PA-WORK-AMOUNT         TO ML153-PADJ-RTL-AMT.                   
  |   **** MOVE WKDPTC-PADJ-RTL-AMT    TO ML153-PADJ-RTL-AMT.                   
  |                                                                             
P18333*    DISPLAY 'LIC FEE = ' WKDPTC-LIC-FEE-COST-AMT.                        
  |        COMPUTE PA-WORK-AMOUNT      = WKDPTC-PADJ-RTV-COST-AMT               
  |                                    + WKDPTC-PADJ-DA-COST-AMT                
  |                                    + WKDPTC-PADJ-OTH-COST-AMT               
23565                                  + WKDPTC-PADJ-FRGT-COST-AMT              
27511                                  + WKDPTC-PADJ-BROP-COST-AMT              
28630                                  + WKDPTC-LIC-FEE-COST-AMT.               
23565      MOVE PA-WORK-AMOUNT         TO ML153-PADJ-NET-COST-AMT.              
  |   **** MOVE WKDPTC-PADJ-NET-COST-AMT                                        
23565 ****                             TO ML153-PADJ-NET-COST-AMT.              
                                                                                
028300     MOVE WKDPTC-INV-ADJ-RTL-AMT TO ML153-INV-ADJ-RTL-AMT.                
028400     MOVE WKDPTC-SHNK-RTL-AMT    TO ML153-SHNK-RTL-AMT.                   
028500                                                                          
028600*    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE             
028700*    FIRST (MOST RECENT) FISCAL WEEK IN THE FISCAL MONTH.                 
028800*    (THE FISCAL WEEKS ARE PROCESSED IN DESCENDING SEQUENCE.)             
028900     IF  DTATTR-FSCL-MN-END-DTE  = PV-PREV-FSCL-MN-END-DTE                
029000     AND WKDPTC-DEPT-NBR         = PV-PREV-DEPT-NBR                       
029100         MOVE ZEROS                  TO ML153-END-INV-RTL-AMT             
029200     ELSE                                                                 
029300         MOVE WKDPTC-END-INV-RTL-AMT TO ML153-END-INV-RTL-AMT             
029400         MOVE DTATTR-FSCL-MN-END-DTE TO PV-PREV-FSCL-MN-END-DTE           
029500         MOVE WKDPTC-DEPT-NBR        TO PV-PREV-DEPT-NBR                  
029600     END-IF.                                                              
029700                                                                          
029800     WRITE DATA-INTEGRITY-EXTR-REC                                        
029900         FROM ML153-DATA-INTEGRITY-EXTR-REC.                              
030000     ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.               
030100                                                                          
030300 EJECT                                                                    
030500******************************************************************        
030600 7000-GET-FSCL-MN-BEG-DTE.                                                
030700******************************************************************        
030800                                                                          
030900     EXEC SQL                                                             
031000         SELECT  FSCL_MN_BEG_DTE                                          
031100           INTO :DTATTR-FSCL-MN-BEG-DTE                                   
031200           FROM TDTATTR                                                   
031300          WHERE KY_DTE = :DTATTR-KY-DTE                                   
W26603          WITH UR                                                         
031400     END-EXEC.                                                            
031500                                                                          
031600     EVALUATE TRUE                                                        
031700         WHEN SQLCODE = 0                                                 
031800             CONTINUE                                                     
031900         WHEN OTHER                                                       
032000             MOVE '7000-GET-FSCL-MN-BEG-DTE'                              
032100                                 TO AA-PARAGRAPH-NAME                     
032200             MOVE 'NO TDTATTR MATCH FOUND FOR KY_DTE'                     
032300                                 TO AA-MESSAGE                            
032400             MOVE 'SELECT FROM'  TO AA-DB2-OPERATION                      
032500             PERFORM 9901-SQL-ERROR                                       
032600     END-EVALUATE.                                                        
032700                                                                          
032800                                                                          
030500******************************************************************        
030600 7050-GET-MXPSTG-FSCL-WK-DTE.                                             
030700******************************************************************        
030800*                                                                         
030900     EXEC SQL                                                             
031000         SELECT  MXPSTG_FSCL_WK_DTE                                       
                 INTO :MLCNTL-MXPSTG-FSCL-WK-DTE                                
031200           FROM TMLCNTL                                                   
W26603         WITH UR                                                          
031400     END-EXEC.                                                            
031500                                                                          
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7050-GET-MXPSTG-FSCL-WK-DTE'                           
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
032700                                                                          
032800                                                                          
032900******************************************************************        
033000 7100-OPEN-CURSOR-TWKDPTC.                                                
033100******************************************************************        
033200*                                                                         
033300     EXEC SQL                                                             
033400         OPEN TWKDPTC_CURSOR                                              
033500     END-EXEC.                                                            
033600                                                                          
033700     EVALUATE TRUE                                                        
033800         WHEN SQLCODE = 0                                                 
033900             CONTINUE                                                     
034000         WHEN OTHER                                                       
034100             MOVE '7100-OPEN-CURSOR-TWKDPTC'                              
034200                                 TO AA-PARAGRAPH-NAME                     
034300             MOVE 'TWKDPTC TABLE ERROR'                                   
034400                                 TO AA-MESSAGE                            
034500             MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION                      
034600             PERFORM 9901-SQL-ERROR                                       
034700     END-EVALUATE.                                                        
034800                                                                          
034900 EJECT                                                                    
035000                                                                          
035100******************************************************************        
035200 7200-FETCH-TWKDPTC.                                                      
035300******************************************************************        
035400*                                                                         
035500     EXEC SQL                                                             
             FETCH TWKDPTC_CURSOR                                               
               INTO :DTATTR-FSCL-MN-NBR,                                        
                    :DTATTR-FSCL-MN-END-DTE,                                    
                    :WKDPTC-FSCL-WK-NBR,                                        
                    :WKDPTC-FSCL-WK-END-DTE,                                    
                    :WKDPTC-DEPT-NBR,                                           
                    :WKDPTC-CHNL-CDE,                                           
                    :WKDPTC-SLS-RTL-AMT,                                        
                    :WKDPTC-MKDN-PLU-RTL-AMT,                                   
                    :WKDPTC-MKDN-RGST-RTL-AMT,                                  
                    :WKDPTC-MKDN-UMTCH-RTL-AMT,                                 
                    :WKDPTC-MKDN-BYR-RTL-AMT,                                   
                    :WKDPTC-MKDN-STR-RTL-AMT,                                   
                    :WKDPTC-MKUP-RTL-AMT,                                       
                    :WKDPTC-XFER-IN-RTL-AMT,                                    
                    :WKDPTC-XFER-OUT-RTL-AMT,                                   
                    :WKDPTC-RCPT-RTL-AMT,                                       
                    :WKDPTC-RCPT-NET-COST-AMT,                                  
                    :WKDPTC-PADJ-RTL-AMT,                                       
                    :WKDPTC-PADJ-NET-COST-AMT,                                  
                    :WKDPTC-PADJ-FRGT-COST-AMT,                                 
                    :WKDPTC-INV-ADJ-RTL-AMT,                                    
                    :WKDPTC-SHNK-RTL-AMT,                                       
                    :WKDPTC-END-INV-RTL-AMT,                                    
                    :WKDPTC-CUM-MKUP-PCT,                                       
                    :WKDPTC-END-INV-COST-AMT,                                   
                    :WKDPTC-SLS-COST-AMT,                                       
23565               :WKDPTC-GRO-MRGN-AMT,                                       
23565               :WKDPTC-PADJ-RTV-COST-AMT,                                  
23565               :WKDPTC-PADJ-RTV-RTL-AMT,                                   
23565               :WKDPTC-PADJ-DA-COST-AMT,                                   
23565               :WKDPTC-PADJ-DA-RTL-AMT,                                    
23565               :WKDPTC-PADJ-OTH-COST-AMT,                                  
23565               :WKDPTC-PADJ-OTH-RTL-AMT,                                   
25554               :PA-NEW-MKDN-RTL-AMT,                                       
W27511              :WKDPTC-PADJ-BROP-COST-AMT,                                 
W28630              :WKDPTC-LIC-FEE-COST-AMT                                    
037400     END-EXEC.                                                            
037500                                                                          
037600     EVALUATE TRUE                                                        
037700         WHEN SQLCODE = 0                                                 
037800             ADD +1              TO PA-TWKDPTC-ROWS-FETCHED               
037900         WHEN SQLCODE = +100                                              
038000             MOVE 'Y'            TO PS-TWKDPTC-DONE                       
038100         WHEN OTHER                                                       
038200             MOVE '7200-FETCH-TWKDPTC'                                    
038300                                 TO AA-PARAGRAPH-NAME                     
038400             MOVE 'TWKDPTC TABLE ERROR'                                   
038500                                 TO AA-MESSAGE                            
038600             MOVE 'FETCH'        TO AA-DB2-OPERATION                      
038700             PERFORM 9901-SQL-ERROR                                       
038800     END-EVALUATE.                                                        
                                                                                
038900                                                                          
039000 EJECT                                                                    
039100                                                                          
039200******************************************************************        
039300 7300-CLOSE-CURSOR-TWKDPTC.                                               
039400******************************************************************        
039500*                                                                         
039600     EXEC SQL                                                             
039700         CLOSE TWKDPTC_CURSOR                                             
039800     END-EXEC.                                                            
039900                                                                          
040000     EVALUATE TRUE                                                        
040100         WHEN SQLCODE = 0                                                 
040200             CONTINUE                                                     
040300         WHEN OTHER                                                       
040400             MOVE '7300-CLOSE-CURSOR-TWKDPTC'                             
040500                                 TO AA-PARAGRAPH-NAME                     
040600             MOVE 'TWKDPTC TABLE ERROR'                                   
040700                                 TO AA-MESSAGE                            
040800             MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION                      
040900             PERFORM 9901-SQL-ERROR                                       
041000     END-EVALUATE.                                                        
041100                                                                          
041200                                                                          
041300******************************************************************        
041400 8000-EOJ.                                                                
041500******************************************************************        
041600*                                                                         
041700     DISPLAY ' '.                                                         
041800     DISPLAY '  PROGRAM MLKUT471 PROCESSING SUMMARY'.                     
041900     DISPLAY '  -----------------------------------'.                     
042000     DISPLAY ' '.                                                         
042100*                                                                         
042200     MOVE PA-TWKDPTC-ROWS-FETCHED TO CD-COUNT-HOLD1.                      
042300     DISPLAY 'TWKDPTC ROWS FETCHED =      ' CD-COUNT-HOLD1.               
042400*                                                                         
042500     MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
042600     DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.               
042700*                                                                         
042800     DISPLAY ' '.                                                         
042900                                                                          
043000     CLOSE DATA-INTEGRITY-CTL-FILE.                                       
043100     CLOSE DATA-INTEGRITY-EXTR-FILE.                                      
043200                                                                          
043300 EJECT                                                                    
043400                                                                          
043500******************************************************************        
043600 9900-COMMON-ABEND-ROUTINE.                                               
043700******************************************************************        
043800*                                                                         
043900     DISPLAY AA-ABEND-LIT.                                                
044000     DISPLAY AA-PROGRAM-LIT.                                              
044100     DISPLAY AA-PARAGRAPH-LIT.                                            
044200     DISPLAY AA-MESSAGE-LINE.                                             
044300     DISPLAY ABEND-CODE.                                                  
044400                                                                          
044500     CALL 'ILBOABN0' USING ABEND-CODE.                                    
044600                                                                          
044700 EJECT                                                                    
044800                                                                          
044900******************************************************************        
045000 9901-SQL-ERROR.                                                          
045100******************************************************************        
045200*                                                                         
045300     CLOSE DATA-INTEGRITY-CTL-FILE                                        
045400           DATA-INTEGRITY-EXTR-FILE.                                      
045500                                                                          
045600     DISPLAY AA-ABEND-LIT.                                                
045700     DISPLAY AA-DB2-ERROR-LIT.                                            
045800     DISPLAY AA-PROGRAM-LIT.                                              
045900     DISPLAY AA-PARAGRAPH-LIT.                                            
046000     DISPLAY AA-MESSAGE-LINE.                                             
046100     DISPLAY AA-DB2-OPERATION-LIT.                                        
046200     SET AC-DB2-ERROR TO TRUE.                                            
046300     MOVE +4013                  TO ABEND-CODE.                           
046400                                                                          
046500     COPY DPPD004.                                                        
046600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
046700                                                                          
