000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.             SUKRF504.                                        
000600 AUTHOR.                 LARRY SCHLEICHER - STRATAGEM                     
000700 INSTALLATION.           KOHLS.                                           
000800 DATE-WRITTEN            JUNE, 1996.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200* THIS PROGRAM DISPLAYS THE DUPLICATE SCAC SCREEN ON THE                  
001300* LRT (LASER RADIO TERMINAL) AND ACCEPTS AN F-KEY.                        
001400******************************************************************        
001410*---------------------------------------------------------------*         
001420*    CHANGES                                                     *        
001430*    MADE BY: CHERI PARMLEY                                      *        
001440*    DATE:    09/25/2014                                         *        
001450*                                                                *        
001460*    CHANGE TO APO FROM 7 DIGITS TO 8 DIGITS ON MAP - COMPILE    *        
001470*    PROGRAM.                                                    *        
001480*                                                                *        
001490*----------------------------------------------------------------*        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000******************************************************************        
002100 DATA DIVISION.                                                           
002200******************************************************************        
002300                                                                          
002400******************************************************************        
002500 WORKING-STORAGE SECTION.                                                 
002600******************************************************************        
002700 01  CA-COMMAREA.                                                         
002800     COPY SUWS001.                                                        
002900                                                                          
003000                                                                          
003100******************************************************************        
003200 01  PC-PROGRAM-CONSTANTS.                                                
003300*    05  PC-COMMAREA-LENGTH              PIC S9(04)  VALUE +300           
003400     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE +0             
003500                                                     COMP SYNC.           
003600     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
003700         'SUKRF504'.                                                      
003800     05  PC-DC-RF-APPL-PGM               PIC  X(08)  VALUE                
003900         'MPKCS027'.                                                      
004000     05  PC-SU-DUPLICATE                 PIC  X(04)  VALUE 'S504'.        
004100     05  PC-SU-CHECK-IN-PGM              PIC  X(08)  VALUE                
004200         'SUKRF502'.                                                      
004300     05  PC-END-OF-SESSION-MSG           PIC  X(19)  VALUE                
004400         'END OF SESSION     '.                                           
004500     05  PC-INVALID-SELECTION-MSG        PIC  X(19)  VALUE                
004600         'INVALID SELECTION  '.                                           
004700     05  PC-INVALID-VALUE-MSG            PIC  X(19)  VALUE                
004800         'INVALID VALUE      '.                                           
004900     05  PC-UNRECOGNIZED-PF-KEY-MSG      PIC  X(19)  VALUE                
005000         'UNRECOGNIZED PF KEY'.                                           
005100     05  PC-ACCESS-NOT-ALLOWED-MSG       PIC  X(19)  VALUE                
005200         'USER NOT AUTHORIZED'.                                           
005300     05  PC-NON-NUMERIC-SELECTION-MSG    PIC  X(19)  VALUE                
005400         'NUMERIC VALUE REQ. '.                                           
005500     05  PC-TRANS-UNAVAILABLE-MSG        PIC  X(19)  VALUE                
005600         'TRANS NOT AVAILABLE'.                                           
005700     05  PC-CICS-ABEND-MSG               PIC  X(19)  VALUE                
005800         'CICS ABEND -CALL DP'.                                           
005900     05  PC-CICSAUTH-MSG                 PIC  X(19)  VALUE                
006000         'CICSAUTH BAD RETURN'.                                           
006100     05  PC-UNAVAIL-SELECTION-MSG        PIC X(19)   VALUE                
006200         'SELECTION NOT AVAIL'.                                           
006210     05  PC-NO-CRTN-DATA-FOUND-MSG       PIC X(19)   VALUE                
006220         'NO CRTN DATA FOUND '.                                           
006230     05  PC-CRT-CRTE-RCV-NO-SML-MSG      PIC X(19)   VALUE                
006240         'CRT CRTE RCV NO SML'.                                           
006300     05  PC-N                            PIC X(01)   VALUE 'N'.           
006400     05  PC-Y                            PIC X(01)   VALUE 'Y'.           
006500     05  PC-F4-IGNORE                    PIC X(08)   VALUE                
006600         'IGNORE  '.                                                      
006700     05  PC-F4-IGNORE-PRINT              PIC X(08)   VALUE                
006800         'IGNR&PRT'.                                                      
006900                                                                          
007000                                                                          
007100 01  PS-PROGRAM-SWITCHES.                                                 
007200     05  PS-ACCESS-ALLOWED-IND           PIC  X(01)  VALUE 'N'.           
007300         88  PS-NO-ACCESS-ALLOWED                    VALUE 'N'.           
007400         88  PS-ACCESS-ALLOWED                       VALUE 'Y'.           
007410     05  PS-CRTN-CREATE-SW               PIC  X(01)  VALUE ' '.           
007420         88  PS-CRTN-CREATE-RCV                      VALUE 'R'.           
007430         88  PS-CRTN-CREATE-ASN                      VALUE 'A'.           
007500                                                                          
007600                                                                          
007700 01  PV-PROGRAM-VARIABLES.                                                
007800     05  PV-MESSAGE-AREA                 PIC  X(19)  VALUE SPACE.         
007900     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS          
008000                                                     COMP SYNC.           
008100     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE.         
008200     05  PV-DUPLICATE-IND                PIC  X(01)  VALUE SPACE.         
008300         88 PV-VALID-DUP-IND             VALUE 'Y', 'N'.                  
008400     05  PV-LABEL-IND                    PIC  X(01)  VALUE SPACE.         
008500         88 PV-VALID-LBL-IND             VALUE 'Y', 'N'.                  
008600     05  PV-FKEY-MSG.                                                     
008700         10  FILLER                      PIC  X(11)  VALUE                
008800             'F5-ADD F4-'.                                                
008900         10  PV-F4MSG                    PIC  X(08).                      
008910     05  PV-DB2-COUNT                    PIC S9(09)    VALUE ZERO         
008920                                                   COMP-3.                
009000     05  PV-DB2-DATE                     PIC  X(10).                      
009100     05  PV-DB2-REFORMAT-DATE.                                            
009200         10  PV-DB2-DATE-CC              PIC  X(02).                      
009300         10  PV-DB2-DATE-YY              PIC  X(02).                      
009400         10  FILLER                      PIC  X(01).                      
009500         10  PV-DB2-DATE-MM              PIC  X(02).                      
009600         10  FILLER                      PIC  X(01).                      
009700         10  PV-DB2-DATE-DD              PIC  X(02).                      
009800     05  PV-DB2-TMST.                                                     
009900         10  PV-DB2-TMST-DATE         PIC X(10).                          
010000         10  FILLER                   PIC X(01).                          
010100         10  PV-DB2-TMST-TIME         PIC X(08).                          
010200         10  FILLER                   PIC X(07).                          
010300     05  PV-DISPLAY-DATE.                                                 
010400         10  PV-DISPLAY-DATE-MM          PIC  X(02).                      
010500         10  FILLER                      PIC  X(01)  VALUE '/'.           
010600         10  PV-DISPLAY-DATE-DD          PIC  X(02).                      
010700         10  FILLER                      PIC  X(01)  VALUE '/'.           
010800         10  PV-DISPLAY-DATE-YY          PIC  X(02).                      
010900     05  PV-DB2-TIME                     PIC  X(08).                      
011000                                                                          
011100******************************************************************        
011200*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
011300******************************************************************        
011400     COPY DPWS013.                                                        
011500                                                                          
011600******************************************************************        
011700*    COPY BOOK FOR THE CICSAUTH SUBROUTINE                                
011800******************************************************************        
011900     COPY DPWS008.                                                        
012000                                                                          
012100******************************************************************        
012200*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)         
012300******************************************************************        
012400     COPY DPWS010I.                                                       
012500                                                                          
012600******************************************************************        
012700*  GENERATED DSECT FOR THE MPKM027 MAPSET                                 
012800******************************************************************        
012900     COPY SUKM504.                                                        
013000                                                                          
013100******************************************************************        
013200*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           
013300******************************************************************        
013400     COPY DPWS015.                                                        
013500                                                                          
013600******************************************************************        
013700*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                              
013800******************************************************************        
013900     COPY DFHAID.                                                         
014000                                                                          
014100*                                                                         
014200*  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE SHIPPING UNIT              
014300*  TABLE                                                                  
014400*                                                                         
014500     EXEC SQL                                                             
014600         INCLUDE TSHPUNT                                                  
014700     END-EXEC.                                                            
014800                                                                          
014900     EXEC SQL                                                             
015000         INCLUDE TSHUNLO                                                  
015100     END-EXEC.                                                            
015200                                                                          
015300*                                                                         
015400*  COMMUNICATIONS AREA FOR DB2                                            
015500*                                                                         
015600     EXEC SQL                                                             
015700         INCLUDE SQLCA                                                    
015800     END-EXEC.                                                            
015900                                                                          
016000******************************************************************        
016100 LINKAGE SECTION.                                                         
016200******************************************************************        
016300*01  DFHCOMMAREA                         PIC  X(300).                     
016400 01  DFHCOMMAREA.                                                         
016500     05  FILLER                       OCCURS 1 TO 4072 TIMES              
016600                                      DEPENDING ON EIBCALEN.              
016700         10  FILLER                   PIC X.                              
016800                                                                          
016900******************************************************************        
017000 PROCEDURE DIVISION.                                                      
017100******************************************************************        
017200 0000-PROGRAM-MAINLINE.                                                   
017300*LMM                                                                      
017400     INITIALIZE  CA-COMMAREA                                              
017500     MOVE DFHCOMMAREA                TO CA-COMMAREA.                      
017600     MOVE LOW-VALUES                 TO SU504AI.                          
017700                                                                          
017800     EVALUATE TRUE                                                        
017900         WHEN EIBCALEN = ZERO                                             
018000         WHEN EIBAID = DFHCLEAR                                           
018100         WHEN SU001-INITIALIZE-SCREEN                                     
018200              MOVE SU001-CARTON TO ACARTNO                                
018300              MOVE SU001-PO     TO APOO                                   
018400              MOVE SU001-STORE  TO ASTRO                                  
018500              MOVE PC-N         TO PV-LABEL-IND                           
019200              MOVE SU001-CARTON TO SHUNLO-SHIPT-UNT-ID                    
019300              PERFORM 2000-SELECT-SHUNLO                                  
019400              MOVE PV-DB2-TMST-DATE        TO PV-DB2-DATE                 
019500              MOVE PV-DB2-DATE             TO PV-DB2-REFORMAT-DATE        
019600              MOVE PV-DB2-DATE-YY          TO PV-DISPLAY-DATE-YY          
019700              MOVE PV-DB2-DATE-MM          TO PV-DISPLAY-DATE-MM          
019800              MOVE PV-DB2-DATE-DD          TO PV-DISPLAY-DATE-DD          
019900              MOVE PV-DISPLAY-DATE           TO APDTEO                    
020000              MOVE PV-DB2-TMST-TIME          TO APTMEO                    
020100              EXEC SQL                                                    
020200                  SET :PV-DB2-DATE = CURRENT DATE                         
020300              END-EXEC                                                    
020400              MOVE PV-DB2-DATE             TO PV-DB2-REFORMAT-DATE        
020500              MOVE PV-DB2-DATE-YY          TO PV-DISPLAY-DATE-YY          
020600              MOVE PV-DB2-DATE-MM          TO PV-DISPLAY-DATE-MM          
020700              MOVE PV-DB2-DATE-DD          TO PV-DISPLAY-DATE-DD          
020800              MOVE PV-DISPLAY-DATE           TO ACDTEO                    
020900              EXEC SQL                                                    
021000                  SET :PV-DB2-TIME = CURRENT TIME                         
021100              END-EXEC                                                    
021200              MOVE PV-DB2-TIME               TO ACTMEO                    
021201                                                                          
021210              IF SU001-PRINTER-IND = PC-N                                 
021220                  MOVE PC-F4-IGNORE          TO PV-F4MSG                  
021230              ELSE                                                        
021240                  MOVE PC-F4-IGNORE-PRINT    TO PV-F4MSG                  
021251              END-IF                                                      
021260              MOVE PV-FKEY-MSG               TO AFKMSGO                   
021270                                                                          
021300              SET SU001-PROCESS-SCREEN TO TRUE                            
021400              PERFORM 8000-SEND-MAP                                       
021500              PERFORM 8200-RETURN-TRANID                                  
021600         WHEN EIBAID = DFHPF5                                             
021700             MOVE PC-Y               TO SU001-DUPLICATE-IND               
021800             IF SU001-PRINTER-IND = PC-N                                  
021900                 MOVE PC-N           TO SU001-LABEL-IND                   
022000             ELSE                                                         
022100                 MOVE PC-Y           TO SU001-LABEL-IND                   
022200             END-IF                                                       
022300             MOVE PC-SU-CHECK-IN-PGM TO PV-TRANSFER-PROGRAM               
022400             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
022500                                                                          
022600         WHEN EIBAID = DFHPF3                                             
022700             MOVE PC-N               TO SU001-DUPLICATE-IND               
022800             MOVE PC-N               TO SU001-LABEL-IND                   
022900             MOVE PC-SU-CHECK-IN-PGM TO PV-TRANSFER-PROGRAM               
023000             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
023100                                                                          
023200         WHEN EIBAID = DFHPF4                                             
023210             PERFORM 2050-SELECT-SHPUNT                                   
023300             MOVE PC-N               TO SU001-DUPLICATE-IND               
023400             MOVE PC-Y               TO SU001-REPRINT-IND                 
023500             IF SU001-PRINTER-IND = PC-N OR PS-CRTN-CREATE-RCV            
023501                IF PS-CRTN-CREATE-RCV                                     
023510                   MOVE 'SUKRF530' TO PV-TRANSFER-PROGRAM                 
023520                   SET SU001-CREATE-SOURCE-RECV   TO TRUE                 
023530                   PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                  
023540                ELSE                                                      
023600                   MOVE PC-N           TO SU001-LABEL-IND                 
023610                END-IF                                                    
023700             ELSE                                                         
023800                 MOVE PC-Y           TO SU001-LABEL-IND                   
023900             END-IF                                                       
024000             MOVE PC-SU-CHECK-IN-PGM TO PV-TRANSFER-PROGRAM               
024100             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
024200                                                                          
024300         WHEN EIBAID = DFHENTER                                           
024400              PERFORM 8100-SEND-DATA-ONLY                                 
024500              PERFORM 8200-RETURN-TRANID                                  
024600         WHEN OTHER                                                       
024700              MOVE PC-UNRECOGNIZED-PF-KEY-MSG                             
024800                                     TO AMSGLNO                           
024900              PERFORM 8100-SEND-DATA-ONLY                                 
025000              PERFORM 8200-RETURN-TRANID                                  
025100     END-EVALUATE.                                                        
025200                                                                          
025300     GOBACK.                                                              
025400*                                                                         
025500*                                                                         
025600 1000-RECEIVE-MAP.                                                        
025700     EXEC CICS                                                            
025800         RECEIVE MAP    ('SU504A')                                        
025900                 MAPSET ('SUKM504')                                       
026000                 RESP   (PV-CICS-RESPONSE)                                
026100     END-EXEC.                                                            
026200                                                                          
026300     EVALUATE TRUE                                                        
026400         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
026500         WHEN PV-CICS-RESPONSE = DFHRESP (MAPFAIL)                        
026600              CONTINUE                                                    
026700         WHEN OTHER                                                       
026800              MOVE '1000-RECEIVE-MAP'                                     
026900                                     TO DP013-PARAGRAPH                   
027000              MOVE 'UNABLE TO RECEIVE MAP'                                
027100                                     TO DP013-MESSAGE-TEXT (1)            
027200              SET DP013-LOGIC-ABEND  TO TRUE                              
027300              SET DP013-NO-ROLLBACK  TO TRUE                              
027400              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
027500     END-EVALUATE.                                                        
027600*                                                                         
027700 2000-SELECT-SHUNLO.                                                      
027800                                                                          
027900     EXEC SQL                                                             
028000         SELECT MAX(CRE_TMST)                                             
028100         INTO   :SHUNLO-CRE-TMST                                          
028200         FROM   TSHUNLO                                                   
028300         WHERE  SHIPT_UNT_ID   = :SHUNLO-SHIPT-UNT-ID                     
028310           AND  SCN_LOC_ID     = 'RECV'                                   
028320**         AND  ACTV_CDE       = 'A'                                      
028330          WITH UR                                                         
028400     END-EXEC.                                                            
028500                                                                          
028600     EVALUATE TRUE                                                        
028700         WHEN SQLCODE = ZERO                                              
028800             MOVE SHUNLO-CRE-TMST TO PV-DB2-TMST                          
028810         WHEN SQLCODE = -305                                              
028820             MOVE SPACES          TO PV-DB2-TMST                          
028900         WHEN SQLWARN0 NOT = SPACES                                       
029000         WHEN SQLCODE NOT EQUAL ZERO                                      
029100             MOVE '2000-SELECT-SHUNLO'                                    
029200                             TO DP013-PARAGRAPH                           
029300             MOVE 'CHECK VALID CARTON'                                    
029400                             TO DP013-MESSAGE-TEXT(1)                     
029500             MOVE SHUNLO-SHIPT-UNT-ID                                     
029600                             TO DP013-MESSAGE-TEXT(2)                     
029700             MOVE SQLCA      TO DP013-SQLCA                               
029800             MOVE 'TSHPUNT'  TO DP013-DB2-TABLE-NAME(1)                   
029900             SET DP013-DB2-ABEND                                          
030000                             TO TRUE                                      
030100             SET DP013-NO-ROLLBACK                                        
030200                             TO TRUE                                      
030300             SET DP013-XCTL-DISPLAY-RESTART                               
030400                             TO TRUE                                      
030500             PERFORM DP014-0000-PROCESS-ABEND                             
030600     END-EVALUATE.                                                        
030700                                                                          
030800*                                                                         
030810*                                                                         
030820 2050-SELECT-SHPUNT.                                                      
030830                                                                          
030831     MOVE SU001-CARTON TO SHUNLO-SHIPT-UNT-ID.                            
030832     MOVE SPACES       TO PS-CRTN-CREATE-SW.                              
030833     MOVE ZEROES       TO PV-DB2-COUNT.                                   
030834                                                                          
030840     EXEC SQL                                                             
030850         SELECT COUNT(*)                                                  
030860         INTO   :PV-DB2-COUNT                                             
030870         FROM   TSHPUNT                                                   
030880         WHERE  SHIPT_UNT_ID      =  :SHUNLO-SHIPT-UNT-ID                 
030890           AND  SHIPT_UNT_SRC_ID  =  'RCV'                                
030891         FETCH FIRST 1 ROW ONLY                                           
030892          WITH UR                                                         
030893     END-EXEC.                                                            
030894                                                                          
030895     EVALUATE TRUE                                                        
030896         WHEN SQLCODE = ZERO                                              
030897              IF PV-DB2-COUNT  > 0                                        
030898                 SET PS-CRTN-CREATE-RCV TO TRUE                           
030899              END-IF                                                      
030900         WHEN SQLWARN0 NOT = SPACES                                       
030901         WHEN SQLCODE NOT EQUAL ZERO                                      
030902             MOVE '2050-SELECT-SHPUNT'                                    
030903                             TO DP013-PARAGRAPH                           
030904             MOVE 'CHECK CREATE SRCE '                                    
030905                             TO DP013-MESSAGE-TEXT(1)                     
030906             MOVE SHUNLO-SHIPT-UNT-ID                                     
030907                             TO DP013-MESSAGE-TEXT(2)                     
030908             MOVE SQLCA      TO DP013-SQLCA                               
030909             MOVE 'TSHPUNT'  TO DP013-DB2-TABLE-NAME(1)                   
030910             SET DP013-DB2-ABEND                                          
030911                             TO TRUE                                      
030912             SET DP013-NO-ROLLBACK                                        
030913                             TO TRUE                                      
030914             SET DP013-XCTL-DISPLAY-RESTART                               
030915                             TO TRUE                                      
030916             PERFORM DP014-0000-PROCESS-ABEND                             
030917     END-EVALUATE.                                                        
030918                                                                          
030919*                                                                         
030920 2100-XCTL-TO-SELECTED-PROGRAM.                                           
031000     SET SU001-INITIALIZE-SCREEN     TO TRUE.                             
031100     MOVE PC-CURRENT-PROGRAM-NAME    TO SU001-LAST-PROGRAM.               
031200                                                                          
031300*LMM                                                                      
031400     MOVE LENGTH OF CA-COMMAREA   TO PV-COMMAREA-LENGTH                   
031500     EXEC CICS                                                            
031600         XCTL                                                             
031700             PROGRAM   (PV-TRANSFER-PROGRAM)                              
031800             COMMAREA  (CA-COMMAREA)                                      
032000             LENGTH    (PV-COMMAREA-LENGTH)                               
032100             RESP      (PV-CICS-RESPONSE)                                 
032200     END-EXEC.                                                            
032300                                                                          
032400     EVALUATE TRUE                                                        
032500         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
032600              CONTINUE                                                    
032700         WHEN OTHER                                                       
032800              MOVE '2100-XCTL-TO-SELECTED-PROGRAM'                        
032900                                     TO DP013-PARAGRAPH                   
033000              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '               
033100                                     TO DP013-MESSAGE-TEXT (1)            
033200              MOVE PV-TRANSFER-PROGRAM                                    
033300                                     TO DP013-MESSAGE-TEXT (2)            
033400              SET DP013-CICS-ABEND   TO TRUE                              
033500              SET DP013-NO-ROLLBACK  TO TRUE                              
033600              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
033700              MOVE PC-TRANS-UNAVAILABLE-MSG                               
033800                                     TO AMSGLNO                           
033900              SET SU001-PROCESS-SCREEN TO TRUE                            
034000              PERFORM 8100-SEND-DATA-ONLY                                 
034100              PERFORM 8200-RETURN-TRANID                                  
034200     END-EVALUATE.                                                        
034300*                                                                         
034400 8000-SEND-MAP.                                                           
034500     EXEC CICS                                                            
034600         SEND MAP       ('SU504A')                                        
034700              MAPSET    ('SUKM504')                                       
034800              ERASE                                                       
034900              RESP      (PV-CICS-RESPONSE)                                
035000     END-EXEC.                                                            
035100                                                                          
035200     EVALUATE TRUE                                                        
035300         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
035400              CONTINUE                                                    
035500         WHEN OTHER                                                       
035600              MOVE '8000-SEND-MAP'   TO DP013-PARAGRAPH                   
035700              MOVE 'UNABLE TO SEND MAP'                                   
035800                                     TO DP013-MESSAGE-TEXT (1)            
035900              SET DP013-CICS-ABEND   TO TRUE                              
036000              SET DP013-NO-ROLLBACK  TO TRUE                              
036100              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
036200     END-EVALUATE.                                                        
036300*                                                                         
036400 8100-SEND-DATA-ONLY.                                                     
036500                                                                          
036600     EXEC CICS                                                            
036700         SEND MAP       ('SU504A')                                        
036800              MAPSET    ('SUKM504')                                       
036900              CURSOR                                                      
037000              DATAONLY                                                    
037100              RESP      (PV-CICS-RESPONSE)                                
037200     END-EXEC.                                                            
037300                                                                          
037400     EVALUATE TRUE                                                        
037500         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
037600              CONTINUE                                                    
037700         WHEN OTHER                                                       
037800              MOVE '8100-SEND-DATA-ONLY'                                  
037900                                     TO DP013-PARAGRAPH                   
038000              MOVE 'UNABLE TO SEND DATA'                                  
038100                                     TO DP013-MESSAGE-TEXT (1)            
038200              SET DP013-CICS-ABEND   TO TRUE                              
038300              SET DP013-NO-ROLLBACK  TO TRUE                              
038400              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
038500     END-EVALUATE.                                                        
038600*                                                                         
038700*                                                                         
038800 8200-RETURN-TRANID.                                                      
038900*LMM                                                                      
039000     MOVE LENGTH OF CA-COMMAREA   TO PV-COMMAREA-LENGTH                   
039100     EXEC CICS                                                            
039200         RETURN TRANSID   (PC-SU-DUPLICATE)                               
039300                COMMAREA  (CA-COMMAREA)                                   
039500                LENGTH    (PV-COMMAREA-LENGTH)                            
039600                RESP      (PV-CICS-RESPONSE)                              
039700     END-EXEC.                                                            
039800                                                                          
039900     EVALUATE TRUE                                                        
040000         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
040100              CONTINUE                                                    
040200         WHEN OTHER                                                       
040300              MOVE '8200-RETURN-TRANID'                                   
040400                                     TO DP013-PARAGRAPH                   
040500              MOVE 'UNABLE TO RETURN TO TRANID'                           
040600                                     TO DP013-MESSAGE-TEXT (1)            
040700              SET DP013-CICS-ABEND   TO TRUE                              
040800              SET DP013-NO-ROLLBACK  TO TRUE                              
040900              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
041000     END-EVALUATE.                                                        
041100*                                                                         
041200 9999-WRITE-MESSAGE-TO-CICS-LOG.                                          
041300     MOVE PC-CURRENT-PROGRAM-NAME    TO DP013-PROGRAM.                    
041400     MOVE DFHEIBLK                   TO DP013-DFHEIBLK.                   
041500     INITIALIZE DP013-SRC-TASK-NUMBER                                     
041600                DP013-STACK.                                              
041700     SET DP013-LINK-RETURN           TO TRUE.                             
041800                                                                          
041900     EXEC CICS                                                            
042000         LINK                                                             
042100             PROGRAM   (DP013-NAME)                                       
042200             COMMAREA  (DP013-PARAMETERS)                                 
042300             LENGTH    (DP013-LENGTH)                                     
042400     END-EXEC.                                                            
042500*----------------------------------------------------------------*      25
042600*    ABEND PROCESSOR MODULE                                      *      25
042700*----------------------------------------------------------------*      25
042800                                                                        25
042900     COPY DPPD014.                                                      25
043000                                                                        25
