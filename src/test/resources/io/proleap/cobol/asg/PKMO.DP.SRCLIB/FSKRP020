      ***************************************************************** 00010002
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030002
       PROGRAM-ID.      FSKRP020.                                       00040054
       AUTHOR.          MARIA KLAMIK.                                   00050000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00060000
       DATE-WRITTEN.    01-22-2002.                                     00070054
                                                                        00080000
      ***************************************************************** 00090000
      *           FS ON-HAND ADJUSTMENT ERROR REPORT                  * 00100058
      ***************************************************************** 00110001
      *  THIS PROGRAM "GETS" MESSAGES OFF THE MQ SERIES DATA QUEUE    * 00120001
      *  THAT CONTAIN ERROR MESSAGES FROM THE ON-HAND ADJUSTMENT      * 00130058
      *  PROCESS IN FSKCS013 (CICS TRIGGERED TRANSACTION).            * 00140058
      *                                                               * 00150001
      *  EACH MESSAGE WILL RESULT IN A RECORD WRITTEN TO THE REPORT.  * 00160054
      *                                                               * 00170001
      ***************************************************************** 00180000
      *    WR/PITS#     DATE        NAME                              * 00190077
      *    --------   ----------   ---------------------------------  * 00200000
      *    WR26355    10/14/2004   MARIA KLAMIK                       * 00210080
      *                                                               * 00210177
      *    DESCRIPTION:                                               * 00210277
      *    STORE NUMBER EXPANSION PROJECT.  INCREASED LOCATION        * 00211077
      *    OUTPUT FIELDS.                                             * 00212077
      ***************************************************************** 00220000
       ENVIRONMENT DIVISION.                                            00230000
      ***************************************************************** 00240002
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
                                                                        00270000
       SOURCE-COMPUTER.       IBM-3090.                                 00280000
       OBJECT-COMPUTER.       IBM-3090.                                 00290000
                                                                        00300000
       INPUT-OUTPUT SECTION.                                            00310000
                                                                        00320000
       FILE-CONTROL.                                                    00330000
           SELECT CONTROL-CARD     ASSIGN TO INP01.                     00340001
           SELECT CONTROL-CARD-2   ASSIGN TO INP02.                     00350006
           SELECT ADJUSTMENT-ERROR-RPT   ASSIGN TO RPT01.               00360069
                                                                        00370000
                                                                        00380000
                                                                        00390000
      ***************************************************************** 00400002
       DATA DIVISION.                                                   00410000
      ***************************************************************** 00420002
       FILE SECTION.                                                    00430000
                                                                        00440000
       FD  CONTROL-CARD                                                 00450001
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           LABEL RECORDS ARE OMITTED.                                   00480000
       01  CONTROL-CARD-LINE                PIC  X(80).                 00490002
                                                                        00500000
       FD  CONTROL-CARD-2                                               00510006
           RECORDING MODE F                                             00520006
           BLOCK CONTAINS 0 RECORDS                                     00530006
           LABEL RECORDS ARE OMITTED.                                   00540006
       01  CONTROL-CARD-LINE-2              PIC  X(80).                 00550006
                                                                        00560006
       FD  ADJUSTMENT-ERROR-RPT                                         00570069
           RECORDING MODE F                                             00580000
           LABEL RECORDS ARE STANDARD                                   00590054
           RECORD CONTAINS 132 CHARACTERS                               00600054
           BLOCK CONTAINS  0 RECORDS                                    00610067
           DATA RECORD IS AE-ADJUSTMENT-ERROR-REC.                      00620054
       01  AE-ADJUSTMENT-ERROR-REC          PIC X(132).                 00630054
                                                                        00640000
      ***************************************************************** 00650002
       WORKING-STORAGE SECTION.                                         00660000
      ***************************************************************** 00670002
      *    API CONTROL BLOCKS                                           00680000
                                                                        00690000
       01  MQM-OBJECT-DESCRIPTOR.                                       00700000
           COPY CMQODV.                                                 00710000
       01  MQM-MESSAGE-DESCRIPTOR.                                      00720000
           COPY CMQMDV.                                                 00730000
       01  MQM-GET-MESSAGE-OPTIONS.                                     00740000
           COPY CMQGMOV.                                                00750000
       01  MQM-PUT-MESSAGE-OPTIONS.                                     00760000
           COPY CMQPMOV.                                                00770000
                                                                        00780000
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   00790000
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          00800000
                                                                        00810000
       01  MQM-CONSTANTS.                                               00820000
           COPY CMQV.                                                   00830000
      *                                                                 00840000
      ***************************************************************** 00850002
      *    CONTROL CARD                                                 00860000
      ***************************************************************** 00870002
       01  CC-CONTROL-CARD.                                             00880002
           05  CC-QMGR-NAME              PIC  X(48).                    00890002
           05  FILLER                    PIC  X(32).                    00900013
      *                                                                 00910006
      ***************************************************************** 00920006
      *    CONTROL CARD-2                                               00930006
      ***************************************************************** 00940006
       01  CC-CONTROL-CARD-2.                                           00950006
           05  CC-ADJ-Q-NAME             PIC  X(48).                    00960056
           05  FILLER                    PIC  X(32).                    00970006
      *                                                                 00980024
      ***************************************************************** 00990002
      *    PROGRAM ACCUMULATORS                                         01000002
      ***************************************************************** 01010002
       01  PA-PROGRAM-ACCUMULATORS.                                     01020002
           05  PA-Q-MESSAGES             PIC 9(07) VALUE 0.             01040024
           05  PA-ADJUSTMENTS            PIC 9(07) VALUE 0.             01050056
                                                                        01060000
      ***************************************************************** 01070002
      *    PROGRAM CONSTANTS                                            01080000
      ***************************************************************** 01090002
       01  PC-PROGRAM-CONSTANTS.                                        01100000
           05  PC-KOHLS-OP-CO            PIC X(02) VALUE '03'.          01110000
           05  PC-FSK020-JOB             PIC X(06) VALUE 'FSK020'.      01120054
           05  PC-PGM-NAME               PIC X(09) VALUE 'FSKRP020 '.   01130054
           05  PC-FIFTY-STARS            PIC X(50)  VALUE ALL '*'.      01140003
           05  PC-30-SPACES              PIC X(30) VALUE SPACES.        01150000
           05  PC-END-PGM-MSG            PIC X(100)                     01160015
                                         VALUE 'END RUN OF PROGRAM '.   01170015
           05  PC-LINE-LIMIT             PIC S9(02)    VALUE +55.       01171065
                                                                        01180000
      ***************************************************************** 01190002
      *    PROGRAM SWITCHES                                             01200000
      ***************************************************************** 01210002
       01  PS-PROGRAM-SWITCHES.                                         01220000
           05  PS-CONTROL-CARD-SW        PIC X(01) VALUE 'N'.           01230003
               88  PS-CONTROL-CARD-END             VALUE 'Y'.           01240003
               88  PS-CONTROL-CARD-START           VALUE 'N'.           01250003
           05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.           01260006
               88  PS-CONTROL-CARD-2-END           VALUE 'Y'.           01270006
               88  PS-CONTROL-CARD-2-START         VALUE 'N'.           01280006
           05  PS-MQGET-SUCCESSFUL-SW    PIC X(01) VALUE 'N'.           01290000
               88  PS-MQGET-SUCCESSFUL             VALUE 'Y'.           01300000
           05  PS-NO-MQ-MESSAGES-IND     PIC X(01) VALUE 'N'.           01310056
               88  PS-NO-MQ-MESSAGES               VALUE 'Y'.           01320056
           05  PS-END-OF-Q-IND           PIC X(01) VALUE 'N'.           01330056
               88  PS-END-OF-Q                     VALUE 'Y'.           01340056
                                                                        01350000
      ***************************************************************** 01360002
      *    PROGRAM VARIABLES                                            01370000
      ***************************************************************** 01380002
       01  PV-PROGRAM-VARIABLES.                                        01390000
           05  PV-ERROR-MESSAGE           PIC X(48)    VALUE SPACES.    01400065
           05  PV-ERROR-LINE-CNT          PIC S9(02)   VALUE +95.       01420065
           05  PV-ERROR-PAGE-NMBR         PIC S9(05)   VALUE ZERO.      01422065
           05  PV-FR-DEPT-NBR-FMT.                                      01422170
               10  FILLER            PIC X(01)     VALUE SPACES.        01422270
               10  PV-FR-DEPT-NBR    PIC X(03)     VALUE SPACES.        01422370
           05  PV-FR-MAJ-CL-NBR-FMT.                                    01422470
               10  FILLER            PIC X(02)     VALUE SPACES.        01422772
               10  PV-FR-MAJ-CL-NBR  PIC X(02)     VALUE SPACES.        01422872
           05  PV-FR-SUB-CL-NBR-FMT.                                    01422970
               10  FILLER            PIC X(02)     VALUE SPACES.        01424072
               10  PV-FR-SUB-CL-NBR  PIC X(02)     VALUE SPACES.        01425072
           05  PV-STR-NBR-FMT             PIC ZZZZZ    VALUE ZERO.      01426079
                                                                        01430064
           05  SD-SYSTEM-DATE.                                          01440064
               10  SD-SYSTEM-YEAR         PIC 9(02)    VALUE ZERO.      01450065
               10  SD-SYSTEM-MONTH        PIC 9(02)    VALUE ZERO.      01460065
               10  SD-SYSTEM-DAY          PIC 9(02)    VALUE ZERO.      01470065
                                                                        01480064
           05  ST-SYSTEM-TIME.                                          01490064
               10  ST-SYSTEM-TIME-FORMAT.                               01500064
                   15  ST-SYSTEM-HOUR     PIC 9(02)    VALUE ZERO.      01510065
                   15  ST-SYSTEM-MINUTE   PIC 9(02)    VALUE ZERO.      01520065
                   15  ST-SYSTEM-SECOND   PIC 9(02)    VALUE ZERO.      01530065
               10  ST-SYSTEM-TIME-NUM  REDEFINES                        01540064
                                        ST-SYSTEM-TIME-FORMAT           01550064
                                          PIC 9(06).                    01560064
               10  FILLER                 PIC X(4)      VALUE SPACES.   01570064
                                                                        01580000
       01  PV-MQ-VARIABLES.                                             01590000
           05  PV-QM-NAME              PIC X(48).                       01600000
           05  PV-PARAGRAPH            PIC X(50).                       01610000
           05  PV-MSGBUFFER            PIC X(200) VALUE SPACES.         01620064
           05  PV-MSGLENGTH            PIC S9(9)  BINARY VALUE +200.    01630054
           05  PV-DATA-LENGTH          PIC S9(9)  BINARY.               01640000
           05  PV-SELECT-COUNT         PIC S9(9)  BINARY VALUE 1.       01650000
           05  PV-SELECTORS.                                            01660006
               10  PV-Q-DEPTH          PIC S9(9)  BINARY VALUE 0.       01670006
           05  PV-INTATTR-COUNT        PIC S9(9)  BINARY VALUE 1.       01680006
           05  PV-INTATTRS.                                             01690006
               10  PV-Q-DEPTH-ATTR     PIC S9(9)  BINARY VALUE 0.       01700006
           05  PV-CHARATTR-LENGTH      PIC S9(9)  BINARY VALUE 0.       01710006
           05  PV-CHARATTRS            PIC X(48).                       01720006
           05  PV-HCONN                PIC S9(9)  BINARY VALUE 0.       01730000
           05  PV-GQ-HANDLE            PIC S9(9)  BINARY VALUE 0.       01740000
           05  PV-OPEN-OPTIONS         PIC S9(9)  BINARY.               01750000
           05  PV-COMPLETION-CODE      PIC S9(9)  BINARY.               01760000
           05  PV-REASON               PIC S9(9)  BINARY.               01770026
           05  PV-CON-REASON           PIC S9(9)  BINARY.               01780000
           05  PV-Q-DEPTH-NBR          PIC S9(9)  COMP-3 VALUE 0.       01790027
                                                                        01800000
       01  PH-PROGRAM-HOLD-VARIABLES.                                   01810000
           05  PH-COMPLETION-CODE          PIC  S9(09) BINARY.          01820000
           05  PH-REASON                   PIC  S9(09) BINARY.          01830000
                                                                        01840000
      ***************************************************************** 01850007
      *    MQ WORKING STORAGE VARIABLES                                 01860007
      ***************************************************************** 01870007
       01  WS-MQCONN                 PIC X(8)  VALUE 'CSQBCONN'.        01880007
       01  WS-MQOPEN                 PIC X(8)  VALUE 'CSQBOPEN'.        01890007
       01  WS-MQINQ                  PIC X(8)  VALUE 'CSQBINQ'.         01900007
       01  WS-MQGET                  PIC X(8)  VALUE 'CSQBGET'.         01910007
       01  WS-MQPUT1                 PIC X(8)  VALUE 'CSQBPUT1'.        01920007
       01  WS-MQBACK                 PIC X(8)  VALUE 'CSQBBACK'.        01930007
       01  WS-MQCMIT                 PIC X(8)  VALUE 'CSQBCOMM'.        01940007
       01  WS-MQCLOSE                PIC X(8)  VALUE 'CSQBCLOS'.        01950007
       01  WS-MQDISC                 PIC X(8)  VALUE 'CSQBDISC'.        01960007
                                                                        01970007
      ***************************************************************** 01980000
      *  ERROR MQ COPYBOOK - INPUT                                      01990054
      ***************************************************************** 02000000
           COPY FSRD013.                                                02010054
                                                                        02020064
      ***************************************************************** 02030064
      *  STANDARD HEADING FOR 132 COLUMNS                               02040064
      ***************************************************************** 02050064
           COPY DPWS132O.                                               02060064
                                                                        02070064
       01  HL1-HEADING-LINE.                                            02080059
           05  FILLER                PIC X(51)     VALUE SPACES.        02090059
           05  FILLER                PIC X(33)     VALUE                02100059
               'ON-LINE ON-HAND ADJUSTMENT ERRORS'.                     02110059
           05  FILLER                PIC X(48)     VALUE SPACES.        02120059
       01  HL2-HEADING-LINE.                                            02130059
           05  FILLER                PIC X(03)     VALUE SPACES.        02140060
           05  FILLER                PIC X(06)     VALUE                02150060
               'ADJ-ID'.                                                02160060
           05  FILLER                PIC X(07)     VALUE SPACES.        02170060
           05  FILLER                PIC X(03)     VALUE                02180060
               'DPT'.                                                   02190060
           05  FILLER                PIC X(05)     VALUE SPACES.        02200062
           05  FILLER                PIC X(02)     VALUE                02210060
               'CL'.                                                    02220060
           05  FILLER                PIC X(06)     VALUE SPACES.        02230060
           05  FILLER                PIC X(02)     VALUE                02240060
               'SC'.                                                    02250060
           05  FILLER                PIC X(06)     VALUE SPACES.        02260060
           05  FILLER                PIC X(03)     VALUE                02270060
               'SKU'.                                                   02280060
           05  FILLER                PIC X(07)     VALUE SPACES.        02290060
           05  FILLER                PIC X(05)     VALUE                02300060
               'STORE'.                                                 02310060
           05  FILLER                PIC X(03)     VALUE SPACES.        02320060
           05  FILLER                PIC X(08)     VALUE                02330060
               'ADJ-TYPE'.                                              02340060
           05  FILLER                PIC X(04)     VALUE SPACES.        02350060
           05  FILLER                PIC X(07)     VALUE                02360060
               'ADJ-ACT'.                                               02370060
           05  FILLER                PIC X(02)     VALUE SPACES.        02380062
           05  FILLER                PIC X(10)     VALUE                02390062
               'OH-ADJ-QTY'.                                            02400062
           05  FILLER                PIC X(03)     VALUE SPACES.        02410062
           05  FILLER                PIC X(10)     VALUE                02420062
               '**********'.                                            02430062
           05  FILLER                PIC X(03)     VALUE SPACES.        02440062
           05  FILLER                PIC X(13)     VALUE                02450062
               'ERROR MESSAGE'.                                         02460062
           05  FILLER                PIC X(03)     VALUE SPACES.        02470062
           05  FILLER                PIC X(10)     VALUE                02480062
               '**********'.                                            02490062
           05  FILLER                PIC X(01)     VALUE SPACES.        02500062
                                                                        02510063
       01  DL1-DETAIL-LINE.                                             02520063
           05  FILLER                PIC X(01)     VALUE SPACES.        02530063
           05  DL1-MDSE-ADJ-ID       PIC ZZZZZZZZZZ VALUE ZERO.         02540063
           05  FILLER                PIC X(04)     VALUE SPACES.        02550063
           05  DL1-FR-DEPT-NBR-FMT.                                     02560063
               10  FILLER            PIC X(01)     VALUE SPACES.        02570063
               10  DL1-FR-DEPT-NBR   PIC X(03)     VALUE SPACES.        02580063
           05  FILLER                PIC X(03)     VALUE SPACES.        02590063
           05  DL1-FR-MAJ-CL-NBR-FMT.                                   02600063
               10  FILLER            PIC X(02)     VALUE SPACES.        02610063
               10  DL1-FR-MAJ-CL-NBR PIC X(02)     VALUE SPACES.        02620063
           05  FILLER                PIC X(04)     VALUE SPACES.        02630063
           05  DL1-FR-SUB-CL-NBR-FMT.                                   02640063
               10  FILLER            PIC X(02)     VALUE SPACES.        02650063
               10  DL1-FR-SUB-CL-NBR PIC X(02)     VALUE SPACES.        02660063
           05  FILLER                PIC X(04)     VALUE SPACES.        02670063
           05  DL1-FR-SKU-NBR        PIC X(08)     VALUE SPACES.        02680063
MK         05  FILLER                PIC X(04)     VALUE SPACES.        02690078
MK         05  DL1-STR-NBR           PIC X(05)     VALUE SPACES.        02710079
MK         05  FILLER                PIC X(06)     VALUE SPACES.        02730078
           05  DL1-ADJ-TYP-CDE       PIC X(02)     VALUE SPACES.        02740063
           05  FILLER                PIC X(10)     VALUE SPACES.        02750063
           05  DL1-ADJ-ACTN-CDE      PIC X(01)     VALUE SPACES.        02760063
           05  FILLER                PIC X(05)     VALUE SPACES.        02770063
           05  DL1-OH-UNT-ADJ-QTY    PIC ZZZZZZZZZZ VALUE ZERO.         02780063
           05  FILLER                PIC X(03)     VALUE SPACES.        02790063
           05  DL1-ERROR-DESCRIPTION PIC X(40)     VALUE SPACES.        02800063
                                                                        02810063
      *                                                                 02820059
      ***************************************************************** 02830002
      *    ABEND CODES THE ARE APPLICATION DEFINED                      02840000
      ***************************************************************** 02850002
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         02860000
                                                    COMP SYNC.          02870000
           88  AC-USER-ERROR                        VALUE +4003.        02880021
           88  AC-MQ-ERROR                          VALUE +4020.        02890000
                                                                        02910000
      ***************************************************************** 02920002
       PROCEDURE DIVISION.                                              02930000
      ***************************************************************** 02940002
       0000-MAINLINE.                                                   02950002
                                                                        02960000
           PERFORM 1000-INITIALIZE.                                     02970000
           PERFORM 1050-PREPARE-HEADING-DATA.                           02980064
                                                                        02990000
           IF PS-CONTROL-CARD-END                                       03000003
             OR PS-CONTROL-CARD-2-END                                   03010040
               CLOSE ADJUSTMENT-ERROR-RPT                               03020075
               SET AC-USER-ERROR TO TRUE                                03030040
               CALL 'ILBOABN0' USING ABEND-CODE                         03040040
           ELSE                                                         03050000
               IF PS-NO-MQ-MESSAGES                                     03060056
                  DISPLAY PC-FIFTY-STARS                                03060176
                  DISPLAY 'NO ADJUSTMENT ERRORS-NO REPORT PRODUCED'     03061076
                  DISPLAY PC-FIFTY-STARS                                03062076
               ELSE                                                     03100056
                  PERFORM 2000-PROCESS-MQ-MESSAGES                      03110056
                    UNTIL(PS-END-OF-Q)                                  03120056
                      OR (NOT PS-MQGET-SUCCESSFUL)                      03130056
           END-IF.                                                      03140000
                                                                        03150003
           PERFORM 3000-EOJ-PROCESSING.                                 03160037
                                                                        03170037
           STOP RUN.                                                    03180000
                                                                        03190000
      ***************************************************************** 03200003
      *  INITIALIZATION ROUTINE.                                        03210000
      ***************************************************************** 03220003
       1000-INITIALIZE.                                                 03230000
                                                                        03240000
           DISPLAY PC-FIFTY-STARS.                                      03250038
           DISPLAY '***** START OF PROGRAM FSKRP020 *****'.             03260054
                                                                        03270038
           SET PS-CONTROL-CARD-START TO TRUE.                           03280003
           SET PS-CONTROL-CARD-2-START TO TRUE.                         03290006
           INITIALIZE PS-END-OF-Q-IND                                   03300064
                      DL1-DETAIL-LINE.                                  03310064
                                                                        03320024
           OPEN INPUT CONTROL-CARD                                      03330006
                      CONTROL-CARD-2.                                   03340006
           OPEN OUTPUT ADJUSTMENT-ERROR-RPT.                            03350069
                                                                        03360000
      *  READ THE FIRST CONTROL CARD FOR THE QUEUE MGR NAME.            03370013
                                                                        03380006
           PERFORM 8000-READ-CONTROL-CARD.                              03390003
           IF PS-CONTROL-CARD-END                                       03400003
               DISPLAY PC-FIFTY-STARS                                   03410038
               DISPLAY PC-PGM-NAME                                      03420000
                    'CONTROL CARD MISSING - FOR QMGR NAME'              03430013
           ELSE                                                         03440030
                IF CC-QMGR-NAME > SPACES                                03450033
                    DISPLAY PC-FIFTY-STARS                              03460033
                    DISPLAY PC-PGM-NAME                                 03470033
                            '- CONTROL CARD FOR QMGR: '                 03480033
                            CC-CONTROL-CARD                             03490033
                ELSE                                                    03500033
                    SET PS-CONTROL-CARD-END TO TRUE                     03510035
                    DISPLAY PC-PGM-NAME                                 03520033
                         'INVALID QMGR NAME '                           03530033
                END-IF                                                  03540033
           END-IF.                                                      03550030
                                                                        03560006
           CLOSE CONTROL-CARD.                                          03570030
                                                                        03580006
      *  READ THE SECOND CONTROL CARD FOR THE QUEUE NAME                03590006
                                                                        03600006
            PERFORM 8010-READ-CONTROL-CARD-2.                           03610006
            IF PS-CONTROL-CARD-2-END                                    03620006
                DISPLAY PC-PGM-NAME                                     03630006
                   'CONTROL CARD MISSING - ADJUST QUEUE NAME '          03640056
            ELSE                                                        03650006
                IF CC-ADJ-Q-NAME > SPACES                               03660056
                    DISPLAY PC-FIFTY-STARS                              03670033
                    DISPLAY PC-PGM-NAME                                 03680033
                            '- CONTROL CARD FOR Q-NAME: '               03690033
                            CC-CONTROL-CARD-2                           03700033
                    DISPLAY PC-FIFTY-STARS                              03710033
                ELSE                                                    03720033
                    SET PS-CONTROL-CARD-2-END TO TRUE                   03730035
                    DISPLAY PC-PGM-NAME                                 03740033
                       'INVALID ADJUST QUEUE NAME'                      03750056
                END-IF                                                  03760033
           END-IF.                                                      03770006
                                                                        03780006
           CLOSE CONTROL-CARD-2.                                        03790006
                                                                        03800006
           IF PS-CONTROL-CARD-END                                       03810006
              OR PS-CONTROL-CARD-2-END                                  03820035
                CONTINUE                                                03830040
            ELSE                                                        03840006
                PERFORM 1100-MQ-CONNECT                                 03850006
                PERFORM 1200-MQ-INQUIRY-OPEN                            03860009
                PERFORM 1300-MQ-INQUIRY-MQINQ                           03870009
                PERFORM 1400-MQ-INQUIRY-CLOSE                           03880009
                PERFORM 1500-MQ-ADJ-INPUT-OPEN                          03890056
                PERFORM 8100-MQGET-MSG                                  03900008
            END-IF.                                                     03910006
                                                                        03920000
      ***************************************************************** 03930003
      *  THIS MODULE PAREPARES THE HEADING DATA THAT WILL BE PRINTED    03940064
      *  ON THE REPORT.                                                 03950064
      ***************************************************************** 03960003
       1050-PREPARE-HEADING-DATA.                                       03970064
                                                                        03980003
           ACCEPT SD-SYSTEM-DATE FROM DATE.                             03990064
           MOVE SD-SYSTEM-MONTH        TO DP132O-RUN-MONTH.             04000064
           MOVE SD-SYSTEM-DAY          TO DP132O-RUN-DAY.               04010064
           MOVE SD-SYSTEM-YEAR         TO DP132O-RUN-YEAR.              04020064
                                                                        04030064
           ACCEPT ST-SYSTEM-TIME FROM TIME.                             04040064
           MOVE ST-SYSTEM-HOUR         TO DP132O-RUN-HOUR.              04050064
           MOVE ST-SYSTEM-MINUTE       TO DP132O-RUN-MINUTE.            04060064
                                                                        04070064
      ***************************************************************** 04080064
      *  MQ - CONNECT TO THE QUEUE MANAGER.                             04090064
      ***************************************************************** 04100064
       1100-MQ-CONNECT.                                                 04110064
                                                                        04120064
      *    CONNECT TO THE QUEUE MANAGER                                 04130000
                                                                        04140000
           MOVE CC-QMGR-NAME TO PV-QM-NAME.                             04150000
           CALL WS-MQCONN USING PV-QM-NAME                              04160000
                                PV-HCONN                                04170003
                                PV-COMPLETION-CODE                      04180003
                                PV-REASON.                              04190003
                                                                        04200000
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              04210000
      *    AND EXIT                                                     04220000
                                                                        04230000
           MOVE PV-REASON TO PV-CON-REASON.                             04240000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04250000
              MOVE 'MQCONN ERROR'   TO PV-ERROR-MESSAGE                 04260000
              MOVE '1100-MQ-CONNECT' TO  PV-PARAGRAPH                   04270004
              PERFORM 9000-DISPLAY-MQ-ERROR                             04280000
           ELSE                                                         04290003
              DISPLAY 'MQCONN SUCCESSFUL TO ', PV-QM-NAME               04300005
           END-IF.                                                      04310000
                                                                        04320000
      ***************************************************************** 04330004
      *  MQ - OPEN THE ADJUSTMENT QUEUE FOR INQUIRY.                    04340056
      ***************************************************************** 04350005
       1200-MQ-INQUIRY-OPEN.                                            04360005
                                                                        04370004
      *    OPEN ADJUSTMENT QUEUE FOR INQUIRY OF QUEUE DEPTH             04380056
                                                                        04390000
           COMPUTE PV-OPEN-OPTIONS = MQOO-INQUIRE +                     04400000
                                  MQOO-FAIL-IF-QUIESCING.               04410000
           MOVE MQOT-Q            TO MQOD-OBJECTTYPE.                   04420004
           MOVE PV-QM-NAME        TO MQOD-OBJECTQMGRNAME.               04430000
           MOVE CC-ADJ-Q-NAME     TO MQOD-OBJECTNAME.                   04440056
                                                                        04450000
           CALL WS-MQOPEN USING PV-HCONN                                04460000
                                MQM-OBJECT-DESCRIPTOR                   04470024
                                PV-OPEN-OPTIONS                         04480024
                                PV-GQ-HANDLE                            04490024
                                PV-COMPLETION-CODE                      04500024
                                PV-REASON.                              04510024
                                                                        04520000
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    04530004
      *    AND EXIT.                                                    04540000
                                                                        04550000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04560000
              MOVE 'MQOPEN ERROR'   TO PV-ERROR-MESSAGE                 04570000
              MOVE '1200-MQ-INQUIRY-OPEN' TO  PV-PARAGRAPH              04580005
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             04590041
              MOVE PV-REASON          TO PH-REASON                      04600041
              PERFORM 1700-DISCONNECT-QMGR                              04610000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             04620041
              MOVE PH-REASON          TO PV-REASON                      04630041
              PERFORM 9000-DISPLAY-MQ-ERROR                             04640000
           ELSE                                                         04650005
              DISPLAY 'MQOPEN SUCCESSFUL - INQUIRE OF Q DEPTH'          04660005
           END-IF.                                                      04670000
                                                                        04680000
      ***************************************************************** 04690005
      *  MQ - INQUIRE ON THE QUEUE DEPTH.                               04700005
      ***************************************************************** 04710005
       1300-MQ-INQUIRY-MQINQ.                                           04720005
                                                                        04730004
      *    INQUIRE ON ADJUSTMENT QUEUE DEPTH                            04740056
                                                                        04750005
           MOVE MQIA-CURRENT-Q-DEPTH TO PV-Q-DEPTH.                     04760005
                                                                        04770004
           CALL WS-MQINQ USING                                          04780029
                        PV-HCONN,                                       04790005
                        PV-GQ-HANDLE,                                   04800005
                        PV-SELECT-COUNT,                                04810026
                        PV-SELECTORS,                                   04820006
                        PV-INTATTR-COUNT,                               04830006
                        PV-INTATTRS,                                    04840006
                        PV-CHARATTR-LENGTH,                             04850006
                        PV-CHARATTRS,                                   04860006
                        PV-COMPLETION-CODE,                             04870005
                        PV-REASON.                                      04880026
                                                                        04890006
      *    IF INQUIRE FAILED THEN DISPLAY ERROR MESSAGE                 04900006
      *    AND EXIT.                                                    04910006
                                                                        04920004
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04930000
              MOVE 'MQINQ ERROR'   TO PV-ERROR-MESSAGE                  04940000
              MOVE '1300-MQ-INQUIRY-MQINQ' TO PV-PARAGRAPH              04950005
              PERFORM 9000-DISPLAY-MQ-ERROR                             04960000
           ELSE                                                         04970000
              DISPLAY 'MQINQ SUCCESSFUL, QUEUE DEPTH: ', PV-Q-DEPTH-ATTR04980006
              MOVE PV-Q-DEPTH-ATTR TO PV-Q-DEPTH-NBR                    04990027
              IF PV-Q-DEPTH-NBR > ZERO                                  05000056
                 CONTINUE                                               05010056
              ELSE                                                      05020056
                 SET PS-NO-MQ-MESSAGES TO TRUE                          05030056
              END-IF                                                    05040056
           END-IF.                                                      05050000
                                                                        05060000
      ***************************************************************** 05070005
      *  MQ - CLOSE THE ADJUSTMENT QUEUE THAT WAS USED FOR INQUIRY      05080056
      ***************************************************************** 05090005
       1400-MQ-INQUIRY-CLOSE.                                           05100006
                                                                        05110006
      *    CLOSE ADJUSTMENT QUEUE FOR INQUIRY ON QUEUE DEPTH            05120056
                                                                        05130006
           CALL WS-MQCLOSE USING PV-HCONN                               05140000
                                 PV-GQ-HANDLE                           05150006
                                 MQCO-NONE                              05160006
                                 PV-COMPLETION-CODE                     05170006
                                 PV-REASON.                             05180006
                                                                        05190006
      *    IF CLOSE FAILED THEN DISPLAY ERROR MESSAGE                   05200006
      *    AND EXIT.                                                    05210006
                                                                        05220006
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05230000
              MOVE 'MQCLOSE ERROR'  TO PV-ERROR-MESSAGE                 05240000
              MOVE '1400-MQ-INQUIRY-CLOSE' TO PV-PARAGRAPH              05250006
              PERFORM 9000-DISPLAY-MQ-ERROR                             05260000
           ELSE                                                         05270000
              DISPLAY 'MQCLOSE SUCCESSFUL - INQUIRE OF Q DEPTH'         05280006
           END-IF.                                                      05290000
                                                                        05300000
      ***************************************************************** 05310006
      *  MQ - OPEN THE ADJUSTMENT QUEUE FOR INPUT.                      05320056
      ***************************************************************** 05330006
       1500-MQ-ADJ-INPUT-OPEN.                                          05340056
                                                                        05350006
      *    OPEN THE ADJUSTMENT QUEUE FOR INPUT                          05360056
                                                                        05370000
           COMPUTE PV-OPEN-OPTIONS = MQOO-INPUT-SHARED +                05380000
                                     MQOO-FAIL-IF-QUIESCING.            05390000
           MOVE MQOT-Q                   TO MQOD-OBJECTTYPE.            05400000
           MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.        05410007
           MOVE CC-ADJ-Q-NAME            TO MQOD-OBJECTNAME.            05420056
      *                                                                 05430000
           CALL WS-MQOPEN USING PV-HCONN                                05440000
                                MQM-OBJECT-DESCRIPTOR                   05450024
                                PV-OPEN-OPTIONS                         05460024
                                PV-GQ-HANDLE                            05470024
                                PV-COMPLETION-CODE                      05480024
                                PV-REASON.                              05490024
      *                                                                 05500000
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    05510000
      *    AND EXIT.                                                    05520000
      *                                                                 05530000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05540000
              MOVE 'MQOPEN ERROR'   TO PV-ERROR-MESSAGE                 05550000
              MOVE '1500-MQ-ADJ-INPUT-OPEN' TO PV-PARAGRAPH             05560056
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             05570041
              MOVE PV-REASON          TO PH-REASON                      05580041
              PERFORM 1700-DISCONNECT-QMGR                              05590041
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             05600041
              MOVE PH-REASON          TO PV-REASON                      05610041
              PERFORM 9000-DISPLAY-MQ-ERROR                             05620000
           ELSE                                                         05630007
              DISPLAY 'MQOPEN SUCCESSFUL - ADJUSTMENT INPUT'            05640056
           END-IF.                                                      05650000
                                                                        05660000
      ***************************************************************** 05670007
      *  MQ - CLOSE THE ADJUSTMENT QUEUE FOR INPUT.                     05680056
      ***************************************************************** 05690007
       1600-MQ-ADJ-INPUT-CLOSE.                                         05700056
                                                                        05710000
           CALL WS-MQCLOSE USING PV-HCONN                               05720000
                                 PV-GQ-HANDLE                           05730000
                                 MQCO-NONE                              05740000
                                 PV-COMPLETION-CODE                     05750000
                                 PV-REASON.                             05760000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05770000
              MOVE 'MQCLOSE ERROR' TO PV-ERROR-MESSAGE                  05780000
              MOVE '1600-MQ-ADJ-INPUT-CLOSE' TO PV-PARAGRAPH            05790056
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             05800041
              MOVE PV-REASON          TO PH-REASON                      05810041
              PERFORM 1700-DISCONNECT-QMGR                              05820041
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             05830041
              MOVE PH-REASON          TO PV-REASON                      05840041
              PERFORM 9000-DISPLAY-MQ-ERROR                             05850000
           ELSE                                                         05860000
              DISPLAY 'MQCLOSE SUCCESSFUL - ADJUSTMENT INPUT'           05870056
           END-IF.                                                      05880000
                                                                        05890000
                                                                        05900000
      ***************************************************************** 05910005
      *  DISCONNECT QUEUE MANAGER                                       05920000
      ***************************************************************** 05930005
       1700-DISCONNECT-QMGR.                                            05940000
                                                                        05950000
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED      05960000
              CALL WS-MQDISC USING PV-HCONN                             05970000
                                   PV-COMPLETION-CODE                   05980024
                                   PV-REASON                            05990024
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                06000000
                 MOVE 'MQDISC ERROR'   TO PV-ERROR-MESSAGE              06010000
                 MOVE '1700-DISCONNECT-QMGR' TO PV-PARAGRAPH            06020005
                 PERFORM 9000-DISPLAY-MQ-ERROR                          06030000
              ELSE                                                      06040000
                 DISPLAY 'MQDISC SUCCESSFUL'                            06050000
              END-IF                                                    06060000
           END-IF.                                                      06070000
                                                                        06080000
      ***************************************************************** 06090008
      *  PROCESS MQ MESSAGES.                                           06100025
      ***************************************************************** 06110008
       2000-PROCESS-MQ-MESSAGES.                                        06120022
                                                                        06130065
           MOVE FS013-MDSE-ADJ-ID          TO DL1-MDSE-ADJ-ID.          06140065
                                                                        06141071
           MOVE FS013-FR-DEPT-NBR          TO PV-FR-DEPT-NBR-FMT.       06150071
           MOVE PV-FR-DEPT-NBR             TO DL1-FR-DEPT-NBR.          06150171
                                                                        06151071
           MOVE FS013-FR-MAJ-CL-NBR        TO PV-FR-MAJ-CL-NBR-FMT.     06160071
           MOVE PV-FR-MAJ-CL-NBR           TO DL1-FR-MAJ-CL-NBR.        06160172
                                                                        06161071
           MOVE FS013-FR-SUB-CL-NBR        TO PV-FR-SUB-CL-NBR-FMT.     06170071
           MOVE PV-FR-SUB-CL-NBR           TO DL1-FR-SUB-CL-NBR.        06170172
                                                                        06171071
           MOVE FS013-FR-SKU-NBR           TO DL1-FR-SKU-NBR.           06180065
                                                                        06181071
           IF FS013-STR-NBR > ZERO                                      06190065
MK            MOVE FS013-STR-NBR           TO PV-STR-NBR-FMT            06201079
MK            MOVE PV-STR-NBR-FMT          TO DL1-STR-NBR               06202079
           ELSE                                                         06210065
              IF FS013-WHSE-NBR > ZERO                                  06220065
MK               MOVE FS013-WHSE-NBR       TO PV-STR-NBR-FMT            06231079
MK               MOVE PV-STR-NBR-FMT          TO DL1-STR-NBR            06232079
              ELSE                                                      06240065
                 MOVE ' ERR'               TO DL1-STR-NBR               06251077
              END-IF                                                    06260065
           END-IF.                                                      06270065
                                                                        06271071
           MOVE FS013-ADJ-TYP-CDE          TO DL1-ADJ-TYP-CDE.          06280065
           MOVE FS013-ADJ-ACTN-CDE         TO DL1-ADJ-ACTN-CDE.         06281065
           MOVE FS013-OH-UNT-ADJ-QTY       TO DL1-OH-UNT-ADJ-QTY.       06282065
           MOVE FS013-ERROR-DESCRIPTION    TO DL1-ERROR-DESCRIPTION.    06283065
                                                                        06290008
           PERFORM 2200-PRINT-ADJ-ERROR-LINE.                           06300066
                                                                        06350865
           PERFORM 8500-COMMIT-MQ.                                      06351065
                                                                        06380024
           PERFORM 8100-MQGET-MSG.                                      06390065
                                                                        06400050
      ***************************************************************** 06410050
      *  THIS PARAGRAPH WILL PRINT THE ADJUSTMENT ERROR LINE.           06420065
      ***************************************************************** 06440050
       2200-PRINT-ADJ-ERROR-LINE.                                       06450065
                                                                        06451065
           IF PV-ERROR-LINE-CNT > PC-LINE-LIMIT                         06452065
              PERFORM 2400-PRINT-ADJ-ERROR-HEADINGS                     06453065
              PERFORM 5000-WRITE-ADJ-RECORD                             06453173
               ADD +2 TO PV-ERROR-LINE-CNT                              06453273
           ELSE                                                         06454065
              PERFORM 5000-WRITE-ADJ-RECORD                             06460065
               ADD +2 TO PV-ERROR-LINE-CNT                              06460165
           END-IF.                                                      06460265
                                                                        06461065
      ***************************************************************** 06461265
      *  THIS PARAGRAPH WILL PRINT THE ADJUSTMENT ERROR LINE.           06461365
      ***************************************************************** 06461465
       2400-PRINT-ADJ-ERROR-HEADINGS.                                   06461565
                                                                        06461665
           MOVE +6  TO PV-ERROR-LINE-CNT.                               06461765
           ADD +1   TO PV-ERROR-PAGE-NMBR.                              06461865
           MOVE 1   TO DP132O-REPORT-NUMBER.                            06461965
           MOVE PV-ERROR-PAGE-NMBR TO DP132O-PAGE-NUMBER.               06462065
           MOVE 'FSKRP020' TO DP132O-PROGRAM-NAME.                      06462165
                                                                        06462265
           PERFORM 5100-WRITE-ADJ-HEADINGS.                             06462365
                                                                        06462865
      ***************************************************************** 06462965
      *  CLOSE FILES AND DISPLAY PROGRAM TOTALS.                        06463065
      *  DISCONNECT FROM QUEUE MANAGER.                                 06464065
      ***************************************************************** 06465065
       3000-EOJ-PROCESSING.                                             06466065
                                                                        06467065
           DISPLAY PC-FIFTY-STARS.                                      06471076
           DISPLAY 'END OF JOB COUNTERS'.                               06480040
           DISPLAY PC-FIFTY-STARS.                                      06490176
           DISPLAY 'NUMBER OF QUEUE MESSAGES: ', PA-Q-MESSAGES.         06490266
           DISPLAY 'NUMBER OF ADJUSTMENT ERRORS WRITTEN: '              06491066
                                                 PA-ADJUSTMENTS.        06492066
           DISPLAY PC-FIFTY-STARS.                                      06521076
                                                                        06530040
           PERFORM 1600-MQ-ADJ-INPUT-CLOSE.                             06540056
           PERFORM 1700-DISCONNECT-QMGR.                                06550040
                                                                        06560008
           CLOSE ADJUSTMENT-ERROR-RPT.                                  06570069
                                                                        06580000
      ***************************************************************** 06590008
      *  WRITE THE ADJUSTMENT RECORD.                                   06600056
      ***************************************************************** 06610008
       5000-WRITE-ADJ-RECORD.                                           06620056
                                                                        06630008
           WRITE AE-ADJUSTMENT-ERROR-REC FROM DL1-DETAIL-LINE           06640065
              AFTER ADVANCING 2 LINES.                                  06641065
                                                                        06641165
           INITIALIZE DL1-DETAIL-LINE.                                  06641265
                                                                        06641365
           ADD +1 TO PA-ADJUSTMENTS.                                    06642065
                                                                        06660008
      ***************************************************************** 06661065
      *  WRITE THE ADJUSTMENT RECORD HEADINGS.                          06662065
      ***************************************************************** 06663065
       5100-WRITE-ADJ-HEADINGS.                                         06664065
                                                                        06665065
           WRITE AE-ADJUSTMENT-ERROR-REC FROM                           06666065
              DP132O-STANDRD-HEADER-132-COLS                            06666165
                AFTER ADVANCING PAGE.                                   06667065
                                                                        06667165
           MOVE SPACES TO DP132O-LASER-NEW-PAGE-INDICATR.               06667265
                                                                        06667365
           WRITE AE-ADJUSTMENT-ERROR-REC FROM                           06667465
              HL1-HEADING-LINE                                          06667565
                AFTER ADVANCING 1 LINE.                                 06667665
                                                                        06668065
           MOVE SPACES TO AE-ADJUSTMENT-ERROR-REC.                      06669165
           WRITE AE-ADJUSTMENT-ERROR-REC                                06669365
              AFTER ADVANCING 2 LINES.                                  06669465
                                                                        06669565
           WRITE AE-ADJUSTMENT-ERROR-REC FROM HL2-HEADING-LINE          06669665
              AFTER ADVANCING 1 LINE.                                   06669765
                                                                        06669865
           MOVE SPACES TO AE-ADJUSTMENT-ERROR-REC.                      06669965
           WRITE AE-ADJUSTMENT-ERROR-REC                                06670065
              AFTER ADVANCING 1 LINES.                                  06670165
                                                                        06670265
      ***************************************************************** 06671003
      * READ CONTROL CARD TO OBTAIN THE INPUT QUEUE MGR NAME.           06680013
      ***************************************************************** 06690003
       8000-READ-CONTROL-CARD.                                          06700003
                                                                        06710024
            READ CONTROL-CARD INTO CC-CONTROL-CARD                      06720003
                AT END                                                  06730000
                    SET PS-CONTROL-CARD-END TO TRUE.                    06740024
                                                                        06750000
      ***************************************************************** 06760006
      * READ SECOND CONTROL CARD TO OBTAIN THE INPUT QUEUE NAME.        06770006
      ***************************************************************** 06780006
       8010-READ-CONTROL-CARD-2.                                        06790026
                                                                        06800024
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                  06810006
                AT END                                                  06820006
                    SET PS-CONTROL-CARD-2-END TO TRUE.                  06830067
                                                                        06840006
      ***************************************************************** 06850003
      * GET ADJUSTMENT MESSAGE FROM MQ SERIES.                          06860056
      ***************************************************************** 06870003
       8100-MQGET-MSG.                                                  06880008
                                                                        06890000
           INITIALIZE PS-MQGET-SUCCESSFUL-SW                            06900015
                      PS-END-OF-Q-IND                                   06910064
                      PV-MSGBUFFER                                      06920024
                      FS013-FS-ERROR-RECORD.                            06930057
                                                                        06940024
           MOVE MQMI-NONE              TO MQMD-MSGID.                   06950000
           MOVE MQCI-NONE              TO MQMD-CORRELID.                06960000
           MOVE CC-ADJ-Q-NAME          TO MQOD-OBJECTNAME.              06970056
           COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                   06980000
                                    MQGMO-FAIL-IF-QUIESCING.            06990015
                                                                        07000000
           CALL WS-MQGET USING PV-HCONN                                 07010000
                               PV-GQ-HANDLE                             07020000
                               MQM-MESSAGE-DESCRIPTOR                   07030000
                               MQM-GET-MESSAGE-OPTIONS                  07040000
                               PV-MSGLENGTH                             07050000
                               PV-MSGBUFFER                             07060000
                               PV-DATA-LENGTH                           07070000
                               PV-COMPLETION-CODE                       07080000
                               PV-REASON.                               07090000
                                                                        07100000
      *        IF GET FAILED THEN DISPLAY ERROR MESSAGE                 07110000
      *        AND BREAK OUT OF LOOP                                    07120000
                                                                        07130000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        07140000
              IF (PV-REASON NOT = MQRC-NO-MSG-AVAILABLE)                07150000
                  MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE         07160000
                  MOVE PV-REASON          TO PH-REASON                  07170000
                  PERFORM 8600-BACK-OUT-MQ                              07180011
                  PERFORM 1600-MQ-ADJ-INPUT-CLOSE                       07190056
                  PERFORM 1700-DISCONNECT-QMGR                          07200000
                  MOVE 'MQGET ERROR'      TO PV-ERROR-MESSAGE           07210000
                  MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE         07220024
                  MOVE PH-REASON          TO PV-REASON                  07230024
                  DISPLAY 'PV-MSGLENGTH = ' PV-MSGLENGTH                07240064
                  DISPLAY 'PV-MSGBUFFER = ' PV-MSGBUFFER                07250064
                  DISPLAY 'PV-DATA-LENGTH = ' PV-DATA-LENGTH            07260064
                  PERFORM 9000-DISPLAY-MQ-ERROR                         07270000
              ELSE                                                      07280000
                  DISPLAY 'MQGET - END OF MQ MESSAGES '                 07290076
                  SET PS-END-OF-Q TO TRUE                               07291067
              END-IF                                                    07300000
           ELSE                                                         07310000
              SET PS-MQGET-SUCCESSFUL TO TRUE                           07320000
              MOVE PV-MSGBUFFER TO FS013-FS-ERROR-RECORD                07360067
              ADD +1 TO PA-Q-MESSAGES                                   07371067
           END-IF.                                                      07390000
                                                                        07400000
      ******************************************************************07410021
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       *07420021
      ******************************************************************07430021
       8500-COMMIT-MQ.                                                  07440021
                                                                        07450021
           CALL WS-MQCMIT USING PV-HCONN                                07460000
                                PV-COMPLETION-CODE                      07470000
                                PV-REASON.                              07480000
                                                                        07490000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        07500000
              MOVE '8500-COMMIT-MQ'  TO PV-PARAGRAPH                    07510008
              MOVE 'MQCMIT ERROR'    TO PV-ERROR-MESSAGE                07520000
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             07530041
              MOVE PV-REASON          TO PH-REASON                      07540041
              PERFORM 8600-BACK-OUT-MQ                                  07550011
              PERFORM 1600-MQ-ADJ-INPUT-CLOSE                           07560056
              PERFORM 1700-DISCONNECT-QMGR                              07570023
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             07580041
              MOVE PH-REASON          TO PV-REASON                      07590041
              PERFORM 9000-DISPLAY-MQ-ERROR                             07600000
           END-IF.                                                      07610000
                                                                        07620000
      ******************************************************************07630000
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT      *07640000
      ******************************************************************07650000
       8600-BACK-OUT-MQ.                                                07660008
                                                                        07670000
           CALL WS-MQBACK USING PV-HCONN                                07680000
                                PV-COMPLETION-CODE                      07690000
                                PV-REASON.                              07700000
                                                                        07710000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   07720000
              MOVE '8600-BACK-OUT-MQ'  TO PV-PARAGRAPH                  07730011
              MOVE 'MQBACK ERROR'    TO PV-ERROR-MESSAGE                07740000
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             07750041
              MOVE PV-REASON          TO PH-REASON                      07760041
              PERFORM 1600-MQ-ADJ-INPUT-CLOSE                           07770056
              PERFORM 1700-DISCONNECT-QMGR                              07780023
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             07790041
              MOVE PH-REASON          TO PV-REASON                      07800041
              PERFORM 9000-DISPLAY-MQ-ERROR                             07810000
           END-IF.                                                      07820000
                                                                        07830023
      ***************************************************************** 07840003
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                   07850000
      ***************************************************************** 07860003
       9000-DISPLAY-MQ-ERROR.                                           07870000
                                                                        07880000
           DISPLAY '************************************************'.  07890021
           DISPLAY '**** ABEND *************************************'.  07900000
           DISPLAY '************************************************'.  07910021
           DISPLAY '** MQSERIES ERROR **'                               07920000
           DISPLAY '** PROGRAM:    ', PC-PGM-NAME                       07930000
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH                      07940000
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 07950000
           DISPLAY '** COMP CODE:  ', PV-COMPLETION-CODE.               07960000
           DISPLAY '** RSN CODE:   ', PV-REASON.                        07970000
           DISPLAY '************************************************'.  07980000
           DISPLAY '**** PROGRAM COUNTERS AT TIME OF ABEND *********'.  07990021
           DISPLAY '************************************************'.  08000021
           DISPLAY '** NUMBER OF Q MESSAGES PROCESSED: ', PA-Q-MESSAGES.08010023
           DISPLAY '** NUMBER OF ADJUSTMENT ERRORS WRITTEN: ',          08020066
                                                      PA-ADJUSTMENTS.   08021066
           DISPLAY '************************************************'.  08030021
           SET AC-MQ-ERROR TO TRUE.                                     08040000
           CALL 'ILBOABN0' USING ABEND-CODE.                            08050000
                                                                        08060068
