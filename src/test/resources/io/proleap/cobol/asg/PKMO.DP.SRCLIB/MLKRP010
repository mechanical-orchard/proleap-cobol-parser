000010 IDENTIFICATION DIVISION.                                                 
000020                                                                          
000030 PROGRAM-ID.    MLKRP010.                                                 
000040 AUTHOR.        TESS BESTE.                                               
000050 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000060 DATE-WRITTEN.  11-07-94.                                                 
000070 DATE-COMPILED.                                                           
000080                                                                          
000090******************************************************************        
000100*      MLKRP010 - SUM BEFORE AND AFTER EDITTED LEDGER TRANSACTIONS        
000110******************************************************************        
000120                                                                          
000130                                                                          
000140                                                                          
000150 ENVIRONMENT DIVISION.                                                    
000160                                                                          
000170                                                                          
000180 CONFIGURATION SECTION.                                                   
000190                                                                          
000200 SOURCE-COMPUTER.        IBM-3090.                                        
000210 OBJECT-COMPUTER.        IBM-3090.                                        
000220                                                                          
000230                                                                          
000240 INPUT-OUTPUT SECTION.                                                    
000250                                                                          
000260 FILE-CONTROL.                                                            
000270                                                                          
000280     SELECT PURCHASE-EXTRACT-FILE-I      ASSIGN TO INP01.                 
000290     SELECT SALE-EXTRACT-FILE-I          ASSIGN TO INP02.                 
000300     SELECT PRICE-CHNG-EXTRACT-FILE-I    ASSIGN TO INP03.                 
000310     SELECT ADJUSTMENT-EXTRACT-FILE-I    ASSIGN TO INP04.                 
000320                                                                          
000330                                                                          
000340                                                                          
000350 DATA DIVISION.                                                           
000360                                                                          
000370                                                                          
000380 FILE SECTION.                                                            
000390                                                                          
000400 FD  PURCHASE-EXTRACT-FILE-I                                              
000410     LABEL RECORDS ARE STANDARD                                           
000420     RECORDING MODE IS F                                                  
000430     BLOCK CONTAINS 0 RECORDS.                                            
000440 01  PURCHASE-EXTRACT-RECORD-I   PIC X(150).                              
000450                                                                          
000460 FD  SALE-EXTRACT-FILE-I                                                  
000470     LABEL RECORDS ARE STANDARD                                           
000480     RECORDING MODE IS F                                                  
000490     BLOCK CONTAINS 0 RECORDS.                                            
000500 01  SALE-EXTRACT-RECORD-I       PIC X(80).                               
000510                                                                          
000520 FD  PRICE-CHNG-EXTRACT-FILE-I                                            
000530     LABEL RECORDS ARE STANDARD                                           
000540     RECORDING MODE IS F                                                  
000550     BLOCK CONTAINS 0 RECORDS.                                            
000560 01  PRICE-CHNG-EXTRACT-RECORD-I                                          
000570                                 PIC X(130).                              
000580                                                                          
000590 FD  ADJUSTMENT-EXTRACT-FILE-I                                            
000600     LABEL RECORDS ARE STANDARD                                           
000610     RECORDING MODE IS F                                                  
000620     BLOCK CONTAINS 0 RECORDS.                                            
000630 01  ADJUSTMENT-EXTRACT-RECORD-I                                          
000640                               PIC X(90).                                 
000650                                                                          
000660                                                                          
000670 WORKING-STORAGE SECTION.                                                 
000680                                                                          
000690 01  PC-PROGRAM-CONSTANTS.                                                
000700     05  PC-CALENDAR-ROUTINE               PIC  X(08) VALUE               
000710                                                       'DPKUT500'.        
000720                                                                          
000730 01  PS-PROGRAM-SWITCHES.                                                 
000740     05  PS-EOF-PURCHASE-EXTRACT-SW        PIC  X     VALUE 'N'.          
000750         88  PS-EOF-PURCHASE-EXTRACT                  VALUE 'Y'.          
000760     05  PS-EOF-SALE-EXTRACT-SW            PIC  X     VALUE 'N'.          
000770         88  PS-EOF-SALE-EXTRACT                      VALUE 'Y'.          
000780     05  PS-EOF-PRICE-CHNG-EXTRACT-SW      PIC  X     VALUE 'N'.          
000790         88  PS-EOF-PRICE-CHNG-EXTRACT                VALUE 'Y'.          
000800     05  PS-EOF-ADJUSTMENT-EXTRACT-SW      PIC  X     VALUE 'N'.          
000810         88  PS-EOF-ADJUSTMENT-EXTRACT                VALUE 'Y'.          
000820                                                                          
000830 01  PV-PROGRAM-VARIABLES.                                                
000840     05  PV-LAST-FD-DB2-ISO-FMT            PIC  X(10).                    
000850     05  PV-LAST-FD-CCYY.                                                 
000860         10  PV-LAST-FD-CC                 PIC  9(02).                    
000870         10  PV-LAST-FD-YY                 PIC  9(02).                    
000880     05  PV-LAST-FD-PERIOD-NBR-X           PIC  X(02).                    
000890     05  PV-LAST-FD-WEEK-NBR               PIC  9(02).                    
000900     05  PV-LAST-FD-PERIOD-WEEK            PIC  9.                        
000910     05  PV-LAST-FD-NUMERIC-WEEK-DAY-X     PIC  X.                        
000920                                                                          
000930                                                                          
000940 01  PA-PROGRAM-ACCUMULATORS.                                             
000950     05  PA-PURCHASE-RECORDS-BFOR          PIC S9(7)     COMP-3           
000960                                                         VALUE 0.         
000970     05  PA-PURCHASE-COST-BFOR             PIC S9(13)V99 COMP-3           
000980                                                         VALUE 0.         
000990     05  PA-PURCHASE-RTL-BFOR              PIC S9(13)V99 COMP-3           
001000                                                         VALUE 0.         
001010     05  PA-PURCHASE-RECORDS-AFTR          PIC S9(7)     COMP-3           
001020                                                         VALUE 0.         
001030     05  PA-PURCHASE-COST-AFTR             PIC S9(13)V99 COMP-3           
001040                                                         VALUE 0.         
001050     05  PA-PURCHASE-RTL-AFTR              PIC S9(13)V99 COMP-3           
001060                                                         VALUE 0.         
001070                                                                          
001080     05  PA-XFER-FROM-RECORDS-BFOR         PIC S9(7)     COMP-3           
001090                                                         VALUE 0.         
001100     05  PA-XFER-FROM-COST-BFOR            PIC S9(13)V99 COMP-3           
001110                                                         VALUE 0.         
001120     05  PA-XFER-FROM-RTL-BFOR             PIC S9(13)V99 COMP-3           
001130                                                         VALUE 0.         
001140     05  PA-XFER-FROM-RECORDS-AFTR         PIC S9(7)     COMP-3           
001150                                                         VALUE 0.         
001160     05  PA-XFER-FROM-COST-AFTR            PIC S9(13)V99 COMP-3           
001170                                                         VALUE 0.         
001180     05  PA-XFER-FROM-RTL-AFTR             PIC S9(13)V99 COMP-3           
001190                                                         VALUE 0.         
001200                                                                          
001210     05  PA-XFER-TO-RECORDS-BFOR           PIC S9(7)     COMP-3           
001220                                                         VALUE 0.         
001230     05  PA-XFER-TO-COST-BFOR              PIC S9(13)V99 COMP-3           
001240                                                         VALUE 0.         
001250     05  PA-XFER-TO-RTL-BFOR               PIC S9(13)V99 COMP-3           
001260                                                         VALUE 0.         
001270     05  PA-XFER-TO-RECORDS-AFTR           PIC S9(7)     COMP-3           
001280                                                         VALUE 0.         
001290     05  PA-XFER-TO-COST-AFTR              PIC S9(13)V99 COMP-3           
001300                                                         VALUE 0.         
001310     05  PA-XFER-TO-RTL-AFTR               PIC S9(13)V99 COMP-3           
001320                                                         VALUE 0.         
001330                                                                          
001340     05  PA-SALE-RECORDS-BFOR              PIC S9(7)     COMP-3           
001350                                                         VALUE 0.         
001360     05  PA-SALE-COST-BFOR                 PIC S9(13)V99 COMP-3           
001370                                                         VALUE 0.         
001380     05  PA-SALE-RTL-BFOR                  PIC S9(13)V99 COMP-3           
001390                                                         VALUE 0.         
001400     05  PA-SALE-RECORDS-AFTR              PIC S9(7)     COMP-3           
001410                                                         VALUE 0.         
001420     05  PA-SALE-COST-AFTR                 PIC S9(13)V99 COMP-3           
001430                                                         VALUE 0.         
001440     05  PA-SALE-RTL-AFTR                  PIC S9(13)V99 COMP-3           
001450                                                         VALUE 0.         
001460                                                                          
001470     05  PA-MARKDOWN-RECORDS-BFOR          PIC S9(7)     COMP-3           
001480                                                         VALUE 0.         
001490     05  PA-MARKDOWN-COST-BFOR             PIC S9(13)V99 COMP-3           
001500                                                         VALUE 0.         
001510     05  PA-MARKDOWN-RTL-BFOR              PIC S9(13)V99 COMP-3           
001520                                                         VALUE 0.         
001530     05  PA-MARKDOWN-RECORDS-AFTR          PIC S9(7)     COMP-3           
001540                                                         VALUE 0.         
001550     05  PA-MARKDOWN-COST-AFTR             PIC S9(13)V99 COMP-3           
001560                                                         VALUE 0.         
001570     05  PA-MARKDOWN-RTL-AFTR              PIC S9(13)V99 COMP-3           
001580                                                         VALUE 0.         
001590                                                                          
001600     05  PA-MARKUP-RECORDS-BFOR            PIC S9(7)     COMP-3           
001610                                                         VALUE 0.         
001620     05  PA-MARKUP-COST-BFOR               PIC S9(13)V99 COMP-3           
001630                                                         VALUE 0.         
001640     05  PA-MARKUP-RTL-BFOR                PIC S9(13)V99 COMP-3           
001650                                                         VALUE 0.         
001660     05  PA-MARKUP-RECORDS-AFTR            PIC S9(7)     COMP-3           
001670                                                         VALUE 0.         
001680     05  PA-MARKUP-COST-AFTR               PIC S9(13)V99 COMP-3           
001690                                                         VALUE 0.         
001700     05  PA-MARKUP-RTL-AFTR                PIC S9(13)V99 COMP-3           
001710                                                         VALUE 0.         
001720                                                                          
001730     05  PA-ADJUSTMENT-RECORDS-BFOR        PIC S9(7)     COMP-3           
001740                                                         VALUE 0.         
001750     05  PA-ADJUSTMENT-COST-BFOR           PIC S9(13)V99 COMP-3           
001760                                                         VALUE 0.         
001770     05  PA-ADJUSTMENT-RTL-BFOR            PIC S9(13)V99 COMP-3           
001780                                                         VALUE 0.         
001790     05  PA-ADJUSTMENT-RECORDS-AFTR        PIC S9(7)     COMP-3           
001800                                                         VALUE 0.         
001810     05  PA-ADJUSTMENT-COST-AFTR           PIC S9(13)V99 COMP-3           
001820                                                         VALUE 0.         
001830     05  PA-ADJUSTMENT-RTL-AFTR            PIC S9(13)V99 COMP-3           
001840                                                         VALUE 0.         
001850 01  DF-DISPLAY-FIELDS.                                                   
001860     05  DF-HEADING-1                 PIC  X(24)    VALUE                 
001870                                       '*** MLKRP010 STATISTICS:'.        
001880     05  DF-HEADING-2.                                                    
001890         10  FILLER                   PIC  X(17)    VALUE SPACE.          
001900         10  FILLER                   PIC  X(09)    VALUE ALL '-'.        
001910         10  FILLER                   PIC  X(21)    VALUE                 
001920                                          ' BEFORE TRANSACTIONS '.        
001930         10  FILLER                   PIC  X(17)    VALUE ALL '-'.        
001940         10  FILLER                   PIC  X(07)    VALUE SPACE.          
001950         10  FILLER                   PIC  X(12)    VALUE ALL '-'.        
001960         10  FILLER                   PIC  X(21)    VALUE                 
001970                                          ' AFTER TRANSACTIONS  '.        
001980         10  FILLER                   PIC  X(12)    VALUE ALL '-'.        
001990                                                                          
002000     05  DF-HEADING-3.                                                    
002010         10  FILLER                   PIC  X(05)    VALUE SPACE.          
002020         10  FILLER                   PIC  X(04)    VALUE 'TYPE'.         
002030         10  FILLER                   PIC  X(10)    VALUE SPACE.          
002040         10  FILLER                   PIC  X(05)    VALUE 'COUNT'.        
002050         10  FILLER                   PIC  X(11)    VALUE SPACE.          
002060         10  FILLER                   PIC  X(04)    VALUE 'COST'.         
002070         10  FILLER                   PIC  X(15)    VALUE SPACE.          
002080         10  FILLER                   PIC  X(06)    VALUE                 
002090                                                         'RETAIL'.        
002100         10  FILLER                   PIC  X(13)    VALUE SPACE.          
002110         10  FILLER                   PIC  X(05)    VALUE 'COUNT'.        
002120         10  FILLER                   PIC  X(11)    VALUE SPACE.          
002130         10  FILLER                   PIC  X(04)    VALUE 'COST'.         
002140         10  FILLER                   PIC  X(15)    VALUE SPACE.          
002150         10  FILLER                   PIC  X(06)    VALUE                 
002160                                                         'RETAIL'.        
002170                                                                          
002180     05  DF-DETAIL.                                                       
002190         10  FILLER                   PIC  X        VALUE SPACE.          
002200         10  DF-TRAN-TYPE             PIC  X(14).                         
002210         10  FILLER                   PIC  X(02)    VALUE SPACE.          
002220         10  DF-RECORDS-BFOR          PIC  Z,ZZZ,ZZ9.                     
002230         10  FILLER                   PIC  X(02)    VALUE SPACE.          
002240         10  DF-COST-BFOR             PIC  Z,ZZZ,ZZZ,ZZZ.99-.             
002250         10  FILLER                   PIC  X(02)    VALUE SPACE.          
002260         10  DF-RTL-BFOR              PIC  Z,ZZZ,ZZZ,ZZZ.99-.             
002270         10  FILLER                   PIC  X(07)    VALUE SPACE.          
002280         10  DF-RECORDS-AFTR          PIC  Z,ZZZ,ZZ9.                     
002290         10  FILLER                   PIC  X(02)    VALUE SPACE.          
002300         10  DF-COST-AFTR             PIC  Z,ZZZ,ZZZ,ZZZ.99-.             
002310         10  FILLER                   PIC  X(02)    VALUE SPACE.          
002320         10  DF-RTL-AFTR              PIC  Z,ZZZ,ZZZ,ZZZ.99-.             
002330                                                                          
002340 01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
002350                                                                          
002360******************************************************************        
002370* CALENDAR ROUTINE WORK AREA.                                             
002380******************************************************************        
002390                                                                          
002400      COPY DPWS500.                                                       
002410                                                                          
002420                                                                          
002430******************************************************************        
002440* INPUT CONTROL CARD LAYOUT.                                              
002450******************************************************************        
002460 01  CC-CONTROL-CARD.                                                     
002470     05  CC-STORE                PIC  X(03).                              
002480     05  FILLER                  PIC  X.                                  
002490     05  CC-DAY                  PIC  X(05).                              
002500     05  FILLER                  PIC  X.                                  
002510     05  CC-ACTL-DC-CTOFF-DTE    PIC  X(10).                              
002520     05  FILLER                  PIC  X.                                  
002530     05  CC-PLND-INV-DTE         PIC  X(10).                              
002540     05  FILLER                  PIC  X(49).                              
002550                                                                          
002560                                                                          
002570*****************************************************************         
002580* PURCHASE EXTRACT RECORD LAYOUT                                          
002590*****************************************************************         
002600                                                                          
002610      COPY MLRD021.                                                       
002620                                                                          
002630                                                                          
002640*****************************************************************         
002650* SALE EXTRACT RECORD LAYOUT                                              
002660*****************************************************************         
002670                                                                          
002680      COPY MLRD022.                                                       
002690                                                                          
002700                                                                          
002710*****************************************************************         
002720* PRICE CHANGE EXTRACT RECORD LAYOUT                                      
002730*****************************************************************         
002740                                                                          
002750      COPY MLRD023.                                                       
002760                                                                          
002770                                                                          
002780*****************************************************************         
002790* ADJUSTMENT EXTRACT RECORD LAYOUT                                        
002800*****************************************************************         
002810                                                                          
002820      COPY MLRD024.                                                       
002830                                                                          
002840                                                                          
002850 LINKAGE SECTION.                                                         
002860                                                                          
002870                                                                          
002880 PROCEDURE DIVISION.                                                      
002890                                                                          
002900 A100-MAIN-MODULE.                                                        
002910                                                                          
002920     PERFORM B050-PROCESS-CONTROL-CARD.                                   
002930                                                                          
002940*    *------------------------------------------------------------        
002950*    * PROCESS THE PURCHASE EXTRACT FILE.                                 
002960*    *------------------------------------------------------------        
002970                                                                          
002980     OPEN INPUT  PURCHASE-EXTRACT-FILE-I.                                 
002990                                                                          
003000     PERFORM Z100-READ-PURCHASE-EXTRACT.                                  
003010     PERFORM B100-PROCESS-PURCHASE-EXT                                    
003020         UNTIL PS-EOF-PURCHASE-EXTRACT.                                   
003030                                                                          
003040     CLOSE PURCHASE-EXTRACT-FILE-I.                                       
003050                                                                          
003060*    *------------------------------------------------------------        
003070*    * PROCESS THE SALE EXTRACT FILE.                                     
003080*    *------------------------------------------------------------        
003090                                                                          
003100     OPEN INPUT  SALE-EXTRACT-FILE-I.                                     
003110                                                                          
003120     PERFORM Z200-READ-SALE-EXTRACT.                                      
003130     PERFORM B200-PROCESS-SALE-EXT                                        
003140         UNTIL PS-EOF-SALE-EXTRACT.                                       
003150                                                                          
003160     CLOSE SALE-EXTRACT-FILE-I.                                           
003170                                                                          
003180*    *------------------------------------------------------------        
003190*    * PROCESS THE PRICE CHANGE EXTRACT FILE.                             
003200*    *------------------------------------------------------------        
003210                                                                          
003220     OPEN INPUT  PRICE-CHNG-EXTRACT-FILE-I.                               
003230                                                                          
003240     PERFORM Z300-READ-PRICE-CHNG-EXTRACT.                                
003250     PERFORM B300-PROCES-PRICE-CHNG-EXT                                   
003260         UNTIL PS-EOF-PRICE-CHNG-EXTRACT.                                 
003270                                                                          
003280     CLOSE PRICE-CHNG-EXTRACT-FILE-I.                                     
003290                                                                          
003300*    *------------------------------------------------------------        
003310*    * PROCESS THE ADJUSTMENT EXTRACT FILE.                               
003320*    *------------------------------------------------------------        
003330                                                                          
003340     OPEN INPUT  ADJUSTMENT-EXTRACT-FILE-I.                               
003350                                                                          
003360     PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                                
003370     PERFORM B400-PROCESS-ADJUSTMENT-EXT                                  
003380         UNTIL PS-EOF-ADJUSTMENT-EXTRACT.                                 
003390                                                                          
003400     CLOSE ADJUSTMENT-EXTRACT-FILE-I.                                     
003410                                                                          
003420*    *------------------------------------------------------------        
003430*    * DISPLAY THE NUMBER OF RECORDS READ AND CHANGED FOR EACH            
003440*    * INPUT FILE.                                                        
003450*    *------------------------------------------------------------        
003460                                                                          
003470     DISPLAY DF-HEADING-1.                                                
003480     DISPLAY ' '.                                                         
003490     DISPLAY DF-HEADING-2.                                                
003500     DISPLAY DF-HEADING-3.                                                
003510     DISPLAY ' '.                                                         
003520                                                                          
003530     MOVE 'PURCHASE'                    TO DF-TRAN-TYPE.                  
003540     MOVE PA-PURCHASE-RECORDS-BFOR      TO DF-RECORDS-BFOR.               
003550     MOVE PA-PURCHASE-COST-BFOR         TO DF-COST-BFOR.                  
003560     MOVE PA-PURCHASE-RTL-BFOR          TO DF-RTL-BFOR.                   
003570     MOVE PA-PURCHASE-RECORDS-AFTR      TO DF-RECORDS-AFTR.               
003580     MOVE PA-PURCHASE-COST-AFTR         TO DF-COST-AFTR.                  
003590     MOVE PA-PURCHASE-RTL-AFTR          TO DF-RTL-AFTR.                   
003600     DISPLAY DF-DETAIL.                                                   
003610                                                                          
003620     MOVE 'TRANSFER FROM'               TO DF-TRAN-TYPE.                  
003630     MOVE PA-XFER-FROM-RECORDS-BFOR     TO DF-RECORDS-BFOR.               
003640     MOVE PA-XFER-FROM-COST-BFOR        TO DF-COST-BFOR.                  
003650     MOVE PA-XFER-FROM-RTL-BFOR         TO DF-RTL-BFOR.                   
003660     MOVE PA-XFER-FROM-RECORDS-AFTR     TO DF-RECORDS-AFTR.               
003670     MOVE PA-XFER-FROM-COST-AFTR        TO DF-COST-AFTR.                  
003680     MOVE PA-XFER-FROM-RTL-AFTR         TO DF-RTL-AFTR.                   
003690     DISPLAY DF-DETAIL.                                                   
003700                                                                          
003710     MOVE 'TRANSFER TO'                 TO DF-TRAN-TYPE.                  
003720     MOVE PA-XFER-TO-RECORDS-BFOR       TO DF-RECORDS-BFOR.               
003730     MOVE PA-XFER-TO-COST-BFOR          TO DF-COST-BFOR.                  
003740     MOVE PA-XFER-TO-RTL-BFOR           TO DF-RTL-BFOR.                   
003750     MOVE PA-XFER-TO-RECORDS-AFTR       TO DF-RECORDS-AFTR.               
003760     MOVE PA-XFER-TO-COST-AFTR          TO DF-COST-AFTR.                  
003770     MOVE PA-XFER-TO-RTL-AFTR           TO DF-RTL-AFTR.                   
003780     DISPLAY DF-DETAIL.                                                   
003790                                                                          
003800     MOVE 'SALE'                        TO DF-TRAN-TYPE.                  
003810     MOVE PA-SALE-RECORDS-BFOR          TO DF-RECORDS-BFOR.               
003820     MOVE PA-SALE-COST-BFOR             TO DF-COST-BFOR.                  
003830     MOVE PA-SALE-RTL-BFOR              TO DF-RTL-BFOR.                   
003840     MOVE PA-SALE-RECORDS-AFTR          TO DF-RECORDS-AFTR.               
003850     MOVE PA-SALE-COST-AFTR             TO DF-COST-AFTR.                  
003860     MOVE PA-SALE-RTL-AFTR              TO DF-RTL-AFTR.                   
003870     DISPLAY DF-DETAIL.                                                   
003880                                                                          
003890     MOVE 'MARKDOWN'                    TO DF-TRAN-TYPE.                  
003900     MOVE PA-MARKDOWN-RECORDS-BFOR      TO DF-RECORDS-BFOR.               
003910     MOVE PA-MARKDOWN-COST-BFOR         TO DF-COST-BFOR.                  
003920     MOVE PA-MARKDOWN-RTL-BFOR          TO DF-RTL-BFOR.                   
003930     MOVE PA-MARKDOWN-RECORDS-AFTR      TO DF-RECORDS-AFTR.               
003940     MOVE PA-MARKDOWN-COST-AFTR         TO DF-COST-AFTR.                  
003950     MOVE PA-MARKDOWN-RTL-AFTR          TO DF-RTL-AFTR.                   
003960     DISPLAY DF-DETAIL.                                                   
003970                                                                          
003980     MOVE 'MARKUP'                      TO DF-TRAN-TYPE.                  
003990     MOVE PA-MARKUP-RECORDS-BFOR        TO DF-RECORDS-BFOR.               
004000     MOVE PA-MARKUP-COST-BFOR           TO DF-COST-BFOR.                  
004010     MOVE PA-MARKUP-RTL-BFOR            TO DF-RTL-BFOR.                   
004020     MOVE PA-MARKUP-RECORDS-AFTR        TO DF-RECORDS-AFTR.               
004030     MOVE PA-MARKUP-COST-AFTR           TO DF-COST-AFTR.                  
004040     MOVE PA-MARKUP-RTL-AFTR            TO DF-RTL-AFTR.                   
004050     DISPLAY DF-DETAIL.                                                   
004060                                                                          
004070     MOVE 'ADJUSTMENT'                  TO DF-TRAN-TYPE.                  
004080     MOVE PA-ADJUSTMENT-RECORDS-BFOR    TO DF-RECORDS-BFOR.               
004090     MOVE PA-ADJUSTMENT-COST-BFOR       TO DF-COST-BFOR.                  
004100     MOVE PA-ADJUSTMENT-RTL-BFOR        TO DF-RTL-BFOR.                   
004110     MOVE PA-ADJUSTMENT-RECORDS-AFTR TO DF-RECORDS-AFTR.                  
004120     MOVE PA-ADJUSTMENT-COST-AFTR       TO DF-COST-AFTR.                  
004130     MOVE PA-ADJUSTMENT-RTL-AFTR        TO DF-RTL-AFTR.                   
004140     DISPLAY DF-DETAIL.                                                   
004150                                                                          
004160     STOP RUN.                                                            
004170                                                                          
004180                                                                          
004190******************************************************************        
004200* ACCEPT THE INPUT CONTROL CARD, VALIDATE ITS DATE, AND DETERMINE         
004210* THE FISCAL YEAR-END DATE ASSOCIATED WITH IT.                            
004220******************************************************************        
004230 B050-PROCESS-CONTROL-CARD.                                               
004240                                                                          
004250     ACCEPT CC-CONTROL-CARD.                                              
004260                                                                          
004270     IF CC-CONTROL-CARD = SPACE                                           
004280         DISPLAY '** ABEND **'                                            
004290         DISPLAY '** PGM = MLKRP010'                                      
004300         DISPLAY '** PARAGRAPH = B050-PROCESS-CONTROL-CARD'               
004310         DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'                 
004320         MOVE +4003 TO ABEND-CODE                                         
004330         CALL 'ILBOABN0' USING ABEND-CODE                                 
004340     END-IF.                                                              
004350                                                                          
004360     DISPLAY '*** MLKRP010 INPUT CONTROL CARD =' CC-CONTROL-CARD.         
004370                                                                          
004380     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
004390     ACCEPT DPG51-SYSTEM-DATE FROM DATE.                                  
004400     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
004410     SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
004420     MOVE CC-PLND-INV-DTE          TO DPG52-LK-DATE-INPUT.                
004430                                                                          
004440     CALL PC-CALENDAR-ROUTINE                                             
004450         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
004460                                                                          
004470     EVALUATE TRUE                                                        
004480         WHEN DPG54-SEVERE-ERROR                                          
004490             DISPLAY '** ABEND **'                                        
004500             DISPLAY '** PGM = MLKRP010'                                  
004510             DISPLAY '** PARAGRAPH = B050-PROCESS-CONTROL-CARD'           
004520             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
004530             DISPLAY '** USING CC-ACTL-INV-DTE AS INPUT.'                 
004540             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
004550             DISPLAY '** DPG51 = ' DPG51                                  
004560             DISPLAY '** DPG52 = ' DPG52                                  
004570             DISPLAY '** DPG53 = ' DPG53                                  
004580             DISPLAY '** DPG54 = ' DPG54                                  
004590             MOVE +4003 TO ABEND-CODE                                     
004600             CALL 'ILBOABN0' USING ABEND-CODE                             
004610         WHEN OTHER                                                       
004620             CONTINUE                                                     
004630     END-EVALUATE.                                                        
004640                                                                          
004650                                                                          
004660     MOVE CC-ACTL-DC-CTOFF-DTE     TO DPG52-LK-DATE-INPUT.                
004670                                                                          
004680     CALL PC-CALENDAR-ROUTINE                                             
004690         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
004700                                                                          
004710     EVALUATE TRUE                                                        
004720         WHEN DPG54-SEVERE-ERROR                                          
004730             DISPLAY '** ABEND **'                                        
004740             DISPLAY '** PGM = MLKRP010'                                  
004750             DISPLAY '** PARAGRAPH = B050-PROCESS-CONTROL-CARD'           
004760             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
004770             DISPLAY '** USING CC-ACTL-DC-CTOFF-DTE AS INPUT.'            
004780             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
004790             DISPLAY '** DPG51 = ' DPG51                                  
004800             DISPLAY '** DPG52 = ' DPG52                                  
004810             DISPLAY '** DPG53 = ' DPG53                                  
004820             DISPLAY '** DPG54 = ' DPG54                                  
004830             MOVE +4003 TO ABEND-CODE                                     
004840             CALL 'ILBOABN0' USING ABEND-CODE                             
004850         WHEN OTHER                                                       
004860             CONTINUE                                                     
004870     END-EVALUATE.                                                        
004880                                                                          
004890                                                                          
004900 B100-PROCESS-PURCHASE-EXT.                                               
004910                                                                          
004920     EVALUATE TRUE                                                        
004930         WHEN ML021-PURCH-PURCHASE                                        
004940*            ** DEBITS WILL BE SUBTRACTED **                              
004950             IF ML021-PURCH-DR-CR-CDE = '02' OR '08'                      
004960                 MULTIPLY -1 BY ML021-PURCH-EXTD-NET-COST-AMT             
004970                 MULTIPLY -1 BY ML021-PURCH-EXTD-RTL-AMT                  
004980             END-IF                                                       
004990*            ** TYP-CDE '1' = RECEIPT **                                  
005000             IF ML021-PURCH-TYP-CDE = '1'                                 
005010                 IF ML021-PURCH-FIN-LIAB-DTE >                            
005020                                              CC-ACTL-DC-CTOFF-DTE        
005030                     ADD +1 TO PA-PURCHASE-RECORDS-AFTR                   
005040                     COMPUTE PA-PURCHASE-COST-AFTR                        
005050                           = PA-PURCHASE-COST-AFTR                        
005060                           + ML021-PURCH-EXTD-NET-COST-AMT                
005070                     COMPUTE PA-PURCHASE-RTL-AFTR                         
005080                           = PA-PURCHASE-RTL-AFTR                         
005090                           + ML021-PURCH-EXTD-RTL-AMT                     
005100                 ELSE                                                     
005110                     ADD +1 TO PA-PURCHASE-RECORDS-BFOR                   
005120                     COMPUTE PA-PURCHASE-COST-BFOR                        
005130                           = PA-PURCHASE-COST-BFOR                        
005140                           + ML021-PURCH-EXTD-NET-COST-AMT                
005150                     COMPUTE PA-PURCHASE-RTL-BFOR                         
005160                           = PA-PURCHASE-RTL-BFOR                         
005170                           + ML021-PURCH-EXTD-RTL-AMT                     
005180                 END-IF                                                   
005190             ELSE                                                         
005200                 IF ML021-PURCH-FIN-LIAB-DTE > CC-PLND-INV-DTE            
005210                     ADD +1 TO PA-PURCHASE-RECORDS-AFTR                   
005220                     COMPUTE PA-PURCHASE-COST-AFTR                        
005230                           = PA-PURCHASE-COST-AFTR                        
005240                           + ML021-PURCH-EXTD-NET-COST-AMT                
005250                     COMPUTE PA-PURCHASE-RTL-AFTR                         
005260                           = PA-PURCHASE-RTL-AFTR                         
005270                           + ML021-PURCH-EXTD-RTL-AMT                     
005280                 ELSE                                                     
005290                     ADD +1 TO PA-PURCHASE-RECORDS-BFOR                   
005300                     COMPUTE PA-PURCHASE-COST-BFOR                        
005310                           = PA-PURCHASE-COST-BFOR                        
005320                           + ML021-PURCH-EXTD-NET-COST-AMT                
005330                     COMPUTE PA-PURCHASE-RTL-BFOR                         
005340                           = PA-PURCHASE-RTL-BFOR                         
005350                           + ML021-PURCH-EXTD-RTL-AMT                     
005360                 END-IF                                                   
005370             END-IF                                                       
005380         WHEN ML021-PURCH-TRANSFER-FRM-STORE                              
005390             IF ML021-PURCH-FIN-LIAB-DTE > CC-PLND-INV-DTE                
005400                 ADD +1 TO PA-XFER-FROM-RECORDS-AFTR                      
005410                 COMPUTE PA-XFER-FROM-COST-AFTR                           
005420                       = PA-XFER-FROM-COST-AFTR                           
005430                       + ML021-PURCH-EXTD-NET-COST-AMT                    
005440                 COMPUTE PA-XFER-FROM-RTL-AFTR                            
005450                       = PA-XFER-FROM-RTL-AFTR                            
005460                       + ML021-PURCH-EXTD-RTL-AMT                         
005470             ELSE                                                         
005480                 ADD +1 TO PA-XFER-FROM-RECORDS-BFOR                      
005490                 COMPUTE PA-XFER-FROM-COST-BFOR                           
005500                       = PA-XFER-FROM-COST-BFOR                           
005510                       + ML021-PURCH-EXTD-NET-COST-AMT                    
005520                 COMPUTE PA-XFER-FROM-RTL-BFOR                            
005530                       = PA-XFER-FROM-RTL-BFOR                            
005540                       + ML021-PURCH-EXTD-RTL-AMT                         
005550             END-IF                                                       
005560         WHEN ML021-PURCH-TRANSFER-TO-STORE                               
005570             IF ML021-PURCH-FIN-LIAB-DTE > CC-PLND-INV-DTE                
005580                 ADD +1 TO PA-XFER-TO-RECORDS-AFTR                        
005590                 COMPUTE PA-XFER-TO-COST-AFTR                             
005600                       = PA-XFER-TO-COST-AFTR                             
005610                       + ML021-PURCH-EXTD-NET-COST-AMT                    
005620                 COMPUTE PA-XFER-TO-RTL-AFTR                              
005630                       = PA-XFER-TO-RTL-AFTR                              
005640                       + ML021-PURCH-EXTD-RTL-AMT                         
005650             ELSE                                                         
005660                 ADD +1 TO PA-XFER-TO-RECORDS-BFOR                        
005670                 COMPUTE PA-XFER-TO-COST-BFOR                             
005680                       = PA-XFER-TO-COST-BFOR                             
005690                       + ML021-PURCH-EXTD-NET-COST-AMT                    
005700                 COMPUTE PA-XFER-TO-RTL-BFOR                              
005710                       = PA-XFER-TO-RTL-BFOR                              
005720                       + ML021-PURCH-EXTD-RTL-AMT                         
005730             END-IF                                                       
005740     END-EVALUATE.                                                        
005750                                                                          
005760     PERFORM Z100-READ-PURCHASE-EXTRACT.                                  
005770                                                                          
005780                                                                          
005790 B200-PROCESS-SALE-EXT.                                                   
005800                                                                          
005810     IF ML022-SALE-FIN-LIAB-DTE > CC-PLND-INV-DTE                         
005820         ADD +1 TO PA-SALE-RECORDS-AFTR                                   
005830         ADD ML022-SALE-EXTD-RTL-AMT TO PA-SALE-RTL-AFTR                  
005840     ELSE                                                                 
005850         ADD +1 TO PA-SALE-RECORDS-BFOR                                   
005860         ADD ML022-SALE-EXTD-RTL-AMT TO PA-SALE-RTL-BFOR                  
005870     END-IF.                                                              
005880                                                                          
005890     PERFORM Z200-READ-SALE-EXTRACT.                                      
005900                                                                          
005910                                                                          
005920 B300-PROCES-PRICE-CHNG-EXT.                                              
005930                                                                          
005940     IF ML023-PC-TRANS-CDE = '40'                                         
005950         IF ML023-PC-FIN-LIAB-DTE > CC-PLND-INV-DTE                       
005960             ADD +1 TO PA-MARKDOWN-RECORDS-AFTR                           
005970             COMPUTE PA-MARKDOWN-RTL-AFTR                                 
005980                   = PA-MARKDOWN-RTL-AFTR                                 
005990                   + ML023-PC-EXTD-RTL-AMT                                
006000         ELSE                                                             
006010             ADD +1 TO PA-MARKDOWN-RECORDS-BFOR                           
006020             COMPUTE PA-MARKDOWN-RTL-BFOR                                 
006030                   = PA-MARKDOWN-RTL-BFOR                                 
006040                   + ML023-PC-EXTD-RTL-AMT                                
006050         END-IF                                                           
006060     ELSE                                                                 
006070         IF ML023-PC-FIN-LIAB-DTE > CC-PLND-INV-DTE                       
006080             ADD +1 TO PA-MARKUP-RECORDS-AFTR                             
006090             COMPUTE PA-MARKUP-RTL-AFTR                                   
006100                   = PA-MARKUP-RTL-AFTR                                   
006110                   + ML023-PC-EXTD-RTL-AMT                                
006120         ELSE                                                             
006130             ADD +1 TO PA-MARKUP-RECORDS-BFOR                             
006140             COMPUTE PA-MARKUP-RTL-BFOR                                   
006150                   = PA-MARKUP-RTL-BFOR                                   
006160                   + ML023-PC-EXTD-RTL-AMT                                
006170         END-IF                                                           
006180     END-IF.                                                              
006190                                                                          
006200     PERFORM  Z300-READ-PRICE-CHNG-EXTRACT.                               
006210                                                                          
006220                                                                          
006230 B400-PROCESS-ADJUSTMENT-EXT.                                             
006240                                                                          
006250     IF ML023-PC-FIN-LIAB-DTE > CC-PLND-INV-DTE                           
006260         ADD +1                    TO PA-ADJUSTMENT-RECORDS-AFTR          
006270         ADD ML024-ADJ-AT-RTL-AMT  TO PA-ADJUSTMENT-RTL-AFTR              
006280         ADD ML024-ADJ-AT-COST-AMT TO PA-ADJUSTMENT-COST-AFTR             
006290     ELSE                                                                 
006300         ADD +1                    TO PA-ADJUSTMENT-RECORDS-BFOR          
006310         ADD ML024-ADJ-AT-RTL-AMT  TO PA-ADJUSTMENT-RTL-BFOR              
006320         ADD ML024-ADJ-AT-COST-AMT TO PA-ADJUSTMENT-COST-BFOR             
006330     END-IF.                                                              
006340                                                                          
006350     PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                                
006360                                                                          
006370                                                                          
006380 Z100-READ-PURCHASE-EXTRACT.                                              
006390                                                                          
006400     READ PURCHASE-EXTRACT-FILE-I                                         
006410         INTO ML021-PURCH-RECORD                                          
006420             AT END SET PS-EOF-PURCHASE-EXTRACT TO TRUE.                  
006430                                                                          
006440                                                                          
006450 Z200-READ-SALE-EXTRACT.                                                  
006460                                                                          
006470     READ SALE-EXTRACT-FILE-I                                             
006480         INTO ML022-SALE-RECORD                                           
006490             AT END SET PS-EOF-SALE-EXTRACT TO TRUE.                      
006500                                                                          
006510                                                                          
006520 Z300-READ-PRICE-CHNG-EXTRACT.                                            
006530                                                                          
006540     READ PRICE-CHNG-EXTRACT-FILE-I                                       
006550         INTO ML023-PC-RECORD                                             
006560             AT END SET PS-EOF-PRICE-CHNG-EXTRACT TO TRUE.                
006570                                                                          
006580                                                                          
006590 Z400-READ-ADJUSTMENT-EXTRACT.                                            
006600                                                                          
006610     READ ADJUSTMENT-EXTRACT-FILE-I                                       
006620         INTO ML024-ADJ-RECORD                                            
006630             AT END SET PS-EOF-ADJUSTMENT-EXTRACT TO TRUE.                
006640                                                                          
006650                                                                          
