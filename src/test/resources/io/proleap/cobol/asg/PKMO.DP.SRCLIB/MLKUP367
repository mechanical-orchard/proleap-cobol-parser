       IDENTIFICATION DIVISION.                                         00010001
                                                                        00020001
       PROGRAM-ID.    MLKUP367.                                         00030001
       AUTHOR.        RONNA MCDONALD.                                   00040001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050001
       DATE-WRITTEN.  2022-10-24.                                       00060001
       DATE-COMPILED.                                                   00070001
                                                                        00080001
      ******************************************************************00081001
      * MLKUP367 - END ROWS IN IMT_DEPT_BRND_VND THAT ARE IN THE        00082001
      *            INPUT FILE.                                          00083001
      ******************************************************************00084001
      *                                                                 00085001
      * THIS PROGRAM WILL PROCESS AN INPUT CSV FILE THAT CONTAINS       00086001
      * A LIST OF DEPARTMENT BRAND VENDOR RELATIONSHIPS THAT            00087001
      * CAN BE ENDED.  THE END DATE WILL BE SET TO THE CURRENT DATE.    00088001
      *                                                                 00089001
      * THERE IS ONE INPUT FILES TO THIS PROGRAM:                       00089101
      *    INP01 - LIST DEPT BRAND VEDNOR ROWS TO END                   00089201
      *                                                                 00089301
      * THIS PROGRAM USES CHECKPOINT RESTART LOGIC TO DETERMINE IF AND  00089401
      * WHEN A CHECKPOINT IS TO BE TAKEN. A MINIMUM OF 10 SECONDS WILL  00089501
      * EXPIRE BEFORE A CHECKPOINT / COMMIT WILL BE PERFORMED.          00089601
      *                                                                 00089701
      * CONDITIONS:                                                     00089801
      * NORMAL START - START FROM THE BEGINNING OF THE INPUT FILE       00089901
      * RESTART - START UPDATING AT THE UPDATED RECORD COMMITTED.       00090001
      *                                                                 00090101
      *CHANGE HISTORY:                                                  00090201
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00090301
      * ------- ---------- ----------- -------------------------------- 00090401
      *                                                                 00090501
      ******************************************************************00090601
                                                                        00340001
       ENVIRONMENT DIVISION.                                            00350001
                                                                        00360001
       CONFIGURATION SECTION.                                           00370001
       SOURCE-COMPUTER.        IBM-3090.                                00380001
       OBJECT-COMPUTER.        IBM-3090.                                00390001
       INPUT-OUTPUT SECTION.                                            00400001
       FILE-CONTROL.                                                    00410001
                                                                        00420001
           SELECT DBV-CSV-END-FILE                                      00441001
               ASSIGN TO INP01.                                         00442001
                                                                        00450001
       DATA DIVISION.                                                   00460001
       FILE SECTION.                                                    00470001
                                                                        00480001
       FD  DBV-CSV-END-FILE                                             00490001
           LABEL RECORDS ARE STANDARD                                   00500001
           RECORDING MODE IS F                                          00510001
           BLOCK CONTAINS 0 RECORDS.                                    00520001
       01  DBV-CSV-END-REC             PIC X(100).                      00530001
                                                                        00540001
       WORKING-STORAGE SECTION.                                         00550001
                                                                        00560001
       01  FILLER                      PIC X(28)   VALUE                00570001
                'BEGINNING OF WORKING STORAGE'.                         00580001
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00590001
                                                                        00600001
       01  PC-PROGRAM-CONSTANTS.                                        00610001
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK802'.    00620003
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP367'.  00630001
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00640001
                                                                        00650001
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00660001
           05  PE-MSG-01                       PIC X(50) VALUE          00690001
                'ATTEMPT TO UPDATE ROW ON IMT_DEPT_BRND_VND FAILED'.    00700004
           05  PE-MSG-02                       PIC X(50) VALUE          00750001
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00760001
           05  PE-MSG-03                       PIC X(50) VALUE          00770001
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00780001
           05  PE-MSG-04                       PIC X(50) VALUE          00790001
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00800001
      *                                                                 00810001
       01  PS-PROGRAM-SWITCHES.                                         00820001
           05  PS-EOF-CSV-FILE-SW           PIC X(01)     VALUE 'N'.    00830001
               88  PS-EOF-CSV-FILE                        VALUE 'Y'.    00840001
           05  PS-MNSCLS-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00850001
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00860001
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00870001
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00880001
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00890001
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00900001
                                                                        00910001
       01  PI-PROGRAM-INDICATORS.                                       00920001
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00930001
               88  PI-UPDATE-MNSCLS-ROW                   VALUE 'U'.    00940001
               88  PI-INSERT-MNSCLS-ROW                   VALUE 'I'.    00950001
      *                                                                 00960001
       01  PROGRAM-VARIABLES.                                           00970001
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00980001
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00990001
      *                                                                 01000001
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01010001
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01020001
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01030001
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01040001
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01050001
      *                                                                 01060001
       01  PA-PROGRAM-ACCUMULATORS.                                     01070001
           05  PA-RECORD-COUNTS.                                        01080001
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE 0.           01090001
               10  PA-INSERTED-COUNT     PIC  9(09)  VALUE 0.           01100001
               10  PA-UPDATED-COUNT      PIC  9(09)  VALUE 0.           01110001
                                                                        01120001
       01  PD-PROGRAM-DISPLAYS.                                         01130001
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01140001
       01  SAVE-AREA.                                                   01150001
           05  SV-PRIMARY-KEY.                                          01160001
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACE.        01170001
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACE.        01180001
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACE.        01190001
               10  SV-SUB-CL-NBR         PIC X(02)  VALUE SPACE.        01200001
               10  SV-STR-NBR            PIC S9(04) COMP VALUE 0.       01210001
           05  SV-UPDATE-FIELDS.                                        01220001
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01230001
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01240001
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250001
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01260001
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01270001
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01280001
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01290001
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01300001
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01310001
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01320001
      *                                                                 01330001
      ******************************************************************01331001
      *  CSV FILE LAYOUT                                                01332001
      ******************************************************************01333001
           COPY MLRD621.                                                01334001
                                                                        01335001
      *                                                                 01380001
      ******************************************************************01390001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01400001
      ******************************************************************01410001
           COPY DPWS004.                                                01420001
      *                                                                 01430001
      ******************************************************************01440001
      *  USED FOR DB2 ERRORS                                            01450001
      ******************************************************************01460001
           EXEC SQL                                                     01470001
               INCLUDE SQLCA                                            01480001
           END-EXEC.                                                    01490001
      *                                                                 01490101
      ******************************************************************01491001
      *    IMT_DEPT_BRND_VND                                          * 01492001
      ******************************************************************01493001
           EXEC SQL                                                     01494001
               INCLUDE IMT00017                                         01495001
           END-EXEC.                                                    01496001
      *                                                                 01500001
      ***************************************************************** 01580001
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01590001
      ***************************************************************** 01600001
           EXEC SQL                                                     01610001
               INCLUDE TCKRSTCN                                         01620001
           END-EXEC.                                                    01630001
      *                                                                 01640001
           EXEC SQL                                                     01650001
               INCLUDE TCKRSTIN                                         01660001
           END-EXEC.                                                    01670001
      *                                                                 01680001
      ******************************************************************01690001
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01700001
      ******************************************************************01710001
       01  CR-CHECKPOINT-RESTART-AREA.                                  01711001
           05  CR-AREA-LEN                  PIC S9(04) VALUE +21  COMP. 01712001
           05  CR-CHECKPOINT-RESTART-VAR.                               01713001
               10  CR-PREV-DTL-KEY          PIC  X(12) VALUE SPACES.    01714001
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01715001
                   15  CR-DEPT-BRND-VND-ID  PIC  X(12).                 01716001
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01717001
      *                                                                 01718001
      *                                                                 01830001
       01  CK-CHECKPOINT-SAVE-AREA.                                     01831001
           05  CK-SAVE-PREV-KEY         PIC  X(12) VALUE SPACES.        01832001
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01833001
               10  CK-DEPT-BRND-VND-ID  PIC  X(12).                     01834001
                                                                        01835001
      *-------------------*                                             01930001
       PROCEDURE DIVISION.                                              01940001
      *-------------------*                                             01950001
                                                                        01960001
       0000-MLKUP367.                                                   01970001
                                                                        01980001
           PERFORM 1000-INITIALIZATION.                                 01990001
      *                                                                 02000001
           PERFORM 2000-PROCESS-INPUT                                   02010001
                UNTIL PS-EOF-CSV-FILE.                                  02020001
      *                                                                 02030001
           PERFORM 8000-EOJ-ROUTINE.                                    02040001
                                                                        02050001
           STOP RUN.                                                    02060001
                                                                        02070001
      ******************************************************************02080001
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02090001
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02100001
      ******************************************************************02110001
      *                                                                 02120001
       1000-INITIALIZATION.                                             02130001
      *                                                                 02140001
           OPEN INPUT DBV-CSV-END-FILE.                                 02150001
      *                                                                 02160001
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02170001
      *                                                                 02180001
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02190001
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02200001
                PERFORM 7500-SELECT-RESTART-INFO                        02210001
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02220001
                                             CR-CHECKPOINT-RESTART-VAR  02230001
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02240001
           ELSE                                                         02250001
               SET PS-FIRST-COMMIT TO TRUE                              02260001
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02270001
           END-IF.                                                      02280001
      *                                                                 02290001
           PERFORM 4000-READ-CSV-FILE UNTIL                             02300001
              (PA-INPUT-COUNT > CR-INPUT-READ-COUNT)                    02310001
               OR                                                       02320001
               PS-EOF-CSV-FILE.                                         02330001
      *                                                                 02340001
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02350001
               DISPLAY 'DEPT BRAND VENDOR DKEY ' ML621-DEPT-BRND-VND-ID 02360002
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02410001
                        PA-INPUT-COUNT                                  02420001
           END-IF.                                                      02430001
                                                                        02440001
           IF PS-EOF-CSV-FILE                                           02450001
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02460001
                   CONTINUE                                             02470001
               ELSE                                                     02480001
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02490001
           ELSE                                                         02500001
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02510001
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02520001
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02530001
           END-IF.                                                      02540001
      *                                                                 02550001
      ******************************************************************02560001
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02570001
      ******************************************************************02580001
       2000-PROCESS-INPUT.                                              02590001
      *                                                                 02600001
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02610001
      *                                                                 02620001
                                                                        02646001
           IF PS-PROGRAM-DEADLOCKED                                     02650001
               CONTINUE                                                 02660001
           ELSE                                                         02670001
               IF ML621-REC-INACTIVATE                                  02671001
                  PERFORM 7000-UPDATE-IMT-DEPT-BRND-VND                 02672001
               END-IF                                                   02673001
               PERFORM 7700-COMMIT-PROCESSING                           02674001
               PERFORM 4000-READ-CSV-FILE                               02675001
           END-IF.                                                      02780001
      *                                                                 02790001
      ******************************************************************03150001
      * READS THE DBV FILE INTO THE TMNSCLS LAYOUT                      03160002
      ******************************************************************03170001
       4000-READ-CSV-FILE.                                              03180001
           READ DBV-CSV-END-FILE INTO ML621-CSV-TE-REC                  03190001
              AT END                                                    03200001
                MOVE 'Y' TO PS-EOF-CSV-FILE-SW.                         03210001
     *                                                                  03220001
           IF PS-EOF-CSV-FILE                                           03230001
               MOVE ML621-DEPT-BRND-VND-ID    TO CR-DEPT-BRND-VND-ID    03240002
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03290001
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03300001
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03310001
                                             TCKRSTINF-WORK-STRG-DESC   03320001
               EXEC SQL                                                 03330001
                 UPDATE TCKRSTINF                                       03340001
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03350001
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03360001
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03370001
               END-EXEC                                                 03380001
               IF SQLCODE = 0                                           03390001
                   CONTINUE                                             03400001
               ELSE                                                     03410001
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED  03420002
                   MOVE '* 4000-READ-CSV-FILE *' TO                     03430001
                        PV-PARAGRAPH-NAME                               03440001
                   PERFORM 9999-SQL-ABEND                               03450001
               END-IF                                                   03460001
           ELSE                                                         03470001
               ADD 1                         TO PA-INPUT-COUNT          03480001
           END-IF.                                                      03490001
      *                                                                 03500001
      ***************************************************************** 04090001
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04100001
      * SUB CLASS AND STORE. CURRENT TABLE VALUES UPDATED TO INCLUDE    04110001
      * VALUES FROM INPUT REC MLRD105                                   04120001
      ***************************************************************** 04130001
       7000-UPDATE-IMT-DEPT-BRND-VND.                                   04140001
                                                                        04150001
           MOVE ML621-DEPT-BRND-VND-ID   TO IMT00017-DEPT-BRND-VND-ID.  04150102
           MOVE ML621-DEPT-NBR           TO IMT00017-DEPT-NBR.          04150202
                                                                        04150302
           EXEC SQL                                                     04190001
               UPDATE IMT_DEPT_BRND_VND                                 04200001
                   SET EFF_END_DTE = CURRENT DATE                       04210001
              WHERE   DEPT_BRND_VND_ID     = :IMT00017-DEPT-BRND-VND-ID 04310002
                AND   DEPT_NBR             = :IMT00017-DEPT-NBR         04330002
           END-EXEC                                                     04360001
      *                                                                 04370001
           EVALUATE SQLCODE                                             04380001
               WHEN 0                                                   04390001
                   CONTINUE                                             04400001
               WHEN -911                                                04410001
                   ADD +1             TO PV-DEADLOCK-COUNT              04420001
                   DISPLAY 'DEADLOCK -911 IN '                          04430001
                           '7000-UPDATE-IMT-DEPT-BRND-VND'              04431001
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04440001
                       MOVE '7000-UPDATE-IMT-DEPT-BRND-VND' TO          04450001
                                                    PV-PARAGRAPH-NAME   04460001
                       PERFORM 9000-DEADLOCK-ERROR                      04470001
                   ELSE                                                 04480001
                       PERFORM 7999-RESTART-PROCESS                     04490001
                   END-IF                                               04500001
               WHEN OTHER                                               04510001
                   MOVE '7000-UPDATE-IMT-DEPT-BRND-VND'                 04520001
                                          TO PV-PARAGRAPH-NAME          04521001
                   MOVE PE-MSG-01         TO PV-OPERATION-ATTEMPTED     04530001
                   PERFORM 9999-SQL-ABEND                               04540001
           END-EVALUATE.                                                04550001
                                                                        04560001
           ADD 1 TO PA-UPDATED-COUNT.                                   04570001
                                                                        04580001
                                                                        05330001
      ******************************************************************05340001
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05350001
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05360001
      *            CONTROL TABLE                                       *05370001
      ******************************************************************05380001
       7300-GET-COMMIT-TIMESTAMP.                                       05390001
                                                                        05400001
      * CHECKPOINT TAKEN BASED ON DB2 CHECKPOINT RESTART TABLE          05410001
           EXEC SQL                                                     05420001
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05430001
               INTO :PV-COMMIT-TIMESTAMP                                05440001
               FROM TCKRSTCNTL                                          05450001
              WHERE JOB_NAME  = :PC-JOB-NAME                            05460001
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05470001
           END-EXEC.                                                    05480001
                                                                        05490001
           EVALUATE TRUE                                                05500001
               WHEN SQLCODE = 0                                         05510001
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05520001
                   CONTINUE                                             05530001
               WHEN SQLCODE NOT EQUAL ZERO                              05540001
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05550001
                   PERFORM 9999-SQL-ABEND                               05560001
           END-EVALUATE.                                                05570001
      *                                                                 05580001
      *                                                                 05590001
      ******************************************************************05600001
      * 7400-INIT-CHECKPOINT-SELECT                                    *05610001
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05620001
      *            IF WE ARE IN A RESTART CONDITION.                   *05630001
      ******************************************************************05640001
       7400-INIT-CHECKPOINT-SELECT.                                     05650001
                                                                        05660001
           EXEC SQL                                                     05670001
             SELECT  CKPT_STAT_CODE                                     05680001
                    ,CKPT_FRQNCY_QTY                                    05690001
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05700001
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05710001
               FROM  TCKRSTCNTL                                         05720001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05730001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05740001
           END-EXEC.                                                    05750001
                                                                        05760001
           IF SQLCODE = 0                                               05770001
               CONTINUE                                                 05780001
           ELSE                                                         05790001
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05800001
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05810001
               PERFORM 9999-SQL-ABEND                                   05820001
           END-IF.                                                      05830001
      *                                                                 05840001
      *                                                                 05850001
      ******************************************************************05860001
      * 7500-SELECT-RESTART-INFO                                       *05870001
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05880001
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05890001
      ******************************************************************05900001
       7500-SELECT-RESTART-INFO.                                        05910001
                                                                        05920001
           EXEC SQL                                                     05930001
             SELECT  WORK_STRG_DESC                                     05940001
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05950001
               FROM  TCKRSTINF                                          05960001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05970001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05980001
           END-EXEC.                                                    05990001
                                                                        06000001
           IF SQLCODE = 0                                               06010001
               CONTINUE                                                 06020001
           ELSE                                                         06030001
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06040001
               PERFORM 9999-SQL-ABEND                                   06050001
           END-IF.                                                      06060001
                                                                        06070001
      ******************************************************************06080001
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06090001
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06100001
      ******************************************************************06110001
       7600-SET-CURRENT-TIMESTAMP.                                      06120001
                                                                        06130001
           EXEC SQL                                                     06140001
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06150001
           END-EXEC.                                                    06160001
           IF SQLCODE = 0                                               06170001
               CONTINUE                                                 06180001
           ELSE                                                         06190001
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06200001
               PERFORM 9999-SQL-ABEND                                   06210001
           END-IF.                                                      06220001
                                                                        06230001
      ******************************************************************06240001
      * 7700-COMMIT-PROCESSING.                                        *06250001
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06260001
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06270001
      ******************************************************************06280001
       7700-COMMIT-PROCESSING.                                          06290001
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06300001
                                                                        06310001
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06320001
                                                                        06330001
      * PREPARE COMMIT RECORD FOR UPDATE                                06340001
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06350001
               MOVE ML621-DEPT-BRND-VND-ID    TO CR-DEPT-BRND-VND-ID    06360002
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06410001
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06420001
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06430001
                                             TCKRSTINF-WORK-STRG-DESC   06440001
               IF PS-FIRST-COMMIT                                       06450001
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06460001
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06470001
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06480001
               END-IF                                                   06490001
               PERFORM 7800-COMMIT-CHANGES                              06500001
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06510001
           END-IF.                                                      06520001
      *                                                                 06530001
      *                                                                 06540001
      ******************************************************************06550001
      * 7800-COMMIT-CHANGES.                                            06560001
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06570001
      *            TAKE A COMMIT.                                       06580001
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06590001
      ******************************************************************06600001
       7800-COMMIT-CHANGES.                                             06610001
                                                                        06620001
           EXEC SQL                                                     06630001
             UPDATE TCKRSTINF                                           06640001
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06650001
              WHERE JOB_NAME       = :PC-JOB-NAME                       06660001
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06670001
           END-EXEC.                                                    06680001
                                                                        06690001
           IF SQLCODE = 0                                               06700001
               CONTINUE                                                 06710001
           ELSE                                                         06720001
               MOVE PE-MSG-02                  TO PV-OPERATION-ATTEMPTED06730002
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06740001
               PERFORM 9999-SQL-ABEND                                   06750001
           END-IF.                                                      06760001
                                                                        06770001
           EXEC SQL                                                     06780001
               COMMIT                                                   06790001
           END-EXEC.                                                    06800001
                                                                        06810001
           IF SQLCODE = 0                                               06820001
               CONTINUE                                                 06830001
           ELSE                                                         06840001
               MOVE PE-MSG-03                  TO PV-OPERATION-ATTEMPTED06850002
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06860001
               PERFORM 9999-SQL-ABEND                                   06870001
           END-IF.                                                      06880001
      *                                                                 06890001
      *                                                                 06900001
      ******************************************************************06910001
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06920001
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06930001
      *                                                                 06940001
      ******************************************************************06950001
       7900-UPDATE-CHECKPOINT-STATUS.                                   06960001
                                                                        06970001
           EXEC SQL                                                     06980001
             UPDATE  TCKRSTCNTL                                         06990001
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07000001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07010001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07020001
           END-EXEC.                                                    07030001
                                                                        07040001
           IF SQLCODE = 0                                               07050001
               CONTINUE                                                 07060001
           ELSE                                                         07070001
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07080001
               PERFORM 9999-SQL-ABEND                                   07090001
           END-IF.                                                      07100001
                                                                        07110001
           EJECT                                                        07120001
      *                                                                 07130001
      *                                                                 07140001
      ******************************************************************07150001
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST IMT_DEPT_BRND_VND     07160002
      * ROW FOR UPDATE                                                  07161002
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07170001
      ******************************************************************07180001
       7999-RESTART-PROCESS.                                            07190001
      *                                                                 07200001
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07210001
      *                                                                 07220001
           CLOSE DBV-CSV-END-FILE.                                      07230002
      *                                                                 07240001
           PERFORM 7500-SELECT-RESTART-INFO.                            07250001
      *                                                                 07260001
           DISPLAY '**** RESTART **** '.                                07270001
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07280001
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07290001
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07300001
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07310001
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                       07320001
           IF PS-FIRST-COMMIT                                           07330001
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07340001
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07350001
                      ' BEGINNING'                                      07360001
           ELSE                                                         07370001
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07380001
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 07390001
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07400001
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07410001
                    CR-CHECKPOINT-RESTART-AREA                          07420001
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07430001
               DISPLAY 'DEPT BRND VEND ID ' CR-DEPT-BRND-VND-ID         07440002
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07490001
           END-IF.                                                      07500001
                                                                        07510001
           OPEN INPUT DBV-CSV-END-FILE.                                 07520002
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07530001
      *                                                                 07540001
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07550001
           PERFORM 4000-READ-CSV-FILE                                   07560001
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07570001
               OR PS-EOF-CSV-FILE.                                      07580001
                                                                        07590001
           IF PS-EOF-CSV-FILE                                           07600001
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07610001
           ELSE                                                         07620001
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07630001
                        PA-INPUT-COUNT                                  07640001
               DISPLAY 'DEPT BRAND VEND ID   ' ML621-DEPT-BRND-VND-ID   07650002
           END-IF.                                                      07700001
                                                                        07710001
      ******************************************************************07720001
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07730001
      ******************************************************************07740001
       8000-EOJ-ROUTINE.                                                07750001
      *                                                                 07760001
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07770001
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07780001
      *                                                                 07790001
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07800001
           DISPLAY 'ROWS INSERTED            ', PA-INSERTED-COUNT.      07810001
           DISPLAY 'ROWS UPDATED             ', PA-UPDATED-COUNT.       07820001
      *                                                                 07830001
           CLOSE DBV-CSV-END-FILE.                                      07840002
      *                                                                 07850001
      *                                                                 07860001
      ******************************************************************07870001
      *  PERFROMED BY 7000-UPDATE                                       07880001
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07890001
      ******************************************************************07900001
       9000-DEADLOCK-ERROR.                                             07910001
      *                                                                 07920001
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07930002
               PERFORM 9999-SQL-ABEND.                                  07940001
      *                                                                 07950001
      ******************************************************************07960001
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07970001
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07980001
      ******************************************************************07990001
       9999-SQL-ABEND.                                                  08000001
                                                                        08010001
           MOVE +4013                 TO ABEND-CODE.                    08020001
           DISPLAY '**  ABEND     **'.                                  08030001
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08040001
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08050001
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08060001
                                                                        08070001
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08080001
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08090001
                                                                        08100001
           COPY DPPD004.                                                08110001
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08120001
