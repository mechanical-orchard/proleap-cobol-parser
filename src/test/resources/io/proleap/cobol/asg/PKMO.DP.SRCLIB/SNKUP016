00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP016
00003  PROGRAM-ID.    SNKUP016.                                            LV003
00004                                                                   SNKUP016
00005  AUTHOR.        ANNE YEKO.                                        SNKUP016
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP016
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP016
00008  DATE-COMPILED.                                                   SNKUP016
00009                                                                   SNKUP016
00010 ******************************************************************SNKUP016
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP016
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP016
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASNCAR TABLE.          **SNKUP016
00014 ******************************************************************SNKUP016
00015 * CHANGED 07/05/2022  BY BOB MCASEY                             **SNKUP009
00016 *   CHG0274374  CHANGE COMMIT FREQUENCY FROM 100 TO 50          **SNKUP009
00016 *               PER THE REQUEST OF THE DBA                      **SNKUP009
00018 *                                                               **SNKUP009
00019 ******************************************************************SNKUP016
00020                                                                   SNKUP016
00021      EJECT                                                        SNKUP016
00022                                                                   SNKUP016
00023  ENVIRONMENT DIVISION.                                            SNKUP016
00024                                                                   SNKUP016
00025  INPUT-OUTPUT SECTION.                                            SNKUP016
00026                                                                   SNKUP016
00027  FILE-CONTROL.                                                    SNKUP016
00028                                                                   SNKUP016
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP016
00030             ASSIGN TO INP01.                                      SNKUP016
00031                                                                   SNKUP016
00032  DATA DIVISION.                                                   SNKUP016
00033                                                                   SNKUP016
00034  FILE SECTION.                                                    SNKUP016
00035                                                                   SNKUP016
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP016
00037      RECORDING MODE IS F                                          SNKUP016
00038      LABEL RECORDS ARE STANDARD                                   SNKUP016
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP016
00041                                                                   SNKUP016
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP016
00044      EJECT                                                        SNKUP016
00045  WORKING-STORAGE SECTION.                                         SNKUP016
00046                                                                   SNKUP016
00047  01  FILLER.                                                      SNKUP016
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP016
00049          ' ***** WORKING STORAGE PGM=SNKUP016 '.                  SNKUP016
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP016
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP016
00052          ' LOCATION='.                                            SNKUP016
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP016
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP016
00055          ' *****'.                                                SNKUP016
00056                                                                   SNKUP016
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP016
00058                                              COMP SYNC.           SNKUP016
00059                                                                   SNKUP016
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP016
00061                                                                   SNKUP016
00062      EJECT                                                        SNKUP016
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP016
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP016
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP016
00066                                                                   SNKUP016
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP016
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP016
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP016
00070                                                                   SNKUP016
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP016
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP016
00073                                              COMP-3.              SNKUP016
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP016
00075                                              COMP-3.              SNKUP016
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP016
00077                                              COMP-3.              SNKUP016
00078                                                                   SNKUP016
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP016
00080                                                                   SNKUP016
00081      COPY SNRD002I.                                               SNKUP016
00082      EJECT                                                        SNKUP016
00083 ******************************************************************SNKUP016
00084 *    DB2 DATABASE AREA                                            SNKUP016
00085 ******************************************************************SNKUP016
00086                                                                   SNKUP016
00087 *    ASN CARTON                                                   SNKUP016
00088                                                                   SNKUP016
00089      EXEC SQL                                                     SNKUP016
00090          INCLUDE TASNCAR                                          SNKUP016
00091      END-EXEC.                                                    SNKUP016
00092                                                                   SNKUP016
00093      EJECT                                                        SNKUP016
00094 *    SQL COMMUNICATION AREA                                       SNKUP016
00095                                                                   SNKUP016
00096      EXEC SQL                                                     SNKUP016
00097          INCLUDE SQLCA                                            SNKUP016
00098      END-EXEC.                                                    SNKUP016
00099      EJECT                                                        SNKUP016
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP016
00101                                                                   SNKUP016
00102      COPY SQLCA2.                                                 SNKUP016
00103      EJECT                                                        SNKUP016
00104                                                                   SNKUP016
00105 ***************************************************************** SNKUP016
00106  PROCEDURE DIVISION.                                              SNKUP016
00107 ***************************************************************** SNKUP016
00108                                                                   SNKUP016
00109  A000-MAIN-DRIVER.                                                SNKUP016
00110                                                                   SNKUP016
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP016
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP016
00113                                                                   SNKUP016
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP016
00115                                                                   SNKUP016
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP016
00117                                                                   SNKUP016
00118      PERFORM B100-DELETE-ROWS                                     SNKUP016
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP016
00120                                                                   SNKUP016
00121      DISPLAY 'SNKUP016 RECORDS READ:    ' PV-RECS-READ.           SNKUP016
00122      DISPLAY 'SNKUP016 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP016
00123                                                                   SNKUP016
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP016
00125                                                                   SNKUP016
00126      GOBACK.                                                      SNKUP016
00127                                                                   SNKUP016
00128      EJECT                                                        SNKUP016
00129                                                                   SNKUP016
00130 ***************************************************************** SNKUP016
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP016
00132 ***************************************************************** SNKUP016
00133  B100-DELETE-ROWS.                                                SNKUP016
00134                                                                   SNKUP016
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP016
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP016
00137                                  TO ABEND-PARAGRAPH.              SNKUP016
00138                                                                   SNKUP016
00139      PERFORM B300-DELETE-ASNCAR.                                  SNKUP016
0922RM*    IF PV-DELETE-COUNTER > +100                                     CL**2
0922RM     IF PV-DELETE-COUNTER > +50                                      CL**2
00141          PERFORM B400-COMMIT                                      SNKUP016
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP016
00143      END-IF.                                                      SNKUP016
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP016
00145                                                                   SNKUP016
00146      EJECT                                                        SNKUP016
00147                                                                   SNKUP016
00148 ***************************************************************** SNKUP016
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP016
00150 ***************************************************************** SNKUP016
00151  B200-READ-INPUT-FILE.                                            SNKUP016
00152                                                                   SNKUP016
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP016
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP016
00155                                  TO ABEND-PARAGRAPH.              SNKUP016
00156                                                                   SNKUP016
00157      READ ASNHDR-DELETE-FILE                                      SNKUP016
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP016
00159          AT END                                                   SNKUP016
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP016
00161                                                                   SNKUP016
00162      IF NOT PS-END-OF-INPUT                                       SNKUP016
00163          ADD 1                   TO PV-RECS-READ                  SNKUP016
00164      END-IF.                                                      SNKUP016
00165                                                                   SNKUP016
00166      EJECT                                                        SNKUP016
00167                                                                   SNKUP016
00168 ***************************************************************** SNKUP016
00169 *    THIS PARAGRAPH DELETES ALL ASNCAR ROWS THAT ARE EQUAL TO     SNKUP016
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP016
00171 ***************************************************************** SNKUP016
00172  B300-DELETE-ASNCAR.                                              SNKUP016
00173                                                                   SNKUP016
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP016
00175      MOVE 'B300-DELETE-ASNCAR        '                            SNKUP016
00176                                  TO ABEND-PARAGRAPH.              SNKUP016
00177                                                                   SNKUP016
00178      EXEC SQL                                                     SNKUP016
00179          DELETE                                                   SNKUP016
00180          FROM   TASNCAR                                           SNKUP016
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP016
00183                                                                   SNKUP016
00184      IF SQLCODE = ZERO                                            SNKUP016
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP016
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP016
00187      ELSE                                                         SNKUP016
00188          IF SQLCODE = +100                                        SNKUP016
00189              CONTINUE                                             SNKUP016
00190          ELSE                                                     SNKUP016
00191              PERFORM Z998-SQL-ERROR                               SNKUP016
00192          END-IF                                                   SNKUP016
00193      END-IF.                                                      SNKUP016
00194                                                                   SNKUP016
00195      EJECT                                                        SNKUP016
00196                                                                   SNKUP016
00197 ***************************************************************** SNKUP016
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP016
00199 ***************************************************************** SNKUP016
00200  B400-COMMIT.                                                     SNKUP016
00201                                                                   SNKUP016
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP016
00203      MOVE 'B400-COMMIT               '                            SNKUP016
00204                                  TO ABEND-PARAGRAPH.              SNKUP016
00205                                                                   SNKUP016
00206      EXEC SQL                                                     SNKUP016
00207          COMMIT                                                   SNKUP016
00208      END-EXEC.                                                    SNKUP016
00209                                                                   SNKUP016
00210      IF SQLCODE = ZERO                                            SNKUP016
00211          CONTINUE                                                 SNKUP016
00212      ELSE                                                         SNKUP016
00213          PERFORM Z998-SQL-ERROR                                   SNKUP016
00214      END-IF.                                                      SNKUP016
00215                                                                   SNKUP016
00216      EJECT                                                        SNKUP016
00217                                                                   SNKUP016
00218 ***************************************************************** SNKUP016
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP016
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP016
00221 ***************************************************************** SNKUP016
00222  Z998-SQL-ERROR.                                                  SNKUP016
00223                                                                   SNKUP016
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP016
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP016
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP016
00227      DISPLAY '** PROGRAM          ** = SNKUP016'.                 SNKUP016
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP016
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP016
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP016
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP016
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP016
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP016
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP016
