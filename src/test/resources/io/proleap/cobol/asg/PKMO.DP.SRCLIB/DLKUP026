000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    DLKUP026.                                                 
000300 AUTHOR.        JEAN KURKOWSKI.                                           
000400                                                                          
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  JULY 2007.                                                
000700 DATE-COMPILED.                                                           
000800                                                                          
000900 ENVIRONMENT DIVISION.                                                    
001000 CONFIGURATION SECTION.                                                   
001100 SOURCE-COMPUTER. IBM-3090.                                               
001200 OBJECT-COMPUTER. IBM-3090.                                               
001300                                                                          
001400****************************************************************          
001500*                                                              *          
001600*    THIS IS A COPY OF DLKUP003 FOR BATCH USE.  ANY CHANGES    *          
001700*    NEED TO BE MADE IN BOTH PLACES.                           *          
001800*                                                              *          
001900*    THERE ARE CURRENTLY EIGHT DIFFERENT PROCESSES THAT CAN    *          
002000*    OCCUR IN THIS PROGRAM.  THEY ALL DEAL WITH THE ASSIGN-    *          
002100*    MENTS (DLT00004) AND ASSOCIATES (DLT00003) LINKED WITH    *          
002200*    THOSE ASSIGNMENTS.                                        *          
002300*                                                              *          
002400*       TWO OF THEM WILL CREATE A NEW ASSIGNMENT FROM THE      *          
002500*           INPUT PASSED IN.  ONE OF THE TWO WILL ALSO ADD     *          
002600*           AN ASSOCIATE TO THE NEW ASSIGNMENT.                *          
002700*                                                              *          
002800*       ONE WILL JUST END THE ASSIGNMENT PASSED IN, NOTHING    *          
002900*           ELSE.                                              *          
003000*                                                              *          
003100*       THE OTHER FOUR WILL ALL COPY THE EXISTING ASSIGNMENT,  *          
003200*           WITH TWO OF THEM ALSO ENDING THE ASSIGNMENT SENT   *          
003300*           IN.  TWO WILL ALSO COMBINE THE COPYING OF THE      *          
003400*           RELATED ASSOCIATES WITH THE NEW ASSIGNMENT.        *          
003500*                                                              *          
003600*    ONLY USED BY CICS PROGRAMS.                               *          
003700*                                                              *          
003800*  COPYBOOKS:                                                  *          
003900*     DLWS022 - USED WHEN CALLING THIS PROGRAM                 *          
004000*     DLWS020 - USED WHEN CALLING DLKUP024                     *          
004100*     DLWS011 - USED TO FORMAT KEY FIELDS                      *          
004200*     DLWS023 - USED WHEN CALLING DLKUT027                     *          
004300*     XLWS091 - USED WHEN CALLING XLKUP046 WHICH WRITES        *          
004400*               INFORMATION TO THE EAI EXPORT TABLES.          *          
004500*                                                              *          
004600*  DB2 ACCESS:                                                 *          
004700*     DLT_TEM_ASGMT (DLT00003) TABLE                           *          
004800*                FETCHES                                       *          
004900*                INSERTS                                       *          
005000*                UPDATES                                       *          
005100*                                                              *          
005200*     DLT_ASGMT (DLT00004) TABLE                               *          
005300*                INSERTS                                       *          
005400*                SELECTS                                       *          
005500*                UPDATES                                       *          
005600*                                                              *          
005700****************************************************************          
005800                                                                          
005900 DATA DIVISION.                                                           
006000 WORKING-STORAGE SECTION.                                                 
006100                                                                          
006200 01  PV-DISPLAY-FIELDS.                                                   
006300     05  PV-DISPLAY-FIELD         PIC 9(09)  VALUE ZEROS.                 
006400     05  PV-DISPLAY-NUMBER        PIC 9(09)  VALUE ZEROS.                 
006500     05  PV-CICS-MSG-AREA2        PIC X(100) VALUE SPACE.                 
006600                                                                          
006700 01  PV-CHARACTER-FIELDS.                                                 
006800     05  PV-CURRENT-DATE          PIC X(10)  VALUE SPACES.                
006900     05  PV-CURRENT-TIMESTAMP     PIC X(26)  VALUE SPACES.                
007000     05  PV-TIMESTAMP             PIC X(26)  VALUE SPACES.                
007100     05  PV-ASGMT-STRT-DTE        PIC X(10)  VALUE SPACES.                
007200     05  PV-CURRENT-TIME          PIC X(08)  VALUE SPACES.                
007300     05  PV-ASGMT-NBR-WRK-TYP-SAV PIC X(06)  VALUE SPACES.                
007400     05  PV-ASGMT-NBR-TXT         PIC X(10)  VALUE SPACES.                
007500     05  FILLER REDEFINES PV-ASGMT-NBR-TXT.                               
007600         10  PV-ASGMT-NBR-DC          PIC 9(04).                          
007700         10  PV-ASGMT-NBR-WRK-TYP     PIC X(06).                          
007800             88  PV-RECV-WRK-TYP          VALUE 'RECV  '.                 
007900             88  PV-RECV1-WRK-TYP         VALUE 'RECV1 '.                 
008000             88  PV-FLAT-WRK-TYP          VALUE 'FLAT  '.                 
008100             88  PV-SHIP-WRK-TYP          VALUE 'SHIP  '.                 
008200             88  PV-P2L-WRK-TYP           VALUE 'P2L   '.                 
008300             88  PV-P2L1-WRK-TYP          VALUE 'P2L1  '.                 
008400             88  PV-RLABL-WRK-TYP         VALUE 'RLABL '.                 
008500     05  PV-ASGMT-NBR-SEQ-NBR-DISP    PIC 9(4).                           
008600     05  PV-BUILD-TIMESTAMP.                                              
008700         10  PV-BUILD-TIMESTAMP-DTE                                       
008800                                  PIC X(10)  VALUE SPACES.                
008900         10  FILLER               PIC X(16)  VALUE                        
009000             '-01.01.01.000001'.                                          
009100                                                                          
009200 01  PV-COMP-FIELDS.                                                      
009300     05  PV-RETRY-COUNTER         PIC S9(04) VALUE 0 COMP.                
009400     05  PV-ASGMT-NBR-SEQ-NBR     PIC S9(09) VALUE 0 COMP.                
009500     05  PV-KOHLS-USR-ID-NULL-IND PIC S9(04) VALUE 0 COMP.                
009600     05  PV-TMP-USR-ID-NULL-IND   PIC S9(04) VALUE 0 COMP.                
009700     05  PV-SET-SEQ-NBR           PIC S9(09) VALUE 0 COMP.                
009800     05  PV-ASSOCIATE-COUNT       PIC S9(09) VALUE 0 COMP.                
009900     05  PV-DOOR-COUNT            PIC S9(09) VALUE 0 COMP.                
010000     05  PV-DIVERT-COUNT          PIC S9(09) VALUE 0 COMP.                
010100     05  PV-SD-WRK-AREA-DOOR-GROUP PIC S9(4) VALUE 0 COMP.                
010200     05  PV-COMMAREA-LENGTH       PIC S9(04) VALUE 0 COMP.                
010300     05  PV-CICS-RESPONSE         PIC S9(08) VALUE 0 COMP.                
010400                                                                          
010500 01  PS-SWITCHES.                                                         
010600     05  PS-ASSIGNMENT-SW             PIC X(01)  VALUE 'N'.               
010700         88  PS-ASSIGNMENT-FOUND                 VALUE 'Y'.               
010800         88  PS-ASSIGNMENT-NOT-FOUND             VALUE 'N'.               
010900     05  PS-ASSOCIATE-UPDATE-SW       PIC X(01)  VALUE 'N'.               
011000         88  PS-ASSOCIATE-UPDATED                VALUE 'Y'.               
011100         88  PS-ASSOCIATE-NOT-UPDATED            VALUE 'N'.               
011200     05  PS-ASSIGNMENT-UPDATE-SW      PIC X(01)  VALUE 'N'.               
011300         88  PS-ASSIGNMENT-UPDATED               VALUE 'Y'.               
011400         88  PS-ASSIGNMENT-NOT-UPDATED           VALUE 'N'.               
011500     05  PS-END-OF-CURSOR-SW          PIC X(01)  VALUE 'N'.               
011600         88  PS-END-OF-CURSOR                    VALUE 'Y'.               
011700         88  PS-NOT-END-OF-CURSOR                VALUE 'N'.               
011800     05  PS-DLPLDU-ROW-SW             PIC X(01)  VALUE 'N'.               
011900         88  PS-DLPLDU-ROW-FOUND                 VALUE 'Y'.               
012000         88  PS-DLPLDU-ROW-NOT-FOUND             VALUE 'N'.               
012100     05  PS-TEAM-ROW-SW               PIC X(01)  VALUE 'N'.               
012200         88  PS-TEAM-ROW-FOUND                   VALUE 'Y'.               
012300         88  PS-TEAM-ROW-NOT-FOUND               VALUE 'N'.               
012400                                                                          
012500 01  PC-CONSTANTS.                                                        
012600     05  PC-MAX-RETRIES           PIC S9(04) VALUE +3 COMP.               
012700     05  PC-A                     PIC X      VALUE 'A'.                   
012800     05  PC-N                     PIC X      VALUE 'N'.                   
012900     05  PC-AC                    PIC X(02)  VALUE 'AC'.                  
013000     05  PC-DLX                   PIC X(03)  VALUE 'DLX'.                 
013100     05  PC-DLPLDU                PIC X(06)  VALUE 'DLPLDU'.              
013200     05  PC-RLABL                 PIC X(05)  VALUE 'RLABL'.               
013300     05  PC-ISTOP                 PIC X(05)  VALUE 'ISTOP'.               
013400     05  PC-DLKUP026              PIC X(08)  VALUE 'DLKUP026'.            
013500     05  PC-INDIRECT-LABOR        PIC X(40)  VALUE                        
013600         'INDIRECT LABOR                          '.                      
013700     05  PC-ASSIGNMENT-CLOSE      PIC X(40)  VALUE                        
013800         'ASSIGNMENT CLOSE                        '.                      
013900     05  PC-DC-LABOR              PIC X(40)  VALUE                        
014000         'DC LABOR                                '.                      
014100     05  PC-ASTERISK-LINE         PIC X(40)  VALUE ALL '*'.               
014200     05  PC-FIVE-BLANKS           PIC X(05)  VALUE '     '.               
014300     05  PC-TEN-BLANKS            PIC X(10)  VALUE SPACES.                
014400                                                                          
014500****************************************************************          
014600*    DLT00003 - DLT_TEM_ASGMT                                  *          
014700*    DLT00004 - DLT_ASGMT                                      *          
014800*                                                              *          
014900****************************************************************          
015000                                                                          
015100     EXEC SQL                                                             
015200         INCLUDE DLT00000                                                 
015300     END-EXEC.                                                            
015400                                                                          
015500     EXEC SQL                                                             
015600         INCLUDE DLT00001                                                 
015700     END-EXEC.                                                            
015800                                                                          
015900     EXEC SQL                                                             
016000         INCLUDE DLT00003                                                 
016100     END-EXEC.                                                            
016200                                                                          
016300     EXEC SQL                                                             
016400         INCLUDE DLT00004                                                 
016500     END-EXEC.                                                            
016600                                                                          
016700     EXEC SQL                                                             
016800         INCLUDE DLT00009                                                 
016900     END-EXEC.                                                            
017000                                                                          
017100     EXEC SQL                                                             
017200         INCLUDE TKRPRPM                                                  
017300     END-EXEC.                                                            
017400                                                                          
017500     EXEC SQL                                                             
017600         INCLUDE TSUCRDV                                                  
017700     END-EXEC.                                                            
017800                                                                          
017900     EXEC SQL                                                             
018000          INCLUDE SQLCA                                                   
018100     END-EXEC.                                                            
018200                                                                          
018300****************************************************************          
018400*    COPYBOOK FOR XLKUT107 - WRITE DATA TO EAI EXPORT TABLES   *          
018500****************************************************************          
018600                                                                          
018700     COPY XLWS091.                                                        
018800                                                                          
018900     COPY DLWS020.                                                        
019000                                                                          
019100     COPY DLWS011.                                                        
019200                                                                          
019300     COPY DLWS023.                                                        
019400                                                                          
019500     EXEC SQL                                                             
019600        DECLARE ASSOCIATE-CURSOR CURSOR FOR                               
019700         SELECT                                                           
019800                TEM.ASGMT_STRT_DTE                                        
019900               ,TEM.ASGMT_NBR_TXT                                         
020000               ,TEM.KOHLS_USR_ID                                          
020100               ,TEM.TMP_USR_ID                                            
020200               ,TEM.JN_TMST                                               
020300               ,TEM.CRE_BY_USR_ID                                         
020400                                                                          
020500          FROM  DLT_TEM_ASGMT   TEM                                       
020600                                                                          
020700         WHERE  TEM.ASGMT_STRT_DTE    = :PV-BUILD-TIMESTAMP-DTE           
020800           AND  TEM.ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                 
020900           AND  TEM.ASGMT_NBR_SEQ_NBR =                                   
021000                              :PV-ASGMT-NBR-SEQ-NBR                       
021100           AND  TEM.LVE_RSN_CDE       = 'T'                               
021101           AND TEM.TEM_ASGMT_SEQ_NBR  IN                                  
021102               (SELECT MAX(B.TEM_ASGMT_SEQ_NBR)                           
021120                  FROM DLT_TEM_ASGMT B                                    
021130                 WHERE TEM.ASGMT_STRT_DTE    = B.ASGMT_STRT_DTE           
021140                   AND TEM.ASGMT_NBR_TXT     = B.ASGMT_NBR_TXT            
021150                   AND TEM.ASGMT_NBR_SEQ_NBR                              
021160                                  = B.ASGMT_NBR_SEQ_NBR                   
021170                   AND ((TEM.KOHLS_USR_ID = B.KOHLS_USR_ID AND            
021180                         B.TMP_USR_ID IS NULL                             
021190                        )                                                 
021191                         OR                                               
021192                        (TEM.TMP_USR_ID = B.TMP_USR_ID AND                
021193                         B.KOHLS_USR_ID IS NULL                           
021194                        )                                                 
021195                       )                                                  
021196               )                                                          
021200     END-EXEC.                                                            
021300                                                                          
021400 LINKAGE SECTION.                                                         
021500     COPY DLWS022.                                                        
021600                                                                          
021700                                                                          
021800 PROCEDURE DIVISION USING DL022-ASGMT-COMMAREA.                           
021900                                                                          
021910     INITIALIZE  PS-SWITCHES                                              
021911                 PV-DISPLAY-FIELDS                                        
021912                 PV-CHARACTER-FIELDS                                      
021913                 PV-COMP-FIELDS.                                          
021920                                                                          
022000     SET DL022-SUCCESSFUL  TO TRUE.                                       
022100                                                                          
022200     EVALUATE TRUE                                                        
022300         WHEN DL022-VALID-PROCESS-CODE                                    
022400              PERFORM 1000-VALIDATE-INPUT-FIELDS                          
022500                                                                          
022600         WHEN OTHER                                                       
022700              SET DL022-INVALID-ACCESS TO TRUE                            
022800                                                                          
022900     END-EVALUATE.                                                        
023000                                                                          
023100     MOVE SQLCA                       TO DL022-SQLCA.                     
023200     MOVE SQLCODE                     TO DL022-ASGMT-SQLCODE.             
023300                                                                          
023400     GOBACK.                                                              
023500                                                                          
023600                                                                          
023700****************************************************************          
023800*    VALIDATE THAT ALL INPUT FIELDS ARE PRESENT.               *          
023900*        THIS VARIES DEPENDING ON THE TYPE OF PROCESSING CODE  *          
024000*        THAT IS BEING REQUESTED.                              *          
024100*                                                              *          
024200*        STOP PROCESSING AND RETURN IF THERE ARE ANY ERRORS.   *          
024300*                                                              *          
024400****************************************************************          
024500                                                                          
024600 1000-VALIDATE-INPUT-FIELDS.                                              
024700                                                                          
024800     EVALUATE TRUE                                                        
024900         WHEN DL022-CREATE-NEW-ASGMT OR                                   
025000              DL022-CREATE-NEW-ASGMT-WTEAM                                
025100              EVALUATE TRUE                                               
025200                  WHEN DL022-NA-FLD-START-DTE = SPACES                    
025300                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
025400                                                                          
025500                  WHEN DL022-NA-FLD-NO-NBR-TXT                            
025600                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
025700                                                                          
025800                  WHEN DL022-NA-FLD-NO-CRE-USR                            
025900                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
026000                                                                          
026100                  WHEN DL022-NA-FLD-NO-CRE-PGM                            
026200                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
026300                                                                          
026400                  WHEN DL022-NA-FLD-NO-WK-TYP                             
026500                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
026600                                                                          
026700                  WHEN DL022-NA-FLD-NO-WK-AREA                            
026800                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
026900                                                                          
027000                  WHEN DL022-NA-FLD-NO-DC-NBR                             
027100                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
027200                                                                          
027300                  WHEN DL022-NA-FLD-START-TM = SPACES                     
027400                       SET DL022-MISSING-NEW-FIELDS TO TRUE               
027500                                                                          
027600                  WHEN OTHER                                              
027700                       IF DL022-USR-IS-KOHLS OR                           
027800                          DL022-USR-IS-TMP                                
027900                          CONTINUE                                        
028000                       ELSE                                               
028100                          SET DL022-MISSING-NEW-FIELDS TO TRUE            
028200                       END-IF                                             
028300                                                                          
028400              END-EVALUATE                                                
028500                                                                          
028600         WHEN DL022-COPY-FROM-ASGMT OR                                    
028700              DL022-COPY-FROM-ASGMT-WTEAM                                 
028800              IF DL022-ECA-KEY-START-DTE = SPACES                         
028900                 SET DL022-MISSING-COPY-FIELDS TO TRUE                    
029000              END-IF                                                      
029100                                                                          
029200              IF DL022-NO-KEY-NBR-TXT                                     
029300                 SET DL022-MISSING-COPY-FIELDS TO TRUE                    
029400              END-IF                                                      
029500                                                                          
029600              IF DL022-NO-KEY-NBR-SEQ-NBR                                 
029700                 SET DL022-MISSING-COPY-FIELDS TO TRUE                    
029800              END-IF                                                      
029900                                                                          
030000              IF DL022-NO-STOP-PGM-NM                                     
030100                 SET DL022-MISSING-COPY-FIELDS TO TRUE                    
030200              END-IF                                                      
030300                                                                          
030400              IF DL022-NO-STOP-RSN-CDE                                    
030500                 SET DL022-MISSING-COPY-FIELDS TO TRUE                    
030600              END-IF                                                      
030700                                                                          
030800     END-EVALUATE.                                                        
030900                                                                          
031000     IF DL022-SUCCESSFUL                                                  
031100        IF DL022-COMMIT-UPDATE OR                                         
031200           DL022-DO-NOT-COMMIT-UPDATE                                     
031300           PERFORM 9400-GET-TIMESTAMP                                     
031400           PERFORM 2000-PROCESS-ASSIGNMENTS                               
031500        ELSE                                                              
031600           SET DL022-COMMIT-CODE-INVALID TO TRUE                          
031700        END-IF                                                            
031800                                                                          
031900     END-IF.                                                              
032000                                                                          
032100****************************************************************          
032200*    ENDING OF THE ASSIGNMENT NEEDS TO HAPPEN FIRST.           *          
032300*        THIS ONLY APPLIES TO DL022-END-ASGMT,                 *          
032400*                             DL022-END-COPY-ASGMT,            *          
032500*                             DL022-END-COPY-ASGMT-WTEAM.      *          
032600*                                                              *          
032700*    THE COMMIT LOGIC IS AT THE END OF THIS PARAGRAPH FOR      *          
032800*        ALL OF THE DIFFERENT PROCESS CODES.                   *          
032900*                                                              *          
033000****************************************************************          
033100                                                                          
033200 2000-PROCESS-ASSIGNMENTS.                                                
033300                                                                          
033400     EVALUATE TRUE                                                        
033500         WHEN DL022-END-ASGMT                                             
033600*             **************************************************          
033700*             *  END ASSIGNMENT AND RETURN TO CALLING PROGRAM. *          
033800*             *                                                *          
033900*             **************************************************          
034000              MOVE DL022-ECA-KEY-START-DTE TO PV-ASGMT-STRT-DTE           
034100              MOVE DL022-ECA-KEY-NBR-TXT   TO PV-ASGMT-NBR-TXT            
034200              MOVE DL022-ECA-KEY-NBR-SEQ-NBR TO                           
034300                                 PV-ASGMT-NBR-SEQ-NBR                     
034400                                                                          
034500              PERFORM 3000-SELECT-ASSIGNMENT                              
034600                                                                          
034700              IF DL022-SUCCESSFUL                                         
034800                 PERFORM 2200-END-ASSIGNMENT                              
034900              END-IF                                                      
035000                                                                          
035100              IF DL022-SUCCESSFUL                                         
035200                 IF DL022-END-COPY-ASGMT-WTEAM                            
035300                    PERFORM 2400-COPY-ASSIGNMENT                          
035400                 END-IF                                                   
035500              END-IF                                                      
035600                                                                          
035700         WHEN DL022-CREATE-NEW-ASGMT                                      
035800*             **************************************************          
035900*             *  ONLY CREATES A NEW ASSIGNMENT (DLT00004) FROM *          
036000*             *  THE INPUT FIELDS.                             *          
036100*             *                                                *          
036200*             **************************************************          
036300              MOVE DL022-NA-FLD-START-DTE TO PV-ASGMT-STRT-DTE            
036400              MOVE DL022-NA-FLD-NBR-TXT   TO PV-ASGMT-NBR-TXT             
036500                                                                          
036600                                                                          
036700              PERFORM 5000-INSERT-ASSIGNMENT                              
036800                                                                          
036900         WHEN DL022-CREATE-NEW-ASGMT-WTEAM                                
037000*             **************************************************          
037100*             *  CREATES A NEW ASSIGNMENT (DLT00004) FROM THE  *          
037200*             *  INPUT FIELDS. THEN CREATES AN ASSOCIATE       *          
037300*             *  (DLT00003) CONNECTED TO THE ASSIGNMENT. INPUT *          
037400*             *  IS ALSO TAKEN FROM THE INPUT FIELDS.          *          
037500*             **************************************************          
037600              MOVE DL022-NA-FLD-START-DTE TO PV-ASGMT-STRT-DTE            
037700              MOVE DL022-NA-FLD-NBR-TXT   TO PV-ASGMT-NBR-TXT             
037800                                                                          
037900              PERFORM 5000-INSERT-ASSIGNMENT                              
038000                                                                          
038100              IF DL022-SUCCESSFUL                                         
038200                 PERFORM 5400-INSERT-ASSOCIATE                            
038300                 PERFORM 5500-INSERT-RDPR-USR                             
038400              END-IF                                                      
038500                                                                          
038600         WHEN DL022-COPY-FROM-ASGMT OR                                    
038700              DL022-END-COPY-ASGMT                                        
038800*             **************************************************          
038900*             *  COPIES DLT_ASGMT (DLT00004) ROW TO NEW        *          
039000*             *      ASSIGNMENT.                               *          
039100*             *                                                *          
039200*             **************************************************          
039300              MOVE DL022-ECA-KEY-START-DTE TO PV-ASGMT-STRT-DTE           
039400              MOVE DL022-ECA-KEY-NBR-TXT   TO PV-ASGMT-NBR-TXT            
039500              MOVE DL022-ECA-KEY-NBR-SEQ-NBR TO                           
039600                                 PV-ASGMT-NBR-SEQ-NBR                     
039700                                                                          
039800              PERFORM 3000-SELECT-ASSIGNMENT                              
039900                                                                          
040000              IF DL022-SUCCESSFUL                                         
040100                 IF DL022-END-COPY-ASGMT                                  
040200                    PERFORM 2200-END-ASSIGNMENT                           
040300                 END-IF                                                   
040400                                                                          
040500                 IF DL022-SUCCESSFUL                                      
040600                    PERFORM 5000-INSERT-ASSIGNMENT                        
040700                 END-IF                                                   
040800              END-IF                                                      
040900                                                                          
041000         WHEN DL022-COPY-FROM-ASGMT-WTEAM OR                              
041100              DL022-END-COPY-ASGMT-WTEAM                                  
041200*             **************************************************          
041300*             *  COPIES DLT_ASGMT (DLT00004) ROW TO NEW        *          
041400*             *      ASSIGNMENT. THEN COPIES THE RELATED       *          
041500*             *      DLT_TEM_ASGMT (DLT00003) ROWS ALSO.       *          
041600*             *                                                *          
041700*             **************************************************          
041800              MOVE DL022-ECA-KEY-START-DTE TO PV-ASGMT-STRT-DTE           
041900              MOVE DL022-ECA-KEY-NBR-TXT   TO PV-ASGMT-NBR-TXT            
042000              MOVE DL022-ECA-KEY-NBR-SEQ-NBR TO                           
042100                                 PV-ASGMT-NBR-SEQ-NBR                     
042200                                                                          
042300              PERFORM 3000-SELECT-ASSIGNMENT                              
042400                                                                          
042500              IF DL022-SUCCESSFUL                                         
042600                 IF DL022-END-COPY-ASGMT-WTEAM                            
042700                    PERFORM 2200-END-ASSIGNMENT                           
042800                 END-IF                                                   
042900                                                                          
043000                 MOVE PV-ASGMT-NBR-WRK-TYP TO                             
043100                         PV-ASGMT-NBR-WRK-TYP-SAV                         
043200                 IF PV-RECV-WRK-TYP OR                                    
043300                    PV-RECV1-WRK-TYP                                      
043400                    PERFORM 8500-COUNT-ASSOCIATES                         
043500                    IF PV-ASSOCIATE-COUNT > 1                             
043600                        SET PV-RECV-WRK-TYP TO TRUE                       
043700                    ELSE                                                  
043800                        SET PV-RECV1-WRK-TYP TO TRUE                      
043900                    END-IF                                                
044000                 END-IF                                                   
044100                 IF PV-P2L-WRK-TYP OR                                     
044200                    PV-P2L1-WRK-TYP                                       
044300                    PERFORM 8500-COUNT-ASSOCIATES                         
044400                    IF PV-ASSOCIATE-COUNT > 1                             
044500                        SET PV-P2L-WRK-TYP TO TRUE                        
044600                    ELSE                                                  
044700                        SET PV-P2L1-WRK-TYP TO TRUE                       
044800                    END-IF                                                
044900                 END-IF                                                   
045000                                                                          
045100                 IF DL022-SUCCESSFUL                                      
045200                    PERFORM 5000-INSERT-ASSIGNMENT                        
045300                 END-IF                                                   
045400                                                                          
045500                 IF DL022-SUCCESSFUL                                      
045600                    MOVE PV-ASGMT-NBR-WRK-TYP-SAV TO                      
045700                         PV-ASGMT-NBR-WRK-TYP                             
045800                    PERFORM 4000-COPY-ASSOCIATES                          
045900                 END-IF                                                   
046000              END-IF                                                      
046100                                                                          
046200     END-EVALUATE.                                                        
046300                                                                          
046400*    ***********************************************************          
046500*    *  FILL IN THE OUTPUT FIELDS AFTER A SUCCESSFUL EXECUTION *          
046600*    *  OF THE FUNCTION SELECTED.                              *          
046700*    *                                                         *          
046800*    ***********************************************************          
046900     IF DL022-SUCCESSFUL                                                  
047000        IF DL022-END-ASGMT                                                
047100           CONTINUE                                                       
047200        ELSE                                                              
047300           MOVE PV-ASGMT-STRT-DTE TO                                      
047400                           DL022-NEW-START-DTE                            
047500           MOVE PV-ASGMT-NBR-TXT   TO                                     
047600                           DL022-NEW-NBR-TXT                              
047700           MOVE PV-SET-SEQ-NBR TO                                         
047800                           DL022-NEW-ASGMT-NBR-SEQ-NBR                    
047900           MOVE PV-CURRENT-TIMESTAMP TO                                   
048000                           DL022-ASGMT-END-TMST                           
048100        END-IF                                                            
048200                                                                          
048300        IF DL022-COMMIT-UPDATE                                            
048400           PERFORM 9000-COMMIT                                            
048500        END-IF                                                            
048600     END-IF.                                                              
048700                                                                          
048800                                                                          
048900****************************************************************          
049000*    PARAGRAPH HAS SEVERAL LOGIC PIECES.                       *          
049100*    1 - CHECKS TO SEE IF AN ISTOP BUSINESS EVENT NEEDS TO BE  *          
049200*        PUT OUT.                                              *          
049300*    2 - IF THIS IS A SHIPPING ASSIGNMENT AND THERE WASN'T ANY *          
049400*        ACTIVITY AGAINST IT (DLT00004-LAST-USD-SEQ-NBR = 0),  *          
049500*        CHECK TO SEE IF THERE ARE ANY DIVERTED CARTONS        *          
049600*        (PV-DIVERT-COUNT > 0).                                *          
049700*                                                              *          
049800*        IF THERE ARE, WE NEED TO PUT OUT DOOR SUMMARY AND     *          
049900*        ASSIGNMENT CLOSE BUSINESS EVENTS.                     *          
050000*                                                              *          
050100****************************************************************          
050200                                                                          
050300 2200-END-ASSIGNMENT.                                                     
050400                                                                          
050500     IF DLT00004-WK-TYP-DESC = PC-RLABL                                   
050600        PERFORM 7000-SELECT-TEAM-ASSIGNMENT                               
050700                                                                          
050800        IF PS-TEAM-ROW-FOUND                                              
050900           PERFORM 7200-SELECT-DLPLDU                                     
051000           IF PS-DLPLDU-ROW-FOUND                                         
051100              PERFORM 7600-FORMAT-XLWS072-ISTOP                           
051200           END-IF                                                         
051300        END-IF                                                            
051400     END-IF.                                                              
051500                                                                          
051600     MOVE 0 TO PV-DIVERT-COUNT.                                           
051700                                                                          
051800     IF DL022-SUCCESSFUL                                                  
051900        IF DLT00004-WK-TYP-DESC = 'SHIP'                                  
052000            PERFORM 3400-COUNT-DIVERTS                                    
052100        END-IF                                                            
052200        IF DLT00004-LAST-USD-SEQ-NBR = 0  AND                             
052300           DLT00004-WK-TYP-DESC = 'SHIP'  AND                             
052400           DL022-END-ASGMT                                                
052500****************************************************************          
052600**   THE FOLLOWING LOGIC IS SPECIAL IN THAT IT CHANGES THE     *          
052700**       PROCESS FOR AN ASSIGNMENT ONLY IF IT'S A SHIPPING     *          
052800**       ASSIGNMENT, THE LAST-USD-SEQ-NBR IS 0 AND IT HAS      *          
052900**       DIVERTED CARTONS.  THIS IS DONE SO THAT THE ASSIGN-   *          
053000**       MENT AND ITS TEAM MEMBERS ARE COPIED AND THE DOOR     *          
053100**       SUMMARY AND ASSIGNMENT CLOSE BUSINESS EVENTS ARE      *          
053200**       PUT OUT IN THIS SPECIAL CASE.                         *          
053300**                                                             *          
053400****************************************************************          
053500           IF PV-DIVERT-COUNT  > 0                                        
053600              SET DL022-END-COPY-ASGMT-WTEAM TO TRUE                      
053700              SET DL022-LOG-TO-EAI-EXPORT TO TRUE                         
053800           END-IF                                                         
053900        END-IF                                                            
054000                                                                          
054100        IF DL022-LOG-TO-EAI-EXPORT                                        
054200           MOVE PV-ASGMT-STRT-DTE TO                                      
054300                PV-BUILD-TIMESTAMP-DTE                                    
054400****************************************************************          
054500**         ONLY PUT OUT AN ASSIGNMENT CLOSE BUSINESS EVENT IF  *          
054600**         THERE HAS BEEN ACTIVITY AGAINST THIS ASSIGNMENT     *          
054700**          (DLT00004-LAST-USD-SEQ-NBR > 0)                    *          
054800**     OR  IF THE ASSIGNMENT HAS DIVERTED CARTONS              *          
054900**  (PV-DIVERT-COUNT > 0 AND DLT00004-LAST-USD-SEQ-NBR = 0     *          
055000**                                                             *          
055100**     ALWAYS PUT OUT DOOR SUMMARY AND ASSIGNMENT CLOSE        *          
055200**     BUSINESS EVENTS IF DLT00004-LAST-USD-SEQ-NBR > 0,       *          
055300**     (THERE WAS ACTIVITY AGAINST THE ASSIGNMENT).            *          
055400****************************************************************          
055500           IF DL022-SUCCESSFUL                                            
055600              IF DLT00004-LAST-USD-SEQ-NBR = 0                            
055700                 IF PV-DIVERT-COUNT  > 0                                  
055800                    PERFORM 7400-FORMAT-XLWS072-CLOSE                     
055900                 END-IF                                                   
056000              ELSE                                                        
056100                 PERFORM 7400-FORMAT-XLWS072-CLOSE                        
056200              END-IF                                                      
056300           END-IF                                                         
056400        END-IF                                                            
056500     END-IF.                                                              
056600                                                                          
056700     IF DL022-SUCCESSFUL                                                  
056800        PERFORM 6000-UPDATE-ASSOCIATE                                     
056900     END-IF.                                                              
057000                                                                          
057100     IF DL022-SUCCESSFUL                                                  
057200        PERFORM 6200-UPDATE-ASSIGNMENT                                    
057300     END-IF.                                                              
057400                                                                          
057500                                                                          
057600****************************************************************          
057700*                                                              *          
057800*                                                              *          
057900****************************************************************          
058000                                                                          
058100 2400-COPY-ASSIGNMENT.                                                    
058200                                                                          
058300     IF DL022-SUCCESSFUL                                                  
058400        PERFORM 5000-INSERT-ASSIGNMENT                                    
058500     END-IF.                                                              
058600                                                                          
058700     IF DL022-SUCCESSFUL                                                  
058800        PERFORM 4000-COPY-ASSOCIATES                                      
058900     END-IF.                                                              
059000                                                                          
059100                                                                          
059200****************************************************************          
059300*                                                              *          
059400*                                                              *          
059500****************************************************************          
059600                                                                          
059700 3000-SELECT-ASSIGNMENT.                                                  
059800                                                                          
059900     PERFORM                                                              
060000         WITH TEST AFTER                                                  
060100         VARYING PV-RETRY-COUNTER                                         
060200         FROM 1 BY 1                                                      
060300         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
060400            OR SQLCODE = ZERO                                             
060500            OR SQLCODE = +100                                             
060600                                                                          
060700         EXEC SQL                                                         
060800             SELECT                                                       
060900                    ASGMT_STRT_TM                                         
061000                   ,WK_TYP_DESC                                           
061100                   ,WK_AREA_DESC                                          
061200                   ,DC_NBR                                                
061300                   ,CRE_BY_USR_ID                                         
061400                   ,CRE_BY_PGM_NM                                         
061500                   ,LAST_USD_SEQ_NBR                                      
061600              INTO                                                        
061700                    :DLT00004-ASGMT-STRT-TM                               
061800                   ,:DLT00004-WK-TYP-DESC                                 
061900                   ,:DLT00004-WK-AREA-DESC                                
062000                   ,:DLT00004-DC-NBR                                      
062100                   ,:DLT00004-CRE-BY-USR-ID                               
062200                   ,:DLT00004-CRE-BY-PGM-NM                               
062300                   ,:DLT00004-LAST-USD-SEQ-NBR                            
062400              FROM  DLT_ASGMT                                             
062500             WHERE  ASGMT_STRT_DTE    = :PV-ASGMT-STRT-DTE                
062600               AND  ASGMT_NBR_SEQ_NBR = :PV-ASGMT-NBR-SEQ-NBR             
062700               AND  ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                 
062800               AND  ASGMT_STP_RSN_CDE = :PC-N                             
062900         END-EXEC                                                         
063000                                                                          
063100         EVALUATE TRUE                                                    
063200             WHEN SQLCODE = ZERO                                          
063300                  SET PS-ASSIGNMENT-FOUND TO TRUE                         
063400                                                                          
063500             WHEN SQLCODE = -904                                          
063600             WHEN SQLCODE = -913                                          
063700             WHEN SQLCODE = -911                                          
063800                  CONTINUE                                                
063900                                                                          
064000             WHEN SQLCODE = +100                                          
064100                  SET PS-ASSIGNMENT-NOT-FOUND TO TRUE                     
064200                  SET DL022-ASGMT-NOT-OPEN    TO TRUE                     
064300                                                                          
064400             WHEN SQLWARN0 NOT = SPACES                                   
064500             WHEN SQLCODE NOT = ZERO                                      
064600                  MOVE +4             TO PV-RETRY-COUNTER                 
064700         END-EVALUATE                                                     
064800     END-PERFORM.                                                         
064900                                                                          
065000     IF SQLCODE = ZERO OR                                                 
065100        SQLCODE = +100                                                    
065200        CONTINUE                                                          
065300     ELSE                                                                 
065400        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
065500           SET DL022-SQL-ERROR TO TRUE                                    
065600           MOVE '3000-'        TO DL022-SQL-ERROR-PARA                    
065700        END-IF                                                            
065800     END-IF.                                                              
065900                                                                          
066000                                                                          
066100****************************************************************          
066200*    COUNTS THE NUMBER OF DIVERTED CARTONS.                    *          
066300*                                                              *          
066400*    SHOULD ONLY BE DONE FOR SHIPPING EVENTS.                  *          
066500****************************************************************          
066600                                                                          
066700 3400-COUNT-DIVERTS.                                                      
066800                                                                          
066900     STRING PV-ASGMT-STRT-DTE                                             
067000            '-'                                                           
067100            DLT00004-ASGMT-STRT-TM                                        
067200            '.000000'                                                     
067300            DELIMITED BY SIZE                                             
067400            INTO PV-TIMESTAMP.                                            
067500     MOVE DLT00004-WK-AREA-DESC  TO DL011-SHIP-DOORGRP-WRK-AREA.          
067600     MOVE DL011-SD-WRK-AREA-DOOR-GROUP TO                                 
067700                                 PV-SD-WRK-AREA-DOOR-GROUP.               
067800                                                                          
067900     MOVE 0 TO PV-DIVERT-COUNT.                                           
068000                                                                          
068100     EXEC SQL                                                             
068200             SELECT  DRVT.OPER_UNT_ID                                     
068300              INTO   :SUCRDV-OPER-UNT-ID                                  
068400                                                                          
068500          FROM  DLT_DOOR_GP      GP                                       
068600               ,DLT_DOOR_GP_TMPL TMPL                                     
068700               ,TSUCRDV          DRVT                                     
068800                                                                          
068900         WHERE  GP.DOOR_GP_NBR       = :PV-SD-WRK-AREA-DOOR-GROUP         
069000           AND  TMPL.DOOR_GP_TMPL_ID = GP.DOOR_GP_TMPL_ID                 
069100           AND  TMPL.DC_NBR          = :DLT00004-DC-NBR                   
069200           AND  TMPL.ACTV_IND        = 'Y'                                
069300           AND  DRVT.DOOR_ID         = GP.DOOR_ID                         
069400           AND  DRVT.ACTV_CDE        = 'A'                                
069500           AND  DRVT.OPER_UNT_ID     = TMPL.DC_NBR                        
069600           AND  DRVT.DIVERT_TMST     > :PV-TIMESTAMP                      
069700                                                                          
069800     END-EXEC.                                                            
069900                                                                          
070000     EVALUATE TRUE                                                        
070100         WHEN SQLCODE = ZERO OR -811                                      
070200              MOVE 1 TO PV-DIVERT-COUNT                                   
070300                                                                          
070400         WHEN SQLCODE = +100                                              
070500              CONTINUE                                                    
070600                                                                          
070700         WHEN OTHER                                                       
070800              SET DL022-SQL-ERROR TO TRUE                                 
070900              MOVE SQLCODE        TO DL022-ASGMT-SQLCODE                  
071000              MOVE '3400-'        TO DL022-SQL-ERROR-PARA                 
071100              MOVE SQLCA          TO DL022-SQLCA                          
071200                                                                          
071300     END-EVALUATE.                                                        
071400                                                                          
071500****************************************************************          
071600*    USED BY COPY-FROM-ASGMT-WTEAM AND                         *          
071700*            END-COPY-ASGMT-WTEAM                              *          
071800*                                                              *          
071900*    COPIES ALL RELATED ASSOCIATES FROM ONE ASSIGNMENT TO      *          
072000*        A SECOND ASSIGMENT.                                   *          
072100****************************************************************          
072200                                                                          
072300 4000-COPY-ASSOCIATES.                                                    
072400                                                                          
072500     SET PS-NOT-END-OF-CURSOR TO TRUE.                                    
072600     PERFORM 4200-OPEN-ASSOCIATE-CURSOR.                                  
072700     PERFORM 4400-FETCH-ASSOCIATE.                                        
072800                                                                          
072900     PERFORM UNTIL (PS-END-OF-CURSOR)                                     
073000         IF PV-RECV-WRK-TYP OR                                            
073100            PV-RECV1-WRK-TYP                                              
073200             IF PV-ASSOCIATE-COUNT > 1                                    
073300                 SET PV-RECV-WRK-TYP TO TRUE                              
073400                 MOVE PV-ASGMT-NBR-TXT TO DLT00003-ASGMT-NBR-TXT          
073500             ELSE                                                         
073600                 SET PV-RECV1-WRK-TYP TO TRUE                             
073700                 MOVE PV-ASGMT-NBR-TXT TO DLT00003-ASGMT-NBR-TXT          
073800             END-IF                                                       
073900         END-IF                                                           
074000         IF PV-P2L-WRK-TYP OR                                             
074100            PV-P2L1-WRK-TYP                                               
074200             IF PV-ASSOCIATE-COUNT > 1                                    
074300                 SET PV-P2L-WRK-TYP TO TRUE                               
074400                 MOVE PV-ASGMT-NBR-TXT TO DLT00003-ASGMT-NBR-TXT          
074500             ELSE                                                         
074600                 SET PV-P2L1-WRK-TYP TO TRUE                              
074700                 MOVE PV-ASGMT-NBR-TXT TO DLT00003-ASGMT-NBR-TXT          
074800             END-IF                                                       
074900         END-IF                                                           
075000                                                                          
075100         PERFORM 5400-INSERT-ASSOCIATE                                    
075200         PERFORM 5500-INSERT-RDPR-USR                                     
075300                                                                          
075400         IF PV-RECV-WRK-TYP  OR                                           
075500            PV-RECV1-WRK-TYP OR                                           
075600            PV-P2L-WRK-TYP   OR                                           
075700            PV-P2L1-WRK-TYP                                               
075800             MOVE PV-ASGMT-NBR-WRK-TYP-SAV TO                             
075900                            PV-ASGMT-NBR-WRK-TYP                          
076000         END-IF                                                           
076100                                                                          
076200         PERFORM 4400-FETCH-ASSOCIATE                                     
076300                                                                          
076400     END-PERFORM.                                                         
076500                                                                          
076600     PERFORM 4600-CLOSE-ASSOCIATE-CURSOR.                                 
076700                                                                          
076800****************************************************************          
076900*    OPEN THE ASSOCIATE CURSOR                                 *          
077000*                                                              *          
077100****************************************************************          
077200                                                                          
077300 4200-OPEN-ASSOCIATE-CURSOR.                                              
077400                                                                          
077500     PERFORM                                                              
077600         WITH TEST AFTER                                                  
077700         VARYING PV-RETRY-COUNTER                                         
077800         FROM 1 BY 1                                                      
077900         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
078000            OR SQLCODE = ZERO                                             
078100            OR SQLCODE = +100                                             
078200                                                                          
078300         EXEC SQL                                                         
078400              OPEN  ASSOCIATE-CURSOR                                      
078500         END-EXEC                                                         
078600                                                                          
078700         EVALUATE TRUE                                                    
078800             WHEN SQLCODE = ZERO                                          
078900                  CONTINUE                                                
079000                                                                          
079100             WHEN SQLCODE = -904                                          
079200             WHEN SQLCODE = -913                                          
079300             WHEN SQLCODE = -911                                          
079400                                                                          
079500             WHEN SQLCODE = +100                                          
079600                  CONTINUE                                                
079700                                                                          
079800             WHEN SQLWARN0 NOT = SPACES                                   
079900             WHEN SQLCODE NOT = ZERO                                      
080000                  MOVE +4             TO PV-RETRY-COUNTER                 
080100         END-EVALUATE                                                     
080200     END-PERFORM.                                                         
080300                                                                          
080400     IF SQLCODE = ZERO OR                                                 
080500        SQLCODE = +100                                                    
080600        CONTINUE                                                          
080700     ELSE                                                                 
080800        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
080900           SET DL022-SQL-ERROR TO TRUE                                    
081000           MOVE '4200-'        TO DL022-SQL-ERROR-PARA                    
081100        END-IF                                                            
081200     END-IF.                                                              
081300                                                                          
081400****************************************************************          
081500*    READ THE ASSOCIATES RELATED TO THE ASSIGNMENT.            *          
081600*                                                              *          
081700****************************************************************          
081800                                                                          
081900 4400-FETCH-ASSOCIATE.                                                    
082000                                                                          
082100     PERFORM                                                              
082200         WITH TEST AFTER                                                  
082300         VARYING PV-RETRY-COUNTER                                         
082400         FROM 1 BY 1                                                      
082500         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
082600            OR SQLCODE = ZERO                                             
082700            OR SQLCODE = +100                                             
082800                                                                          
082900         EXEC SQL                                                         
083000             FETCH  ASSOCIATE-CURSOR                                      
083100                                                                          
083200              INTO  :DLT00003-ASGMT-STRT-DTE                              
083300                   ,:DLT00003-ASGMT-NBR-TXT                               
083400                   ,:DLT00003-KOHLS-USR-ID                                
083500                              :PV-KOHLS-USR-ID-NULL-IND                   
083600                   ,:DLT00003-TMP-USR-ID                                  
083700                              :PV-TMP-USR-ID-NULL-IND                     
083800                   ,:DLT00003-JN-TMST                                     
083900                   ,:DLT00003-CRE-BY-USR-ID                               
084000                                                                          
084100         END-EXEC                                                         
084200                                                                          
084300         EVALUATE TRUE                                                    
084400             WHEN SQLCODE = ZERO                                          
084500                  CONTINUE                                                
084600                                                                          
084700             WHEN SQLCODE = -904                                          
084800             WHEN SQLCODE = -911                                          
084900             WHEN SQLCODE = -913                                          
085000                  CONTINUE                                                
085100                                                                          
085200             WHEN SQLCODE = +100                                          
085300                  SET PS-END-OF-CURSOR TO TRUE                            
085400                                                                          
085500             WHEN SQLWARN0 NOT = SPACES                                   
085600             WHEN SQLCODE NOT = ZERO                                      
085700                  MOVE +4             TO PV-RETRY-COUNTER                 
085800         END-EVALUATE                                                     
085900     END-PERFORM.                                                         
086000                                                                          
086100     IF SQLCODE = ZERO OR                                                 
086200        SQLCODE = +100                                                    
086300        CONTINUE                                                          
086400     ELSE                                                                 
086500        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
086600           MOVE '4400-'        TO DL022-SQL-ERROR-PARA                    
086700           SET DL022-SQL-ERROR      TO TRUE                               
086800        END-IF                                                            
086900     END-IF.                                                              
087000                                                                          
087100****************************************************************          
087200*                                                              *          
087300*                                                              *          
087400****************************************************************          
087500                                                                          
087600 4600-CLOSE-ASSOCIATE-CURSOR.                                             
087700                                                                          
087800     PERFORM                                                              
087900         WITH TEST AFTER                                                  
088000         VARYING PV-RETRY-COUNTER                                         
088100         FROM 1 BY 1                                                      
088200         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
088300            OR SQLCODE = ZERO                                             
088400            OR SQLCODE = +100                                             
088500                                                                          
088600         EXEC SQL                                                         
088700              CLOSE ASSOCIATE-CURSOR                                      
088800         END-EXEC                                                         
088900                                                                          
089000         EVALUATE TRUE                                                    
089100             WHEN SQLCODE = ZERO                                          
089200                  CONTINUE                                                
089300                                                                          
089400             WHEN SQLCODE = -904                                          
089500             WHEN SQLCODE = -913                                          
089600             WHEN SQLCODE = -911                                          
089700                                                                          
089800             WHEN SQLCODE = +100                                          
089900                  CONTINUE                                                
090000                                                                          
090100             WHEN SQLWARN0 NOT = SPACES                                   
090200             WHEN SQLCODE NOT = ZERO                                      
090300                  MOVE +4             TO PV-RETRY-COUNTER                 
090400         END-EVALUATE                                                     
090500     END-PERFORM.                                                         
090600                                                                          
090700     IF SQLCODE = ZERO OR                                                 
090800        SQLCODE = +100                                                    
090900        CONTINUE                                                          
091000     ELSE                                                                 
091100        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
091200           SET DL022-SQL-ERROR TO TRUE                                    
091300           MOVE '4600-'        TO DL022-SQL-ERROR-PARA                    
091400        END-IF                                                            
091500     END-IF.                                                              
091600                                                                          
091700****************************************************************          
091800*    INSERTS EITHER USING INPUT FROM COMMAREA OR               *          
091900*        BY COPYING FROM ANOTHER ASSIGNMENT.                   *          
092000*                                                              *          
092100****************************************************************          
092200                                                                          
092300 5000-INSERT-ASSIGNMENT.                                                  
092400                                                                          
092500     IF  DL022-COPY-FROM-ASGMT                                            
092600     OR  DL022-END-COPY-ASGMT                                             
092700     OR  DL022-COPY-FROM-ASGMT-WTEAM                                      
092800     OR  DL022-END-COPY-ASGMT-WTEAM                                       
092900         CONTINUE                                                         
093000     ELSE                                                                 
093100         MOVE DL022-NA-FLD-WK-TYP-DESC    TO                              
093200              DLT00004-WK-TYP-DESC                                        
093300         MOVE DL022-NA-FLD-CRE-BY-USR-ID  TO                              
093400              DLT00004-CRE-BY-USR-ID                                      
093500         MOVE DL022-NA-FLD-CRE-PGM-NM     TO                              
093600              DLT00004-CRE-BY-PGM-NM                                      
093700         MOVE DL022-NA-FLD-WK-AREA-DESC   TO                              
093800              DLT00004-WK-AREA-DESC                                       
093900         MOVE DL022-NA-FLD-DC-NBR         TO                              
094000              DLT00004-DC-NBR                                             
094100     END-IF.                                                              
094200                                                                          
094300     PERFORM                                                              
094400         WITH TEST AFTER                                                  
094500         VARYING PV-RETRY-COUNTER                                         
094600         FROM 1 BY 1                                                      
094700         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
094800            OR SQLCODE = ZERO                                             
094900         MOVE PV-ASGMT-STRT-DTE TO PV-BUILD-TIMESTAMP-DTE                 
095000         MOVE PV-CURRENT-DATE TO PV-ASGMT-STRT-DTE                        
095100         IF DL022-CREATE-NEW-ASGMT OR                                     
095200            DL022-CREATE-NEW-ASGMT-WTEAM                                  
095300            EXEC SQL                                                      
095400                INSERT INTO DLT_ASGMT                                     
095500                      (ASGMT_STRT_DTE                                     
095600                      ,ASGMT_NBR_TXT                                      
095700                      ,ASGMT_STRT_TM                                      
095800                      ,WK_TYP_DESC                                        
095900                      ,WK_AREA_DESC                                       
096000                      ,DC_NBR                                             
096100                      ,CRE_BY_USR_ID                                      
096200                      ,CRE_BY_PGM_NM                                      
096300                      ,ASGMT_STP_TMST                                     
096400                      ,ASGMT_STP_RSN_CDE                                  
096500                      ,STP_BY_PGM_NM                                      
096600                      ,LAST_USD_SEQ_NBR)                                  
096700                                                                          
096800               VALUES (:PV-CURRENT-DATE                                   
096900                      ,:PV-ASGMT-NBR-TXT                                  
097000                      ,:PV-CURRENT-TIME                                   
097100                      ,:DLT00004-WK-TYP-DESC                              
097200                      ,:DLT00004-WK-AREA-DESC                             
097300                      ,:DLT00004-DC-NBR                                   
097400                      ,:DLT00004-CRE-BY-USR-ID                            
097500                      ,:DLT00004-CRE-BY-PGM-NM                            
097600                      ,NULL                                               
097700                      ,:PC-N                                              
097800                      ,NULL                                               
097900                      ,0)                                                 
098000            END-EXEC                                                      
098100         ELSE                                                             
098200            EXEC SQL                                                      
098300                INSERT INTO DLT_ASGMT                                     
098400                      (ASGMT_STRT_DTE                                     
098500                      ,ASGMT_NBR_TXT                                      
098600                      ,ASGMT_STRT_TM                                      
098700                      ,WK_TYP_DESC                                        
098800                      ,WK_AREA_DESC                                       
098900                      ,DC_NBR                                             
099000                      ,CRE_BY_USR_ID                                      
099100                      ,CRE_BY_PGM_NM                                      
099200                      ,ASGMT_STP_TMST                                     
099300                      ,ASGMT_STP_RSN_CDE                                  
099400                      ,STP_BY_PGM_NM                                      
099500                      ,LAST_USD_SEQ_NBR)                                  
099600                                                                          
099700               VALUES (:PV-CURRENT-DATE                                   
099800                      ,:PV-ASGMT-NBR-TXT                                  
099900                      ,:PV-CURRENT-TIME                                   
100000                      ,:DLT00004-WK-TYP-DESC                              
100100                      ,:DLT00004-WK-AREA-DESC                             
100200                      ,:DLT00004-DC-NBR                                   
100300                      ,:DLT00004-CRE-BY-USR-ID                            
100400                      ,:DLT00004-CRE-BY-PGM-NM                            
100500                      ,NULL                                               
100600                      ,:PC-N                                              
100700                      ,NULL                                               
100800                      ,0)                                                 
100900            END-EXEC                                                      
101000         END-IF                                                           
101100                                                                          
101200         EVALUATE TRUE                                                    
101300             WHEN SQLCODE = ZERO                                          
101400                  CONTINUE                                                
101500                                                                          
101600             WHEN SQLCODE = -904                                          
101700             WHEN SQLCODE = -913                                          
101800             WHEN SQLCODE = -911                                          
101900                 CONTINUE                                                 
102000             WHEN SQLWARN0 NOT = SPACES                                   
102100             WHEN SQLCODE NOT = ZERO                                      
102200                  MOVE +4             TO PV-RETRY-COUNTER                 
102300                                                                          
102400         END-EVALUATE                                                     
102500     END-PERFORM.                                                         
102600                                                                          
102700     IF SQLCODE = 0                                                       
102800     OR SQLCODE = -803                                                    
102900        EXEC SQL SET :PV-SET-SEQ-NBR =                                    
103000                      IDENTITY_VAL_LOCAL ()                               
103100        END-EXEC                                                          
103200                                                                          
103300        IF SQLCODE = 0                                                    
103400        OR SQLCODE = -803                                                 
103500           CONTINUE                                                       
103600        ELSE                                                              
103700           SET DL022-SQL-ERROR      TO TRUE                               
103800           MOVE '5000-' TO DL022-SQL-ERROR-PARA                           
103900        END-IF                                                            
104000     ELSE                                                                 
104100        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
104200           SET DL022-SQL-ERROR      TO TRUE                               
104300           MOVE '5000-' TO DL022-SQL-ERROR-PARA                           
104400        END-IF                                                            
104500     END-IF.                                                              
104600                                                                          
104700****************************************************************          
104800*    CREATES USING INPUT FROM COMMAREA FOR CREATE OR           *          
104900*        BY COPYING ASSOCIATES FROM ANOTHER ASSIGNMENT.        *          
105000*                                                              *          
105100*    NOTE: TEM_ASGMT_SEQ_NBR IS NOT INCLUDED IN THE INSERT     *          
105200*          AS IT'S ASSIGNED BY DB2.                            *          
105300****************************************************************          
105400                                                                          
105500 5400-INSERT-ASSOCIATE.                                                   
105600                                                                          
105700     PERFORM                                                              
105800         WITH TEST AFTER                                                  
105900         VARYING PV-RETRY-COUNTER                                         
106000         FROM 1 BY 1                                                      
106100         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
106200            OR SQLCODE = ZERO                                             
106210            OR SQLCODE = -530                                             
106300                                                                          
106400         IF DL022-CREATE-NEW-ASGMT-WTEAM                                  
106410***                                                                       
106500***         IF DL022-USR-IS-KOHLS                                         
106600               EXEC SQL                                                   
106700                   INSERT INTO DLT_TEM_ASGMT                              
106800                         (ASGMT_STRT_DTE                                  
106900                         ,ASGMT_NBR_TXT                                   
107000                         ,ASGMT_NBR_SEQ_NBR                               
107100                         ,KOHLS_USR_ID                                    
107200                         ,TMP_USR_ID                                      
107300                         ,JN_TMST                                         
107400                         ,CRE_BY_USR_ID                                   
107500                         ,LVE_TMST                                        
107600                         ,LVE_RSN_CDE                                     
107700                         ,LVE_BY_PGM_NM)                                  
107800                                                                          
107900                  VALUES (:PV-ASGMT-STRT-DTE                              
108000                         ,:PV-ASGMT-NBR-TXT                               
108100                         ,:PV-SET-SEQ-NBR                                 
108200                         ,:DL022-NA-FLD-CRE-BY-USR-ID                     
108300                         ,NULL                                            
108400                         ,:PV-CURRENT-TIMESTAMP                           
108500                         ,:DL022-NA-FLD-CRE-BY-USR-ID                     
108600                         ,NULL                                            
108700                         ,:PC-N                                           
108800                         ,NULL)                                           
108900               END-EXEC                                                   
108901                                                                          
108910               IF  SQLCODE = -530                                         
108920                                                                          
109100               EXEC SQL                                                   
109200                   INSERT INTO DLT_TEM_ASGMT                              
109300                         (ASGMT_STRT_DTE                                  
109400                         ,ASGMT_NBR_TXT                                   
109500                         ,ASGMT_NBR_SEQ_NBR                               
109600                         ,KOHLS_USR_ID                                    
109700                         ,TMP_USR_ID                                      
109800                         ,JN_TMST                                         
109900                         ,CRE_BY_USR_ID                                   
110000                         ,LVE_TMST                                        
110100                         ,LVE_RSN_CDE                                     
110200                         ,LVE_BY_PGM_NM)                                  
110300                                                                          
110400                  VALUES (:PV-ASGMT-STRT-DTE                              
110500                         ,:PV-ASGMT-NBR-TXT                               
110600                         ,:PV-SET-SEQ-NBR                                 
110700                         ,NULL                                            
110800                         ,:DL022-NA-FLD-CRE-BY-USR-ID                     
110900                         ,:PV-CURRENT-TIMESTAMP                           
111000                         ,:DL022-NA-FLD-CRE-BY-USR-ID                     
111100                         ,NULL                                            
111200                         ,:PC-N                                           
111300                         ,NULL)                                           
111400               END-EXEC                                                   
111500               END-IF                                                     
111600                                                                          
111700         ELSE                                                             
111800            IF PV-KOHLS-USR-ID-NULL-IND = -1                              
111900                EXEC SQL                                                  
112000                    INSERT INTO DLT_TEM_ASGMT                             
112100                          (ASGMT_STRT_DTE                                 
112200                          ,ASGMT_NBR_TXT                                  
112300                          ,ASGMT_NBR_SEQ_NBR                              
112400                          ,KOHLS_USR_ID                                   
112500                          ,TMP_USR_ID                                     
112600                          ,JN_TMST                                        
112700                          ,CRE_BY_USR_ID                                  
112800                          ,LVE_TMST                                       
112900                          ,LVE_RSN_CDE                                    
113000                          ,LVE_BY_PGM_NM)                                 
113100                                                                          
113200                   VALUES (:PV-CURRENT-DATE                               
113300                          ,:DLT00003-ASGMT-NBR-TXT                        
113400                          ,:PV-SET-SEQ-NBR                                
113500                          ,NULL                                           
113600                          ,:DLT00003-TMP-USR-ID                           
113700                          ,:PV-CURRENT-TIMESTAMP                          
113800                          ,:DLT00003-CRE-BY-USR-ID                        
113900                          ,NULL                                           
114000                          ,:PC-N                                          
114100                          ,NULL)                                          
114200                END-EXEC                                                  
114300                                                                          
114400            ELSE                                                          
114500                EXEC SQL                                                  
114600                    INSERT INTO DLT_TEM_ASGMT                             
114700                          (ASGMT_STRT_DTE                                 
114800                          ,ASGMT_NBR_TXT                                  
114900                          ,ASGMT_NBR_SEQ_NBR                              
115000                          ,KOHLS_USR_ID                                   
115100                          ,TMP_USR_ID                                     
115200                          ,JN_TMST                                        
115300                          ,CRE_BY_USR_ID                                  
115400                          ,LVE_TMST                                       
115500                          ,LVE_RSN_CDE                                    
115600                          ,LVE_BY_PGM_NM)                                 
115700                                                                          
115800                   VALUES (:PV-CURRENT-DATE                               
115900                          ,:DLT00003-ASGMT-NBR-TXT                        
116000                          ,:PV-SET-SEQ-NBR                                
116100                          ,:DLT00003-KOHLS-USR-ID                         
116200                          ,NULL                                           
116300                          ,:PV-CURRENT-TIMESTAMP                          
116400                          ,:DLT00003-CRE-BY-USR-ID                        
116500                          ,NULL                                           
116600                          ,:PC-N                                          
116700                          ,NULL)                                          
116800                END-EXEC                                                  
116900                                                                          
117000             END-IF                                                       
117100         END-IF                                                           
117200                                                                          
117300         EVALUATE TRUE                                                    
117400             WHEN SQLCODE = ZERO                                          
117410             WHEN SQLCODE = -530                                          
117500             WHEN SQLCODE = -904                                          
117600             WHEN SQLCODE = -913                                          
117700             WHEN SQLCODE = -911                                          
117800                 CONTINUE                                                 
117900             WHEN SQLWARN0 NOT = SPACES                                   
118000             WHEN SQLCODE NOT = ZERO                                      
118100                  MOVE +4             TO PV-RETRY-COUNTER                 
118200         END-EVALUATE                                                     
118300     END-PERFORM.                                                         
118400                                                                          
118500     IF SQLCODE = 0  OR                                                   
118510        SQLCODE = -530                                                    
118600        CONTINUE                                                          
118700     ELSE                                                                 
118800        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
118900           SET DL022-SQL-ERROR      TO TRUE                               
119000           MOVE '5400-' TO DL022-SQL-ERROR-PARA                           
119100        END-IF                                                            
119200     END-IF.                                                              
119300                                                                          
119400 5500-INSERT-RDPR-USR.                                                    
119500                                                                          
119600     PERFORM                                                              
119700         WITH TEST AFTER                                                  
119800         VARYING PV-RETRY-COUNTER                                         
119900         FROM 1 BY 1                                                      
120000         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
120100            OR SQLCODE = ZERO OR SQLCODE = -803                           
120200            OR SQLCODE = -530                                             
120300                                                                          
120400         IF DL022-CREATE-NEW-ASGMT-WTEAM                                  
120500*            TRY AS A KOHLS USER FIRST                                    
120600             EXEC SQL                                                     
120700                 INSERT INTO DLT_RDPR_USR_LSTG                            
120800                     (USR_ID                                              
120900                     ,LOC_NBR                                             
121000                     ,TMP_USR_ID                                          
121100                     ,CRE_TMST)                                           
121200                                                                          
121300                   VALUES (:DL022-NA-FLD-CRE-BY-USR-ID                    
121400                          ,:DLT00004-DC-NBR                               
121500                          ,NULL                                           
121600                          ,:PV-CURRENT-TIMESTAMP)                         
121700                END-EXEC                                                  
121800                                                                          
121900*            TRY AS A TEMP USER                                           
122000             IF SQLCODE = -530                                            
122100                 EXEC SQL                                                 
122200                   INSERT INTO DLT_RDPR_USR_LSTG                          
122300                      (USR_ID                                             
122400                      ,LOC_NBR                                            
122500                      ,TMP_USR_ID                                         
122600                      ,CRE_TMST)                                          
122700                                                                          
122800                   VALUES (NULL                                           
122900                          ,:DLT00004-DC-NBR                               
123000                          ,:DL022-NA-FLD-CRE-BY-USR-ID                    
123100                          ,:PV-CURRENT-TIMESTAMP)                         
123200                END-EXEC                                                  
123300             END-IF                                                       
123400                                                                          
123500         ELSE                                                             
123600             IF PV-KOHLS-USR-ID-NULL-IND = -1                             
123700                 EXEC SQL                                                 
123800                     INSERT INTO DLT_RDPR_USR_LSTG                        
123900                            (USR_ID                                       
124000                            ,LOC_NBR                                      
124100                            ,TMP_USR_ID                                   
124200                            ,CRE_TMST)                                    
124300                                                                          
124400                         VALUES (NULL                                     
124500                                ,:DLT00004-DC-NBR                         
124600                                ,:DLT00003-TMP-USR-ID                     
124700                                ,:PV-CURRENT-TIMESTAMP)                   
124800                      END-EXEC                                            
124900                                                                          
125000             ELSE                                                         
125100                                                                          
125200                 EXEC SQL                                                 
125300                     INSERT INTO DLT_RDPR_USR_LSTG                        
125400                            (USR_ID                                       
125500                            ,LOC_NBR                                      
125600                            ,TMP_USR_ID                                   
125700                            ,CRE_TMST)                                    
125800                                                                          
125900                         VALUES (:DLT00003-KOHLS-USR-ID                   
126000                                ,:DLT00004-DC-NBR                         
126100                                ,NULL                                     
126200                                ,:PV-CURRENT-TIMESTAMP)                   
126300                      END-EXEC                                            
126400             END-IF                                                       
126500                                                                          
126600         END-IF                                                           
126700                                                                          
126800         EVALUATE TRUE                                                    
126900             WHEN SQLCODE = ZERO                                          
127000             WHEN SQLCODE = -530                                          
127100             WHEN SQLCODE = -803                                          
127200             WHEN SQLCODE = -904                                          
127300             WHEN SQLCODE = -913                                          
127400             WHEN SQLCODE = -911                                          
127500                 CONTINUE                                                 
127600             WHEN SQLWARN0 NOT = SPACES                                   
127700             WHEN SQLCODE NOT = ZERO                                      
127800                  MOVE +4             TO PV-RETRY-COUNTER                 
127900         END-EVALUATE                                                     
128000     END-PERFORM.                                                         
128100                                                                          
128200     IF SQLCODE = 0 OR -803 OR -530                                       
128300        CONTINUE                                                          
128400     ELSE                                                                 
128500        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
128600           SET DL022-SQL-ERROR      TO TRUE                               
128700           MOVE '5500-' TO DL022-SQL-ERROR-PARA                           
128800        END-IF                                                            
128900     END-IF.                                                              
129000                                                                          
129100****************************************************************          
129200*    ENDS THE ASSOCIATES ASSIGNMENT TO A TASK.                 *          
129300*                                                              *          
129400****************************************************************          
129500                                                                          
129600 6000-UPDATE-ASSOCIATE.                                                   
129700                                                                          
129800     PERFORM                                                              
129900         WITH TEST AFTER                                                  
130000         VARYING PV-RETRY-COUNTER                                         
130100         FROM 1 BY 1                                                      
130200         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
130300            OR SQLCODE = ZERO                                             
130400            OR SQLCODE = +100                                             
130500                                                                          
130600         EXEC SQL                                                         
130700             UPDATE DLT_TEM_ASGMT                                         
130800                SET LVE_TMST      = :PV-CURRENT-TIMESTAMP                 
130900                   ,LVE_RSN_CDE   = :DL022-ECA-STOP-RSN-CDE               
131000                   ,LVE_BY_PGM_NM = :DL022-ECA-STOP-PGM-NM                
131100                                                                          
131200             WHERE  ASGMT_STRT_DTE    = :PV-ASGMT-STRT-DTE                
131300               AND  ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                 
131400               AND  ASGMT_NBR_SEQ_NBR = :PV-ASGMT-NBR-SEQ-NBR             
131500               AND  LVE_RSN_CDE       = :PC-N                             
131600         END-EXEC                                                         
131700                                                                          
131800         EVALUATE TRUE                                                    
131900             WHEN SQLCODE = ZERO                                          
132000                  SET PS-ASSOCIATE-UPDATED TO TRUE                        
132100                                                                          
132200             WHEN SQLCODE = +100                                          
132300                  SET PS-ASSOCIATE-NOT-UPDATED TO TRUE                    
132400                                                                          
132500             WHEN SQLCODE = -904                                          
132600             WHEN SQLCODE = -913                                          
132700             WHEN SQLCODE = -911                                          
132800                  CONTINUE                                                
132900                                                                          
133000             WHEN SQLWARN0 NOT = SPACES                                   
133100             WHEN SQLCODE NOT = ZERO                                      
133200                  MOVE +4             TO PV-RETRY-COUNTER                 
133300         END-EVALUATE                                                     
133400     END-PERFORM.                                                         
133500                                                                          
133600                                                                          
133700     IF SQLCODE = 0 OR                                                    
133800        SQLCODE = +100                                                    
133900        CONTINUE                                                          
134000     ELSE                                                                 
134100        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
134200           SET DL022-SQL-ERROR      TO TRUE                               
134300           MOVE '6000-' TO DL022-SQL-ERROR-PARA                           
134400        END-IF                                                            
134500     END-IF.                                                              
134600                                                                          
134700****************************************************************          
134800*    ENDS THE ASSIGNMENT.                                      *          
134900*                                                              *          
135000****************************************************************          
135100                                                                          
135200 6200-UPDATE-ASSIGNMENT.                                                  
135300                                                                          
135400     PERFORM                                                              
135500         WITH TEST AFTER                                                  
135600         VARYING PV-RETRY-COUNTER                                         
135700         FROM 1 BY 1                                                      
135800         UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                          
135900            OR SQLCODE = ZERO                                             
136000            OR SQLCODE = +100                                             
136100                                                                          
136200         EXEC SQL                                                         
136300             UPDATE DLT_ASGMT                                             
136400                SET ASGMT_STP_TMST    = :PV-CURRENT-TIMESTAMP             
136500                   ,ASGMT_STP_RSN_CDE = :DL022-ECA-STOP-RSN-CDE           
136600                   ,STP_BY_PGM_NM     = :DL022-ECA-STOP-PGM-NM            
136700                                                                          
136800             WHERE  ASGMT_STRT_DTE    = :PV-ASGMT-STRT-DTE                
136900               AND  ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                 
137000               AND  ASGMT_NBR_SEQ_NBR = :PV-ASGMT-NBR-SEQ-NBR             
137100               AND  ASGMT_STP_RSN_CDE = :PC-N                             
137200         END-EXEC                                                         
137300                                                                          
137400         EVALUATE TRUE                                                    
137500             WHEN SQLCODE = ZERO                                          
137600                  SET PS-ASSIGNMENT-UPDATED TO TRUE                       
137700                                                                          
137800             WHEN SQLCODE = +100                                          
137900                  SET PS-ASSIGNMENT-NOT-UPDATED TO TRUE                   
138000                                                                          
138100             WHEN SQLCODE = -904                                          
138200             WHEN SQLCODE = -913                                          
138300             WHEN SQLCODE = -911                                          
138400                  CONTINUE                                                
138500                                                                          
138600             WHEN SQLWARN0 NOT = SPACES                                   
138700             WHEN SQLCODE NOT = ZERO                                      
138800                  MOVE +4             TO PV-RETRY-COUNTER                 
138900         END-EVALUATE                                                     
139000     END-PERFORM.                                                         
139100                                                                          
139200     IF SQLCODE = 0 OR                                                    
139300        SQLCODE = +100                                                    
139400        CONTINUE                                                          
139500     ELSE                                                                 
139600        IF PV-RETRY-COUNTER > PC-MAX-RETRIES                              
139700           SET DL022-SQL-ERROR      TO TRUE                               
139800           MOVE '6200-' TO DL022-SQL-ERROR-PARA                           
139900        END-IF                                                            
140000     END-IF.                                                              
140100                                                                          
140200****************************************************************          
140300*    IF CLOSING AN ASSIGNMENT AND THE WRK-TYPE = 'RLABL', THEN *          
140400*    WE NEED TO TAKE THE TEMP USERID FROM THE DLT_TEM_ASGMT    *          
140500*    TABLE AND THE DC NUMBER FROM THE DLT_ASGMT TABLE TO       *          
140600*    READ THE TKRPRPM TABLE.                                   *          
140700*                                                              *          
140800****************************************************************          
140900                                                                          
141000 7000-SELECT-TEAM-ASSIGNMENT.                                             
141100                                                                          
141200     EXEC SQL                                                             
141300       SELECT   TMP_USR_ID                                                
141400               ,(JN_TMST + 1 SECOND)                                      
141500                                                                          
141600         INTO   :DLT00003-TMP-USR-ID                                      
141700               ,:DLT00003-JN-TMST                                         
141800                                                                          
141900         FROM   DLT_TEM_ASGMT                                             
142000                                                                          
142100        WHERE   ASGMT_STRT_DTE    = :PV-ASGMT-STRT-DTE                    
142200          AND   ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                     
142300          AND   ASGMT_NBR_SEQ_NBR = :PV-ASGMT-NBR-SEQ-NBR                 
142400          AND   LVE_RSN_CDE       = :PC-N                                 
142500     END-EXEC.                                                            
142600                                                                          
142700     EVALUATE TRUE                                                        
142800         WHEN SQLCODE = ZERO                                              
142900              SET PS-TEAM-ROW-FOUND TO TRUE                               
143000                                                                          
143100         WHEN SQLCODE = +100                                              
143200              SET PS-TEAM-ROW-NOT-FOUND TO TRUE                           
143300                                                                          
143400         WHEN SQLWARN0 NOT = SPACES                                       
143500         WHEN SQLCODE NOT = ZERO                                          
143600              SET DL022-SQL-ERROR      TO TRUE                            
143700              MOVE '7000-' TO DL022-SQL-ERROR-PARA                        
143800                                                                          
143900     END-EVALUATE.                                                        
144000                                                                          
144100                                                                          
144200****************************************************************          
144300*    IF A ROW IS FOUND ON THE TKRPRPM TABLE FOR THE TEMPORARY  *          
144400*    USERID AND DC, THEN WE WILL NEED TO CALL XLKUT107 TO      *          
144500*    WRITE OUT AN ISTOP IN-INDIRECT BUSINESS EVENT.            *          
144600*                                                              *          
144700****************************************************************          
144800                                                                          
144900 7200-SELECT-DLPLDU.                                                      
145000                                                                          
145100     EXEC SQL                                                             
145200         SELECT   FUNC_TXT                                                
145300          INTO   :KRPRPM-FUNC-TXT                                         
145400                                                                          
145500          FROM   TKRPRPM                                                  
145600                                                                          
145700         WHERE   LOC_ID            = :DLT00004-DC-NBR                     
145800           AND   INTFC_SYS_CDE     = :PC-DLX                              
145900           AND   FUNC_PRC_STAT_CDE = :PC-AC                               
146000           AND   SYS_PRC_FUNC_CDE  = :PC-DLPLDU                           
146100           AND   FUNC_TXT          = :DLT00003-TMP-USR-ID                 
146200     END-EXEC.                                                            
146300                                                                          
146400     EVALUATE TRUE                                                        
146500         WHEN SQLCODE = ZERO                                              
146600              SET PS-DLPLDU-ROW-FOUND TO TRUE                             
146700                                                                          
146800         WHEN SQLCODE = +100                                              
146900              SET PS-DLPLDU-ROW-NOT-FOUND TO TRUE                         
147000                                                                          
147100         WHEN SQLWARN0 NOT = SPACES                                       
147200         WHEN SQLCODE NOT = ZERO                                          
147300              SET DL022-SQL-ERROR      TO TRUE                            
147400              MOVE '7200-' TO DL022-SQL-ERROR-PARA                        
147500                                                                          
147600     END-EVALUATE.                                                        
147700                                                                          
147800****************************************************************          
147900*    IF CLOSING AN ASSIGNMENT AND THE DL022-LOG-TO-EAI-EXPORT  *          
148000*    SWITCH IS ON, WE CALL XLKUT107 TO WRITE THE APPROPRIATE   *          
148100*    INFORMATION TO EITHER THE PCT00011 OR SUT00037 TABLES.    *          
148200*                                                              *          
148300****************************************************************          
148400                                                                          
148500 7400-FORMAT-XLWS072-CLOSE.                                               
148600                                                                          
148700     EVALUATE TRUE                                                        
148800         WHEN DLT00004-WK-TYP-DESC = 'SHIP'                               
148900              MOVE DLT00004-WK-AREA-DESC  TO                              
149000                                    DL011-SHIP-DOORGRP-WRK-AREA           
149100              MOVE DL011-SD-WRK-AREA-DOOR-GROUP TO                        
149200                                    DL023-DOOR-GP-NBR                     
149300              MOVE DLT00004-DC-NBR     TO DL023-DC-NBR                    
149400                                                                          
149500              PERFORM 8400-COUNT-DOORS                                    
149600              IF DL022-SUCCESSFUL                                         
149700                 PERFORM 8600-CALL-DLKUT024                               
149800              END-IF                                                      
149900              IF DL022-SUCCESSFUL                                         
150000                 PERFORM 8000-CALL-DLKUT027                               
150100              END-IF                                                      
150200                                                                          
150300         WHEN OTHER                                                       
150400              CONTINUE                                                    
150500                                                                          
150600     END-EVALUATE.                                                        
150700                                                                          
150800     INITIALIZE XL091-EAI-EXPORT-COMMAREA.                                
150900                                                                          
151000     EVALUATE TRUE                                                        
151100         WHEN DLT00004-WK-TYP-DESC = 'P2L'                                
151200         WHEN DLT00004-WK-TYP-DESC = 'RLABL'                              
151300              SET XL091-PC-APP-ID TO TRUE                                 
151400                                                                          
151500         WHEN OTHER                                                       
151600              SET XL091-SU-APP-ID TO TRUE                                 
151700                                                                          
151800     END-EVALUATE.                                                        
151900                                                                          
152000     MOVE PC-DC-LABOR         TO XL091-EVNT-OBJ-TXT.                      
152100     MOVE PC-ASSIGNMENT-CLOSE TO XL091-EVNT-OBJ-ACTN-TXT.                 
152200     MOVE DL022-ORIG-CALLING-PGM-NM TO XL091-CRE-BY-USR-ID.               
152300     MOVE PC-DLKUP026               TO XL091-CRE-PGM-NM.                  
152400                                                                          
152500     SET XL091-DO-NOT-COMMIT-UPDATE TO TRUE.                              
152600     SET XL091-COPYBOOK-NOT-USED    TO TRUE.                              
152700                                                                          
152800     MOVE PV-ASGMT-NBR-SEQ-NBR TO XL091-EVNT-KY-1-NBR.                    
152900     MOVE PV-ASGMT-NBR-TXT     TO XL091-EVNT-KY-1-TXT.                    
153000     MOVE PV-BUILD-TIMESTAMP   TO XL091-EVNT-KY-1-TMST.                   
153100     MOVE DLT00004-DC-NBR      TO XL091-EVNT-KY-2-NBR.                    
153200                                                                          
153300     PERFORM 7800-CALL-XLKUT107.                                          
153400                                                                          
153500****************************************************************          
153600*                                                              *          
153700*                                                              *          
153800****************************************************************          
153900                                                                          
154000 7600-FORMAT-XLWS072-ISTOP.                                               
154100                                                                          
154200     INITIALIZE XL091-EAI-EXPORT-COMMAREA.                                
154300                                                                          
154400     SET XL091-PC-APP-ID          TO TRUE.                                
154500                                                                          
154600     MOVE PC-DC-LABOR             TO XL091-EVNT-OBJ-TXT.                  
154700     MOVE PC-INDIRECT-LABOR       TO XL091-EVNT-OBJ-ACTN-TXT.             
154800     MOVE DLT00003-TMP-USR-ID     TO XL091-CRE-BY-USR-ID.                 
154900     MOVE PC-DLKUP026             TO XL091-CRE-PGM-NM.                    
155000     MOVE DLT00004-DC-NBR         TO XL091-EVNT-KY-1-NBR.                 
155100     MOVE PC-ISTOP                TO XL091-EVNT-KY-1-TXT.                 
155200     MOVE DLT00003-JN-TMST        TO XL091-EVNT-KY-1-TMST.                
155300                                                                          
155400     SET XL091-COMMIT-UPDATE      TO TRUE.                                
155500     MOVE PC-DLKUP026             TO XL091-ORIG-CALLING-PGM-NM.           
155600                                                                          
155700     SET XL091-COPYBOOK-NOT-USED TO TRUE.                                 
155800                                                                          
155900     PERFORM 7800-CALL-XLKUT107.                                          
156000                                                                          
156100                                                                          
156200****************************************************************          
156300*    IF CLOSING AN ASSIGNMENT AND THE DL022-LOG-TO-EAI-EXPORT  *          
156400*    SWITCH IS ON, WE CALL XLKUT107 TO WRITE THE APPROPRIATE   *          
156500*    INFORMATION TO EITHER THE PCT00011 OR SUT00037 TABLES.    *          
156600*                                                              *          
156700****************************************************************          
156800                                                                          
156900 7800-CALL-XLKUT107.                                                      
157000                                                                          
157100     CALL XL091-USER-PGM USING XL091-EAI-EXPORT-COMMAREA.                 
157200                                                                          
157300     IF XL091-SUCCESSFUL                                                  
157400        CONTINUE                                                          
157500     ELSE                                                                 
157600        SET DL022-CALLED-MODULE-FAILURE TO TRUE                           
157700        MOVE XL091-USER-PGM        TO DL022-CALL-MOD-FAIL-NM              
157800        MOVE XL091-RETURN-CODE     TO DL022-CALL-MOD-FAIL-ERR-CDE         
157900        MOVE XL091-RETURN-SQLCODES TO DL022-RETURN-SQLCODES               
158000     END-IF.                                                              
158100                                                                          
158200******************************************************************        
158300*    THIS WILL ONLY BE USED WHEN CLOSING A SHIPPING ASSIGNMENT.  *        
158400*        IT CALLS DLKUT027 TO GENERATE A "SHIPPING DOOR SUMMARY" *        
158500*        BUSINESS EVENT FOR EVERY DOOR WITHIN THE DOOR GROUP     *        
158600*        TEMPLATE ID.                                            *        
158700******************************************************************        
158800                                                                          
158900 8000-CALL-DLKUT027.                                                      
159000                                                                          
159100     INITIALIZE DL023-ASGMT-COMMAREA.                                     
159200     SET  DL023-BY-DOOR-GROUP-ID TO TRUE.                                 
159300                                                                          
159400     MOVE DL022-ECA-KEY-START-DTE   TO DL023-ASGMT-START-DTE.             
159500     MOVE DL022-ECA-KEY-NBR-TXT     TO DL023-ASGMT-NBR-TXT.               
159600     MOVE DL022-ECA-KEY-NBR-SEQ-NBR TO DL023-ASGMT-NBR-SEQ-NBR.           
159700                                                                          
159800     MOVE DLT00004-WK-AREA-DESC  TO DL011-SHIP-DOORGRP-WRK-AREA.          
159900     MOVE DL011-SD-WRK-AREA-DOOR-GROUP TO DL023-DOOR-GP-NBR.              
160000     MOVE DLT00004-DC-NBR        TO DL023-DC-NBR.                         
160100                                                                          
160200**   TRIP IS NOW DETERMINED IN DLKUT027.                                  
160300                                                                          
160400     STRING DL022-ECA-KEY-START-DTE                                       
160500            '-'                                                           
160600            DLT00004-ASGMT-STRT-TM                                        
160700            '.000000'                                                     
160800            DELIMITED BY SIZE                                             
160900            INTO DL023-ASGMT-STRT-TMST.                                   
161000                                                                          
161100     MOVE PV-CURRENT-TIMESTAMP TO DL023-ASGMT-STOP-TMST.                  
161200                                                                          
161300     MOVE DL020-SEQ-START-NBR  TO DL023-BEGIN-DETAIL-SEQ-NBR.             
161400     MOVE PV-DOOR-COUNT        TO DL023-NBR-DOORS.                        
161500     MOVE DL022-ORIG-CALLING-PGM-NM TO DL023-USR-ID.                      
161600                                                                          
161700     CALL DL023-USER-PGM USING DL023-ASGMT-COMMAREA.                      
161800                                                                          
161900     IF DL023-SUCCESSFUL                                                  
162000        CONTINUE                                                          
162100     ELSE                                                                 
162200        SET  DL022-CALLED-MODULE-FAILURE TO TRUE                          
162300        MOVE DL023-USER-PGM        TO DL022-CALL-MOD-FAIL-NM              
162400        MOVE DL023-RETURN-CODE     TO DL022-CALL-MOD-FAIL-ERR-CDE         
162500        MOVE DL023-RETURN-SQLCODES TO DL022-RETURN-SQLCODES               
162600     END-IF.                                                              
162700                                                                          
162800****************************************************************          
162900*                                                              *          
163000*                                                              *          
163100****************************************************************          
163200                                                                          
163300 8400-COUNT-DOORS.                                                        
163400                                                                          
163500     MOVE 0 TO PV-DOOR-COUNT.                                             
163600                                                                          
163700     EXEC SQL                                                             
163800           SELECT COUNT(*)                                                
163900                                                                          
164000             INTO :PV-DOOR-COUNT                                          
164100                                                                          
164200             FROM DLT_DOOR_GP       GP                                    
164300                 ,DLT_DOOR_GP_TMPL  TMPL                                  
164400            WHERE GP.DOOR_GP_NBR     = :DL023-DOOR-GP-NBR                 
164500              AND TMPL.DC_NBR        = :DL023-DC-NBR                      
164600              AND TMPL.ACTV_IND      = 'Y'                                
164700              AND GP.DOOR_GP_TMPL_ID = TMPL.DOOR_GP_TMPL_ID               
164800                                                                          
164900     END-EXEC.                                                            
165000                                                                          
165100     EVALUATE TRUE                                                        
165200         WHEN SQLCODE = ZERO                                              
165300              CONTINUE                                                    
165400                                                                          
165500         WHEN SQLCODE = +100                                              
165600              CONTINUE                                                    
165700                                                                          
165800         WHEN SQLCODE = -904                                              
165900         WHEN SQLCODE = -913                                              
166000         WHEN SQLCODE = -911                                              
166100                                                                          
166200         WHEN SQLWARN0 NOT = SPACES                                       
166300         WHEN SQLCODE NOT = ZERO                                          
166400              SET DL022-SQL-ERROR TO TRUE                                 
166500              MOVE SQLCODE        TO DL022-ASGMT-SQLCODE                  
166600              MOVE '8400-'        TO DL022-SQL-ERROR-PARA                 
166700              MOVE SQLCA          TO DL022-SQLCA                          
166800     END-EVALUATE.                                                        
166900****************************************************************          
167000*                                                              *          
167100*                                                              *          
167200****************************************************************          
167300 8500-COUNT-ASSOCIATES.                                                   
167400                                                                          
167500     MOVE 0 TO PV-ASSOCIATE-COUNT.                                        
167600                                                                          
167700     EXEC SQL                                                             
167800         SELECT COUNT(*)                                                  
167900          INTO :PV-ASSOCIATE-COUNT                                        
168000                                                                          
168100          FROM  DLT_TEM_ASGMT                                             
168200                                                                          
168300         WHERE  ASGMT_STRT_DTE    = :PV-BUILD-TIMESTAMP-DTE               
168400           AND  ASGMT_NBR_TXT     = :PV-ASGMT-NBR-TXT                     
168500           AND  ASGMT_NBR_SEQ_NBR =                                       
168600                              :PV-ASGMT-NBR-SEQ-NBR                       
168700           AND  LVE_RSN_CDE       = 'T'                                   
168800     END-EXEC.                                                            
168900                                                                          
169000     EVALUATE TRUE                                                        
169100         WHEN SQLCODE = ZERO                                              
169200              CONTINUE                                                    
169300                                                                          
169400         WHEN SQLCODE = +100                                              
169500              CONTINUE                                                    
169600                                                                          
169700         WHEN SQLCODE = -904                                              
169800         WHEN SQLCODE = -913                                              
169900         WHEN SQLCODE = -911                                              
170000                                                                          
170100         WHEN SQLWARN0 NOT = SPACES                                       
170200         WHEN SQLCODE NOT = ZERO                                          
170300              SET DL022-SQL-ERROR TO TRUE                                 
170400              MOVE SQLCODE        TO DL022-ASGMT-SQLCODE                  
170500              MOVE '8500-'        TO DL022-SQL-ERROR-PARA                 
170600              MOVE SQLCA          TO DL022-SQLCA                          
170700     END-EVALUATE.                                                        
170800                                                                          
170900                                                                          
171000******************************************************************        
171100*    RESERVE THE SAME NUMBER OF SEQUENCE NUMBERS THAT THERE ARE  *        
171200*       NUMBER OF DOORS (PV-DOOR-COUNT).                         *        
171300*                                                                *        
171400******************************************************************        
171500                                                                          
171600 8600-CALL-DLKUT024.                                                      
171700                                                                          
171800     INITIALIZE DL020-ASGMT-COMMAREA.                                     
171900                                                                          
172000     SET DL020-BY-ASGMT-KEY         TO TRUE.                              
172100     SET DL020-COMMIT-UPDATE        TO TRUE.                              
172200     SET DL020-FIND-NEXT-ASGMT      TO TRUE.                              
172300                                                                          
172400     MOVE PV-ASGMT-STRT-DTE    TO DL020-KEY-START-DTE.                    
172500     MOVE PV-ASGMT-NBR-TXT     TO DL020-KEY-NBR-TXT.                      
172600     MOVE PV-ASGMT-NBR-SEQ-NBR TO DL020-KEY-NBR-SEQ-NBR.                  
172700                                                                          
172800     MOVE PV-DOOR-COUNT TO DL020-NBR-OF-NBRS-NEED-QTY.                    
172900     MOVE PC-DLKUP026   TO DL020-ORIG-CALLING-PGM-NM.                     
173000                                                                          
173100     CALL DL020-USER-PGM USING DL020-ASGMT-COMMAREA.                      
173200                                                                          
173300     IF DL020-SUCCESSFUL                                                  
173400*       **********************************************************        
173500*       *  ASSIGNMENT SENT IN HAS BEEN CLOSED, DLKUT024 HAS SENT *        
173600*       *  BACK CURRENT OPEN ASSIGNMENT.                         *        
173700*       *                                                        *        
173800*       **********************************************************        
173900        IF DL020-ASGMT-NEXT-USED                                          
174000           MOVE DL020-NXT-START-DTE TO  DL023-ASGMT-START-DTE             
174100           MOVE DL020-NXT-NBR-TXT   TO  DL023-ASGMT-NBR-TXT               
174200           MOVE DL020-NXT-NBR-SEQ-NBR TO                                  
174300                              DL023-ASGMT-NBR-SEQ-NBR                     
174400**??    ELSE                                                              
174500**??       MOVE DL020-SEQ-START-NBR TO                                    
174600**??                          DL023-ASGMT-NBR-SEQ-NBR                     
174700        END-IF                                                            
174800                                                                          
174900     ELSE                                                                 
175000        SET DL022-CALLED-MODULE-FAILURE TO TRUE                           
175100        MOVE DL020-CALL-MOD-FAIL-NM TO                                    
175200                            DL022-CALL-MOD-FAIL-NM                        
175300        MOVE DL020-CALL-MOD-FAIL-ERR-CDE TO                               
175400                            DL022-CALL-MOD-FAIL-ERR-CDE                   
175500                                                                          
175600     END-IF.                                                              
175700                                                                          
175800******************************************************************        
175900*                                                                *        
176000******************************************************************        
176100 9000-COMMIT.                                                             
176200                                                                          
176300     EXEC SQL                                                             
176400          COMMIT                                                          
176500     END-EXEC.                                                            
176600                                                                          
176700     EVALUATE TRUE                                                        
176800         WHEN SQLCODE = ZERO                                              
176900         WHEN SQLCODE = +100                                              
177000             CONTINUE                                                     
177100         WHEN OTHER                                                       
177200              DISPLAY 'DLKUP026 - ERROR ON COMMIT'                        
177300     END-EVALUATE.                                                        
177400*                                                              *          
177500*                                                              *          
177600****************************************************************          
177700                                                                          
177800 9400-GET-TIMESTAMP.                                                      
177900                                                                          
178000     EXEC SQL                                                             
178100          SET  :PV-CURRENT-TIMESTAMP   =  CURRENT_TIMESTAMP               
178200              ,:PV-CURRENT-TIME        =  CURRENT_TIME                    
178300              ,:PV-CURRENT-DATE        =  CURRENT_DATE                    
178400     END-EXEC.                                                            
