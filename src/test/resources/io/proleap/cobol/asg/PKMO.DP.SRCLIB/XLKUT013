      * ------------------------------------------------------------- *         
       IDENTIFICATION DIVISION.                                                 
      * ------------------------------------------------------------- *         
       PROGRAM-ID.      XLKUT013.                                               
       AUTHOR.          TKMA536.                                                
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    07-22-2002.                                             
                                                                                
      *****************************************************************         
      *******     COPY MESSAGE QUEUE TO FILE                 **********         
      *****************************************************************         
      *                                                               *         
      *                       Program  Logic                          *         
      *                       --------------                          *         
      *  main                                                         *         
      *  ----                                                         *         
      *                                                               *         
      *   Open the message output sequential file.                    *         
      *                                                               *         
      *   Connect to the queue manager.                               *         
      *   If connection failed then                                   *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE and exit           *         
      *                                                               *         
      *   Open the specified message queue (MQOPEN).                  *         
      *   If open failed then                                         *         
      *            Disconnect from queue manager                      *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE and exit           *         
      *   Endif.                                                      *         
      *                                                               *         
      *   Set the GET message options.                                *         
      *            GET messages from queue (MQGET)                    *         
      *            If GET failed                                      *         
      *                     Call 9000-DISPLAY-ERROR-MESSAGE           *         
      *            Endif                                              *         
      *                                                               *         
      *   Write message to output sequential file                     *         
      *                                                               *         
      *   Close the message queue.                                    *         
      *   If close failed then                                        *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *   Disconnect from the queue manager.                          *         
      *   If disconnect failed then                                   *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *   Close the message output file.                              *         
      *                                                               *         
      *   Exit program.                                               *         
      *                                                               *         
      *****************************************************************         
      * ------------------------------------------------------------- *         
       ENVIRONMENT DIVISION.                                                    
      * ------------------------------------------------------------- *         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.       IBM-3090.                                         
       OBJECT-COMPUTER.       IBM-3090.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-1                                                
                  ASSIGN TO INP01.                                              
           SELECT CONTROL-CARD-2                                                
                  ASSIGN TO INP02.                                              
           SELECT ERR-EXTRACT                                                   
                  ASSIGN TO OUT01.                                              
                                                                                
      * ------------------------------------------------------------- *         
       DATA DIVISION.                                                           
      * ------------------------------------------------------------- *         
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-1-LINE              PIC  X(80).                         
                                                                                
       FD  CONTROL-CARD-2                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-2-LINE              PIC  X(80).                         
                                                                                
       FD  ERR-EXTRACT                                                          
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  ERR-EXTRACT-RECORD               PIC X(500).                         
                                                                                
      * ------------------------------------------------------------- *         
       WORKING-STORAGE SECTION.                                                 
      * ------------------------------------------------------------- *         
      *                                                                         
                                                                                
                                                                                
       01  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.                  
       01  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.                  
       01  WS-MQGET                PIC X(8)  VALUE 'CSQBGET'.                   
       01  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.                  
       01  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.                  
       01  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.                  
       01  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.                  
      *                                                                         
      *    General work fields                                                  
      *                                                                         
       01  NUMRECS                 PIC 9(9)  VALUE ZEROS.                       
       01  NUMGETS                 PIC S9(9) BINARY VALUE 0.                    
       01  NUMPUTS                 PIC 9(9)  VALUE ZEROS.                       
       01  NUMCMTS                 PIC 9(9)  VALUE ZEROS.                       
       01  ERROR-MESSAGE           PIC X(48) VALUE SPACES.                      
       01  PV-PARAGRAPH            PIC X(48) VALUE SPACES.                      
      *                                                                         
      *   Parameter variables                                                   
      *                                                                         
       01  QM-NAME               PIC X(48).                                     
       01  QNAME                 PIC X(48).                                     
       01  PADCHAR               PIC X(1) VALUE '*'.                            
       01  MSGBUFFER             pic X(500) VALUE SPACES.                       
       01  NUMMSGS               PIC S9(9) BINARY VALUE 1.                      
       01  MSGLENGTH             PIC S9(9) BINARY.                              
       01  DATA-LENGTH           PIC S9(9) BINARY.                              
       01  MQCHECK               PIC X(8) VALUE 'MQR2C'.                        
       01  MQ-REASON-TEXT        PIC X(30) VALUE SPACES.                        
       01  HOLD-REASON           PIC S9(9) BINARY.                              
       01  HOLD-COMPLETION-CODE  PIC S9(9) BINARY.                              
      *                                                                         
      *    API fields                                                           
      *                                                                         
       01  HCONN                   PIC S9(9) BINARY VALUE 0.                    
       01  GQ-HANDLE               PIC S9(9) BINARY VALUE 0.                    
       01  OPEN-OPTIONS            PIC S9(9) BINARY.                            
       01  COMPLETION-CODE         PIC S9(9) BINARY.                            
       01  REASON                  PIC S9(9) BINARY.                            
       01  CON-REASON              PIC S9(9) BINARY.                            
                                                                                
       01  PV-ABEND-VARIABLES.                                                  
           05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
           88  ABEND-4019-CAL-SUB-ERROR                    VALUE +4019.         
           88  ABEND-4020-MQ-ERROR                         VALUE +4020.         
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444.         
                                                                                
                                                                                
        1  PE-MQ-ABEND-VARIABLES.                                               
           10  PE-MQ-QMANAGER           PIC X(05) VALUE SPACES.                 
           10  PE-MQ-QNAME              PIC X(30) VALUE SPACES.                 
           10  PE-MQ-COMPLETION-CODE    PIC 9(04) VALUE ZEROS.                  
           10  PE-MQ-REASON-CODE        PIC 9(04) VALUE ZEROS.                  
                                                                                
      *                                                                         
      *    API control blocks                                                   
      *                                                                         
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
       01  MQM-GET-MESSAGE-OPTIONS.                                             
           COPY CMQGMOV.                                                        
      *                                                                         
      *    MQV contains constants (for filling in the control blocks)           
      *    and return codes (for testing the result of a call)                  
      *                                                                         
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
      *                                                                         
      *----------------------------------------------------------------*        
      *    CONTROL CARD                                                         
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-QMGR-NAME              PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
       01  CC-CONTROL-CARD-2.                                                   
           05  CC-STORE-EOD-MSG-QNAME    PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM CONSTANTS                                                    
      *----------------------------------------------------------------*        
           05  PC-PGM-NAME               PIC X(09) VALUE 'XLKUT013 '.           
           05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM SWITCHES                                                     
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
           05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-2-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-2-START         VALUE 'N'.                   
                                                                                
      * ------------------------------------------------------------- *         
       PROCEDURE DIVISION.                                                      
      * ------------------------------------------------------------- *         
       1000-MAIN SECTION.                                                       
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET QUEUE MANAGER NAME                  *         
      * ------------------------------------------------------------- *         
            SET PS-CONTROL-CARD-1-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-1.                                          
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-1-END                                            
                DISPLAY PC-PGM-NAME                                             
                    'CONTROL CARD MISSING - FOR QMGR NAME'                      
                GO TO 4000-END-PROGRAM                                          
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-1                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-1.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET STORE EOD MSG QUEUE NAME            *         
      * ------------------------------------------------------------- *         
                                                                                
            SET PS-CONTROL-CARD-2-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-2.                                          
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
                AT END                                                          
                    SET PS-CONTROL-CARD-2-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-2-END                                            
                DISPLAY PC-PGM-NAME                                             
                   'CONTROL CARD MISSING - STORE EOD MSG QUEUE NAME'            
                GO TO 4000-END-PROGRAM                                          
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-2                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-2.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  OPEN OUTPUT MESSAGE SEQUENTIAL FILE                          *         
      * ------------------------------------------------------------- *         
                                                                                
           OPEN OUTPUT ERR-EXTRACT.                                             
           INITIALIZE ERR-EXTRACT-RECORD.                                       
                                                                                
      * ------------------------------------------------------------- *         
      *                                                                         
      *    Connect to the queue manager                                         
      *                                                                         
           MOVE CC-QMGR-NAME TO QM-NAME.                                        
           CALL WS-MQCONN USING QM-NAME                                         
                               HCONN                                            
                               COMPLETION-CODE                                  
                               REASON.                                          
      *                                                                         
      *    If connection failed then display error message                      
      *    and exit                                                             
      *                                                                         
           MOVE REASON TO CON-REASON.                                           
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              MOVE 'MQCONN'   TO ERROR-MESSAGE                                  
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 4000-END-PROGRAM                                            
           END-IF.                                                              
           DISPLAY 'MQCONN SUCCESSFUL TO ', QM-NAME.                            
      *                                                                         
      *    Open the queue for input                                             
      *                                                                         
           COMPUTE OPEN-OPTIONS = MQOO-INPUT-SHARED +                           
                                  MQOO-FAIL-IF-QUIESCING.                       
           MOVE CC-STORE-EOD-MSG-QNAME   TO MQOD-OBJECTNAME.                    
           MOVE QM-NAME TO MQOD-OBJECTQMGRNAME.                                 
      *                                                                         
           CALL WS-MQOPEN USING HCONN                                           
                               MQM-OBJECT-DESCRIPTOR                            
                               OPEN-OPTIONS                                     
                               GQ-HANDLE                                        
                               COMPLETION-CODE                                  
                               REASON.                                          
      *                                                                         
      *    If open failed then display error message                            
      *    and exit.                                                            
      *                                                                         
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              MOVE 'MQOPEN'   TO ERROR-MESSAGE                                  
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 3000-DISCONNECT-QMGR                                        
           END-IF.                                                              
           DISPLAY 'MQOPEN SUCCESSFUL'.                                         
      *                                                                         
           PERFORM 2000-MQGET-EOD-MSG.                                          
      *                                                                         
      *    Close the queue                                                      
      *                                                                         
           CALL WS-MQCLOSE USING HCONN                                          
                                GQ-HANDLE                                       
                                MQCO-NONE                                       
                                COMPLETION-CODE                                 
                                REASON.                                         
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              MOVE 'MQCLOSE'  TO ERROR-MESSAGE                                  
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              DISPLAY 'MQCLOSE SUCCESSFUL'                                      
           END-IF.                                                              
      *                                                                         
           PERFORM 3000-DISCONNECT-QMGR.                                        
           GO TO 4000-END-PROGRAM.                                              
      *                                                                         
      * ------------------------------------------------------------- *         
      * PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
      * ------------------------------------------------------------- *         
       2000-MQGET-EOD-MSG.                                                      
                                                                                
           PERFORM                                                              
             UNTIL (COMPLETION-CODE IS EQUAL TO MQCC-FAILED)                    
               MOVE MQMI-NONE TO MQMD-MSGID                                     
               MOVE MQCI-NONE TO MQMD-CORRELID                                  
               MOVE 500 TO MSGLENGTH                                            
               COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                       
                                        MQGMO-WAIT                              
                                                                                
               CALL WS-MQGET USING HCONN                                        
                                  GQ-HANDLE                                     
                                  MQM-MESSAGE-DESCRIPTOR                        
                                  MQM-GET-MESSAGE-OPTIONS                       
                                  MSGLENGTH                                     
                                  MSGBUFFER                                     
                                  DATA-LENGTH                                   
                                  COMPLETION-CODE                               
                                  REASON                                        
                                                                                
      *        If put failed then display error message                         
      *        and break out of loop                                            
                                                                                
               IF (COMPLETION-CODE NOT = MQCC-OK) THEN                          
                  MOVE 'MQGET'     TO ERROR-MESSAGE                             
                  PERFORM 9000-DISPLAY-ERROR-MESSAGE                            
               ELSE                                                             
                  ADD 1 TO NUMRECS                                              
                      IF NUMRECS > 1000                                         
                          PERFORM 3600-MQ-COMMIT                                
                          MOVE ZERO TO NUMRECS                                  
                      END-IF                                                    
                  ADD 1 TO NUMGETS                                              
                  PERFORM 8000-WRITE-EXTRACT-RECORD                             
               END-IF                                                           
           END-PERFORM.                                                         
      *                                                                         
      * 2000-END-MQGET-EOD-MSG.                                                 
      *                                                                         
      * ------------------------------------------------------------- *         
      * CLOSE AND DISCONNECT FROM THE MQ QUEUE MANAGER                          
      * ------------------------------------------------------------- *         
       3000-DISCONNECT-QMGR.                                                    
      *                                                                         
      *    Disconnect from the queue manager                                    
      *                                                                         
           IF CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED                 
              CALL WS-MQDISC USING HCONN                                        
                                  COMPLETION-CODE                               
                                  REASON                                        
              IF (COMPLETION-CODE NOT = MQCC-OK) THEN                           
                 MOVE 'MQDISC'   TO ERROR-MESSAGE                               
                 PERFORM 9000-DISPLAY-ERROR-MESSAGE                             
              ELSE                                                              
                 DISPLAY 'MQDISC SUCCESSFUL'                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *    Close the message extract output file.                               
                                                                                
           CLOSE ERR-EXTRACT.                                                   
      *                                                                         
      * 3000-END-DISCONNECT.                                                    
      ******************************************************************        
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT                
      ******************************************************************        
       3600-MQ-COMMIT.                                                          
                                                                                
           CALL WS-MQCMIT USING HCONN                                           
                                COMPLETION-CODE                                 
                                REASON.                                         
                                                                                
           IF (COMPLETION-CODE NOT = MQCC-OK)                                   
               CALL MQCHECK USING REASON                                        
                                     MQ-REASON-TEXT                             
              MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                           
              MOVE 'MQCMIT ERROR'     TO ERROR-MESSAGE                          
              MOVE COMPLETION-CODE    TO HOLD-COMPLETION-CODE                   
              MOVE REASON             TO HOLD-REASON                            
              PERFORM 3700-BACK-OUT-MQ                                          
              MOVE HOLD-COMPLETION-CODE TO COMPLETION-CODE                      
                                              PE-MQ-COMPLETION-CODE             
              MOVE HOLD-REASON          TO REASON                               
                                              PE-MQ-REASON-CODE                 
              PERFORM 9200-MQ-ABEND                                             
           ELSE                                                                 
              ADD +1 TO NUMCMTS                                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT               
      ******************************************************************        
       3700-BACK-OUT-MQ.                                                        
                                                                                
           CALL WS-MQBACK USING HCONN                                           
                                COMPLETION-CODE                                 
                                REASON.                                         
                                                                                
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
               CALL MQCHECK USING REASON                                        
                                  MQ-REASON-TEXT                                
              MOVE '3700-BACK-OUT-MQ'  TO PV-PARAGRAPH                          
              MOVE 'MQBACK ERROR'      TO ERROR-MESSAGE                         
              PERFORM 9200-MQ-ABEND                                             
           END-IF.                                                              
      ******************************************************************        
                                                                                
      * ------------------------------------------------------------- *         
      * END THE PROGRAM                                                         
      * ------------------------------------------------------------- *         
       4000-END-PROGRAM.                                                        
      *                                                                         
      *    Display the number of messages successfully put                      
      *    to the queue                                                         
      *                                                                         
           DISPLAY NUMGETS, ' MESSAGES READ FROM QUEUE'.                        
           DISPLAY NUMPUTS, ' ERR MSGS PUT TO DISK    '.                        
           DISPLAY NUMCMTS, ' NO. OF COMMITS TAKEN    '.                        
      *                                                                         
           IF NUMPUTS > 0                                                       
              MOVE 4095 TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
      *                                                                         
      * ------------------------------------------------------------- *         
      * WRITE THE ERROR MESSAGE QUEUE TO A SEQUENTIAL FILE.                     
      * ------------------------------------------------------------- *         
       8000-WRITE-EXTRACT-RECORD.                                               
      * ------------------------------------------------------------- *         
      *                                                                         
           INITIALIZE ERR-EXTRACT-RECORD.                                       
           MOVE MSGBUFFER TO ERR-EXTRACT-RECORD.                                
           WRITE ERR-EXTRACT-RECORD.                                            
           ADD +1 TO NUMPUTS.                                                   
      *                                                                         
      * ------------------------------------------------------------- *         
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
      * ------------------------------------------------------------- *         
       9000-DISPLAY-ERROR-MESSAGE.                                              
      * ------------------------------------------------------------- *         
      *                                                                         
           DISPLAY '************************************************'.          
           DISPLAY '* ', ERROR-MESSAGE.                                         
           DISPLAY '* COMPLETION CODE : ', COMPLETION-CODE.                     
           DISPLAY '* REASON CODE     : ', REASON.                              
           DISPLAY '************************************************'.          
      *                                                                         
      * 9000-DISPLAY-ERROR-MSG-END.                                             
      *                                                                         
                                                                                
      ****************************************************************          
      ** PERFORM MQ-SERIES ABEND PROCESSING                                     
      ****************************************************************          
       9200-MQ-ABEND.                                                           
                                                                                
           DISPLAY '**** ABEND *************************************'.          
           DISPLAY '** MQSERIES ERROR **'.                                      
           DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                              
           DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                            
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
           DISPLAY '** MESSAGE:    ', ERROR-MESSAGE.                            
           DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.                    
           DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                        
           DISPLAY '** RSN TEXT:   ', MQ-REASON-TEXT.                           
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
                                                                                
206800******************************************************************        
206900**                      END OF PROGRAM                                    
207000*                                                                         
207100******************************************************************        
