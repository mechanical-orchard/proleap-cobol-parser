       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT014.                                                 
       AUTHOR.        JON FIEDLER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/23/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      *    THIS PROGRAM EXTRACTS INVENTORY DATA FOR THE EDI 852                 
      *    DOCUMENT - PRODUCT ACTIVITY DATA.                                    
      *                                                                         
      *    ON-HANDS MAY BE REPORTED DAILY OR WEEKLY.                            
      *    ON-HANDS MAY BE REPORTED AT CORP LEVEL (ALL STORES), STORE           
      *    LEVEL OR STORE LEVEL WITH CORPORATE SUMMARY.                         
      *                                                                         
      *    WEEKLY REPORTING IS FOR A KOHLS DEPARTMENT STORES FISCAL             
      *    WEEK, WHICH BEGINS ON SUNDAY AND ENDS ON SATURDAY.                   
      *    ALL FIGURES ARE FOR END OF THE FISCAL WEEK (PRIOR TO RUN             
      *    DATE).                                                               
      *                                                                         
      *    DAILY REPORTING PROVIDES ON-HANDS AT END OF PRIOR DAY.               
      *    (ALTHOUGH THERE ARE SOME TRANSACTION AGAINST THIS DATA               
      *     DURING THE DAY, SO THIS DATA IS DYNAMIC.)                           
      *                                                                         
      *    ( NOTE:  THE CONTROL CARD INPUT PROCESSING DATE IS THE DATE          
      *             THE JOB IS EXECUTED MINUS 1 DAY  (PREV DAY). )              
      *                                                                         
      *    THIS PROGRAM USES THE NEW 'F4' CALENDAR TO DETERMINE                 
      *    ALL DATES REQUIRED, FROM THE CALENDAR ROUTINE.                       
      *    THUS, THE ONLY REQUIRED INPUTS WILL BE THE SCHEDULED                 
      *    RUN DATE (- 1 DAY), AND THE FISCAL CALENDAR TYPE OF 'F4'.            
      *                                                                         
*******************************************************************             
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ITEM-SELECT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EDI-SALES-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ITEM-SELECT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ITEM-SELECT-RECORD.                                   
                                                                                
       01  ITEM-SELECT-RECORD                   PIC X(80).                      
                                                                                
       FD  EDI-SALES-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS EDI-SALES-EXTRACT-RECORD.                             
                                                                                
       01  EDI-SALES-EXTRACT-RECORD             PIC X(80).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUN-DATE.                                                     
               10  CC-RUN-MM                    PIC X(02)  VALUE SPACES.        
               10  CC-RUN-DD                    PIC X(02)  VALUE SPACES.        
               10  CC-RUN-YY                    PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(01)  VALUE SPACES.        
           05  CC-FISCAL-CALENDAR-TYPE          PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(71)  VALUE SPACES.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(08)  VALUE                
                                                           'ETKUT014'.          
           05  PC-DPKUT500-CALENDAR             PIC X(08)  VALUE                
                                                           'DPKUT500'.          
           05  PC-YES                           PIC X(1)   VALUE 'Y'.           
           05  PC-NO                            PIC X(1)   VALUE 'N'.           
           05  PC-SALES-REC                     PIC 9(1)   VALUE 1.             
           05  PC-INVENTORY-REC                 PIC 9(1)   VALUE 2.             
           05  PC-ONORDER-REC                   PIC 9(1)   VALUE 3.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
                                                                                
           05  PS-INPUT-EOF-SW                  PIC X(01)  VALUE 'N'.           
               88  PS-EOF-INPUT                            VALUE 'Y'.           
                                                                                
           05  PS-MORE-ITEMS-SW                 PIC X(01)  VALUE 'N'.           
               88  PS-MORE-ITEMS                           VALUE 'Y'.           
               88  PS-NO-MORE-ITEMS                        VALUE 'N'.           
                                                                                
           05  PS-MORE-STORES-SW                PIC X(01)  VALUE 'N'.           
               88  PS-MORE-STORES                          VALUE 'Y'.           
               88  PS-NO-MORE-STORES                       VALUE 'N'.           
                                                                                
           05  PS-PROCESS-VENDOR-SW             PIC X(01)  VALUE 'N'.           
               88  PS-PROCESS-THIS-VENDOR                  VALUE 'Y'.           
                                                                                
           05  PS-PROCESS-ONORDER-SW            PIC X(01)  VALUE 'N'.           
               88  PS-PROCESS-ONORDER                      VALUE 'Y'.           
                                                                                
           05  PS-PROCESS-DAILY-SW              PIC X(01)  VALUE 'N'.           
               88  PS-PROCESS-DAILY                        VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.        
           05  PV-DEPT                          PIC S9(04) COMP-3.              
           05  PV-DEPT-INHSE-NBR                PIC S9(03) COMP-3.              
JF0305     05  PV-BEGIN-STORE                   PIC S9(04) COMP.                
JF0305     05  PV-END-STORE                     PIC S9(04) COMP.                
JF0305     05  PV-LOC-NMBR                      PIC 9(05).                      
                                                                                
       01  PV-PROCESSING-DATES.                                                 
                                                                                
           05  PV-YESTERDAY-WKDY                PIC 9(01)  VALUE ZERO.          
               88  PV-YESTERDAY-SUNDAY          VALUE 1.                        
               88  PV-YESTERDAY-MONDAY          VALUE 2.                        
               88  PV-YESTERDAY-TUESDAY         VALUE 3.                        
               88  PV-YESTERDAY-WEDNESDAY       VALUE 4.                        
               88  PV-YESTERDAY-THURSDAY        VALUE 5.                        
               88  PV-YESTERDAY-FRIDAY          VALUE 6.                        
               88  PV-YESTERDAY-SATURDAY        VALUE 7.                        
                                                                                
           05  PV-RUN-DATE-WKDY                 PIC 9(01)  VALUE ZERO.          
               88  PV-RUN-DATE-SUNDAY           VALUE 1.                        
               88  PV-RUN-DATE-MONDAY           VALUE 2.                        
               88  PV-RUN-DATE-TUESDAY          VALUE 3.                        
               88  PV-RUN-DATE-WEDNESDAY        VALUE 4.                        
               88  PV-RUN-DATE-THURSDAY         VALUE 5.                        
               88  PV-RUN-DATE-FRIDAY           VALUE 6.                        
               88  PV-RUN-DATE-SATURDAY         VALUE 7.                        
                                                                                
           05  PV-PRE-WK-STRT-DATE.                                             
               10 PV-PRE-WK-STRT-CCYY           PIC X(04).                      
               10 FILLER                        PIC X(01).                      
               10 PV-PRE-WK-STRT-MM             PIC X(02).                      
               10 FILLER                        PIC X(01).                      
               10 PV-PRE-WK-STRT-DD             PIC X(02).                      
                                                                                
           05  PV-PRE-WK-STRT-DATE-FMT.                                         
               10 PV-PRE-WK-STRT-CCYY-FMT       PIC X(04).                      
               10 PV-PRE-WK-STRT-MM-FMT         PIC X(02).                      
               10 PV-PRE-WK-STRT-DD-FMT         PIC X(02).                      
                                                                                
           05  PV-PRE-WK-END-DATE.                                              
               10 PV-PRE-WK-END-CCYY            PIC X(04).                      
               10 FILLER                        PIC X(01).                      
               10 PV-PRE-WK-END-MM              PIC X(02).                      
               10 FILLER                        PIC X(01).                      
               10 PV-PRE-WK-END-DD              PIC X(02).                      
                                                                                
           05  PV-PRE-WK-END-DATE-FMT.                                          
               10 PV-PRE-WK-END-CCYY-FMT        PIC X(04).                      
               10 PV-PRE-WK-END-MM-FMT          PIC X(02).                      
               10 PV-PRE-WK-END-DD-FMT          PIC X(02).                      
                                                                                
           05  PV-CUR-WK-STRT-DATE-DB2ISO-FMT   PIC X(10)  VALUE SPACES.        
           05  PV-CC-DATE-DB2ISO                PIC X(08)  VALUE SPACES.        
           05  PV-GREG-DTE                      PIC X(06)  VALUE SPACES.        
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YY                            PIC 9(02)  VALUE ZEROES.        
           05  TD-MM                            PIC 9(02)  VALUE ZEROES.        
           05  TD-DD                            PIC 9(02)  VALUE ZEROES.        
                                                                                
       01  ABEND-CODE                           PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
                                                                                
      *****************************************************************         
      *    CALENDAR ROUTINE WORKING STORAGE  'DPKUT500'                         
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *    INPUT RECORD LAYOUT - SELECT ITEM RECORD                             
      ******************************************************************        
           COPY ETRD007.                                                        
                                                                                
      *****************************************************************         
      *    OUTPUT RECORD LAYOUT - EDI 852 DATA EXTRACT RECORD                   
      *****************************************************************         
           COPY ETRD008.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *    SKU DB LAYOUT                                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSK                                                      
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU CROSS REFERENCE DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    UPC TO SKU CROSS REFERENCE DB LAYOUT                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU/STORE DB LAYOUT     ( FOR RETREVING DAILY ON-HANDS )             
      *                                                                         
      *    NOTE :  ONHANDS ARE AS OF THE BEGINNING OF THE TRANSMIT DAY,         
      *            PLUS OR MINUS ANY ADJUSTMENTS MADE ONLINE THUS FAR.          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU/STORE WEEKLY INVENTORY DB LAYOUT                                 
      *                                                                         
      *    NOTE :  ONHANDS ARE AS OF THE BEGINNING OF THE WEEK.                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKSTWKI                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING PARTNER DB LAYOUT                                            
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDPRT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING TRANSACTION DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDTRN                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *     SELECT ALL ITEMS FOR A DEPT/INHSE COMBINATION                       
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE ITEM_UPC CURSOR FOR                                      
               SELECT ITEM_NMBR,                                                
                      UPC_NBR                                                   
               FROM   TSK, TUPCSKU, TSKXREF                                     
               WHERE  TSKXREF.DEPT      = :PV-DEPT                              
                 AND  VNDR_IN_HSE_NMBR  = :PV-DEPT-INHSE-NBR                    
                 AND  EDI_ORD_IND       = :PC-YES                               
                 AND  TSK.ITEM_NMBR     = TUPCSKU.ITEM_NBR                      
                 AND  TSK.ITEM_NMBR     = TSKXREF.ITM_NBR                       
               ORDER BY TSK.ITEM_NMBR                                           
            END-EXEC.                                                           
                                                                                
      *****************************************************************         
      * SELECT DAILY ON-HANDS FOR A SPECIFIC ITEM                               
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE DAILY_ONHANDS CURSOR FOR                                 
JF0305         SELECT TSKST.LOC_NBR,                                            
                      ONHAND_QTY                                                
               FROM   TSKST                                                     
               WHERE  ITEM_NMBR = :TSK-ITEM-NMBR                                
JF0305           AND  TSKST.LOC_NBR BETWEEN :PV-BEGIN-STORE AND                 
                                               :PV-END-STORE                    
                 AND ONHAND_QTY <> 0                                            
            END-EXEC.                                                           
                                                                                
      *****************************************************************         
      * SELECT WEEKLY ON-HANDS FOR A SPECIFIC ITEM                              
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE WEEKLY_ONHANDS CURSOR FOR                                
JF0305         SELECT TSKSTWKI.LOC_NBR,                                         
                      INVEN_ONHAND_QTY                                          
               FROM   TSKSTWKI                                                  
               WHERE  ITEM_NMBR    = :TSK-ITEM-NMBR                             
                 AND  FSCL_BOW_DTE = :PV-CUR-WK-STRT-DATE-DB2ISO-FMT            
JF0305           AND  TSKSTWKI.LOC_NBR BETWEEN :PV-BEGIN-STORE AND              
                                                  :PV-END-STORE                 
            END-EXEC.                                                           
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
      ******************************************************************        
      *    OPEN ALL FILES                                                       
      *    PERFORM INITIALIZATIONS                                              
      *    PROCESS EACH ITEM SELECT FILE RECORD UNTIL END-OF-FILE               
      *    CLOSE ALL FILES                                                      
      ******************************************************************        
                                                                                
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           OPEN INPUT  ITEM-SELECT-FILE                                         
                OUTPUT EDI-SALES-EXTRACT-FILE.                                  
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-READ-INPUT-FILE.                                        
                                                                                
           PERFORM B200-PROCESS-SELECT-FILE                                     
             UNTIL PS-EOF-INPUT.                                                
                                                                                
           CLOSE ITEM-SELECT-FILE                                               
                 EDI-SALES-EXTRACT-FILE.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-HOUSEKEEPING.                                                       
                                                                                
      ******************************************************************        
      *    ONE TIME PROCESSING AND INITIALIZATIONS PERFORMED                    
      *    -   EDIT THE CONTROL CARD                                            
      *    -   INITIALIZE EXTRACT FILE WITH COMMON DATA                         
      ******************************************************************        
                                                                                
           MOVE 'B100-HOUSEKEEPING' TO PV-PARAGRAPH-NAME.                       
                                                                                
           INITIALIZE ET008-EDI-852-EXTRACT-REC.                                
                                                                                
           ACCEPT TD-TODAYS-DATE FROM DATE.                                     
           ACCEPT CC-CONTROL-CARD.                                              
                                                                                
           PERFORM Z300-EDIT-CONTROL-CARD.                                      
                                                                                
           COMPUTE PV-RUN-DATE-WKDY = (PV-YESTERDAY-WKDY + 1)                   
           IF PV-RUN-DATE-WKDY = 8                                              
              MOVE 1 TO PV-RUN-DATE-WKDY                                        
           END-IF.                                                              
                                                                                
           DISPLAY 'PV-RUN-DATE-WKDY = ' PV-RUN-DATE-WKDY.                      
                                                                                
       B200-PROCESS-SELECT-FILE.                                                
                                                                                
           MOVE 'B200-PROCESS-SELECT-FILE' TO PV-PARAGRAPH-NAME.                
                                                                                
           IF ET007-VENDOR-REC                                                  
              IF ET007-REPORT-ONHAND                                            
                 PERFORM C100-PROCESS-VENDOR                                    
              ELSE                                                              
                 MOVE PC-NO TO PS-PROCESS-VENDOR-SW                             
              END-IF                                                            
           ELSE                                                                 
              IF PS-PROCESS-THIS-VENDOR                                         
                 PERFORM C200-PROCESS-DEPT-REC                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-INPUT-FILE.                                        
                                                                                
       C100-PROCESS-VENDOR.                                                     
                                                                                
      ******************************************************************        
      *     DETERMINE IF THIS VENDOR IS:                                        
      *     A) TO BE TRANSMITTED THIS RUN                                       
      *        RUN DATE WKDY IS THE NUMERIC DAY OF WEEK RETURNED FROM           
      *        THE DATE ROUTINE.  1=SUNDAY, 2=MONDAY, ETC.                      
      *     B) AN ACTIVE 852 EDI VENDOR                                         
      *                                                                         
      *     IF BOTH OF ABOVE ARE TRUE SET SWITCH TO PROCESS THE VENDOR          
      *                                                                         
      ******************************************************************        
                                                                                
           MOVE 'C100-PROCESS-VENDOR' TO PV-PARAGRAPH-NAME.                     
                                                                                
           MOVE PC-NO TO PS-PROCESS-VENDOR-SW.                                  
                                                                                
           IF PV-RUN-DATE-SUNDAY AND ET007-TRANSMIT-SUN                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-MONDAY AND ET007-TRANSMIT-MON                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-TUESDAY AND ET007-TRANSMIT-TUE                        
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-WEDNESDAY AND ET007-TRANSMIT-WED                      
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-THURSDAY AND ET007-TRANSMIT-THR                       
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-FRIDAY AND ET007-TRANSMIT-FRI                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-SATURDAY AND ET007-TRANSMIT-SAT                       
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF ET007-REPORT-ORDERED                                              
              MOVE PC-YES TO PS-PROCESS-ONORDER-SW                              
           ELSE                                                                 
              MOVE PC-NO TO PS-PROCESS-ONORDER-SW                               
           END-IF.                                                              
                                                                                
           IF ET007-DAILY-REPORT                                                
              MOVE PC-YES TO PS-PROCESS-DAILY-SW                                
           ELSE                                                                 
              MOVE PC-NO TO PS-PROCESS-DAILY-SW                                 
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              MOVE ET007-VENDOR-ENT-ID TO TRDPRT-ENT-ID                         
              PERFORM C110-CHECK-EDI-STATUS                                     
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              IF ET007-STORE-LEVEL                                              
JF0305           MOVE '00001' TO PV-BEGIN-STORE                                 
JF0305           MOVE '10000' TO PV-END-STORE                                   
              ELSE                                                              
                 IF ET007-STORE-SUM-LEVEL                                       
JF0305             MOVE '00000' TO PV-BEGIN-STORE                               
JF0305             MOVE '10000' TO PV-END-STORE                                 
                 ELSE                                                           
JF0305             MOVE '00000' TO PV-BEGIN-STORE                               
JF0305             MOVE '00000' TO PV-END-STORE                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       C110-CHECK-EDI-STATUS.                                                   
                                                                                
           MOVE 'C110-CHECK-EDI-STATUS' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
                SELECT TTRDPRT.ENT_ID                                           
                  INTO :TRDPRT-ENT-ID                                           
                  FROM TTRDPRT,                                                 
                       TTRDTRN                                                  
                 WHERE TTRDPRT.ENT_ID       = :TRDPRT-ENT-ID                    
                   AND TTRDTRN.TP_DKEY      = TTRDPRT.TP_DKEY                   
                   AND TTRDTRN.TRAN_SET_CDE = '852'                             
                   AND TTRDPRT.INACTV_DTE   = '9999-09-09'                      
                   AND TTRDTRN.INACTV_DTE   = '9999-09-09'                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE PC-NO TO PS-PROCESS-VENDOR-SW                             
              ELSE                                                              
                 MOVE 'SELECT TTRDPRT/TTRDTRN' TO                               
                      PV-DB2-OPERATION-ATTEMPTED                                
                 PERFORM Z999-SQL-ABEND                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       C200-PROCESS-DEPT-REC.                                                   
                                                                                
      ******************************************************************        
      *     1)  SELECT ALL ITEMS FOR THE DEPT/INHSE VEND NMBR                   
      *     2)  FOR EACH ITEM GET THE ONHANDS TO BE REPORTED                    
      *     3)  WRITE A RECORD FOR EACH STORE TO REPORT                         
      ******************************************************************        
                                                                                
           MOVE 'C200-PROCESS-DEPT-REC' TO PV-PARAGRAPH-NAME.                   
                                                                                
           MOVE ET007-DEPT TO PV-DEPT.                                          
           MOVE ET007-DEPT-INHOUSE TO PV-DEPT-INHSE-NBR.                        
           MOVE PC-YES TO PS-MORE-ITEMS-SW.                                     
                                                                                
           PERFORM C202-OPEN-ITEM.                                              
                                                                                
           PERFORM C204-FETCH-ITEM.                                             
                                                                                
           PERFORM C210-PROCESS-ITEM UNTIL PS-NO-MORE-ITEMS.                    
                                                                                
           PERFORM C206-CLOSE-ITEM.                                             
                                                                                
       C202-OPEN-ITEM.                                                          
                                                                                
           MOVE 'C202-OPEN-ITEM' TO PV-PARAGRAPH-NAME.                          
                                                                                
           EXEC SQL                                                             
               OPEN ITEM_UPC                                                    
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
               MOVE 'OPEN ITEM CURSOR'    TO                                    
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       C204-FETCH-ITEM.                                                         
                                                                                
           MOVE 'C204-FETCH-ITEM' TO PV-PARAGRAPH-NAME.                         
                                                                                
           EXEC SQL                                                             
                FETCH ITEM_UPC                                                  
                INTO                                                            
                     :TSK-ITEM-NMBR,                                            
                     :TUPCSKU-UPC-NBR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               MOVE PC-NO TO PS-MORE-ITEMS-SW                                   
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               MOVE PC-YES TO PS-MORE-ITEMS-SW                                  
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                   MOVE 'FETCH ITEM CURSOR'  TO                                 
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM Z999-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       C206-CLOSE-ITEM.                                                         
                                                                                
           MOVE 'C206-CLOSE-ITEM' TO PV-PARAGRAPH-NAME.                         
                                                                                
           EXEC SQL                                                             
               CLOSE ITEM_UPC                                                   
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
               MOVE 'CLOSE ITEM CURSOR'   TO                                    
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       C210-PROCESS-ITEM.                                                       
                                                                                
           MOVE 'C210-PROCESS-ITEM' TO PV-PARAGRAPH-NAME.                       
                                                                                
           IF PS-PROCESS-DAILY                                                  
              PERFORM C220-PROCESS-DAILY-ONHANDS                                
           ELSE                                                                 
              PERFORM C230-PROCESS-WEEKLY-ONHANDS                               
           END-IF.                                                              
                                                                                
           PERFORM C204-FETCH-ITEM.                                             
                                                                                
       C220-PROCESS-DAILY-ONHANDS.                                              
                                                                                
           MOVE 'C220-PROCESS-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.              
                                                                                
           MOVE PC-YES TO PS-MORE-STORES-SW.                                    
                                                                                
           PERFORM C222-OPEN-DAILY-ONHANDS.                                     
                                                                                
           PERFORM C224-FETCH-DAILY-ONHANDS                                     
              UNTIL PS-NO-MORE-STORES.                                          
                                                                                
           PERFORM C226-CLOSE-DAILY-ONHANDS.                                    
                                                                                
       C222-OPEN-DAILY-ONHANDS.                                                 
                                                                                
           MOVE 'C222-OPEN-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                 
                                                                                
           EXEC SQL                                                             
               OPEN DAILY_ONHANDS                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
               MOVE 'OPEN DAILY ONHANDS CURSOR'    TO                           
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       C224-FETCH-DAILY-ONHANDS.                                                
                                                                                
           MOVE 'C224-FETCH-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                
                                                                                
           INITIALIZE ET008-EDI-852-EXTRACT-REC.                                
                                                                                
           EXEC SQL                                                             
                FETCH DAILY_ONHANDS                                             
                INTO                                                            
JF0305             :TSKST-LOC-NBR,                                              
                   :TSKST-ONHAND-QTY                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               MOVE PC-NO TO PS-MORE-STORES-SW                                  
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               MOVE PC-YES TO PS-MORE-STORES-SW                                 
      ******************************************************************        
      * WRITE OUT INVENTORY RECORD WITH ONHANDS                                 
      ******************************************************************        
               MOVE PC-INVENTORY-REC    TO ET008-REC-TYPE                       
               MOVE ET007-VENDOR-ENT-ID TO ET008-ENT-ID                         
               MOVE ET007-DEPT          TO ET008-DEPT                           
               MOVE TUPCSKU-UPC-NBR     TO ET008-UPC-CODE                       
JF0305         MOVE TSKST-LOC-NBR       TO PV-LOC-NMBR                          
JF0305         MOVE PV-LOC-NMBR         TO ET008-STORE-NUMBER                   
               MOVE TSKST-ONHAND-QTY    TO ET008-ITEM-QUANTITY                  
               MOVE PV-CC-DATE-DB2ISO   TO ET008-START-DATE                     
               MOVE SPACES              TO ET008-END-DATE                       
               PERFORM Z700-WRITE-EXTRACT-FILE                                  
      ******************************************************************        
      * WRITE OUT ZERO QUANTITY SALES RECORD                                    
      ******************************************************************        
               MOVE PC-SALES-REC TO ET008-REC-TYPE                              
               MOVE 0            TO ET008-ITEM-QUANTITY                         
               PERFORM Z700-WRITE-EXTRACT-FILE                                  
      ******************************************************************        
      * WRITE OUT ZERO QUANTITY ON ORDER RECORD IF THE VENDOR HAS               
      * REQUESTED ON ORDER DATA                                                 
      ******************************************************************        
               IF PS-PROCESS-ONORDER                                            
                  MOVE PC-ONORDER-REC TO ET008-REC-TYPE                         
                  MOVE 0              TO ET008-ITEM-QUANTITY                    
                  PERFORM Z700-WRITE-EXTRACT-FILE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                   MOVE 'FETCH DAILY ONHANDS CURSOR'  TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM Z999-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       C226-CLOSE-DAILY-ONHANDS.                                                
                                                                                
           MOVE 'C226-CLOSE-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                
                                                                                
           EXEC SQL                                                             
               CLOSE DAILY_ONHANDS                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
               MOVE 'CLOSE DAILY ONHANDS CURSOR'   TO                           
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       C230-PROCESS-WEEKLY-ONHANDS.                                             
                                                                                
           MOVE 'C230-PROCESS-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.             
                                                                                
           MOVE PC-YES TO PS-MORE-STORES-SW.                                    
                                                                                
           PERFORM C232-OPEN-WEEKLY-ONHANDS.                                    
                                                                                
           PERFORM C234-FETCH-WEEKLY-ONHANDS                                    
              UNTIL PS-NO-MORE-STORES.                                          
                                                                                
           PERFORM C236-CLOSE-WEEKLY-ONHANDS.                                   
                                                                                
       C232-OPEN-WEEKLY-ONHANDS.                                                
                                                                                
           MOVE 'C232-OPEN-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.                
                                                                                
           EXEC SQL                                                             
               OPEN WEEKLY_ONHANDS                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
               MOVE 'OPEN WEEKLY ONHANDS CURSOR'    TO                          
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       C234-FETCH-WEEKLY-ONHANDS.                                               
                                                                                
           MOVE 'C234-FETCH-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.               
                                                                                
           INITIALIZE ET008-EDI-852-EXTRACT-REC.                                
                                                                                
           EXEC SQL                                                             
                FETCH WEEKLY_ONHANDS                                            
                INTO                                                            
JF0305              :SKSTWKI-LOC-NBR,                                           
                    :SKSTWKI-INVEN-ONHAND-QTY                                   
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               MOVE PC-NO TO PS-MORE-STORES-SW                                  
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               MOVE PC-YES TO PS-MORE-STORES-SW                                 
      ******************************************************************        
      * WRITE OUT INVENTORY RECORD WITH ONHANDS                                 
      ******************************************************************        
               MOVE PC-INVENTORY-REC         TO ET008-REC-TYPE                  
               MOVE ET007-VENDOR-ENT-ID      TO ET008-ENT-ID                    
               MOVE ET007-DEPT               TO ET008-DEPT                      
               MOVE TUPCSKU-UPC-NBR          TO ET008-UPC-CODE                  
JF0305         MOVE SKSTWKI-LOC-NBR          TO PV-LOC-NMBR                     
JF0305         MOVE PV-LOC-NMBR              TO ET008-STORE-NUMBER              
               MOVE SKSTWKI-INVEN-ONHAND-QTY TO ET008-ITEM-QUANTITY             
               MOVE PV-PRE-WK-STRT-DATE-FMT  TO ET008-START-DATE                
               MOVE PV-PRE-WK-END-DATE-FMT   TO ET008-END-DATE                  
               PERFORM Z700-WRITE-EXTRACT-FILE                                  
      ******************************************************************        
      * WRITE OUT ZERO QUANTITY SALES RECORD                                    
      ******************************************************************        
               MOVE PC-SALES-REC TO ET008-REC-TYPE                              
               MOVE 0            TO ET008-ITEM-QUANTITY                         
               PERFORM Z700-WRITE-EXTRACT-FILE                                  
      ******************************************************************        
      * WRITE OUT ZERO QUANTITY ON ORDER RECORD IF THE VENDOR HAS               
      * REQUESTED ON ORDER DATA                                                 
      ******************************************************************        
               IF PS-PROCESS-ONORDER AND PV-RUN-DATE-SUNDAY                     
                  MOVE PC-ONORDER-REC TO ET008-REC-TYPE                         
                  MOVE 0              TO ET008-ITEM-QUANTITY                    
                  PERFORM Z700-WRITE-EXTRACT-FILE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                   MOVE 'FETCH WEEKLY ONHANDS CURSOR'  TO                       
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM Z999-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       C236-CLOSE-WEEKLY-ONHANDS.                                               
                                                                                
           MOVE 'C236-CLOSE-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.               
                                                                                
           EXEC SQL                                                             
               CLOSE WEEKLY_ONHANDS                                             
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
               MOVE 'CLOSE WEEKLY ONHANDS CURSOR'   TO                          
                    PV-DB2-OPERATION-ATTEMPTED                                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       Z300-EDIT-CONTROL-CARD.                                                  
                                                                                
      ******************************************************************        
      *    VERIFY THAT THE RUN DATE RECEIVED ON THE CONTROL CARD IS             
      *    NUMERIC, THEN UTILIZE THIS DATE TO CALL THE CALENDAR ROUTINE         
      *    AND RETREIVE ALL THE OTHER REQUIRED PROCESSING DATES.                
      ******************************************************************        
                                                                                
           MOVE 'Z300-EDIT-CONTROL-CARD' TO PV-PARAGRAPH-NAME.                  
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
           IF (CC-RUN-DATE NOT NUMERIC)          OR                             
              (CC-FISCAL-CALENDAR-TYPE = SPACES)                                
                                                                                
               DISPLAY '  '                                                     
               DISPLAY '** ABEND IN ETKUT003 **'                                
               DISPLAY 'CONTROL CARD RUN DATE OR CALENDAR TYPE INVALID'         
               DISPLAY 'CC-RUN-DATE          ' CC-RUN-DATE                      
               DISPLAY 'CC-FISCAL-CALENDAR-TYPE '                               
                                               CC-FISCAL-CALENDAR-TYPE          
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
      *        ** USE THE CALENDAR ROUTINE TO RETREIVE THE FISCAL YEAR          
      *        ** AND WEEK FOR THE RUN DATE RECEIVED                            
                                                                                
               MOVE CC-RUN-DATE TO DPG52-GREG-DATE                              
               PERFORM Z400-CALL-CALENDAR-DPKUT500                              
                                                                                
               IF NOT DPG54-SEVERE-ERROR                                        
                                                                                
                   MOVE DPG55-NUMERIC-WEEK-DAY TO                               
                                        PV-YESTERDAY-WKDY                       
                   MOVE DPG55-DB2-ISO-DATE TO PV-CC-DATE-DB2ISO                 
                   DISPLAY 'PV-CC-DATE-DB2ISO = ' PV-CC-DATE-DB2ISO             
                                                                                
                  IF DPG55-ALPHA-WEEK-DAY-ABBR3 = 'SAT'                         
                                                                                
                       MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                        
                                                    PV-PRE-WK-STRT-DATE         
                       DISPLAY 'PV-PRE-WK-STRT-DATE = '                         
                                                    PV-PRE-WK-STRT-DATE         
                                                                                
                       MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO                       
                                                    PV-PRE-WK-END-DATE          
                       DISPLAY 'PV-PRE-WK-END-DATE = '                          
                                                    PV-PRE-WK-END-DATE          
                                                                                
      *           **  CALL THE CALENDAR ROUTINE A SECOND TIME, WITH             
      *           **  END DATE OF RUN DATE WEEK, PLUS 1                         
      *           **  (THIS WILL RETURN THE CURRENT WEEK'S START DATE           
      *           **  FOR A SATURDAY CONTROL CARD DATE)                         
                                                                                
                       MOVE DPG55-LAST-WKDY-GREG TO  PV-GREG-DTE                
                       INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56           
                       MOVE PV-GREG-DTE          TO DPG52-GREG-DATE             
                                                                                
                       MOVE 1 TO DPG51-INCR-DECR-DAYS-9                         
                       SET DPG51-INCREMENT-DATE  TO TRUE                        
                       PERFORM Z400-CALL-CALENDAR-DPKUT500                      
                                                                                
                       IF NOT DPG54-SEVERE-ERROR                                
                                                                                
                          MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                     
                                        PV-CUR-WK-STRT-DATE-DB2ISO-FMT          
                          DISPLAY 'PV-CUR-WK-STRT-DATE-DB2ISO-FMT = '           
                                        PV-CUR-WK-STRT-DATE-DB2ISO-FMT          
                                                                                
                       END-IF                                                   
                  ELSE                                                          
                                                                                
                   MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                            
                                        PV-CUR-WK-STRT-DATE-DB2ISO-FMT          
                   DISPLAY 'PV-CUR-WK-STRT-DATE-DB2ISO-FMT = '                  
                                        PV-CUR-WK-STRT-DATE-DB2ISO-FMT          
                                                                                
      *           **  CALL THE CALENDAR ROUTINE A SECOND TIME, WITH             
      *           **  END DATE OF RUN DATE WEEK, MINUS 1                        
      *           **  (THIS WILL RETURN THE PREVIOUS WEEK'S INFO)               
                   MOVE DPG55-1ST-WKDY-GREG TO  PV-GREG-DTE                     
                   INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56               
                   MOVE PV-GREG-DTE         TO DPG52-GREG-DATE                  
                                                                                
                   MOVE 1 TO DPG51-INCR-DECR-DAYS-9                             
                   SET DPG51-DECREMENT-DATE  TO TRUE                            
                   PERFORM Z400-CALL-CALENDAR-DPKUT500                          
                                                                                
                   IF NOT DPG54-SEVERE-ERROR                                    
                                                                                
                       MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                        
                                                    PV-PRE-WK-STRT-DATE         
                       DISPLAY 'PV-PRE-WK-STRT-DATE = '                         
                                                    PV-PRE-WK-STRT-DATE         
                                                                                
                       MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO                       
                                                    PV-PRE-WK-END-DATE          
                       DISPLAY 'PV-PRE-WK-END-DATE = '                          
                                                    PV-PRE-WK-END-DATE          
                   END-IF                                                       
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FORMAT PREVIOUS WEEK START AND END DATE                                 
      * DB2 ISO FORMAT --> DB2 ISO                                              
      *                                                                         
      * 2004-01-29  --> 20040129                                                
      ******************************************************************        
           MOVE PV-PRE-WK-END-CCYY TO PV-PRE-WK-END-CCYY-FMT.                   
           MOVE PV-PRE-WK-END-MM   TO PV-PRE-WK-END-MM-FMT.                     
           MOVE PV-PRE-WK-END-DD   TO PV-PRE-WK-END-DD-FMT.                     
                                                                                
           MOVE PV-PRE-WK-STRT-CCYY TO PV-PRE-WK-STRT-CCYY-FMT.                 
           MOVE PV-PRE-WK-STRT-MM   TO PV-PRE-WK-STRT-MM-FMT.                   
           MOVE PV-PRE-WK-STRT-DD   TO PV-PRE-WK-STRT-DD-FMT.                   
                                                                                
       Z400-CALL-CALENDAR-DPKUT500.                                             
                                                                                
      ******************************************************************        
      *    CALL THE KOHLS INHOUSE CALENDAR ROUTINE TO DETERMINE                 
      *    ALL REQUIRED PROCESSING DATE INFORMATION.                            
      *                                                                         
      ******************************************************************        
                                                                                
           MOVE 'Z400-CALL-CALENDAR-DPKUT500' TO PV-PARAGRAPH-NAME.             
                                                                                
           MOVE TD-TODAYS-DATE          TO  DPG51-SYSTEM-DATE.                  
           MOVE CC-FISCAL-CALENDAR-TYPE TO  DPG51-CALENDAR-TYPE.                
                                                                                
           SET DPG52-LK-DTE-GREG        TO  TRUE.                               
      *    ** 'DPG52-GREG-DATE' IS SET BEFORE THIS PARAGRAPH **                 
                                                                                
           CALL  PC-DPKUT500-CALENDAR  USING  DPG51 DPG52                       
                                              DPG53 DPG54                       
                                              DPG55 DPG56.                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DPG54-SEVERE-ERROR                                          
                     DISPLAY '** DPKUT500 - SEVERE ERROR :   '                  
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
                     MOVE +4019 TO ABEND-CODE                                   
                     CALL 'ILBOABN0' USING ABEND-CODE                           
               WHEN DPG54-WARNING-ERROR                                         
                     DISPLAY '** DPKUT500 - WARNING :   '                       
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
               WHEN DPG54-NO-ERROR                                              
                     CONTINUE                                                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       Z100-READ-INPUT-FILE.                                                    
                                                                                
           MOVE 'Z100-READ-INPUT-FILE' TO PV-PARAGRAPH-NAME.                    
                                                                                
           READ ITEM-SELECT-FILE                                                
                INTO ET007-ITEM-SELECT-RECORD                                   
                AT END MOVE PC-YES TO PS-INPUT-EOF-SW.                          
                                                                                
       Z700-WRITE-EXTRACT-FILE.                                                 
                                                                                
           MOVE 'Z700-WRITE-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.                 
                                                                                
           WRITE EDI-SALES-EXTRACT-RECORD                                       
              FROM ET008-EDI-852-EXTRACT-REC.                                   
                                                                                
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE 'Z999-SQL-ABEND' TO PV-PARAGRAPH-NAME.                          
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'PROGRAM   = ' PC-PROGRAM-NAME.                              
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
