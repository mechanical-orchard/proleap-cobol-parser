       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP022.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           01/30/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_UPC REPORT                                     00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_UPC ERROR DATA INTO A REPORT           00100000
      ******************************************************************00110000
      *DATE:01/30/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.                        IBM-3090.                00160000
       OBJECT-COMPUTER.                        IBM-3090.                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO RPT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS ARE STANDARD                                   00310000
           BLOCK CONTAINS 0 CHARACTERS.                                 00320000
       01  INPUT-RECORD                    PIC X(080).                  00330000
                                                                        00340000
       FD  REPORT-FILE                                                  00350000
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 CHARACTERS.                                 00380000
       01  REPORT-RECORD                   PIC X(133).                  00390000
                                                                        00400000
       WORKING-STORAGE SECTION.                                         00410000
      ******************************************************************00420000
      *SWITCHES                                                         00430000
      ******************************************************************00440000
       01  PS-PROGRAM-SWITCHES.                                         00450000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00460000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00470000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00480000
      ******************************************************************00490000
      **TIME FIELDS                                                     00500000
      ******************************************************************00510000
       01  DF-DATETIME-FIELDS.                                          00520000
           05  DF-ACCEPT-TIME.                                          00530000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00540000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00550000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00560000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00570000
           05  DF-ACCEPT-DATE.                                          00580000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00590000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00600000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00610000
      ******************************************************************00620000
      *PROGRAM CONSTANTS                                                00630000
      ******************************************************************00640000
       01  PC-PROGRAM-CONSTANTS.                                        00650000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP022'.   00660000
      ******************************************************************00670000
      **PROGRAM COUNTS                                                  00680000
      ******************************************************************00690000
       01  PC-PROGRAM-COUNTERS.                                         00700000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00710000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00720000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00730000
      ***************************************************************** 00740000
      **ABEND CODE FIELDS                                               00750000
      ***************************************************************** 00760000
       01  ABEND-CODE                     PIC S9(4)                     00770000
                                                 VALUE ZEROS COMP SYNC. 00780000
       01  PV-MISC-ABEND-FIELDS.                                        00790000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00800000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00810000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00820000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00830000
      ***************************************************************** 00840000
      **REPORT HEADINGS                                                 00850000
      ***************************************************************** 00860000
       01  REPORT-HEADING-1.                                            00870000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00880000
           05  FILLER                     PIC  X(22)                    00890000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00900000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00910000
       01  REPORT-HEADING-2.                                            00920000
           05  FILLER                     PIC  X(55)  VALUE SPACES.     00930000
           05  FILLER                     PIC  X(24)                    00940000
                                 VALUE 'XST_UPC INTEGRITY REPORT'.      00950000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00960000
       01  REPORT-HEADING-3.                                            00970000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     00980000
           05  FILLER                     PIC  X(06)                    00990000
                                 VALUE 'UPC_ID'.                        01000000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01010000
           05  FILLER                     PIC  X(11)                    01020000
                                 VALUE 'INTR_UPC_ID'.                   01030000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01040000
           05  FILLER                     PIC  X(11)                    01041000
                                 VALUE 'UPC_TYP_CDE'.                   01042000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01042100
           05  FILLER                     PIC  X(07)                    01042200
                                 VALUE 'UPC_NBR'.                       01042300
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01042400
       01  REPORT-HEADING-4.                                            01042500
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01042600
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01042700
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01042800
           05  FILLER                     PIC  X(11)  VALUE ALL '-'.    01042900
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043000
           05  FILLER                     PIC  X(11)  VALUE ALL '-'.    01043100
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043200
           05  FILLER                     PIC  X(15)  VALUE ALL '-'.    01043300
      ***************************************************************** 01043400
      **REPORT DETAIL                                                   01043500
      ***************************************************************** 01043600
       01  REPORT-DETAIL-1.                                             01043700
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043800
           05  RD-XS-UPC-ID               PIC  ZZZZZZZZZ9.              01043900
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01044000
           05  RD-XS-INTRL-UPC-ID         PIC  ZZZZZZZZZ9.              01045000
           05  FILLER                     PIC  X(03)  VALUE SPACES.     01046000
           05  RD-XS-UPC-TYP-CDE          PIC  X(01).                   01047000
           05  FILLER                     PIC  X(12)  VALUE SPACES.     01048000
           05  RD-XS-UPC-NBR              PIC  ZZZZZZZZZZZZZZ9.         01049000
                                                                        01050000
       01  REPORT-DETAIL-2.                                             01060000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01070000
           05  RD-VSM-UPC-ID              PIC  ZZZZZZZZZ9.              01080000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01090000
           05  RD-VSM-INTRL-UPC-ID        PIC  ZZZZZZZZZ9.              01100000
           05  FILLER                     PIC  X(03)  VALUE SPACES.     01110000
           05  RD-VSM-UPC-TYP-CDE         PIC  X(01).                   01120000
           05  FILLER                     PIC  X(12)  VALUE SPACES.     01121000
           05  RD-VSM-UPC-NBR             PIC  ZZZZZZZZZZZZZZ9.         01122000
                                                                        01123000
       01  RF-END-OF-REPORT.                                            01124000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01125000
           05  FILLER                     PIC X(21)                     01126000
                                 VALUE '***END OF REPORT***'.           01127000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01128000
      ***************************************************************** 01129000
      **COPYBOOKS                                                       01130000
      ***************************************************************** 01140000
      ****ABEND ROUTINE INFORMATION                                     01150000
           COPY DPWS900.                                                01160000
      ****ABEND ROUTINE INFORMATION                                     01170000
           COPY DPWS004.                                                01180007
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01190000
           COPY DPWS132O.                                               01200000
      ***INPUT RECORD                                                   01210000
           COPY XSRD014.                                                01220000
                                                                        01230000
       PROCEDURE DIVISION.                                              01240000
      ******************************************************************01250000
       0000-MAINLINE.                                                   01260000
                                                                        01270000
           PERFORM 1000-INITIALIZATION.                                 01280000
           PERFORM 2000-PROCESS-FILES                                   01290000
                   UNTIL PS-EOF-INPUT-FILE.                             01300000
           PERFORM 9999-WRAPUP.                                         01310000
           GOBACK.                                                      01320000
                                                                        01330000
       EJECT.                                                           01340000
      ******************************************************************01350000
      *INITIALIZATION ROUTINE                                           01360000
      ******************************************************************01370000
       1000-INITIALIZATION.                                             01380000
                                                                        01390000
           OPEN INPUT   INPUT-FILE                                      01400000
                OUTPUT  REPORT-FILE.                                    01410000
                                                                        01420000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01430000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01440000
                                                                        01450000
           PERFORM 1100-SETUP-HEADINGS.                                 01460000
                                                                        01470000
           PERFORM 9999-READ-INPUT-FILE.                                01480000
                                                                        01490000
       EJECT.                                                           01500000
      ******************************************************************01510000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     01520000
      ******************************************************************01530000
       1100-SETUP-HEADINGS.                                             01540000
                                                                        01550000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             01560000
                                                                        01570000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               01580000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              01590000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                01600000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               01610000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             01620000
                                                                        01630000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                01640000
           MOVE 1                TO DP132O-REPORT-NUMBER.               01650000
                                                                        01660000
           PERFORM 4000-PRINT-HEADINGS.                                 01670000
                                                                        01680000
       EJECT.                                                           01690000
      ******************************************************************01700000
      *PROCESS BOTH FILES                                               01710000
      ******************************************************************01720000
       2000-PROCESS-FILES.                                              01730000
                                                                        01740000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              01750000
                                                                        01760000
           PERFORM 4100-PRINT-DETAIL.                                   01770000
                                                                        01780000
           IF  PC-LINE-COUNT > 60                                       01790000
               PERFORM 4000-PRINT-HEADINGS                              01800000
           END-IF.                                                      01810000
                                                                        01820000
           PERFORM 9999-READ-INPUT-FILE.                                01830000
                                                                        01840000
       EJECT.                                                           01850000
      ******************************************************************01860000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            01870000
      ******************************************************************01880000
       4000-PRINT-HEADINGS.                                             01890000
                                                                        01900000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             01910000
                                                                        01920000
           INITIALIZE PC-LINE-COUNT.                                    01930000
           ADD +1                     TO PC-PAGE-COUNT.                 01940000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            01950000
                                                                        01960000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      01970000
                                                  AFTER ADVANCING PAGE. 01980000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    01990000
                                               AFTER ADVANCING 2 LINES. 02000000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    02010000
                                               AFTER ADVANCING 1 LINES. 02020000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    02030000
                                               AFTER ADVANCING 3 LINES. 02040000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    02050000
                                               AFTER ADVANCING 1 LINES. 02060000
                                                                        02070000
           ADD +7                     TO PC-LINE-COUNT.                 02080000
                                                                        02090000
       EJECT.                                                           02100000
      ******************************************************************02110000
      *PRINTS DETAIL REPORT                                             02120000
      ******************************************************************02130000
       4100-PRINT-DETAIL.                                               02140000
                                                                        02150000
           MOVE '4100-PRINT-DETAIL' TO PV-PARAGRAPH-NAME.               02160000
                                                                        02170000
           MOVE XSRD014-XS-UPC-ID TO RD-XS-UPC-ID.                      02180000
           MOVE XSRD014-XS-INTRL-UPC-ID TO RD-XS-INTRL-UPC-ID.          02190000
           MOVE XSRD014-XS-UPC-TYP-CDE TO RD-XS-UPC-TYP-CDE.            02200000
           MOVE XSRD014-XS-UPC-NBR TO RD-XS-UPC-NBR.                    02210000
                                                                        02220000
           MOVE XSRD014-VSM-UPC-ID TO RD-VSM-UPC-ID.                    02230000
           MOVE XSRD014-VSM-INTRL-UPC-ID TO RD-VSM-INTRL-UPC-ID.        02240000
           MOVE XSRD014-VSM-UPC-TYP-CDE TO RD-VSM-UPC-TYP-CDE.          02250000
           MOVE XSRD014-VSM-UPC-NBR TO RD-VSM-UPC-NBR.                  02260000
                                                                        02270000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     02280000
                                            AFTER ADVANCING 2 LINES.    02290000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     02300000
                                            AFTER ADVANCING 1 LINE.     02301000
                                                                        02302000
           ADD +3                   TO PC-LINE-COUNT.                   02303000
                                                                        02304000
       EJECT.                                                           02305000
      ******************************************************************02306000
      *READS INPUT FILE                                                 02307000
      ******************************************************************02308000
       9999-READ-INPUT-FILE.                                            02309000
                                                                        02310000
           READ INPUT-FILE INTO XSRD014-UPC-ERR-RECORD                  02320000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     02330000
           END-READ.                                                    02340000
                                                                        02350000
           ADD +1  TO PC-RECORDS-READ.                                  02360000
                                                                        02370000
       EJECT.                                                           02380000
      ******************************************************************02390000
      *PROGRAM END PROCESSING                                           02400000
      ******************************************************************02410000
       9999-WRAPUP.                                                     02420000
                                                                        02430000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    02440000
                                    AFTER ADVANCING 2 LINES.            02450000
                                                                        02460000
           CLOSE INPUT-FILE                                             02470000
                 REPORT-FILE.                                           02480000
                                                                        02490000
           DISPLAY '*************************************************'. 02500000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     02510000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      02520000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      02530000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          02540000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          02550000
           DISPLAY '*************************************************'. 02560000
                                                                        02570000
       EJECT.                                                           02580000
                                                                        02590000
       EJECT.                                                           02600000
