      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT175.                                        
       AUTHOR.                 DAVID WELLER                                     
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           AUGUST 2010                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT175 -                                                  *        
      *                                                                *        
      *    LOG ON TO ORACLE AND OPEN CONTROL CARD.  READ THE CONTROL   *        
      *    CARDTO GET THE RUN DATE AND HOW MANY DAYS BACK YOU WANT TO  *        
      *    START.  RUN A SELECT STATEMENT TAKING THE RUN DATE AND      *        
      *    SUBTRACTING THE NUMBER OF DAYS FROM THE CONTROL CARD FOR    *        
      *    THE DELETE DATE.  ADD A TIMESTAMP TO THE NEW DELETE DATE.   *        
      *    THEN RUN THE DELETE STATEMENT WITH THE DELETE TIMESTAMP.    *        
      *    WHEN DONE CLOSE THE CONTROL CARD AND DISPLAY A MESSAGE      *        
      *    SAYING DONE.                                                *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    10/24/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *    ABENDED ON 'FAILED TO EXTEND ROLLBACK SEGMENT (ORA 1650)'.  *        
      *    CALLED DBA ON CALL - HE CLEARED OUT ROLLBACK SEGMENT (JUST  *        
      *    RUNNING OUT OF ROLLBACK SEGMENT AND GOT BIT).               *        
      *    I CREATED PC-END-TMST-FOR-ABEND - THIS ONLY DELETES ROWS    *        
      *    BEFORE 1:00 PM INSTEAD OF THE WHOLE DAY (JUST TO BREAK UP   *        
      *    THE DELETE A BIT).  RENAME THIS TO PC-END-TMST, MOVE PROG   *        
      *    UP AND RERUN.  THEN CAN RENAME THE OLD ONE BACK THAT        *        
      *    DELETES BEFORE 23:59 AND RUN JOB AGAIN TO GET RID OF THE    *        
      *    REST OF THE DAY.                                            *        
      *                                                                *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD                                                  
               ASSIGN                       TO INP01.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE                PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
070941                                                                          
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUN-DATE                  PIC X(08).                          
           05  FILLER                       PIC X(01).                          
           05  CC-NBR-DAYS-BACK             PIC X(04).                          
           05  FILLER                       PIC X(01).                          
           05  CC-DEL-TYPE                  PIC X(02).                          
               88  CC-REBATE                VALUE 'RB'.                         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-SW           PIC  X(01)     VALUE 'N'.           
               88  PS-CONTROL-CARD-END                     VALUE 'Y'.           
               88  PS-CONTROL-CARD-START                   VALUE 'N'.           
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(1)       VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(1)       VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
070941                                                                          
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                            VALUE '/'.           
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
070941                                                                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
                                                           'PMKUT175'.          
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-END-TMST-FOR-ABEND        PIC  X(08)  VALUE                   
                                                          '13:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4) VALUE ZEROS               
070941                                                COMP SYNC.                
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4) VALUE ZEROS               
070941                                                COMP SYNC.                
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-DELETE-DTE                PIC  X(10)  VALUE SPACES.           
           05  PV-DELETE-TMST               PIC  X(26)  VALUE SPACES.           
           05  PV-ROW-TO-BE-DELETED         PIC S9(20)  VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORDS-READ            PIC 9(09)   VALUE ZEROS.              
           05  PA-RECORDS-INSERTED        PIC 9(09)   VALUE ZEROS.              
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - CLOSE FILES                                                *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           MOVE 0 TO PV-ROW-TO-BE-DELETED.                                      
           PERFORM B110-ORACLE-SETUP.                                           
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-CONTROL-CARD.                                      
                                                                                
           PERFORM C100-FIND-DATE.                                              
                                                                                
           IF  CC-REBATE                                                        
               PERFORM C140-COUNT-INTFC-TBL                                     
               PERFORM C150-DELETE-INTFC-TBL                                    
               DISPLAY 'DONE DELETING PMT_REBT_INTFC_CNTL_TBL TABLE'            
           END-IF.                                                              
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  CONTROL-CARD.                                            
070941     PERFORM B110-ORACLE-SETUP.                                           
070941                                                                          
070941 B110-ORACLE-SETUP.                                                       
070941                                                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     PERFORM B120-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941              ' - ORACLE LOGON CONTROL CARD NOT PROVIDED'                 
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME ' -LOGON TO ORACLE AS USER '        
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941         ' - ERROR LOGGING ON TO ORACLE'                                  
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941******************************************************************        
070941* NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
070941* OTHERWISE GET AN ORACLE ERROR (IE: 3113).                               
070941******************************************************************        
070941     EXEC SQL                                                             
070941      ALTER SESSION                                                       
070941            SET CURSOR_SHARING = 'EXACT'                                  
070941     END-EXEC.                                                            
070941                                                                          
070941                                                                          
070941*****************************************************************         
070941*  B120-READ-ORACLE-LOGON.                                      *         
070941* ==> PROCESSES:                                                *         
070941*   - READ ORCALE CONTROL CARD                                  *         
070941* ==> PERFORMED FROM:  B110-ORACLE-SETUP                        *         
070941*****************************************************************         
070941 B120-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      *****************************************************************         
      * B200-READ-CONTROL-CARD.                                       *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE RUN DATE AND NBR OF DAYS                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-CONTROL-CARD.                                                  
                                                                                
           SET PS-CONTROL-CARD-START       TO TRUE.                             
                                                                                
           READ CONTROL-CARD INTO CC-CONTROL-CARD                               
              AT END                                                            
                 SET PS-CONTROL-CARD-END TO TRUE                                
           END-READ.                                                            
                                                                                
           IF PS-CONTROL-CARD-END                                               
               MOVE 'NO CONTROL CARD FILE TO PROCESS'                           
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-RUN-DATE      = ' CC-RUN-DATE                        
               DISPLAY 'CC-NBR-DAYS-BACK = ' CC-NBR-DAYS-BACK                   
               DISPLAY 'CC-DEL-TYPE      = ' CC-DEL-TYPE                        
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  CONTROL-CARD.                                                 
                                                                                
      *****************************************************************         
      * C100-FIND-DATES:                                              *         
      * ==> PROCESSES:                                                *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-DATE.                                                          
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   TO_CHAR(TO_DATE(:CC-RUN-DATE, 'YYYYMMDD')                    
                                 - :CC-NBR-DAYS-BACK, 'YYYY-MM-DD')             
               INTO                                                             
                   :PV-DELETE-DTE                                               
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'PV-DELETE-DTE  = ' PV-DELETE-DTE                    
                   STRING PV-DELETE-DTE DELIMITED BY SPACE                      
                          PC-SPACE DELIMITED BY SIZE                            
                          PC-END-TMST   DELIMITED BY SIZE                       
                          INTO PV-DELETE-TMST                                   
                   DISPLAY 'PV-DELETE-TMST = ' PV-DELETE-TMST                   
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'C100-FIND-DATE'                                     
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C140-COUNT-INTFC-TBL                                          *         
      * ==> PROCESSES:                                                *         
      *  - COUNT THE RECORDS THAT WILL BE DELETED                     *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = DELETE WAS SUCCESSFULL                                 *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C140-COUNT-INTFC-TBL.                                                    
                                                                                
           EXEC SQL                                                             
             SELECT COUNT(*)                                                    
             INTO :PV-ROW-TO-BE-DELETED                                         
             FROM                                                               
                 PMT_REBT_INTFC_CNTL_TBL                                        
             WHERE                                                              
                 ISRT_TMST <= TO_DATE(:PV-DELETE-TMST,                          
                                    'YYYY-MM-DD HH24:MI:SS')                    
              AND PRC_IND = 'Y'                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
             WHEN SQLCODE = 1403                                                
                 DISPLAY 'NUMBER OF ROWS TO BE DELETED = '                      
                                                  PV-ROW-TO-BE-DELETED          
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                     TO PV-SQL                       
               MOVE 'PMT_REBT_INTFC_CNTL_TBL'   TO PV-TABLE-NAME                
               MOVE 'C150-DELETE-INTFC-TBL'     TO PV-PARAGRAPH-NAME            
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * C150-DELETE-INTFC-TBL                                         *         
      * ==> PROCESSES:                                                *         
      *  - DELETE THE PMT_REBT_INTFC_CNTL_TBL                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = DELETE WAS SUCCESSFULL                                 *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C150-DELETE-INTFC-TBL.                                                   
                                                                                
           EXEC SQL                                                             
             DELETE                                                             
             FROM                                                               
                 PMT_REBT_INTFC_CNTL_TBL                                        
             WHERE                                                              
                 ISRT_TMST <= TO_DATE(:PV-DELETE-TMST,                          
                                    'YYYY-MM-DD HH24:MI:SS')                    
              AND PRC_IND = 'Y'                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 PERFORM C200-COMMIT                                            
                                                                                
             WHEN SQLCODE = 1403                                                
                 CONTINUE                                                       
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                     TO PV-SQL                       
               MOVE 'PMT_REBT_INTFC_CNTL_TBL'   TO PV-TABLE-NAME                
               MOVE 'C150-DELETE-INTFC-TBL'     TO PV-PARAGRAPH-NAME            
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C200-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZEROES                                                  
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
               MOVE 'C200-COMMIT'               TO PV-PARAGRAPH-NAME            
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
