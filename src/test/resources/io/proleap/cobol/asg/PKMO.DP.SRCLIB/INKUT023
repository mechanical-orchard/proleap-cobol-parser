       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT023.                                            
       AUTHOR.             THERESE RAMSEYER.                            00040024
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       09-01-2009.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ***    N E W   P R O G R A M   F O R   H I L O                 ***        
      ******************************************************************        
      *   INKUT023 - CREATE THE OPN_FSCL_MN_DTE & OPN_FSCL_MN_NBR      *        
      *              FROM TMLCNTL IN A FORMATTED CONTROL CARD TO       *        
      *              USE WITH THE IBM UNLOAD UTILITY.                  *        
      *              THIS IS USED IN INK011 FOR THE CURRENT FISCAL     *        
      *              MONTH UNLOADS OF INT_SUB_CLS_INV_LDGR & TINSCLS   *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00113027
      * -------  ----------  ----------------------------------------   00114027
      * WR33304  09/01/2009  THERESE RAMSEYER - INITIAL INSTALLATION.   00119027
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-FILE       ASSIGN TO OUT01.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-RECORD              PIC  X(80).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE +0.                
                                                                                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-TOT-RECS-WRIT             PIC S9(09) COMP-3 VALUE +0.         
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'INKUT023'.        
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +2.                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.              
           05  PV-DISPLAY                   PIC S9(05) COMP-3 VALUE +0.         
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.             
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
                                                                                
       01  CONTROL-CARD-VERBIAGE1.                                              
           05  PV-WHEN                 PIC  X(31) VALUE                         
               '     WHEN (FSCL_MN_END_DTE  = '''.                              
           05  PV-DATE                 PIC  X(10).                              
           05  PV-CLOSE-APOSTROPHE-1   PIC  X(01) VALUE X'7D'.                  
           05  FILLER                  PIC  X(38) VALUE SPACE.                  
                                                                                
       01  CONTROL-CARD-VERBIAGE2.                                              
           05  PV-AND                  PIC  X(31) VALUE                         
               '       AND FSCL_MN_NBR      = '''.                              
           05  PV-NBR                  PIC  X(02).                              
           05  PV-CLOSE-APOSTROPHE-2   PIC  X(01) VALUE X'7D'.                  
           05  FILLER                  PIC  X(38) VALUE SPACE.                  
                                                                                
      *****************************************************************         
      * DB2 FORMATTED ERROR MESSAGE                                             
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE MERCH LEDGER CONTROL TABLE                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SQL COMMUNICATIONS WORK AREA                                            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-BUILD-CONTROL-CARD.                                     
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  OPEN OUTPUT FILE.                                             *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN OUTPUT CONTROL-CARD-FILE.                                       
                                                                                
      ******************************************************************        
      *  GET THE TMLCNTL: OPN_FSCL_MN_DTE & OPN_FSCL_MN_NBR AND        *        
      *  ASSEMBLE THE CONTROL CARD.                                    *        
      ******************************************************************        
       2000-BUILD-CONTROL-CARD.                                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0)                                             
                                                                                
               EXEC SQL                                                         
                   SELECT OPN_FSCL_MN_DTE                                       
                         ,OPN_FSCL_MN_NBR                                       
                   INTO  :MLCNTL-OPN-FSCL-MN-DTE                                
                        ,:MLCNTL-OPN-FSCL-MN-NBR                                
                   FROM TMLCNTL                                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       MOVE MLCNTL-OPN-FSCL-MN-DTE TO PV-DATE                   
                       MOVE MLCNTL-OPN-FSCL-MN-NBR TO PV-NBR                    
                       PERFORM 4000-WRITE-CONTROL-CARD                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '2000-BUILD-CONTROL-CARD'                           
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT TMLCNTL'                                    
                           TO PV-DB2-OPER-ATTEMPTED                             
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '2000-BUILD-CONTROL-CARD' TO PV-PARAGRAPH-NAME              
               MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE BOTH LINES TO CONTROL CARD                             *         
      ******************************************************************        
       4000-WRITE-CONTROL-CARD.                                                 
                                                                                
           WRITE CONTROL-CARD-RECORD FROM CONTROL-CARD-VERBIAGE1.               
           WRITE CONTROL-CARD-RECORD FROM CONTROL-CARD-VERBIAGE2.               
                                                                                
      ******************************************************************        
      *    - CLOSE THE OUTPUT FILE                                     *        
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CONTROL-CARD-FILE.                                             
                                                                                
           DISPLAY SPACE.                                                       
           DISPLAY '*************************************************'. 06840000
           DISPLAY '***********    INKUT023 RUN STATS    ************'. 06840000
           DISPLAY '*************************************************'. 06840000
           DISPLAY 'TMLCNTL OPN_FSCL_MN_DTE : ' MLCNTL-OPN-FSCL-MN-DTE          
           DISPLAY 'TMLCNTL OPN_FSCL_MN_NBR : ' MLCNTL-OPN-FSCL-MN-NBR          
           DISPLAY '*************************************************'. 06840000
           DISPLAY SPACE.                                                       
                                                                                
      ******************************************************************        
      * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
      * CODE OCCURS.                                                            
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      *-----------------------------------------------------------------        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *-----------------------------------------------------------------        
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
