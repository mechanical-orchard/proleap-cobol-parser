      *-----------------------*                                         12/22/94
00002  IDENTIFICATION DIVISION.                                         INKCS006
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    INKCS006.                                         INKCS006
00005  AUTHOR.        M. FURLICK - STRATAGEM                            INKCS006
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS006
00007  DATE-WRITTEN.  08/93.                                            INKCS006
00008  DATE-COMPILED.                                                   INKCS006
00009                                                                   INKCS006
00010 *---------------------------------------------------------------* INKCS006
00011 *    I006 - PHYSICAL INVENTORY INVALID DATA COMPARISON LIST     * INKCS006
00012 *                                                               * INKCS006
00013 *    THIS PROGRAM GENERATES A LIST OF ALL ITMES WITH THE        * INKCS006
00014 *    INVENTORIED DATA AND THE CORRECTED DATA FOR THE SELECTED   * INKCS006
00015 *    STORE AND AREA.                                            * INKCS006
00016 *---------------------------------------------------------------* INKCS006
00017 *                      CHANGE LOG                               * INKCS006
00018 *---------------------------------------------------------------* INKCS006
00019 * 03/94   M. BLAZICH                                            * INKCS006
00020 *    ADDED CODE TO MAKE PROGRAM WORK OFF OF SCAN-LINE-NBR       * INKCS006
00021 *    INSTEAD OF INV-LINE-NBR                                    * INKCS006
00022 *---------------------------------------------------------------* INKCS006
00023 * 08/94   KIM WRIGHT                                            * INKCS006
00024 *    ADDED CODE TO HIGHLIGHT THE INACTIVATED INVALID SKU OR UPC * INKCS006
00025 *---------------------------------------------------------------* INKCS006
W21732* 10/24/02 CHANGED BY IGSI     WR# 21732                        * INKCS006
W21732*                                                               *         
W21732*     CHANGED THE PROGRAM TO DISPLAY THE NON-INTELLIGENT SKU    *         
W21732*     INSTEAD OF DEPT/CLASS/SEQ NUMBER.                         *         
00023 *                                                                    CL**4
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
00025 *---------------------------------------------------------------* INKCS006
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
1122RM*---------------------------------------------------------------* INKCS006
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
263126*----------------------------------------------------------------*   CL*17
00026 /                                                                 INKCS006
00027  ENVIRONMENT DIVISION.                                            INKCS006
00028                                                                   INKCS006
00029  DATA DIVISION.                                                   INKCS006
00030                                                                   INKCS006
00031  WORKING-STORAGE SECTION.                                         INKCS006
00032                                                                   INKCS006
00033  01  PC-PROGRAM-CONSTANTS.                                        INKCS006
00034      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS006
00035                                                     'IN006A  '.   INKCS006
00036      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS006
00037                                                     'INKM006 '.   INKCS006
00038      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS006
00039                                                     'INKCS006'.   INKCS006
00040      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS006
00041      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS006
00042      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS006
00043      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13     INKCS006
00044                                                     COMP SYNC.    INKCS006
00045      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS006
00046                                                     COMP SYNC.    INKCS006
00047      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS006
00048                                                     +1000000      INKCS006
00049                                                     COMP-3.       INKCS006
00050      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS006
00051                                                     COMP SYNC.    INKCS006
00052      05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       INKCS006
00053                                                     COMP-3.       INKCS006
W21732     05  PC-000                         PIC  9(08)  VALUE         INKCS006
W21732                                                    ZEROES.       INKCS006
00055      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  INKCS006
00056      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  INKCS006
00057      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  INKCS006
00058      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  INKCS006
00059      05  PC-Y                           PIC  X(01)  VALUE 'Y'.    INKCS006
00060      05  PC-N                           PIC  X(01)  VALUE 'N'.    INKCS006
00061      05  PC-INTER-APPL-CODES.                                     INKCS006
00062          10  PC-INTER-APPL-CRIT-SPEC                              INKCS006
00063                                   PIC  X(01)  VALUE '1'.          INKCS006
00064          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.          INKCS006
00065                                                                   INKCS006
00066      05  PC-TSYMSG-NUMBERS.                                       INKCS006
00067          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS006
00068          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS006
00069          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS006
00070          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS006
00071          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS006
00072          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS006
00073          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS006
00074          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS006
00075          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS006
00076          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS006
00077          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS006
00078          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS006
00079          10  PC-TSYMSG-00148            PIC  9(05)  VALUE 00148.  INKCS006
00080          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS006
00081          10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  INKCS006
00082 *                                                                 INKCS006
00083  01  PS-PROGRAM-SWITCHES.                                         INKCS006
00084      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS006
00085          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS006
00086          88  PS-ERROR                               VALUE 'N'.    INKCS006
00087      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    INKCS006
00088          88  PS-COMMAREA-VALID                      VALUE 'Y'.    INKCS006
00089          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    INKCS006
00090      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS006
00091          88  PS-DONE                                VALUE 'Y'.    INKCS006
00092          88  PS-NOT-DONE                            VALUE 'N'.    INKCS006
00093 *                                                                 INKCS006
00094  01  PV-PROGRAM-VARIABLES.                                        INKCS006
W26600     05  PV-LOC-NBR           PIC X(04)  VALUE SPACES.            INKCS006
W26600     05  PV-LOC-NBR-NUM REDEFINES                                 INKCS006
W26600         PV-LOC-NBR           PIC 9(04).                          INKCS006
00095      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS006
00096                                                     COMP SYNC.    INKCS006
00097      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS006
00098                                                     COMP SYNC.    INKCS006
00099      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS006
00100                                                     COMP-3.       INKCS006
00101      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS006
00102                                                     COMP-3.       INKCS006
00103      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS006
00104                                                     COMP-3.       INKCS006
00105      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS006
00106                                                     COMP-3.       INKCS006
00107      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS006
00108                                                     COMP-3.       INKCS006
00109      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS006
00110                                                     COMP-3.       INKCS006
00111      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS006
00112                                                     COMP-3.       INKCS006
00113      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS006
00114                                                     COMP SYNC.    INKCS006
00115      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS006
00116                                                     COMP SYNC.    INKCS006
00117      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS006
00118                                                     COMP SYNC.    INKCS006
00119      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS006
00120                                                     COMP SYNC.    INKCS006
00121      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS006
00122                                                     COMP SYNC.    INKCS006
00123      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS006
00124                                                     COMP SYNC.    INKCS006
00125      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS006
00126                                                     COMP SYNC.    INKCS006
00127      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS006
00128                                                     COMP SYNC.    INKCS006
00129      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS006
00130                                                     COMP SYNC.    INKCS006
00131      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS006
00132                                                     COMP SYNC.    INKCS006
00133      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS006
00134                                                     COMP SYNC.    INKCS006
00135      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS006
00136                                                     COMP SYNC.    INKCS006
00137      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS006
00138                                                     COMP SYNC.    INKCS006
00139      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS006
00140                                                     COMP SYNC.    INKCS006
00141      05  PV-SQL-SUB                     PIC S9(04)  VALUE +0      INKCS006
00142                                                     COMP SYNC.    INKCS006
00143      05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      INKCS006
00144                                                     COMP SYNC.    INKCS006
00145      05  PV-WORK-SKU.                                             INKCS006
00146          10  FILLER                     PIC X(01)   VALUE SPACE.  INKCS006
W21732         10  PV-WS-SKU                  PIC X(08).                INKCS006
00152      05  PV-WORK-UPC                    PIC Z(12)9.               INKCS006
00153 /                                                                 INKCS006
00154 *---------------------------------------------------------------* INKCS006
00155 *    MAP LAYOUT                                                 * INKCS006
00156 *---------------------------------------------------------------* INKCS006
00157                                                                   INKCS006
W26600     COPY INKM006.                                                INKCS006
00159 *                                                                 INKCS006
00160  01  MR-OCCURS-LAYOUT                   REDEFINES  IN006AO.       INKCS006
W26600     05  FILLER                         PIC X(94).                INKCS006
00162      05  MR-SCROLL-AREA                 OCCURS 13 TIMES.          INKCS006
00163                                                                   INKCS006
00164          10  MR-LINEL                   PIC S9(04) COMP.          INKCS006
00165          10  MR-LINEA                   PIC  X(01).               INKCS006
00166          10  MR-LINEC                   PIC  X(01).               INKCS006
00167          10  MR-LINEH                   PIC  X(01).               INKCS006
00168          10  MR-LINE-X.                                           INKCS006
00169              15  MR-LINE                PIC ZZZZZZ9.              INKCS006
00170                                                                   INKCS006
00171          10  MR-INV-SKU-UPCL            PIC S9(04) COMP.          INKCS006
00172          10  MR-INV-SKU-UPCA            PIC  X(01).               INKCS006
00173          10  MR-INV-SKU-UPCC            PIC  X(01).               INKCS006
00174          10  MR-INV-SKU-UPCH            PIC  X(01).               INKCS006
00175          10  MR-INV-SKU-UPC             PIC  X(13).               INKCS006
00176                                                                   INKCS006
00177          10  MR-CORR-SKU-UPCL           PIC S9(04) COMP.          INKCS006
00178          10  MR-CORR-SKU-UPCA           PIC  X(01).               INKCS006
00179          10  MR-CORR-SKU-UPCC           PIC  X(01).               INKCS006
00180          10  MR-CORR-SKU-UPCH           PIC  X(01).               INKCS006
00181          10  MR-CORR-SKU-UPC            PIC  X(13).               INKCS006
00182                                                                   INKCS006
00183          10  MR-INV-QTYL                PIC S9(04) COMP.          INKCS006
00184          10  MR-INV-QTYA                PIC  X(01).               INKCS006
00185          10  MR-INV-QTYC                PIC  X(01).               INKCS006
00186          10  MR-INV-QTYH                PIC  X(01).               INKCS006
00187          10  MR-INV-QTY-X.                                        INKCS006
00188              15  MR-INV-QTY             PIC  Z(6)9.               INKCS006
00189                                                                   INKCS006
00190          10  MR-CORR-QTYL               PIC S9(04) COMP.          INKCS006
00191          10  MR-CORR-QTYA               PIC  X(01).               INKCS006
00192          10  MR-CORR-QTYC               PIC  X(01).               INKCS006
00193          10  MR-CORR-QTYH               PIC  X(01).               INKCS006
00194          10  MR-CORR-QTY-X.                                       INKCS006
00195              15  MR-CORR-QTY            PIC  Z(6)9.               INKCS006
00196                                                                   INKCS006
00197          10  MR-INV-PR-AMTL             PIC S9(04) COMP.          INKCS006
00198          10  MR-INV-PR-AMTA             PIC  X(01).               INKCS006
00199          10  MR-INV-PR-AMTC             PIC  X(01).               INKCS006
00200          10  MR-INV-PR-AMTH             PIC  X(01).               INKCS006
00201          10  MR-INV-PR-AMT-X.                                     INKCS006
00202              15  MR-INV-PR-AMT          PIC  Z(7).99.             INKCS006
00203                                                                   INKCS006
00204          10  MR-CORR-PR-AMTL            PIC S9(04) COMP.          INKCS006
00205          10  MR-CORR-PR-AMTA            PIC  X(01).               INKCS006
00206          10  MR-CORR-PR-AMTC            PIC  X(01).               INKCS006
00207          10  MR-CORR-PR-AMTH            PIC  X(01).               INKCS006
00208          10  MR-CORR-PR-AMT-X.                                    INKCS006
00209              15  MR-CORR-PR-AMT         PIC  Z(5).99.             INKCS006
00210 /                                                                 INKCS006
00211 *---------------------------------------------------------------* INKCS006
00212 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS006
00213 *---------------------------------------------------------------* INKCS006
00214                                                                   INKCS006
00215      COPY DPWS015.                                                INKCS006
00216                                                                   INKCS006
00217 *---------------------------------------------------------------* INKCS006
00218 *    FUNCTION KEYS COPYBOOK.                                    * INKCS006
00219 *---------------------------------------------------------------* INKCS006
00220                                                                   INKCS006
00221      COPY DPWS016.                                                INKCS006
00222                                                                   INKCS006
00223 *---------------------------------------------------------------* INKCS006
00224 *    NUMERIC EDIT LAYOUT.                                       * INKCS006
00225 *---------------------------------------------------------------* INKCS006
00226                                                                   INKCS006
00227      COPY DPWS010I.                                               INKCS006
00228                                                                   INKCS006
00229 *---------------------------------------------------------------* INKCS006
00230 *    TSQB AREA RECORD LAYOUT.                                   * INKCS006
00231 *---------------------------------------------------------------* INKCS006
00232                                                                   INKCS006
00233      COPY INWS002.                                                INKCS006
00234 /                                                                 INKCS006
00235 *---------------------------------------------------------------* INKCS006
00236 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS006
00237 *---------------------------------------------------------------* INKCS006
00238                                                                   INKCS006
00239      COPY DPWS030.                                                INKCS006
1122RM     COPY DPWS930.                                                INKCS006
00240 /                                                                 INKCS006
00241 *---------------------------------------------------------------* INKCS006
00242 *    STANDARD COMMAREA.                                         * INKCS006
00243 *---------------------------------------------------------------* INKCS006
00244                                                                   INKCS006
00245      COPY DPWS020.                                                INKCS006
00246      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS006
00247                                                                   INKCS006
00248          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS006
W26600     COPY INWS001.                                                INKCS006
00250 *---------------------------------------------------------------* INKCS006
00251 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS006
00252 *---------------------------------------------------------------* INKCS006
00253          10  ASC-SPECIFIC-COMMAREA.                               INKCS006
00254              15  ASC-LIST-KEYS.                                   INKCS006
W26600                 20  ASC-KEY-STORE-NBR               PIC  X(04).  INKCS006
00256                  20  ASC-KEY-AREA-NBR                PIC S9(08)   INKCS006
00257                                                      COMP.        INKCS006
00258                  20  ASC-KEY-LINE-NBR                PIC S9(08)   INKCS006
00259                                                      COMP.        INKCS006
00260              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS006
00261                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS006
00262                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS006
00263                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS006
00264                                                                   INKCS006
00265                  20  ASC-ITEM-ARRAY.                              INKCS006
00266                      25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.        INKCS006
00267                          30  ASC-LIST-ITEM-NUMBER                 INKCS006
00268                                                      PIC S9(09)   INKCS006
00269                                                      COMP-3.      INKCS006
00270                                                                   INKCS006
00271                          30  ASC-LINE-NBR            PIC S9(08)   INKCS006
00272                                                           COMP.   INKCS006
00273                          30  ASC-INV-SKU-UPC         PIC  X(13).  INKCS006
00274                          30  ASC-CORR-SKU-UPC        PIC  X(13).  INKCS006
00275                          30  ASC-INV-QTY             PIC S9(07)   INKCS006
00276                                                           COMP-3. INKCS006
00277                          30  ASC-CORR-QTY            PIC S9(07)   INKCS006
00278                                                           COMP-3. INKCS006
00279                          30  ASC-INV-PR-AMT          PIC S9(07)V99INKCS006
00280                                                           COMP-3. INKCS006
00281                          30  ASC-CORR-PR-AMT         PIC S9(07)V99INKCS006
00282                                                           COMP-3. INKCS006
00283                          30  ASC-INACTIVATED-FL      PIC X(01).   INKCS006
00284                                                                   INKCS006
00285              15  ASC-BLK-SUB                         PIC S9(04)   INKCS006
00286                                                           COMP    INKCS006
00287                                                           SYNC.   INKCS006
00288              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS006
00289                                                           COMP    INKCS006
00290                                                           SYNC.   INKCS006
00291              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS006
00292                                                           COMP-3. INKCS006
00293              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS006
00294                                                           COMP-3. INKCS006
00295              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS006
00296                                                           COMP-3. INKCS006
00297              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS006
00298                                                           COMP-3. INKCS006
00299              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS006
00300                                                           COMP-3. INKCS006
00301              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS006
00302                                                           COMP    INKCS006
00303                                                           SYNC.   INKCS006
00304              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS006
00305                                                           COMP-3. INKCS006
00306              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS006
00307                                                           COMP-3. INKCS006
00308              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS006
00309                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS006
00310                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS006
00311              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS006
00312                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS006
00313                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS006
00314              15  ASC-LIST-QUEUE-NAME.                             INKCS006
00315                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS006
00316                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS006
00317                                                           COMP-3. INKCS006
00318                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS006
00319              15  ASC-SEL-QUEUE-NAME.                              INKCS006
00320                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS006
00321                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS006
00322                                                           COMP-3. INKCS006
00323                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS006
00324                                                                   INKCS006
00325              15  ASC-INT-APPL-FORMAT-IND             PIC  X(01).  INKCS006
00326              15  ASC-USE-SELECTION-QUEUE-IND                      INKCS006
00327                                            PIC  X(01).            INKCS006
00328                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. INKCS006
00329              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.    INKCS006
00330              15  ASC-SEL-SUB               PIC S9(04)  COMP.      INKCS006
00331              15  ASC-SEL-ITEM              PIC S9(04)  COMP.      INKCS006
00332                                                                   INKCS006
00333              15  ASC-CURSOR-SW                       PIC  X(01).  INKCS006
00334                  88  ASC-NO-CURSOR                   VALUE 'Z'.   INKCS006
00335                  88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.   INKCS006
00336                  88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.   INKCS006
00337                  88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.   INKCS006
00338              15  ASC-START-ITEM-REQUEST.                          INKCS006
00339                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS006
00340              15  ASC-HOLD-INTER-APPL                 PIC X(200).  INKCS006
00341 /                                                                 INKCS006
00342 *---------------------------------------------------------------* INKCS006
00343 *    ABEND PROCESSING COPYBOOK.                                 * INKCS006
00344 *---------------------------------------------------------------* INKCS006
00345                                                                   INKCS006
00346      COPY DPWS013.                                                INKCS006
00347                                                                   INKCS006
00348 /                                                                 INKCS006
00349 *---------------------------------------------------------------* INKCS006
00350 *    DB2 AREA FOR TINVDTL                                       * INKCS006
00351 *---------------------------------------------------------------* INKCS006
00352      EXEC SQL                                                     INKCS006
00353           INCLUDE TINVDTL                                         INKCS006
00354      END-EXEC.                                                    INKCS006
00355 *---------------------------------------------------------------* INKCS006
00356 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS006
00357 *---------------------------------------------------------------* INKCS006
00358      EXEC SQL                                                     INKCS006
00359           INCLUDE SQLCA                                           INKCS006
00360      END-EXEC.                                                    INKCS006
00361                                                                   INKCS006
00362 *---------------------------------------------------------------* INKCS006
00363 *    TINVDTL CURSOR DECLARE.                                    * INKCS006
00364 *---------------------------------------------------------------* INKCS006
00365                                                                   INKCS006
00366      EXEC SQL                                                     INKCS006
00367           DECLARE LINE-CSR CURSOR                                 INKCS006
00368           FOR SELECT                                              INKCS006
00369                 REC_UPC_NBR                                       INKCS006
W21732              , REC_SKU_NBR                                       INKCS006
00373               , REC_QTY                                           INKCS006
00374               , REC_PR_AMT                                        INKCS006
00375               , CORR_UPC_NBR                                      INKCS006
W21732              , CORR_SKU_NBR                                      INKCS006
00379               , CORR_QTY                                          INKCS006
00380               , CORR_PR_AMT                                       INKCS006
00381               , INVALID_REC_IND                                   INKCS006
00382               , INVALID_ITM_IND                                   INKCS006
00383               , SCAN_LINE_NBR                                     INKCS006
00384           FROM                                                    INKCS006
00385                 TINVDTL                                           INKCS006
00386           WHERE                                                   INKCS006
W26600                LOC_NBR      =   :INVDTL-LOC-NBR                  INKCS006
00388             AND                                                   INKCS006
W21732                AREA_NBR     =   :INVDTL-AREA-NBR                 INKCS006
00390             AND                                                   INKCS006
W21732                SCAN_LINE_NBR >  :INVDTL-SCAN-LINE-NBR            INKCS006
00392           ORDER BY                                                INKCS006
00393                 SCAN_LINE_NBR                                     INKCS006
00394      END-EXEC.                                                    INKCS006
00395                                                                   INKCS006
00396 /                                                                 INKCS006
00397  LINKAGE SECTION.                                                 INKCS006
00398                                                                   INKCS006
00399  01  DFHCOMMAREA.                                                 INKCS006
00400      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS006
00401                                         DEPENDING ON EIBCALEN.    INKCS006
00402          10  FILLER                     PIC X.                    INKCS006
00403                                                                   INKCS006
00404  PROCEDURE DIVISION.                                              INKCS006
00405 **--------------------------------------------------------------**INKCS006
00406 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS006
00407 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS006
00408 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS006
00409 **--------------------------------------------------------------**INKCS006
00410                                                                   INKCS006
00411  0000-MAIN-MODULE.                                                INKCS006
00412                                                                   INKCS006
00413      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS006
00414      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS006
00415      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS006
00416      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS006
00417      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS006
00418                                                                   INKCS006
00419      MOVE LENGTH OF IN006AI        TO DP030-MAP-LENGTH (1).       INKCS006
00420      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS006
00421      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS006
00422                                                                   INKCS006
00423      PERFORM 1000-CONTROL-PROCESSING                              INKCS006
00424                                                                   INKCS006
00425      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS006
00426      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS006
00427                                                                   INKCS006
00428 *----------------------------------------------------------------*INKCS006
00429 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS006
00430 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS006
00431 *----------------------------------------------------------------*INKCS006
00432                                                                   INKCS006
00433  0001-CALL-CICS-ARCH-API.                                         INKCS006
00434                                                                   INKCS006
00435***   CALL 'DPKCS030' USING DFHEIBLK                               INKCS006
1122RM     CALL DP930-CICS-ARCH-API                                     INKCS002
1122RM                     USING DFHEIBLK                                       
00436                            DFHCOMMAREA                            INKCS006
00437                            DP030-CICS-API-FIELDS                  INKCS006
00438                            DP020-STANDARD-COMMAREA                INKCS006
00439                            IN006AI.                               INKCS006
00440                                                                   INKCS006
00441      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS006
00442          CONTINUE                                                 INKCS006
00443      ELSE                                                         INKCS006
00444          SET DP013-NO-ROLLBACK                                    INKCS006
00445              DP013-XCTL-DISPLAY-RESTART                           INKCS006
00446              DP013-CICS-ABEND      TO TRUE                        INKCS006
00447          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS006
00448                                    TO DP013-PARAGRAPH             INKCS006
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS006
1122RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS006
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00451          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS006
00452          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
00453      END-IF.                                                      INKCS006
00454                                                                   INKCS006
00455 *----------------------------------------------------------------*INKCS006
00456 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS006
00457 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS006
00458 *                                                                *INKCS006
00459 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS006
00460 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS006
00461 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS006
00462 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS006
00463 *----------------------------------------------------------------*INKCS006
00464                                                                   INKCS006
00465  1000-CONTROL-PROCESSING.                                         INKCS006
00466                                                                   INKCS006
00467      EVALUATE TRUE                                                INKCS006
00468          WHEN DP020-NEXT-ACT-INITIAL                              INKCS006
00469               PERFORM 1100-FIND-OBJECT-KEYS                       INKCS006
00470               IF  PS-ERROR                                        INKCS006
00471                   CONTINUE                                        INKCS006
00472               ELSE                                                INKCS006
00473                   MOVE INTER-APPL-COMM-AREA                       INKCS006
00474                                    TO  ASC-HOLD-INTER-APPL        INKCS006
00475                   MOVE EIBTRMID    TO ASC-LIST-TERM-ID            INKCS006
00476                   MOVE DP020-SRC-TASK-NUMBER                      INKCS006
00477                                    TO ASC-LIST-TASK-NUMBER        INKCS006
00478                   MOVE PC-LIST-QUEUE-SEQ                          INKCS006
00479                                    TO ASC-LIST-SEQ                INKCS006
00480                                                                   INKCS006
00481                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS006
00482                   IF  SQLCODE = +100                              INKCS006
00483                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE       INKCS006
00484                       MOVE PC-TSYMSG-00148                        INKCS006
00485                                    TO  DP020-MSG-NUMBER           INKCS006
00486                       SET DP020-MSG-INFORMATIONAL TO TRUE         INKCS006
00487                   END-IF                                          INKCS006
00488               END-IF                                              INKCS006
00489                                                                   INKCS006
00490          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS006
00491               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS006
00492                                    TO PV-TOP-ITEM                 INKCS006
00493               PERFORM 2000-PROCESS-PANEL                          INKCS006
00494                                                                   INKCS006
00495          WHEN DP020-NEXT-ACT-RETURN                               INKCS006
00496               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS006
00497                                    TO PV-TOP-ITEM                 INKCS006
00498               MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS006
00499               MOVE ASC-HOLD-INTER-APPL                            INKCS006
00500                                    TO INTER-APPL-COMM-AREA        INKCS006
00501               PERFORM 6000-REFRESH-PANEL-FROM-TEMP                INKCS006
00502               PERFORM 3000-EDIT-DATA-IN-COMMAREA                  INKCS006
00503               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS006
00504                                                                   INKCS006
00505          WHEN OTHER                                               INKCS006
00506               SET DP013-LOGIC-ABEND TO TRUE                       INKCS006
00507               SET DP013-NO-ROLLBACK TO TRUE                       INKCS006
00508               MOVE '1000-CONTROL-PROCESSING'                      INKCS006
00509                                    TO DP013-PARAGRAPH             INKCS006
00510               MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'   INKCS006
00511                                    TO DP013-MESSAGE-TEXT(1)       INKCS006
00512               PERFORM DP013-0000-PROCESS-ABEND                    INKCS006
00513      END-EVALUATE.                                                INKCS006
00514                                                                   INKCS006
00515 *----------------------------------------------------------------*INKCS006
00516 *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE    *INKCS006
00517 *  INTER-APPLICATION FORMAT CODE.                                *INKCS006
00518 *----------------------------------------------------------------*INKCS006
00519                                                                   INKCS006
00520  1100-FIND-OBJECT-KEYS.                                           INKCS006
00521                                                                   INKCS006
00522      MOVE DP020-INT-APPL-FORMAT-IND                               INKCS006
00523                                    TO  ASC-INT-APPL-FORMAT-IND.   INKCS006
00524                                                                   INKCS006
00525      EVALUATE DP020-INT-APPL-FORMAT-IND                           INKCS006
00526          WHEN PC-INTER-APPL-CRIT-SPEC                             INKCS006
00527               IF (IN001-STORE-NBR NUMERIC                         INKCS006
00528               AND IN001-AREA-NBR NUMERIC)                         INKCS006
00529                   MOVE IN001-STORE-NBR                            INKCS006
00530                                    TO  ASC-KEY-STORE-NBR          INKCS006
00531                   MOVE IN001-AREA-NBR                             INKCS006
00532                                    TO  ASC-KEY-AREA-NBR           INKCS006
00533               ELSE                                                INKCS006
00534                   MOVE PC-TSYMSG-00148                            INKCS006
00535                                    TO  DP020-MSG-NUMBER           INKCS006
00536                   SET DP020-MSG-FATAL                             INKCS006
00537                       DP020-NEXT-ACT-APPL-ERROR                   INKCS006
00538                       PS-ERROR                                    INKCS006
00539                                    TO  TRUE                       INKCS006
00540               END-IF                                              INKCS006
00541                                                                   INKCS006
00542          WHEN PC-INTER-APPL-LIST                                  INKCS006
00543               SET ASC-USE-SELECTION-QUEUE TO TRUE                 INKCS006
00544               MOVE IN001-NUMBER-OF-SELECTED-ITEMS                 INKCS006
00545                                    TO ASC-NUMBER-OF-SELECTED-ITEMSINKCS006
00546               MOVE IN001-NUMBER-OF-TS-ITEMS                       INKCS006
00547                                    TO  ASC-NUMBER-OF-TS-ITEMS     INKCS006
00548               MOVE IN001-SEL-QUEUE-NAME                           INKCS006
00549                                    TO  ASC-SEL-QUEUE-NAME         INKCS006
00550               PERFORM 1120-PREPARE-LIST-ENTRY                     INKCS006
00551                                                                   INKCS006
00552          WHEN OTHER                                               INKCS006
00553               MOVE PC-TSYMSG-00148 TO  DP020-MSG-NUMBER           INKCS006
00554               SET DP020-MSG-FATAL                                 INKCS006
00555                   DP020-NEXT-ACT-APPL-ERROR                       INKCS006
00556                   PS-ERROR                                        INKCS006
00557                                    TO TRUE                        INKCS006
00558      END-EVALUATE.                                                INKCS006
00559                                                                   INKCS006
00560 *----------------------------------------------------------------*INKCS006
00561 * ENTRY IS FROM A LIST.  GET THE MESSAGE NUMBER FROM THE TEMP    *INKCS006
00562 * STORAGE QUEUE PASSED FROM THE LIST.                            *INKCS006
00563 *----------------------------------------------------------------*INKCS006
00564                                                                   INKCS006
00565  1120-PREPARE-LIST-ENTRY.                                         INKCS006
00566                                                                   INKCS006
00567      MOVE IN002-MAX-ITEMS          TO ASC-SEL-SUB.                INKCS006
00568      MOVE ZERO                     TO ASC-SEL-ITEM.               INKCS006
00569      PERFORM 2101-GET-NEXT-AREA.                                  INKCS006
00570                                                                   INKCS006
00571 /                                                                 INKCS006
00572 *----------------------------------------------------------------*INKCS006
00573 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS006
00574 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS006
00575 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS006
00576 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS006
00577 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS006
00578 *----------------------------------------------------------------*INKCS006
00579                                                                   INKCS006
00580  2000-PROCESS-PANEL.                                              INKCS006
00581                                                                   INKCS006
00582      IF  IN001-STORE-NBR NOT NUMERIC                              INKCS006
00583          MOVE ASC-HOLD-INTER-APPL  TO INTER-APPL-COMM-AREA        INKCS006
00584      END-IF.                                                      INKCS006
00585                                                                   INKCS006
00586      EVALUATE TRUE                                                INKCS006
00587          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS006
00588              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS006
00589              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS006
00590              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS006
00591                                                                   INKCS006
00592          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS006
00593              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS006
00594              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS006
00595                                                                   INKCS006
00596          WHEN OTHER                                               INKCS006
00597              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 INKCS006
00598              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS006
00599              IF  PS-NO-ERROR                                      INKCS006
00600                  SET DP030-CONTINUE-GO                            INKCS006
00601                                  TO TRUE                          INKCS006
00602                  PERFORM 2100-CHECK-FUNCTION-KEY                  INKCS006
00603              ELSE                                                 INKCS006
00604                  SET DP030-OVERRIDE-GO                            INKCS006
00605                                  TO TRUE                          INKCS006
00606              END-IF                                               INKCS006
00607      END-EVALUATE.                                                INKCS006
00608 /                                                                 INKCS006
00609 *----------------------------------------------------------------*INKCS006
00610 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS006
00611 *----------------------------------------------------------------*INKCS006
00612                                                                   INKCS006
00613  2050-DELETE-LIST-QUEUE.                                          INKCS006
00614                                                                   INKCS006
00615      EXEC CICS                                                    INKCS006
00616          DELETEQ TS                                               INKCS006
00617              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS006
00618              RESP  (PV-CICS-RESP)                                 INKCS006
00619      END-EXEC.                                                    INKCS006
00620                                                                   INKCS006
00621      IF (PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS006
00622      OR  PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS006
00623          CONTINUE                                                 INKCS006
00624      ELSE                                                         INKCS006
00625          SET DP013-CICS-ABEND      TO TRUE                        INKCS006
00626          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS006
00627                                                                   INKCS006
00628          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS006
00629                                    TO DP013-PARAGRAPH             INKCS006
00630          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS006
00631                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
00632          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
00633      END-IF.                                                      INKCS006
00634                                                                   INKCS006
00635      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS006
00636 /                                                                 INKCS006
00637 *----------------------------------------------------------------*INKCS006
00638 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS006
00639 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS006
00640 * CICS ARCHITECTURE API.                                         *INKCS006
00641 *----------------------------------------------------------------*INKCS006
00642                                                                   INKCS006
00643  2100-CHECK-FUNCTION-KEY.                                         INKCS006
00644                                                                   INKCS006
00645      EVALUATE TRUE                                                INKCS006
00646                                                                   INKCS006
00647          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS006
00648              MOVE +1               TO PV-TOP-ITEM                 INKCS006
00649              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS006
00650                                                                   INKCS006
00651          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS006
00652              PERFORM 2110-BROWSE-BACKWARD                         INKCS006
00653              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS006
00654                                                                   INKCS006
00655          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS006
00656              PERFORM 2120-BROWSE-FORWARD                          INKCS006
00657              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS006
00658                                                                   INKCS006
00659          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             INKCS006
00660               IF  ASC-USE-SELECTION-QUEUE                         INKCS006
00661                   PERFORM 2101-GET-NEXT-AREA                      INKCS006
00662                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS006
00663                   PERFORM 3200-RESET-ATTRIBUTES                   INKCS006
00664               ELSE                                                INKCS006
00665 *                 --- BOTTOM OF LIST ---                          INKCS006
00666                   MOVE PC-TSYMSG-00070                            INKCS006
00667                                    TO DP020-MSG-NUMBER            INKCS006
00668                   SET DP020-MSG-INFORMATIONAL                     INKCS006
00669                                    TO TRUE                        INKCS006
00670               END-IF                                              INKCS006
00671                                                                   INKCS006
00672               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)        INKCS006
00673                    IF  ASC-USE-SELECTION-QUEUE                    INKCS006
00674                        PERFORM 2102-GET-PREV-AREA                 INKCS006
00675                        PERFORM 4000-BUILD-INITIAL-PANEL           INKCS006
00676                        PERFORM 3200-RESET-ATTRIBUTES              INKCS006
00677                    ELSE                                           INKCS006
00678 *                      --- TOP OF LIST ---                        INKCS006
00679                        MOVE PC-TSYMSG-00069                       INKCS006
00680                                         TO DP020-MSG-NUMBER       INKCS006
00681                        SET DP020-MSG-INFORMATIONAL                INKCS006
00682                                         TO TRUE                   INKCS006
00683                    END-IF                                         INKCS006
00684                                                                   INKCS006
00685          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS006
00686              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS006
00687              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS006
00688                                                                   INKCS006
00689          WHEN OTHER                                               INKCS006
00690              SET DP013-NO-ROLLBACK                                INKCS006
00691                  DP013-XCTL-DISPLAY-RESTART                       INKCS006
00692                  DP013-CICS-ABEND  TO TRUE                        INKCS006
00693              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS006
00694                                    TO DP013-PARAGRAPH             INKCS006
00695              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS006
00696                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
00697              PERFORM DP013-0000-PROCESS-ABEND                     INKCS006
00698      END-EVALUATE.                                                INKCS006
00699                                                                   INKCS006
00700 *----------------------------------------------------------------*INKCS006
00701 *    RETRIEVE THE NEXT KEY FOR PROCESSING.                       *INKCS006
00702 *----------------------------------------------------------------*INKCS006
00703                                                                   INKCS006
00704  2101-GET-NEXT-AREA.                                              INKCS006
00705                                                                   INKCS006
00706      ADD +1                        TO  ASC-SEL-SUB.               INKCS006
00707      IF  ASC-SEL-SUB > IN002-MAX-ITEMS                            INKCS006
00708          ADD +1                    TO  ASC-SEL-ITEM               INKCS006
00709          MOVE +1                   TO  ASC-SEL-SUB                INKCS006
00710      END-IF.                                                      INKCS006
00711                                                                   INKCS006
00712      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                INKCS006
00713          PERFORM 9100-READ-SEL-QUEUE                              INKCS006
00714          IF  IN002-ITEM (ASC-SEL-SUB) > ZERO                      INKCS006
00715              MOVE IN002-AREA-NBR (ASC-SEL-SUB)                    INKCS006
00716                                    TO  ASC-KEY-AREA-NBR           INKCS006
00717              SET IN002-INQ (ASC-SEL-SUB)                          INKCS006
00718                                    TO  TRUE                       INKCS006
00719              PERFORM 9200-REWRITE-LIST-QUEUE                      INKCS006
00720          ELSE                                                     INKCS006
00721              SUBTRACT +1 FROM ASC-SEL-SUB                         INKCS006
00722 *            --- BOTTOM OF LIST ---                               INKCS006
00723              MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER           INKCS006
00724              SET DP020-MSG-INFORMATIONAL                          INKCS006
00725                                    TO  TRUE                       INKCS006
00726          END-IF                                                   INKCS006
00727      ELSE                                                         INKCS006
00728          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS006
00729          MOVE IN002-MAX-ITEMS      TO  ASC-SEL-SUB                INKCS006
00730          MOVE PC-TSYMSG-00070      TO  DP020-MSG-NUMBER           INKCS006
00731          SET DP020-MSG-INFORMATIONAL                              INKCS006
00732                                    TO  TRUE                       INKCS006
00733      END-IF.                                                      INKCS006
00734                                                                   INKCS006
00735 *----------------------------------------------------------------*INKCS006
00736 *  GET THE MESSAGE NUMBER (KEY) OF THE PREVIOUS SELECTED         *INKCS006
00737 *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.           *INKCS006
00738 *----------------------------------------------------------------*INKCS006
00739                                                                   INKCS006
00740  2102-GET-PREV-AREA.                                              INKCS006
00741                                                                   INKCS006
00742      SUBTRACT +1 FROM ASC-SEL-SUB.                                INKCS006
00743      IF  ASC-SEL-SUB < +1                                         INKCS006
00744          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS006
00745          MOVE IN002-MAX-ITEMS      TO ASC-SEL-SUB                 INKCS006
00746      END-IF.                                                      INKCS006
00747                                                                   INKCS006
00748      IF  ASC-SEL-ITEM > ZERO                                      INKCS006
00749          PERFORM 9100-READ-SEL-QUEUE                              INKCS006
00750          MOVE IN002-AREA-NBR (ASC-SEL-SUB)                        INKCS006
00751                                    TO ASC-KEY-AREA-NBR            INKCS006
00752      ELSE                                                         INKCS006
00753          MOVE +1                   TO ASC-SEL-ITEM                INKCS006
00754                                       ASC-SEL-SUB                 INKCS006
00755 *        --- TOP OF LIST ---                                      INKCS006
00756          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS006
00757          SET DP020-MSG-INFORMATIONAL                              INKCS006
00758                                    TO TRUE                        INKCS006
00759      END-IF.                                                      INKCS006
00760 /                                                                 INKCS006
00761 *----------------------------------------------------------------*INKCS006
00762 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS006
00763 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS006
00764 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS006
00765 *    MESSAGE.                                                    *INKCS006
00766 *----------------------------------------------------------------*INKCS006
00767                                                                   INKCS006
00768  2110-BROWSE-BACKWARD.                                            INKCS006
00769                                                                   INKCS006
00770      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS006
00771          GIVING PV-TOP-ITEM.                                      INKCS006
00772                                                                   INKCS006
00773      IF  ((PV-TOP-ITEM < +1)                                      INKCS006
00774        OR (PV-TOP-ITEM = +1))                                     INKCS006
00775          MOVE +1                   TO PV-TOP-ITEM                 INKCS006
00776 *        -- TOP OF LIST --                                        INKCS006
00777          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS006
00778          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS006
00779      END-IF.                                                      INKCS006
00780                                                                   INKCS006
00781 /                                                                 INKCS006
00782 *----------------------------------------------------------------*INKCS006
00783 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS006
00784 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS006
00785 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS006
00786 *----------------------------------------------------------------*INKCS006
00787                                                                   INKCS006
00788  2120-BROWSE-FORWARD.                                             INKCS006
00789                                                                   INKCS006
00790      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS006
00791          GIVING PV-TOP-ITEM.                                      INKCS006
00792      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS006
00793          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS006
00794                                    TO PV-TOP-ITEM                 INKCS006
00795 *        -- BOTTOM OF LIST --                                     INKCS006
00796          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS006
00797          SET DP020-MSG-INFORMATIONAL                              INKCS006
00798                                    TO TRUE                        INKCS006
00799      ELSE                                                         INKCS006
00800          COMPUTE PV-BOTTOM-ITEM =                                 INKCS006
00801              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS006
00802          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS006
00803 *            -- BOTTOM OF LIST --                                 INKCS006
00804              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS006
00805              SET DP020-MSG-INFORMATIONAL                          INKCS006
00806                                    TO TRUE                        INKCS006
00807          END-IF                                                   INKCS006
00808      END-IF.                                                      INKCS006
00809 /                                                                 INKCS006
00810 *----------------------------------------------------------------*INKCS006
00811 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS006
00812 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS006
00813 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS006
00814 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS006
00815 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS006
00816 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS006
00817 *----------------------------------------------------------------*INKCS006
00818                                                                   INKCS006
00819  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS006
00820                                                                   INKCS006
00821      SET PS-NO-ERROR               TO TRUE.                       INKCS006
00822      IF (ASC-START-ITEM-REQUEST = SPACE)                          INKCS006
00823          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS006
00824                                    TO PV-TOP-ITEM                 INKCS006
00825      ELSE                                                         INKCS006
00826          MOVE ASC-START-ITEM-REQUEST                              INKCS006
00827                                    TO DP010I-UNEDITED-FIELD       INKCS006
00828          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS006
00829          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS006
00830          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS006
00831                                    TO TRUE                        INKCS006
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00833                                                                   INKCS006
00834          IF  NOT DP010I-ERROR-DETECTED                            INKCS006
00835              MOVE DP010I-NUMERIC-FIELD                            INKCS006
00836                                    TO  ASC-START-ITEM-REQUEST-N   INKCS006
00837              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS006
00838                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS006
00839                                           ASC-NUMBER-OF-ITEMS-READINKCS006
00840                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS006
00841                      COMPUTE PV-TOP-ITEM =                        INKCS006
00842                              ASC-NUMBER-OF-ITEMS-READ             INKCS006
00843                              - PC-ITEMS-PER-PANEL + 1             INKCS006
00844                      IF  PV-TOP-ITEM < +1                         INKCS006
00845                          MOVE +1   TO PV-TOP-ITEM                 INKCS006
00846                          MOVE PC-TSYMSG-00070                     INKCS006
00847                                    TO DP020-MSG-NUMBER            INKCS006
00848                          SET DP020-MSG-INFORMATIONAL              INKCS006
00849                                    TO TRUE                        INKCS006
00850                      END-IF                                       INKCS006
00851                  ELSE                                             INKCS006
00852                      MOVE ASC-START-ITEM-REQUEST-N                INKCS006
00853                                    TO PV-TOP-ITEM                 INKCS006
00854                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS006
00855                      COMPUTE PV-BOTTOM-ITEM =                     INKCS006
00856                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS006
00857                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS006
00858 *                        -- BOTTOM OF LIST --                     INKCS006
00859                          MOVE PC-TSYMSG-00070                     INKCS006
00860                                    TO DP020-MSG-NUMBER            INKCS006
00861                          SET DP020-MSG-INFORMATIONAL              INKCS006
00862                                    TO TRUE                        INKCS006
00863                      END-IF                                       INKCS006
00864                  END-IF                                           INKCS006
00865              ELSE                                                 INKCS006
00866 *                --- VALUE OUT OF RANGE ----                      INKCS006
00867                  MOVE PC-TSYMSG-00101                             INKCS006
00868                                    TO DP020-MSG-NUMBER            INKCS006
00869                  SET DP030-SET-CURSOR-APPL-1                      INKCS006
00870                                    TO TRUE                        INKCS006
00871                  MOVE DP015-REVERSE                               INKCS006
00872                                    TO ASTRTITH                    INKCS006
00873                  MOVE DP015-RED    TO ASTRTITC                    INKCS006
00874                  MOVE DP015-UNP-NUM-BRT-OFF                       INKCS006
00875                                    TO ASTRTITA                    INKCS006
00876                  SET DP020-MSG-FATAL                              INKCS006
00877                                    TO TRUE                        INKCS006
00878                  SET PS-ERROR      TO TRUE                        INKCS006
00879              END-IF                                               INKCS006
00880                                                                   INKCS006
00881          ELSE                                                     INKCS006
00882              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS006
00883              MOVE DP015-REVERSE    TO ASTRTITH                    INKCS006
00884              MOVE DP015-RED        TO ASTRTITC                    INKCS006
00885              MOVE DP015-UNP-NUM-BRT-OFF                           INKCS006
00886                                    TO ASTRTITA                    INKCS006
00887              SET DP020-MSG-FATAL   TO TRUE                        INKCS006
00888              SET PS-ERROR          TO TRUE                        INKCS006
00889          END-IF                                                   INKCS006
00890      END-IF.                                                      INKCS006
00891 *----------------------------------------------------------------*INKCS006
00892 *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *INKCS006
00893 *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *INKCS006
00894 *----------------------------------------------------------------*INKCS006
00895                                                                   INKCS006
00896  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS006
00897                                                                   INKCS006
00898      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS006
00899      MOVE +1                       TO PV-SUB.                     INKCS006
00900                                                                   INKCS006
00901       IF  ASTRTITL > ZERO                                         INKCS006
00902           MOVE ASTRTITI            TO ASC-START-ITEM-REQUEST      INKCS006
00903       ELSE                                                        INKCS006
00904           IF  ASTRTITF EQUAL DP015-ERASE-EOF                      INKCS006
00905               INITIALIZE ASC-START-ITEM-REQUEST                   INKCS006
00906           END-IF                                                  INKCS006
00907       END-IF.                                                     INKCS006
00908                                                                   INKCS006
00909      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS006
00910                                                                   INKCS006
00911 *----------------------------------------------------------------*INKCS006
00912 *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *INKCS006
00913 *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *INKCS006
00914 *----------------------------------------------------------------*INKCS006
00915                                                                   INKCS006
00916  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS006
00917                                                                   INKCS006
00918      SET PS-NO-ERROR               TO TRUE.                       INKCS006
00919      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS006
00920      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS006
00921 **                                                                INKCS006
00922 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS006
00923 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS006
00924 **                                                                INKCS006
00925      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS006
00926          ASC-ITEMS-DISPLAYED - 1.                                 INKCS006
00927      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS006
00928 /                                                                 INKCS006
00929 *----------------------------------------------------------------*INKCS006
00930 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS006
00931 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS006
00932 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS006
00933 *                                                                *INKCS006
00934 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS006
00935 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS006
00936 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS006
00937 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS006
00938 *----------------------------------------------------------------*INKCS006
00939                                                                   INKCS006
00940  3100-SET-SUBSCRIPTS.                                             INKCS006
00941                                                                   INKCS006
00942      COMPUTE ASC-ITEM-SUB =                                       INKCS006
00943          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS006
00944                                                                   INKCS006
00945      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS006
00946          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS006
00947                                                                   INKCS006
00948          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS006
00949      ELSE                                                         INKCS006
00950          MOVE +1                     TO ASC-BLK-SUB               INKCS006
00951      END-IF.                                                      INKCS006
00952                                                                   INKCS006
00953 *----------------------------------------------------------------*INKCS006
00954 *  RESET THE ATTRIBUTES OF ALL FIELDS ON THE MAP.                 INKCS006
00955 *----------------------------------------------------------------*INKCS006
00956                                                                   INKCS006
00957  3200-RESET-ATTRIBUTES.                                           INKCS006
00958                                                                   INKCS006
00959      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS006
00960      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS006
00961      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS006
00962 /                                                                 INKCS006
00963 *----------------------------------------------------------------*INKCS006
00964 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS006
00965 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS006
00966 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS006
00967 *    DISPLAY THE PANEL TO USER.                                  *INKCS006
00968 *----------------------------------------------------------------*INKCS006
00969                                                                   INKCS006
00970  4000-BUILD-INITIAL-PANEL.                                        INKCS006
00971                                                                   INKCS006
00972      PERFORM 5120-DELETE-ASC-LISTQ.                               INKCS006
00973      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS006
00974                                  ASC-BLOCK-ENTRIES (2).           INKCS006
00975      MOVE +1                 TO ASC-BLK-SUB.                      INKCS006
00976      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS006
00977                              TO TRUE.                             INKCS006
00978                                                                   INKCS006
00979      MOVE +0                 TO ASC-ITEM-SUB                      INKCS006
00980                                 ASC-NUMBER-OF-ITEMS-READ          INKCS006
00981                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS006
00982                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS006
00983                                 ASC-START-OF-SELECTED-RANGE       INKCS006
00984                                 ASC-END-OF-SELECTED-RANGE         INKCS006
00985                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS006
00986      PERFORM 4100-PREPARE-CURSOR.                                 INKCS006
00987                                                                   INKCS006
00988      PERFORM 4010-OPEN-CURSOR.                                    INKCS006
00989                                                                   INKCS006
00990      PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS006
00991                                                                   INKCS006
00992      PERFORM 4020-CLOSE-CURSOR.                                   INKCS006
00993                                                                   INKCS006
00994      MOVE +1                   TO  PV-TOP-ITEM.                   INKCS006
00995      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS006
00996          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    INKCS006
00997          SET DP020-MSG-FATAL   TO  TRUE                           INKCS006
00998          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS006
00999      ELSE                                                         INKCS006
01000          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS006
01001      END-IF.                                                      INKCS006
01002 /                                                                 INKCS006
01003 *----------------------------------------------------------------*INKCS006
01004 * OPEN THE CURSOR.                                               *INKCS006
01005 *----------------------------------------------------------------*INKCS006
01006                                                                   INKCS006
01007  4010-OPEN-CURSOR.                                                INKCS006
01008                                                                   INKCS006
01009      PERFORM                                                      INKCS006
01010          WITH TEST AFTER                                          INKCS006
01011          VARYING PV-RETRY-COUNTER                                 INKCS006
01012          FROM 1 BY 1                                              INKCS006
01013          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               INKCS006
01014                    OR (SQLCODE = ZERO))                           INKCS006
01015                                                                   INKCS006
W26600         MOVE ASC-KEY-STORE-NBR      TO PV-LOC-NBR                INKCS006
W26600         MOVE PV-LOC-NBR-NUM         TO INVDTL-LOC-NBR            INKCS006
W21732         MOVE ASC-KEY-AREA-NBR       TO INVDTL-AREA-NBR           INKCS006
W21732         MOVE ASC-KEY-LINE-NBR       TO INVDTL-SCAN-LINE-NBR      INKCS006
W21732                                                                          
01016          EXEC SQL                                                 INKCS006
01017               OPEN LINE-CSR                                       INKCS006
01018          END-EXEC                                                 INKCS006
01019                                                                   INKCS006
01020          EVALUATE TRUE                                            INKCS006
01021              WHEN SQLCODE = ZERO                                  INKCS006
01022              WHEN SQLCODE = -904                                  INKCS006
01023              WHEN SQLCODE = -913                                  INKCS006
01024                  CONTINUE                                         INKCS006
01025              WHEN SQLWARN0 NOT = SPACES                           INKCS006
01026              WHEN SQLCODE < ZERO                                  INKCS006
01027              WHEN SQLCODE > ZERO                                  INKCS006
01028                  MOVE '4010-OPEN-CURSOR'                          INKCS006
01029                                    TO DP013-PARAGRAPH             INKCS006
01030                  MOVE 'OPEN LINE CURSOR - '                       INKCS006
01031                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
01032                  MOVE SQLCA        TO DP013-SQLCA                 INKCS006
01033                  MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)    INKCS006
01034                  SET DP013-DB2-ABEND                              INKCS006
01035                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS006
01036                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS006
01037          END-EVALUATE                                             INKCS006
01038      END-PERFORM.                                                 INKCS006
01039                                                                   INKCS006
01040      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        INKCS006
01041          MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH             INKCS006
01042          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'   INKCS006
01043                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
01044          MOVE SQLCA                TO DP013-SQLCA                 INKCS006
01045          MOVE 'TSYMSG  '           TO DP013-DB2-TABLE-NAME (1)    INKCS006
01046          SET DP013-DB2-ABEND                                      INKCS006
01047              DP013-XCTL-DISPLAY-RESTART                           INKCS006
01048                                    TO TRUE                        INKCS006
01049          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01050      END-IF.                                                      INKCS006
01051                                                                   INKCS006
01052 /                                                                 INKCS006
01053 *----------------------------------------------------------------*INKCS006
01054 *                                                                *INKCS006
01055 * CLOSE THE LINE CURSOR.                                         *INKCS006
01056 *                                                                *INKCS006
01057 *----------------------------------------------------------------*INKCS006
01058                                                                   INKCS006
01059  4020-CLOSE-CURSOR.                                               INKCS006
01060                                                                   INKCS006
01061      EXEC SQL                                                     INKCS006
01062           CLOSE LINE-CSR                                          INKCS006
01063      END-EXEC.                                                    INKCS006
01064                                                                   INKCS006
01065      EVALUATE TRUE                                                INKCS006
01066          WHEN SQLCODE = ZERO                                      INKCS006
01067              CONTINUE                                             INKCS006
01068          WHEN SQLWARN0 NOT = SPACES                               INKCS006
01069          WHEN SQLCODE < ZERO                                      INKCS006
01070          WHEN SQLCODE > ZERO                                      INKCS006
01071              MOVE '4020-CLOSE-CURSOR'                             INKCS006
01072                                TO DP013-PARAGRAPH                 INKCS006
01073              MOVE 'CLOSE LINE CURSOR '                            INKCS006
01074                                TO DP013-MESSAGE-TEXT (1)          INKCS006
01075              MOVE SQLCA        TO DP013-SQLCA                     INKCS006
01076              MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS006
01077              SET DP013-DB2-ABEND                                  INKCS006
01078                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS006
01079              PERFORM DP013-0000-PROCESS-ABEND                     INKCS006
01080      END-EVALUATE.                                                INKCS006
01081 /                                                                 INKCS006
01082 *----------------------------------------------------------------*INKCS006
01083 *    SET UP THE HOST VARIABLES.                                  *INKCS006
01084 *----------------------------------------------------------------*INKCS006
01085                                                                   INKCS006
01086  4100-PREPARE-CURSOR.                                             INKCS006
01087                                                                   INKCS006
01088      MOVE IN001-STORE-NBR      TO  ASC-KEY-STORE-NBR.             INKCS006
01089      IF  ASC-USE-SELECTION-QUEUE                                  INKCS006
01090          CONTINUE                                                 INKCS006
01091      ELSE                                                         INKCS006
01092          MOVE IN001-AREA-NBR   TO  ASC-KEY-AREA-NBR               INKCS006
01093      END-IF.                                                      INKCS006
01094      MOVE ZERO                 TO  ASC-KEY-LINE-NBR.              INKCS006
01095                                                                   INKCS006
01096 /                                                                 INKCS006
01097 *----------------------------------------------------------------*INKCS006
01098 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS006
01099 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS006
01100 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS006
01101 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS006
01102 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS006
01103 *                                                                *INKCS006
01104 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS006
01105 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS006
01106 *    UPON INITIALIZATION.                                        *INKCS006
01107 *----------------------------------------------------------------*INKCS006
01108                                                                   INKCS006
01109  4300-READ-ITEMS-FROM-DB.                                         INKCS006
01110                                                                   INKCS006
01111      PERFORM                                                      INKCS006
01112          VARYING PV-READ-COUNT                                    INKCS006
01113          FROM 1 BY 1                                              INKCS006
01114          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS006
01115             OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS006
01116                                                                   INKCS006
01117          PERFORM 4310-FETCH                                       INKCS006
01118                                                                   INKCS006
01119          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS006
01120              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                INKCS006
01121                  ADD +1        TO ASC-ITEM-SUB                    INKCS006
01122              ELSE                                                 INKCS006
01123                  ADD +1        TO ASC-BLK-SUB                     INKCS006
01124                  MOVE +1       TO ASC-ITEM-SUB                    INKCS006
01125                                                                   INKCS006
01126                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         INKCS006
01127                      MOVE +1   TO ASC-BLK-SUB                     INKCS006
01128                      SET ASC-BLOCK-MODIFIED (1)                   INKCS006
01129                                TO TRUE                            INKCS006
01130                      PERFORM 5100-WRITE-TEMP-STORAGE              INKCS006
01131                      MOVE ASC-BLOCK-ENTRIES                       INKCS006
01132                                (PC-MAX-BLOCKS-IN-ARRAY)           INKCS006
01133                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)  INKCS006
01134                      ADD +1    TO ASC-BLK-SUB                     INKCS006
01135                      INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    INKCS006
01136                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      INKCS006
01137                                TO TRUE                            INKCS006
01138                  END-IF                                           INKCS006
01139              END-IF                                               INKCS006
01140                                                                   INKCS006
01141              ADD +1            TO ASC-NUMBER-OF-ITEMS-READ        INKCS006
01142              MOVE ASC-NUMBER-OF-ITEMS-READ                        INKCS006
01143                                    TO ASC-LIST-ITEM-NUMBER        INKCS006
01144                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS006
01145              MOVE INVDTL-SCAN-LINE-NBR                            INKCS006
01146                                    TO  ASC-LINE-NBR               INKCS006
01147                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
W21732             IF  INVDTL-REC-SKU-NBR  GREATER THAN PC-000          INKCS006
W21732                 MOVE INVDTL-REC-SKU-NBR                          INKCS006
W21732                                   TO  PV-WS-SKU                  INKCS006
W21732                 MOVE PV-WORK-SKU  TO  ASC-INV-SKU-UPC            INKCS006
W21732                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01157              ELSE                                                 INKCS006
01158                  MOVE INVDTL-REC-UPC-NBR                          INKCS006
01159                                    TO  PV-WORK-UPC                INKCS006
01160                  MOVE PV-WORK-UPC  TO  ASC-INV-SKU-UPC            INKCS006
01161                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01162              END-IF                                               INKCS006
01163                                                                   INKCS006
W21732             IF  INVDTL-CORR-SKU-NBR  GREATER THAN PC-000         INKCS006
W21732                 MOVE INVDTL-CORR-SKU-NBR                         INKCS006
W21732                                   TO  PV-WS-SKU                  INKCS006
01171                  MOVE PV-WORK-SKU  TO  ASC-CORR-SKU-UPC           INKCS006
01172                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01173              ELSE                                                 INKCS006
01174                  MOVE INVDTL-CORR-UPC-NBR                         INKCS006
01175                                    TO  PV-WORK-UPC                INKCS006
01176                  MOVE PV-WORK-UPC  TO  ASC-CORR-SKU-UPC           INKCS006
01177                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01178              END-IF                                               INKCS006
01179                                                                   INKCS006
01180              MOVE INVDTL-REC-QTY   TO  ASC-INV-QTY                INKCS006
01181                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01182              MOVE INVDTL-REC-PR-AMT TO ASC-INV-PR-AMT             INKCS006
01183                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01184              MOVE INVDTL-CORR-QTY  TO  ASC-CORR-QTY               INKCS006
01185                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01186              MOVE INVDTL-CORR-PR-AMT TO ASC-CORR-PR-AMT           INKCS006
01187                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS006
01188 *  KSW - DETERMINE IF THE UPC/SKU IS INACTIVATED INVALID AND      INKCS006
01189 *        SET AN INACTIVATED FLAG TO YES.                          INKCS006
01190             IF (INVDTL-CORR-UPC-NBR   = ZEROES AND                INKCS006
W21732                INVDTL-CORR-SKU-NBR   = ZEROES AND                INKCS006
01194                 INVDTL-CORR-QTY       = ZEROES AND                INKCS006
01195                 INVDTL-CORR-PR-AMT    = ZEROES AND                INKCS006
01196                 INVDTL-INVALID-REC-IND = 'N'   AND                INKCS006
01197                 INVDTL-INVALID-ITM-IND = 'N')                     INKCS006
01198                    MOVE PC-Y TO  ASC-INACTIVATED-FL               INKCS006
01199                                  (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS006
01200             ELSE                                                  INKCS006
01201                    MOVE PC-N TO  ASC-INACTIVATED-FL               INKCS006
01202                                  (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS006
01203             END-IF                                                INKCS006
01204          END-IF                                                   INKCS006
01205      END-PERFORM.                                                 INKCS006
01206                                                                   INKCS006
01207      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS006
01208          MOVE +1               TO ASC-BLK-SUB                     INKCS006
01209          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS006
01210          ADD +1                TO ASC-BLK-SUB                     INKCS006
01211          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS006
01212                                                                   INKCS006
01213          MOVE 1                TO ASC-BLK-SUB                     INKCS006
01214                                   PV-BLOCK-NUMBER                 INKCS006
01215          PERFORM 4410-READ-TEMP-STORAGE                           INKCS006
01216          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS006
01217              MOVE 2            TO ASC-BLK-SUB                     INKCS006
01218                                   PV-BLOCK-NUMBER                 INKCS006
01219              PERFORM 4410-READ-TEMP-STORAGE                       INKCS006
01220          END-IF                                                   INKCS006
01221      END-IF.                                                      INKCS006
01222                                                                   INKCS006
01223      MOVE INVDTL-SCAN-LINE-NBR TO  ASC-KEY-LINE-NBR.              INKCS006
01224 /                                                                 INKCS006
01225 *----------------------------------------------------------------*INKCS006
01226 *    FETCH THE CURSOR.                                           *INKCS006
01227 *----------------------------------------------------------------*INKCS006
01228                                                                   INKCS006
01229  4310-FETCH.                                                      INKCS006
01230                                                                   INKCS006
01230                                                                   INKCS006
01231      EXEC SQL                                                     INKCS006
01232           FETCH                                                   INKCS006
01233                 LINE-CSR                                          INKCS006
01234           INTO                                                    INKCS006
01235                :INVDTL-REC-UPC-NBR                                INKCS006
W21732             , :INVDTL-REC-SKU-NBR                                INKCS006
01239              , :INVDTL-REC-QTY                                    INKCS006
01240              , :INVDTL-REC-PR-AMT                                 INKCS006
01241              , :INVDTL-CORR-UPC-NBR                               INKCS006
W21732             , :INVDTL-CORR-SKU-NBR                               INKCS006
01245              , :INVDTL-CORR-QTY                                   INKCS006
01246              , :INVDTL-CORR-PR-AMT                                INKCS006
01247              , :INVDTL-INVALID-REC-IND                            INKCS006
01248              , :INVDTL-INVALID-ITM-IND                            INKCS006
01249              , :INVDTL-SCAN-LINE-NBR                              INKCS006
01250      END-EXEC.                                                    INKCS006
01251                                                                   INKCS006
01252      EVALUATE TRUE                                                INKCS006
01253          WHEN SQLCODE = ZERO                                      INKCS006
01254          WHEN SQLCODE = +100                                      INKCS006
01255               CONTINUE                                            INKCS006
01256          WHEN SQLWARN0 NOT = SPACE                                INKCS006
01257          WHEN SQLCODE NOT = ZERO                                  INKCS006
01258               MOVE '4310-FETCH-CURSOR'                            INKCS006
01259                                TO DP013-PARAGRAPH                 INKCS006
01260               MOVE 'FETCH LINE CURSOR'                            INKCS006
01261                                TO DP013-MESSAGE-TEXT (1)          INKCS006
01262               MOVE SQLCA       TO DP013-SQLCA                     INKCS006
01263               SET DP013-DB2-ABEND                                 INKCS006
01264                                TO TRUE                            INKCS006
01265               PERFORM DP013-0000-PROCESS-ABEND                    INKCS006
01266      END-EVALUATE.                                                INKCS006
01267                                                                   INKCS006
01268      IF  SQLCODE EQUAL +100                                       INKCS006
01269          SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS006
01270                                    TO TRUE                        INKCS006
01271      END-IF.                                                      INKCS006
01272 /                                                                 INKCS006
01273 *----------------------------------------------------------------*INKCS006
01274 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS006
01275 *----------------------------------------------------------------*INKCS006
01276                                                                   INKCS006
01277  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS006
01278                                                                   INKCS006
W26600     MOVE IN001-STORE-NBR          TO  ASTRNBRO.                  INKCS006
01279      MOVE IN001-STORE-NAME         TO  ASTRNAMO.                  INKCS006
01280      MOVE ASC-KEY-AREA-NBR         TO  AAREAO.                    INKCS006
01281      MOVE -1                       TO  ASTRTITL.                  INKCS006
01282      SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                      INKCS006
01283                                                                   INKCS006
01284      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS006
01285          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS006
01286          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS006
01287              MOVE +1    TO ASC-BLK-SUB                            INKCS006
01288              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS006
01289              ADD +1     TO ASC-BLK-SUB                            INKCS006
01290              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS006
01291              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS006
01292                  GIVING PV-BLOCK-NUMBER                           INKCS006
01293                  REMAINDER PV-REMAINDER                           INKCS006
01294              IF  PV-REMAINDER > ZERO                              INKCS006
01295                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS006
01296              END-IF                                               INKCS006
01297              MOVE +1    TO ASC-BLK-SUB                            INKCS006
01298              PERFORM 4410-READ-TEMP-STORAGE                       INKCS006
01299              ADD +1     TO ASC-BLK-SUB                            INKCS006
01300                            PV-BLOCK-NUMBER                        INKCS006
01301              PERFORM 4410-READ-TEMP-STORAGE                       INKCS006
01302      END-IF.                                                      INKCS006
01303                                                                   INKCS006
01304      MOVE +1            TO ASC-BLK-SUB.                           INKCS006
01305      COMPUTE ASC-ITEM-SUB =                                       INKCS006
01306          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS006
01307          + PC-ITEMS-PER-PANEL).                                   INKCS006
01308                                                                   INKCS006
01309      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS006
01310          ADD +1         TO ASC-BLK-SUB                            INKCS006
01311          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS006
01312      END-IF.                                                      INKCS006
01313                                                                   INKCS006
01314      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS006
01315          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS006
01316      PERFORM 4420-FORMAT-LIST-LINES                               INKCS006
01317          VARYING PV-SUB                                           INKCS006
01318          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS006
01319          UNTIL PV-SUB < +1.                                       INKCS006
01320                                                                   INKCS006
01321      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS006
01322          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS006
01323      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.   INKCS006
01324      IF  PS-NO-ERROR                                              INKCS006
01325          MOVE PV-TOP-ITEM          TO ASTRTITO                    INKCS006
01326      END-IF.                                                      INKCS006
01327      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS006
01328      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS006
01329                                                                   INKCS006
01330      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS006
01331          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS006
01332 *            --- MORE ITEMS TO DISPLAY ----                       INKCS006
01333              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS006
01334              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS006
01335          END-IF                                                   INKCS006
01336      END-IF.                                                      INKCS006
01337                                                                   INKCS006
01338 *----------------------------------------------------------------*INKCS006
01339 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS006
01340 *----------------------------------------------------------------*INKCS006
01341                                                                   INKCS006
01342  4410-READ-TEMP-STORAGE.                                          INKCS006
01343                                                                   INKCS006
01344      EXEC CICS                                                    INKCS006
01345          READQ TS                                                 INKCS006
01346              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS006
01347              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS006
01348              ITEM  (PV-BLOCK-NUMBER)                              INKCS006
01349              RESP  (PV-CICS-RESP)                                 INKCS006
01350      END-EXEC.                                                    INKCS006
01351                                                                   INKCS006
01352      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS006
01353        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS006
01354          CONTINUE                                                 INKCS006
01355      ELSE                                                         INKCS006
01356          SET DP013-CICS-ABEND     TO TRUE                         INKCS006
01357          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS006
01358          MOVE '4410-READ-TEMP-STORAGE'                            INKCS006
01359                                   TO DP013-PARAGRAPH              INKCS006
01360          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS006
01361                                   TO DP013-MESSAGE-TEXT(1)        INKCS006
01362          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01363      END-IF.                                                      INKCS006
01364                                                                   INKCS006
01365      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS006
01366                                                                   INKCS006
01367      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS006
01368          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS006
01369      END-IF.                                                      INKCS006
01370 /                                                                 INKCS006
01371 *----------------------------------------------------------------*INKCS006
01372 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS006
01373 *----------------------------------------------------------------*INKCS006
01374                                                                   INKCS006
01375  4420-FORMAT-LIST-LINES.                                          INKCS006
01376                                                                   INKCS006
01377      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS006
01378          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS006
01379          MOVE SPACE              TO MR-LINE-X       (PV-SUB)      INKCS006
01380                                     MR-INV-SKU-UPC  (PV-SUB)      INKCS006
01381                                     MR-CORR-SKU-UPC (PV-SUB)      INKCS006
01382                                     MR-INV-QTY-X    (PV-SUB)      INKCS006
01383                                     MR-CORR-QTY-X   (PV-SUB)      INKCS006
01384                                     MR-INV-PR-AMT-X (PV-SUB)      INKCS006
01385                                     MR-CORR-PR-AMT-X(PV-SUB)      INKCS006
01386      ELSE                                                         INKCS006
01387                                                                   INKCS006
01388          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS006
01389              MOVE ASC-LINE-NBR      (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01390                                  TO MR-LINE          (PV-SUB)     INKCS006
01391              MOVE ASC-INV-SKU-UPC   (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01392                                  TO MR-INV-SKU-UPC   (PV-SUB)     INKCS006
01393              MOVE ASC-CORR-SKU-UPC  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01394                                  TO MR-CORR-SKU-UPC  (PV-SUB)     INKCS006
01395              MOVE ASC-INV-QTY       (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01396                                  TO MR-INV-QTY       (PV-SUB)     INKCS006
01397              MOVE ASC-CORR-QTY      (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01398                                  TO MR-CORR-QTY      (PV-SUB)     INKCS006
01399              MOVE ASC-INV-PR-AMT    (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01400                                  TO MR-INV-PR-AMT    (PV-SUB)     INKCS006
01401              MOVE ASC-CORR-PR-AMT   (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS006
01402                                  TO MR-CORR-PR-AMT   (PV-SUB)     INKCS006
01403 * KSW - IF INACTIVATED INVALID UPC/SKU, HIGHLIGHT IN TURQUOISE    INKCS006
01404              IF ASC-INACTIVATED-FL                                INKCS006
01405                            (ASC-BLK-SUB ASC-ITEM-SUB)  =  PC-Y    INKCS006
01406                 MOVE DP015-TURQUOISE TO MR-LINEC         (PV-SUB) INKCS006
01407                                         MR-INV-SKU-UPCC  (PV-SUB) INKCS006
01408                                         MR-CORR-SKU-UPCC (PV-SUB) INKCS006
01409                                         MR-INV-QTYC      (PV-SUB) INKCS006
01410                                         MR-CORR-QTYC     (PV-SUB) INKCS006
01411                                         MR-INV-PR-AMTC   (PV-SUB) INKCS006
01412                                         MR-CORR-PR-AMTC  (PV-SUB) INKCS006
01413              ELSE                                                 INKCS006
01414                 MOVE DP015-BLUE      TO MR-LINEC         (PV-SUB) INKCS006
01415                                         MR-INV-SKU-UPCC  (PV-SUB) INKCS006
01416                                         MR-CORR-SKU-UPCC (PV-SUB) INKCS006
01417                                         MR-INV-QTYC      (PV-SUB) INKCS006
01418                                         MR-CORR-QTYC     (PV-SUB) INKCS006
01419                                         MR-INV-PR-AMTC   (PV-SUB) INKCS006
01420                                         MR-CORR-PR-AMTC  (PV-SUB) INKCS006
01421              END-IF                                               INKCS006
01422          END-IF                                                   INKCS006
01423      END-IF.                                                      INKCS006
01424                                                                   INKCS006
01425      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS006
01426      IF  ASC-ITEM-SUB < + 1                                       INKCS006
01427          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS006
01428          MOVE +1                 TO ASC-BLK-SUB                   INKCS006
01429      END-IF.                                                      INKCS006
01430 /                                                                 INKCS006
01431 *----------------------------------------------------------------*INKCS006
01432 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS006
01433 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS006
01434 *----------------------------------------------------------------*INKCS006
01435                                                                   INKCS006
01436  5100-WRITE-TEMP-STORAGE.                                         INKCS006
01437                                                                   INKCS006
01438      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS006
01439                                    TO PV-LIST-ITEM-NUMBER.        INKCS006
01440      COMPUTE PV-BLOCK-NUMBER =                                    INKCS006
01441          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS006
01442                                                                   INKCS006
01443      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS006
01444      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS006
01445          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS006
01446              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS006
01447          END-IF                                                   INKCS006
01448      ELSE                                                         INKCS006
01449          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS006
01450          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS006
01451          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS006
01452          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS006
01453      END-IF.                                                      INKCS006
01454                                                                   INKCS006
01455      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS006
01456                                                                   INKCS006
01457 ******************************************************************INKCS006
01458 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS006
01459 ** SPECIFIC COMMAREA.                                           **INKCS006
01460 ******************************************************************INKCS006
01461                                                                   INKCS006
01462  5105-REWRITE-ASC-LISTQ.                                          INKCS006
01463                                                                   INKCS006
01464      EXEC CICS                                                    INKCS006
01465          WRITEQ TS                                                INKCS006
01466              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS006
01467              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS006
01468              ITEM  (PV-BLOCK-NUMBER)                              INKCS006
01469              REWRITE                                              INKCS006
01470              RESP  (PV-CICS-RESP)                                 INKCS006
01471      END-EXEC.                                                    INKCS006
01472                                                                   INKCS006
01473      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS006
01474          CONTINUE                                                 INKCS006
01475      ELSE                                                         INKCS006
01476          SET DP013-CICS-ABEND            TO TRUE                  INKCS006
01477          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS006
01478          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS006
01479          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS006
01480                                          TO DP013-MESSAGE-TEXT(1) INKCS006
01481          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01482      END-IF.                                                      INKCS006
01483                                                                   INKCS006
01484 ******************************************************************INKCS006
01485 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS006
01486 ** SPECIFIC COMMAREA.                                           **INKCS006
01487 ******************************************************************INKCS006
01488                                                                   INKCS006
01489  5110-WRITE-ASC-LISTQ.                                            INKCS006
01490                                                                   INKCS006
01491      EXEC CICS                                                    INKCS006
01492          WRITEQ TS                                                INKCS006
01493              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS006
01494              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS006
01495              ITEM  (PV-BLOCK-NUMBER)                              INKCS006
01496              RESP  (PV-CICS-RESP)                                 INKCS006
01497      END-EXEC.                                                    INKCS006
01498      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS006
01499          SET DP013-CICS-ABEND      TO TRUE                        INKCS006
01500          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS006
01501          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS006
01502                                    TO DP013-PARAGRAPH             INKCS006
01503          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS006
01504                                    TO DP013-MESSAGE-TEXT(1)       INKCS006
01505          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01506      END-IF.                                                      INKCS006
01507                                                                   INKCS006
01508 ******************************************************************INKCS006
01509 ** DELETE THE LIST TEMP STORAGE QUEUE.                          **INKCS006
01510 ******************************************************************INKCS006
01511                                                                   INKCS006
01512  5120-DELETE-ASC-LISTQ.                                           INKCS006
01513                                                                   INKCS006
01514      EXEC CICS                                                    INKCS006
01515           DELETEQ TS                                              INKCS006
01516              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS006
01517              RESP  (PV-CICS-RESP)                                 INKCS006
01518      END-EXEC.                                                    INKCS006
01519                                                                   INKCS006
01520      IF (PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS006
01521      OR  PV-CICS-RESP = DFHRESP (QIDERR))                         INKCS006
01522          CONTINUE                                                 INKCS006
01523      ELSE                                                         INKCS006
01524          SET DP013-CICS-ABEND      TO TRUE                        INKCS006
01525          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS006
01526          MOVE '5120-DELETE-ASC-LISTQ'                             INKCS006
01527                                    TO DP013-PARAGRAPH             INKCS006
01528          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS006
01529                                    TO DP013-MESSAGE-TEXT(1)       INKCS006
01530          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01531      END-IF.                                                      INKCS006
01532 /                                                                 INKCS006
01533 *----------------------------------------------------------------*INKCS006
01534 *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *INKCS006
01535 *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *INKCS006
01536 *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *INKCS006
01537 *  THE USER.                                                     *INKCS006
01538 *----------------------------------------------------------------*INKCS006
01539                                                                   INKCS006
01540  6000-REFRESH-PANEL-FROM-TEMP.                                    INKCS006
01541                                                                   INKCS006
01542      IF  ASC-UPDATE-TSQ                                           INKCS006
01543          SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS006
01544          MOVE ZERO                 TO PV-SAVE-ITEM                INKCS006
01545                                       PV-SEL-QUEUE-ITEM           INKCS006
01546          INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS006
01547                     ASC-BLOCK-ENTRIES (2)                         INKCS006
01548      END-IF.                                                      INKCS006
01549 /                                                                 INKCS006
01550 *----------------------------------------------------------------*INKCS006
01551 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS006
01552 *----------------------------------------------------------------*INKCS006
01553                                                                   INKCS006
01554  9100-READ-SEL-QUEUE.                                             INKCS006
01555                                                                   INKCS006
01556      EXEC CICS                                                    INKCS006
01557          READQ TS                                                 INKCS006
01558              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS006
01559              INTO  (IN002-SELECTED-AREAS)                         INKCS006
01560              ITEM  (ASC-SEL-ITEM)                                 INKCS006
01561              RESP  (PV-CICS-RESP)                                 INKCS006
01562      END-EXEC.                                                    INKCS006
01563                                                                   INKCS006
01564      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS006
01565          CONTINUE                                                 INKCS006
01566      ELSE                                                         INKCS006
01567          SET DP013-LOGIC-ABEND                                    INKCS006
01568              DP013-NO-ROLLBACK     TO TRUE                        INKCS006
01569          MOVE '9100-READ-SEL-QUEUE'                               INKCS006
01570                                    TO DP013-PARAGRAPH             INKCS006
01571          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   INKCS006
01572                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
01573          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01574      END-IF.                                                      INKCS006
01575                                                                   INKCS006
01576 *----------------------------------------------------------------*INKCS006
01577 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS006
01578 *----------------------------------------------------------------*INKCS006
01579                                                                   INKCS006
01580  9200-REWRITE-LIST-QUEUE.                                         INKCS006
01581                                                                   INKCS006
01582      EXEC CICS                                                    INKCS006
01583           WRITEQ TS                                               INKCS006
01584               QUEUE  (ASC-SEL-QUEUE-NAME)                         INKCS006
01585               FROM   (IN002-SELECTED-AREAS)                       INKCS006
01586               ITEM   (ASC-SEL-ITEM)                               INKCS006
01587               RESP   (PV-CICS-RESP)                               INKCS006
01588               REWRITE                                             INKCS006
01589      END-EXEC.                                                    INKCS006
01590                                                                   INKCS006
01591      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                   INKCS006
01592          SET DP013-CICS-ABEND                                     INKCS006
01593              DP013-NO-ROLLBACK     TO TRUE                        INKCS006
01594          MOVE '9200-REWRITE-LIST-QUEUE'                           INKCS006
01595                                    TO DP013-PARAGRAPH             INKCS006
01596          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   INKCS006
01597                                    TO DP013-MESSAGE-TEXT (1)      INKCS006
01598          PERFORM DP013-0000-PROCESS-ABEND                         INKCS006
01599      END-IF.                                                      INKCS006
01600 /                                                                 INKCS006
01601 *----------------------------------------------------------------*INKCS006
01602 *    ABEND PROCESSOR MODULE                                      *INKCS006
01603 *----------------------------------------------------------------*INKCS006
01604                                                                   INKCS006
01605      COPY DPPD013.                                                INKCS006
