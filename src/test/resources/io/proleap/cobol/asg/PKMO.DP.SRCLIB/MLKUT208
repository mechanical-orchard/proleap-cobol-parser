       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT208.                                         00020014
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  10-06-2006.                                       00040001
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:   THE PROGRAM IS DESIGNED TO PERFORM THE LEVEL I DATA   00080086
      *         SMOOTHING CANDIDATE SELECTION PROCESS.                  00090086
      *                (SEE THE FLOW SECTION FOR MORE DETAILS)          00091099
      *                                                                 00100000
      *CHANGE HISTORY:                                                  00110000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00120000
      * ------- ---------- ----------- -------------------------------- 00130000
      * 28582   10-06-2006 ROD JANSSEN CREATION.....                    00140099
W28582* W28582  11-20-2006 JOHN SELWITSCHKA                             00150099
      *                    SET NON-SMOOTH-CANDIDATE. ADD COUNTER FOR    00151099
      *                    DETAIL LEDGER RECORDS W/O FCALC.             00152099
A28582* 28582   12-01-2006 ROD JANSSEN NON-OUTLYER SELECTION ISSUE.     00154099
X28582* 28582   12-15-2006 ROD JANSSEN INACTIVE DEPARTMENTS ISSUE.      00155099
B28582* 28582   01-02-2007 ROD JANSSEN GROSS MARGIN $ RULE CHANGE       00156099
C28582* 28582   01-10-2007 ROD JANSSEN GROSS MARGIN $ REFINEMENT CHANGE 00157099
D28582* 28582   01-12-2007 ROD JANSSEN DECISION TREE RULE CHANGES       00158099
D28582*                                1. GM$ = ZEROS, SET TO ID "3"    00159099
D28582*                                2. EIC <= ZEROS, SET TO ID "9"   00159199
E28582* 28582   01-23-2007 ROD JANSSEN EIC <= ZERO, SET TO ID "3"       00159299
G28582* 28582   03-12-2007 ROD JANSSEN CMU OVERRIDE DEFAULTS TO ZEROS.  00159399
G28582*                                WHY?                             00159499
INC032* 2071787 03-02-2012 COGNIZANT   INC000002071787                  00159599
INC032*                      (KARTHIK) MLK211A-H ABENDED DUE TO         00159699
INC032*                                OVERRIDE TABLE - OVERFLOW ERROR  00159799
INC032*                                INCREASED TABLE SIZE FROM 30999  00159899
INC032*                                TO 40999                         00159999
INC399* 2071787 03-02-2012 COGNIZANT   INC1902399                       00160099
INC399*                                MLK211A-H ABENDED DUE TO         00160199
INC399*                                OVERRIDE TABLE - OVERFLOW ERROR  00160299
INC399*                                INCREASED TABLE SIZE FROM 40999  00160399
INC399*                                TO 50999                         00160499
INC407* 0166121 04-07-2017 COGNIZANT   INC4073216                       00160599
INC407*                                MLK211A-H ABENDED DUE TO         00160699
INC407*                                OVERRIDE TABLE - OVERFLOW ERROR  00160799
INC407*                                INCREASED TABLE SIZE FROM 50999  00160899
INC407*                                TO 60999                         00160999
INC120* 89094   08-06-2021 COGNIZANT   INC12089094                      00160599
INC120*                                MLK211A-H ABENDED DUE TO         00160699
INC120*                                OVERRIDE TABLE - OVERFLOW ERROR  00160799
INC120*                                INCREASED TABLE SIZE FROM 60999  00160899
INC120*                                TO 70999                         00160999
INC125* 56755   12-03-2021 COGNIZANT   INC12556755                      00160599
INC125*                                MLK211A-H ABENDED DUE TO         00160699
INC125*                                OVERRIDE TABLE - OVERFLOW ERROR  00160799
INC125*                                INCREASED TABLE SIZE FROM 70999  00160899
INC125*                                TO 80999                         00160999
INC127* 73102   02-04-2022 COGNIZANT   INC12773102                      00160599
INC127*                                MLK211A-H ABENDED DUE TO         00160699
INC127*                                OVERRIDE TABLE - OVERFLOW ERROR  00160799
INC127*                                INCREASED TABLE SIZE FROM 80999  00160899
INC127*                                TO 90999                         00160999
      ******************************************************************00161099
                                                                        00170000
       ENVIRONMENT DIVISION.                                            00180000
       CONFIGURATION SECTION.                                           00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
       INPUT-OUTPUT SECTION.                                            00220000
       FILE-CONTROL.                                                    00230000
                                                                        00240000
           SELECT FCALC-CONTROL-IN                                      00250000
               ASSIGN TO INP01.                                         00260000
                                                                        00270000
           SELECT DTL-LDGR-FILE-IN                                      00280000
               ASSIGN TO INP02.                                         00290000
                                                                        00300000
           SELECT REQUEST-CNTL-FILE                                     00310000
               ASSIGN TO INP03.                                         00320000
                                                                        00330000
           SELECT OVERRIDE-TBL-FILE                                     00340002
               ASSIGN TO INP04.                                         00350002
                                                                        00360002
           SELECT LDGR-SMOOTH-CAND-FILE-OUT                             00370032
               ASSIGN TO OUT01.                                         00380000
                                                                        00390000
       DATA DIVISION.                                                   00400000
       FILE SECTION.                                                    00410000
                                                                        00420000
       FD  FCALC-CONTROL-IN                                             00430000
           LABEL RECORDS ARE OMITTED                                    00440000
           RECORDING MODE IS F                                          00450000
           BLOCK CONTAINS 0 RECORDS.                                    00460000
       01  FCALC-CONTROL-IN-REC            PIC X(400).                  00470017
                                                                        00480000
       FD  DTL-LDGR-FILE-IN                                             00490000
           LABEL RECORDS ARE OMITTED                                    00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS.                                    00520000
       01  DTL-LDGR-FILE-IN-REC            PIC X(400).                  00530017
                                                                        00540000
       FD  REQUEST-CNTL-FILE                                            00550000
           RECORDING MODE IS F                                          00560000
           LABEL RECORDS ARE STANDARD                                   00570000
           BLOCK CONTAINS 0 RECORDS.                                    00580000
       01  REQUEST-CNTL-REC                PIC X(080).                  00590001
                                                                        00600000
       FD  OVERRIDE-TBL-FILE                                            00610002
           RECORDING MODE IS F                                          00620002
           LABEL RECORDS ARE STANDARD                                   00630002
           BLOCK CONTAINS 0 RECORDS.                                    00640002
B28582     COPY MLRD036.                                                00650099
                                                                        00660002
       FD  LDGR-SMOOTH-CAND-FILE-OUT                                    00670032
           LABEL RECORDS ARE OMITTED                                    00680000
           RECORDING MODE IS F                                          00690000
           BLOCK CONTAINS 0 RECORDS.                                    00700000
       01  LDGR-SMOOTH-CAND-LVL1-REC       PIC X(400).                  00710032
                                                                        00720000
      /*****************************************************************00730000
       WORKING-STORAGE SECTION.                                         00740000
      ******************************************************************00750000
       01  RQ-CNTL-TYPE-AREA.                                           00760000
           05  RQ-CNTL-TYPE-IND            PIC  X(01)      VALUE SPACE. 00770000
               88  RQ-COMMENTS                             VALUE '*'.   00780000
           05  RQ-CNTL-TYPE-SW             PIC  X(08)      VALUE SPACES.00790000
               88  RQ-CNTL-TYPE-WEEKLY                     VALUE        00800000
                   'WEEKLY  '.                                          00810000
               88  RQ-CNTL-TYPE-MONTHLY                    VALUE        00820000
                   'MONTHLY '.                                          00830000
           05  FILLER                      PIC  X(01)      VALUE SPACE. 00840000
           05  RQ-CNTL-TYPE-DATE           PIC  X(10)      VALUE SPACES.00850000
           05  FILLER REDEFINES RQ-CNTL-TYPE-DATE.                      00860000
               10  RQ-CNTL-TYPE-DATE-CCYY  PIC  X(04).                  00870000
               10  FILLER                  PIC  X(01).                  00880000
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-1      VALUE '-'.   00890000
               10  RQ-CNTL-TYPE-DATE-MM    PIC  X(02).                  00900000
               10  FILLER                  PIC  X(01).                  00910000
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-2      VALUE '-'.   00920000
               10  RQ-CNTL-TYPE-DATE-DD    PIC  X(02).                  00930000
           05  FILLER                      PIC  X(01)      VALUE SPACE. 00940000
           05  RQ-CNTL-TABLE-NAME          PIC  X(18)      VALUE SPACES.00950000
           05  FILLER                      PIC  X(41)      VALUE SPACES.00960000
                                                                        00970000
       01  ABEND-CODE                     PIC S9(04) COMP VALUE ZEROS.  00980000
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 00990000
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 01000000
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 01010000
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 01020000
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 01030000
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 01040000
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 01050000
           88  ABEND-4008-DATA-ERROR                       VALUE +4008. 01060000
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009. 01070000
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012. 01080010
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 01090000
           88  ABEND-4019-DATE-ROUTINE-ERROR               VALUE +4019. 01100000
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444. 01110000
                                                                        01120000
       01  PC-PROGRAM-CONSTANTS.                                        01130000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'MLKUT208'.     01140014
INC032*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 30999.   01150099
INC032*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 40999.   01151099
INC399*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 50999.   01152099
INC407*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 60999.   01153099
INC120*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 70999.   01153099
INC125*    05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 80999.   01153099
INC127     05  PC-MAX-OVRRDE           PIC S9(15) COMP-3 VALUE 90999.   01153099
           05  PC-MAX-TARGET           PIC S9(15) COMP-3 VALUE 25999.   01160066
                                                                        01170000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01180005
           05  PE-MSG-02                       PIC X(30) VALUE          01190005
                'DB2 SELECT ATTEMPTED         '.                        01200005
           05  PE-MSG-03                       PIC X(30) VALUE          01210005
                'ATTEMPTING TO OPEN DB2 CURSOR'.                        01220005
           05  PE-MSG-05                       PIC X(30) VALUE          01230005
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                        01240005
                                                                        01250005
       01  PV-PROGRAM-VARIABLES.                                        01260000
           05  PV-DTL-LDGR-READ        PIC S9(15) COMP-3 VALUE ZEROS.   01270000
           05  PV-FCALC-READ           PIC S9(15) COMP-3 VALUE ZEROS.   01280005
W28582     05  PV-DTL-NO-FCALC         PIC S9(15) COMP-3 VALUE ZEROS.   01281097
           05  PV-OVRRDE-READ          PIC S9(15) COMP-3 VALUE ZEROS.   01290005
           05  PV-WRITE-RECS           PIC S9(15) COMP-3 VALUE ZEROS.   01300000
           05  PV-WRITE-TARGET         PIC S9(15) COMP-3 VALUE ZEROS.   01310005
           05  PV-REQUEST-COUNT        PIC S9(15) COMP-3 VALUE ZEROS.   01330011
           05  PV-DISPLAY-AMOUNT  PIC ZZZZZZZZZZZZZZZZZ9    VALUE ZEROS.01340000
           05  PV-DISPLAY-AMOUNT2 PIC ZZZZZZZZZZZZZZZZZ9.99 VALUE ZEROS.01350000
           05  PV-SAVE-KEY.                                             01360000
               10  PV-SV-PARTN-NBR     PIC S9(4) COMP   VALUE ZEROES.   01370015
               10  PV-SV-FSCL-PERIOD   PIC X(02)        VALUE SPACES.   01380015
               10  PV-SV-FSCL-END-DTE  PIC X(10)        VALUE SPACES.   01390015
               10  PV-SV-DEPT-NBR      PIC S9(4) COMP-3 VALUE ZEROES.   01400015
               10  PV-SV-MAJ-CL-NBR    PIC S9(4) COMP-3 VALUE ZEROES.   01410015
               10  PV-SV-SUB-CL-NBR    PIC S9(4) COMP-3 VALUE ZEROES.   01420015
               10  PV-SV-LOC-NBR       PIC S9(4) COMP   VALUE ZEROES.   01430015
               10  PV-SV-ENT-ID        PIC S9(9) COMP   VALUE ZEROES.   01440015
               10  PV-SV-PARTN-GROUP   PIC S9(4) COMP   VALUE ZEROES.   01450015
               10  PV-SV-LBL-SEQ-NBR   PIC S9(9) COMP   VALUE ZEROES.   01460015
               10  PV-SV-ML-LWST-ID    PIC S9(9) COMP   VALUE ZEROES.   01470015
               10  FILLER              PIC X(09)        VALUE SPACES.   01480015
           05  PV-WORK-AMOUNT          PIC S9(11)V99 COMP-3 VALUE +0.   01490015
           05  PV-WORK-AMOUNT2         PIC S9(11)V99 COMP-3 VALUE +0.   01500015
           05  PV-OPERATION-ATTEMPTED  PIC X(30)       VALUE SPACES.    01510015
           05  PV-PARAGRAPH-NAME       PIC X(30)       VALUE SPACES.    01520015
           05  PV-CUM-MKUP-PCT-MAX     PIC S9(9)V9(7)  VALUE ZEROES     01530021
                               COMP-3.                                  01540021
           05  PV-CURR-GRO-MRGN-AMT    PIC S9(11)V9(2) VALUE ZEROES     01550051
                               COMP-3.                                  01560051
           05  PV-CUM-TO-DATE-VARIABLES.                                01570015
               10  PV-CUM-TO-DATE-PURCH-RETAIL                          01580015
                                           PIC S9(11)V9(2) VALUE ZEROES 01590015
                               COMP-3.                                  01600015
               10  PV-CUM-TO-DATE-PURCH-COST                            01610015
                                           PIC S9(11)V9(2) VALUE ZEROES 01620015
                               COMP-3.                                  01630015
               10  PV-CUM-SEASON-MKUP      PIC S9(11)V9(2) VALUE ZEROES 01640015
                               COMP-3.                                  01650015
           05  PV-PREV-FISCAL-DATA.                                     01660015
               10  PV-PREV-END-INV-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES 01670015
                               COMP-3.                                  01680015
               10  PV-PREV-END-INV-COST-AMT                             01690015
                                           PIC S9(11)V9(2) VALUE ZEROES 01700015
                               COMP-3.                                  01710015
               10  FILLER                  PIC X(18)       VALUE SPACES.01720015
           05  PV-SEASON-TO-DATE-DATA.                                  01730015
               10  PV-SEAS-RCPT-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES    01740015
                               COMP-3.                                  01750015
               10  PV-SEAS-RCPT-NET-COST-AMT                            01760015
                                           PIC S9(11)V9(2) VALUE ZEROES 01770015
                               COMP-3.                                  01780015
               10  PV-SEAS-PADJ-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES    01790015
                               COMP-3.                                  01800015
               10  PV-SEAS-PADJ-NET-COST-AMT                            01810015
                                           PIC S9(11)V9(2) VALUE ZEROES 01820015
                               COMP-3.                                  01830015
               10  PV-SEAS-MKUP-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES    01840015
                               COMP-3.                                  01850015
               10  PV-SEAS-XFER-IN-RTL-AMT                              01860015
                                           PIC S9(11)V9(2) VALUE ZEROES 01870015
                               COMP-3.                                  01880015
               10  PV-SEAS-XFER-OUT-RTL-AMT                             01890015
                                           PIC S9(11)V9(2) VALUE ZEROES 01900015
                               COMP-3.                                  01910015
               10  PV-SEAS-INV-COST-ADJ1                                01911093
                                           PIC S9(11)V9(2) VALUE ZEROES 01912093
                               COMP-3.                                  01913093
               10  PV-SEAS-INV-COST-ADJ2                                01914093
                                           PIC S9(11)V9(2) VALUE ZEROES 01915093
                               COMP-3.                                  01916093
B28582     05  PV-SAVE-DEPT-NBR          PIC S9(4)       VALUE ZEROES   01917099
B28582                     COMP-3.                                      01918099
B28582     05  PV-SAVE-ML-LWST-ID        PIC S9(9)       VALUE ZEROES   01920299
B28582                         COMP.                                    01920399
B28582     05  PV-SAVE-OUTLIER-IND PIC X(01) VALUE SPACES.              01920499
                                                                        01921000
       01  PS-PROGRAM-SWITCHES.                                         01930000
           05  PS-PROCESS-COMPLETE-SW    PIC X(01)   VALUE 'N'.         01940000
               88  PS-PROCESS-COMPLETE               VALUE 'Y'.         01950000
           05  PS-FCALC-COMPLETE-SW      PIC X(01)   VALUE 'N'.         01960005
               88  PS-FCALC-COMPLETE                 VALUE 'Y'.         01970005
           05  PS-OVRRDE-FOUND-SW        PIC X(01)   VALUE 'N'.         01980027
               88  PS-OVRRDE-FOUND                   VALUE 'Y'.         01990027
           05  PS-OVRRDE-COMPLETE-SW     PIC X(01)   VALUE 'N'.         02000027
               88  PS-OVRRDE-COMPLETE                VALUE 'Y'.         02010027
           05  PS-TARGET-DONE-SW         PIC X(01)   VALUE 'N'.         02020005
               88  PS-TARGET-COMPLETE                VALUE 'Y'.         02030005
           05  PS-TARGET-FOUND-SW        PIC X(01)   VALUE 'N'.         02040029
               88  PS-TARGET-FOUND                   VALUE 'Y'.         02050016
           05  PS-END-OF-REQUEST-CNTL    PIC X(01)   VALUE 'N'.         02090016
               88  END-OF-REQUEST-CNTL               VALUE 'Y'.         02100016
               88  NOT-END-OF-REQUEST-CNTL           VALUE 'N'.         02110016
                                                                        02150000
       01  WS-TARGET-TABLE.                                             02160005
           05  WS-TARGET-TABLE-ELEMENTS     OCCURS 25999 TIMES          02170087
                                            INDEXED BY TGT-NDX.         02180005
               10 WS-TGT-DEPT-NBR           PIC S9(4)V COMP-3.          02190005
               10 WS-TGT-EFF-BEG-DTE        PIC X(10).                  02200005
               10 WS-TGT-MIN-CUM-MKUP-PCT   PIC S9(4)V9(7) COMP-3.      02210005
               10 WS-TGT-MAX-CUM-MKUP-PCT   PIC S9(4)V9(7) COMP-3.      02220005
               10 WS-TGT-EFF-END-DTE        PIC X(10).                  02230005
               10 WS-TGT-MIN-GRO-MRGN-AMT   PIC S9(11)V9(2) COMP-3.     02240005
               10 WS-TGT-MAX-GRO-MRGN-AMT   PIC S9(11)V9(2) COMP-3.     02250005
                                                                        02260005
       01  WS-CMU-OVRRDE-TABLE.                                         02270029
INC032*    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 30999 TIMES          02280099
      *    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 40999 TIMES          02281099
      *    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 50999 TIMES          02282099
INC407*    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 60999 TIMES          02283099
INC120*    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 70999 TIMES          02283099
INC125*    05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 80999 TIMES          02283099
INC127     05  WS-CMUOVR-TABLE-ELEMENTS     OCCURS 90999 TIMES          02283099
                                            INDEXED BY OVR-NDX.         02290027
               10 WS-CMO-DEPT-NBR           PIC  S9(4)V COMP-3.         02300025
               10 WS-CMO-LBL-SEQ-NBR        PIC  S9(4) COMP.            02310025
               10 WS-CMO-ENT-ID             PIC  S9(9) COMP.            02320025
               10 WS-CMO-ML-LO-MDSE-LVL-ID  PIC  S9(9) COMP.            02330025
               10 WS-CMO-EFF-BEG-DTE        PIC   X(10).                02340025
               10 WS-CMO-OVRID-CUM-MKUP-PCT PIC  S9(4)V9(7) COMP-3.     02350025
               10 WS-CMO-EFF-END-DTE        PIC   X(10).                02360025
B28582         10 WS-CMO-DBV-UNKNOWN-IND    PIC   X(01).                02361099
                                                                        02370025
      /*****************************************************************02380017
      * COPYBOOK - INPUT FOR MLT_DEPT_BRND_VND TABLE RECALC FILE        02390086
      ******************************************************************02400017
           COPY MLRD070 REPLACING ML070 BY MLDBV.                       02410017
                                                                        02420017
      /*****************************************************************02430017
      * COPYBOOK - INPUT FOR DETAIL_LEDGER TABLE RECALC FILE            02440086
      ******************************************************************02450017
           COPY MLRD070 REPLACING ML070 BY MLDTL.                       02460017
                                                                        02470017
      /*****************************************************************02480017
      * COPYBOOK - OUTPUT FOR DETAIL_LEDGER SMOOTH CANDIDATE FILE       02490086
      ******************************************************************02500017
           COPY MLRD070.                                                02510017
                                                                        02520017
      ******************************************************************02530004
      * COPYBOOK -                                                      02540004
      *    DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA      02550004
      * MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS.     02560004
      ******************************************************************02570004
           COPY DPWS004.                                                02580004
                                                                        02590004
      ******************************************************************02600004
           EXEC SQL                                                     02610004
               INCLUDE SQLCA                                            02620004
           END-EXEC.                                                    02630004
                                                                        02640004
      ******************************************************************02650004
      * DB2 - MLT_CUM_MKUP_TGT.                                         02660004
      ******************************************************************02670004
           EXEC SQL                                                     02680004
               INCLUDE MLT00013                                         02690004
           END-EXEC.                                                    02700004
                                                                        02710004
      ******************************************************************02720004
      * DB2 - MLT_CORP_GLBL_DFLT.                                       02730004
      ******************************************************************02740004
           EXEC SQL                                                     02750004
               INCLUDE MLT00014                                         02760004
           END-EXEC.                                                    02770004
                                                                        02780004
      /***************************************************************  02790004
      *  DB2 TARGET TABLE CURSOR DECLARATION                            02800004
      ****************************************************************  02810004
           EXEC SQL                                                     02820004
             DECLARE TARGET_CURSOR CURSOR FOR                           02830004
               SELECT  DEPT_NBR                                         02840004
                      ,EFF_BEG_DTE                                      02850004
                      ,MIN_CUM_MKUP_PCT                                 02860004
                      ,MAX_CUM_MKUP_PCT                                 02870004
                      ,VALUE(EFF_END_DTE,'9999-12-31')                  02880016
                      ,MIN_GRO_MRGN_AMT                                 02890004
                      ,MAX_GRO_MRGN_AMT                                 02900004
               FROM MLT_CUM_MKUP_TGT                                    02910004
               WHERE EFF_BEG_DTE <= :MLT00013-EFF-BEG-DTE               02920004
                 AND (EFF_END_DTE >= :MLT00013-EFF-END-DTE              02930004
                  OR  EFF_END_DTE IS NULL)                              02940004
               UNION                                                    02950004
               SELECT  SMALLINT(0)                                      02960004
                      ,EFF_BEG_DTE                                      02970004
                      ,MIN_CUM_MKUP_PCT                                 02980004
                      ,MAX_CUM_MKUP_PCT                                 02990004
                      ,VALUE(EFF_END_DTE,'9999-12-31')                  03000016
                      ,MIN_GRO_MRGN_AMT                                 03010004
                      ,MAX_GRO_MRGN_AMT                                 03020004
               FROM MLT_CORP_GLBL_DFLT                                  03030004
               WHERE EFF_BEG_DTE <= :MLT00014-EFF-BEG-DTE               03040004
                 AND (EFF_END_DTE >= :MLT00014-EFF-END-DTE              03050004
                  OR  EFF_END_DTE IS NULL)                              03060004
               ORDER BY 1,2,5                                           03070004
               WITH UR                                                  03080006
           END-EXEC.                                                    03090004
                                                                        03100004
      /*****************************************************************03110000
      *FLOW: TO DETERMINE LEVEL I DATA SMOOTH CANDIDATES.               03120099
      *                                                                 03130015
      *      BREAKS OCCUR AT THE DEPT AND DKEY LEVELS.                  03136099
      *                                                                 03139399
      *      THE F_CALC TABLE, INPUT01, CONTROLS THE CANDIDATE SELECTION03139499
      *      PROCESS. WITH LEVEL I SMOOTHING, WE ARE DETERMINNG WHICH   03139599
      *      ROWS ARE TO BE SMOOTHED, INPUT02, TO THE DEPT/BRAND/VENDOR 03139699
      *      LEVEL BY COMPARING TABLE THE F_CALC TO THE TO BE SMOOTHED  03139799
      *      TABLE COLUMNS. AN INDICATOR IS SET BASED ON THE CANDIDATE  03139899
      *      MATCHING DEFINED CRITERIA.                                 03139999
      *                                                                 03140099
      *       TYPE 0 - NON-OUTLYER (SMOOTH USING DBV CMU PERCENT)       03141015
      *       TYPE 1 - CUM MKUP PCT OUTLYER                             03150015
      *       TYPE 2 - GROSS MARGIN/CUM MKUP PCT OUTLYER                03160015
      *       TYPE 3 - OUTLYER OTHER. A CANDIDATE FOR LEVEL 2.5 SMOOTH  03170099
      *       TYPE 4 - N/A                                              03180023
      *       TYPE 5 - N/A                                              03190023
      *       TYPE 6 - N/A                                              03200023
      *       TYPE 7 - N/A                                              03210023
      *       TYPE 8 - N/A                                              03220023
      *       TYPE 9 - NON SMOOTHING CANDIDATE                          03230099
      *                                                                 03240000
      *      INPUTS:                                                    03360299
      *           INP01 - MLT_DEPT_BRND_VND RECALC EXTRACT (MLK202)     03360399
      *                   (&PSQENV..&SYSID..MLT00010.EXT.MLK199S&RUNLTR)03360499
      *           INP02 - MLT_WKLY_SUM/MLT_MNLY_SUM RECALC EXTRACT      03360599
      *                   (MLK188/MLK189/MLK202)                        03360699
      *                   (&PSQENV..&SYSID..&TBL..EXTRACT.SRTD&RUNLTR)  03360799
      *           INP03 - THE FISCAL PERIOD (MLK190)                    03360899
      *                   (&PSQENV..&SYSID..RECALC.FISCAL.PERIOD.&TBL)  03360999
      *           INP04 - CMU PERCENT TARGET/OVERRIDE VARIABLES (MLK201)03361099
      *                   (&PSQENV..&SYSID..&TBL..OVRRDE.EXTRACT)       03361199
      *                                                                 03361299
      *      OUTPUT:                                                    03361399
      *           OUT01 - &PSQENV..&SYSID..&TBL..LEVELI.CANDI&RUNLTR    03361899
      *                                                                 03361999
      *      TABLES USED:                                               03362099
      *         MLT_CUM_MKUP_TGT                                        03362199
      *         MLT_CORP_GLBL_DFLT                                      03362299
      *                                                                 03362599
                                                                        03363015
      ******************************************************************03370000
      ******************************************************************03380000
       PROCEDURE DIVISION.                                              03390003
      ******************************************************************03400000
                                                                        03410000
       0000-MAINLINE-MLKUT208.                                          03420014
                                                                        03430000
           PERFORM 1000-INITIALIZATION.                                 03440000
                                                                        03450000
           PERFORM 2000-LVLI-CANDIDATE-MAIN-LOOP                        03460015
               UNTIL PS-PROCESS-COMPLETE.                               03471073
                                                                        03480000
           PERFORM 9000-END-OF-JOB-TASKS.                               03490000
                                                                        03500000
       9999-EXIT-PROGRAM.                                               03510000
           STOP RUN.                                                    03520000
                                                                        03530000
      /*****************************************************************03540000
      * 1000-INITIALIZATION.                                            03550000
      ******************************************************************03560000
       1000-INITIALIZATION.                                             03570000
                                                                        03580000
           DISPLAY '******************************************'.        03590000
           DISPLAY '*  START OF ', PC-PROGRAM-NAME                      03600000
           DISPLAY '******************************************'.        03610000
           DISPLAY ' '.                                                 03620000
                                                                        03630000
           OPEN INPUT  FCALC-CONTROL-IN                                 03640000
                INPUT  DTL-LDGR-FILE-IN                                 03650000
                INPUT  REQUEST-CNTL-FILE                                03660000
                INPUT  OVERRIDE-TBL-FILE                                03670002
                OUTPUT LDGR-SMOOTH-CAND-FILE-OUT.                       03680032
                                                                        03690000
           INITIALIZE MLDBV-COMMON-RECALC-EXTRACT.                      03691086
           INITIALIZE MLDTL-COMMON-RECALC-EXTRACT.                      03691186
           INITIALIZE ML070-COMMON-RECALC-EXTRACT.                      03691286
           INITIALIZE ML036-RECORD.                                     03691386
                                                                        03692086
           MOVE 'N'            TO PS-PROCESS-COMPLETE-SW.               03700005
           MOVE 'N'            TO PS-FCALC-COMPLETE-SW.                 03710005
           MOVE 'N'            TO PS-OVRRDE-COMPLETE-SW.                03720005
                                                                        03730000
           MOVE ZEROS          TO PV-DTL-LDGR-READ                      03740024
           MOVE ZEROS          TO PV-FCALC-READ                         03750024
W28582     MOVE ZEROS          TO PV-DTL-NO-FCALC                       03751097
           MOVE ZEROS          TO PV-OVRRDE-READ                        03760024
           MOVE ZEROS          TO PV-WRITE-RECS                         03770024
           MOVE ZEROS          TO PV-WRITE-TARGET                       03780024
           MOVE ZEROS          TO PV-REQUEST-COUNT.                     03790024
                                                                        03800024
      ******************************************************************03810015
           DISPLAY '********************************************'.      03820000
           DISPLAY '*** CONTROL/FISCAL DATES BEING PROCESSED.'          03830000
           DISPLAY ' '.                                                 03831066
                                                                        03840000
           PERFORM 1200-PROCESS-RQ-CNTL-TYPE.                           03850000
                                                                        03860000
           IF PV-REQUEST-COUNT > ZEROES                                 03870000
               IF PV-REQUEST-COUNT > +1                                 03880000
                   DISPLAY ' ** MULTIPLE REQUEST CONTROL: '             03890000
                           PV-REQUEST-COUNT                             03900000
                   DISPLAY ' ** USE THE LAST REQUEST'                   03910000
               END-IF                                                   03920000
               DISPLAY ' *** TABLE BEING SMOOTHED ', RQ-CNTL-TABLE-NAME 03930011
           ELSE                                                         03940000
               DISPLAY ' *** NO CONTROL/FISCAL DATE TO PROCESS.'        03950000
               MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME          03960063
               SET LOGICAL-ISSUE-ABEND   TO TRUE                        03970000
               PERFORM 9999-ABEND                                       03980000
           END-IF.                                                      03990000
                                                                        04000000
      ******************************************************************04010067
           DISPLAY '********************************************'.      04020067
           DISPLAY '*** FIRST FCALC READ.  '                            04030067
           DISPLAY ' '.                                                 04040067
                                                                        04050067
           PERFORM 7500-READ-FCALC-FILE.                                04060067
           IF PS-FCALC-COMPLETE                                         04070067
               DISPLAY '********************************************'   04080067
               DISPLAY '*** ', PC-PROGRAM-NAME, ' NOTHING TO PROCESS '  04090067
               DISPLAY '********************************************'   04100067
           END-IF.                                                      04110067
                                                                        04120067
      ******************************************************************04130067
           DISPLAY '********************************************'.      04140067
           DISPLAY '*** FIRST DETAIL LEDGER TABLE READ.  '              04150067
           DISPLAY ' '.                                                 04160067
                                                                        04170067
           PERFORM 7000-READ-DTL-LDGR-FILE.                             04180067
           IF (PS-FCALC-COMPLETE AND                                    04190067
               PS-PROCESS-COMPLETE)                                     04200067
               NEXT SENTENCE                                            04210067
           ELSE                                                         04220067
           IF PS-PROCESS-COMPLETE                                       04230067
               DISPLAY '*** ABEND - DETAIL LEDGER TABLE FILE IS EMPTY ' 04240067
               MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME          04250067
               SET LOGICAL-ISSUE-ABEND   TO TRUE                        04260067
               PERFORM 9999-ABEND                                       04270067
           ELSE                                                         04280067
               DISPLAY '*******************************************'    04290067
               DISPLAY '*** TARGET TABLE LOAD '                         04300067
               MOVE 'N'                TO PS-TARGET-DONE-SW             04310067
               INITIALIZE WS-TARGET-TABLE                               04320067
               MOVE RQ-CNTL-TYPE-DATE  TO MLT00013-EFF-BEG-DTE          04330067
               MOVE RQ-CNTL-TYPE-DATE  TO MLT00013-EFF-END-DTE          04340067
               MOVE RQ-CNTL-TYPE-DATE  TO MLT00014-EFF-BEG-DTE          04350067
               MOVE RQ-CNTL-TYPE-DATE  TO MLT00014-EFF-END-DTE          04360067
               PERFORM 7200-OPEN-TARGET-TABLE                           04370067
               PERFORM 7300-LOAD-TARGET-TABLE                           04380067
                 VARYING TGT-NDX FROM 1 BY 1                            04390067
                   UNTIL PS-TARGET-COMPLETE OR                          04400067
                         TGT-NDX IS GREATER THAN PC-MAX-TARGET          04410067
               PERFORM 7400-CLOSE-TARGET-TABLE                          04420067
                                                                        04430067
               IF (PV-WRITE-TARGET IS EQUAL TO ZEROS) OR                04440067
                  (TGT-NDX IS GREATER THAN PC-MAX-TARGET)               04450067
                   DISPLAY '*** ABEND - TARGET TABLE LD EXCEEDED  '     04460076
                   MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME      04470067
                   SET LOGICAL-ISSUE-ABEND   TO TRUE                    04480067
                   PERFORM 9999-ABEND                                   04490067
               ELSE                                                     04500067
                   DISPLAY '*** TARGET TABLE LOADED - ' PV-WRITE-TARGET 04510067
                   DISPLAY '*******************************************'04520067
                   DISPLAY ' '                                          04530067
                                                                        04540075
      ******************************************************************04570067
                   DISPLAY '*******************************************'04580075
                   DISPLAY '*** OVERRIDE TABLE LOAD                '    04590075
                   PERFORM 7600-READ-OVRRDE-FILE                        04680075
                   IF PS-OVRRDE-COMPLETE                                04690075
                     DISPLAY '*** ABEND - OVERRIDE TABLE FILE IS EMPTY '04700075
                     MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME    04710075
                     SET LOGICAL-ISSUE-ABEND   TO TRUE                  04720075
                     PERFORM 9999-ABEND                                 04730075
                   ELSE                                                 04740075
                     INITIALIZE WS-CMU-OVRRDE-TABLE                     04750075
                     PERFORM 7800-LOAD-OVRRDE-TABLE                     04760075
                       VARYING OVR-NDX FROM 1 BY 1                      04770075
                         UNTIL PS-OVRRDE-COMPLETE OR                    04780075
                               OVR-NDX IS GREATER THAN PC-MAX-OVRRDE    04790075
                                                                        04800075
                     IF (PV-OVRRDE-READ IS EQUAL TO ZEROS) OR           04810075
                        (OVR-NDX IS GREATER THAN PC-MAX-OVRRDE)         04820075
                       DISPLAY '*** ABEND - OVERRIDE TABLE LD EXCEEDED' 04830076
                       MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME  04840075
                       SET LOGICAL-ISSUE-ABEND   TO TRUE                04850075
                       PERFORM 9999-ABEND                               04860075
                     ELSE                                               04870075
                       DISPLAY '*** OVERRIDE TABLE LOADED - ',          04880075
                                   PV-OVRRDE-READ                       04881075
                       DISPLAY                                          04890075
                         '*******************************************'  04891075
                       DISPLAY ' '                                      04900075
                     END-IF                                             04910075
                   END-IF                                               04920075
                END-IF                                                  04921075
             END-IF.                                                    04930075
                                                                        04940067
      ******************************************************************04950067
           DISPLAY '********************************************'.      04960067
           DISPLAY '*** PRIME FOR FIRST SMOOTHING CANDIDATE '.          04970067
           DISPLAY '********************************************'.      04980067
           DISPLAY ' '.                                                 04990067
           IF (PS-FCALC-COMPLETE AND                                    05000067
               PS-PROCESS-COMPLETE)                                     05010067
               NEXT SENTENCE                                            05020067
           ELSE                                                         05030067
               PERFORM 3000-CALC-SEASON-TO-DATE-PURCH                   05031067
               PERFORM 3100-CALC-CUMM-TO-DATE-PURCH                     05032067
               PERFORM 3200-CALC-CUMM-MARKUP-PCT                        05033067
               PERFORM 3900-CALC-GROSS-MARGIN-AMT                       05034067
               PERFORM 3300-SEARCH-CMU-PCT-TARGET                       05035067
               DISPLAY '********************************************'   05035170
               DISPLAY                                                  05035276
                  '*** LEVEL I SMOOTHING CANDIDATE SELECTION BEGINS'    05035376
               DISPLAY '********************************************'   05035470
               DISPLAY ' '                                              05035570
           END-IF.                                                      05036067
                                                                        05037067
      ******************************************************************05040000
      * PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES           05050000
      ******************************************************************05060000
       1200-PROCESS-RQ-CNTL-TYPE.                                       05070000
                                                                        05080000
           DISPLAY 'EXTRACT REQUESTED:'.                                05090000
           DISPLAY '  ----+----1----+----2----+----3----+----4'.        05100000
           PERFORM 7700-READ-REQUEST-CNTL.                              05110009
                                                                        05120000
           MOVE ZEROES             TO PV-REQUEST-COUNT.                 05130000
                                                                        05140000
           PERFORM                                                      05150000
               UNTIL END-OF-REQUEST-CNTL                                05160000
                                                                        05170000
               DISPLAY '  ' RQ-CNTL-TYPE-AREA                           05180000
               IF RQ-COMMENTS                                           05190000
                   CONTINUE                                             05200000
               ELSE                                                     05210000
                   IF RQ-CNTL-TYPE-DATE-CCYY IS NUMERIC                 05220000
                       AND RQ-CNTL-TYPE-DATE-MM IS NUMERIC              05230000
                       AND RQ-CNTL-TYPE-DATE-DD IS NUMERIC              05240000
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-1               05250000
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-2               05260000
                       ADD +1                  TO PV-REQUEST-COUNT      05270000
                       DISPLAY ' ** TYPE: ' RQ-CNTL-TYPE-SW             05280000
                               ', DATE: ' RQ-CNTL-TYPE-DATE             05290000
                   END-IF                                               05300000
               END-IF                                                   05310000
                                                                        05320000
               PERFORM 7700-READ-REQUEST-CNTL                           05330009
                                                                        05340000
           END-PERFORM.                                                 05350000
                                                                        05360000
      /*****************************************************************05370000
      * 2000-LVLI-CANDIDATE-MAIN-LOOP.                                  05380015
      ******************************************************************05390000
       2000-LVLI-CANDIDATE-MAIN-LOOP.                                   05400015
                                                                        05410000
           IF MLDBV-DEPT-NBR IS NOT EQUAL TO PV-SV-DEPT-NBR             05420062
               DISPLAY ' PROCESSING DEPT/DATE ' MLDBV-DEPT-NBR          05430044
                       '/' MLDBV-FSCL-END-DTE                           05440044
                       ' ... ' FUNCTION CURRENT-DATE                    05450044
               MOVE MLDBV-FSCL-END-DTE  TO PV-SV-FSCL-END-DTE           05451062
               MOVE MLDBV-DEPT-NBR      TO PV-SV-DEPT-NBR               05452062
               MOVE MLDBV-MAJ-CL-NBR    TO PV-SV-MAJ-CL-NBR             05453062
               MOVE MLDBV-SUB-CL-NBR    TO PV-SV-SUB-CL-NBR             05454062
               MOVE MLDBV-LOC-NBR       TO PV-SV-LOC-NBR                05455062
               MOVE MLDBV-ENT-ID        TO PV-SV-ENT-ID                 05456062
               MOVE MLDBV-LBL-SEQ-NBR   TO PV-SV-LBL-SEQ-NBR            05457062
               MOVE MLDBV-ML-LWST-ID    TO PV-SV-ML-LWST-ID             05458062
           END-IF.                                                      05520034
                                                                        05530034
           IF MLDBV-DEPT-NBR = MLDTL-DEPT-NBR                           05590039
             IF MLDBV-ML-LWST-ID = MLDTL-ML-LWST-ID                     05600039
               PERFORM 3500-CMU-CANDIDATE-SELECT                        05610023
B28582******   PERFORM 3600-G-MRGN-CANDIDATE-SELECT                     05620099
               PERFORM 7100-WRITE-EXTRACT-REC                           05630023
               PERFORM 7000-READ-DTL-LDGR-FILE                          05640039
             ELSE                                                       05650039
             IF MLDBV-ML-LWST-ID > MLDTL-ML-LWST-ID                     05651054
W28582         ADD 1 TO PV-DTL-NO-FCALC                                 05651197
               PERFORM 5000-CALC-SEASON-TO-DATE-PURCH                   05651255
               PERFORM 5100-CALC-CUMM-TO-DATE-PURCH                     05651356
               PERFORM 5200-CALC-CUMM-MARKUP-PCT                        05651455
               PERFORM 5900-CALC-GROSS-MARGIN-AMT                       05651555
               PERFORM 5300-SEARCH-CMU-PCT-TARGET                       05651655
               PERFORM 5500-CMU-CANDIDATE-SELECT                        05651755
B28582******   PERFORM 5600-G-MRGN-CANDIDATE-SELECT                     05651899
               PERFORM 7100-WRITE-EXTRACT-REC                           05651954
               PERFORM 7000-READ-DTL-LDGR-FILE                          05652054
             ELSE                                                       05653054
               PERFORM 7500-READ-FCALC-FILE                             05660039
               PERFORM 3000-CALC-SEASON-TO-DATE-PURCH                   05750039
               PERFORM 3100-CALC-CUMM-TO-DATE-PURCH                     05760039
               PERFORM 3200-CALC-CUMM-MARKUP-PCT                        05770039
               PERFORM 3900-CALC-GROSS-MARGIN-AMT                       05780051
               PERFORM 3300-SEARCH-CMU-PCT-TARGET                       05790039
           ELSE                                                         05850023
           IF MLDBV-DEPT-NBR > MLDTL-DEPT-NBR                           05851074
             PERFORM 5000-CALC-SEASON-TO-DATE-PURCH                     05851274
             PERFORM 5100-CALC-CUMM-TO-DATE-PURCH                       05851374
             PERFORM 5200-CALC-CUMM-MARKUP-PCT                          05851474
             PERFORM 5900-CALC-GROSS-MARGIN-AMT                         05851574
             PERFORM 5300-SEARCH-CMU-PCT-TARGET                         05851674
             PERFORM 5500-CMU-CANDIDATE-SELECT                          05851774
B28582****   PERFORM 5600-G-MRGN-CANDIDATE-SELECT                       05851899
             PERFORM 7100-WRITE-EXTRACT-REC                             05851974
             PERFORM 7000-READ-DTL-LDGR-FILE                            05852074
           ELSE                                                         05853074
             PERFORM 7500-READ-FCALC-FILE                               05860039
             PERFORM 3000-CALC-SEASON-TO-DATE-PURCH                     05950039
             PERFORM 3100-CALC-CUMM-TO-DATE-PURCH                       05960039
             PERFORM 3200-CALC-CUMM-MARKUP-PCT                          05970039
             PERFORM 3900-CALC-GROSS-MARGIN-AMT                         05980051
             PERFORM 3300-SEARCH-CMU-PCT-TARGET                         05990071
           END-IF.                                                      06050013
                                                                        06060013
      /**************************************************************** 06070015
      * 3000 SERIES ROUTINES - SMOOTHING USING DEPARTMENT/BRAND/VENDOR  06071081
      *                        TABLE ROW INFORMATION!!                  06072086
      *                                                                 06073081
      * 3000-CALC-SEASON-TO-DATE-PURCH.                                 06080015
      * CALCULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING 06090015
      ***************************************************************** 06100000
       3000-CALC-SEASON-TO-DATE-PURCH.                                  06110015
                                                                        06120000
           ADD MLDBV-CURR-RCPT-RTL-AMT                                  06130015
               MLDBV-SEAS-RCPT-RTL-AMT                                  06140015
                 GIVING PV-SEAS-RCPT-RTL-AMT.                           06150015
                                                                        06160015
           ADD MLDBV-CURR-RCPT-NET-COST-AMT                             06170015
               MLDBV-SEAS-RCPT-NET-COST-AMT                             06180015
                 GIVING PV-SEAS-RCPT-NET-COST-AMT.                      06190016
                                                                        06200015
           ADD MLDBV-CURR-PADJ-RTL-AMT                                  06210015
               MLDBV-SEAS-PADJ-RTL-AMT                                  06220015
                 GIVING PV-SEAS-PADJ-RTL-AMT.                           06230016
                                                                        06240015
           ADD MLDBV-CURR-PADJ-NET-COST-AMT                             06250015
               MLDBV-SEAS-PADJ-NET-COST-AMT                             06260015
                 GIVING PV-SEAS-PADJ-NET-COST-AMT.                      06270016
                                                                        06280015
           ADD MLDBV-CURR-MKUP-RTL-AMT                                  06290015
               MLDBV-SEAS-MKUP-RTL-AMT                                  06300015
                 GIVING PV-SEAS-MKUP-RTL-AMT.                           06310016
                                                                        06320015
           ADD MLDBV-CURR-XFER-IN-RTL-AMT                               06330015
               MLDBV-SEAS-XFER-IN-RTL-AMT                               06340015
                 GIVING PV-SEAS-XFER-IN-RTL-AMT.                        06350016
                                                                        06360015
           ADD MLDBV-CURR-XFER-OUT-RTL-AMT                              06370015
               MLDBV-SEAS-XFER-OUT-RTL-AMT                              06380015
                 GIVING PV-SEAS-XFER-OUT-RTL-AMT.                       06390016
                                                                        06400015
           ADD MLDBV-INV-COST-ADJ1                                      06401093
               MLDBV-SEAS-INV-COST-ADJ1                                 06402093
                 GIVING PV-SEAS-INV-COST-ADJ1.                          06403093
                                                                        06404093
           ADD MLDBV-INV-COST-ADJ2                                      06405093
               MLDBV-SEAS-INV-COST-ADJ2                                 06406093
                 GIVING PV-SEAS-INV-COST-ADJ2.                          06407093
                                                                        06408093
      ******************************************************************06410015
      * 3100-CALC-CUMM-TO-DATE-PURCH.                                   06420015
      * DETERMINE CUMULATIVE RETAIL AND COST                            06430015
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS    06440015
      ******************************************************************06450015
       3100-CALC-CUMM-TO-DATE-PURCH.                                    06460015
                                                                        06470015
B28582     MOVE MLDBV-CUM-TO-DATE-PURCH-RTL                             06470199
B28582                         TO PV-CUM-TO-DATE-PURCH-RETAIL.          06471099
B28582     MOVE MLDBV-CUM-TO-DATE-PURCH-COST                            06472199
B28582                         TO PV-CUM-TO-DATE-PURCH-COST.            06473099
B28582                                                                  06476099
B28582*    COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                        06480099
B28582*                                   (  MLDBV-BEG-SEAS-INV-RTL-AMT 06490099
B28582*                                    + PV-SEAS-XFER-IN-RTL-AMT    06500099
B28582*                                    - PV-SEAS-XFER-OUT-RTL-AMT   06510099
B28582*                                    + PV-SEAS-RCPT-RTL-AMT       06520099
B28582*                                    + PV-SEAS-PADJ-RTL-AMT       06530099
B28582*                                    + PV-SEAS-MKUP-RTL-AMT).     06540099
B28582*                                                                 06550099
B28582*    COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                        06560099
B28582*                                  (  MLDBV-BEG-SEAS-INV-COST-AMT 06570099
B28582*                                   + PV-SEAS-RCPT-NET-COST-AMT   06580099
W28582*                                   + PV-SEAS-PADJ-NET-COST-AMT). 06590099
W28582*                                   + PV-SEAS-INV-COST-ADJ1       06591097
W28582*                                   + PV-SEAS-INV-COST-ADJ2).     06592097
                                                                        06600015
      ***************************************************************** 06610021
      * 3200-CALC-CUMM-MARKUP-PCT.                                      06620021
      * FINAL CALCULATION OF CUMULATIVE MARKUP PERCENT                  06630021
      ***************************************************************** 06640021
       3200-CALC-CUMM-MARKUP-PCT.                                       06650021
                                                                        06660021
           COMPUTE PV-CUM-SEASON-MKUP = (PV-CUM-TO-DATE-PURCH-RETAIL    06670021
                                      - PV-CUM-TO-DATE-PURCH-COST).     06680021
                                                                        06690021
           IF PV-CUM-TO-DATE-PURCH-RETAIL NOT EQUAL 0                   06700021
               COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED                      06710021
                                      = (PV-CUM-SEASON-MKUP * 100)      06720021
                                      / PV-CUM-TO-DATE-PURCH-RETAIL     06730021
G28582     ELSE                                                         06731099
G28582         MOVE 100 TO PV-CUM-MKUP-PCT-MAX                          06732099
           END-IF.                                                      06740021
                                                                        06750021
      /**************************************************************** 06760027
      * 3300-SEARCH-CMU-PCT-TARGET.                                     06770027
      * SEARCH FOR THE LEVEL I SMOOTHING DEPT/CORP TARGET RANGE.....    06780027
      *                                                                 06790027
      * RULES:                                                          06800027
      ** 1.  FIND THE DEPARTMENT LEVEL CMU PERCENT TARGET RANGE......   06810027
      ** 2.  FIND THE CORPORATE LEVEL CMU PERCENT TARGET RANGE......    06820027
      ** 3.  NOT FOUND, ABEND....                                       06830047
      ***************************************************************** 06840027
       3300-SEARCH-CMU-PCT-TARGET.                                      06850027
                                                                        06860027
           MOVE 'N'            TO PS-TARGET-FOUND-SW.                   06870027
                                                                        06880027
      ** FIND THE DEPARTMENT LEVEL CMU PERCENT TARGET RANGE......       06890027
           SET TGT-NDX         TO 1.                                    06900027
           SEARCH WS-TARGET-TABLE-ELEMENTS                              06910027
             AT END                                                     06920027
               MOVE 'N'        TO PS-TARGET-FOUND-SW                    06930027
             WHEN  MLDBV-DEPT-NBR = WS-TGT-DEPT-NBR (TGT-NDX)           06940027
               AND (MLDBV-FSCL-END-DTE >= WS-TGT-EFF-BEG-DTE (TGT-NDX)  06961083
               AND  MLDBV-FSCL-END-DTE <= WS-TGT-EFF-END-DTE (TGT-NDX)) 06962083
               MOVE 'Y'                   TO PS-TARGET-FOUND-SW         06970027
           END-SEARCH.                                                  06980027
                                                                        06990027
      ** FIND THE CORPORATE LEVEL CMU PERCENT TARGET RANGE......        07000027
           IF (NOT PS-TARGET-FOUND)                                     07010027
             SET TGT-NDX         TO 1                                   07020027
             SEARCH WS-TARGET-TABLE-ELEMENTS                            07030027
               AT END                                                   07040027
                 DISPLAY '*** ABEND - '                                 07050047
                 DISPLAY '*** 3300-SEARCH-CMU-PCT-TARGET'               07060027
                 DISPLAY '*** NO CMU PERCENT TARGET FOUND - '           07070027
                 DISPLAY '    USE THE D/B/V TABLE DEFAULT...'           07080027
                 DISPLAY ' DEPT   - ', MLDBV-DEPT-NBR,                  07090027
                         ' FISCAL - ', MLDBV-FSCL-END-DTE               07100027
                         ' DBV KEY - ', MLDBV-KEY-DATA                  07110027
                 MOVE '3300-SEARCH-CMU-P'  TO PV-PARAGRAPH-NAME         07120047
                 SET LOGICAL-ISSUE-ABEND   TO TRUE                      07130047
                 PERFORM 9999-ABEND                                     07140047
               WHEN  WS-TGT-DEPT-NBR (TGT-NDX) = 0                      07150057
                AND (MLDBV-FSCL-END-DTE >= WS-TGT-EFF-BEG-DTE (TGT-NDX) 07160083
                AND  MLDBV-FSCL-END-DTE <= WS-TGT-EFF-END-DTE (TGT-NDX))07170083
                 MOVE 'Y'                   TO PS-TARGET-FOUND-SW       07180027
             END-SEARCH                                                 07190027
           END-IF.                                                      07200027
                                                                        07210027
      ***************************************************************** 07220015
      * 3500-CMU-CANDIDATE-SELECT.                                      07230023
      ***************************************************************** 07240015
       3500-CMU-CANDIDATE-SELECT.                                       07250023
B28582** RULE - OTHER SMOOTH <DBV CANDIDATE IS UNKNOWN/UNKNOWN>         07300399
B28582**    THE DEPT/BRAND/VENDOR COMBINATION IS UNKNOWN BRAND/UNKNOWN  07300599
B28582**    VENDOR....                                                  07300699
B28582     PERFORM 3800-DBV-UNKNWN-UNKNWN-TEST                          07301099
C28582     IF MLDTL-OUTLYER-OTHER-SMOOTH                                07301199
B28582         NEXT SENTENCE                                            07301299
B28582     ELSE                                                         07301399
E28582** RULE - NON-SMOOTH <END INV COST = ZEROS>                       07301499
E28582     IF MLDBV-CURR-END-INV-COST-AMT IS LESS THAN ZEROS OR         07301599
E28582        MLDBV-CURR-END-INV-COST-AMT IS EQUAL TO ZEROS             07301699
E28582         SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                   07301799
E28582****     MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   07301899
E28582****     SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   07301999
           ELSE                                                         07302099
C28582** RULE - NON-SMOOTH <GROSS MARGIN AMOUNT = ZEROS>                07302199
C28582     IF PV-CURR-GRO-MRGN-AMT IS EQUAL TO ZEROS                    07302299
D28582         SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                   07302399
D28582****     MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   07302499
D28582****     SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   07302599
C28582     ELSE                                                         07302699
C28582** RULE - GM $ SMOOTH <GROSS MARGIN RANGE TEST>                   07302799
B28582     IF (PV-CURR-GRO-MRGN-AMT                                     07302899
B28582                     >= WS-TGT-MIN-GRO-MRGN-AMT (TGT-NDX) AND     07302999
B28582         PV-CURR-GRO-MRGN-AMT                                     07303099
B28582                     <= WS-TGT-MAX-GRO-MRGN-AMT (TGT-NDX) )       07303199
B28582         SET MLDTL-OUTLYER-GM-SMOOTH TO TRUE                      07303299
B28582     ELSE                                                         07303399
X28582** RULE - NON-OUTLYER  <INACTIVE DEPARTMENT>                      07303499
X28582     IF WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) = -999.0000000 AND      07303599
X28582        WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) = +999.0000000          07303699
X28582         MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   07303799
X28582         SET MLDTL-NONOUTLYER-CANDIDATE TO TRUE                   07303899
X28582     ELSE                                                         07304099
W28582** RULE - NON-SMOOTH                                              07351099
      **    1.  D/B/V CUMULATIVE COST AMOUNT < ZEROS..                  07352099
      **    2.  D/B/V CUMULATIVE RETAIL AMOUNT < ZEROS..                07353099
      **    3.  D/B/V CUM MKUP PERCENT IS WITHIN TARGET RANGE....       07354099
           IF (PV-CUM-TO-DATE-PURCH-RETAIL LESS THAN ZEROS AND          07355099
               PV-CUM-TO-DATE-PURCH-COST LESS THAN ZEROS)   AND         07356099
              (MLDBV-CURR-CUM-MKUP-PCT                                  07357099
                             >= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) AND   07358099
               MLDBV-CURR-CUM-MKUP-PCT                                  07359099
                             <= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     07359199
                MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT  07359299
W28582         SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   07359499
           ELSE                                                         07359599
      ** RULE - OUTLYER CMU SMOOTH                                      07360016
      **    IN THE CURRENT LEDGER SYSTEM, IF THE CM% IS CALCULATED      07370016
      **    OVER +/- 9999.99, THE APPLICATION WILL RESET THE CM% TO     07380016
      **    100%.  THIS PROCESS WILL CONTINUE, BUT A CUMM COST          07390016
      **    ADJUSTMENT WILL BE CREATED TO SET THE COST COMPONENT        07400016
      **    SUCH THAT THE CM% WILL CALCULATE TO 100%.                   07410016
           IF ( PV-CUM-MKUP-PCT-MAX > +9999.9999999 OR                  07420022
                PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                  07430021
                MOVE +100.0000000 TO MLDTL-TGT-CUM-MKUP-PCT             07440022
                SET MLDTL-OUTLYER-CMU-SMOOTH TO TRUE                    07450023
           ELSE                                                         07460021
      ** RULE - OUTLYER CMU SMOOTH                                      07600016
      **    <DBV CUM MKUP PERCENT IS OUTSIDE TARGET RANGE>              07610016
           IF (MLDBV-CURR-CUM-MKUP-PCT                                  07620016
                             <= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) OR    07630016
               MLDBV-CURR-CUM-MKUP-PCT                                  07640016
                             >= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     07650016
                PERFORM 4100-GET-CMU-OVERRIDE-PCT                       07660016
                SET MLDTL-OUTLYER-CMU-SMOOTH TO TRUE                    07670023
           ELSE                                                         07680016
A28582** RULE - NON-OUTLYER CMU SMOOTH                                  07681099
A28582**    <DBV CUM MKUP PERCENT IS WITHIN TARGET RANGE>               07682099
A28582     IF (MLDBV-CURR-CUM-MKUP-PCT                                  07683099
A28582                       >= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) AND   07684099
A28582         MLDBV-CURR-CUM-MKUP-PCT                                  07685099
A28582                       <= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     07686099
A28582          MOVE MLDBV-CURR-CUM-MKUP-PCT   TO MLDTL-TGT-CUM-MKUP-PCT07688199
A28582          SET MLDTL-NONOUTLYER-CANDIDATE TO TRUE                  07688299
A28582     ELSE                                                         07689099
               MOVE MLDBV-CURR-CUM-MKUP-PCT   TO MLDTL-TGT-CUM-MKUP-PCT 07690023
W28582         SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   07700199
           END-IF.                                                      07711099
                                                                        07720016
      ***************************************************************** 07730023
      * 3600-G-MRGN-CANDIDATE-SELECT.                                   07740023
      ***************************************************************** 07750023
       3600-G-MRGN-CANDIDATE-SELECT.                                    07760023
      ** RULE - OUTLYER GROSS MARGIN SMOOTH                             07806099
      **    1.  AN OUTLYER CANDIDATE IS PRESENT. (3500- ROUTINE)        07807099
      **    2.  D/B/V GROSS MARGIN IS WITHIN TARGET RANGE....           07808099
           IF MLDTL-OUTLYER-CANDIDATES                                  07810069
             IF (PV-CURR-GRO-MRGN-AMT                                   07820076
                           >= WS-TGT-MIN-GRO-MRGN-AMT (TGT-NDX) AND     07830084
                 PV-CURR-GRO-MRGN-AMT                                   07840076
                           <= WS-TGT-MAX-GRO-MRGN-AMT (TGT-NDX) )       07850069
                 SET MLDTL-OUTLYER-GM-SMOOTH TO TRUE                    07860069
             END-IF                                                     07870069
           END-IF.                                                      07890023
                                                                        07900023
B28582***************************************************************** 08027699
B28582* 3800-DBV-UNKNWN-UNKNWN-TEST.                                    08027799
B28582***************************************************************** 08027899
B28582 3800-DBV-UNKNWN-UNKNWN-TEST.                                     08028099
B28582** RULE - NO SMOOTH <DBV CANDIDATE IS UNKNOWN/UNKNOWN>            08029099
B28582**    THE DEPT/BRAND/VENDOR COMBINATION IS UNKNOWN BRAND/UNKNOWN  08029199
B28582**    VENDOR....                                                  08029299
B28582**    (3500- ROUTINE)                                             08029399
B28582                                                                  08029499
B28582     MOVE SPACES            TO MLDTL-OUTLYER-CANDI-IND.           08029599
B28582                                                                  08029699
B28582** SEARCH AT DEPARTMENT LEVEL - D/B/V SPECIFIC                    08029799
B28582     IF MLDBV-DEPT-NBR = PV-SAVE-DEPT-NBR   AND                   08029999
B28582        MLDBV-ML-LWST-ID = PV-SAVE-ML-LWST-ID                     08030099
B28582       MOVE PV-SAVE-OUTLIER-IND  TO MLDTL-OUTLYER-CANDI-IND       08030199
B28582     ELSE                                                         08030299
B28582       MOVE MLDBV-DEPT-NBR   TO PV-SAVE-DEPT-NBR                  08030599
B28582       MOVE MLDBV-ML-LWST-ID TO PV-SAVE-ML-LWST-ID                08030699
B28582       SET OVR-NDX           TO 1                                 08030799
B28582       SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08030899
B28582         AT END  MOVE SPACES TO MLDTL-OUTLYER-CANDI-IND           08030999
B28582                 MOVE SPACES TO PV-SAVE-OUTLIER-IND               08031099
B28582       WHEN  MLDBV-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08031199
B28582         AND MLDBV-ML-LWST-ID = WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX)08031299
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'Y'               08031399
B28582         AND (MLDBV-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08031499
B28582         AND  MLDBV-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08031599
C28582           SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                 08031899
C28582           MOVE '3'        TO PV-SAVE-OUTLIER-IND                 08031999
B28582       END-SEARCH                                                 08032099
           END-IF.                                                      08032199
B28582                                                                  08033099
      ******************************************************************08036050
      * 3900-CALC-GROSS-MARGIN-AMT.                                     08040050
      ******************************************************************08050050
       3900-CALC-GROSS-MARGIN-AMT.                                      08060050
                                                                        08070050
           COMPUTE PV-CURR-GRO-MRGN-AMT                                 08080051
                               = MLDBV-CURR-SLS-RTL-AMT                 08090051
                               - MLDBV-CURR-SLS-COST-AMT.               08100051
                                                                        08110050
      /**************************************************************** 08120027
      * 4100-GET-CMU-OVERRIDE-PCT.                                      08130027
      *   GET THE CMU OVERRIDE PERCENT FOR THE D/B/V COMBINATION BEING  08140027
      * PROCESSED.                                                      08150027
      *                                                                 08160027
      * RULES:                                                          08170027
      ** 1.  GET THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT              08180027
      **     - D/B/V SPECIFIC                                           08190027
      ** 2.  GET THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL 08200027
      ** 3.  GET THE CORPORATE LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL  08210047
      ** 4.  NOT FOUND, ABEND....                                       08220047
      ***************************************************************** 08230027
       4100-GET-CMU-OVERRIDE-PCT.                                       08240027
                                                                        08250027
           MOVE 'N'              TO PS-OVRRDE-FOUND-SW.                 08260027
                                                                        08270027
      ** FIND THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - D/B/V SPECIFIC08280027
           SET OVR-NDX           TO 1.                                  08290027
           SEARCH WS-CMUOVR-TABLE-ELEMENTS                              08300027
               AT END  MOVE 'N'  TO PS-OVRRDE-FOUND-SW                  08310027
             WHEN  MLDBV-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08320027
               AND MLDBV-ML-LWST-ID = WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX)08330029
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08331099
               AND (MLDBV-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08340027
               AND  MLDBV-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08350027
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08351099
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08360028
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08370027
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08380027
           END-SEARCH.                                                  08390027
                                                                        08400027
      ** FIND THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL    08410027
           IF (NOT PS-OVRRDE-FOUND)                                     08420027
             SET OVR-NDX         TO 1                                   08430027
             SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08440027
               AT END  MOVE 'N'  TO PS-OVRRDE-FOUND-SW                  08450047
             WHEN  MLDBV-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08460027
               AND WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX) = 0               08470057
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08471099
               AND (MLDBV-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08480027
               AND  MLDBV-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08490027
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08490199
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08491068
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08500027
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08510027
             END-SEARCH                                                 08520027
           END-IF.                                                      08530027
                                                                        08540027
      ** FIND THE CORPORATE DEFAULT CMU OVERRIDE PERCENT - DEPT LEVEL   08550047
           IF (NOT PS-OVRRDE-FOUND)                                     08560047
             SET OVR-NDX         TO 1                                   08570047
             SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08580047
               AT END                                                   08590047
                 DISPLAY '*** ABEND '                                   08600047
                 DISPLAY '*** 4100-GET-CMU-OVERRIDE-PCT'                08610047
                 DISPLAY '*** NO CMU PERCENT OVRRDE FOUND - '           08620047
                 DISPLAY '    USE THE D/B/V TABLE DEFAULT...'           08630047
                 DISPLAY ' DEPT   - ', MLDBV-DEPT-NBR,                  08640047
                         ' LWST ID - ', MLDBV-ML-LWST-ID,               08641099
                         ' FISCAL - ', MLDBV-FSCL-END-DTE               08650047
                 MOVE '4100-GET-CMU-OVER'  TO PV-PARAGRAPH-NAME         08670047
                 SET LOGICAL-ISSUE-ABEND   TO TRUE                      08680047
                 PERFORM 9999-ABEND                                     08690047
             WHEN  WS-CMO-DEPT-NBR (OVR-NDX) = 0                        08700057
               AND WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX) = 0               08710057
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08711099
               AND (MLDBV-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08720047
               AND  MLDBV-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08730047
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08730199
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08731068
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08740047
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08750047
             END-SEARCH                                                 08760047
           END-IF.                                                      08770047
                                                                        08780047
      /**************************************************************** 08891055
      * 5000 SERIES ROUTINES - SMOOTHING USING DETAIL LEDGER ROW        08891181
      *                        INFORMATION!!                            08891281
      *                                                                 08891381
      * 5000-CALC-SEASON-TO-DATE-PURCH.                                 08892055
      * CALCULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING 08893055
      ***************************************************************** 08894055
       5000-CALC-SEASON-TO-DATE-PURCH.                                  08895055
                                                                        08896055
           ADD MLDTL-CURR-RCPT-RTL-AMT                                  08897055
               MLDTL-SEAS-RCPT-RTL-AMT                                  08898055
                 GIVING PV-SEAS-RCPT-RTL-AMT.                           08899055
                                                                        08899155
           ADD MLDTL-CURR-RCPT-NET-COST-AMT                             08899255
               MLDTL-SEAS-RCPT-NET-COST-AMT                             08899355
                 GIVING PV-SEAS-RCPT-NET-COST-AMT.                      08899455
                                                                        08899555
           ADD MLDTL-CURR-PADJ-RTL-AMT                                  08899655
               MLDTL-SEAS-PADJ-RTL-AMT                                  08899755
                 GIVING PV-SEAS-PADJ-RTL-AMT.                           08899855
                                                                        08899955
           ADD MLDTL-CURR-PADJ-NET-COST-AMT                             08900055
               MLDTL-SEAS-PADJ-NET-COST-AMT                             08900155
                 GIVING PV-SEAS-PADJ-NET-COST-AMT.                      08900255
                                                                        08900355
           ADD MLDTL-CURR-MKUP-RTL-AMT                                  08900455
               MLDTL-SEAS-MKUP-RTL-AMT                                  08900555
                 GIVING PV-SEAS-MKUP-RTL-AMT.                           08900655
                                                                        08900755
           ADD MLDTL-CURR-XFER-IN-RTL-AMT                               08900855
               MLDTL-SEAS-XFER-IN-RTL-AMT                               08900955
                 GIVING PV-SEAS-XFER-IN-RTL-AMT.                        08901055
                                                                        08901155
           ADD MLDTL-CURR-XFER-OUT-RTL-AMT                              08901255
               MLDTL-SEAS-XFER-OUT-RTL-AMT                              08901355
                 GIVING PV-SEAS-XFER-OUT-RTL-AMT.                       08901455
                                                                        08901555
           ADD MLDTL-INV-COST-ADJ1                                      08901693
               MLDTL-SEAS-INV-COST-ADJ1                                 08901793
                 GIVING PV-SEAS-INV-COST-ADJ1.                          08901893
                                                                        08901993
           ADD MLDTL-INV-COST-ADJ2                                      08902093
               MLDTL-SEAS-INV-COST-ADJ2                                 08902193
                 GIVING PV-SEAS-INV-COST-ADJ2.                          08902293
                                                                        08902393
      ******************************************************************08902455
      * 5100-CALC-CUMM-TO-DATE-PURCH.                                   08902555
      * DETERMINE CUMULATIVE RETAIL AND COST                            08902655
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS    08902755
      ******************************************************************08902855
       5100-CALC-CUMM-TO-DATE-PURCH.                                    08902955
                                                                        08903055
B28582     MOVE MLDTL-CUM-TO-DATE-PURCH-RTL                             08903199
B28582                         TO PV-CUM-TO-DATE-PURCH-RETAIL.          08903299
B28582     MOVE MLDTL-CUM-TO-DATE-PURCH-COST                            08903399
B28582                         TO PV-CUM-TO-DATE-PURCH-COST.            08903499
B28582                                                                  08903599
B28582*    COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                        08903699
B28582*                                   (  MLDTL-BEG-SEAS-INV-RTL-AMT 08903799
B28582*                                    + PV-SEAS-XFER-IN-RTL-AMT    08903899
B28582*                                    - PV-SEAS-XFER-OUT-RTL-AMT   08903999
B28582*                                    + PV-SEAS-RCPT-RTL-AMT       08904099
B28582*                                    + PV-SEAS-PADJ-RTL-AMT       08904199
B28582*                                    + PV-SEAS-MKUP-RTL-AMT).     08904299
B28582*                                                                 08904399
B28582*    COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                        08904499
B28582*                                  (  MLDTL-BEG-SEAS-INV-COST-AMT 08904599
B28582*                                   + PV-SEAS-RCPT-NET-COST-AMT   08904699
W28582*                                   + PV-SEAS-PADJ-NET-COST-AMT). 08904799
W28582*                                   + PV-SEAS-INV-COST-ADJ1       08904899
W28582*                                   + PV-SEAS-INV-COST-ADJ2).     08904999
                                                                        08905099
      ***************************************************************** 08906655
      * 5200-CALC-CUMM-MARKUP-PCT.                                      08906755
      * FINAL CALCULATION OF CUMULATIVE MARKUP PERCENT                  08906855
      ***************************************************************** 08906955
       5200-CALC-CUMM-MARKUP-PCT.                                       08907055
                                                                        08907155
           COMPUTE PV-CUM-SEASON-MKUP = (PV-CUM-TO-DATE-PURCH-RETAIL    08907255
                                      - PV-CUM-TO-DATE-PURCH-COST).     08907355
                                                                        08907455
           IF PV-CUM-TO-DATE-PURCH-RETAIL NOT EQUAL 0                   08907555
               COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED                      08907655
                                      = (PV-CUM-SEASON-MKUP * 100)      08907755
                                      / PV-CUM-TO-DATE-PURCH-RETAIL     08907855
G28582     ELSE                                                         08907999
G28582         MOVE 100 TO PV-CUM-MKUP-PCT-MAX                          08908099
           END-IF.                                                      08908155
                                                                        08908255
      /**************************************************************** 08908355
      * 5300-SEARCH-CMU-PCT-TARGET.                                     08908455
      * SEARCH FOR THE LEVEL I SMOOTHING DEPT/CORP TARGET RANGE.....    08908555
      *                                                                 08908655
      * RULES:                                                          08908755
      ** 1.  FIND THE DEPARTMENT LEVEL CMU PERCENT TARGET RANGE......   08908855
      ** 2.  FIND THE CORPORATE LEVEL CMU PERCENT TARGET RANGE......    08908955
      ** 3.  NOT FOUND, ABEND....                                       08909055
      ***************************************************************** 08909155
       5300-SEARCH-CMU-PCT-TARGET.                                      08909255
                                                                        08909355
           MOVE 'N'            TO PS-TARGET-FOUND-SW.                   08909455
                                                                        08909555
      ** FIND THE DEPARTMENT LEVEL CMU PERCENT TARGET RANGE......       08909655
           SET TGT-NDX         TO 1.                                    08909755
           SEARCH WS-TARGET-TABLE-ELEMENTS                              08909855
             AT END                                                     08909955
               MOVE 'N'        TO PS-TARGET-FOUND-SW                    08910055
             WHEN  MLDTL-DEPT-NBR = WS-TGT-DEPT-NBR (TGT-NDX)           08910155
               AND (MLDTL-FSCL-END-DTE >= WS-TGT-EFF-BEG-DTE (TGT-NDX)  08910255
               AND  MLDTL-FSCL-END-DTE <= WS-TGT-EFF-END-DTE (TGT-NDX)) 08910355
               MOVE 'Y'                   TO PS-TARGET-FOUND-SW         08910455
           END-SEARCH.                                                  08910555
                                                                        08910655
      ** FIND THE CORPORATE LEVEL CMU PERCENT TARGET RANGE......        08910755
           IF (NOT PS-TARGET-FOUND)                                     08910855
             SET TGT-NDX         TO 1                                   08910955
             SEARCH WS-TARGET-TABLE-ELEMENTS                            08911055
               AT END                                                   08911155
                 DISPLAY '*** ABEND - '                                 08911255
                 DISPLAY '*** 5300-SEARCH-CMU-PCT-TARGET'               08911391
                 DISPLAY '*** NO CMU PERCENT TARGET FOUND - '           08911455
                 DISPLAY '    USE THE D/B/V TABLE DEFAULT...'           08911555
                 DISPLAY ' DEPT   - ', MLDTL-DEPT-NBR,                  08911655
                         ' FISCAL - ', MLDTL-FSCL-END-DTE               08911755
                         ' DBV KEY - ', MLDTL-KEY-DATA                  08911855
                 MOVE '5300-SEARCH-CMU-P'  TO PV-PARAGRAPH-NAME         08911955
                 SET LOGICAL-ISSUE-ABEND   TO TRUE                      08912055
                 PERFORM 9999-ABEND                                     08912155
               WHEN  WS-TGT-DEPT-NBR (TGT-NDX) = 0                      08912257
                AND (MLDTL-FSCL-END-DTE >= WS-TGT-EFF-BEG-DTE (TGT-NDX) 08912355
                AND  MLDTL-FSCL-END-DTE <= WS-TGT-EFF-END-DTE (TGT-NDX))08912455
                 MOVE 'Y'                   TO PS-TARGET-FOUND-SW       08912555
             END-SEARCH                                                 08912655
           END-IF.                                                      08912755
                                                                        08912855
      ***************************************************************** 08912955
      * 5500-CMU-CANDIDATE-SELECT.                                      08913055
      ***************************************************************** 08913155
       5500-CMU-CANDIDATE-SELECT.                                       08913255
B28582** RULE - OTHER SMOOTH <DBV CANDIDATE IS UNKNOWN/UNKNOWN>         08913399
B28582**    THE DEPT/BRAND/VENDOR COMBINATION IS UNKNOWN BRAND/UNKNOWN  08913499
B28582**    VENDOR....                                                  08913599
B28582     PERFORM 5800-DBV-UNKNWN-UNKNWN-TEST                          08913699
C28582     IF MLDTL-OUTLYER-OTHER-SMOOTH                                08913799
B28582         NEXT SENTENCE                                            08913899
B28582     ELSE                                                         08913999
E28582** RULE - NON-SMOOTH <END INV COST = ZEROS>                       08914099
E28582     IF MLDTL-CURR-END-INV-COST-AMT IS LESS THAN ZEROS OR         08914199
E28582        MLDTL-CURR-END-INV-COST-AMT IS EQUAL TO ZEROS             08914299
E28582         SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                   08914399
E28582****     MOVE MLDTL-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   08914499
E28582****     SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   08914599
           ELSE                                                         08914699
C28582** RULE - NON-SMOOTH <GROSS MARGIN AMOUNT = ZEROS>                08914799
C28582     IF PV-CURR-GRO-MRGN-AMT IS EQUAL TO ZEROS                    08914899
D28582         SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                   08914999
D28582****     MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   08915099
D28582****     SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   08915199
C28582     ELSE                                                         08915299
C28582** RULE - GM $ SMOOTH <GROSS MARGIN RANGE TEST>                   08915399
B28582     IF (PV-CURR-GRO-MRGN-AMT                                     08915499
B28582                     >= WS-TGT-MIN-GRO-MRGN-AMT (TGT-NDX) AND     08915599
B28582         PV-CURR-GRO-MRGN-AMT                                     08915699
B28582                     <= WS-TGT-MAX-GRO-MRGN-AMT (TGT-NDX) )       08915799
B28582         SET MLDTL-OUTLYER-GM-SMOOTH TO TRUE                      08915899
B28582     ELSE                                                         08915999
X28582** RULE - NON-OUTLYER  <INACTIVE DEPARTMENT>                      08916099
X28582     IF WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) = -999.0000000 AND      08916199
X28582        WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) = +999.0000000          08916299
X28582         MOVE MLDBV-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT   08916399
X28582         SET MLDTL-NONOUTLYER-CANDIDATE TO TRUE                   08916499
X28582     ELSE                                                         08916599
W28582** RULE - NON-SMOOTH                                              08916699
      **    1.  D/B/V CUMULATIVE COST AMOUNT < ZEROS..                  08916799
      **    2.  D/B/V CUMULATIVE RETAIL AMOUNT < ZEROS..                08916899
      **    3.  D/B/V CUM MKUP PERCENT IS WITHIN TARGET RANGE....       08916999
           IF (PV-CUM-TO-DATE-PURCH-RETAIL LESS THAN ZEROS AND          08917099
               PV-CUM-TO-DATE-PURCH-COST LESS THAN ZEROS)   AND         08917199
              (MLDTL-CURR-CUM-MKUP-PCT                                  08917299
                             >= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) AND   08917399
               MLDTL-CURR-CUM-MKUP-PCT                                  08917499
                             <= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     08917599
                MOVE MLDTL-CURR-CUM-MKUP-PCT TO MLDTL-TGT-CUM-MKUP-PCT  08917699
W28582         SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   08917799
           ELSE                                                         08917899
      ** RULE - OUTLYER CMU SMOOTH                                      08917998
      **    IN THE CURRENT LEDGER SYSTEM, IF THE CM% IS CALCULATED      08918098
      **    OVER +/- 9999.99, THE APPLICATION WILL RESET THE CM% TO     08918198
      **    100%.  THIS PROCESS WILL CONTINUE, BUT A CUMM COST          08918298
      **    ADJUSTMENT WILL BE CREATED TO SET THE COST COMPONENT        08918398
      **    SUCH THAT THE CM% WILL CALCULATE TO 100%.                   08918498
           IF ( PV-CUM-MKUP-PCT-MAX > +9999.9999999 OR                  08918598
                PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                  08918698
                MOVE +100.0000000 TO MLDTL-TGT-CUM-MKUP-PCT             08918798
                SET MLDTL-OUTLYER-CMU-SMOOTH TO TRUE                    08918898
           ELSE                                                         08918998
      ** RULE - OUTLYER CMU SMOOTH                                      08919098
      **    <DBV CUM MKUP PERCENT IS OUTSIDE TARGET RANGE>              08919198
           IF (MLDTL-CURR-CUM-MKUP-PCT                                  08919298
                             <= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) OR    08919398
               MLDTL-CURR-CUM-MKUP-PCT                                  08919498
                             >= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     08919598
                PERFORM 5700-GET-CMU-OVERRIDE-PCT                       08919698
                SET MLDTL-OUTLYER-CMU-SMOOTH TO TRUE                    08919798
           ELSE                                                         08919898
A28582** RULE - NON-OUTLYER CMU SMOOTH                                  08919999
A28582**    <DBV CUM MKUP PERCENT IS WITHIN TARGET RANGE>               08920099
A28582     IF (MLDTL-CURR-CUM-MKUP-PCT                                  08920199
A28582                       >= WS-TGT-MIN-CUM-MKUP-PCT (TGT-NDX) AND   08920299
A28582         MLDTL-CURR-CUM-MKUP-PCT                                  08920399
A28582                       <= WS-TGT-MAX-CUM-MKUP-PCT (TGT-NDX) )     08920499
A28582          MOVE MLDTL-CURR-CUM-MKUP-PCT   TO MLDTL-TGT-CUM-MKUP-PCT08920599
A28582          SET MLDTL-NONOUTLYER-CANDIDATE TO TRUE                  08920699
A28582     ELSE                                                         08920799
               MOVE MLDTL-CURR-CUM-MKUP-PCT   TO MLDTL-TGT-CUM-MKUP-PCT 08920898
W28582         SET MLDTL-NON-SMOOTH-CANDIDATE TO TRUE                   08920998
           END-IF.                                                      08921099
                                                                        08921198
      ***************************************************************** 08921298
      * 5600-G-MRGN-CANDIDATE-SELECT.                                   08921398
      ***************************************************************** 08921498
       5600-G-MRGN-CANDIDATE-SELECT.                                    08921598
      ** RULE - OUTLYER GROSS MARGIN SMOOTH                             08921698
      **    1.  AN OUTLYER CANDIDATE IS PRESENT. (5500- ROUTINE)        08921799
      **    2.  D/B/V GROSS MARGIN IS WITHIN TARGET RANGE....           08921898
           IF MLDTL-OUTLYER-CANDIDATES                                  08921998
             IF (PV-CURR-GRO-MRGN-AMT                                   08922098
                           >= WS-TGT-MIN-GRO-MRGN-AMT (TGT-NDX) AND     08922198
                 PV-CURR-GRO-MRGN-AMT                                   08922298
                           <= WS-TGT-MAX-GRO-MRGN-AMT (TGT-NDX) )       08922398
                 SET MLDTL-OUTLYER-GM-SMOOTH TO TRUE                    08922498
             END-IF                                                     08922598
           END-IF.                                                      08922698
                                                                        08922798
      ***************************************************************** 08922898
      * 5700-GET-CMU-OVERRIDE-PCT.                                      08922998
      *   GET THE CMU OVERRIDE PERCENT FOR THE D/B/V COMBINATION BEING  08923098
      * PROCESSED.                                                      08923198
      *                                                                 08923298
      * RULES:                                                          08923398
      ** 1.  GET THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT              08923498
      **     - D/B/V SPECIFIC                                           08923598
      ** 2.  GET THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL 08923698
      ** 3.  GET THE CORPORATE LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL  08923798
      ** 4.  NOT FOUND, ABEND....                                       08923898
      ***************************************************************** 08923998
       5700-GET-CMU-OVERRIDE-PCT.                                       08924098
                                                                        08924198
           MOVE 'N'              TO PS-OVRRDE-FOUND-SW.                 08924298
                                                                        08924398
      ** FIND THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - D/B/V SPECIFIC08924498
           SET OVR-NDX           TO 1.                                  08924598
           SEARCH WS-CMUOVR-TABLE-ELEMENTS                              08924698
               AT END  MOVE 'N'  TO PS-OVRRDE-FOUND-SW                  08924798
             WHEN  MLDTL-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08924898
               AND MLDTL-ML-LWST-ID = WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX)08924998
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08925099
               AND (MLDTL-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08925198
               AND  MLDTL-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08925298
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08925399
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08925498
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08925598
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08925698
           END-SEARCH.                                                  08925798
                                                                        08925898
      ** FIND THE DEPARTMENT LEVEL CMU OVERRIDE PERCENT - DEPT LEVEL    08925998
           IF (NOT PS-OVRRDE-FOUND)                                     08926098
             SET OVR-NDX         TO 1                                   08926198
             SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08926298
               AT END  MOVE 'N'  TO PS-OVRRDE-FOUND-SW                  08926398
             WHEN  MLDTL-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08926498
               AND WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX) = 0               08926598
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08926699
               AND (MLDTL-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08926798
               AND  MLDTL-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08926898
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08926999
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08927098
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08927198
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08927298
             END-SEARCH                                                 08927398
           END-IF.                                                      08927498
                                                                        08927598
      ** FIND THE CORPORATE DEFAULT CMU OVERRIDE PERCENT - DEPT LEVEL   08927698
           IF (NOT PS-OVRRDE-FOUND)                                     08927798
             SET OVR-NDX         TO 1                                   08927898
             SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08927998
               AT END                                                   08928098
                 DISPLAY '*** ABEND '                                   08928198
                 DISPLAY '*** 5700-GET-CMU-OVERRIDE-PCT'                08928298
                 DISPLAY '*** NO CMU PERCENT OVRRDE FOUND - '           08928398
                 DISPLAY '    USE THE D/B/V TABLE DEFAULT...'           08928498
                 DISPLAY ' DEPT   - ', MLDTL-DEPT-NBR,                  08928598
                         ' LWST ID - ', MLDTL-ML-LWST-ID,               08928699
                         ' FISCAL - ', MLDTL-FSCL-END-DTE               08928798
                 MOVE '5700-GET-CMU-OVER'  TO PV-PARAGRAPH-NAME         08928898
                 SET LOGICAL-ISSUE-ABEND   TO TRUE                      08928998
                 PERFORM 9999-ABEND                                     08929098
             WHEN  WS-CMO-DEPT-NBR (OVR-NDX) = 0                        08929198
               AND WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX) = 0               08929298
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'N'               08929399
               AND (MLDTL-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08929498
               AND  MLDTL-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08929598
G28582         AND WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX) NOT = ZEROS      08929699
                 MOVE 'Y'        TO PS-OVRRDE-FOUND-SW                  08929798
                 MOVE WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX)               08929898
                                 TO MLDTL-TGT-CUM-MKUP-PCT              08929998
             END-SEARCH                                                 08930098
           END-IF.                                                      08930198
                                                                        08930298
B28582***************************************************************** 08930399
B28582* 5800-DBV-UNKNWN-UNKNWN-TEST.                                    08930499
B28582***************************************************************** 08930599
B28582 5800-DBV-UNKNWN-UNKNWN-TEST.                                     08930699
B28582** RULE - NO SMOOTH <DBV CANDIDATE IS UNKNOWN/UNKNOWN>            08930799
B28582**    THE DEPT/BRAND/VENDOR COMBINATION IS UNKNOWN BRAND/UNKNOWN  08930899
B28582**    VENDOR....                                                  08930999
B28582**    (5500- ROUTINE)                                             08931099
B28582                                                                  08931199
B28582     MOVE SPACES            TO MLDTL-OUTLYER-CANDI-IND.           08931299
B28582                                                                  08931399
B28582** SEARCH AT DEPARTMENT LEVEL - D/B/V SPECIFIC                    08931499
B28582     IF MLDTL-DEPT-NBR = PV-SAVE-DEPT-NBR   AND                   08931599
B28582        MLDTL-ML-LWST-ID = PV-SAVE-ML-LWST-ID                     08931699
B28582       MOVE PV-SAVE-OUTLIER-IND  TO MLDTL-OUTLYER-CANDI-IND       08931799
B28582     ELSE                                                         08931899
B28582       MOVE MLDTL-DEPT-NBR   TO PV-SAVE-DEPT-NBR                  08931999
B28582       MOVE MLDTL-ML-LWST-ID TO PV-SAVE-ML-LWST-ID                08932099
B28582       SET OVR-NDX           TO 1                                 08932199
B28582       SEARCH WS-CMUOVR-TABLE-ELEMENTS                            08932299
B28582         AT END  MOVE SPACES TO MLDTL-OUTLYER-CANDI-IND           08932399
B28582                 MOVE SPACES TO PV-SAVE-OUTLIER-IND               08932499
B28582       WHEN  MLDTL-DEPT-NBR = WS-CMO-DEPT-NBR (OVR-NDX)           08932599
B28582         AND MLDTL-ML-LWST-ID = WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX)08932699
B28582         AND WS-CMO-DBV-UNKNOWN-IND (OVR-NDX) = 'Y'               08932799
B28582         AND (MLDTL-FSCL-END-DTE >= WS-CMO-EFF-BEG-DTE (OVR-NDX)  08932899
B28582         AND  MLDTL-FSCL-END-DTE <= WS-CMO-EFF-END-DTE (OVR-NDX)) 08932999
C28582           SET MLDTL-OUTLYER-OTHER-SMOOTH TO TRUE                 08933099
C28582           MOVE '3'        TO PV-SAVE-OUTLIER-IND                 08933199
B28582       END-SEARCH                                                 08933299
B28582     END-IF.                                                      08933399
B28582                                                                  08934099
      ******************************************************************08935998
      * 5900-CALC-GROSS-MARGIN-AMT.                                     08936098
      ******************************************************************08936198
       5900-CALC-GROSS-MARGIN-AMT.                                      08936298
                                                                        08936398
           COMPUTE PV-CURR-GRO-MRGN-AMT                                 08936498
                               = MLDTL-CURR-SLS-RTL-AMT                 08936598
                               - MLDTL-CURR-SLS-COST-AMT.               08936698
                                                                        08936798
      /**************************************************************** 08936898
      * 7000-READ-DTL-LDGR-FILE. READ THE VENDOR LVL SALES CUM FILE.    08936998
      ***************************************************************** 08937098
       7000-READ-DTL-LDGR-FILE.                                         08937198
                                                                        08937298
           READ DTL-LDGR-FILE-IN NEXT                                   08937398
             INTO MLDTL-COMMON-RECALC-EXTRACT                           08937498
               AT END      MOVE 'Y' TO PS-PROCESS-COMPLETE-SW           08937598
               NOT AT END  ADD 1 TO PV-DTL-LDGR-READ                    08937698
           END-READ.                                                    08937798
                                                                        08937898
      ******************************************************************08937998
      * 7100-WRITE-EXTRACT-REC.                                         08938098
      ******************************************************************08938100
       7100-WRITE-EXTRACT-REC.                                          08939000
                                                                        08940000
           MOVE MLDTL-COMMON-RECALC-EXTRACT                             08950017
                               TO ML070-COMMON-RECALC-EXTRACT.          08960017
                                                                        08970017
********** DISPLAY '*WRITE* '.                                          08971089
********** DISPLAY '  ML070-DEPT-NBR    ', ML070-DEPT-NBR ,             08972089
**********         '  ML070-ML-LWST-ID  ', ML070-ML-LWST-ID,            08973089
**********         '  ML070-LOC-NBR     ', ML070-LOC-NBR                08974089
********** DISPLAY '   ML070-CURR-GRO-MRGN-AMT ',                       08975089
**********        ML070-CURR-GRO-MRGN-AMT,                              08975189
**********       '   ML070-CURR-CMU-MKUP-PCT ',                         08976089
**********        ML070-CURR-CUM-MKUP-PCT,                              08976189
**********       '   ML070-OUTLYER-CANDI-IND ',                         08977089
**********        ML070-OUTLYER-CANDI-IND,                              08977189
**********       '   ML070-TGT-CUM-MKUP-PCT  ',                         08978089
**********        ML070-TGT-CUM-MKUP-PCT                                08978489
********** DISPLAY ' WS-TGT-MIN-CUM-MKUP-PCT ',                         08978589
**********             WS-TGT-MIN-CUM-MKUP-PCT(TGT-NDX),                08978689
**********       ' WS-TGT-MAX-CUM-MKUP-PCT ',                           08978789
**********             WS-TGT-MAX-CUM-MKUP-PCT(TGT-NDX),                08978889
**********       ' MLDBV-CURR-CMU-MKUP-PCT ', MLDBV-CURR-CUM-MKUP-PCT,  08978989
**********       ' MLDBV-CURR-GRO-MRGN-AMT ', MLDBV-CURR-GRO-MRGN-AMT   08979089
**********                                                              08980089
           WRITE LDGR-SMOOTH-CAND-LVL1-REC                              08981032
                               FROM ML070-COMMON-RECALC-EXTRACT.        08990017
                                                                        09000017
           ADD 1               TO PV-WRITE-RECS.                        09010017
                                                                        09020000
      ***************************************************************** 09030005
      * 7200-OPEN-TARGET-TABLE.                                         09040005
      ***************************************************************** 09050005
       7200-OPEN-TARGET-TABLE.                                          09060005
                                                                        09070005
           EXEC SQL                                                     09080005
               OPEN TARGET_CURSOR                                       09090005
           END-EXEC.                                                    09100005
                                                                        09110005
           EVALUATE TRUE                                                09120005
             WHEN SQLCODE IS EQUAL TO ZERO                              09130005
               CONTINUE                                                 09140005
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        09150005
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          09160005
              MOVE '7200-OPEN-TARGET-TABLE' TO PV-PARAGRAPH-NAME        09170005
              MOVE PE-MSG-03           TO PV-OPERATION-ATTEMPTED        09180005
              MOVE 'Y'                 TO PS-TARGET-DONE-SW             09190005
              PERFORM 9500-SQL-ABEND                                    09200005
           END-EVALUATE.                                                09210005
                                                                        09220005
      ***************************************************************** 09230005
      * 7300-LOAD-TARGET-TABLE.                                         09240027
      ***************************************************************** 09250005
       7300-LOAD-TARGET-TABLE.                                          09260027
                                                                        09270005
           EXEC SQL                                                     09280005
               FETCH TARGET_CURSOR                                      09290005
               INTO :MLT00013-DEPT-NBR                                  09300005
                   ,:MLT00013-EFF-BEG-DTE                               09310006
                   ,:MLT00013-MIN-CUM-MKUP-PCT                          09320006
                   ,:MLT00013-MAX-CUM-MKUP-PCT                          09330006
                   ,:MLT00013-EFF-END-DTE                               09340006
                   ,:MLT00013-MIN-GRO-MRGN-AMT                          09350006
                   ,:MLT00013-MAX-GRO-MRGN-AMT                          09360006
           END-EXEC.                                                    09370005
                                                                        09380005
           EVALUATE TRUE                                                09390005
             WHEN SQLCODE IS EQUAL TO ZERO                              09400005
               MOVE MLT00013-DEPT-NBR                                   09410005
                               TO WS-TGT-DEPT-NBR (TGT-NDX)             09420005
               MOVE MLT00013-EFF-BEG-DTE                                09430005
                               TO WS-TGT-EFF-BEG-DTE (TGT-NDX)          09440005
               MOVE MLT00013-MIN-CUM-MKUP-PCT                           09450005
                               TO WS-TGT-MIN-CUM-MKUP-PCT(TGT-NDX)      09460005
               MOVE MLT00013-MAX-CUM-MKUP-PCT                           09470005
                               TO WS-TGT-MAX-CUM-MKUP-PCT(TGT-NDX)      09480005
               MOVE MLT00013-EFF-END-DTE                                09490005
                               TO WS-TGT-EFF-END-DTE(TGT-NDX)           09500005
               MOVE MLT00013-MIN-GRO-MRGN-AMT                           09510005
                               TO WS-TGT-MIN-GRO-MRGN-AMT(TGT-NDX)      09520005
               MOVE MLT00013-MAX-GRO-MRGN-AMT                           09530005
                               TO WS-TGT-MAX-GRO-MRGN-AMT(TGT-NDX)      09540005
               ADD 1           TO PV-WRITE-TARGET                       09550005
             WHEN SQLCODE IS EQUAL TO +100                              09560005
               MOVE 'Y' TO PS-TARGET-DONE-SW                            09570005
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        09580005
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          09590005
               MOVE '7300-LOAD-TARGET-TABLE' TO PV-PARAGRAPH-NAME       09600027
               MOVE PE-MSG-02  TO PV-OPERATION-ATTEMPTED                09610005
               MOVE 'Y'        TO PS-TARGET-DONE-SW                     09620005
               PERFORM 9500-SQL-ABEND                                   09630005
           END-EVALUATE.                                                09640005
                                                                        09650005
      ***************************************************************** 09660005
      * 7400-CLOSE-TARGET-TABLE.                                        09670005
      ***************************************************************** 09680005
       7400-CLOSE-TARGET-TABLE.                                         09690005
                                                                        09700005
           EXEC SQL                                                     09710005
               CLOSE TARGET_CURSOR                                      09720005
           END-EXEC.                                                    09730005
                                                                        09740005
           EVALUATE TRUE                                                09750005
             WHEN SQLCODE IS EQUAL TO ZERO                              09760005
               CONTINUE                                                 09770005
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        09780005
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          09790005
              MOVE '7400-CLOSE-TARGET-TABLE' TO PV-PARAGRAPH-NAME       09800005
              MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED       09810005
              MOVE 'Y'                 TO PS-TARGET-DONE-SW             09820005
              PERFORM 9500-SQL-ABEND                                    09830005
           END-EVALUATE.                                                09840005
                                                                        09850005
      /**************************************************************** 09860005
      * 7500-READ-FCALC-FILE.                                           09870005
      ***************************************************************** 09880005
       7500-READ-FCALC-FILE.                                            09890005
                                                                        09900005
           READ FCALC-CONTROL-IN NEXT                                   09910005
             INTO MLDBV-COMMON-RECALC-EXTRACT                           09920017
               AT END      MOVE 'Y' TO PS-FCALC-COMPLETE-SW             09930071
           END-READ.                                                    09950005
                                                                        09960005
           ADD 1               TO PV-FCALC-READ.                        09961092
                                                                        09962092
      /**************************************************************** 09970005
      * 7600-READ-OVRRDE-FILE.                                          09980005
      ***************************************************************** 09990005
       7600-READ-OVRRDE-FILE.                                           10000005
                                                                        10010005
           READ OVERRIDE-TBL-FILE NEXT                                  10020005
               AT END      MOVE 'Y' TO PS-OVRRDE-COMPLETE-SW            10030049
           END-READ.                                                    10050005
                                                                        10060005
           ADD 1               TO PV-OVRRDE-READ.                       10061092
                                                                        10062092
      ******************************************************************10070010
      * 7700-READ-REQUEST-CNTL.                                         10080010
      ******************************************************************10090010
       7700-READ-REQUEST-CNTL.                                          10100010
                                                                        10110010
           READ REQUEST-CNTL-FILE INTO RQ-CNTL-TYPE-AREA                10120010
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                  10130010
                                                                        10140010
      ***************************************************************** 10150027
      * 7800-LOAD-OVRRDE-TABLE.                                         10160027
      ***************************************************************** 10170027
       7800-LOAD-OVRRDE-TABLE.                                          10180027
                                                                        10190027
           MOVE ML036-DEPT-NBR    TO WS-CMO-DEPT-NBR (OVR-NDX).         10200061
           MOVE ML036-LBL-SEQ-NBR TO WS-CMO-LBL-SEQ-NBR (OVR-NDX).      10210061
           MOVE ML036-ENT-ID      TO WS-CMO-ENT-ID (OVR-NDX).           10220061
           MOVE ML036-ML-LO-MDSE-LVL-ID                                 10230046
                               TO WS-CMO-ML-LO-MDSE-LVL-ID (OVR-NDX).   10240061
           MOVE ML036-EFF-BEG-DTE TO WS-CMO-EFF-BEG-DTE (OVR-NDX).      10250061
           MOVE ML036-OVRID-CUM-MKUP-PCT                                10260046
                               TO WS-CMO-OVRID-CUM-MKUP-PCT (OVR-NDX).  10270061
           MOVE ML036-EFF-END-DTE TO WS-CMO-EFF-END-DTE (OVR-NDX).      10280061
B28582     MOVE ML036-DBV-UNKNWN-IND                                    10281099
B28582                            TO WS-CMO-DBV-UNKNOWN-IND (OVR-NDX).  10282099
                                                                        10290027
           PERFORM 7600-READ-OVRRDE-FILE.                               10300027
                                                                        10310027
      /*****************************************************************10320000
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.        10330000
      ******************************************************************10340000
       9000-END-OF-JOB-TASKS.                                           10350000
                                                                        10360000
           DISPLAY '******************************************'.        10440000
           DISPLAY '** RECORD COUNT SUMMARY **'.                        10450000
                                                                        10460000
           MOVE PV-FCALC-READ           TO PV-DISPLAY-AMOUNT.           10470045
           DISPLAY '* READ - F-CALC RECORDS:         '                  10480045
                                           PV-DISPLAY-AMOUNT.           10490000
           MOVE PV-DTL-LDGR-READ        TO PV-DISPLAY-AMOUNT.           10500045
           DISPLAY '* READ - DETAIL LEDGER RECORDS : '                  10510045
                                           PV-DISPLAY-AMOUNT.           10520045
W28582     MOVE PV-DTL-NO-FCALC         TO PV-DISPLAY-AMOUNT.           10521097
W28582     DISPLAY '* DETAIL LEDGER RECS W/O FCALC : '                  10522099
W28582                                     PV-DISPLAY-AMOUNT.           10523097
           MOVE PV-WRITE-RECS           TO PV-DISPLAY-AMOUNT.           10530000
           DISPLAY '* WRITE - TOTAL RECORDS WRITTEN: '                  10540000
                                           PV-DISPLAY-AMOUNT.           10550000
           DISPLAY '******************************************'.        10560000
                                                                        10570000
           DISPLAY ' '.                                                 10580000
           CLOSE FCALC-CONTROL-IN                                       10630000
                 DTL-LDGR-FILE-IN                                       10640000
                 REQUEST-CNTL-FILE                                      10650000
                 OVERRIDE-TBL-FILE                                      10660002
                 LDGR-SMOOTH-CAND-FILE-OUT.                             10670032
                                                                        10680000
           DISPLAY '******************************************'.        10681086
           DISPLAY '*  END OF ' PC-PROGRAM-NAME                         10682086
           DISPLAY '******************************************'.        10683086
                                                                        10684086
      ******************************************************************10690000
      * 9500-SQL-ABEND.                                                 10700000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               10710000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             10720000
      ******************************************************************10730000
       9500-SQL-ABEND.                                                  10740000
                                                                        10750000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       10760000
                                                                        10770000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         10780000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        10790000
           COPY DPPD004.                                                10800000
                                                                        10810000
           PERFORM 9999-ABEND.                                          10820000
                                                                        10830000
      ******************************************************************10840000
      * 9999-ABEND.                                                     10850000
      *   THIS ROUTINE FORCES AN ABEND                                  10860000
      ******************************************************************10870000
       9999-ABEND.                                                      10880000
                                                                        10890000
           DISPLAY '**  ABEND     **'.                                  10900000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              10910000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            10920000
           DISPLAY '**'                                                 10930000
           DISPLAY '**'                                                 10940000
           DISPLAY '**********************************'                 10950000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           10960000
                                                                        10970000
