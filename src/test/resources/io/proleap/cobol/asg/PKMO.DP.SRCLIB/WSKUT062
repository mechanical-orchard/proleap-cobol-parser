000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    WSKUT062.                                                 
000400 AUTHOR.        GERMAN BANDA.                                             
000500 DATE-WRITTEN.  JUNE 2018.                                                
000510*****************************************************************         
000520*  This program will read in a file of cycle count locations.   *         
000530*  For each record read an SQL update record will be written to *         
000540*  the output file for processing in the job.                   *         
000550*                                                               *         
000594*****************************************************************         
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
000900 FILE-CONTROL.                                                            
001000     SELECT CYCLE-LOC-EXTRACT-FILE    ASSIGN TO INP01.                    
001120     SELECT CYCLE-LOC-QUERY-FILE      ASSIGN TO OUT01.                    
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600                                                                          
001700 FD  CYCLE-LOC-EXTRACT-FILE                                               
001800     RECORDING MODE IS F                                                  
001900     LABEL RECORDS ARE STANDARD                                           
002000     RECORD CONTAINS 35                                                   
002100     BLOCK CONTAINS 0 RECORDS                                             
002200     DATA RECORD IS CYCLE-CNT-LOC-RECORD.                                 
002300 01  CYCLE-CNT-LOC-RECORD             PIC X(35).                          
002400                                                                          
002500 FD  CYCLE-LOC-QUERY-FILE                                                 
002600     RECORDING MODE IS F                                                  
002700     LABEL RECORDS ARE STANDARD                                           
002800     RECORD CONTAINS 80                                                   
002900     BLOCK CONTAINS 0 RECORDS                                             
003000     DATA RECORD IS CYCLE-LOC-QUERY-RECORD.                               
003100 01  CYCLE-LOC-QUERY-RECORD      PIC X(80).                               
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003600                                                   COMP SYNC.             
003700                                                                          
003800 01  PS-PROGRAM-SWITCHES.                                                 
003900     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
004000         88  PS-END-EXTRACT                      VALUE 'Y'.               
004300                                                                          
004400                                                                          
004500 01  PV-PROGRAM-VARIABLES.                                                
004600     05  PV-RETRY-COUNT               PIC S9(04)  VALUE ZERO              
004700                                                  COMP SYNC.              
004800     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
004900     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
005000     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005010     05  PV-TMST                      PIC X(26)   VALUE SPACES.           
005100     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005700     05  PV-DISP-NBR                  PIC Z(8)9.                          
006200     05  PV-AUDIT-DURATION            PIC S9(9) COMP-3 VALUE ZERO.        
006300                                                                          
006400 01  PV-COUNTERS COMP-3.                                                  
006500     05  PV-READ-COUNT                PIC S9(9)    VALUE ZERO.            
006600     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006710     05  PV-COMMIT-COUNT              PIC S9(9)    VALUE ZERO.            
006800                                                                          
006900 01  PC-PROGRAM-CONSTANTS.                                                
007000     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'WSKUT062'.        
007100     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
007200                                                 COMP SYNC.               
007300     05  PC-COMMIT                    PIC S9(04) VALUE 999.               
008510 01  PV-IN-RECORD.                                                        
008520     05  PV-LOCN-GRP-IN               PIC X(09).                          
008520     05  PV-LOCN-ID-IN                PIC X(09).                          
008530     05  FILLER                       PIC X(17).                          
008540 01  PV-OUT-RECORD.                                                       
008550     05  PV-SQL-REC-1.                                                    
008551         10  PV-SQL-STMT-1               PIC X(24)  VALUE                 
008552             'update wmos_efc.locn_grp'.                                  
008553         10  PV-FILLER-1                 PIC X(56)  VALUE SPACE.          
008554     05  PV-SQL-REC-2.                                                    
008590         10  PV-SQL-STMT-2               PIC X(16)  VALUE                 
008591             'set grp_attr = '''.                                         
008555         10  PV-LOCN-GRP-OUT             PIC X(09).                       
008590         10  PV-QUOTE-2                  PIC X(04)  VALUE                 
008591             '''  '.                                                      
008557         10  PV-FILLER-2                 PIC X(51)  VALUE SPACE.          
008558     05  PV-SQL-REC-3.                                                    
008559         10  PV-SQL-STMT-3               PIC X(30)  VALUE                 
008560             'where grp_type = ''99''       '.                            
008570         10  PV-FILLER-3                 PIC X(50)  VALUE SPACE.          
008580     05  PV-SQL-REC-4.                                                    
008590         10  PV-SQL-STMT-4               PIC X(29)  VALUE SPACE.          
008594         10  PV-FILLER-4                 PIC X(51)  VALUE SPACE.          
008595 01  PV-COMMIT-RECORD.                                                    
008597     05  PV-COMMIT-STMT-1                PIC X(03)  VALUE                 
008598                                         'com'.                           
008599     05  PV-COMMIT-STMT-2                PIC X(04)  VALUE                 
008600                                         'mit;'.                          
008601     05  PV-FILLER-5                     PIC X(73)  VALUE SPACE.          
008610/                                                                         
011100 PROCEDURE DIVISION.                                                      
011200                                                                          
011300     PERFORM 1000-INIT.                                                   
011400                                                                          
011500     PERFORM 2000-PROCESS UNTIL PS-END-EXTRACT.                           
011600     IF PV-READ-COUNT  > 0                                                
011700        PERFORM 8200-WRITE-COMMIT-REC                                     
011701     END-IF.                                                              
011702                                                                          
011710     PERFORM 9000-TERMINATE.                                              
011800                                                                          
011900     GOBACK.                                                              
012000                                                                          
012100 1000-INIT.                                                               
012200                                                                          
012300     OPEN INPUT  CYCLE-LOC-EXTRACT-FILE                                   
012400          OUTPUT CYCLE-LOC-QUERY-FILE                                     
012500                                                                          
012700                                                                          
012800     PERFORM 8000-READ-CYCL-LOC-REC.                                      
012900                                                                          
013000 2000-PROCESS.                                                            
013010                                                                          
013100     MOVE PV-LOCN-GRP-IN TO PV-LOCN-GRP-OUT.                              
                                                                                
      * BUG FIX: Not all LOCN_IDs are 9 digits long.  If the input file         
      * contained LOCN_ID 1234567, then without quotes around LOCN_ID           
      * in the outputted SQL statement was updating LOCN_ID 1234567 and         
      * 001234567.  To avoid this, quotes were added to LOCN_ID.                
      * And to handle LOCN_ID values shorter than 9 digits correctly            
      * trailing spaces have to be removed from the inputted LOCN_ID            
      * when building the fourth line of the SQL statement.                     
           MOVE SPACES         TO PV-SQL-STMT-4.                                
           STRING 'and locn_id = ''' DELIMITED BY SIZE                          
                   PV-LOCN-ID-IN DELIMITED BY SPACE                             
                   ''' ;' DELIMITED BY SIZE                                     
                   INTO PV-SQL-STMT-4.                                          
013200                                                                          
013400     PERFORM 8100-WRITE-EXTRACT-REC.                                      
013500                                                                          
013600     PERFORM 8000-READ-CYCL-LOC-REC.                                      
013700/                                                                         
026100 8000-READ-CYCL-LOC-REC.                                                  
026200                                                                          
026300     READ CYCLE-LOC-EXTRACT-FILE INTO PV-IN-RECORD                        
026500     AT END                                                               
026600         SET PS-END-EXTRACT           TO TRUE                             
026700     NOT AT END                                                           
026800         ADD 1                        TO PV-READ-COUNT                    
026900     END-READ.                                                            
027000                                                                          
027100 8100-WRITE-EXTRACT-REC.                                                  
027200                                                                          
027300     ADD 1                         TO PV-WRITE-COUNT                      
027400                                      PV-COMMIT-COUNT.                    
027500     WRITE CYCLE-LOC-QUERY-RECORD FROM PV-SQL-REC-1.                      
027510     WRITE CYCLE-LOC-QUERY-RECORD FROM PV-SQL-REC-2.                      
027520     WRITE CYCLE-LOC-QUERY-RECORD FROM PV-SQL-REC-3.                      
027530     WRITE CYCLE-LOC-QUERY-RECORD FROM PV-SQL-REC-4.                      
027540                                                                          
027550     IF PV-COMMIT-COUNT > PC-COMMIT                                       
027560        PERFORM 8200-WRITE-COMMIT-REC                                     
027561        MOVE +0 TO   PV-COMMIT-COUNT                                      
027570     END-IF.                                                              
027600/                                                                         
027610 8200-WRITE-COMMIT-REC.                                                   
027620                                                                          
027640                                                                          
027650     WRITE CYCLE-LOC-QUERY-RECORD FROM PV-COMMIT-RECORD.                  
027690/                                                                         
027700 9000-TERMINATE.                                                          
027800                                                                          
027900     CLOSE CYCLE-LOC-EXTRACT-FILE                                         
028000           CYCLE-LOC-QUERY-FILE                                           
028100                                                                          
028200     DISPLAY '************************'.                                  
028300     DISPLAY '******* WSKUT062 *******'.                                  
028400     DISPLAY '************************'.                                  
028500     DISPLAY SPACES                                                       
028600                                                                          
028700     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
028800     DISPLAY 'CYCLE COUNT LOCATIONS READ   = ' PV-DISP-NBR                
028900                                                                          
029000     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
029100     DISPLAY 'SELECTED EXTRACTS WRITTEN    = ' PV-DISP-NBR                
029300     DISPLAY SPACES                                                       
029400     DISPLAY '************************'.                                  
029500                                                                          
