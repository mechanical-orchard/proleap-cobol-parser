       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.      XSKUT022.                                       00020067
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030005
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040004
       DATE-WRITTEN.    10/07/2002.                                     00050067
       DATE-COMPILED.                                                   00060004
      ******************************************************************00070007
      **                XS DOMAIN ERROR QUEUE PROCESSOR                 00080059
      **                     FOR THE ITEM TABLES                        00090068
      ******************************************************************00100007
      **THIS PROGRAM MOVES MESSAGES FROM THE ERROR QUEUE TO EITHER THE  00110060
      **     XS DOMAIN ITEM QUEUE FOR REPROCESSING OR TO A FLAT FILE    00120060
      **     FOR MANUAL MANIPULATION.                                   00130063
      ******************************************************************00140007
      *DATE:10/07/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00150067
      *DATE:11/13/2002   MADE BY:JULIE SMITH ADD CORRELID TO PUT        00160069
      ******************************************************************00170014
       ENVIRONMENT DIVISION.                                            00180004
       CONFIGURATION SECTION.                                           00190004
       SOURCE-COMPUTER.        IBM-3090.                                00200004
       OBJECT-COMPUTER.        IBM-3090.                                00210004
       INPUT-OUTPUT SECTION.                                            00220004
       FILE-CONTROL.                                                    00230004
                                                                        00240004
           SELECT CONTROL-CARD-1                                        00250021
                  ASSIGN TO INP01.                                      00260007
           SELECT CONTROL-CARD-2                                        00270021
                  ASSIGN TO INP02.                                      00280021
           SELECT CONTROL-CARD-3                                        00290022
                  ASSIGN TO INP03.                                      00300022
           SELECT CONTROL-CARD-4                                        00310061
                  ASSIGN TO INP04.                                      00320061
           SELECT MESSAGE-LOG                                           00330021
                  ASSIGN TO OUT01.                                      00340023
                                                                        00350004
       DATA DIVISION.                                                   00360004
       FILE SECTION.                                                    00370004
                                                                        00380004
       FD  CONTROL-CARD-1                                               00390021
           RECORDING MODE F                                             00400007
           BLOCK CONTAINS 0 RECORDS                                     00410007
           LABEL RECORDS ARE OMITTED                                    00420007
           DATA RECORD IS CONTROL-CARD-1-RECORD.                        00430021
       01  CONTROL-CARD-1-RECORD           PIC X(080).                  00440022
                                                                        00450004
       FD  CONTROL-CARD-2                                               00460022
           RECORDING MODE F                                             00470022
           BLOCK CONTAINS 0 RECORDS                                     00480022
           LABEL RECORDS ARE OMITTED                                    00490022
           DATA RECORD IS CONTROL-CARD-2-RECORD.                        00500022
       01  CONTROL-CARD-2-RECORD           PIC X(080).                  00510022
                                                                        00520022
       FD  CONTROL-CARD-3                                               00530022
           RECORDING MODE F                                             00540022
           BLOCK CONTAINS 0 RECORDS                                     00550022
           LABEL RECORDS ARE OMITTED                                    00560022
           DATA RECORD IS CONTROL-CARD-3-RECORD.                        00570022
       01  CONTROL-CARD-3-RECORD           PIC X(080).                  00580022
                                                                        00590022
       FD  CONTROL-CARD-4                                               00600061
           RECORDING MODE F                                             00610061
           BLOCK CONTAINS 0 RECORDS                                     00620061
           LABEL RECORDS ARE OMITTED                                    00630061
           DATA RECORD IS CONTROL-CARD-4-RECORD.                        00640061
       01  CONTROL-CARD-4-RECORD           PIC X(080).                  00650061
                                                                        00660061
       FD  MESSAGE-LOG                                                  00670028
           RECORDING MODE F                                             00680028
           BLOCK CONTAINS 0 RECORDS                                     00690028
           LABEL RECORDS ARE OMITTED                                    00700028
           DATA RECORD IS MESSAGE-LOG-RECORD.                           00710028
       01  MESSAGE-LOG-RECORD              PIC X(450).                  00720067
                                                                        00730028
       WORKING-STORAGE SECTION.                                         00740004
      ******************************************************************00750007
      **PROGRAM SWITCHES                                                00760024
      ******************************************************************00770007
       01  PS-PROGRAM-SWITCHES.                                         00780024
           05  PS-PROCESS-INDICATOR-SW   PIC X(01)   VALUE 'Y'.         00790060
               88  PS-PROCESS-COMPLETE               VALUE 'N'.         00800060
               88  PS-PROCESS-CONTINUE               VALUE 'Y'.         00810060
      ******************************************************************00820023
      **CONTROL CARD 1 RECORD LAYOUT - QUEUE MANAGER NAME               00830023
      ******************************************************************00840023
       01  CC-CONTROL-CARD-1-LAYOUT.                                    00850022
           05  CC-QUEUE-MANAGER-NAME      PIC X(48)   VALUE SPACES.     00860022
           05  FILLER                     PIC X(32)   VALUE SPACES.     00870022
      ******************************************************************00880061
      **CONTROL CARD 2 RECORD LAYOUT - QUEUE NAME                       00890064
      ******************************************************************00900061
       01  CC-CONTROL-CARD-2-LAYOUT.                                    00910061
           05  CC-ITEM-QUEUE-NAME         PIC X(48)   VALUE SPACES.     00920061
           05  FILLER                     PIC X(32)   VALUE SPACES.     00930061
      ******************************************************************00940061
      **CONTROL CARD 3 RECORD LAYOUT - ERROR QUEUE NAME                 00950064
      ******************************************************************00960061
       01  CC-CONTROL-CARD-3-LAYOUT.                                    00970061
           05  CC-ITEM-ERROR-QUEUE-NAME   PIC X(48)   VALUE SPACES.     00980061
           05  FILLER                     PIC X(32)   VALUE SPACES.     00990061
      ******************************************************************01000022
      **CONTROL CARD 4 RECORD LAYOUT - WRITE MESSAGES TO FILE OR ITEM Q 01010060
      ******************************************************************01020022
       01  CC-CONTROL-CARD-4-LAYOUT.                                    01030061
           05  CC-MESSAGE-PLACEMENT-IND   PIC X(10).                    01040060
               88  CC-WRITE-TO-ITEM-QUEUE             VALUE 'ITEMQUEUE'.01050060
               88  CC-WRITE-TO-FLAT-FILE              VALUE 'FLATFILE '.01060060
           05  FILLER                     PIC X(70)   VALUE SPACES.     01070060
      ******************************************************************01080061
      **TIMESTAMP VARIABLES                                             01090061
      ******************************************************************01100061
       01  SD-TIMESTAMP                        PIC  X(26)  VALUE SPACES.01110061
       01  FILLER REDEFINES SD-TIMESTAMP.                               01120061
           05  SD-CURR-DTE                     PIC  X(10).              01130061
           05  FILLER                          PIC  X(01).              01140061
           05  SD-HOUR                         PIC  X(02).              01150061
           05  FILLER                          PIC  X(01).              01160061
           05  SD-MINUTE                       PIC  X(02).              01170061
           05  FILLER                          PIC  X(01).              01180061
           05  SD-SECOND                       PIC  X(02).              01190061
           05  FILLER                          PIC  X(01).              01200061
           05  SD-MICROSEC                     PIC  X(06).              01210061
      ******************************************************************01220022
      **PROGRAM CONSTANTS                                               01230022
      ******************************************************************01240022
       01  PC-PROGRAM-CONSTANTS.                                        01250022
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)   VALUE 'XSKUT022'. 01260067
      ******************************************************************01270022
      **MQ WORKING STORAGE                                              01280022
      ******************************************************************01290022
       01  MQV-WORKING-STORAGE.                                         01300036
           05  MQV-MQCONN                 PIC  X(08) VALUE 'CSQBCONN'.  01310036
           05  MQV-MQOPEN                 PIC  X(08) VALUE 'CSQBOPEN'.  01320036
           05  MQV-MQPUT                  PIC  X(08) VALUE 'CSQBPUT'.   01330036
           05  MQV-MQGET                  PIC  X(08) VALUE 'CSQBGET'.   01340036
           05  MQV-MQCMIT                 PIC  X(08) VALUE 'CSQBCOMM'.  01350036
           05  MQV-MQBACK                 PIC  X(08) VALUE 'CSQBBACK'.  01360036
           05  MQV-MQCLOSE                PIC  X(08) VALUE 'CSQBCLOS'.  01370036
           05  MQV-MQDISC                 PIC  X(08) VALUE 'CSQBDISC'.  01380036
           05  MQV-MQCHECK                PIC  X(08) VALUE 'MQR2C'.     01390036
           05  MQV-HCONN                  PIC S9(09) BINARY VALUE 0.    01400036
           05  MQV-COMPLETION-CODE        PIC S9(09) BINARY.            01410036
           05  MQV-REASON                 PIC S9(09) BINARY.            01420036
           05  MQV-OPEN-OPTIONS           PIC S9(09) BINARY.            01430036
           05  MQV-ITEM-HANDLE            PIC S9(09) BINARY VALUE 0.    01440050
           05  MQV-ERROR-HANDLE           PIC S9(09) BINARY VALUE 0.    01450050
           05  MQV-REASON-TEXT            PIC X(30)  VALUE SPACES.      01460063
           05  MQV-MSGBUFFER              PIC X(450) VALUE SPACES.      01470036
           05  MQV-MSG                    PIC X(450) VALUE SPACES.      01480036
           05  MQV-ERRMSGBUF              PIC X(500) VALUE SPACES.      01490036
           05  MQV-MSG-LENGTH             PIC S9(09) BINARY VALUE 450.  01500067
           05  MQV-DATA-LENGTH            PIC S9(09) BINARY.            01510036
      ******************************************************************01520004
      **COPYBOOKS                                                       01530007
      ******************************************************************01540004
      ***INPUT FILE LAYOUT - VSM ITEM MESSAGE COPYBOOK                  01550022
           COPY IMRD008.                                                01560067
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01570007
           COPY DPWS004.                                                01580004
      ***DB2 ABEND COPYBOOK                                             01590007
           COPY DPWS900.                                                01600007
      ***DB2 MESSAGE AREA                                               01610007
           COPY SQLCA2.                                                 01620018
      ***OBJECT-DESCRIPTOR.                                             01630022
       01  MQM-OBJECT-DESCRIPTOR.                                       01640022
           COPY CMQODV.                                                 01650022
      ***MESSAGE-DESCRIPTOR.                                            01660022
       01  MQM-MESSAGE-DESCRIPTOR.                                      01670022
           COPY CMQMDV.                                                 01680022
      ***GET-MESSAGE-OPTIONS.                                           01690022
       01  MQM-GET-MESSAGE-OPTIONS.                                     01700022
           COPY CMQGMOV.                                                01710022
      ***PUT-MESSAGE-OPTIONS.                                           01720022
       01  MQM-PUT-MESSAGE-OPTIONS.                                     01730022
           COPY CMQPMOV.                                                01740022
      ***CONSTANTS.                                                     01750022
       01  MQM-CONSTANTS.                                               01760022
           COPY CMQV.                                                   01770022
      ******************************************************************01780028
      **PROGRAM COUNTERS                                                01790054
      ******************************************************************01800028
       01  PC-PROGRAM-COUNTERS.                                         01810028
           05  PC-MESSAGE-FROM-QUEUE        PIC 9(06) VALUE ZEROS.      01820050
           05  PC-MESSAGES-WRITTEN-TO-FILE  PIC 9(06) VALUE ZEROS.      01830050
           05  PC-MESSAGES-TO-QUEUE         PIC 9(06) VALUE ZEROS.      01840060
      ******************************************************************01850024
      **ERROR HANDLING                                                  01860024
      ******************************************************************01870024
       01  PV-MISC-ABEND-FIELDS.                                        01880007
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01890007
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01900007
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01910007
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01920007
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01930007
           88  ABEND-4001-WRITE-ERROR            VALUE +4001.           01940007
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01950007
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           01960007
           88  ABEND-4004-TABLE-ERROR            VALUE +4004.           01970007
           88  ABEND-4005-OPEN-ERROR             VALUE +4005.           01980007
           88  ABEND-4006-CLOSE-ERROR            VALUE +4006.           01990007
           88  ABEND-4007-SEQUENCE-ERROR         VALUE +4007.           02000007
           88  ABEND-4008-DATA-ERROR             VALUE +4008.           02010007
           88  ABEND-4009-KEY-DATA-ERROR         VALUE +4009.           02020007
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           02050007
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           02060007
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           02070007
      ******************************************************************02080026
      **TABLE INCLUSIONS                                                02090026
      ******************************************************************02100026
      ***DB2 COMMUNICATIONS AREA                                        02110026
           EXEC SQL                                                     02120026
                INCLUDE SQLCA                                           02130026
           END-EXEC.                                                    02140026
                                                                        02150004
       PROCEDURE DIVISION.                                              02160004
      ***************************************************************** 02170004
                                                                        02180004
           PERFORM 1000-INITIALIZATION.                                 02190007
           PERFORM 2000-PROCESS-RECORDS                                 02200024
                   UNTIL PS-PROCESS-COMPLETE.                           02210053
           PERFORM 9999-WRAPUP.                                         02220007
           GOBACK.                                                      02230004
                                                                        02240004
       EJECT.                                                           02250007
      ******************************************************************02260004
      **INITIALIZATION AREA - SETUP QUEUES FOR PROCESSING               02270054
      ******************************************************************02280004
       1000-INITIALIZATION.                                             02290007
                                                                        02300004
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             02310007
                                                                        02320007
           OPEN INPUT  CONTROL-CARD-1                                   02330022
                       CONTROL-CARD-2                                   02340022
                       CONTROL-CARD-3                                   02350022
                       CONTROL-CARD-4                                   02360061
                OUTPUT MESSAGE-LOG.                                     02370022
                                                                        02380007
           PERFORM 9999-READ-CONTROL-CARD-1.                            02390022
           PERFORM 9999-READ-CONTROL-CARD-2.                            02400022
           PERFORM 9999-READ-CONTROL-CARD-3.                            02410022
           PERFORM 9999-READ-CONTROL-CARD-4.                            02420061
                                                                        02430022
           IF  CC-QUEUE-MANAGER-NAME    =  SPACES                       02440022
            OR CC-ITEM-QUEUE-NAME       =  SPACES                       02450022
            OR CC-ITEM-ERROR-QUEUE-NAME =  SPACES                       02460022
               DISPLAY 'EMPTY CONTROL CARD'                             02470053
               DISPLAY 'QUEUE MANAGER NAME    ' CC-QUEUE-MANAGER-NAME   02480053
               DISPLAY 'ITEM QUEUE NAME       ' CC-ITEM-QUEUE-NAME      02490022
               DISPLAY 'ITEM ERROR QUEUE NAME ' CC-ITEM-ERROR-QUEUE-NAME02500022
               PERFORM 9999-MQ-ABEND                                    02510036
           END-IF.                                                      02520022
                                                                        02530022
           PERFORM 1100-SELECT-DB2-TIME.                                02540061
           PERFORM 1200-CONNECT-TO-QMGR.                                02550061
           PERFORM 1300-OPEN-ITEM-QUEUE.                                02560061
           PERFORM 1400-OPEN-ITEM-ERROR-QUEUE.                          02570061
                                                                        02580024
           PERFORM 9999-RETRIEVE-MESSAGE.                               02590036
                                                                        02600022
       EJECT.                                                           02610007
      ******************************************************************02620061
      **RETRIEVE TIME FROM DB2                                          02630061
      ******************************************************************02640061
       1100-SELECT-DB2-TIME.                                            02650061
                                                                        02660061
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02670061
                                                                        02680061
           EXEC SQL                                                     02690061
                SELECT CURRENT TIMESTAMP                                02700061
                INTO  :SD-TIMESTAMP                                     02710061
                FROM   SYSIBM.SYSDUMMY1                                 02720061
           END-EXEC.                                                    02730061
                                                                        02740061
           IF  SQLCODE NOT EQUAL ZERO                                   02750061
               PERFORM 9999-ABEND-ROUTINE                               02760061
           END-IF.                                                      02770061
                                                                        02780061
       EJECT.                                                           02790061
      ******************************************************************02800007
      **CONNECT TO THE QUEUE MANAGER                                    02810022
      ******************************************************************02820007
       1200-CONNECT-TO-QMGR.                                            02830061
                                                                        02840007
           MOVE '1200-CONNECT-TO-QMGR'       TO PV-PARAGRAPH-NAME.      02850061
                                                                        02860022
           CALL MQV-MQCONN USING CC-QUEUE-MANAGER-NAME                  02870036
                                 MQV-HCONN                              02880053
                                 MQV-COMPLETION-CODE                    02890053
                                 MQV-REASON.                            02900053
                                                                        02910022
           EVALUATE TRUE                                                02920022
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        02930036
                   DISPLAY 'SUCCESSFULLY CONNECTED TO: '                02940054
                                                  CC-QUEUE-MANAGER-NAME 02950053
              WHEN OTHER                                                02960022
                   CALL MQV-MQCHECK USING MQV-REASON                    02970036
                                          MQV-REASON-TEXT               02980036
                   DISPLAY 'QUEUE CONNECTING TO ' CC-QUEUE-MANAGER-NAME 02990023
                   MOVE 'UNABLE TO CONNECT'  TO PV-OPERATION-MSG-1      03000053
                   PERFORM 9999-MQ-ABEND                                03010036
           END-EVALUATE.                                                03020022
                                                                        03030022
       EJECT.                                                           03040022
      ******************************************************************03050022
      **OPEN THE ITEM QUEUE                                             03060050
      ******************************************************************03070022
       1300-OPEN-ITEM-QUEUE.                                            03080061
                                                                        03090022
           MOVE '1300-OPEN-ITEM-QUEUE' TO PV-PARAGRAPH-NAME.            03100061
                                                                        03110022
           MOVE CC-ITEM-QUEUE-NAME       TO MQOD-OBJECTNAME             03120022
           MOVE CC-QUEUE-MANAGER-NAME    TO MQOD-OBJECTQMGRNAME.        03130022
                                                                        03140022
           COMPUTE MQV-OPEN-OPTIONS      = MQOO-INPUT-SHARED            03150036
                                         + MQOO-OUTPUT                  03160022
                                         + MQOO-SET-IDENTITY-CONTEXT    03170022
                                         + MQOO-FAIL-IF-QUIESCING.      03180022
                                                                        03190022
           CALL MQV-MQOPEN USING MQV-HCONN                              03200036
                                 MQM-OBJECT-DESCRIPTOR                  03210036
                                 MQV-OPEN-OPTIONS                       03220036
                                 MQV-ITEM-HANDLE                        03230050
                                 MQV-COMPLETION-CODE                    03240036
                                 MQV-REASON.                            03250036
                                                                        03260022
           EVALUATE TRUE                                                03270022
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        03280036
                   DISPLAY 'SUCCESSFULLY OPENED QUEUE: '                03290054
                                                      CC-ITEM-QUEUE-NAME03300053
              WHEN OTHER                                                03310022
                   CALL MQV-MQCHECK USING MQV-REASON                    03320036
                                         MQV-REASON-TEXT                03330036
                   DISPLAY 'CANNOT OPEN QUEUE ' CC-QUEUE-MANAGER-NAME   03340053
                   MOVE 'CANNOT OPEN QUEUE'  TO PV-OPERATION-MSG-1      03350023
                   PERFORM 9999-DISCONNECT-QMGR                         03360050
                   PERFORM 9999-MQ-ABEND                                03370036
           END-EVALUATE.                                                03380022
                                                                        03390022
       EJECT.                                                           03400022
      ******************************************************************03410050
      **OPEN THE ITEM ERROR QUEUE                                       03420050
      ******************************************************************03430050
       1400-OPEN-ITEM-ERROR-QUEUE.                                      03440061
                                                                        03450050
           MOVE '1400-OPEN-ITEM-ERROR-QUEUE' TO PV-PARAGRAPH-NAME.      03460061
                                                                        03470050
           MOVE CC-ITEM-ERROR-QUEUE-NAME     TO MQOD-OBJECTNAME         03480050
           MOVE CC-QUEUE-MANAGER-NAME        TO MQOD-OBJECTQMGRNAME.    03490050
                                                                        03500050
           COMPUTE MQV-OPEN-OPTIONS      = MQOO-INPUT-SHARED            03510050
                                         + MQOO-OUTPUT                  03520050
                                         + MQOO-SET-IDENTITY-CONTEXT    03530050
                                         + MQOO-FAIL-IF-QUIESCING.      03540050
                                                                        03550050
           CALL MQV-MQOPEN USING MQV-HCONN                              03560050
                                 MQM-OBJECT-DESCRIPTOR                  03570050
                                 MQV-OPEN-OPTIONS                       03580050
                                 MQV-ERROR-HANDLE                       03590050
                                 MQV-COMPLETION-CODE                    03600050
                                 MQV-REASON.                            03610050
                                                                        03620050
           EVALUATE TRUE                                                03630050
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        03640050
                   DISPLAY 'SUCCESSFULLY OPENED QUEUE: '                03650054
                                                CC-ITEM-ERROR-QUEUE-NAME03660053
              WHEN OTHER                                                03670050
                   CALL MQV-MQCHECK USING MQV-REASON                    03680050
                                          MQV-REASON-TEXT               03690050
                   MOVE 'CANNOT OPEN QUEUE'  TO PV-OPERATION-MSG-1      03700050
                   PERFORM 9999-DISCONNECT-QMGR                         03710050
                   PERFORM 9999-MQ-ABEND                                03720050
           END-EVALUATE.                                                03730050
                                                                        03740050
       EJECT.                                                           03750050
      ******************************************************************03760022
      **READ ITEM MESSAGE FROM ERROR QUEUE AND PLACE ON ITEM QUEUE OR   03770061
      **     WRITE TO A FLAT FILE.                                      03780061
      ******************************************************************03790022
       2000-PROCESS-RECORDS.                                            03800022
                                                                        03810022
           MOVE '2000-PROCESS-RECORDS'         TO PV-PARAGRAPH-NAME.    03820050
                                                                        03830007
      *INITIALIZE VARIABLES FOR EACH MESSAGE RETRIEVED                  03840050
           INITIALIZE MESSAGE-LOG-RECORD.                               03850050
                                                                        03860024
           EVALUATE TRUE                                                03870061
              WHEN CC-WRITE-TO-FLAT-FILE                                03880061
                   PERFORM 2300-COMMIT-MESSAGE                          03890061
                   PERFORM 2400-BACKUP-MESSAGE                          03900061
              WHEN CC-WRITE-TO-ITEM-QUEUE                               03910061
                   PERFORM 2400-BACKUP-MESSAGE                          03920061
                   PERFORM 2500-PUT-ON-ITEM-QUEUE                       03930061
                   PERFORM 2300-COMMIT-MESSAGE                          03940061
              WHEN OTHER                                                03950062
                   DISPLAY 'INVALID CONTROL CARD #4'                    03960062
                   DISPLAY 'CONTROL CARD CONTENTS: '                    03970062
                                              CC-CONTROL-CARD-4-LAYOUT  03980062
                   PERFORM 9999-ABEND-ROUTINE                           03990062
           END-EVALUATE.                                                04000061
                                                                        04010024
           IF  PS-PROCESS-CONTINUE                                      04020063
               PERFORM 9999-RETRIEVE-MESSAGE                            04030063
           END-IF.                                                      04040063
                                                                        04050031
       EJECT.                                                           04060007
      ******************************************************************04070024
      **REMOVES MESSAGE FROM QUEUE                                      04080053
      ******************************************************************04090024
       2300-COMMIT-MESSAGE.                                             04100050
                                                                        04110024
           MOVE '2300-COMMIT-MESSAGE'          TO PV-PARAGRAPH-NAME.    04120053
                                                                        04130024
           CALL MQV-MQCMIT USING MQV-HCONN                              04140036
                                 MQV-COMPLETION-CODE                    04150053
                                 MQV-REASON.                            04160053
                                                                        04170024
           EVALUATE TRUE                                                04180024
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        04190036
                   CONTINUE                                             04200024
              WHEN OTHER                                                04210024
                   CALL MQV-MQCHECK USING MQV-REASON                    04220036
                                          MQV-REASON-TEXT               04230053
                   MOVE 'ERROR COMMITING DATA' TO PV-OPERATION-MSG-1    04240024
                   PERFORM 9999-BACK-OUT-MQV-CHANGES                    04250036
                   PERFORM 9999-DISCONNECT-QMGR                         04260052
                   PERFORM 9999-MQ-ABEND                                04270036
           END-EVALUATE.                                                04280024
                                                                        04290024
       EJECT.                                                           04300024
      ******************************************************************04310024
      **WRITES MESSAGE TO A LOG IN CASE RECOVERY IS NEEDED              04320024
      ******************************************************************04330024
       2400-BACKUP-MESSAGE.                                             04340050
                                                                        04350024
           MOVE '2400-BACKUP-MESSAGE' TO PV-PARAGRAPH-NAME.             04360050
                                                                        04370024
           MOVE MQV-MSGBUFFER TO MESSAGE-LOG-RECORD.                    04380036
           WRITE MESSAGE-LOG-RECORD.                                    04390025
                                                                        04400024
           ADD +1                     TO PC-MESSAGES-WRITTEN-TO-FILE.   04410062
                                                                        04420062
       EJECT.                                                           04430024
      ******************************************************************04440053
      **PUTS MESSAGE THAT HAD ERROR ON ERROR QUEUE                      04450053
      ******************************************************************04460053
       2500-PUT-ON-ITEM-QUEUE.                                          04470061
                                                                        04480053
           MOVE '2500-PUT-ON-ITEM-QUEUE' TO PV-PARAGRAPH-NAME.          04490061
                                                                        04500053
           MOVE CC-ITEM-QUEUE-NAME       TO MQOD-OBJECTNAME             04510061
           MOVE CC-QUEUE-MANAGER-NAME    TO MQOD-OBJECTQMGRNAME.        04520061
                                                                        04530053
           MOVE IMRD008-RECORD           TO MQV-ERRMSGBUF.              04540061
                                                                        04550053
           MOVE MQMI-NONE                TO MQMD-MSGID.                 04560061
111302     MOVE MQCI-NONE                TO MQMD-CORRELID.              04570069
           MOVE MQFMT-STRING             TO MQMD-FORMAT.                04580061
           COMPUTE MQPMO-OPTIONS  =  MQPMO-SYNCPOINT.                   04590053
                                                                        04600053
                                                                        04610053
           CALL MQV-MQPUT USING MQV-HCONN                               04620053
                                MQV-ITEM-HANDLE                         04630061
                                MQM-MESSAGE-DESCRIPTOR                  04640053
                                MQM-PUT-MESSAGE-OPTIONS                 04650053
                                MQV-MSG-LENGTH                          04660053
                                MQV-ERRMSGBUF                           04670053
                                MQV-COMPLETION-CODE                     04680053
                                MQV-REASON.                             04690053
                                                                        04700053
           EVALUATE TRUE                                                04710053
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        04720053
                   ADD +1                 TO PC-MESSAGES-TO-QUEUE       04730061
              WHEN OTHER                                                04740053
                   DISPLAY 'UNABLE TO WRITE TO ITEM ERROR QUEUE'        04750053
                   CALL MQV-MQCHECK USING MQV-REASON                    04760053
                                          MQV-REASON-TEXT               04770053
                   MOVE 'MQBACK ERROR'      TO PV-OPERATION-MSG-1       04780053
                   PERFORM 9999-DISCONNECT-QMGR                         04790053
                   PERFORM 9999-MQ-ABEND                                04800053
           END-EVALUATE.                                                04810053
                                                                        04820053
       EJECT.                                                           04830053
      ******************************************************************04840028
      **RETRIEVES NEXT MESSAGE FROM THE ERROR QUEUE                     04850061
      ******************************************************************04860028
       9999-RETRIEVE-MESSAGE.                                           04870028
                                                                        04880028
           MOVE '9999-RETRIEVE-MESSAGE' TO PV-PARAGRAPH-NAME.           04890028
                                                                        04900028
           INITIALIZE MQV-MSGBUFFER                                     04910036
                      MQV-ERRMSGBUF                                     04920036
                      MQV-MSG.                                          04930036
                                                                        04940024
           MOVE MQMI-NONE            TO MQMD-MSGID.                     04950024
           MOVE MQFMT-STRING         TO MQMD-FORMAT.                    04960024
           MOVE 60                   TO MQGMO-WAITINTERVAL.             04970065
                                                                        04980057
           COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                   04990057
                                    MQGMO-WAIT.                         05000057
                                                                        05010024
           COMPUTE MQGMO-OPTIONS  =  MQGMO-SYNCPOINT +                  05020024
                                     MQGMO-CONVERT   +                  05030024
                                     MQGMO-ACCEPT-TRUNCATED-MSG +       05040024
                                     MQGMO-WAIT.                        05050024
                                                                        05060024
           CALL MQV-MQGET USING MQV-HCONN                               05070036
                                MQV-ERROR-HANDLE                        05080061
                                MQM-MESSAGE-DESCRIPTOR                  05090036
                                MQM-GET-MESSAGE-OPTIONS                 05100036
                                MQV-MSG-LENGTH                          05110036
                                MQV-MSGBUFFER                           05120036
                                MQV-DATA-LENGTH                         05130036
                                MQV-COMPLETION-CODE                     05140036
                                MQV-REASON.                             05150036
                                                                        05160024
           IF  MQV-COMPLETION-CODE = MQCC-OK                            05170036
               ADD +1             TO PC-MESSAGE-FROM-QUEUE              05180050
               INITIALIZE IMRD008-RECORD                                05190024
               MOVE MQV-MSGBUFFER TO IMRD008-RECORD                     05200036
           ELSE                                                         05210024
               EVALUATE TRUE                                            05220024
                  WHEN MQV-REASON = MQRC-NO-MSG-AVAILABLE               05230036
                       SET PS-PROCESS-COMPLETE  TO TRUE                 05240061
                  WHEN OTHER                                            05250024
                       CALL MQV-MQCHECK USING MQV-REASON                05260036
                                              MQV-REASON-TEXT           05270057
                       MOVE 'ERROR RETRIEVING MESSAGE'                  05280024
                                             TO PV-OPERATION-MSG-1      05290024
                       PERFORM 9999-BACK-OUT-MQV-CHANGES                05300036
                       PERFORM 9999-DISCONNECT-QMGR                     05310050
                       PERFORM 9999-MQ-ABEND                            05320036
               END-EVALUATE                                             05330024
           END-IF.                                                      05340024
                                                                        05350024
       EJECT.                                                           05360024
      ***************************************************************** 05370050
      **ROLLBACK MQ CHANGES                                             05380050
      ***************************************************************** 05390050
       9999-BACK-OUT-MQV-CHANGES.                                       05400050
                                                                        05410050
           CALL MQV-MQBACK USING MQV-HCONN                              05420050
                                 MQV-COMPLETION-CODE                    05430050
                                 MQV-REASON.                            05440050
                                                                        05450050
           EVALUATE TRUE                                                05460050
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        05470050
                   CONTINUE                                             05480050
              WHEN OTHER                                                05490050
                   CALL MQV-MQCHECK USING MQV-REASON                    05500050
                                          MQV-REASON-TEXT               05510050
                   MOVE 'MQBACK ERROR'  TO PV-OPERATION-MSG-1           05520050
                   PERFORM 9999-DISCONNECT-QMGR                         05530050
                   PERFORM 9999-MQ-ABEND                                05540050
           END-EVALUATE.                                                05550050
                                                                        05560050
       EJECT.                                                           05570050
      ***************************************************************** 05580050
      **DISCONNECT FROM THE QUEUE MANAGER                               05590050
      ***************************************************************** 05600050
       9999-DISCONNECT-QMGR.                                            05610050
                                                                        05620050
           CALL MQV-MQDISC USING MQV-HCONN                              05630050
                                 MQV-COMPLETION-CODE                    05640050
                                 MQV-REASON.                            05650052
                                                                        05660050
           EVALUATE TRUE                                                05670050
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        05680050
                   CONTINUE                                             05690050
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                05700050
                   CALL MQV-MQCHECK USING MQV-REASON                    05710050
                                          MQV-REASON-TEXT               05720050
                   MOVE 'MQDISC ERROR'       TO PV-OPERATION-MSG-1      05730050
                   PERFORM 9999-MQ-ABEND                                05740050
           END-EVALUATE.                                                05750050
                                                                        05760050
       EJECT.                                                           05770050
      ***************************************************************** 05780050
      **CLOSE XS ITEM AND XS ITEM ERROR QUEUE                           05790050
      ***************************************************************** 05800050
       9999-CLOSE-QUEUES.                                               05810050
                                                                        05820050
           CALL MQV-MQCLOSE USING MQV-HCONN                             05830050
                                  MQV-ITEM-HANDLE                       05840050
                                  MQCO-NONE                             05850050
                                  MQV-COMPLETION-CODE                   05860050
                                  MQV-REASON.                           05870050
                                                                        05880050
           EVALUATE TRUE                                                05890050
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        05900050
                   CONTINUE                                             05910050
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                05920050
                   CALL MQV-MQCHECK USING MQV-REASON                    05930050
                                          MQV-REASON-TEXT               05940050
                   MOVE 'CANNOT CLOSE ITEM QUEUE' TO PV-OPERATION-MSG-1 05950050
                   PERFORM 9999-MQ-ABEND                                05960050
           END-EVALUATE.                                                05970050
                                                                        05980050
           CALL MQV-MQCLOSE USING MQV-HCONN                             05990050
                                  MQV-ERROR-HANDLE                      06000050
                                  MQCO-NONE                             06010050
                                  MQV-COMPLETION-CODE                   06020050
                                  MQV-REASON.                           06030050
                                                                        06040050
           EVALUATE TRUE                                                06050050
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        06060050
                   CONTINUE                                             06070050
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                06080050
                   CALL MQV-MQCHECK USING MQV-REASON                    06090050
                                          MQV-REASON-TEXT               06100050
                   MOVE 'CANNOT CLOSE ERROR QUEUE' TO PV-OPERATION-MSG-106110050
                   PERFORM 9999-MQ-ABEND                                06120050
           END-EVALUATE.                                                06130050
                                                                        06140050
       EJECT.                                                           06150050
      ***************************************************************** 06160023
      **READ CONTROL CARD 1 - MQ CONNECTION MANAGER                     06170061
      ***************************************************************** 06180023
       9999-READ-CONTROL-CARD-1.                                        06190023
                                                                        06200023
           READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1-LAYOUT.           06210022
                                                                        06220007
       EJECT.                                                           06230007
      ***************************************************************** 06240022
      **READ CONTROL CARD 2 - ITEM QUEUE NAME                           06250061
      ***************************************************************** 06260022
       9999-READ-CONTROL-CARD-2.                                        06270022
                                                                        06280022
           READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2-LAYOUT.           06290022
                                                                        06300022
       EJECT.                                                           06310022
      ***************************************************************** 06320022
      **READ CONTROL CARD 3 - ERROR QUEUE NAME                          06330061
      ***************************************************************** 06340022
       9999-READ-CONTROL-CARD-3.                                        06350022
                                                                        06360022
           READ CONTROL-CARD-3 INTO CC-CONTROL-CARD-3-LAYOUT.           06370022
                                                                        06380022
       EJECT.                                                           06390022
      ***************************************************************** 06400061
      **READ CONTROL CARD 4 - INDICATES SEND MESSAGES TO FILE OR QUEUE  06410061
      ***************************************************************** 06420061
       9999-READ-CONTROL-CARD-4.                                        06430061
                                                                        06440061
           READ CONTROL-CARD-4 INTO CC-CONTROL-CARD-4-LAYOUT.           06450061
                                                                        06460061
       EJECT.                                                           06470061
      ****************************************************************  06480007
      *END OF PROGRAM ROUTINE                                           06490007
      ****************************************************************  06500007
       9999-WRAPUP.                                                     06510007
                                                                        06520007
           PERFORM 9999-CLOSE-QUEUES.                                   06530052
           PERFORM 9999-DISCONNECT-QMGR.                                06540050
                                                                        06550050
           CLOSE CONTROL-CARD-1                                         06560028
                 CONTROL-CARD-2                                         06570028
                 CONTROL-CARD-3                                         06580028
                 CONTROL-CARD-4                                         06590061
                 MESSAGE-LOG.                                           06600028
                                                                        06610028
           PERFORM 9999-DISPLAY-STATISTICS.                             06620028
                                                                        06630007
       EJECT.                                                           06640007
      ****************************************************************  06650028
      *DISPLAY PROGRAM STATISTICS FOR THIS RUN                          06660028
      ****************************************************************  06670028
       9999-DISPLAY-STATISTICS.                                         06680028
                                                                        06690028
           DISPLAY '*********************************************'.     06700028
           DISPLAY 'PROGRAM NAME  : ' PC-CURRENT-PROGRAM-NAME.          06710061
           DISPLAY 'DATE/TIMESTAMP: ' SD-TIMESTAMP.                     06720061
           DISPLAY '*********************************************'.     06730028
           DISPLAY 'MESSAGES FROM ITEM ERROR QUEUE: '                   06740061
                                            PC-MESSAGE-FROM-QUEUE.      06750061
           DISPLAY 'MESSAGES WRITTEN TO FLAT FILE: '                    06760061
                                            PC-MESSAGES-WRITTEN-TO-FILE.06770061
           DISPLAY 'MESSAGES WRITTEN TO ITEM QUEUE: '                   06780061
                                            PC-MESSAGES-TO-QUEUE.       06790061
           DISPLAY '*********************************************'.     06800050
                                                                        06810028
       EJECT.                                                           06820028
      ******************************************************************06830007
      **MQ ABEND ROUTINE                                                06840022
      ******************************************************************06850007
       9999-MQ-ABEND.                                                   06860036
                                                                        06870007
           DISPLAY '**** ABEND *************************************'.  06880022
           DISPLAY '** MQSERIES ERROR **'.                              06890022
           DISPLAY '** PROGRAM:    ' PC-CURRENT-PROGRAM-NAME.           06900022
           DISPLAY '** ABEND CODE: ' ABEND-CODE.                        06910022
           DISPLAY '** PARAGRAPH:  ' PV-PARAGRAPH-NAME.                 06920022
           DISPLAY '** MESSAGE:    ' PV-OPERATION-MSG-1.                06930022
           DISPLAY '** COMP CODE:  ' MQV-COMPLETION-CODE.               06940036
           DISPLAY '** RSN CODE:   ' MQV-REASON.                        06950036
           DISPLAY '** RSN TEXT:   ' MQV-REASON-TEXT.                   06960036
           DISPLAY '************************************************'.  06970022
           MOVE +4013 TO ABEND-CODE.                                    06980022
           CALL 'ILBOABN0' USING ABEND-CODE.                            06990022
                                                                        07000022
       EJECT.                                                           07010022
      ******************************************************************07020061
      **SQL ABEND ROUTINE                                               07030061
      ******************************************************************07040061
       9999-ABEND-ROUTINE.                                              07050061
                                                                        07060061
           DISPLAY '** ABEND     **'.                                   07070061
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07080061
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07090061
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07100061
      ******************************************************************07110061
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07120061
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07130061
      ******************************************************************07140061
           COPY DPPD004.                                                07150071
           SET ABEND-4013-DB2-ERROR TO TRUE.                            07160061
           CALL 'ILBOABN0' USING ABEND-CODE.                            07170061
                                                                        07180061
       EJECT.                                                           07190064
