       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              MLKUT421.                                       
       AUTHOR.                  BARBARA HULL.                                   
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/14/98.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *            SUB-CLASS MERCHANDISE LEDGER EXTRACT.                        
      *                                                                         
      * THIS PROGRAM WILL EXTRACT FROM THE MONTH-TO-DATE SUB-CLASS              
      * LEDGER TABLE, THE DATA TO BE REPORTED ON THE SUB-CLASS LEVEL            
      * LEDGER REPORT.                                                          
      *                                                                         
      * IT IS DRIVEN BY A CURSOR OF DEPT/SUB-CLASS THAT WERE ACTIVE ON          
      * MBS FOR MERCHANDISE LEDGER AS OF THE END OF THE OPEN FISCAL             
      * MONTH.                                                                  
      *                                                                         
      * FOR EACH DEPT/SCL, THE PROGRAM  EXTRACTS THE BEGINNING                  
      * INVENTORY AT COST AND RETAIL FOR THE OPEN FISCAL YEAR, AND              
      * ALL COST AND RETAIL AMOUNTS FOR EACH MONTH FROM THE                     
      * BEGINNING OF THE OPEN FISCAL YEAR THROUGH THE POSTING FISCAL            
      * MONTH.                                                                  
      *                                                                         
      * JBF 07/07/00 -CHANGED THE PROGRAM TO USE THE POSTING MONTH              
      *               INSTEAD OF THE OPEN FISCAL MONTH WHEN THE                 
      *               FISCAL YEARS ARE NOT THE SAME.                            
      *                                                                         
W26603******************************************************************        
W26603* CHG ID   DATE        DESCRIPTION                               *        
W26603* ------   ----------  ----------------------------------------- *        
W26603* W26603   03-24-2004  STORE EXPANSION.  EXPANDED LRECL OF       *        
W26603*                      OUT02.                                    *        
W26603*                      ADDED 'WITH UR' TO DB2 SELECTS.           *        
W26603*                      - RONNA MCDONALD                          *        
      ******************************************************************        
                                                                                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT LEDGER-EXTRACT-DATE-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT LEDGER-EXTRACT-FILE                                           
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD              PIC X(80).                          
                                                                                
       FD  LEDGER-EXTRACT-DATE-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  LEDGER-EXTRACT-DATE-RECORD       PIC X(80).                          
                                                                                
       FD  LEDGER-EXTRACT-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603 01  LEDGER-EXTRACT-RECORD            PIC X(1288).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR LEDGER EXTRACT DATE RECORD                            
      ****************************************************************          
                                                                                
           COPY MLRD114.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR LEDGER EXTRACT RECORD                                 
      ****************************************************************          
                                                                                
           COPY MLRD102.                                                        
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      ****************************************************************          
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-EXTRACT-DTE               PIC X(10).                          
           05  FILLER                       PIC X(70).                          
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
               88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
           05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
               88  PS-DATE-FOUND                       VALUE 'Y'.               
               88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
           05  PS-END-OF-DEPT-SCL-NBR-CSR-SW PIC X     VALUE 'N'.               
               88  PS-END-OF-DEPT-SCL-NBR-CSR          VALUE 'Y'.               
           05  PS-END-OF-LEDGER-CSR-SW      PIC X      VALUE 'N'.               
               88  PS-END-OF-LEDGER-CSR                VALUE 'Y'.               
               88  PS-NOT-END-OF-LEDGER-CSR            VALUE 'N'.               
           05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.               
               88  PS-MBS-INFO-FOUND                   VALUE 'Y'.               
               88  PS-MBS-INFO-NOT-FOUND               VALUE 'N'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-FSCL-YR-BEG-DTE           PIC X(10)  VALUE SPACE.             
           05  PV-FSCL-YR-NBR               PIC X(04)  VALUE SPACE.             
           05  PV-PREV-FSCL-YR-END-DTE      PIC X(10)  VALUE SPACE.             
                                                                                
           05  PV-SPLIT-CL-NBR.                                                 
               10  PV-SPLIT-CL-NBR-BYTES-1-N-2.                                 
                   15  PV-SPLIT-CL-NBR-BYTE-1                                   
                                            PIC X.                              
                   15  PV-SPLIT-CL-NBR-BYTE-2                                   
                                            PIC X.                              
               10  FILLER                   PIC X.                              
                                                                                
           05  PV-MAJ-CL-NBR                PIC X(02).                          
           05  PV-SUB-CL-NBR                PIC X(02).                          
                                                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
       01  SS-SUBSCRIPTS.                                                       
           05  SS-FSCL-MN                   PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
      ****************************************************************          
      * LEDGER MONTH-TO-DATE SUB-CLASS TABLE                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNSCL                                                   
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * DATE ATTRIBUTE TABLE / LEDGER OPEN FISCAL DATE INFORMATION              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS MERCHANDISE AREA TABLE                                              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS APPLICATION AVAILABILITY TABLE                                      
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  DEPARTMENT / SUB-CLASS NUMBER CURSOR                                   
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE DEPT-SCL-NBR-CSR CURSOR FOR                               
                  SELECT                                                        
                      DEPT_NBR                                                  
                     ,SUBSTR(SUB_CL_NBR,2,2)                                    
                  FROM                                                          
                      TMDSEAR A                                                 
                     ,TAPLAVL B                                                 
                                                                                
                  WHERE A.OPERCO_NBR     = :PC-OPERCO-NBR                       
                    AND A.MDSELV_CDE     = 'SCL'                                
                    AND :ML114-EXTRACT-DTE                                      
                         BETWEEN A.STRT_DTE                                     
                             AND A.END_DTE                                      
                    AND A.MDSEAR_DKEY    =  B.MDSEAR_DKEY                       
                    AND B.APLSYS_CDE     = 'ML'                                 
                    AND :ML114-EXTRACT-DTE                                      
                         BETWEEN B.MDSEAR_STRT_DTE                              
                             AND B.MDSEAR_END_DTE                               
                                                                                
              UNION                                                             
                                                                                
                  SELECT                                                   CL**3
                      DEPT_NBR                                                  
                     ,SUB_CL_NBR                                                
                  FROM                                                          
                      TMNSCL                                                    
                  WHERE FSCL_MN_END_DTE                                         
                            BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
                                AND :ML114-EXTRACT-DTE                          
               FOR FETCH ONLY                                                   
W26603        WITH UR                                                           
           END-EXEC.                                                            
      ****************************************************************          
      *  LEDGER CURSOR                                                          
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE LEDGER-CSR CURSOR FOR                                     
                  SELECT                                                        
                      FSCL_MN_NBR                                               
                     ,FSCL_MN_END_DTE                                           
                     ,SLS_RTL_AMT                                               
                     ,MKDN_RTL_AMT                                              
                     ,MKUP_RTL_AMT                                              
                     ,XFER_IN_RTL_AMT                                           
                     ,XFER_OUT_RTL_AMT                                          
                     ,RCPT_RTL_AMT                                              
                     ,RCPT_NET_COST_AMT                                         
                     ,PADJ_RTL_AMT                                              
                     ,PADJ_NET_COST_AMT                                         
                     ,INV_ADJ_RTL_AMT                                           
                     ,SHNK_RTL_AMT                                              
                     ,END_INV_RTL_AMT                                           
                     ,END_INV_COST_AMT                                          
                     ,SLS_COST_AMT                                              
                     ,GRO_MRGN_AMT                                              
                  FROM                                                          
                      TMNSCL                                                    
                  WHERE FSCL_MN_END_DTE                                         
                            BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
                                AND :ML114-EXTRACT-DTE                          
                    AND DEPT_NBR   = :MDSEAR-DEPT-NBR                           
                    AND SUB_CL_NBR = :PV-SUB-CL-NBR                             
               FOR FETCH ONLY                                                   
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
                OUTPUT LEDGER-EXTRACT-DATE-FILE                                 
                       LEDGER-EXTRACT-FILE.                                     
                                                                                
           PERFORM 1150-SET-EXTRACT-DTE.                                        
                                                                                
           PERFORM 8200-OPEN-DEPT-SCL-NBR-CSR.                                  
                                                                                
           PERFORM 8300-FETCH-DEPT-SCL-NBR-CSR.                                 
                                                                                
           PERFORM 2000-EXTRACT-SCL-DATA                                        
               UNTIL PS-END-OF-DEPT-SCL-NBR-CSR.                                
                                                                                
           PERFORM 8400-CLOSE-DEPT-SCL-NBR-CSR.                                 
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
                 LEDGER-EXTRACT-DATE-FILE                                       
                 LEDGER-EXTRACT-FILE.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1150-SET-EXTRACT-DTE.                                                    
                                                                                
           PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
                                                                                
           IF PS-END-OF-CONTROL-CARDS                                           
           OR CC-EXTRACT-DTE = SPACE                                            
               PERFORM 1170-USE-ML-CNTL-DTES                                    
           ELSE                                                                 
               DISPLAY '** INPUT CONTROL CARD: ' CC-CONTROL-CARD                
               PERFORM 1160-USE-CONTROL-CARD-DTE                                
           END-IF.                                                              
                                                                                
           DISPLAY '** SELECTED EXTRACT DATE: ' ML114-EXTRACT-DTE.              
                                                                                
           PERFORM  7100-WRITE-LEDGER-EXTRACT-DATE.                             
                                                                                
      *    GET THE MONTH-END DATE FOR THE LAST MONTH OF THE PREVIOUS            
      *    ** PREVIOUS FISCAL YEAR.  THIS DATE WILL BE USED AS THE              
      *    ** STARTING DATE FOR EXTRACTING THE MONTHLY DATA SO                  
      *    ** THAT THE BEGINNING INVENTORIES FOR THE OPEN FISCAL                
      *    ** YEAR CAN BE DETERMINED.                                           
                                                                                
           MOVE PV-FSCL-YR-BEG-DTE  TO DTATTR-KY-DTE.                           
                                                                                
           PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF PS-DATE-NOT-FOUND                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT421 **'                                 
               DISPLAY '** PARAGRAPH 1150-SET-EXTRACT-DTE'                      
               DISPLAY '** PREV FSCL YR BEG DTE IS NOT A VALID DATE.'           
               MOVE +4008 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE DTATTR-PFM-END-DTE      TO PV-PREV-FSCL-YR-END-DTE.             
                                                                                
                                                                                
       1160-USE-CONTROL-CARD-DTE.                                               
                                                                                
           DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'               
                                                                                
           MOVE CC-EXTRACT-DTE TO DTATTR-KY-DTE.                                
                                                                                
           PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF  PS-DATE-FOUND                                                    
           AND DTATTR-KY-DTE = DTATTR-FSCL-MN-END-DTE                           
               MOVE DTATTR-FSCL-MN-END-DTE TO ML114-EXTRACT-DTE                 
               MOVE DTATTR-FSCL-MN-NBR     TO ML114-EXTRACT-MN-NBR              
               MOVE DTATTR-FSCL-YR-BEG-DTE TO PV-FSCL-YR-BEG-DTE                
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT421 **'                                 
               DISPLAY '** PARAGRAPH 1160-USE-CONTROL-CARD-DTE'                 
               DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'                
               DISPLAY '** MONTH-END DATE. '                                    
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
                                                                                
       1170-USE-ML-CNTL-DTES.                                                   
                                                                                
           PERFORM 8000-GET-ML-CNTL-DTES.                                       
                                                                                
           DISPLAY                                                              
            '** POSTING FISCAL MONTH DATE: ' MLCNTL-PSTG-FSCL-MN-DTE            
           DISPLAY                                                              
            '** MAX POSTING MONTH DATE: ' MLCNTL-MXPSTG-FSCL-MN-DTE.            
                                                                                
           MOVE MLCNTL-MXPSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                     
                                                                                
           PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF PS-DATE-NOT-FOUND                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT421 **'                                 
               DISPLAY '** PARAGRAPH 1170-USE-ML-CNTL-DTES'                     
               DISPLAY '** MAX POSTING DATE IS NOT A VALID DATE.'               
               MOVE +4008 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE.                 
           MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR.              
           MOVE DTATTR-FSCL-YR-NBR        TO PV-FSCL-YR-NBR.                    
           MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE.                
                                                                                
           MOVE MLCNTL-PSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                       
                                                                                
           PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF PS-DATE-NOT-FOUND                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT421 **'                                 
               DISPLAY '** PARAGRAPH 1170-USE-ML-CNTL-DTES'                     
               DISPLAY                                                          
               '** POSTING FISCAL MONTH DATE IS NOT A VALID DATE.'              
               MOVE +4008 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF DTATTR-FSCL-YR-NBR NOT = PV-FSCL-YR-NBR                           
               DISPLAY                                                          
               '** USING POSTING FISCAL MONTH FOR EXTRACT DATE.'                
               MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE              
               MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR           
               MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE             
           ELSE                                                                 
               DISPLAY '** USING MAX POSTING MONTH FOR EXTRACT DATE.'           
           END-IF.                                                              
                                                                                
                                                                                
       2000-EXTRACT-SCL-DATA.                                                   
                                                                                
           INITIALIZE ML102-LEDGER-EXTRACT-REC.                                 
                                                                                
           MOVE PV-MAJ-CL-NBR TO ML102-MAJ-CL-NBR.                              
           MOVE PV-SUB-CL-NBR TO ML102-SUB-CL-NBR.                              
                                                                                
           PERFORM 2050-GET-MBS-INFO.                                           
                                                                                
           SET PS-NOT-END-OF-LEDGER-CSR TO TRUE.                                
           PERFORM 8500-OPEN-LEDGER-CSR.                                        
                                                                                
           PERFORM 8600-FETCH-LEDGER-CSR.                                       
                                                                                
           PERFORM 3000-EXTRACT-MONTHS-FOR-SCL                                  
               UNTIL PS-END-OF-LEDGER-CSR.                                      
                                                                                
           PERFORM 8700-CLOSE-LEDGER-CSR.                                       
                                                                                
           PERFORM 7200-WRITE-LEDGER-EXTRACT.                                   
                                                                                
           PERFORM 8300-FETCH-DEPT-SCL-NBR-CSR.                                 
                                                                                
                                                                                
       2050-GET-MBS-INFO.                                                       
                                                                                
      *****************************************************************         
      * GET THE MBS INFO THAT WAS EFFECTIVE FOR THE MOST RECENT PERIOD          
      * BEING REPORTED. IF THE DEPARTMENT WAS NOT ACTIVE AT THAT TIME,          
      * GET THE MOST RECENT MBS INFO FOR THE DEPARTMENT.                        
      *****************************************************************         
                                                                                
           PERFORM 8060-GET-EFFECTIVE-MBS-INFO.                                 
                                                                                
           IF PS-MBS-INFO-NOT-FOUND                                             
               PERFORM 8070-GET-LATEST-MBS-INFO                                 
           END-IF.                                                              
                                                                                
           MOVE MDSEAR-DEPT-NBR     TO ML102-DEPT-NBR.                          
                                                                                
           IF PS-MBS-INFO-FOUND                                                 
               MOVE MDSEAR-GMA-NBR  TO ML102-GMA-NBR                            
               MOVE MDSEAR-DMA-NBR  TO ML102-DMA-NBR                            
               MOVE MDSEAR-BYR-NBR  TO ML102-BYR-NBR                            
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXTRACT-MONTHS-FOR-SCL.                                             
                                                                                
           IF MNSCL-FSCL-MN-END-DTE = PV-PREV-FSCL-YR-END-DTE                   
               MOVE MNSCL-END-INV-COST-AMT                                      
                                TO ML102-BEG-INV-COST-AMT                       
               MOVE MNSCL-END-INV-RTL-AMT                                       
                                TO ML102-BEG-INV-RTL-AMT                        
           ELSE                                                                 
               MOVE MNSCL-FSCL-MN-NBR                                           
                                TO SS-FSCL-MN                                   
               MOVE MNSCL-SLS-RTL-AMT                                           
                                TO ML102-SLS-RTL-AMT       (SS-FSCL-MN)         
               MOVE MNSCL-MKDN-RTL-AMT                                          
                                TO ML102-MKDN-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-MKUP-RTL-AMT                                          
                                TO ML102-MKUP-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-XFER-IN-RTL-AMT                                       
                                TO ML102-XFER-IN-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNSCL-XFER-OUT-RTL-AMT                                      
                                TO ML102-XFER-OUT-RTL-AMT  (SS-FSCL-MN)         
               MOVE MNSCL-RCPT-RTL-AMT                                          
                                TO ML102-RCPT-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-RCPT-NET-COST-AMT                                     
                                TO ML102-RCPT-NET-COST-AMT (SS-FSCL-MN)         
               MOVE MNSCL-PADJ-RTL-AMT                                          
                                TO ML102-PADJ-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-PADJ-NET-COST-AMT                                     
                                TO ML102-PADJ-NET-COST-AMT (SS-FSCL-MN)         
               MOVE MNSCL-INV-ADJ-RTL-AMT                                       
                                TO ML102-INV-ADJ-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNSCL-SHNK-RTL-AMT                                          
                                TO ML102-SHNK-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-END-INV-RTL-AMT                                       
                                TO ML102-END-INV-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNSCL-END-INV-COST-AMT                                      
                                TO ML102-END-INV-COST-AMT  (SS-FSCL-MN)         
               MOVE MNSCL-SLS-COST-AMT                                          
                                TO ML102-SLS-COST-AMT      (SS-FSCL-MN)         
               MOVE MNSCL-GRO-MRGN-AMT                                          
                                TO ML102-GRO-MRGN-AMT      (SS-FSCL-MN)         
           END-IF.                                                              
                                                                                
           PERFORM 8600-FETCH-LEDGER-CSR.                                       
                                                                                
                                                                                
       7000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
                                                                                
                                                                                
       7100-WRITE-LEDGER-EXTRACT-DATE.                                          
                                                                                
           WRITE LEDGER-EXTRACT-DATE-RECORD                                     
               FROM ML114-LEDGER-EXTRACT-DATE-REC.                              
                                                                                
                                                                                
       7200-WRITE-LEDGER-EXTRACT.                                               
                                                                                
           WRITE LEDGER-EXTRACT-RECORD                                          
               FROM ML102-LEDGER-EXTRACT-REC.                                   
                                                                                
                                                                                
       8000-GET-ML-CNTL-DTES.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    PSTG_FSCL_MN_DTE                                            
                   ,MXPSTG_FSCL_MN_DTE                                          
                INTO                                                            
                    :MLCNTL-PSTG-FSCL-MN-DTE                                    
                   ,:MLCNTL-MXPSTG-FSCL-MN-DTE                                  
                FROM                                                            
                    TMLCNTL                                                     
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8000-GET-ML-CNTL-DTES' TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMLCNTL'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8050-GET-DATE-ATTRIBUTES.                                                
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_YR_NBR                                                 
                   ,FSCL_MN_NBR                                                 
                   ,FSCL_MN_END_DTE                                             
                   ,FSCL_YR_BEG_DTE                                             
                   ,PFM_END_DTE                                                 
                INTO                                                            
                    :DTATTR-FSCL-YR-NBR                                         
                   ,:DTATTR-FSCL-MN-NBR                                         
                   ,:DTATTR-FSCL-MN-END-DTE                                     
                   ,:DTATTR-FSCL-YR-BEG-DTE                                     
                   ,:DTATTR-PFM-END-DTE                                         
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    KY_DTE = :DTATTR-KY-DTE                                     
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-DATE-FOUND     TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-DATE-NOT-FOUND TO TRUE                                
               WHEN OTHER                                                       
                   DISPLAY '** DTATTR-KY-DTE: ' DTATTR-KY-DTE                   
                   MOVE    '8050-GET-DATE-ATTRIBUTES'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8060-GET-EFFECTIVE-MBS-INFO.                                             
                                                                                
      ******************************************************************        
      * GET THE MBS INFO FROM THE EFFECTIVE ROW FOR THE DEPARTMENT.             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    GMA_NBR                                                     
                   ,DMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR                                                     
                WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
                  AND MDSELV_CDE  = 'DPT'                                       
                  AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
                  AND :ML114-EXTRACT-DTE                                        
                       BETWEEN STRT_DTE                                         
                           AND END_DTE                                          
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-MBS-INFO-FOUND TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8060-GET-EFFECTIVE-MBS-INFO'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMDSEAR'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8070-GET-LATEST-MBS-INFO.                                                
                                                                                
      ******************************************************************        
      * GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    GMA_NBR                                                     
                   ,DMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR                                                     
                WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
                  AND MDSELV_CDE  = 'DPT'                                       
                  AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
                  AND END_DTE     =                                             
                        (SELECT MAX (END_DTE)                                   
                         FROM   TMDSEAR                                         
                         WHERE  OPERCO_NBR = :PC-OPERCO-NBR                     
                           AND  MDSELV_CDE = 'DPT'                         CL**3
                           AND  DEPT_NBR   = :MDSEAR-DEPT-NBR)                  
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-MBS-INFO-FOUND TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8070-GET-LATEST-MBS-INFO'                              
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMDSEAR'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
                                                                                
       8200-OPEN-DEPT-SCL-NBR-CSR.                                              
                                                                                
               EXEC SQL                                                         
                    OPEN DEPT-SCL-NBR-CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8200-OPEN-DEPT-SCL-NBR-CSR'                        
                                                TO PV-PARAGRAPH-NAME            
                       MOVE 'OPEN DEPT-SCL-NBR-CSR'                             
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9998-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
                                                                                
       8300-FETCH-DEPT-SCL-NBR-CSR.                                             
      *****************************************************************         
      * RETRY LOGIC IS NOT INCLUDED IN THIS PARAGRAPH BECAUSE A -911            
      * CAUSES THE CURSOR TO BE CLOSED.  ALSO, FOR THIS CURSOR,                 
      * THE ROWS ARE ACTUALLY RETRIEVED WHEN THE CURSOR IS OPENED, NOT          
      * WHEN THE ROW IS FETCHED, SO IF A PAGE WAS LOCKED, A -911                
      * WOULD OCCUR ON THE OPEN, NOT ON THE FETCH.                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   DEPT-SCL-NBR-CSR                                             
               INTO                                                             
                   :MDSEAR-DEPT-NBR                                             
                  ,:MDSEAR-SUB-CL-NBR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE MDSEAR-SUB-CL-NBR TO     PV-SPLIT-CL-NBR                
                   MOVE PV-SPLIT-CL-NBR-BYTES-1-N-2 TO                          
                                                 PV-SUB-CL-NBR                  
                   MOVE 0 TO PV-SPLIT-CL-NBR-BYTE-2                             
                   MOVE PV-SPLIT-CL-NBR-BYTES-1-N-2 TO                          
                                                 PV-MAJ-CL-NBR                  
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-DEPT-SCL-NBR-CSR TO TRUE                       
               WHEN OTHER                                                       
                   MOVE '8300-FETCH-DEPT-SCL-NBR-CSR'                           
                                           TO PV-PARAGRAPH-NAME                 
                   MOVE 'FETCH DEPT-SCL-NBR-CSR'                                
                                           TO PV-DB2-OPER-ATTEMPTED             
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8400-CLOSE-DEPT-SCL-NBR-CSR.                                             
                                                                                
           EXEC SQL                                                             
               CLOSE DEPT-SCL-NBR-CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8400-CLOSE-DEPT-SCL-NBR-CSR'                           
                                           TO PV-PARAGRAPH-NAME                 
                   MOVE 'CLOSE DEPT-SCL-NBR-CSR'                        D       
                                           TO PV-DB2-OPER-ATTEMPTED             
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8500-OPEN-LEDGER-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                OPEN LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8500-OPEN-LEDGER-CSR'                                  
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN LEDGER-CSR'                                       
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8600-FETCH-LEDGER-CSR.                                                   
      *****************************************************************         
      * RETRY LOGIC IS NOT INCLUDED IN THIS PARAGRAPH BECAUSE A -911            
      * CAUSES THE CURSOR TO BE CLOSED.                                         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   LEDGER-CSR                                                   
               INTO                                                             
                  :MNSCL-FSCL-MN-NBR                                            
                 ,:MNSCL-FSCL-MN-END-DTE                                        
                 ,:MNSCL-SLS-RTL-AMT                                            
                 ,:MNSCL-MKDN-RTL-AMT                                           
                 ,:MNSCL-MKUP-RTL-AMT                                           
                 ,:MNSCL-XFER-IN-RTL-AMT                                        
                 ,:MNSCL-XFER-OUT-RTL-AMT                                       
                 ,:MNSCL-RCPT-RTL-AMT                                           
                 ,:MNSCL-RCPT-NET-COST-AMT                                      
                 ,:MNSCL-PADJ-RTL-AMT                                           
                 ,:MNSCL-PADJ-NET-COST-AMT                                      
                 ,:MNSCL-INV-ADJ-RTL-AMT                                        
                 ,:MNSCL-SHNK-RTL-AMT                                           
                 ,:MNSCL-END-INV-RTL-AMT                                        
                 ,:MNSCL-END-INV-COST-AMT                                       
                 ,:MNSCL-SLS-COST-AMT                                           
                 ,:MNSCL-GRO-MRGN-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-LEDGER-CSR TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '8600-FETCH-LEDGER-CSR'                                 
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8700-CLOSE-LEDGER-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               CLOSE LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8700-CLOSE-LEDGER-CSR'                                 
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKUT421'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
