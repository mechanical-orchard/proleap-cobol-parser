       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT014.                                       
       AUTHOR.                  COLLEEN CUMMINGS.                               
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/18/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *   CALCULATE ML017-PC-TOT-DOC-QTY BY READING INPUT FILE         *        
      *   SORTED BY DOC-NBR (PRCHG_ID).                                *        
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
27813 * 27813   03-14-2005 ROD JANSSEN NEW PERM PRICING ISSUES.                 
29145 *W29145   11-20-2005 S. JACKSON  CHANGES FOR MLRD017 EXPANSION            
PM3403*PM3403   04-20-2012 S. JACKSON  LARGE RETAIL MLRD017 RECOMPILE           
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT ML-INFILE                                                     
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ML-DOC-QTY-FILE                                               
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ML-OUTFILE                                                    
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  ML-INFILE                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W29145     RECORD CONTAINS 200 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
W29145*01  ML-IN-REC                       PIC  X(120).                         
W29145 01  ML-IN-REC                       PIC  X(200).                         
                                                                                
       FD  ML-DOC-QTY-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 50 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ML-DOC-QTY-IN-REC               PIC  X(50).                          
                                                                                
       FD  ML-OUTFILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
W29145     RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS ML-OUT-REC.                                           
W29145*01  ML-OUT-REC                      PIC X(120).                          
W29145 01  ML-OUT-REC                      PIC X(200).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'MLKUT014'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-ML-INFILE-READ-CNT   PIC 9(9)        VALUE ZERO               
                                                       COMP-3.                  
           05  PV-ML-DOC-QTY-READ-CNT  PIC 9(9)        VALUE ZERO               
                                                       COMP-3.                  
           05  PV-RECS-WRITTEN-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-OUTPUT-CNT           PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-INPUT-CNT-A          PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
       01  PV-TOT-DOC-QTY-RECORD.                                               
           05  PV-DOC-NBR              PIC 9(10)       VALUE ZEROES.            
           05  PV-TOT-DOC-QTY          PIC 9(09)       VALUE ZEROES.            
           05  FILLER                  PIC X(31)       VALUE SPACES.            
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-ML-INFILE-EOF-SW         PIC X       VALUE 'N'.               
               88  PS-ML-INFILE-EOF                    VALUE 'Y'.               
           05  PS-ML-DOC-QTY-EOF-SW        PIC X       VALUE 'N'.               
               88  PS-ML-DOC-QTY-FILE-EOF              VALUE 'Y'.               
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR ML MARKDOWN FILE                                       
      ******************************************************************        
           COPY MLRD017.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2100-READ-ML-INFILE.                                         
           PERFORM 2200-READ-ML-DOC-QTY-FILE.                                   
                                                                                
           IF PS-ML-INFILE-EOF                                                  
              DISPLAY '*******************************************'             
              DISPLAY '*** NO SKUS FOUND ON ML INFILE RECORD   ***'             
              DISPLAY '*******************************************'             
           ELSE                                                                 
               IF PS-ML-DOC-QTY-FILE-EOF                                        
                  DISPLAY '*******************************************'         
                  DISPLAY '*** NO SKUS FOUND ON ML DOC QTY RECORD  ***'         
                  DISPLAY '*******************************************'         
               ELSE                                                             
                  PERFORM 2300-PROCESS-RECORDS                                  
                    UNTIL PS-ML-INFILE-EOF                                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & INIT                                                    
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF MLKUT014               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           OPEN INPUT  ML-INFILE                                                
                       ML-DOC-QTY-FILE                                          
                OUTPUT ML-OUTFILE.                                              
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE ML-INFILE                                                      
                 ML-DOC-QTY-FILE                                                
                 ML-OUTFILE.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* MLKUT014 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL ML-INFILE READ: ' PV-ML-INFILE-READ-CNT.            
           DISPLAY '* TOTAL ML-DOC-QTY-FILE READ: '                             
                    PV-ML-DOC-QTY-READ-CNT.                                     
           DISPLAY '* TOTAL OUTPUT RECORDS: '                                   
                                         PV-RECS-WRITTEN-CNT.                   
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF MLKUT014                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE ML MARKDOWN FILE.                           
      ******************************************************************        
       2100-READ-ML-INFILE.                                                     
                                                                                
           READ ML-INFILE                                                       
               INTO ML017-PRICE-CHANGE-RECORD                                   
               AT END                                                           
                   SET PS-ML-INFILE-EOF TO TRUE                                 
               NOT AT END                                                       
                   ADD +1 TO PV-ML-INFILE-READ-CNT.                             
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE ML DOC QTY FILE.                            
      ******************************************************************        
       2200-READ-ML-DOC-QTY-FILE.                                               
                                                                                
           READ ML-DOC-QTY-FILE                                                 
               INTO PV-TOT-DOC-QTY-RECORD                                       
               AT END                                                           
                   SET PS-ML-DOC-QTY-FILE-EOF TO TRUE                           
               NOT AT END                                                       
                   ADD +1 TO PV-ML-DOC-QTY-READ-CNT.                            
                                                                                
      ******************************************************************        
      * PROCESS RECORDS.                                                        
      ******************************************************************        
       2300-PROCESS-RECORDS.                                                    
                                                                                
27813 *    DISPLAY 'ML017-PC-DOC-7-NBR-PIC9 = ' ML017-PC-DOC-7-NBR-PIC9         
27813 *    DISPLAY 'PV-DOC-NBR         = ' PV-DOC-NBR                           
                                                                                
           IF ML017-PC-DOC-7-NBR-PIC9 = PV-DOC-NBR                              
               MOVE PV-TOT-DOC-QTY TO ML017-PC-TOT-DOC-QTY                      
               PERFORM 8000-WRITE-OUTPUT-REC                                    
           ELSE                                                                 
               IF PS-ML-DOC-QTY-FILE-EOF                                        
                  DISPLAY 'ML-DOC-QTY-FILE IS DONE'                             
               ELSE                                                             
                   PERFORM 2200-READ-ML-DOC-QTY-FILE                            
27813 *            DISPLAY '#ML017-PC-DOC-7-NBR-PIC9 = '                        
27813 *                     ML017-PC-DOC-7-NBR-PIC9                             
27813 *            DISPLAY '#PV-DOC-NBR         = ' PV-DOC-NBR                  
                   IF ML017-PC-DOC-7-NBR-PIC9 = PV-DOC-NBR                      
                       MOVE PV-TOT-DOC-QTY TO ML017-PC-TOT-DOC-QTY              
                       PERFORM 8000-WRITE-OUTPUT-REC                            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-ML-INFILE.                                         
                                                                                
      ******************************************************************        
      *    WRITE SUMMARY RECORDS - ONE RECORD FOR ALL STORES                    
      ******************************************************************        
       8000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE ML-OUT-REC FROM ML017-PRICE-CHANGE-RECORD                      
           ADD +1 TO PV-OUTPUT-CNT                                              
                     PV-RECS-WRITTEN-CNT.                                       
                                                                                
