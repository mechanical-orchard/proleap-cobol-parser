000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             RCKRP026.                                        
000600 AUTHOR.                 DAVE MUSKAT - COMPUWARE.                         
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            10-27-98.                                        
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*                                                               *         
001300*  THIS PROGRAM WILL CREATE THE DMM SUMMARY REPORT, A SUMMARY   *         
001400*  OF THE MONTHLY UNITS VERIFIED REPORT (RCKRP009-1) THAT WAS   *         
001500*  CREATED FROM THE DAILY EXTRACT FILE.                         *         
001600*                                                               *         
001600*  THE SUMMARY REPORT SHOWS A BREAKDOWN OF VERIFIED QUANTITIES  *         
001600*  BY PREPACK CODE AND THE PERCENT OF THE TOTAL THE QUANTITIES  *         
001600*  REPRESENT.  LIKE THE MONTHLY UNITS VERIFIED REPORT ITSELF,   *         
001600*  THIS SUMMARY SHOWS ONLY UNITS VERIFIED WITHIN THE CURRENT    *         
001600*  SEASON.  THE CURRENT SEASON (EITHER AUGUST THRU JANUARY OR   *         
001600*  FEBRUARY THRU JULY) IS DETERMINED BASED ON THE MONTH FROM    *         
001600*  THE SYSTEM DATE.  IF THE INPUT FILE TO THIS PROGRAM CANTAINS *         
001600*  NO RECORDS, A MESSAGE SAYING NO SUMMARY DATA WAS AVAILABLE   *         
001600*  IS PRINTED ON THE REPORT DETAIL LINE.                        *         
001700*****************************************************************         
001800*                                                               *         
001900*  CHANGED 10/27/98 BY DAVE MUSKAT - COMPUWARE.                 *         
002000*   WR# 4650 NEW PROGRAM                                        *         
002100*                                                               *         
002200*                                                               *         
001700*****************************************************************         
001800*                                                               *         
001900*  CHANGED 12/14/98 BY PAUL JORGENSEN                                     
002000*   WR# 4650 CHANGE THE HEADING - IT IS NO LONGER REPORTING BY  *         
002100*   'SEASON'.  IT IS WEEKLY AND MONTHLY.                                  
      *   ACCEPT A CONTROL CARD FOR A NEW WEEKLY REPORT OPTION.                 
002200*****************************************************************         
LF0114*                                                                         
LF0114*  CHANGED 01/02/14 BY LINDA FRIESS                                       
LF0114*   CHG0081355 PACKAGING CODE MODIFICATIONS TO USE CROSS-DOCK &           
LF0114*   NON-CROSS-DOCK QUANTITIES FROM MODIFIED INPUT FILE. CHANGED           
LF0114*   RECORD LENGTH FROM 100 TO 135.                                        
LF0114*****************************************************************         
002300*****************************************************************         
002400 ENVIRONMENT DIVISION.                                                    
002500*****************************************************************         
002600                                                                          
002700 CONFIGURATION SECTION.                                                   
002800                                                                          
002900 SOURCE-COMPUTER.        IBM-3090.                                        
003000 OBJECT-COMPUTER.        IBM-3090.                                        
003100                                                                          
003200 INPUT-OUTPUT SECTION.                                                    
003300                                                                          
003400 FILE-CONTROL.                                                            
003500                                                                          
003600     SELECT DMM-SUMMARY-FILE                                              
003700         ASSIGN TO INP01.                                                 
003800                                                                          
003900     SELECT DMM-SUMMARY-RPT-FILE                                          
004000         ASSIGN TO RPT01.                                                 
004100                                                                          
004200                                                                          
004300******************************************************************        
004400 DATA DIVISION.                                                           
004500******************************************************************        
004600                                                                          
004700 FILE SECTION.                                                            
004800                                                                          
004900 FD  DMM-SUMMARY-FILE                                                     
005000     RECORDING MODE IS F                                                  
005100     LABEL RECORDS ARE STANDARD                                           
005200     BLOCK CONTAINS 0 RECORDS.                                            
005300                                                                          
LF0114 01  RC044-INPUT-RECORD               PIC  X(135).                        
005500                                                                          
005600 FD  DMM-SUMMARY-RPT-FILE                                                 
005700     RECORDING MODE IS F                                                  
005800     BLOCK CONTAINS 0  RECORDS.                                           
005900 01  DMM-SUMMARY-RPT-RECORD          PIC  X(132).                         
006000                                                                          
006100                                                                          
006200 WORKING-STORAGE SECTION.                                                 
006300                                                                          
006400 01  FILLER                      PIC  X(50)    VALUE                      
006500     '**** WORKING STORAGE FOR RCKRP026 STARTS HERE ****'.                
006600                                                                          
       01  CC-CONTROL-CARD.                                                     
           05  CC-REPORT-TYPE                  PIC  X.                          
               88  CC-VALID-REPORT-TYPE        VALUE                            
                   'W' 'M'.                                                     
               88  CC-WEEKLY-REPORT            VALUE                            
                   'W'.                                                         
               88  CC-MONTHLY-REPORT           VALUE                            
                   'M'.                                                         
      *                                                                         
      *  WORK AREA FOR FISCAL CALENDAR ROUTINE.                                 
      *                                                                         
           COPY DPWS500.                                                        
                                                                                
006700 01  PROGRAM-SWITCHES.                                                    
006800     05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.             
006900         88  PS-NOT-END-OF-FILE                    VALUE 'N'.             
007000         88  PS-END-OF-FILE                        VALUE 'Y'.             
006800     05  PS-DATA-PRESENT-SW          PIC  X(01)    VALUE 'Y'.             
006900         88  PS-DATA-NOT-PRESENT                   VALUE 'N'.             
007000         88  PS-DATA-PRESENT                       VALUE 'Y'.             
007100                                                                          
007200 01  PC-CONSTANTS.                                                        
007300     05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.             
007400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
007500                                                   'RCKRP026'.            
007600     05  PC-REPORT-NBR-1             PIC  9(01)    VALUE 1.               
008003                                                                          
008004 01  PV-ACCUMULATOR-FIELDS.                                               
008005     05  PV-PAGE-CNT                 PIC S9(04)    VALUE ZERO             
008100                                                   COMP-3.                
008200     05  PV-LINE-CNT                 PIC S9(04)    VALUE ZERO             
008300                                                   COMP SYNC.             
008400     05  PV-GMM-ACCUM-FIELDS.                                             
008500         10  PV-GMM-VRFYD-QTY        PIC S9(09)    VALUE ZERO             
008600                                                   COMP-3.                
008700         10  PV-GMM-GOH-QTY          PIC S9(09)    VALUE ZERO             
008800                                                   COMP-3.                
008900         10  PV-GMM-PRETKT-QTY       PIC S9(09)    VALUE ZERO             
009000                                                   COMP-3.                
009100         10  PV-GMM-SINGLE-QTY       PIC S9(09)    VALUE ZERO             
009200                                                   COMP-3.                
009300         10  PV-GMM-MULTI-QTY        PIC S9(09)    VALUE ZERO             
009400                                                   COMP-3.                
009500         10  PV-GMM-INNER-QTY        PIC S9(09)    VALUE ZERO             
009600                                                   COMP-3.                
009700         10  PV-GMM-BULK-QTY         PIC S9(09)    VALUE ZERO             
009800                                                   COMP-3.                
009900         10  PV-GMM-CASE-QTY         PIC S9(09)    VALUE ZERO             
010000                                                   COMP-3.                
010100         10  PV-GMM-PBS-QTY          PIC S9(09)    VALUE ZERO             
010200                                                   COMP-3.                
LF0114         10  PV-GMM-CROSS-DOCK-QTY   PIC S9(09)    VALUE ZERO             
LF0114                                                   COMP-3.                
LF0114         10  PV-GMM-NON-CROSS-DK-QTY PIC S9(09)    VALUE ZERO             
LF0114                                                   COMP-3.                
010300     05  PV-COR-ACCUM-FIELDS.                                             
010400         10  PV-COR-VRFYD-QTY        PIC S9(09)    VALUE ZERO             
010500                                                   COMP-3.                
010600         10  PV-COR-GOH-QTY          PIC S9(09)    VALUE ZERO             
010700                                                   COMP-3.                
010800         10  PV-COR-PRETKT-QTY       PIC S9(09)    VALUE ZERO             
010900                                                   COMP-3.                
011000         10  PV-COR-SINGLE-QTY       PIC S9(09)    VALUE ZERO             
011100                                                   COMP-3.                
011200         10  PV-COR-MULTI-QTY        PIC S9(09)    VALUE ZERO             
011300                                                   COMP-3.                
011400         10  PV-COR-INNER-QTY        PIC S9(09)    VALUE ZERO             
011500                                                   COMP-3.                
011600         10  PV-COR-BULK-QTY         PIC S9(09)    VALUE ZERO             
011700                                                   COMP-3.                
011800         10  PV-COR-CASE-QTY         PIC S9(09)    VALUE ZERO             
011900                                                   COMP-3.                
012000         10  PV-COR-PBS-QTY          PIC S9(09)    VALUE ZERO             
012100                                                   COMP-3.                
LF0114         10  PV-COR-CROSS-DOCK-QTY   PIC S9(09)    VALUE ZERO             
LF0114                                                   COMP-3.                
LF0114         10  PV-COR-NON-CROSS-DK-QTY PIC S9(09)    VALUE ZERO             
LF0114                                                   COMP-3.                
012200 01  PV-CALC-WORK-FIELDS.                                                 
012300     05  PV-PERCENT-SHIFTED          PIC S9(03)V9  VALUE ZERO             
012400                                                   COMP-3.                
012500     05  PV-PERCENT-DECIMAL          PIC S9V999    VALUE ZERO             
012600                                                   COMP-3.                
012700     05  PV-PARTIAL-QTY              PIC S9(09)    VALUE ZERO             
012800                                                   COMP-3.                
012900     05  PV-WRK-PRETKT-QTY           PIC S9(09)    VALUE ZERO             
013000                                                   COMP-3.                
012900     05  PV-WRK-GOH-QTY              PIC S9(09)    VALUE ZERO             
013000                                                   COMP-3.                
013100     05  PV-WRK-SINGLE-QTY           PIC S9(09)    VALUE ZERO             
013200                                                   COMP-3.                
013300     05  PV-WRK-MULTI-QTY            PIC S9(09)    VALUE ZERO             
013400                                                   COMP-3.                
013500     05  PV-WRK-INNER-QTY            PIC S9(09)    VALUE ZERO             
013600                                                   COMP-3.                
013700     05  PV-WRK-BULK-QTY             PIC S9(09)    VALUE ZERO             
013800                                                   COMP-3.                
013900     05  PV-WRK-CASE-QTY             PIC S9(09)    VALUE ZERO             
014000                                                   COMP-3.                
014100     05  PV-WRK-PBS-QTY              PIC S9(09)    VALUE ZERO             
014200                                                   COMP-3.                
014300     05  PV-WRK-TOTAL-QTY            PIC S9(09)    VALUE ZERO             
014400                                                   COMP-3.                
014500 EJECT                                                                    
014600                                                                          
014700 01  SAVE-FIELDS.                                                         
014800     05  SAVE-GMM-NBR                PIC  X(02)    VALUE SPACES.          
014900     05  SAVE-GMM-NAME               PIC  X(25)    VALUE SPACES.          
015000                                                                          
015100 01  PV-MISC-FIELDS.                                                      
015200     05  PV-PRINT-RECORD             PIC X(132)    VALUE SPACES.          
015300     05  PV-LINE-SPACING             PIC S9(04)    VALUE ZERO             
015400                                                   COMP SYNC.             
015500     05  PV-LINE-CNT-AHEAD           PIC S9(04)    VALUE ZERO             
015600                                                   COMP SYNC.             
015700     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
015800                                                   COMP SYNC.             
015900     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
016000                                                                          
016010 01  PV-DATE-FIELDS.                                                      
016090     05  PV-START-DATE-CCYYMMDD.                                          
016091         10  PV-START-DATE-CC        PIC  9(02).                          
016092         10  PV-START-DATE-YY        PIC  9(02).                          
016093         10  PV-START-DATE-MM        PIC  9(02).                          
016094         10  PV-START-DATE-DD        PIC  9(02).                          
016095     05  FILLER REDEFINES PV-START-DATE-CCYYMMDD.                         
016096         10  PV-START-DATE-CCYY      PIC  9(04).                          
016097         10  FILLER                  PIC  9(04).                          
016098                                                                          
016099     05  PV-END-DATE-CCYYMMDD.                                            
016100         10  PV-END-DATE-CC          PIC  9(02).                          
016101         10  PV-END-DATE-YY          PIC  9(02).                          
016102         10  PV-END-DATE-MM          PIC  9(02).                          
016103         10  PV-END-DATE-DD          PIC  9(02).                          
016104     05  FILLER REDEFINES PV-END-DATE-CCYYMMDD.                           
016105         10  PV-END-DATE-CCYY        PIC  9(04).                          
016106         10  FILLER                  PIC  9(04).                          
016107                                                                          
016110     05  PV-CURR-DATE-CCYYMMDD.                                           
016200         10  PV-CURR-DATE-CC         PIC  9(02).                          
016300         10  PV-CURR-DATE-YYMMDD.                                         
016400             15  PV-CURR-DATE-YY     PIC  9(02).                          
016500             15  PV-CURR-DATE-MM     PIC  9(02).                          
016510             15  PV-CURR-DATE-DD     PIC  9(02).                          
016520     05  FILLER REDEFINES PV-CURR-DATE-CCYYMMDD.                          
016530         10  PV-CURR-DATE-CCYY       PIC  9(04).                          
016540         10  FILLER                  PIC  9(04).                          
                                                                                
016520     05  PV-FISCAL-PRD-CCYYPP.                                            
016530         10  PV-FISCAL-PRD-CCYY      PIC  9(04).                          
016530         10  PV-FISCAL-PRD-PP        PIC  9(02).                          
016550                                                                          
016600     05  PV-CURR-TIME.                                                    
016700         10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.            
016800         10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.            
016900         10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.            
017000                                                                          
017100 EJECT                                                                    
017200******************************************************************        
017300*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
017400******************************************************************        
017500     COPY DPWS132O.                                                       
017600                                                                          
017700******************************************************************        
017800*  SUMMARY REPORT HEADING LINES.                                          
017900******************************************************************        
018000 01  HL0-HEADING-LINE-0.                                                  
018100     05  FILLER                      PIC  X(39)    VALUE SPACES.          
018200     05  FILLER                      PIC  X(30)    VALUE                  
018300         'TOTAL NUMBER OF UNITS VERIFIED'.                                
018400     05  FILLER                      PIC  X(30)    VALUE                  
018500         ', DMM SUMMARY - CORPORATE     '.                                
018600     05  FILLER                      PIC  X(33)    VALUE SPACES.          
018610 01  HL1-HEADING-LINE-1.                                                  
           05  FILLER                  PIC  X(42)    VALUE SPACES.              
           05  FILLER                  PIC  X(24)    VALUE                      
              'ITEMS VERIFIED BETWEEN: '.                                       
           05  HL1-START-DATE.                                                  
               10  HL1-START-CCYY      PIC  X(04).                              
               10  FILLER              PIC  X(01)    VALUE '-'.                 
               10  HL1-START-MM        PIC  X(02).                              
               10  FILLER              PIC  X(01)    VALUE '-'.                 
               10  HL1-START-DD        PIC  X(02).                              
           05  FILLER                  PIC  X(05)    VALUE ' AND '.             
           05  HL1-END-DATE.                                                    
               10  HL1-END-CCYY        PIC  X(04).                              
               10  FILLER              PIC  X(01)    VALUE '-'.                 
               10  HL1-END-MM          PIC  X(02).                              
               10  FILLER              PIC  X(01)    VALUE '-'.                 
               10  HL1-END-DD          PIC  X(02).                              
           05  FILLER                  PIC  X(41)    VALUE SPACES.              
018710 01  HL2-HEADING-LINE-2.                                                  
018800     05  FILLER                      PIC  X(48)    VALUE SPACES.          
018900     05  FILLER                      PIC  X(32)    VALUE                  
019000         '*-------------------------------'.                              
019100     05  FILLER                      PIC  X(19)    VALUE                  
019200         ' P A C K A G I N G '.                                           
019300     05  FILLER                      PIC  X(32)    VALUE                  
019400         '-------------------------------*'.                              
019500     05  FILLER                      PIC  X(01)    VALUE SPACES.          
019600 01  HL3-HEADING-LINE-3.                                                  
019700     05  FILLER                      PIC  X(10)    VALUE SPACES.          
019800     05  FILLER                      PIC  X(35)    VALUE                  
019900         '  VRFYD            PRE             '.                           
020000     05  FILLER                      PIC  X(87)    VALUE SPACES.          
020100 01  HL4-HEADING-LINE-4.                                                  
020200     05  FILLER                      PIC  X(40)    VALUE                  
020300         ' DMM         QTY             TKT        '.                      
020400     05  FILLER                      PIC  X(40)    VALUE                  
020500         '   GOH           PBS        SINGLE      '.                      
020600     05  FILLER                      PIC  X(40)    VALUE                  
020700         '   MULTI          CASE         INNER    '.                      
020800     05  FILLER                      PIC  X(12)    VALUE                  
020900         '      BULK  '.                                                  
021000******************************************************************        
021100*  SUMMARY REPORT DETAIL LINE.                                            
021200******************************************************************        
021300 01  DL-DETAIL-LINE.                                                      
021400     05  FILLER                      PIC  X(01)    VALUE SPACES.          
021500     05  DL-DMM-NBR                  PIC  X(02)    VALUE SPACES.          
021600     05  FILLER                      PIC  X(06)    VALUE SPACES.          
021700     05  DL-VRFYD-QTY                PIC Z,ZZZ,ZZ9.                       
021800     05  FILLER                      PIC  X(05)    VALUE SPACES.          
021900     05  DL-PRETKT-QTY               PIC Z,ZZZ,ZZ9.                       
022000     05  FILLER                      PIC  X(05)    VALUE SPACES.          
022100     05  DL-GOH-QTY                  PIC Z,ZZZ,ZZ9.                       
022200     05  FILLER                      PIC  X(05)    VALUE SPACES.          
022300     05  DL-PBS-QTY                  PIC Z,ZZZ,ZZ9.                       
022400     05  FILLER                      PIC  X(05)    VALUE SPACES.          
022500     05  DL-SINGLE-QTY               PIC Z,ZZZ,ZZ9.                       
022600     05  FILLER                      PIC  X(05)    VALUE SPACES.          
023100     05  DL-MULTI-QTY                PIC Z,ZZZ,ZZ9.                       
023200     05  FILLER                      PIC  X(05)    VALUE SPACES.          
022700     05  DL-CASE-QTY                 PIC Z,ZZZ,ZZ9.                       
022800     05  FILLER                      PIC  X(05)    VALUE SPACES.          
022900     05  DL-INNER-QTY                PIC Z,ZZZ,ZZ9.                       
023000     05  FILLER                      PIC  X(05)    VALUE SPACES.          
023300     05  DL-BULK-QTY                 PIC Z,ZZZ,ZZ9.                       
023400     05  FILLER                      PIC  X(02)    VALUE SPACES.          
023500******************************************************************        
023600*  SUMMARY REPORT PERCENT LINE (FOR DETAIL LINES AND TOTALS).             
023700******************************************************************        
023800 01  PL-PERCENT-LINE.                                                     
023900     05  FILLER                      PIC  X        VALUE SPACES.          
023900     05  PL-DMM-NAME                 PIC  X(25).                          
024100     05  FILLER                      PIC  X(01)    VALUE SPACE.           
024000     05  PL-PRE-TKT-PCNT             PIC  ZZ9.9.                          
024100     05  FILLER                      PIC  X(09)    VALUE                  
024200         ' %       '.                                                     
024000     05  PL-GOH-PCNT                 PIC  ZZ9.9.                          
024100     05  FILLER                      PIC  X(09)    VALUE                  
024200         ' %       '.                                                     
024300     05  PL-PBS-PCNT                 PIC  ZZ9.9.                          
024400     05  FILLER                      PIC  X(09)    VALUE                  
024500         ' %       '.                                                     
024600     05  PL-SINGLE-PCNT              PIC  ZZ9.9.                          
024700     05  FILLER                      PIC  X(09)    VALUE                  
024800         ' %       '.                                                     
024900     05  PL-MULTI-PCNT               PIC  ZZ9.9.                          
025000     05  FILLER                      PIC  X(09)    VALUE                  
025100         ' %       '.                                                     
025200     05  PL-CASE-PCNT                PIC  ZZ9.9.                          
025300     05  FILLER                      PIC  X(09)    VALUE                  
025400         ' %       '.                                                     
025500     05  PL-INNER-PCNT               PIC  ZZ9.9.                          
025600     05  FILLER                      PIC  X(09)    VALUE                  
025700         ' %       '.                                                     
025800     05  PL-BULK-PCNT                PIC  ZZ9.9.                          
025900     05  FILLER                      PIC  X(02)    VALUE                  
026000         ' %'.                                                            
026100******************************************************************        
026200*  SUMMARY REPORT TOTAL LINES.                                            
026300******************************************************************        
026400 01  TL1-TOTAL-LINE-1.                                                    
026500     05  FILLER                      PIC  X(06)    VALUE                  
026600         ' GMM  '.                                                        
026700     05  TL1-GMM-NAME                PIC  X(25)    VALUE SPACES.          
026800     05  FILLER                      PIC  X(101)   VALUE SPACES.          
026900 01  TL2-TOTAL-LINE-2.                                                    
027000     05  FILLER                      PIC  X(07)    VALUE                  
027100         ' TOTAL '.                                                       
027200     05  TL2-VRFYD-QTY               PIC  ZZZ,ZZZ,ZZ9.                    
027300     05  FILLER                      PIC  X(03)    VALUE SPACES.          
027400     05  TL2-PRETKT-QTY              PIC  ZZZ,ZZZ,ZZ9.                    
027500     05  FILLER                      PIC  X(03)    VALUE SPACES.          
027600     05  TL2-GOH-QTY                 PIC  ZZZ,ZZZ,ZZ9.                    
027700     05  FILLER                      PIC  X(03)    VALUE SPACES.          
027800     05  TL2-PBS-QTY                 PIC  ZZZ,ZZZ,ZZ9.                    
027900     05  FILLER                      PIC  X(03)    VALUE SPACES.          
028000     05  TL2-SINGLE-QTY              PIC  ZZZ,ZZZ,ZZ9.                    
028100     05  FILLER                      PIC  X(03)    VALUE SPACES.          
028600     05  TL2-MULTI-QTY               PIC  ZZZ,ZZZ,ZZ9.                    
028700     05  FILLER                      PIC  X(03)    VALUE SPACES.          
028200     05  TL2-CASE-QTY                PIC  ZZZ,ZZZ,ZZ9.                    
028300     05  FILLER                      PIC  X(03)    VALUE SPACES.          
028400     05  TL2-INNER-QTY               PIC  ZZZ,ZZZ,ZZ9.                    
028500     05  FILLER                      PIC  X(03)    VALUE SPACES.          
028800     05  TL2-BULK-QTY                PIC  ZZZ,ZZZ,ZZ9.                    
028900     05  FILLER                      PIC  X(02)    VALUE SPACES.          
029000 01  TL3-TOTAL-LINE-3.                                                    
029100     05  FILLER                      PIC  X(06)    VALUE                  
029200         ' GRAND'.                                                        
029300     05  FILLER                      PIC  X(126)   VALUE SPACES.          
026900 01  TL4-GROUP-TOTAL-LINE.                                                
027000     05  FILLER                      PIC  X(48)    VALUE SPACES.          
027200     05  FILLER                      PIC  X(16)    VALUE                  
027300         'X-DOCK TOTAL -->'.                                              
027400     05  TL4-GROUP-TOTAL-1           PIC  ZZZ,ZZZ,ZZ9.                    
027000     05  FILLER                      PIC  X        VALUE SPACES.          
027400     05  TL4-GROUP-PCNT-1            PIC  ZZ9.9.                          
027200     05  FILLER                      PIC  X(28)    VALUE                  
027300         ' %      NON X-DOCK TOTAL -->'.                                  
027400     05  TL4-GROUP-TOTAL-2           PIC  ZZZ,ZZZ,ZZ9.                    
027000     05  FILLER                      PIC  X        VALUE SPACES.          
027400     05  TL4-GROUP-PCNT-2            PIC  ZZ9.9.                          
027200     05  FILLER                      PIC  X(06)    VALUE                  
               ' %    '.                                                        
029500                                                                          
029600                                                                          
029700 01  MISCELLANEOUS-LINES.                                                 
029800     05  END-OF-REPORT-LINE.                                              
029900         10  FILLER                  PIC  X(44)    VALUE SPACES.          
030000         10  FILLER                  PIC  X(45)    VALUE                  
030100             '*******   E N D   O F   R E P O R T   *******'.             
030200         10  FILLER                  PIC  X(43)    VALUE SPACES.          
030210     05  EMPTY-REPORT-LINE.                                               
030220         10  FILLER                  PIC  X(20)    VALUE SPACES.          
030230         10  FILLER                  PIC  X(46)    VALUE                  
030240             ' ***  N O   S U M M A R Y   D A T A   A V A I '.            
030250         10  FILLER                  PIC  X(46)    VALUE                  
030260             'L A B L E   F O R   T H I S   R E P O R T  ***'.            
030270         10  FILLER                  PIC  X(20)    VALUE SPACES.          
030300                                                                          
030400                                                                          
030500******************************************************************        
030600*    DMM SUMMARY FILE LAYOUT                                              
030700******************************************************************        
030800     COPY RCRD044.                                                        
030900                                                                          
031000*****************************************************************         
031100* PROCEDURE DIVISION.                                           *         
031200*****************************************************************         
031300 PROCEDURE DIVISION.                                                      
031400                                                                          
031500 0000-MAINLINE-PROCESSING.                                                
031600                                                                          
031700     PERFORM 1000-INITIALIZATION.                                         
031800     PERFORM 2000-PROCESS-SUMMARY-RECS                                    
031900       UNTIL PS-END-OF-FILE.                                              
032000     IF PS-DATA-PRESENT                                                   
032000         PERFORM 2200-PRINT-GMM-TOTAL-LINE                                
032100         PERFORM 3000-PRINT-COR-TOTAL-LINE                                
032000     END-IF.                                                              
032200     PERFORM 3100-PRINT-END-OF-RPT-LINE.                                  
032300                                                                          
032400     CLOSE DMM-SUMMARY-FILE                                               
032500           DMM-SUMMARY-RPT-FILE.                                          
032600                                                                          
032800     STOP RUN.                                                            
032900                                                                          
033000 EJECT                                                                    
033100******************************************************************        
033200** READ THE DB2 CONTROL TABLE AND READ THE FIRST ASN EXTRACT REC *        
033300******************************************************************        
033400 1000-INITIALIZATION.                                                     
033500                                                                          
033600     OPEN INPUT  DMM-SUMMARY-FILE                                         
033700          OUTPUT DMM-SUMMARY-RPT-FILE.                                    
033800                                                                          
121098     ACCEPT CC-CONTROL-CARD.                                              
121098                                                                          
033900     ACCEPT PV-CURR-DATE-YYMMDD FROM DATE.                                
033910                                                                          
033920     IF PV-CURR-DATE-YY > 80                                              
033930         MOVE 19 TO PV-CURR-DATE-CC                                       
033940     ELSE                                                                 
033950         MOVE 20 TO PV-CURR-DATE-CC                                       
033960     END-IF.                                                              
034000                                                                          
034100     ACCEPT PV-CURR-TIME FROM TIME.                                       
034200                                                                          
034300     MOVE PV-CURR-DATE-MM        TO DP132O-RUN-MONTH.                     
034400     MOVE PV-CURR-DATE-DD        TO DP132O-RUN-DAY.                       
034500     MOVE PV-CURR-DATE-YY        TO DP132O-RUN-YEAR.                      
034660                                                                          
034700     MOVE PV-CURR-TIME-HH        TO DP132O-RUN-HOUR.                      
034800     MOVE PV-CURR-TIME-MM        TO DP132O-RUN-MINUTE.                    
034900     MOVE PC-CURRENT-PROGRAM-NAME                                         
035000                                 TO DP132O-PROGRAM-NAME.                  
035001                                                                          
121498     PERFORM 1200-FORMAT-HEADING.                                         
035020                                                                          
035100     PERFORM 7000-READ-INPUT.                                             
035200                                                                          
035210     IF PS-END-OF-FILE                                                    
035600         PERFORM 8000-PRINT-HEADINGS                                      
035211         MOVE EMPTY-REPORT-LINE  TO PV-PRINT-RECORD                       
035212         MOVE +2                 TO PV-LINE-SPACING                       
035213         PERFORM 8200-WRITE-REPORT-LINE                                   
035213         SET PS-DATA-NOT-PRESENT TO TRUE                                  
035214     ELSE                                                                 
035300         MOVE RC044-GMM-NBR          TO SAVE-GMM-NBR                      
035400         MOVE RC044-GMM-NAME         TO SAVE-GMM-NAME                     
035600         PERFORM 8000-PRINT-HEADINGS                                      
035500     END-IF.                                                              
035510                                                                          
035601                                                                          
121498 1200-FORMAT-HEADING.                                                     
035620******************************************************************        
035630*    DETERMINE DATE RANGE AND FORMAT HEADING.                             
035640******************************************************************        
           IF CC-VALID-REPORT-TYPE                                              
              CONTINUE                                                          
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM = RCKRP026 **'                               
               DISPLAY '** PARAGRAPH = 1200-FORMAT-HEADING. **'                 
               DISPLAY '** INVALID REPORT TYPE ***'                             
               DISPLAY '** REPORT TYPE= ' CC-REPORT-TYPE                        
               MOVE +4003 TO PV-ABEND-CODE                                      
               CALL 'ILBOABN0' USING PV-ABEND-CODE                              
           END-IF.                                                              
           SET DPG51-FISCAL-454-CALENDAR                                        
                                        TO TRUE                                 
           MOVE PV-CURR-DATE-CCYYMMDD   TO DPG52-LK-DATE-INPUT                  
           SET DPG52-LK-DTE-ISO         TO TRUE                                 
           IF CC-WEEKLY-REPORT                                                  
               SET DPG51-DECREMENT-DATE TO TRUE                                 
               MOVE 7                   TO DPG51-INCR-DECR-DAYS-9               
           ELSE                                                                 
               SET DPG51-DO-NOT-INCR-DECR-DATE                                  
                                        TO TRUE                                 
           END-IF.                                                              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
                                                   DPG54 DPG55 DPG56            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   CONTINUE                                                     
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM = RCKRP026 -- CALLED MODULE '            
                           'DPKUT500 **'                                        
                   DISPLAY '** PARAGRAPH = 1200-FORMAT-HEADING **'              
                   DISPLAY '** DATE INVALID: ' PV-CURR-DATE-CCYYMMDD            
                   DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4003 TO PV-ABEND-CODE                                  
                   CALL 'ILBOABN0' USING PV-ABEND-CODE                          
               WHEN OTHER                                                       
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM = RCKRP026 -- CALLED MODULE '            
                           'DPKUT500 **'                                        
                   DISPLAY '** PARAGRAPH = 1200-FORMAT HEADING **'              
                   DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4003 TO PV-ABEND-CODE                                  
                   CALL 'ILBOABN0' USING PV-ABEND-CODE                          
           END-EVALUATE.                                                        
                                                                                
           IF CC-WEEKLY-REPORT                                                  
               MOVE DPG55-1ST-DAY-OF-WEEK      TO HL1-START-DATE                
               MOVE DPG55-LAST-DAY-OF-WEEK     TO HL1-END-DATE                  
           ELSE                                                                 
               PERFORM 1210-PREVIOUS-FISCAL-PRD                                 
           END-IF.                                                              
                                                                                
       1210-PREVIOUS-FISCAL-PRD.                                                
           MOVE DPG56-FISCAL-YEAR-CCYY  TO PV-FISCAL-PRD-CCYY                   
           MOVE DPG56-FISCAL-PERIOD-NBR TO PV-FISCAL-PRD-PP                     
           MOVE PV-FISCAL-PRD-CCYYPP    TO DPG52-LK-DATE-INPUT                  
           SET DPG52-LK-DTE-FISC-PRD-CC TO TRUE                                 
           MOVE 1                       TO DPG51-INCR-DECR-FISCAL-PRD-9         
           MOVE 0                       TO DPG51-INCR-DECR-DAYS-9               
           SET DPG51-DECREMENT-DATE TO TRUE                                     
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
                                                   DPG54 DPG55 DPG56            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   CONTINUE                                                     
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM = RCKRP026 -- CALLED MODULE '            
                           'DPKUT500 **'                                        
                   DISPLAY '** PARAGRAPH = 1210-PREVIOUS-FISCAL-PRD **'         
                   DISPLAY '** DATE INVALID: ' PV-CURR-DATE-CCYYMMDD            
                   DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4003 TO PV-ABEND-CODE                                  
                   CALL 'ILBOABN0' USING PV-ABEND-CODE                          
               WHEN OTHER                                                       
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM = RCKRP026 -- CALLED MODULE '            
                           'DPKUT500 **'                                        
                   DISPLAY '** PARAGRAPH = 1210-PREVIOUS-FISCAL-PRD **'         
                   DISPLAY '** ERROR MESSAGE FROM CALLED MODULE: '              
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4003 TO PV-ABEND-CODE                                  
                   CALL 'ILBOABN0' USING PV-ABEND-CODE                          
               END-EVALUATE.                                                    
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO HL1-START-DATE                    
           MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO HL1-END-DATE.                     
                                                                                
035710 EJECT                                                                    
035800******************************************************************        
035900*  PROCESS THE DMM SUMMARY REPORT FILE.                          *        
036000*                                                                *        
036100******************************************************************        
036200 2000-PROCESS-SUMMARY-RECS.                                               
036300                                                                          
036400     IF RC044-GMM-NBR = SAVE-GMM-NBR                                      
036500         MOVE +2             TO PV-LINE-SPACING                           
036600         PERFORM 2100-PRINT-DETAIL-LINE                                   
036700     ELSE                                                                 
036800         PERFORM 2200-PRINT-GMM-TOTAL-LINE                                
036900         MOVE RC044-GMM-NBR  TO SAVE-GMM-NBR                              
037000         MOVE RC044-GMM-NAME TO SAVE-GMM-NAME                             
037100         MOVE +3             TO PV-LINE-SPACING                           
037200         PERFORM 2100-PRINT-DETAIL-LINE                                   
037300     END-IF.                                                              
037400                                                                          
037500     PERFORM 2300-ACCUM-FIELDS.                                           
037600                                                                          
037700     PERFORM 7000-READ-INPUT.                                             
037800                                                                          
037900 2100-PRINT-DETAIL-LINE.                                                  
038000******************************************************************        
038100*  THIS PARAGRAPH WILL PRINT THE DMM SUMMARY REPORT DETAIL LINE. *        
038200*  NOTE:  PV-LINE-SPACING IS SET BY CALLING PARAGRAPH.           *        
038300*         IF PAGE BREAK OCCURS, PV-LINE-SPACING IS SET HERE.     *        
038400******************************************************************        
038500                                                                          
038600*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
038700*** IS THERE ROOM FOR DETAIL LINE AND PERCENT LINE ON SAME PAGE?          
038800*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
038900     MOVE PV-LINE-CNT         TO PV-LINE-CNT-AHEAD.                       
039000     ADD  PV-LINE-SPACING     TO PV-LINE-CNT-AHEAD.                       
039100     ADD  +1                  TO PV-LINE-CNT-AHEAD.                       
039200     IF PV-LINE-CNT-AHEAD > PC-LINE-LIMIT                                 
039300         PERFORM 8000-PRINT-HEADINGS                                      
039400         MOVE +2              TO PV-LINE-SPACING                          
039500     END-IF.                                                              
039600                                                                          
039700     MOVE RC044-DMM-NBR       TO DL-DMM-NBR.                              
039800     MOVE RC044-VRFYD-QTY     TO DL-VRFYD-QTY.                            
039900     MOVE RC044-GOH-QTY       TO DL-GOH-QTY.                              
040000     MOVE RC044-PRETKT-QTY    TO DL-PRETKT-QTY.                           
040100     MOVE RC044-SINGLE-QTY    TO DL-SINGLE-QTY.                           
040200     MOVE RC044-MULTI-QTY     TO DL-MULTI-QTY.                            
040300     MOVE RC044-INNER-QTY     TO DL-INNER-QTY.                            
040400     MOVE RC044-BULK-QTY      TO DL-BULK-QTY.                             
040500     MOVE RC044-CASE-QTY      TO DL-CASE-QTY.                             
040600     MOVE RC044-PBS-QTY       TO DL-PBS-QTY.                              
040700                                                                          
040800     MOVE DL-DETAIL-LINE      TO PV-PRINT-RECORD.                         
040900     PERFORM 8200-WRITE-REPORT-LINE.                                      
041000                                                                          
041100     MOVE RC044-VRFYD-QTY     TO PV-WRK-TOTAL-QTY.                        
039900     MOVE RC044-GOH-QTY       TO PV-WRK-GOH-QTY.                          
041200     MOVE RC044-PRETKT-QTY    TO PV-WRK-PRETKT-QTY.                       
041300     MOVE RC044-SINGLE-QTY    TO PV-WRK-SINGLE-QTY.                       
041400     MOVE RC044-MULTI-QTY     TO PV-WRK-MULTI-QTY.                        
041500     MOVE RC044-INNER-QTY     TO PV-WRK-INNER-QTY.                        
041600     MOVE RC044-BULK-QTY      TO PV-WRK-BULK-QTY.                         
041700     MOVE RC044-CASE-QTY      TO PV-WRK-CASE-QTY.                         
041800     MOVE RC044-PBS-QTY       TO PV-WRK-PBS-QTY.                          
041900                                                                          
039700     MOVE RC044-DMM-NAME      TO PL-DMM-NAME.                             
042000     PERFORM 5000-PRINT-PERCENT-LINE.                                     
042100                                                                          
      * * *  TOTAL FOR CROSS-DOCKABLE                                           
LF0114     MOVE RC044-CROSS-DOCK-QTY TO TL4-GROUP-TOTAL-1                       
LF0114                                  PV-PARTIAL-QTY.                         
           IF PV-PARTIAL-QTY > ZERO                                             
056500         PERFORM 5010-CALCULATE-PERCENT                                   
056600         MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-1                     
           ELSE                                                                 
056600         MOVE ZERO                TO TL4-GROUP-PCNT-1                     
056700     END-IF.                                                              
                                                                                
      * * *  TOTAL FOR NON CROSS-DOCKABLE                                       
LF0114     MOVE RC044-NON-CROSS-DOCK-QTY TO TL4-GROUP-TOTAL-2                   
LF0114                                      PV-PARTIAL-QTY.                     
           IF PV-PARTIAL-QTY > ZERO                                             
056500         PERFORM 5010-CALCULATE-PERCENT                                   
056600         MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-2                     
           ELSE                                                                 
056600         MOVE ZERO                TO TL4-GROUP-PCNT-2                     
056700     END-IF.                                                              
                                                                                
040800     MOVE TL4-GROUP-TOTAL-LINE    TO PV-PRINT-RECORD.                     
040900     PERFORM 8200-WRITE-REPORT-LINE.                                      
                                                                                
                                                                                
042200 2200-PRINT-GMM-TOTAL-LINE.                                               
042300******************************************************************        
042400*  THIS PARAGRAPH WILL PRINT THE GMM TOTAL LINES.                *        
042500******************************************************************        
042600                                                                          
042700*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
042800*** IS THERE ROOM FOR TOTAL LINES AND PERCENT LINE ON SAME PAGE?          
042900*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
043000     MOVE PV-LINE-CNT           TO PV-LINE-CNT-AHEAD.                     
043100     ADD +5                     TO PV-LINE-CNT-AHEAD.                     
043200     IF PV-LINE-CNT-AHEAD > PC-LINE-LIMIT                                 
043300         PERFORM 8000-PRINT-HEADINGS                                      
043400     END-IF.                                                              
043500                                                                          
043600     MOVE SAVE-GMM-NAME        TO TL1-GMM-NAME.                           
043700     MOVE PV-GMM-VRFYD-QTY     TO TL2-VRFYD-QTY.                          
043800     MOVE PV-GMM-GOH-QTY       TO TL2-GOH-QTY.                            
043900     MOVE PV-GMM-PRETKT-QTY    TO TL2-PRETKT-QTY.                         
044000     MOVE PV-GMM-SINGLE-QTY    TO TL2-SINGLE-QTY.                         
044100     MOVE PV-GMM-MULTI-QTY     TO TL2-MULTI-QTY.                          
044200     MOVE PV-GMM-INNER-QTY     TO TL2-INNER-QTY.                          
044300     MOVE PV-GMM-BULK-QTY      TO TL2-BULK-QTY.                           
044400     MOVE PV-GMM-CASE-QTY      TO TL2-CASE-QTY.                           
044500     MOVE PV-GMM-PBS-QTY       TO TL2-PBS-QTY.                            
044600                                                                          
044700     MOVE TL1-TOTAL-LINE-1     TO PV-PRINT-RECORD.                        
044800     MOVE +2                   TO PV-LINE-SPACING.                        
044900     PERFORM 8200-WRITE-REPORT-LINE.                                      
045000                                                                          
045100     MOVE TL2-TOTAL-LINE-2     TO PV-PRINT-RECORD.                        
045200     MOVE +1                   TO PV-LINE-SPACING.                        
045300     PERFORM 8200-WRITE-REPORT-LINE.                                      
045400                                                                          
045500     MOVE PV-GMM-VRFYD-QTY     TO PV-WRK-TOTAL-QTY.                       
045600     MOVE PV-GMM-PRETKT-QTY    TO PV-WRK-PRETKT-QTY.                      
           MOVE PV-GMM-GOH-QTY       TO PV-WRK-GOH-QTY.                         
045700     MOVE PV-GMM-SINGLE-QTY    TO PV-WRK-SINGLE-QTY.                      
045800     MOVE PV-GMM-MULTI-QTY     TO PV-WRK-MULTI-QTY.                       
045900     MOVE PV-GMM-INNER-QTY     TO PV-WRK-INNER-QTY.                       
046000     MOVE PV-GMM-BULK-QTY      TO PV-WRK-BULK-QTY.                        
046100     MOVE PV-GMM-CASE-QTY      TO PV-WRK-CASE-QTY.                        
046200     MOVE PV-GMM-PBS-QTY       TO PV-WRK-PBS-QTY.                         
046300                                                                          
046400     PERFORM 5000-PRINT-PERCENT-LINE.                                     
042100                                                                          
      * * *  TOTAL FOR CROSS-DOCKABLE                                           
LF0114     MOVE PV-GMM-CROSS-DOCK-QTY   TO TL4-GROUP-TOTAL-1                    
LF0114                                     PV-PARTIAL-QTY.                      
           IF PV-PARTIAL-QTY > ZERO                                             
               PERFORM 5010-CALCULATE-PERCENT                                   
               MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-1                     
           ELSE                                                                 
               MOVE ZERO                TO TL4-GROUP-PCNT-1                     
           END-IF.                                                              
                                                                                
      * * *  TOTAL FOR NON CROSS-DOCKABLE                                       
LF0114     MOVE PV-GMM-NON-CROSS-DK-QTY TO TL4-GROUP-TOTAL-2                    
LF0114                                     PV-PARTIAL-QTY.                      
           IF PV-PARTIAL-QTY > ZERO                                             
               PERFORM 5010-CALCULATE-PERCENT                                   
               MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-2                     
           ELSE                                                                 
               MOVE ZERO                TO TL4-GROUP-PCNT-2                     
           END-IF.                                                              
                                                                                
040800     MOVE TL4-GROUP-TOTAL-LINE    TO PV-PRINT-RECORD.                     
040900     PERFORM 8200-WRITE-REPORT-LINE.                                      
046600                                                                          
046500     INITIALIZE PV-GMM-ACCUM-FIELDS.                                      
                                                                                
046700 2300-ACCUM-FIELDS.                                                       
046800******************************************************************        
046900*  THIS PARAGRAPH ADDS DMM TOTALS TO THE GMM AND GRAND TOTALS.   *        
047000******************************************************************        
047100                                                                          
047200     ADD RC044-VRFYD-QTY             TO PV-GMM-VRFYD-QTY                  
047300                                        PV-COR-VRFYD-QTY.                 
047400     ADD RC044-GOH-QTY               TO PV-GMM-GOH-QTY                    
047500                                        PV-COR-GOH-QTY.                   
047600     ADD RC044-PRETKT-QTY            TO PV-GMM-PRETKT-QTY                 
047700                                        PV-COR-PRETKT-QTY.                
047800     ADD RC044-SINGLE-QTY            TO PV-GMM-SINGLE-QTY                 
047900                                        PV-COR-SINGLE-QTY.                
048000     ADD RC044-MULTI-QTY             TO PV-GMM-MULTI-QTY                  
048100                                        PV-COR-MULTI-QTY.                 
048200     ADD RC044-INNER-QTY             TO PV-GMM-INNER-QTY                  
048300                                        PV-COR-INNER-QTY.                 
048400     ADD RC044-BULK-QTY              TO PV-GMM-BULK-QTY                   
048500                                        PV-COR-BULK-QTY.                  
048600     ADD RC044-CASE-QTY              TO PV-GMM-CASE-QTY                   
048700                                        PV-COR-CASE-QTY.                  
048800     ADD RC044-PBS-QTY               TO PV-GMM-PBS-QTY                    
048900                                        PV-COR-PBS-QTY.                   
LF0114     ADD RC044-CROSS-DOCK-QTY        TO PV-GMM-CROSS-DOCK-QTY             
LF0114                                        PV-COR-CROSS-DOCK-QTY.            
LF0114     ADD RC044-NON-CROSS-DOCK-QTY    TO PV-GMM-NON-CROSS-DK-QTY           
LF0114                                        PV-COR-NON-CROSS-DK-QTY.          
049000                                                                          
049100 3000-PRINT-COR-TOTAL-LINE.                                               
049200******************************************************************        
049300*  THIS PARAGRAPH WILL PRINT THE GRAND TOTAL LINES.              *        
049400******************************************************************        
049500                                                                          
049600*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
049700*** IS THERE ROOM FOR TOTAL LINES AND PERCENT LINE ON SAME PAGE?          
049800*** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
049900     MOVE PV-LINE-CNT           TO PV-LINE-CNT-AHEAD.                     
050000     ADD +5                     TO PV-LINE-CNT-AHEAD.                     
050100     IF PV-LINE-CNT-AHEAD > PC-LINE-LIMIT                                 
050200         PERFORM 8000-PRINT-HEADINGS                                      
050300     END-IF.                                                              
050400                                                                          
050500     MOVE PV-COR-VRFYD-QTY     TO TL2-VRFYD-QTY.                          
050600     MOVE PV-COR-GOH-QTY       TO TL2-GOH-QTY.                            
050700     MOVE PV-COR-PRETKT-QTY    TO TL2-PRETKT-QTY.                         
050800     MOVE PV-COR-SINGLE-QTY    TO TL2-SINGLE-QTY.                         
050900     MOVE PV-COR-MULTI-QTY     TO TL2-MULTI-QTY.                          
051000     MOVE PV-COR-INNER-QTY     TO TL2-INNER-QTY.                          
051100     MOVE PV-COR-BULK-QTY      TO TL2-BULK-QTY.                           
051200     MOVE PV-COR-CASE-QTY      TO TL2-CASE-QTY.                           
051300     MOVE PV-COR-PBS-QTY       TO TL2-PBS-QTY.                            
051400                                                                          
051500     MOVE TL3-TOTAL-LINE-3     TO PV-PRINT-RECORD.                        
051600     MOVE +2                   TO PV-LINE-SPACING.                        
051700     PERFORM 8200-WRITE-REPORT-LINE.                                      
051800                                                                          
051900     MOVE TL2-TOTAL-LINE-2     TO PV-PRINT-RECORD.                        
052000     MOVE +1                   TO PV-LINE-SPACING.                        
052100     PERFORM 8200-WRITE-REPORT-LINE.                                      
052200                                                                          
052300     MOVE PV-COR-VRFYD-QTY     TO PV-WRK-TOTAL-QTY.                       
           MOVE PV-COR-GOH-QTY       TO PV-WRK-GOH-QTY.                         
   400     MOVE PV-COR-PRETKT-QTY    TO PV-WRK-PRETKT-QTY.                      
052500     MOVE PV-COR-SINGLE-QTY    TO PV-WRK-SINGLE-QTY.                      
052600     MOVE PV-COR-MULTI-QTY     TO PV-WRK-MULTI-QTY.                       
052700     MOVE PV-COR-INNER-QTY     TO PV-WRK-INNER-QTY.                       
052800     MOVE PV-COR-BULK-QTY      TO PV-WRK-BULK-QTY.                        
052900     MOVE PV-COR-CASE-QTY      TO PV-WRK-CASE-QTY.                        
053000     MOVE PV-COR-PBS-QTY       TO PV-WRK-PBS-QTY.                         
053100                                                                          
053200     PERFORM 5000-PRINT-PERCENT-LINE.                                     
053300                                                                          
      * * *  TOTAL FOR CROSS-DOCKABLE                                           
LF0114     MOVE PV-COR-CROSS-DOCK-QTY   TO TL4-GROUP-TOTAL-1                    
LF0114                                     PV-PARTIAL-QTY.                      
           IF PV-PARTIAL-QTY > ZERO                                             
               PERFORM 5010-CALCULATE-PERCENT                                   
               MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-1                     
           ELSE                                                                 
               MOVE ZERO                TO TL4-GROUP-PCNT-1                     
           END-IF.                                                              
                                                                                
      * * *  TOTAL FOR NON CROSS-DOCKABLE                                       
LF0114     MOVE PV-COR-NON-CROSS-DK-QTY TO TL4-GROUP-TOTAL-2                    
LF0114                                     PV-PARTIAL-QTY.                      
           IF PV-PARTIAL-QTY > ZERO                                             
               PERFORM 5010-CALCULATE-PERCENT                                   
               MOVE  PV-PERCENT-SHIFTED TO TL4-GROUP-PCNT-2                     
           ELSE                                                                 
               MOVE ZERO                TO TL4-GROUP-PCNT-2                     
           END-IF.                                                              
                                                                                
040800     MOVE TL4-GROUP-TOTAL-LINE    TO PV-PRINT-RECORD.                     
040900     PERFORM 8200-WRITE-REPORT-LINE.                                      
046600                                                                          
053400 3100-PRINT-END-OF-RPT-LINE.                                              
053500******************************************************************        
053600*  THIS PARAGRAPH WILL PRINT THE 'END OF REPORT' LINE.           *        
053700******************************************************************        
053800     IF  PV-LINE-CNT > PC-LINE-LIMIT                                      
053900         PERFORM 8000-PRINT-HEADINGS                                      
054000     END-IF.                                                              
054100                                                                          
054200     MOVE END-OF-REPORT-LINE     TO PV-PRINT-RECORD.                      
054300     MOVE +2                     TO PV-LINE-SPACING.                      
054400     PERFORM 8200-WRITE-REPORT-LINE.                                      
054500                                                                          
054600 EJECT                                                                    
054700 5000-PRINT-PERCENT-LINE.                                                 
054800******************************************************************        
054900*  CALCULATE PERCENTAGES OF ACCUMULATED TOTALS.                  *        
055000*  FORMAT THE PERCENT LINE WITH CALCULATED RESULTS.              *        
055100*  PERFORM PARAGRAPH TO PRINT THE PERCENT LINE.                  *        
055200******************************************************************        
055300                                                                          
055400     MOVE ZEROES                      TO PL-PRE-TKT-PCNT                  
055500                                         PL-SINGLE-PCNT                   
055600                                         PL-MULTI-PCNT                    
055700                                         PL-INNER-PCNT                    
055800                                         PL-BULK-PCNT                     
055900                                         PL-CASE-PCNT                     
056000                                         PL-PBS-PCNT                      
056000                                         PL-GOH-PCNT.                     
056100                                                                          
056200     IF PV-WRK-TOTAL-QTY > ZERO                                           
056300         IF PV-WRK-PRETKT-QTY > ZERO                                      
056400             MOVE  PV-WRK-PRETKT-QTY  TO PV-PARTIAL-QTY                   
056500             PERFORM 5010-CALCULATE-PERCENT                               
056600             MOVE  PV-PERCENT-SHIFTED TO PL-PRE-TKT-PCNT                  
056700         END-IF                                                           
056300         IF PV-WRK-GOH-QTY > ZERO                                         
056400             MOVE  PV-WRK-GOH-QTY     TO PV-PARTIAL-QTY                   
056500             PERFORM 5010-CALCULATE-PERCENT                               
056600             MOVE  PV-PERCENT-SHIFTED TO PL-GOH-PCNT                      
056700         END-IF                                                           
056800         IF PV-WRK-SINGLE-QTY > ZERO                                      
056900             MOVE  PV-WRK-SINGLE-QTY  TO PV-PARTIAL-QTY                   
057000             PERFORM 5010-CALCULATE-PERCENT                               
057100             MOVE  PV-PERCENT-SHIFTED TO PL-SINGLE-PCNT                   
057200         END-IF                                                           
057300         IF PV-WRK-MULTI-QTY > ZERO                                       
057400             MOVE  PV-WRK-MULTI-QTY   TO PV-PARTIAL-QTY                   
057500             PERFORM 5010-CALCULATE-PERCENT                               
057600             MOVE  PV-PERCENT-SHIFTED TO PL-MULTI-PCNT                    
057700         END-IF                                                           
057800         IF PV-WRK-INNER-QTY > ZERO                                       
057900             MOVE  PV-WRK-INNER-QTY   TO PV-PARTIAL-QTY                   
058000             PERFORM 5010-CALCULATE-PERCENT                               
058100             MOVE  PV-PERCENT-SHIFTED TO PL-INNER-PCNT                    
058200         END-IF                                                           
058300         IF PV-WRK-BULK-QTY > ZERO                                        
058400             MOVE  PV-WRK-BULK-QTY    TO PV-PARTIAL-QTY                   
058500             PERFORM 5010-CALCULATE-PERCENT                               
058600             MOVE  PV-PERCENT-SHIFTED TO PL-BULK-PCNT                     
058700         END-IF                                                           
058800         IF PV-WRK-CASE-QTY > ZERO                                        
058900             MOVE  PV-WRK-CASE-QTY    TO PV-PARTIAL-QTY                   
059000             PERFORM 5010-CALCULATE-PERCENT                               
059100             MOVE  PV-PERCENT-SHIFTED TO PL-CASE-PCNT                     
059200         END-IF                                                           
059300         IF PV-WRK-PBS-QTY > ZERO                                         
059400             MOVE  PV-WRK-PBS-QTY     TO PV-PARTIAL-QTY                   
059500             PERFORM 5010-CALCULATE-PERCENT                               
059600             MOVE  PV-PERCENT-SHIFTED TO PL-PBS-PCNT                      
059700         END-IF                                                           
059800     END-IF.                                                              
059900                                                                          
060000     MOVE PL-PERCENT-LINE             TO PV-PRINT-RECORD.                 
060100     MOVE +1                          TO PV-LINE-SPACING.                 
060200     PERFORM 8200-WRITE-REPORT-LINE.                                      
060300                                                                          
039700     MOVE SPACES                      TO PL-DMM-NAME.                     
060400                                                                          
060500 5010-CALCULATE-PERCENT.                                                  
060600******************************************************************        
060700*  CALCULATE A PERCENTAGE OF A TOTAL QUANTITY                    *        
060800******************************************************************        
060900                                                                          
061000     COMPUTE PV-PERCENT-DECIMAL ROUNDED =                                 
061100         PV-PARTIAL-QTY / PV-WRK-TOTAL-QTY.                               
061200                                                                          
061300     COMPUTE PV-PERCENT-SHIFTED =                                         
061400         PV-PERCENT-DECIMAL * 100.                                        
061500                                                                          
061600                                                                          
061700******************************************************************        
061800*  READ THE ASN EXTRACT RECORDS.                                 *        
061900******************************************************************        
062000 7000-READ-INPUT.                                                         
062100                                                                          
062200     READ DMM-SUMMARY-FILE                                                
062300       INTO RC044-REC                                                     
062400         AT END                                                           
062500           SET PS-END-OF-FILE TO TRUE.                                    
062600                                                                          
062700 EJECT                                                                    
062800 8000-PRINT-HEADINGS.                                                     
062900******************************************************************        
063000*  PRINT ALL REPORT HEADING LINES.                               *        
063100******************************************************************        
063200                                                                          
063300     ADD 1                        TO PV-PAGE-CNT.                         
063400                                                                          
063500     MOVE PV-PAGE-CNT             TO DP132O-PAGE-NUMBER.                  
063600                                                                          
063700     MOVE PC-REPORT-NBR-1        TO DP132O-REPORT-NUMBER.                 
063800     MOVE DP132O-STANDRD-HEADER-132-COLS                                  
063900                                 TO PV-PRINT-RECORD.                      
064000     PERFORM 8100-WRITE-REPORT-TOP-OF-PAGE.                               
064100                                                                          
064200     MOVE HL0-HEADING-LINE-0     TO PV-PRINT-RECORD.                      
064300     MOVE +1                     TO PV-LINE-SPACING.                      
064400     PERFORM 8200-WRITE-REPORT-LINE.                                      
064500                                                                          
064600     MOVE HL1-HEADING-LINE-1     TO PV-PRINT-RECORD.                      
064700     MOVE +1                     TO PV-LINE-SPACING.                      
064800     PERFORM 8200-WRITE-REPORT-LINE.                                      
064900                                                                          
064600     MOVE HL2-HEADING-LINE-2     TO PV-PRINT-RECORD.                      
064700     MOVE +2                     TO PV-LINE-SPACING.                      
064800     PERFORM 8200-WRITE-REPORT-LINE.                                      
064900                                                                          
065000     MOVE HL3-HEADING-LINE-3     TO PV-PRINT-RECORD.                      
065100     MOVE +1                     TO PV-LINE-SPACING.                      
065200     PERFORM 8200-WRITE-REPORT-LINE.                                      
065300                                                                          
065400     MOVE HL4-HEADING-LINE-4     TO PV-PRINT-RECORD.                      
065500     MOVE +1                     TO PV-LINE-SPACING.                      
065600     PERFORM 8200-WRITE-REPORT-LINE.                                      
065700                                                                          
065800                                                                          
065900 8100-WRITE-REPORT-TOP-OF-PAGE.                                           
066000******************************************************************        
066100*  WRITE FIRST REPORT LINE AFTER PAGE BREAK.                     *        
066200******************************************************************        
066300                                                                          
066400     WRITE DMM-SUMMARY-RPT-RECORD                                         
066500       FROM PV-PRINT-RECORD                                               
066600         AFTER ADVANCING PAGE.                                            
066700     MOVE +1             TO PV-LINE-CNT.                                  
066800                                                                          
066900 8200-WRITE-REPORT-LINE.                                                  
067000******************************************************************        
067100*  WRITE REPORT LINE AFTER ADVANCING LINE SPACING.               *        
067200******************************************************************        
067300                                                                          
067400     WRITE DMM-SUMMARY-RPT-RECORD                                         
067500       FROM PV-PRINT-RECORD                                               
067600         AFTER ADVANCING PV-LINE-SPACING.                                 
067700                                                                          
067800     ADD PV-LINE-SPACING TO PV-LINE-CNT.                                  
067900                                                                          
