000010*****************************************************************         
000020 IDENTIFICATION DIVISION.                                                 
000030*****************************************************************         
000040                                                                          
000050 PROGRAM-ID.             SUKUT184.                                        
000060 AUTHOR.                 CHERI PARMLEY.                                   
000070 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000080 DATE-WRITTEN            FEBRUARY, 2017.                                  
000090 DATE-COMPILED.                                                           
000091*****************************************************************         
000092*****************************************************************         
000094*  THIS PROGRAM BUMP THE TRIP-CARTON FILE WITH THE MASTER-CHILD *         
000095*  CARTON FILE. IT WILL INCLUDE THE MASTER(PARENT) ON THE 2               
 00095*  OUTPUT FILES GOING EITHER TO OMS OR MLS.                               
 00095*  THE DRIVER FILE WILL BE THE MASTER (SURD142) FILE                      
000097*****************************************************************         
      *  06/14/2019 COGNIZANT       MOVING VALUE TO THE NEWLY ADDED             
      *                             FIELD OF011-OPC-SET-IND IN OMS OUT          
      *                             FILE FROM TRIP EXTRACT INPUT FILE.          
      *                                                                         
      *                             CHG: CHG0227744                             
000120*****************************************************************         
000130 ENVIRONMENT DIVISION.                                                    
000140*****************************************************************         
000150                                                                          
000160 CONFIGURATION SECTION.                                                   
000170                                                                          
000180 SOURCE-COMPUTER.        IBM-3090.                                        
000190 OBJECT-COMPUTER.        IBM-3090.                                        
000191                                                                          
000192 INPUT-OUTPUT SECTION.                                                    
000193                                                                          
000194 FILE-CONTROL.                                                            
000198*                                                                         
000199     SELECT TRIP-EXTRACT-FILE                                             
000200         ASSIGN TO INP01.                                                 
000201                                                                          
000202     SELECT MASTER-EXTRACT-FILE                                           
000203         ASSIGN TO INP02.                                                 
000201                                                                          
000202     SELECT OMS-MERGE-FILE                                                
000203         ASSIGN TO OUT01.                                                 
000210                                                                          
000202     SELECT MLS-MERGE-FILE                                                
000203         ASSIGN TO OUT02.                                                 
000220*****************************************************************         
000230 DATA DIVISION.                                                           
000240*****************************************************************         
000250*                                                                         
000260 FILE SECTION.                                                            
000270*                                                                         
000296 FD  TRIP-EXTRACT-FILE                                                    
000297     RECORDING MODE IS F                                                  
000298     BLOCK CONTAINS 0  RECORDS.                                           
000299 01  TRIP-EXTRACT-RECORD             PIC  X(200).                         
000300                                                                          
000301 FD  MASTER-EXTRACT-FILE                                                  
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  MASTER-EXTRACT-RECORD           PIC  X(30).                          
000300                                                                          
000301 FD  OMS-MERGE-FILE                                                       
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  OMS-MERGE-RECORD                PIC  X(200).                         
000300                                                                          
000301 FD  MLS-MERGE-FILE                                                       
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304*01  MLS-MERGE-RECORD                PIC  X(100).                         
000304 01  MLS-MERGE-RECORD                PIC  X(110).                         
000305*                                                                         
000310                                                                          
000320 WORKING-STORAGE SECTION.                                                 
000321*                                                                         
000322 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
000323                                                   COMP SYNC.             
000331*                                                                         
000340 01  PROGRAM-SWITCHES.                                                    
000362     05  PS-EOF-TRIP-EXTRACT-SW     PIC  X(01)    VALUE 'N'.              
000363         88  PS-EOF-TRIP-EXTRACT                  VALUE 'Y'.              
000364         88  PS-NOT-EOF-TRIP-EXTRACT             VALUE 'N'.               
000362     05  PS-EOF-MASTER-EXTRACT-SW  PIC  X(01)    VALUE 'N'.               
000363         88  PS-EOF-MASTER-EXTRACT               VALUE 'Y'.               
000364         88  PS-NOT-EOF-MASTER-EXTRACT           VALUE 'N'.               
000362     05  PS-FIRST-TIME-SW          PIC  X(01)    VALUE 'Y'.               
000363         88  PS-FIRST-TIME                       VALUE 'Y'.               
000364         88  PS-NOT-FIRST-TIME                   VALUE 'N'.               
000409*                                                                         
000410 01  PC-CONSTANTS.                                                        
000431     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
000432         'SUKUT184'.                                                      
           05  PC-ASN                      PIC  X(03)    VALUE 'ASN'.           
           05  PC-CKN                      PIC  X(03)    VALUE 'CKN'.           
           05  PC-MST                      PIC  X(03)    VALUE 'MST'.           
000440*                                                                         
000450 01  PV-MISC-FIELDS.                                                      
000495     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
000496                                                   COMP SYNC.             
000497     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
000498     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
000499     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
000503     05  PV-MSTR-COUNT                   PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
000503     05  PV-TRIP-COUNT                   PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
000503     05  PV-OMS-COUNT                    PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
000503     05  PV-MLS-COUNT                    PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
000503     05  PV-MSTR-OUT-COUNT               PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
           05  PV-MSTR-KEY.                                                     
               10  PV-MSTR-TRIP-KEY         PIC 9(09).                          
               10  PV-MSTR-SHIP-UNT-KEY     PIC 9(18).                          
           05  PV-TRIP-KEY.                                                     
               10  PV-TRIP-TRIP-KEY         PIC 9(09).                          
               10  PV-TRIP-SHIP-UNT-KEY     PIC 9(18).                          
                                                                                
000801     05  PV-HOLD-DEST-LOC-NBR         PIC  S9(04)  VALUE 0 COMP.          
000802     05  PV-HOLD-OTBND-TRIP-ID        PIC  S9(09)  VALUE 0 COMP.          
000803     05  PV-HOLD-SHIPPED-TMST         PIC   X(26)  VALUE SPACES.          
           05  PV-HOLD-TRLR-SEAL-ID         PIC   X(20)  VALUE SPACES.          
000804     05  PV-HOLD-SML-TYPE             PIC   X(03)  VALUE SPACES.          
           05  PV-HOLD-MSTR-UNT-ID          PIC 9(18)      VALUE 0.             
           05  PV-HOLD-SHIPT-UNT-ID         PIC 9(18)      VALUE 0.             
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE TRIP EXTRACT INPUT FILE                          
000557******************************************************************        
000530     COPY SURD136.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE MASTER EXTRACT INPUT FILE                        
000557******************************************************************        
000530     COPY SURD142.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE OMS MERGE OUTPUT FILE                            
000557******************************************************************        
000530     COPY OFRD011.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE MLS MERGE OUTPUT FILE                            
000557******************************************************************        
000530     COPY SURD103.                                                        
000511                                                                          
001196 EJECT                                                                    
001499*                                                                         
001500 PROCEDURE DIVISION.                                                      
001510*                                                                         
001520 0000-MAINLINE-PROCESSING.                                                
001530******************************************************************        
001540**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
001550******************************************************************        
001580*                                                                         
001590     PERFORM 1000-INITIALIZATION.                                         
001591*                                                                         
           IF PS-NOT-EOF-MASTER-EXTRACT                                         
001592         PERFORM 2000-BUMP-FILES                                          
00                 UNTIL PS-EOF-TRIP-EXTRACT                                    
           ELSE                                                                 
      *        NO RECORDS ON THE MASTER (SURD142) FILE                          
      *        WRITE OUT ALL THE SURD136 RECORDS                                
               PERFORM 7300-WRITE-ALL-OUTPUT                                    
                   UNTIL PS-EOF-TRIP-EXTRACT                                    
           END-IF.                                                              
                                                                                
001590     PERFORM 9000-EOJ-ROUTINE.                                            
001596*                                                                         
001612     GOBACK.                                                              
001613*                                                                         
001614*                                                                         
001615 1000-INITIALIZATION.                                                     
001620******************************************************************        
001630**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
001640******************************************************************        
001650                                                                          
           DISPLAY ' **** START SUKUT184 ****'.                                 
                                                                                
001652     OPEN INPUT  TRIP-EXTRACT-FILE                                        
                       MASTER-EXTRACT-FILE                                      
001653          OUTPUT OMS-MERGE-FILE                                           
001653          OUTPUT MLS-MERGE-FILE.                                          
001690                                                                          
           SET PS-NOT-EOF-TRIP-EXTRACT                                          
               PS-NOT-EOF-MASTER-EXTRACT  TO TRUE                               
                                                                                
001691     PERFORM 6001-READ-MSTR-EXTRACT-FILE.                                 
001691     PERFORM 6000-READ-TRIP-EXTRACT-FILE.                                 
001692                                                                          
001693     IF PS-EOF-MASTER-EXTRACT                                             
001695         DISPLAY '** MASTER EXTRACT IS EMPTY **'                          
001698     END-IF.                                                              
                                                                                
001693     IF PS-EOF-TRIP-EXTRACT                                               
001695         DISPLAY '** TRIP EXTRACT IS EMPTY **'                            
001698     END-IF.                                                              
001692                                                                          
002150*                                                                         
002150****************************************************************          
002160*  2000-BUMP-FILES.                                                       
002160*  MATCH THE RECORDS OF WHAT IS ON THE MASTER/CHILD FILE WITH             
002160*  WHAT IS ON THE TRIP FILE.                                              
002160*  MATCH THE RECORDS AT THE FOLLOWING LEVEL:               .              
002160*  SHIP UNT ID/TRIP ID                                                    
002150****************************************************************          
002160 2000-BUMP-FILES.                                                         
                                                                                
      *    SURD142 - MASTER/CHILD   SURD136 - TRIPS                             
                                                                                
002161     IF PV-MSTR-KEY  <  PV-TRIP-KEY                                       
               PERFORM 6001-READ-MSTR-EXTRACT-FILE                              
           ELSE                                                                 
002161         IF PV-MSTR-KEY  > PV-TRIP-KEY                                    
      *            NEED TO WRITE OUT THE OMS/MLS RECORD WITHOUT MASTER          
      * NEED TO NOT ZERO THIS OUT AND JUST MOVE ZEROS TO THE FILES              
      * NEED TO DO IN 7300- ALSO JUST MOVE ZEROS AFTER 7000 AND 7100            
      * TO THE OF001-MSTR AND SU103-MSTR                                        
      *            MOVE ZERO TO SU142-MSTR-UNT-ID                               
                   IF SU136-OMS-RECORD                                          
                       PERFORM 7000-MOVE-TO-OUTPUT                              
                       MOVE ZERO TO OF011-MSTR-UNT-ID                           
                       PERFORM 8000-WRITE-OMS-MERGE-FILE                        
                       PERFORM 6000-READ-TRIP-EXTRACT-FILE                      
                   ELSE                                                         
                       PERFORM 7100-MOVE-TO-OUTPUT                              
                       MOVE ZERO TO SU103-MSTR-UNT-ID                           
                       PERFORM 8100-WRITE-MLS-MERGE-FILE                        
                       PERFORM 6000-READ-TRIP-EXTRACT-FILE                      
                   END-IF                                                       
               ELSE                                                             
      *            RECORDS ARE EQUAL                                            
      *            AND KEEP READING WILL BE MORE SAME KEYS ON TRIP FILE         
      *            DEPENDING IF MLS OR OMS WRITE TO ONE OF THE FILES            
                   IF SU136-OMS-RECORD                                          
                       PERFORM 7000-MOVE-TO-OUTPUT                              
                       PERFORM 8000-WRITE-OMS-MERGE-FILE                        
                       PERFORM 6000-READ-TRIP-EXTRACT-FILE                      
                   ELSE                                                         
                       PERFORM 7200-CHECK-TO-WRITE-MSTR                         
                       PERFORM 7100-MOVE-TO-OUTPUT                              
                       PERFORM 8100-WRITE-MLS-MERGE-FILE                        
                       PERFORM 6000-READ-TRIP-EXTRACT-FILE                      
                   END-IF                                                       
               END-IF.                                                          
068514                                                                          
068514****************************************************************          
068514* 6000-READ-TRIP-EXTRACT-FILE                                  *          
068514* READ THE NEXT RECORD OF THE TRIP EXTRACT FILE                *          
068514****************************************************************          
068515 6000-READ-TRIP-EXTRACT-FILE.                                             
068517                                                                          
068518     READ TRIP-EXTRACT-FILE                                               
           INTO SU136-EXTRACT-RECORD                                            
068519        AT END                                                            
068520           SET PS-EOF-TRIP-EXTRACT TO TRUE                                
068521     END-READ.                                                            
           IF PS-NOT-EOF-TRIP-EXTRACT                                           
               MOVE SU136-OTBND-TRIP-ID TO PV-TRIP-TRIP-KEY                     
               MOVE SU136-SHIPT-UNT-ID  TO PV-TRIP-SHIP-UNT-KEY                 
               ADD 1 TO PV-TRIP-COUNT                                           
           END-IF.                                                              
068522                                                                          
068514****************************************************************          
068514* 6001-READ-MSTR-EXTRACT-FILE                                  *          
068514* READ THE NEXT RECORD OF THE MASTER EXTRACT FILE              *          
068514****************************************************************          
068515 6001-READ-MSTR-EXTRACT-FILE.                                             
068517                                                                          
068518     READ MASTER-EXTRACT-FILE                                             
           INTO SU142-EXTRACT-RECORD                                            
068519        AT END                                                            
      *********  DISPLAY 'AT END ON MASTER'                                     
                 MOVE 999999999           TO PV-MSTR-TRIP-KEY                   
                 MOVE 9999999999999999999 TO PV-MSTR-SHIP-UNT-KEY               
068520           SET PS-EOF-MASTER-EXTRACT TO TRUE                              
068521     END-READ.                                                            
068520     IF PS-NOT-EOF-MASTER-EXTRACT                                         
               MOVE SU142-OTBND-TRIP-ID TO PV-MSTR-TRIP-KEY                     
               MOVE SU142-SHIPT-UNT-ID  TO PV-MSTR-SHIP-UNT-KEY                 
      *********DISPLAY 'PV-MSTR-KEY = ' PV-MSTR-KEY                             
               ADD 1 TO PV-MSTR-COUNT                                           
           END-IF.                                                              
068524*                                                                         
        7000-MOVE-TO-OUTPUT.                                                    
      *    OMS OUTPUT                                                           
           INITIALIZE OF011-EXTRACT-RECORD.                                     
           MOVE SU136-DEST-LOC-NBR      TO OF011-DEST-LOC-NBR.                  
           MOVE SU136-OTBND-TRIP-ID     TO OF011-OTBND-TRIP-ID.                 
           MOVE SU136-SHIPPED-TMST      TO OF011-SHIPPED-TMST.                  
           MOVE SU136-SHIPT-UNT-ID      TO OF011-SHIPT-UNT-ID.                  
           MOVE SU136-SKU-NBR           TO OF011-SKU-NBR.                       
           MOVE SU136-SHPD-UNT-QTY      TO OF011-SHPD-UNT-QTY.                  
           MOVE SU136-ORGN-OPER-UNT-ID  TO OF011-ORGN-OPER-UNT-ID.              
           MOVE SU136-TRLR-ID           TO OF011-TRLR-ID.                       
           MOVE SU136-TRLR-SRL-NBR      TO OF011-TRLR-SRL-NBR.                  
           MOVE SU136-TRLR-SEAL-ID      TO OF011-TRLR-SEAL-ID.                  
           MOVE SU136-SHIP-SCAC-CDE     TO OF011-SHIP-SCAC-CDE.                 
           MOVE SU136-TRN-NBR           TO OF011-TRN-NBR.                       
           MOVE SU136-RCVR-SEQ-NBR      TO OF011-RCVR-SEQ-NBR.                  
           MOVE SU136-UNIT-RETAIL       TO OF011-UNIT-RETAIL.                   
           MOVE SU136-COST-AMT          TO OF011-COST-AMT.                      
           MOVE SU136-SML-TYPE          TO OF011-SML-TYPE.                      
           IF OF011-SML-TYPE = PC-ASN                                           
               MOVE PC-CKN              TO OF011-SML-TYPE                       
           END-IF.                                                              
           MOVE SU136-TMST-ON-TRLR      TO OF011-TMST-ON-TRLR                   
           MOVE SU136-DTE-AT-DC         TO OF011-DTE-AT-DC.                     
           MOVE SU136-TM-AT-DC          TO OF011-TM-AT-DC.                      
           MOVE SU142-MSTR-UNT-ID       TO OF011-MSTR-UNT-ID.                   
      *CHG0227744 START                                                         
           MOVE SU136-OPC-SET-IND       TO OF011-OPC-SET-IND.                   
      *CHG0227744 END                                                           
                                                                                
        7100-MOVE-TO-OUTPUT.                                                    
      *    MLS OUTPUT                                                           
           INITIALIZE SU103-EXTRACT-RECORD.                                     
           MOVE SU136-DEST-LOC-NBR      TO SU103-DEST-LOC-NBR.                  
           MOVE SU136-OTBND-TRIP-ID     TO SU103-OTBND-TRIP-ID.                 
           MOVE SU136-SHIPPED-TMST      TO SU103-SHIPPED-TMST.                  
           MOVE SU136-SHIPT-UNT-ID      TO SU103-SHIPT-UNT-ID.                  
           MOVE SU136-SKU-NBR           TO SU103-SKU-NBR.                       
           MOVE SU136-SHPD-UNT-QTY      TO SU103-SHPD-UNT-QTY.                  
           MOVE SU136-TRLR-SEAL-ID      TO SU103-TRLR-SEAL-ID.                  
           MOVE SU136-SML-TYPE          TO SU103-SML-TYPE.                      
           IF SU136-SML-TYPE = PC-ASN                                           
               MOVE PC-CKN              TO SU103-SML-TYPE                       
           END-IF.                                                              
           MOVE SU142-MSTR-UNT-ID       TO SU103-MSTR-UNT-ID.                   
164325     MOVE SU136-VENDOR-STYLE      TO SU103-VENDOR-STYLE.                  
164325     MOVE SU136-CATEGORY          TO SU103-CATEGORY.                      
164325     MOVE SU136-OPC-SET-IND       TO SU103-OPC-SET-IND.                   
164325     MOVE SU136-VENDOR-CART-ID    TO SU103-VENDOR-CART-ID.                
                                                                                
       7200-CHECK-TO-WRITE-MSTR.                                                
                                                                                
      **   IF PS-FIRST-TIME                                                     
      **       SET PS-NOT-FIRST-TIME        TO TRUE                             
      **       IF SU142-MSTR-UNT-ID NOT = ZERO                                  
      **          INITIALIZE SU103-EXTRACT-RECORD                               
      **          MOVE SU136-DEST-LOC-NBR      TO PV-HOLD-DEST-LOC-NBR          
      **                                          SU103-DEST-LOC-NBR            
      **          MOVE SU136-OTBND-TRIP-ID     TO PV-HOLD-OTBND-TRIP-ID         
      **                                          SU103-OTBND-TRIP-ID           
      **          MOVE SU136-SHIPPED-TMST      TO PV-HOLD-SHIPPED-TMST          
      **                                          SU103-SHIPPED-TMST            
      **          MOVE SU136-TRLR-SEAL-ID      TO PV-HOLD-TRLR-SEAL-ID          
      **                                          SU103-TRLR-SEAL-ID            
      **          MOVE PC-MST                  TO PV-HOLD-SML-TYPE              
      **                                          SU103-SML-TYPE                
      **          MOVE SU136-SML-TYPE          TO PV-HOLD-SML-TYPE              
      *                                        SU103-SML-TYPE                   
      *           IF SU103-SML-TYPE = PC-ASN                                    
      *               MOVE PC-CKN              TO SU103-SML-TYPE                
      *           END-IF                                                        
      **          MOVE SU142-MSTR-UNT-ID       TO PV-HOLD-MSTR-UNT-ID           
      **                                          SU103-MSTR-UNT-ID             
068526**          WRITE MLS-MERGE-RECORD                                        
068527**              FROM SU103-EXTRACT-RECORD                                 
      **          ADD 1 TO PV-MSTR-OUT-COUNT                                    
      **     END-IF                                                             
      **   END-IF.                                                              
                                                                                
           IF PV-HOLD-SHIPT-UNT-ID  = SU136-SHIPT-UNT-ID                        
      **** IF PV-HOLD-MSTR-UNT-ID  = SU142-MSTR-UNT-ID                          
                 CONTINUE                                                       
              ELSE                                                              
                  IF SU142-MSTR-UNT-ID NOT = ZERO                               
      *               WRITE A MASTER RECORD                                     
                      INITIALIZE SU103-EXTRACT-RECORD                           
                      MOVE SU136-DEST-LOC-NBR  TO SU103-DEST-LOC-NBR            
                      MOVE SU136-OTBND-TRIP-ID TO SU103-OTBND-TRIP-ID           
                      MOVE SU136-SHIPPED-TMST  TO SU103-SHIPPED-TMST            
                      MOVE SU136-TRLR-SEAL-ID  TO SU103-TRLR-SEAL-ID            
                      MOVE PC-MST              TO SU103-SML-TYPE                
      *               IF SU103-SML-TYPE = PC-ASN                                
      *                   MOVE PC-CKN          TO SU103-SML-TYPE                
      *               END-IF                                                    
                      MOVE SU142-MSTR-UNT-ID   TO SU103-MSTR-UNT-ID             
164325                MOVE ZEROES              TO SU103-VENDOR-STYLE            
164325                MOVE ZEROES              TO SU103-CATEGORY                
068526                WRITE MLS-MERGE-RECORD                                    
068527                   FROM SU103-EXTRACT-RECORD                              
                      ADD 1 TO PV-MSTR-OUT-COUNT                                
      ****    END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SU136-DEST-LOC-NBR      TO PV-HOLD-DEST-LOC-NBR.                
           MOVE SU136-OTBND-TRIP-ID     TO PV-HOLD-OTBND-TRIP-ID.               
           MOVE SU136-SHIPPED-TMST      TO PV-HOLD-SHIPPED-TMST.                
           MOVE SU136-TRLR-SEAL-ID      TO PV-HOLD-TRLR-SEAL-ID.                
           MOVE SU136-SML-TYPE          TO PV-HOLD-SML-TYPE.                    
           MOVE SU136-SHIPT-UNT-ID      TO PV-HOLD-SHIPT-UNT-ID.                
           MOVE SU142-MSTR-UNT-ID       TO PV-HOLD-MSTR-UNT-ID.                 
                                                                                
      ****************************************************************          
      * THIS WOULD ONLY BE EXECUTED IF THERE WERE NO RECORDS ON THE             
      * SURD142 FILE                                                            
      ****************************************************************          
       7300-WRITE-ALL-OUTPUT.                                                   
           IF SU136-OMS-RECORD                                                  
               PERFORM 7000-MOVE-TO-OUTPUT                                      
               MOVE ZERO TO OF011-MSTR-UNT-ID                                   
               PERFORM 8000-WRITE-OMS-MERGE-FILE                                
               PERFORM 6000-READ-TRIP-EXTRACT-FILE                              
           ELSE                                                                 
               PERFORM 7100-MOVE-TO-OUTPUT                                      
               MOVE ZERO TO SU103-MSTR-UNT-ID                                   
               PERFORM 8100-WRITE-MLS-MERGE-FILE                                
               PERFORM 6000-READ-TRIP-EXTRACT-FILE                              
           END-IF.                                                              
                                                                                
068514****************************************************************          
068514* 8000-WRITE-OMS-MERGE-FILE                                    *          
068514* WRITE THE NEXT RECORD OF THE OMS MERGE FILE                  *          
068514****************************************************************          
068525 8000-WRITE-OMS-MERGE-FILE.                                               
068526     WRITE OMS-MERGE-RECORD                                               
068527       FROM OF011-EXTRACT-RECORD.                                         
           ADD 1 TO PV-OMS-COUNT.                                               
068528*                                                                         
068514****************************************************************          
068514* 8100-WRITE-MLS-MERGE-FILE                                    *          
068514* WRITE THE NEXT RECORD OF THE MLS MERGE FILE                  *          
068514****************************************************************          
068525 8100-WRITE-MLS-MERGE-FILE.                                               
                                                                                
068526     WRITE MLS-MERGE-RECORD                                               
068527       FROM SU103-EXTRACT-RECORD.                                         
           ADD 1 TO PV-MLS-COUNT.                                               
                                                                                
068529 9000-EOJ-ROUTINE.                                                        
068531                                                                          
           DISPLAY 'MASTER REC IN COUNT = ' PV-MSTR-COUNT.                      
           DISPLAY '  TRIP REC IN COUNT = ' PV-TRIP-COUNT.                      
           DISPLAY '  OMS  REC OUT COUNT = ' PV-OMS-COUNT.                      
           DISPLAY '  MLS  REC OUT COUNT = ' PV-MLS-COUNT.                      
           DISPLAY '  MSTR REC OUT COUNT = ' PV-MSTR-OUT-COUNT.                 
                                                                                
068532     CLOSE TRIP-EXTRACT-FILE                                              
068532           MASTER-EXTRACT-FILE                                            
068533           OMS-MERGE-FILE                                                 
068533           MLS-MERGE-FILE.                                                
068534                                                                          
