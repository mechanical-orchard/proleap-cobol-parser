      ******************************************************************00010002
       IDENTIFICATION DIVISION.                                         00020002
      ******************************************************************00030002
                                                                        00040002
       PROGRAM-ID.             EPKUP958.                                00050002
       AUTHOR.                 JIM HUNTER.                              00060002
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070002
       DATE-WRITTEN.           AUGUST, 2012.                            00080002
       DATE-COMPILED.                                                   00090002
                                                                        00100002
      ******************************************************************00110002
      * THIS PROGRAM WILL INSERT ROWS TO THE TEPPART TABLE FOR THE NEW  00120002
      * PARTITIONS BEING ADDED AS PART OF THE 2012 FSH PROJECT.         00130002
      * IT WILL RUN ONCE ON 9/29/2012.                                  00140005
      * IT MAY NEED TO BE MODIFIED AND RUN AGAIN IN 2013 WHEN THE       00150002
      * TEMPORARY PARTITIONS ARE CONVERTED TO PERMANENT PARTITIONS.     00160002
      *                                                                 00170002
      * THIS PROGRAM NEEDS TO RUN BEFORE THE NEW DAILY PARTITION        00180002
      * ASSIGNMENT PROGRAM (EPKUP058) RUNS ON 9/29/2012.                00190002
      * IT WILL ASSIGN THE NEXT BUSINESS DATE'S PARTITION               00200005
      * PARTN 96 = SALES DATE '2012-09-30'.  EPKUP058 WILL THEN RUN     00210002
      * RUN AND ASSIGN PARTN 97 = SALES DATE '2012-10-01'.              00220002
      * IT WILL CONTINUE TO RUN DAILY AND ASSIGN PARTITIONS 2 DAYS      00230002
      * IN ADVANCE OF THE BUSINESS DATE ASSIGNED TO THEM.               00240002
      * THE INITIALIZATION PROGRAM WILL ALSO ASSIGN NEW PARTITIONS      00250002
      * 97 THRU 500 WITH SALES DATES OF '9999-12-31' SO THAT            00260005
      * THEY ARE THE NEXT TO BE ASSIGNED.  STR_GP_CDE AND DB_STRUC_CDE  00270005
      * WILL BE ASSIGNED TO 'N' TO PREVENT OTHER PROGRAMS FROM          00280005
      * ACCIDENTLY REFERENCING THE UNASSIGNED PARTITIONS.               00290005
      *                                                                 00300005
      ******************************************************************00310002
      * CHANGE LOG: INITIAL PROGRAM.                                    00320005
      * DATE:  AUGUST, 2012                                             00330005
      * PRGMR: JIM HUNTER                                               00340005
      * WHY:   FSH (FLEXIBLE STORE HOUR) PROJECT - WR40932              00350005
      *                                                                 00360005
      ******************************************************************00370005
      *                                                                 00380002
      ******************************************************************00390002
                                                                        00400002
       ENVIRONMENT DIVISION.                                            00410002
      ******************************************************************00420002
                                                                        00430002
       CONFIGURATION SECTION.                                           00440002
       SOURCE-COMPUTER.        IBM-3090.                                00450002
       OBJECT-COMPUTER.        IMB-3090.                                00460002
                                                                        00470002
       INPUT-OUTPUT SECTION.                                            00480002
       FILE-CONTROL.                                                    00490002
                                                                        00500002
      ******************************************************************00510002
       DATA DIVISION.                                                   00520002
      ******************************************************************00530002
       FILE SECTION.                                                    00540002
                                                                        00550002
      ******************************************************************00560002
       WORKING-STORAGE SECTION.                                         00570002
      ******************************************************************00580002
                                                                        00590002
       01  ABEND-CODE                  PIC S9(04)   VALUE ZEROS         00600002
                                                    COMP SYNC.          00610002
           88  AC-DB2-ERROR                         VALUE +4013.        00620002
                                                                        00630002
       01  PC-PROGRAM-CONSTANTS.                                        00640002
           05  PC-PROGRAM-NAME         PIC  X(08)   VALUE 'EPKUP958'.   00650002
           05  PC-OPERATIONAL          PIC  X(01)   VALUE 'O'.          00660002
           05  PC-A                    PIC  X(01)   VALUE 'A'.          00670002
                                                                        00680002
                                                                        00690002
       01  PV-PROGRAM-VARIABLES.                                        00700002
           05  PV-PARAGRAPH-NAME       PIC  X(30)   VALUE SPACE.        00710002
           05  PV-OPERATION-MSG-1      PIC  X(40)   VALUE SPACE.        00720002
           05  PV-OPERATION-MSG-2      PIC  X(40)   VALUE SPACE.        00730002
           05  PV-DB2-OPERATION        PIC  X(30)   VALUE SPACE.        00740002
           05  PV-CURR-DATE.                                            00750002
              10 PV-CURR-DATE-CC       PIC  X(02)   VALUE SPACES.       00760002
              10 PV-CURR-DATE-YY       PIC  X(02)   VALUE SPACES.       00770002
              10 FILLER                PIC  X(01)   VALUE '-'.          00780002
              10 PV-CURR-DATE-MM       PIC  X(02)   VALUE SPACES.       00790002
              10 FILLER                PIC  X(01)   VALUE '-'.          00800002
              10 PV-CURR-DATE-DD       PIC  X(02)   VALUE SPACES.       00810002
                                                                        00820002
           05  PV-PARTN-NBR            PIC  S9(4)        VALUE 0.       00830003
                                                                        00840002
      ******************************************************************00850002
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00860002
      ******************************************************************00870002
                                                                        00880002
           COPY DPWS004.                                                00890002
                                                                        00900002
      ******************************************************************00910002
      *  SQL AND COBOL DECLARATIONS FOR THE PARTITION TABLE - TEPPART   00920002
      ******************************************************************00930002
                                                                        00940002
           EXEC SQL                                                     00950002
               INCLUDE TEPPART                                          00960002
           END-EXEC.                                                    00970002
                                                                        00980002
      ******************************************************************00990002
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                01000002
      ******************************************************************01010002
                                                                        01020002
           EXEC SQL                                                     01030002
               INCLUDE SQLCA                                            01040002
           END-EXEC.                                                    01050002
                                                                        01060002
      ******************************************************************01070002
       PROCEDURE DIVISION.                                              01080002
      ******************************************************************01090002
                                                                        01100002
      ******************************************************************01110002
       0000-MAINLINE.                                                   01120002
      ******************************************************************01130002
                                                                        01140002
           PERFORM 1000-INSERT-TEPPART-PARTN-ROWS                       01150002
              VARYING PV-PARTN-NBR FROM 96 BY 1                         01160002
                UNTIL PV-PARTN-NBR > 500.                               01170002
                                                                        01180002
           STOP RUN.                                                    01190002
                                                                        01200002
                                                                        01210002
      ******************************************************************01220002
       1000-INSERT-TEPPART-PARTN-ROWS.                                  01230002
      *     INSERT 1 NEW ROW TO TEPPART FOR EACH OF THE NEW             01240002
      *     PARTITIONS BEING ADDED FOR THE FSH PROJECT.                 01250002
      ******************************************************************01260002
                                                                        01270002
           MOVE PV-PARTN-NBR      TO EPPART-PARTN-NBR                   01280003
                                                                        01290003
           IF PV-PARTN-NBR = 96                                         01300003
               MOVE '2012-09-30'  TO EPPART-SLS-DTE                     01310002
               MOVE 'A'           TO EPPART-STR-GP-CDE                  01320003
               MOVE 'O'           TO EPPART-DB-STRUC-CDE                01330003
           ELSE                                                         01340002
               MOVE '9999-12-31'  TO EPPART-SLS-DTE                     01350002
               MOVE 'N'           TO EPPART-STR-GP-CDE                  01360003
               MOVE 'N'           TO EPPART-DB-STRUC-CDE                01370003
           END-IF.                                                      01380002
                                                                        01390002
           EXEC SQL                                                     01400002
              INSERT INTO TEPPART                                       01410002
                   ( PARTN_NBR                                          01420002
                   , SLS_DTE                                            01430002
                   , STR_GP_CDE                                         01440002
                   , DB_STRUC_CDE)                                      01450002
              VALUES                                                    01460002
                   (:EPPART-PARTN-NBR                                   01470003
                   ,:EPPART-SLS-DTE                                     01480002
                   ,:EPPART-STR-GP-CDE                                  01490004
                   ,:EPPART-DB-STRUC-CDE)                               01500004
           END-EXEC.                                                    01510002
                                                                        01520002
           IF SQLCODE = ZERO                                            01530002
              DISPLAY 'PARTN ADDED = ' PV-PARTN-NBR                     01540002
           ELSE                                                         01550002
              MOVE '1000-INSERT-TEPPART-PARTN-ROWS'                     01560002
                                            TO PV-PARAGRAPH-NAME        01570002
              MOVE SQLCODE                  TO PV-DB2-OPERATION         01580002
              STRING 'TRYING TO INSERT PARTN ' PV-PARTN-NBR             01590002
                     DELIMITED BY SIZE                                  01600002
                     INTO PV-OPERATION-MSG-1                            01610002
              PERFORM 9100-SQL-ABEND                                    01620002
           END-IF.                                                      01630002
                                                                        01640002
                                                                        01650002
      ******************************************************************01660002
       9100-SQL-ABEND.                                                  01670002
      *     SQL ABEND CODE.                                             01680002
      ******************************************************************01690002
                                                                        01700002
           DISPLAY '** ABEND     **'.                                   01710002
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                01720002
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              01730002
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               01740002
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-1.             01750002
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-2.             01760002
                                                                        01770002
      *************** SQLCA MESSAGE DISPLAY COPY BOOK ******************01780002
                                                                        01790002
           COPY DPPD004.                                                01800002
                                                                        01810002
           SET AC-DB2-ERROR TO TRUE.                                    01820002
           CALL 'ILBOABN0' USING ABEND-CODE.                            01830002
                                                                        01840002
      ******************* END OF EPKUP958 ******************************01850002
