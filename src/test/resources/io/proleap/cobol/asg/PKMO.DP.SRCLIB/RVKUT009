000001******************************************************************10/30/95
000002 IDENTIFICATION DIVISION.                                         RVKUT009
000003******************************************************************   LV001
000004                                                                  RVKUT009
000005 PROGRAM-ID.    RVKUT009.                                         RVKUT009
000006 AUTHOR.        ROBIN CHMIEL.                                     RVKUT009
000007 INSTALLATION.  KOHLS.                                            RVKUT009
000008 DATE-WRITTEN   APRIL, 1999.                                      RVKUT009
000009 DATE-COMPILED.                                                   RVKUT009
000010                                                                  RVKUT009
000011******************************************************************RVKUT009
000012*  THIS PROGRAM IS USED TO EXTRACT DATA FROM                      RVKUT009
000013*  THE 'RETURN TO VENDOR' TABLES DOWN TO A FLAT FILE TO BE USED   RVKUT009
000014*  BY AUDITORS FOR FOLLOW-UP ON RETURNED MERCHANDISE.             RVKUT009
000015******************************************************************RVKUT009
000016                                                                  RVKUT009
000017******************************************************************RVKUT009
000018 ENVIRONMENT DIVISION.                                            RVKUT009
000019******************************************************************RVKUT009
000020                                                                  RVKUT009
000021 CONFIGURATION SECTION.                                           RVKUT009
000022                                                                  RVKUT009
000023 SOURCE-COMPUTER.        IBM-3090.                                RVKUT009
000024 OBJECT-COMPUTER.        IBM-3090.                                RVKUT009
000025                                                                  RVKUT009
000026 INPUT-OUTPUT SECTION.                                            RVKUT009
000027                                                                  RVKUT009
000028 FILE-CONTROL.                                                    RVKUT009
000029                                                                  RVKUT009
000030     SELECT RTV-EXTRACT                                           RVKUT009
000031         ASSIGN TO OUT01.                                         RVKUT009
000032                                                                  RVKUT009
000033******************************************************************RVKUT009
000034 DATA DIVISION.                                                   RVKUT009
000035******************************************************************RVKUT009
000036                                                                  RVKUT009
000037 FILE SECTION.                                                    RVKUT009
000038                                                                  RVKUT009
000039 FD  RTV-EXTRACT                                                  RVKUT009
000040     RECORDING MODE IS F                                          RVKUT009
000041     LABEL RECORDS ARE STANDARD                                   RVKUT009
000042     BLOCK CONTAINS 0 RECORDS.                                    RVKUT009
000043                                                                  RVKUT009
000044 01  RTV-EXTRACT-RECORD.                                          RVKUT009
000045     05 RTV1-DFCT-ORD-ID            PIC  S9(12)V COMP-3.                  
000046     05 RTV1-VENDOR-DUNS-NBR        PIC   X(9).                           
000047     05 RTV1-ORD-DTE                PIC   X(10).                          
000048     05 RTV1-DFCT-UID-NBR           PIC   X(8).                           
000049     05 RTV1-VEND-ATTN-NAME         PIC   X(25).                          
000050     05 RTV1-VEND-NAME              PIC   X(25).                          
000051     05 RTV1-VEND-ADDR-DESC         PIC   X(25).                          
000052     05 RTV1-VEND-CITY-NAME         PIC   X(15).                          
000053     05 RTV1-VEND-STATE-CDE         PIC   X(2).                           
000054     05 RTV1-VEND-ZIP-CDE           PIC  S9(9)V COMP-3.                   
000055     05 RTV1-PHN-AREA-CDE           PIC  S9(3)V COMP-3.                   
000056     05 RTV1-PHN-EXCH-NBR           PIC  S9(3)V COMP-3.                   
000057     05 RTV1-PHN-NBR                PIC  S9(4)V COMP-3.                   
000058     05 RTV1-FAX-PHN-AREA-CDE       PIC  S9(3)V COMP-3.                   
000059     05 RTV1-FAX-EXCH-NBR           PIC  S9(3)V COMP-3.                   
000060     05 RTV1-FAX-PHN-NBR            PIC  S9(4)V COMP-3.                   
000061     05 RTV1-OPER-UNT-ID            PIC  S9(4) COMP.                      
000062     05 RTV1-LOC-ID                 PIC   X(15).                          
000063     05 RTV1-DFCT-ORD-ID-CDE        PIC   X(1).                           
000064     05 RTV1-STATUS-CHG-DTE         PIC   X(10).                          
000065     05 RTV1-DFCT-ORD-STAT-CDE      PIC   X(1).                           
000066     05 RTV2-DFCT-ORD-ID            PIC  S9(6)V COMP-3.                   
000067     05 RTV2-SEQ-NBR                PIC  S9(5)V COMP-3.                   
000068     05 RTV2-DFCT-ITEM-NBR          PIC  S9(15)V COMP-3.                  
000069     05 RTV2-DFCT-RET-DTE           PIC   X(10).                          
000070     05 RTV2-RTL-COST-AMT           PIC  S9(5)V9(2) COMP-3.               
000071     05 RTV2-UNIT-COST-AMT          PIC  S9(5)V9(2) COMP-3.               
000072     05 RTV2-MKOUT-ORD-ID           PIC  S9(11)V COMP-3.                  
000073     05 RTV2-MKOUT-ORD-STAT-CDE     PIC   X(1).                           
000074     05 RTV2-MDSE-DEPT-NBR          PIC   X(3).                           
000075     05 RTV2-MDSE-CLASS-NBR         PIC   X(2).                           
000076*                                                                 RVKUT009
000077*                                                                 RVKUT009
000078******************************************************************RVKUT009
000079 WORKING-STORAGE SECTION.                                         RVKUT009
000080******************************************************************RVKUT009
000081*                                                                 RVKUT009
000082*                                                                 RVKUT009
000083******************************************************************RVKUT009
000084*  DB2 FORMATTED MESSAGE AREA                                     RVKUT009
000085******************************************************************RVKUT009
000086     COPY DPWS004.                                                RVKUT009
000087*                                                                 RVKUT009
000088*                                                                 RVKUT009
000089******************************************************************RVKUT009
000090*  COMMUNICATIONS AREA FOR DB2                                    RVKUT009
000091******************************************************************RVKUT009
000092     EXEC SQL                                                     RVKUT009
000093          INCLUDE SQLCA                                           RVKUT009
000094     END-EXEC.                                                    RVKUT009
000095*                                                                 RVKUT009
000096*                                                                 RVKUT009
000097******************************************************************RVKUT009
000098*  DB2 TABLE DECLARATION - DEFECTIVE ITEM HEADER TABLE (TDFCTOR)  RVKUT009
000099******************************************************************RVKUT009
000100     EXEC SQL                                                     RVKUT009
000101          INCLUDE TDFCTOR                                         RVKUT009
000102     END-EXEC.                                                    RVKUT009
000103*                                                                 RVKUT009
000104*                                                                 RVKUT009
000105******************************************************************RVKUT009
000106*  DB2 TABLE DECLARATION - DEFECTIVE ITEM DETAIL TABLE (TDFMEIT)  RVKUT009
000107******************************************************************RVKUT009
000108     EXEC SQL                                                     RVKUT009
000109          INCLUDE TDFMEIT                                         RVKUT009
000110     END-EXEC.                                                    RVKUT009
000111                                                                  RVKUT009
000112*                                                                 RVKUT009
000113***  CALENDAR ROUTINE COPYBOOK                                            
000114     COPY DPWS500.                                                        
000115                                                                          
000116*                                                                 RVKUT009
000117 01  ABEND-CODE                      PIC S9(04)    VALUE +0       RVKUT009
000118                                                   COMP SYNC.     RVKUT009
000119***  CONTROL CARD VARIABLES                                               
000120 01  CC-CONTROL-CARD.                                                     
000121     05  CC-BEGIN-DATE               PIC  X(10).                          
000122     05  FILLER                      PIC  X(01).                          
000123     05  CC-END-DATE                 PIC  X(10).                          
000124*                                                                 RVKUT009
000125 01  PA-PROGRAM-ACCUMULATORS.                                     RVKUT009
000126     05  PA-GRAND-TOTALS.                                         RVKUT009
000127         10 PA-TDFCTOR-TOTAL-CNT         PIC S9(08)  VALUE +0     RVKUT009
000128                                                     COMP-3.      RVKUT009
000129         10 PA-TDFMEIT-TOTAL-CNT         PIC S9(08)  VALUE +0     RVKUT009
000130                                                     COMP-3.      RVKUT009
000131         10 PA-EXTRACT-TOTAL-CNT         PIC S9(10)  VALUE +0     RVKUT009
000132                                                     COMP-3.      RVKUT009
000133*                                                                 RVKUT009
000134 01  PS-PROGRAM-SWITCHES.                                         RVKUT009
000135     05  PS-EOC-TDFCTOR-CURSOR-SW        PIC  X(01)  VALUE 'N'.   RVKUT009
000136         88  PS-EOC-TDFCTOR-CURSOR                   VALUE 'Y'.   RVKUT009
000137         88  PS-NOT-EOC-TDFCTOR-CURSOR               VALUE 'N'.   RVKUT009
000138     05  PS-EOC-TDFMEIT-CURSOR-SW        PIC  X(01)  VALUE 'N'.   RVKUT009
000139         88  PS-EOC-TDFMEIT-CURSOR                   VALUE 'Y'.   RVKUT009
000140         88  PS-NOT-EOC-TDFMEIT-CURSOR               VALUE 'N'.   RVKUT009
000141     05  PS-FIRST-TDFMEIT-RECORD-SW      PIC  X(01)  VALUE 'Y'.   RVKUT009
000142         88  PS-FIRST-TDFMEIT-RECORD                 VALUE 'Y'.   RVKUT009
000143         88  PS-NOT-FIRST-TDFMEIT-RECORD             VALUE 'N'.   RVKUT009
000144                                                                  RVKUT009
000145 01  PV-PROGRAM-VARIABLES.                                        RVKUT009
000146     05  PV-BEGIN-DATE                   PIC  X(10).              RVKUT009
000147     05  PV-END-DATE                     PIC  X(10).              RVKUT009
000148*                                                                 RVKUT009
000149******************************************************************RVKUT009
000150*  CURSOR DECLARATION FOR RETURN TO VENDOR TABLE TDFCTOR         *RVKUT009
000151******************************************************************RVKUT009
000152                                                                  RVKUT009
000153     EXEC SQL                                                     RVKUT009
000154          DECLARE TDFCTOR_CURSOR CURSOR FOR                       RVKUT009
000155           SELECT                                                 RVKUT009
000156                  DFCTOR.DFCT_ORD_ID                              RVKUT009
000157                 ,DFCTOR.VENDOR_DUNS_NBR                          RVKUT009
000158                 ,DFCTOR.ORD_DTE                                  RVKUT009
000159                 ,DFCTOR.DFCT_UID_NBR                             RVKUT009
000160                 ,DFCTOR.VEND_ATTN_NAME                           RVKUT009
000161                 ,DFCTOR.VEND_NAME                                RVKUT009
000162                 ,DFCTOR.VEND_ADDR_DESC                           RVKUT009
000163                 ,DFCTOR.VEND_CITY_NAME                           RVKUT009
000164                 ,DFCTOR.VEND_STATE_CDE                           RVKUT009
000165                 ,DFCTOR.VEND_ZIP_CDE                             RVKUT009
000166                 ,DFCTOR.PHN_AREA_CDE                             RVKUT009
000167                 ,DFCTOR.PHN_EXCH_NBR                             RVKUT009
000168                 ,DFCTOR.PHN_NBR                                  RVKUT009
000169                 ,DFCTOR.FAX_PHN_AREA_CDE                         RVKUT009
000170                 ,DFCTOR.FAX_EXCH_NBR                             RVKUT009
000171                 ,DFCTOR.FAX_PHN_NBR                              RVKUT009
000172                 ,DFCTOR.OPER_UNT_ID                              RVKUT009
000173                 ,DFCTOR.LOC_ID                                   RVKUT009
000174                 ,DFCTOR.DFCT_ORD_ID_CDE                          RVKUT009
000175                 ,DFCTOR.STATUS_CHG_DTE                           RVKUT009
000176                 ,DFCTOR.DFCT_ORD_STAT_CDE                        RVKUT009
000177             FROM                                                 RVKUT009
000178                  TDFCTOR DFCTOR                                  RVKUT009
000179             WHERE                                                RVKUT009
000180                  DFCTOR.ORD_DTE BETWEEN :PV-BEGIN-DATE           RVKUT009
000181                                     AND :PV-END-DATE             RVKUT009
000182             ORDER BY DFCTOR.DFCT_ORD_ID                          RVKUT009
000183     END-EXEC.                                                    RVKUT009
000184                                                                  RVKUT009
000185*                                                                 RVKUT009
000186******************************************************************RVKUT009
000187*  CURSOR DECLARATION FOR RETURN TO VENDOR TABLE TDFMEIT         *RVKUT009
000188******************************************************************RVKUT009
000189                                                                  RVKUT009
000190     EXEC SQL                                                     RVKUT009
000191          DECLARE TDFMEIT_CURSOR CURSOR FOR                       RVKUT009
000192           SELECT                                                 RVKUT009
000193                  DFMEIT.DFCT_ORD_ID                              RVKUT009
000194                 ,DFMEIT.SEQ_NBR                                  RVKUT009
000195                 ,DFMEIT.DFCT_ITEM_NBR                                    
000196                 ,DFMEIT.DFCT_RET_DTE                                     
000197                 ,DFMEIT.RTL_COST_AMT                                     
000198                 ,DFMEIT.UNIT_COST_AMT                                    
000199                 ,DFMEIT.MKOUT_ORD_ID                                     
000200                 ,DFMEIT.MKOUT_ORD_STAT_CDE                               
000201                 ,DFMEIT.MDSE_DEPT_NBR                                    
000202                 ,DFMEIT.MDSE_CLASS_NBR                                   
000203             FROM                                                 RVKUT009
000204                  TDFMEIT DFMEIT                                  RVKUT009
000205             WHERE                                                RVKUT009
000206                   DFMEIT.DFCT_ORD_ID = :DFCTOR-DFCT-ORD-ID       RVKUT009
000207             ORDER BY DFMEIT.DFCT_ORD_ID                          RVKUT009
000208                     ,DFMEIT.SEQ_NBR                              RVKUT009
000209     END-EXEC.                                                    RVKUT009
000210                                                                  RVKUT009
000211*                                                                 RVKUT009
000212*                                                                 RVKUT009
000213******************************************************************RVKUT009
000214 LINKAGE SECTION.                                                 RVKUT009
000215******************************************************************RVKUT009
000216                                                                  RVKUT009
000217******************************************************************RVKUT009
000218 PROCEDURE DIVISION.                                              RVKUT009
000219******************************************************************RVKUT009
000220                                                                  RVKUT009
000221******************************************************************RVKUT009
000222*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM    RVKUT009
000223******************************************************************RVKUT009
000224 0000-PROGRAM-MAINLINE.                                           RVKUT009
000225                                                                  RVKUT009
000226     PERFORM 1000-INITIALIZE.                                     RVKUT009
000227                                                                  RVKUT009
000228     SET PS-NOT-EOC-TDFCTOR-CURSOR TO TRUE.                       RVKUT009
000229     PERFORM 2000-CREATE-EXTRACT-FILE                             RVKUT009
000230         UNTIL PS-EOC-TDFCTOR-CURSOR.                             RVKUT009
000231                                                                  RVKUT009
000232     PERFORM 9000-EOJ-ROUTINE.                                    RVKUT009
000233     STOP RUN.                                                    RVKUT009
000234*                                                                 RVKUT009
000235*0000-EXIT.                                                       RVKUT009
000236******************************************************************RVKUT009
000237*  1000-INITIALIZE -  OPENS AND INITIALIZES THE EXTRACT FILE,     RVKUT009
000238*  OPENS THE TDFCTOR CURSOR AND FETCHES THE FIRST RECORD,         RVKUT009
000239*  AND INITIALIZES THE COUNT FIELDS.                              RVKUT009
000240******************************************************************RVKUT009
000241 1000-INITIALIZE.                                                 RVKUT009
000242                                                                  RVKUT009
000243     OPEN OUTPUT RTV-EXTRACT.                                     RVKUT009
000244                                                                  RVKUT009
000245     INITIALIZE PA-GRAND-TOTALS,                                  RVKUT009
000246                RTV-EXTRACT-RECORD.                               RVKUT009
000247                                                                          
000248     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000249                                                                          
000250     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000251                                                                          
000252     DISPLAY 'CONTROL CARD : '  CC-CONTROL-CARD.                          
000253     DISPLAY 'BEGIN DATE   : '  PV-BEGIN-DATE.                            
000254     DISPLAY 'END DATE     : '  PV-END-DATE.                              
000255                                                                          
000256                                                                  RVKUT009
000257     PERFORM 6100-OPEN-TDFCTOR-CURSOR.                            RVKUT009
000258                                                                  RVKUT009
000259     PERFORM 6300-FETCH-TDFCTOR-CURSOR.                           RVKUT009
000260                                                                  RVKUT009
000261*1000-EXIT.                                                       RVKUT009
000262                                                                  RVKUT009
000263******************************************************************        
000264*** 1100-VALIDATE-CONTROL-CARD : VALIDATES THE DATE GIVEN IN THE          
000265*** CONTROL CARD. IF DATE GIVEN IS NOT CORRECT, ABEND THE PROGRAM .       
000266******************************************************************        
000267 1100-VALIDATE-CONTROL-CARD.                                              
000268******  VALIDATE BEGIN DATE                                               
000269     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
000270                                                                          
000271     SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                              
000272     SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                              
000273                                                                          
000274     MOVE CC-BEGIN-DATE             TO DPG52-LK-DATE-INPUT.               
000275                                                                          
000276     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
000277          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000278                                                                          
000279     EVALUATE TRUE                                                        
000280         WHEN DPG54-NO-ERROR                                              
000281             MOVE DPG55-GREG-CC-DATE-FMT TO PV-BEGIN-DATE                 
000282         WHEN DPG54-DATE-INVALID                                          
000283             DISPLAY '** ABEND **'                                        
000284             DISPLAY '** PROGRAM = RVKUT009 - CALLED MODULE '             
000285                     'DP500-KOHLS-CALENDAR-ROUTINE ***'                   
000286             DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'             
000287             DISPLAY '** INVALID CC BEGIN DTE: ' CC-BEGIN-DATE            
000288             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
000289             DISPLAY DPG54-ERROR-MESSAGE                                  
000290             MOVE +4019 TO ABEND-CODE                                     
000291             CALL 'ILBOABN0' USING ABEND-CODE                             
000292         WHEN OTHER                                                       
000293             DISPLAY DPG54-ERROR-MESSAGE                                  
000294     END-EVALUATE.                                                        
000295                                                                          
000296******  VALIDATE END DATE                                                 
000297     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
000298                                                                          
000299     SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                              
000300     SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                              
000301                                                                          
000302     MOVE CC-END-DATE               TO DPG52-LK-DATE-INPUT.               
000303                                                                          
000304     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
000305          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000306                                                                          
000307     EVALUATE TRUE                                                        
000308         WHEN DPG54-NO-ERROR                                              
000309             MOVE DPG55-GREG-CC-DATE-FMT TO PV-END-DATE                   
000310         WHEN DPG54-DATE-INVALID                                          
000311             DISPLAY '** ABEND **'                                        
000312             DISPLAY '** PROGRAM = RVKUT009 - CALLED MODULE '             
000313                     'DP500-KOHLS-CALENDAR-ROUTINE ***'                   
000314             DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'             
000315             DISPLAY '** INVALID CC END DATE : ' CC-END-DATE              
000316             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
000317             DISPLAY DPG54-ERROR-MESSAGE                                  
000318             MOVE +4019 TO ABEND-CODE                                     
000319             CALL 'ILBOABN0' USING ABEND-CODE                             
000320         WHEN OTHER                                                       
000321             DISPLAY DPG54-ERROR-MESSAGE                                  
000322     END-EVALUATE.                                                        
000323                                                                          
000324                                                                  RVKUT009
000325******************************************************************RVKUT009
000326* 2000-CREATE-EXTRACT-FILE                                        RVKUT009
000327* PROCESS:  THIS PARAGRAPH WILL SET UP AND WRITE EXTRACT RECORDS  RVKUT009
000328******************************************************************RVKUT009
000329 2000-CREATE-EXTRACT-FILE.                                        RVKUT009
000330                                                                  RVKUT009
000331     PERFORM 7100-OPEN-TDFMEIT-CURSOR.                            RVKUT009
000332                                                                  RVKUT009
000333     SET PS-NOT-EOC-TDFMEIT-CURSOR TO TRUE.                       RVKUT009
000334     PERFORM 7300-FETCH-TDFMEIT-CURSOR                            RVKUT009
000335         UNTIL PS-EOC-TDFMEIT-CURSOR.                             RVKUT009
000336                                                                  RVKUT009
000337     PERFORM 7200-CLOSE-TDFMEIT-CURSOR.                           RVKUT009
000338                                                                  RVKUT009
000339     PERFORM 6300-FETCH-TDFCTOR-CURSOR.                           RVKUT009
000340                                                                  RVKUT009
000341*                                                                 RVKUT009
000342******************************************************************RVKUT009
000343* 6100-OPEN-TDFCTOR-CURSOR.                                      *RVKUT009
000344******************************************************************RVKUT009
000345 6100-OPEN-TDFCTOR-CURSOR.                                        RVKUT009
000346                                                                  RVKUT009
000347     EXEC SQL                                                     RVKUT009
000348          OPEN TDFCTOR_CURSOR                                     RVKUT009
000349     END-EXEC.                                                    RVKUT009
000350                                                                  RVKUT009
000351     EVALUATE TRUE                                                RVKUT009
000352         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000353         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000354             DISPLAY '** ABEND **'                                RVKUT009
000355             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000356             DISPLAY '** PARAGRAPH = 6100-OPEN-TDFCTOR-CURSOR **' RVKUT009
000357             PERFORM 9100-SQL-ABEND                               RVKUT009
000358     END-EVALUATE.                                                RVKUT009
000359                                                                  RVKUT009
000360*                                                                 RVKUT009
000361******************************************************************RVKUT009
000362* 6200-CLOSE-TDFCTOR-CURSOR.                                     *RVKUT009
000363******************************************************************RVKUT009
000364 6200-CLOSE-TDFCTOR-CURSOR.                                       RVKUT009
000365                                                                  RVKUT009
000366     EXEC SQL                                                     RVKUT009
000367         CLOSE TDFCTOR_CURSOR                                     RVKUT009
000368     END-EXEC.                                                    RVKUT009
000369                                                                  RVKUT009
000370     EVALUATE TRUE                                                RVKUT009
000371         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000372         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000373             DISPLAY '** ABEND **'                                RVKUT009
000374             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000375             DISPLAY '** PARAGRAPH = 6200-CLOSE-TDFCTOR-CURSOR**' RVKUT009
000376             PERFORM 9100-SQL-ABEND                               RVKUT009
000377     END-EVALUATE.                                                RVKUT009
000378                                                                  RVKUT009
000379*                                                                 RVKUT009
000380******************************************************************RVKUT009
000381* 6300-FETCH-TDFCTOR-CURSOR.                                     *RVKUT009
000382******************************************************************RVKUT009
000383 6300-FETCH-TDFCTOR-CURSOR.                                       RVKUT009
000384                                                                  RVKUT009
000385     EXEC SQL                                                     RVKUT009
000386          FETCH TDFCTOR_CURSOR                                    RVKUT009
000387          INTO                                                    RVKUT009
000388                  :DFCTOR-DFCT-ORD-ID                             RVKUT009
000389                , :DFCTOR-VENDOR-DUNS-NBR                         RVKUT009
000390                , :DFCTOR-ORD-DTE                                 RVKUT009
000391                , :DFCTOR-DFCT-UID-NBR                            RVKUT009
000392                , :DFCTOR-VEND-ATTN-NAME                          RVKUT009
000393                , :DFCTOR-VEND-NAME                               RVKUT009
000394                , :DFCTOR-VEND-ADDR-DESC                          RVKUT009
000395                , :DFCTOR-VEND-CITY-NAME                          RVKUT009
000396                , :DFCTOR-VEND-STATE-CDE                          RVKUT009
000397                , :DFCTOR-VEND-ZIP-CDE                            RVKUT009
000398                , :DFCTOR-PHN-AREA-CDE                            RVKUT009
000399                , :DFCTOR-PHN-EXCH-NBR                            RVKUT009
000400                , :DFCTOR-PHN-NBR                                 RVKUT009
000401                , :DFCTOR-FAX-PHN-AREA-CDE                        RVKUT009
000402                , :DFCTOR-FAX-EXCH-NBR                            RVKUT009
000403                , :DFCTOR-FAX-PHN-NBR                             RVKUT009
000404                , :DFCTOR-OPER-UNT-ID                             RVKUT009
000405                , :DFCTOR-LOC-ID                                  RVKUT009
000406                , :DFCTOR-DFCT-ORD-ID-CDE                         RVKUT009
000407                , :DFCTOR-STATUS-CHG-DTE                          RVKUT009
000408                , :DFCTOR-DFCT-ORD-STAT-CDE                       RVKUT009
000409     END-EXEC.                                                    RVKUT009
000410                                                                  RVKUT009
000411     EVALUATE TRUE                                                RVKUT009
000412         WHEN SQLCODE = ZERO                                      RVKUT009
000413              ADD +1 TO PA-TDFCTOR-TOTAL-CNT                      RVKUT009
000414              SET PS-FIRST-TDFMEIT-RECORD TO TRUE                 RVKUT009
000415                                                                  RVKUT009
000416         WHEN SQLCODE = +100                                      RVKUT009
000417              SET PS-EOC-TDFCTOR-CURSOR TO  TRUE                  RVKUT009
000418                                                                  RVKUT009
000419         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000420         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000421             DISPLAY '** ABEND **'                                RVKUT009
000422             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000423             DISPLAY '** PARAGRAPH = 6300-FETCH-TDFCTOR-CURSOR**' RVKUT009
000424             PERFORM 9100-SQL-ABEND                               RVKUT009
000425     END-EVALUATE.                                                RVKUT009
000426                                                                  RVKUT009
000427 EJECT                                                            RVKUT009
000428*                                                                 RVKUT009
000429******************************************************************RVKUT009
000430* 6400-MOVE-TDFCTOR-INFO.                                        *RVKUT009
000431******************************************************************RVKUT009
000432 6400-MOVE-TDFCTOR-INFO.                                          RVKUT009
000433                                                                  RVKUT009
000434     MOVE DFCTOR-DFCT-ORD-ID      TO RTV1-DFCT-ORD-ID.            RVKUT009
000435     MOVE DFCTOR-VENDOR-DUNS-NBR  TO RTV1-VENDOR-DUNS-NBR.        RVKUT009
000436     MOVE DFCTOR-ORD-DTE          TO RTV1-ORD-DTE.                RVKUT009
000437     MOVE DFCTOR-DFCT-UID-NBR     TO RTV1-DFCT-UID-NBR.           RVKUT009
000438     MOVE DFCTOR-VEND-ATTN-NAME   TO RTV1-VEND-ATTN-NAME.         RVKUT009
000439     MOVE DFCTOR-VEND-NAME         TO RTV1-VEND-NAME.             RVKUT009
000440     MOVE DFCTOR-VEND-ADDR-DESC    TO RTV1-VEND-ADDR-DESC.        RVKUT009
000441     MOVE DFCTOR-VEND-CITY-NAME    TO RTV1-VEND-CITY-NAME.        RVKUT009
000442     MOVE DFCTOR-VEND-STATE-CDE    TO RTV1-VEND-STATE-CDE.        RVKUT009
000443     MOVE DFCTOR-VEND-ZIP-CDE      TO RTV1-VEND-ZIP-CDE.          RVKUT009
000444     MOVE DFCTOR-PHN-AREA-CDE      TO RTV1-PHN-AREA-CDE.          RVKUT009
000445     MOVE DFCTOR-PHN-EXCH-NBR      TO RTV1-PHN-EXCH-NBR.          RVKUT009
000446     MOVE DFCTOR-PHN-NBR           TO RTV1-PHN-NBR.               RVKUT009
000447     MOVE DFCTOR-FAX-PHN-AREA-CDE  TO RTV1-FAX-PHN-AREA-CDE.      RVKUT009
000448     MOVE DFCTOR-FAX-EXCH-NBR      TO RTV1-FAX-EXCH-NBR.          RVKUT009
000449     MOVE DFCTOR-FAX-PHN-NBR       TO RTV1-FAX-PHN-NBR.           RVKUT009
000450     MOVE DFCTOR-OPER-UNT-ID       TO RTV1-OPER-UNT-ID.           RVKUT009
000451     MOVE DFCTOR-LOC-ID            TO RTV1-LOC-ID.                RVKUT009
000452     MOVE DFCTOR-DFCT-ORD-ID-CDE   TO RTV1-DFCT-ORD-ID-CDE.       RVKUT009
000453     MOVE DFCTOR-STATUS-CHG-DTE    TO RTV1-STATUS-CHG-DTE.        RVKUT009
000454     MOVE DFCTOR-DFCT-ORD-STAT-CDE TO RTV1-DFCT-ORD-STAT-CDE.     RVKUT009
000455                                                                  RVKUT009
000456 EJECT                                                            RVKUT009
000457*                                                                 RVKUT009
000458******************************************************************RVKUT009
000459* 7100-OPEN-TDFMEIT-CURSOR.                                       RVKUT009
000460******************************************************************RVKUT009
000461 7100-OPEN-TDFMEIT-CURSOR.                                        RVKUT009
000462                                                                  RVKUT009
000463     EXEC SQL                                                     RVKUT009
000464          OPEN TDFMEIT_CURSOR                                     RVKUT009
000465     END-EXEC.                                                    RVKUT009
000466                                                                  RVKUT009
000467     EVALUATE TRUE                                                RVKUT009
000468         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000469         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000470             DISPLAY '** ABEND **'                                RVKUT009
000471             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000472             DISPLAY '** PARAGRAPH = 7100-OPEN-TDFMEIT-CURSOR **' RVKUT009
000473             PERFORM 9100-SQL-ABEND                               RVKUT009
000474     END-EVALUATE.                                                RVKUT009
000475                                                                  RVKUT009
000476*                                                                 RVKUT009
000477******************************************************************RVKUT009
000478* 7200-CLOSE-TDFMEIT-CURSOR.                                      RVKUT009
000479******************************************************************RVKUT009
000480 7200-CLOSE-TDFMEIT-CURSOR.                                       RVKUT009
000481                                                                  RVKUT009
000482     EXEC SQL                                                     RVKUT009
000483         CLOSE TDFMEIT_CURSOR                                     RVKUT009
000484     END-EXEC.                                                    RVKUT009
000485                                                                  RVKUT009
000486     EVALUATE TRUE                                                RVKUT009
000487         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000488         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000489             DISPLAY '** ABEND **'                                RVKUT009
000490             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000491             DISPLAY '** PARAGRAPH = 7200-CLOSE-TDFMEIT-CURSOR**' RVKUT009
000492             PERFORM 9100-SQL-ABEND                               RVKUT009
000493     END-EVALUATE.                                                RVKUT009
000494                                                                  RVKUT009
000495 EJECT                                                            RVKUT009
000496*                                                                 RVKUT009
000497******************************************************************RVKUT009
000498* 7300-FETCH-TDFMEIT-CURSOR.                                      RVKUT009
000499******************************************************************RVKUT009
000500 7300-FETCH-TDFMEIT-CURSOR.                                       RVKUT009
000501                                                                  RVKUT009
000502     EXEC SQL                                                     RVKUT009
000503          FETCH TDFMEIT_CURSOR                                    RVKUT009
000504          INTO                                                    RVKUT009
000505                  :DFMEIT-DFCT-ORD-ID                             RVKUT009
000506                , :DFMEIT-SEQ-NBR                                 RVKUT009
000507                , :DFMEIT-DFCT-ITEM-NBR                           RVKUT009
000508                , :DFMEIT-DFCT-RET-DTE                            RVKUT009
000509                , :DFMEIT-RTL-COST-AMT                            RVKUT009
000510                , :DFMEIT-UNIT-COST-AMT                           RVKUT009
000511                , :DFMEIT-MKOUT-ORD-ID                            RVKUT009
000512                , :DFMEIT-MKOUT-ORD-STAT-CDE                      RVKUT009
000513                , :DFMEIT-MDSE-DEPT-NBR                           RVKUT009
000514                , :DFMEIT-MDSE-CLASS-NBR                          RVKUT009
000515     END-EXEC.                                                    RVKUT009
000516                                                                  RVKUT009
000517     EVALUATE TRUE                                                RVKUT009
000518         WHEN SQLCODE = ZERO                                      RVKUT009
000519              PERFORM 6400-MOVE-TDFCTOR-INFO                      RVKUT009
000520              PERFORM 7400-MOVE-TDFMEIT-INFO                      RVKUT009
000521              PERFORM 8000-WRITE-EXTRACT-RECORD                   RVKUT009
000522                                                                  RVKUT009
000523         WHEN SQLCODE = +100                                      RVKUT009
000524              SET PS-EOC-TDFMEIT-CURSOR TO TRUE                   RVKUT009
000525              IF PS-FIRST-TDFMEIT-RECORD                          RVKUT009
000526                  PERFORM 6400-MOVE-TDFCTOR-INFO                  RVKUT009
000527                  PERFORM 7500-INIT-TDFMEIT-INFO                  RVKUT009
000528                  PERFORM 8000-WRITE-EXTRACT-RECORD               RVKUT009
000529              END-IF                                              RVKUT009
000530                                                                  RVKUT009
000531         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT009
000532         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT009
000533             DISPLAY '** ABEND **'                                RVKUT009
000534             DISPLAY '** PROGRAM = RVKUT009 **'                   RVKUT009
000535             DISPLAY '** PARAGRAPH = 7300-FETCH-TDFMEIT-CURSOR**' RVKUT009
000536             PERFORM 9100-SQL-ABEND                               RVKUT009
000537     END-EVALUATE.                                                RVKUT009
000538                                                                  RVKUT009
000539*                                                                 RVKUT009
000540******************************************************************RVKUT009
000541* 7400-MOVE-TDFMEIT-INFO.                                         RVKUT009
000542******************************************************************RVKUT009
000543 7400-MOVE-TDFMEIT-INFO.                                          RVKUT009
000544                                                                  RVKUT009
000545     MOVE DFMEIT-DFCT-ORD-ID        TO RTV2-DFCT-ORD-ID.          RVKUT009
000546     MOVE DFMEIT-SEQ-NBR            TO RTV2-SEQ-NBR.              RVKUT009
000547     MOVE DFMEIT-DFCT-ITEM-NBR      TO RTV2-DFCT-ITEM-NBR.                
000548     MOVE DFMEIT-DFCT-RET-DTE       TO RTV2-DFCT-RET-DTE.         RVKUT009
000549     MOVE DFMEIT-RTL-COST-AMT       TO RTV2-RTL-COST-AMT.         RVKUT009
000550     MOVE DFMEIT-UNIT-COST-AMT      TO RTV2-UNIT-COST-AMT.                
000551     MOVE DFMEIT-MKOUT-ORD-ID       TO RTV2-MKOUT-ORD-ID.         RVKUT009
000552     MOVE DFMEIT-MKOUT-ORD-STAT-CDE TO RTV2-MKOUT-ORD-STAT-CDE.   RVKUT009
000553     MOVE DFMEIT-MDSE-DEPT-NBR      TO RTV2-MDSE-DEPT-NBR.                
000554     MOVE DFMEIT-MDSE-CLASS-NBR     TO RTV2-MDSE-CLASS-NBR.       RVKUT009
000555                                                                  RVKUT009
000556     ADD +1 TO PA-TDFMEIT-TOTAL-CNT.                              RVKUT009
000557     IF PS-FIRST-TDFMEIT-RECORD                                   RVKUT009
000558         SET PS-NOT-FIRST-TDFMEIT-RECORD TO TRUE                  RVKUT009
000559     END-IF.                                                              
000560                                                                          
000561*                                                                 RVKUT009
000562******************************************************************RVKUT009
000563* 7500-INIT-TDFMEIT-INFO.                                         RVKUT009
000564******************************************************************RVKUT009
000565 7500-INIT-TDFMEIT-INFO.                                          RVKUT009
000566                                                                  RVKUT009
000567     MOVE SPACES TO RTV2-DFCT-RET-DTE,                                    
000568                    RTV2-MKOUT-ORD-STAT-CDE,                              
000569                    RTV2-MDSE-DEPT-NBR,                                   
000570                    RTV2-MDSE-CLASS-NBR.                                  
000571                                                                          
000572     MOVE +0     TO RTV2-DFCT-ORD-ID,                                     
000573                    RTV2-SEQ-NBR,                                         
000574                    RTV2-DFCT-ITEM-NBR,                                   
000575                    RTV2-RTL-COST-AMT,                                    
000576                    RTV2-UNIT-COST-AMT,                                   
000577                    RTV2-MKOUT-ORD-ID.                                    
000578                                                                          
000579*                                                                 RVKUT009
000580 8000-WRITE-EXTRACT-RECORD.                                       RVKUT009
000581***************************************************************** RVKUT009
000582*  WRITE THE EXTRACT RECORD TO AN OUTPUT FILE.                  * RVKUT009
000583***************************************************************** RVKUT009
000584                                                                  RVKUT009
000585     WRITE RTV-EXTRACT-RECORD.                                    RVKUT009
000586                                                                          
000587     ADD +1 TO PA-EXTRACT-TOTAL-CNT.                              RVKUT009
000588                                                                  RVKUT009
000589*8000-EXIT.                                                       RVKUT009
000590*                                                                 RVKUT009
000591******************************************************************RVKUT009
000592* 9000-EOJ-ROUTINE - CLOSES THE TDFCTOR CURSOR, CLOSES THE EXTRACTRVKUT009
000593* FILE, AND DISPLAYS THE RECORD COUNTS.                           RVKUT009
000594******************************************************************RVKUT009
000595 9000-EOJ-ROUTINE.                                                RVKUT009
000596                                                                  RVKUT009
000597     PERFORM 6200-CLOSE-TDFCTOR-CURSOR.                           RVKUT009
000598                                                                  RVKUT009
000599     CLOSE RTV-EXTRACT.                                           RVKUT009
000600                                                                  RVKUT009
000601     DISPLAY '***********************************************'.   RVKUT009
000602     DISPLAY '*                                              '.   RVKUT009
000603     DISPLAY '* TOTAL TDFCTOR RECORDS = ' PA-TDFCTOR-TOTAL-CNT.   RVKUT009
000604     DISPLAY '* TOTAL TDFMEIT RECORDS = ' PA-TDFMEIT-TOTAL-CNT.   RVKUT009
000605     DISPLAY '* TOTAL EXTRACT RECORDS = ' PA-EXTRACT-TOTAL-CNT.   RVKUT009
000606     DISPLAY '*                                              '.   RVKUT009
000607     DISPLAY '***********************************************'.   RVKUT009
000608                                                                          
000609                                                                          
000610*9000-EXIT.                                                       RVKUT009
000611******************************************************************RVKUT009
000612* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      RVKUT009
000613* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                 RVKUT009
000614******************************************************************RVKUT009
000615 9100-SQL-ABEND.                                                  RVKUT009
000616******************************************************************RVKUT009
000617* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    RVKUT009
000618* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         RVKUT009
000619* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     RVKUT009
000620* FORMAT.                                                         RVKUT009
000621******************************************************************RVKUT009
000622*    COPY DPPD004.                                                RVKUT009
000623     COPY DPPD004.                                                RVKUT009
000624                                                                  RVKUT009
000625     MOVE +4013 TO ABEND-CODE.                                    RVKUT009
000626     CALL 'ILBOABN0' USING ABEND-CODE.                            RVKUT009
000627*9100-EXIT.                                                       RVKUT009
