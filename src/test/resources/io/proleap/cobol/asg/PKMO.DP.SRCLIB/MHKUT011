       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MHKUT011.                                                 
       AUTHOR.        LINDA FRIESS.                                             
       DATE-WRITTEN.  JANUARY, 2006.                                            
      *****************************************************************         
      *  MH -  SERVICE LEVEL - MERGE FILES                                      
      *****************************************************************         
      *  THE PURPOSE OF THIS PROGRAM IS TO MATCH THE SERVICE LEVEL    *         
      *    ITEM/STORE LIST WITH THE DAILY SERVICE LEVEL EXTRACTS.     *         
      *  ALL MATCHES ARE WRITTEN OUT TO A LOAD FILE.                            
      *  ALL RECORDS ONLY ON ITEM/STORE LIST ARE WRITTEN OUT ALSO.              
      *  ALL RECORDS ONLY ON DAILY FILE ARE BYPASSED.                           
      *****************************************************************         
      *  CHANGE MADE:                                                 *         
      *                                                               *         
      *  CHANGED 02/24/06 BY LINDA FRIESS               WR# 28981     *         
      *    CHANGED TIMESTAMPS ON OUTPUT FILE TO BE ALL ZEROES.                  
      *                                                                         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT ITEM-STORE-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DAILY-EXTRACT-FILE                                            
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT SVC-LVL-LOAD-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
       FD  ITEM-STORE-FILE                                                      
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ITEM-STORE-RECORD        PIC  X(40).                                 
                                                                                
       FD  DAILY-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DAILY-EXTRACT-RECORD     PIC  X(35).                                 
                                                                                
       FD  SVC-LVL-LOAD-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SVC-LVL-DATA-LOAD-RECORD PIC  X(80).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-----------------------------------------------------------------        
      *  RECORD LAYOUT FOR THE ITEM/STORE INPUT FILE                            
      *-----------------------------------------------------------------        
           COPY MHRD008.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  INPUT RECORD LAYOUT FOR THE DAILY EXTRACT FILE                         
      *-----------------------------------------------------------------        
           COPY MHRD006.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  OUTPUT RECORD LAYOUT FOR THE SVC LEVEL LOAD FILE                       
      *-----------------------------------------------------------------        
           COPY MHRD009.                                                        
                                                                                
      *-----------------------------------------------------------------        
      * WORKING-STORAGE FOR CALENDAR ROUTINE                                    
      *-----------------------------------------------------------------        
           COPY DPWS500.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *    CONTROL CARD INPUT DATE YYYY-MM-DD                                   
      *-----------------------------------------------------------------        
       01  CD-SCHEDULE-DATE.                                                    
           05  CD-SCHED-YEAR              PIC X(04).                            
           05  FILLER                     PIC X(01).                            
           05  CD-SCHED-MONTH             PIC X(02).                            
           05  FILLER                     PIC X(01).                            
           05  CD-SCHED-DAY               PIC X(02).                            
                                                                                
                                                                                
       01  ABEND-CODE                     PIC S9(04)  VALUE +0                  
                                                      COMP SYNC.                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-OUTPUT-RECS             PIC  9(11) VALUE ZERO.                
           05  PV-DAYS-OUT-STOCK          PIC  S9(3) COMP-3 VALUE ZERO.         
           05  PV-DAYS-BELOW-MIN          PIC  S9(3) COMP-3 VALUE ZERO.         
                                                                                
           05  PV-YYYYMMDD-DATE.                                                
               10  PV-YEAR                PIC X(04).                            
               10  PV-MONTH               PIC X(02).                            
               10  PV-DAY                 PIC X(02).                            
                                                                                
           05  PV-ORACLE-DATE.                                                  
               10  PV-ORACLE-YMD          PIC  X(10).                           
               10  FILLER                 PIC  X(01) VALUE '-'.                 
               10  FILLER                 PIC  X(08) VALUE '00.00.00'.          
                                                                                
           05  PV-ORACLE-BOW-DATE.                                              
               10  PV-BOW-DATE            PIC  X(10).                           
               10  FILLER                 PIC  X(01) VALUE '-'.                 
               10  FILLER                 PIC  X(08) VALUE '00.00.00'.          
      *                                                                         
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-ITEM-STR-SW   PIC  X(01) VALUE 'N'.                       
               88  PS-END-ITEM-STR-FILE        VALUE 'Y'.                       
           05  PS-END-DAILY-EXT-SW  PIC  X(01) VALUE 'N'.                       
               88  PS-END-DAILY-EXT-FILE       VALUE 'Y'.                       
                                                                                
               EJECT                                                            
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
      *-----------------------------------------------------------------        
           OPEN OUTPUT SVC-LVL-LOAD-FILE                                        
                 INPUT ITEM-STORE-FILE                                          
                       DAILY-EXTRACT-FILE.                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MATCH-MERGE                                             
               UNTIL PS-END-ITEM-STR-FILE.                                      
                                                                                
           CLOSE SVC-LVL-LOAD-FILE                                              
                 ITEM-STORE-FILE                                                
                 DAILY-EXTRACT-FILE.                                            
                                                                                
           DISPLAY 'DATA LOAD RECORDS WRITTEN -- '                              
                   PV-OUTPUT-RECS.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
      *----------------------------------------------------------------*        
           ACCEPT CD-SCHEDULE-DATE.                                             
           MOVE CD-SCHEDULE-DATE TO PV-ORACLE-YMD.                              
      *                                                                         
           PERFORM 1500-GET-BOW-DATE.                                           
                                                                                
      *    ----- INITIALIZE OUTPUT RECORD -----                                 
           MOVE PV-ORACLE-BOW-DATE TO MH009-BOW-DATE.                           
           MOVE PV-ORACLE-DATE     TO MH009-CREATE-DATE.                        
           MOVE 'N'                TO MH009-SIZE-PRFL-IND.                      
      *                                                                         
           PERFORM 7000-READ-ITEM-STORE-FILE.                                   
                                                                                
           PERFORM 7100-READ-DAILY-EXTRACT.                                     
                                                                                
      *                                                                         
      ****************************************************************          
      *    GET THE BEGINNING OF WEEK DATE                                       
      ****************************************************************          
       1500-GET-BOW-DATE.                                                       
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2                                          
               DPG51-DO-NOT-INCR-DECR-DATE                                      
               DPG52-LK-DTE-ISO            TO TRUE.                             
                                                                                
           MOVE CD-SCHED-YEAR    TO PV-YEAR.                                    
           MOVE CD-SCHED-MONTH   TO PV-MONTH.                                   
           MOVE CD-SCHED-DAY     TO PV-DAY.                                     
           MOVE PV-YYYYMMDD-DATE TO DPG52-LK-DATE-INPUT.                        
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           IF DPG54-SEVERE-ERROR                                                
               DISPLAY '** ABEND ** IN MHKUT011'                                
               DISPLAY '** PARAGRAPH: 1500-GET-BOW-DATE'                        
               DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '                   
                           DPG54-ERROR-MESSAGE                                  
               DISPLAY '** DPG51 = ' DPG51                                      
               DISPLAY '** DPG52 = ' DPG52                                      
               DISPLAY '** DPG53 = ' DPG53                                      
               DISPLAY '** DPG54 = ' DPG54                                      
               DISPLAY '** DPG55 = ' DPG55                                      
               DISPLAY '** DPG56 = ' DPG56                                      
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               IF DPG54-WARNING-ERROR                                           
                   DISPLAY '** WARNING RETURNED BY CALENDAR ROUTINE'            
                   DISPLAY '** CALLED BY 1500-GET-BOW-DATE'                     
                   DISPLAY '** WARNING MESSAGE = ' DPG54-ERROR-MESSAGE          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PV-BOW-DATE.                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** BEGIN OF WEEK DATE: ' PV-BOW-DATE.                       
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    MERGE THE ITEM/STORE LIST WITH THE DAILY OUT-OF-STOCK                
      *    EXTRACT FILE.  ADD THE OUT-OF-STOCK COUNTERS TOGETHER                
      *    WHEN FIND A MATCH.  WRITE ALL ITEM/STORES TO LOAD FILE.              
      *----------------------------------------------------------------*        
       2000-MATCH-MERGE.                                                        
                                                                                
           IF PS-END-DAILY-EXT-FILE                                             
              MOVE MH008-DAYS-OUT-STOCK TO PV-DAYS-OUT-STOCK                    
              MOVE MH008-DAYS-BELOW-MIN TO PV-DAYS-BELOW-MIN                    
                                                                                
              PERFORM 8000-WRITE-LOAD-RECORD                                    
              PERFORM 7000-READ-ITEM-STORE-FILE                                 
           ELSE                                                                 
                                                                                
           IF MH008-ITEM-NBR = MH006-ITEM-NBR                                   
              IF MH008-LOC-NBR = MH006-LOC-NBR                                  
                                                                                
      ***        --- FILES MATCH, CALCULATE OUT-OF-STOCKS ---                   
                 ADD MH006-DAYS-OUT-STOCK, MH008-DAYS-OUT-STOCK                 
                    GIVING PV-DAYS-OUT-STOCK                                    
                                                                                
                 ADD MH006-DAYS-BELOW-MIN, MH008-DAYS-BELOW-MIN                 
                    GIVING PV-DAYS-BELOW-MIN                                    
                                                                                
                 PERFORM 8000-WRITE-LOAD-RECORD                                 
                                                                                
                 PERFORM 7000-READ-ITEM-STORE-FILE                              
                 PERFORM 7100-READ-DAILY-EXTRACT                                
                                                                                
              ELSE                                                              
      *          --- ITEMS EQUAL, STORE NUMBERS NOT ---                         
                 IF ((MH008-LOC-NBR > MH006-LOC-NBR)                            
                    AND NOT PS-END-DAILY-EXT-FILE)                              
                       PERFORM 7100-READ-DAILY-EXTRACT                          
                 ELSE                                                           
      ***           --- NO MATCH (NO DAILY OUT-OF-STOCKS) ---                   
                    MOVE MH008-DAYS-OUT-STOCK TO PV-DAYS-OUT-STOCK              
                    MOVE MH008-DAYS-BELOW-MIN TO PV-DAYS-BELOW-MIN              
                                                                                
                    PERFORM 8000-WRITE-LOAD-RECORD                              
                    PERFORM 7000-READ-ITEM-STORE-FILE                           
                 END-IF                                                         
              END-IF                                                            
                                                                                
           ELSE                                                                 
      ***     --- ITEM NUMBERS DO NOT MATCH ---                                 
              IF MH008-ITEM-NBR > MH006-ITEM-NBR                                
                 IF PS-END-DAILY-EXT-FILE                                       
                                                                                
      ***           --- NO MATCH (NO DAILY OUT-OF-STOCKS) ---                   
                    MOVE MH008-DAYS-OUT-STOCK TO PV-DAYS-OUT-STOCK              
                    MOVE MH008-DAYS-BELOW-MIN TO PV-DAYS-BELOW-MIN              
                                                                                
                    PERFORM 8000-WRITE-LOAD-RECORD                              
                    PERFORM 7000-READ-ITEM-STORE-FILE                           
                 ELSE                                                           
                    PERFORM 7100-READ-DAILY-EXTRACT                             
                 END-IF                                                         
              ELSE                                                              
      ***        --- NO MATCH (NO DAILY OUT-OF-STOCKS) ---                      
                 MOVE MH008-DAYS-OUT-STOCK TO PV-DAYS-OUT-STOCK                 
                 MOVE MH008-DAYS-BELOW-MIN TO PV-DAYS-BELOW-MIN                 
                                                                                
                 PERFORM 8000-WRITE-LOAD-RECORD                                 
                 PERFORM 7000-READ-ITEM-STORE-FILE                              
              END-IF                                                            
           END-IF                                                               
           END-IF.                                                              
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       7000-READ-ITEM-STORE-FILE.                                               
      *-----------------------------------------------------------------        
           READ ITEM-STORE-FILE                                                 
               INTO MH008-SVC-LEVEL-DATA-RECORD                                 
             AT END                                                             
                SET PS-END-ITEM-STR-FILE TO TRUE.                               
                                                                                
      *-----------------------------------------------------------------        
       7100-READ-DAILY-EXTRACT.                                                 
      *-----------------------------------------------------------------        
           READ DAILY-EXTRACT-FILE                                              
               INTO MH006-SVC-LVL-DAILY-RECORD                                  
             AT END                                                             
                SET PS-END-DAILY-EXT-FILE TO TRUE.                              
                                                                                
      *-----------------------------------------------------------------        
       8000-WRITE-LOAD-RECORD.                                                  
      *-----------------------------------------------------------------        
                                                                                
           MOVE MH008-CW-SKU-ID   TO MH009-CW-SKU-ID.                           
           MOVE MH008-CW-LOC-ID   TO MH009-CW-LOC-ID.                           
           MOVE MH008-STATUS-CODE TO MH009-STATUS-CODE.                         
           MOVE MH008-REPLNMT-IND TO MH009-REPLNMT-IND.                         
           MOVE PV-DAYS-OUT-STOCK TO MH009-DAYS-OUT-STOCK.                      
           MOVE PV-DAYS-BELOW-MIN TO MH009-DAYS-BELOW-MIN.                      
                                                                                
           WRITE SVC-LVL-DATA-LOAD-RECORD                                       
               FROM MH009-SVC-LEVEL-LOAD-RECORD.                                
                                                                                
           INITIALIZE SVC-LVL-DATA-LOAD-RECORD.                                 
           ADD +1 TO PV-OUTPUT-RECS.                                            
                                                                                
