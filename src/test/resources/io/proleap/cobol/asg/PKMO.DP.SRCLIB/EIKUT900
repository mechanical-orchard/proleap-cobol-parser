      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     EIKUT900.                                                
       AUTHOR.         KARI HAAK.                                               
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/13/11.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM WILL READ DATES  FROM A CONTROL CARD AND CALL    *        
      *  THE CALENDAR ROUTINE TO RETURN FISCAL DATE INFORMATION.       *        
      *                                                                *        
      *  CONTROL CARD START AND END DATES: YYYY-MM-DD YYYY-MM-DD       *        
      *  START DATE MUST BE 1 DAY PRIOR TO THE ACTUAL DATE YOU WISH TO *        
      *     BEGIN WITH!!!                                              *        
      *  END DATE MUST BE THE ACTUAL DATE YOU WISH TO END WITH.        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-CONTROL-CARD                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EI-DATE-COBOL-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT EI-DATE-COUNT-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-CONTROL-CARD                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-CONTROL-RECORD         PIC X(80).                               
                                                                                
       FD  EI-DATE-COBOL-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EI-DATE-COBOL-RECORD         PIC X(345).                             
                                                                                
       FD  EI-DATE-COUNT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EI-DATE-COUNT-RECORD         PIC X(015).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DATE-CONTROL-REC.                                                    
           05  DC-START-DATE                  PIC   X(10).                      
           05                                 PIC   X(01).                      
           05  DC-END-DATE                    PIC   X(10).                      
           05                                 PIC   X(59).                      
                                                                                
       01  WS-COUNT-RECORD.                                                     
           05  WS-COUNT-NBR                   PIC   9(15).                      
                                                                                
      *  TIME DIMENSION                                                         
           COPY EIRD002.                                                        
                                                                                
      *  CALENDAR INCLUDES                                                      
           COPY DPWS500.                                                        
                                                                                
       01  ABEND-CODE                    PIC S9(4)  VALUE ZEROS                 
                                         COMP SYNC.                             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DATE-CONTROL-CARD-SW                                   
                                         PIC X(1)   VALUE 'N'.                  
               88  PS-END-OF-DATE-CONTROL-CARD      VALUE 'Y'.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(8)   VALUE 'EIKUT900'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-FISCAL-WEEK-OF-MONTH   PIC 9(1)   VALUE ZERO.                 
           05  PV-MONTH                  PIC X(2).                              
               88 PV-FIRST-QUARTER       VALUE '01' '02' '03'.                  
               88 PV-SECOND-QUARTER      VALUE '04' '05' '06'.                  
               88 PV-THIRD-QUARTER       VALUE '07' '08' '09'.                  
               88 PV-FOURTH-QUARTER      VALUE '10' '11' '12'.                  
           05  PV-WEEKDAY                PIC X(1).                              
               88 PV-WEEKDAY-NUM         VALUE '2' '3' '4' '5' '6'.             
           05  PV-NBR-FISCAL-WEEKS       PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  PV-DATES.                                                            
           05  PV-PROCESS-DATE           PIC   X(10).                           
           05  PV-TEMP-DATE              PIC   X(10).                           
                                                                                
       01  PV-SUMMARY-COUNTS.                                                   
           05  PV-EI-DATE-LOAD-REC-OUT   PIC  9(15)   VALUE ZERO.               
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-DATES                                           
               UNTIL PV-PROCESS-DATE >= DC-END-DATE                             
                  OR PS-END-OF-DATE-CONTROL-CARD.                               
                                                                                
           PERFORM 5000-WRITE-COUNT.                                            
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, AND       *        
      *  READING THE FIRST DAILY SALES FILE RECORD.                    *        
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  DATE-CONTROL-CARD.                                       
           OPEN OUTPUT EI-DATE-COBOL-FILE.                                      
           OPEN OUTPUT EI-DATE-COUNT-FILE.                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                                  
                                                                                
           PERFORM 7000-READ-DATE-CONTROL.                                      
           DISPLAY 'CONTROL CARD: '.                                            
           DISPLAY 'START DATE/END DATE'.                                       
           DISPLAY DATE-CONTROL-REC.                                            
                                                                                
           MOVE DC-START-DATE TO PV-PROCESS-DATE.                               
                                                                                
      ******************************************************************        
      *  PROCESS DATES.                                                *        
      ******************************************************************        
                                                                                
       2000-PROCESS-DATES.                                                      
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-INCREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
           MOVE 1                TO DPG51-INCR-DECR-DAYS-9.                     
           MOVE PV-PROCESS-DATE  TO DPG52-LK-DATE-INPUT.                        
                                                                                
      *    DISPLAY 'PV-PROCESS-DATE:  ' PV-PROCESS-DATE.                        
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
                                                                                
           PERFORM 4000-CREATE-EI-DATE-LOAD-REC.                                
                                                                                
      ******************************************************************        
      *  CREATE AND WRITE THE DW DATE RECORD.                          *        
      ******************************************************************        
                                                                                
       4000-CREATE-EI-DATE-LOAD-REC.                                            
                                                                                
           INITIALIZE EI002-TM-DIM-RECORD.                                      
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT    TO EI002-TM-DIM-KY-DTE.               
           MOVE DPG55-JULIAN-DATE-CCYYDDD TO EI002-JLN-DTE.                     
           MOVE DPG55-ALPHA-WEEK-DAY      TO EI002-DAY-NM.                      
           MOVE DPG55-ALPHA-MONTH         TO EI002-CAL-MN-NM.                   
           MOVE DPG56-FISCAL-YEAR-CCYY    TO EI002-FSCL-WK-ID-CCYY              
                                             EI002-FSCL-QTR-ID-CCYY             
                                             EI002-SEA-ID-CCYY                  
                                             EI002-FSCL-YR-NBR                  
                                             EI002-FSCL-MN-ID-CCYY              
                                             EI002-FSCL-YR-DAY-CCYY.            
                                                                                
           MOVE DPG56-NBR-FISCAL-DAYS     TO EI002-FSCL-YR-DAY-DDD.             
           MOVE DPG55-DB2-ISO-DATE-FMT-DY TO EI002-CAL-DAY-OF-MN-NBR.           
           MOVE DPG55-DB2-ISO-DATE-FMT-YR TO EI002-CAL-YR-NBR.                  
           MOVE DPG56-FISCAL-WEEK-NBR     TO EI002-FSCL-WK-NBR                  
                                             EI002-FSCL-WK-ID-WK.               
           MOVE DPG56-FISCAL-PERIOD-NBR   TO EI002-FSCL-MN-NBR                  
                                             EI002-FSCL-MN-ID-MN.               
                                                                                
           MOVE DPG56-FISCAL-PERIOD-WEEK  TO PV-FISCAL-WEEK-OF-MONTH.           
           MOVE PV-FISCAL-WEEK-OF-MONTH   TO EI002-FSCL-WK-OF-MN-NBR.           
                                                                                
           MOVE DPG56-FISCAL-QTR-NBR      TO EI002-FSCL-QTR-NBR                 
                                             EI002-FSCL-QTR-ID-QTR.             
           MOVE DPG56-CURR-FISCAL-SEASON  TO EI002-SEA-DESC.                    
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT-MO TO PV-MONTH                           
                                             EI002-CAL-MN-NBR.                  
      *  MOVE THIS WEEKS 1ST DAY OF WEEK (SUNDAY DATE)                          
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT  TO EI002-BEGG-FSCL-WTD-DTE.          
                                                                                
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO EI002-BEGG-FSCL-MTD-DTE.          
                                                                                
           MOVE DPG56-FIRST-FQ-DB2-ISO-FMT TO EI002-BEGG-FSCL-QTD-DTE.          
                                                                                
      *  MOVE THIS WEEKS LAST DAY OF WEEK (SATURDAY DATE)                       
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO EI002-ENDG-FSCL-WTD-DTE.          
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST DAY OF A QUARTER                    
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FQ-DB2-ISO-FMT               
               MOVE 'Y'         TO EI002-BEG-DAY-IN-QTR-IND                     
           ELSE                                                                 
               MOVE 'N'         TO EI002-BEG-DAY-IN-QTR-IND                     
           END-IF.                                                              
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST DAY OF A YEAR                       
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FD-DB2-ISO-FMT               
               MOVE 'Y'         TO EI002-BEG-DAY-IN-YR-IND                      
           ELSE                                                                 
               MOVE 'N'         TO EI002-BEG-DAY-IN-YR-IND                      
           END-IF.                                                              
                                                                                
           MOVE DPG56-FIRST-FS-DB2-ISO-FMT TO EI002-BEGG-FSCL-STD-DTE.          
           MOVE DPG56-FIRST-FD-DB2-ISO-FMT TO EI002-BEGG-FSCL-YTD-DTE.          
                                                                                
           EVALUATE TRUE                                                        
              WHEN PV-FIRST-QUARTER                                             
                   MOVE '1' TO EI002-CAL-QTR-NBR                                
              WHEN PV-SECOND-QUARTER                                            
                   MOVE '2' TO EI002-CAL-QTR-NBR                                
              WHEN PV-THIRD-QUARTER                                             
                   MOVE '3' TO EI002-CAL-QTR-NBR                                
              WHEN PV-FOURTH-QUARTER                                            
                   MOVE '4' TO EI002-CAL-QTR-NBR                                
           END-EVALUATE.                                                        
           IF DPG55-DB2-ISO-DATE-FMT-DY = 1                                     
             MOVE 'Y' TO EI002-CAL-BOM-IND                                      
           ELSE                                                                 
             MOVE 'N' TO EI002-CAL-BOM-IND.                                     
           IF DPG55-DB2-ISO-DATE-FMT-DY = DPG55-DAYS-IN-MONTH                   
             MOVE 'Y' TO EI002-CAL-EOM-IND                                      
           ELSE                                                                 
             MOVE 'N' TO EI002-CAL-EOM-IND.                                     
           MOVE DPG55-NUMERIC-WEEK-DAY TO PV-WEEKDAY                            
                                          EI002-DAY-NBR.                        
           IF PV-WEEKDAY-NUM                                                    
              MOVE 'Y' TO EI002-WKDA-IND                                        
           ELSE                                                                 
              MOVE 'N' TO EI002-WKDA-IND.                                       
           IF DPG56-CURR-FISCAL-SEASON = 'SPRING'                               
               MOVE 'S'             TO EI002-SEA-S-F                            
           ELSE                                                                 
               MOVE 'F'             TO EI002-SEA-S-F.                           
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST OR LAST DAY OF A SEASON             
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FS-DB2-ISO-FMT               
               MOVE 'Y'         TO EI002-BEG-SEA-IND                            
           ELSE                                                                 
               MOVE 'N'         TO EI002-BEG-SEA-IND                            
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FS-DB2-ISO-FMT                
               MOVE 'Y'         TO EI002-END-SEA-IND                            
           ELSE                                                                 
               MOVE 'N'         TO EI002-END-SEA-IND                            
           END-IF.                                                              
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST OR LAST DAY OF A MONTH/WEEK         
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FP-DB2-ISO-FMT               
               MOVE 'Y'         TO EI002-BEG-DAY-IN-MN-IND                      
           ELSE                                                                 
               MOVE 'N'         TO EI002-BEG-DAY-IN-MN-IND                      
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FP-DB2-ISO-FMT                
               MOVE 'Y'         TO EI002-LAST-DAY-IN-MN-IND                     
           ELSE                                                                 
               MOVE 'N'         TO EI002-LAST-DAY-IN-MN-IND                     
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG55-1ST-WKDY-DB2ISO-FMT                
               MOVE 'Y'         TO EI002-BEG-DAY-IN-WK-IND                      
           ELSE                                                                 
               MOVE 'N'         TO EI002-BEG-DAY-IN-WK-IND                      
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG55-LAST-WKDY-DB2ISO-FMT               
               MOVE 'Y'         TO EI002-LAST-DAY-IN-WK-IND                     
           ELSE                                                                 
               MOVE 'N'         TO EI002-LAST-DAY-IN-WK-IND                     
           END-IF.                                                              
                                                                                
           MOVE DPG56-FISCAL-PERIOD-NAME  TO EI002-FSCL-MN-NM.                  
                                                                                
           MOVE DPG55-STORE-HOLIDAY-IND   TO EI002-HOL-IND.                     
                                                                                
      *  FILL HOLIDAY NAME IF THIS DATE IS A STORE HOLIDAY                      
           IF DPG55-STORE-HOLIDAY                                               
              MOVE DPG55-HOLIDAY-NAME TO EI002-PRIM-HOL-NM                      
           END-IF.                                                              
                                                                                
                                                                                
      *  SAVE THE CURRENT DATE BEING PROCESSED (FOR LOOPING)                    
           MOVE DPG55-DB2-ISO-DATE-FMT TO PV-PROCESS-DATE.                      
           MOVE DPG56-NBR-FISCAL-WEEKS TO PV-NBR-FISCAL-WEEKS.                  
                                                                                
           PERFORM 4100-FIND-LY-TDA-DTE.                                        
                                                                                
      *  DETERMINE LAST WEEKS 1ST DAY (SUNDAY DATE)                             
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 7                      TO DPG51-INCR-DECR-DAYS-9.               
           MOVE EI002-BEGG-FSCL-WTD-DTE TO DPG52-LK-DATE-INPUT.                 
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-TDA-PWK-DTE.                    
                                                                                
      *  DETERMINE 2 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT         TO TRUE.                            
           MOVE 14                          TO DPG51-INCR-DECR-DAYS-9.          
           MOVE EI002-BEGG-FSCL-WTD-DTE TO DPG52-LK-DATE-INPUT.                 
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-TDA-PWK2-DTE.                   
                                                                                
      *  DETERMINE 3 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 21                     TO DPG51-INCR-DECR-DAYS-9.               
           MOVE EI002-BEGG-FSCL-WTD-DTE TO DPG52-LK-DATE-INPUT.                 
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-TDA-PWK3-DTE.                   
                                                                                
      *  DETERMINE 4 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 28                     TO DPG51-INCR-DECR-DAYS-9.               
           MOVE EI002-BEGG-FSCL-WTD-DTE TO DPG52-LK-DATE-INPUT.                 
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-TDA-PWK4-DTE.                   
                                                                                
      *  DETERMINE NEXT WEEKS 1ST DAY (SUNDAY DATE)                             
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-INCREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 7                      TO DPG51-INCR-DECR-DAYS-9.               
           MOVE EI002-BEGG-FSCL-WTD-DTE TO DPG52-LK-DATE-INPUT.                 
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-TDA-NWK-DTE.                    
                                                                                
      *  DETERMINE PREVIOUS SEASON'S LAST FISCAL SUNDAY DATE                    
      *  AND PREVIOUS SEASON'S LAST FISCAL SATURDAY DATE                        
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
      *  INCLUDE LOGIC FOR 53 WEEK FISCAL YEAR.                                 
           IF EI002-FSCL-WK-ID-WK = '53'                                        
               MOVE 189 TO DPG51-INCR-DECR-DAYS-9                               
           ELSE                                                                 
               MOVE 182 TO DPG51-INCR-DECR-DAYS-9                               
           END-IF.                                                              
      *  REPLACE INPUT DATE WITH CURRENT DATE BEING PROCESSED                   
           MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT.                         
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-LAST-FISCAL-SEASON-DAY TO PV-TEMP-DATE.                   
           MOVE PV-TEMP-DATE TO DPG52-LK-DATE-INPUT.                            
           MOVE 0 TO DPG51-INCR-DECR-DAYS-9.                                    
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                                    
                                       EI002-LAST-SEA-LAST-BOW-DTE.             
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO                                   
                                       EI002-LAST-SEA-EOW-DTE.                  
                                                                                
      *  DETERMINE PREVIOUS YEAR'S LAST FISCAL SUNDAY DATE                      
      *  AND PREVIOUS YEAR'S LAST FISCAL SATURDAY DATE                          
      *  IF FISCAL SEASON IS SPRING , DECREMENT 182, ELSE 364                   
           IF EI002-SEA-S-F = 'S'                                               
               IF EI002-FSCL-WK-ID-WK = '53'                                    
                   MOVE 189 TO DPG51-INCR-DECR-DAYS-9                           
               ELSE                                                             
                   MOVE 182 TO DPG51-INCR-DECR-DAYS-9                           
               END-IF                                                           
           ELSE                                                                 
               IF EI002-FSCL-WK-ID-WK = '53'                                    
                   MOVE 371 TO DPG51-INCR-DECR-DAYS-9                           
               ELSE                                                             
                   MOVE 364 TO DPG51-INCR-DECR-DAYS-9                           
               END-IF                                                           
           END-IF.                                                              
           MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT.                         
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-LAST-FISCAL-SEASON-DAY TO PV-TEMP-DATE.                   
           MOVE 0 TO DPG51-INCR-DECR-DAYS-9.                                    
           MOVE PV-TEMP-DATE TO DPG52-LK-DATE-INPUT.                            
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO EI002-LY-LAST-BOW-DTE.             
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO EI002-LY-LAST-EOW-DTE.            
                                                                                
      *  CALCULATE FISCAL MONTH ID FOR 1 YEAR PREVIOUS (ADDED FIELD             
      *    FOR ML PROJECT)                                                      
           MOVE ZEROES TO EI002-LAST-YR-FSCL-MN-ID.                             
           COMPUTE EI002-LAST-YR-FSCL-MN-ID = EI002-FSCL-MN-ID                  
                   - 100.                                                       
                                                                                
      *  DETERMINE IF THIS DAY IS THE LAST SUNDAY DATE OF THE MONTH.            
           IF EI002-DAY-NM = 'SUNDAY'                                           
               SET DPG51-FISCAL-454-CALENDAR                                    
                   DPG51-INCREMENT-DATE                                         
                   DPG52-LK-DTE-ISO-FMT    TO TRUE                              
               MOVE 6 TO DPG51-INCR-DECR-DAYS-9                                 
               MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT                      
               PERFORM 7200-CALL-CALENDAR-ROUTINE                               
               IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FP-DB2-ISO-FMT            
                   MOVE 'Y' TO EI002-FSCL-MN-LST-BOW-IND                        
               ELSE                                                             
                   MOVE 'N' TO EI002-FSCL-MN-LST-BOW-IND                        
               END-IF                                                           
           ELSE                                                                 
               MOVE 'N' TO EI002-FSCL-MN-LST-BOW-IND                            
           END-IF.                                                              
                                                                                
      *  RETRIEVE PREVIOUS FISCAL MONTH                                         
      *  USE CURRENT FISCAL PERIOD (CCYYMM) AS INPUT DATE BEING                 
      *  PROCESSED AND SUBTRACT 1 FISCAL PERIOD (MONTH).                        
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                                
           MOVE EI002-FSCL-MN-ID        TO DPG52-LK-DATE-INPUT.                 
           MOVE 1                       TO DPG51-INCR-DECR-FISCAL-PRD-9.        
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-FISCAL-YEAR-CCYY    TO EI002-PREV-FSCL-MN-CCYY            
           MOVE DPG56-FISCAL-PERIOD-NBR   TO EI002-PREV-FSCL-MN.                
                                                                                
      *  RETRIEVE PREVIOUS FISCAL QUARTER                                       
      *  USE CURRENT FISCAL PERIOD (CCYYMM) AS INPUT DATE BEING                 
      *  PROCESSED AND SUBTRACT 3 FISCAL PERIODS (MONTHS).                      
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                                
           MOVE EI002-FSCL-MN-ID        TO DPG52-LK-DATE-INPUT.                 
           MOVE 3                       TO DPG51-INCR-DECR-FISCAL-PRD-9.        
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-FISCAL-YEAR-CCYY    TO EI002-PREV-FSCL-QTR-CCYY.          
           MOVE DPG56-FISCAL-QTR-NBR      TO EI002-PREV-FSCL-QTR.               
                                                                                
      *  RETRIEVE PREVIOUS FISCAL SEASON                                        
      *  USE CURRENT FISCAL PERIOD (CCYYMM) AS INPUT DATE BEING                 
      *  PROCESSED AND SUBTRACT 6 FISCAL PERIODS (MONTHS).                      
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                                
           MOVE EI002-FSCL-MN-ID        TO DPG52-LK-DATE-INPUT.                 
           MOVE 6                       TO DPG51-INCR-DECR-FISCAL-PRD-9.        
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-FISCAL-YEAR-CCYY    TO EI002-PREV-SEA-CCYY.               
           IF DPG56-CURR-FISCAL-SEASON = 'SPRING'                               
              MOVE 'S' TO EI002-PREV-SEA-S-F                                    
           ELSE                                                                 
              MOVE 'F' TO EI002-PREV-SEA-S-F.                                   
                                                                                
      *  RETRIEVE LAST YEAR FISCAL MONTH, QUARTER, SEASON, YEAR                 
      *  USE CURRENT FISCAL PERIOD (CCYYMM) AS INPUT DATE BEING                 
      *  PROCESSED.                                                             
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                                
           MOVE EI002-FSCL-MN-ID        TO DPG52-LK-DATE-INPUT.                 
           MOVE 12                      TO DPG51-INCR-DECR-FISCAL-PRD-9.        
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-FISCAL-YEAR-CCYY TO EI002-LAST-YR-FSCL-MN-CCYY            
                                          EI002-LY-FSCL-QTR-CCYY                
                                          EI002-LY-SEA-ID-CCYY                  
                                          EI002-LY-FSCL-YR-NBR.                 
           MOVE DPG56-FISCAL-PERIOD-NBR   TO EI002-LAST-YR-FSCL-MN.             
           MOVE DPG56-FISCAL-QTR-NBR      TO EI002-LY-FSCL-QTR.                 
           IF DPG56-CURR-FISCAL-SEASON = 'SPRING'                               
              MOVE 'S' TO EI002-LY-SEA-S-F                                      
           ELSE                                                                 
              MOVE 'F' TO EI002-LY-SEA-S-F                                      
           END-IF.                                                              
                                                                                
      *  UPDATE RECORD COUNT                                                    
           ADD +1                        TO PV-EI-DATE-LOAD-REC-OUT.            
           PERFORM 7150-WRITE-EI-DATE-COBOL-REC.                                
                                                                                
      ******************************************************************        
      *  FIND LAST YEAR'S DATE FOR TODAY                               *        
      ******************************************************************        
       4100-FIND-LY-TDA-DTE.                                                    
                                                                                
      *  DETERMINE LAST YEAR THIS DAY (THERE ARE 364 DAYS IN OUR                
      *  FISCAL CALENDAR FOR NORMAL 52 WEEK YEARS. FOR A 52 WEEK YEAR           
      *  THAT FOLLOWS A 53 WEEK YEAR, USE 371 DAYS BACK FOR LY DAY.)            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 364                    TO DPG51-INCR-DECR-DAYS-9.               
           MOVE PV-PROCESS-DATE        TO DPG52-LK-DATE-INPUT.                  
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
                                                                                
      *    THIS YEAR HAS 52 FISCAL WEEKS                                        
           IF PV-NBR-FISCAL-WEEKS = 52                                          
      *        LAST YEAR HAD 53 FISCAL WEEKS, SO NEED TO SUBTRACT               
      *        7 MORE DAYS FOR A TOTAL OF 371 DAYS                              
               IF DPG56-NBR-FISCAL-WEEKS = 53                                   
                   SET DPG51-FISCAL-454-CALENDAR                                
                       DPG51-DECREMENT-DATE                                     
                       DPG52-LK-DTE-ISO-FMT    TO TRUE                          
                   MOVE 7                      TO DPG51-INCR-DECR-DAYS-9        
                   MOVE DPG55-DB2-ISO-DATE-FMT TO DPG52-LK-DATE-INPUT           
                   PERFORM 7200-CALL-CALENDAR-ROUTINE                           
                   MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-LY-TDA-DTE              
               ELSE                                                             
      *            LAST YEAR HAD 52 FISCAL WEEKS                                
                   MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-LY-TDA-DTE              
               END-IF                                                           
           ELSE                                                                 
      *        THIS YEAR HAS 53 FISCAL WEEKS                                    
               MOVE DPG55-DB2-ISO-DATE-FMT TO EI002-LY-TDA-DTE                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE THE RECORD COUNT FILE.                                  *        
      ******************************************************************        
       5000-WRITE-COUNT.                                                        
                                                                                
           MOVE PV-EI-DATE-LOAD-REC-OUT TO WS-COUNT-NBR.                        
           WRITE EI-DATE-COUNT-RECORD FROM WS-COUNT-RECORD.                     
                                                                                
      ******************************************************************        
      *  READ THE DATE CONTROL CARD.                                   *        
      ******************************************************************        
                                                                                
       7000-READ-DATE-CONTROL.                                                  
                                                                                
           READ DATE-CONTROL-CARD                                               
               INTO DATE-CONTROL-REC                                            
               AT END                                                           
                   SET PS-END-OF-DATE-CONTROL-CARD TO TRUE                      
           END-READ.                                                            
                                                                                
           IF PS-END-OF-DATE-CONTROL-CARD                                       
               DISPLAY 'EMPTY DATE CONTROL CARD'                                
               MOVE +4008 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE A DATE LOAD FILE RECORD.                                *        
      ******************************************************************        
                                                                                
       7150-WRITE-EI-DATE-COBOL-REC.                                            
                                                                                
           WRITE EI-DATE-COBOL-RECORD FROM EI002-TM-DIM-RECORD.                 
                                                                                
      ******************************************************************        
      *    CALL THE CALENDAR ROUTINE                                   *        
      ******************************************************************        
                                                                                
       7200-CALL-CALENDAR-ROUTINE.                                              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52                 
                                                    DPG53 DPG54                 
                                                    DPG55 DPG56.                
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY '** ABEND - PROGRAM '                                
                           PC-PROGRAM-NAME ' **'                                
                   DISPLAY '** BAD CALL TO CALENDAR MODULE DPKUT500'            
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4019 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE ALL FILES AND PRINT CONTROL TOTALS.                     *        
      ******************************************************************        
                                                                                
       8000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY 'DATE RECS WRITTEN: ' PV-EI-DATE-LOAD-REC-OUT.               
                                                                                
           CLOSE DATE-CONTROL-CARD                                              
                 EI-DATE-COBOL-FILE                                             
                 EI-DATE-COUNT-FILE.                                            
                                                                                
      ******************************************************************        
      ******************************************************************        
