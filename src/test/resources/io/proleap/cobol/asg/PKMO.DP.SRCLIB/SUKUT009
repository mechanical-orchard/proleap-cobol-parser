      *****************************************************************         
      * RFID AUDIT VERSION - CHECKED OUT BY DAVE WELLER 01/03/2014              
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             SUKUT009.                                        
       AUTHOR.                 MARY SARDINA.                                    
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            SEPTEMBER, 1997.                                 
       DATE-COMPILED.                                                           
      *****************************************************************         
      *****************************************************************         
      *                                                               *         
      *  THIS PROGRAM WILL CREATE AN EXTRACT FILES FROM THE AUDIT     *         
      *  DATABASE.                                                    *         
      *****************************************************************         
      *  CHANGED 02/26/02 BY GERMAN BANDA                             *         
      *   WR# 22757  ADD CHECK TO NOT SELECT SCAN TYPE CODES OF 'E'   *         
      *            IN THE ITEM-CSR                                    *         
      *  CHANGED 10/05/09 BY SAURABH DUDDALWAR                        *         
      *   CHANGES ARE DONE TO HANDLE DATA CONTENTION ERRORS IN THE    *         
      *            PROGRAM WHILE REPORT GENERATION.                   *         
      *  CHANGED 04/10/14 BY SHOBHIT BAIJAL                           *         
      *   PR# 5148D  ADDED LOGIC FOR FETCHING RFID UPC MISPACK WITH A *         
      *              ERROR COUNT OF 1.                                *         
      *****************************************************************         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT CARDIN-AUDIT-NBR-FILE                                         
               ASSIGN TO CARDIN.                                                
                                                                                
           SELECT SU-VENDOR-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT SU-RECEIVER-EXTRACT-FILE                                      
               ASSIGN TO OUT02.                                                 
      *                                                                         
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  CARDIN-AUDIT-NBR-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  CC-RECORD                       PIC X(80).                           
                                                                                
       FD  SU-VENDOR-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  SU-VENDOR-EXTRACT-RECORD        PIC  X(200).                         
      *                                                                         
                                                                                
       FD  SU-RECEIVER-EXTRACT-FILE                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  SU-RECEIVER-EXTRACT-RECORD      PIC  X(200).                         
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
      *                                                                         
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-ITEM-CURSOR-SW    PIC  X(01)    VALUE 'N'.             
               88  PS-END-OF-ITEM-CURSOR                 VALUE 'Y'.             
               88  PS-NOT-END-OF-ITEM-CURSOR             VALUE 'N'.             
           05  PS-END-OF-TRIP-CURSOR-SW    PIC  X(01)    VALUE 'N'.             
               88  PS-END-OF-TRIP-CURSOR                 VALUE 'Y'.             
               88  PS-NOT-END-OF-TRIP-CURSOR             VALUE 'N'.             
           05  PS-END-OF-CARTON-CURSOR-SW PIC   X(01)    VALUE 'N'.             
               88  PS-END-OF-CARTON-CURSOR               VALUE 'Y'.             
               88  PS-NOT-END-OF-CARTON-CURSOR           VALUE 'N'.             
           05  PS-END-OF-CARDIN-SW         PIC  X(01)    VALUE 'N'.             
               88  PS-END-OF-CARDIN-FILE                 VALUE 'Y'.             
               88  PS-NOT-END-OF-CARDIN-FILE             VALUE 'N'.             
           05  PS-CURSOR-SW                PIC  X(01)    VALUE ' '.             
               88  PS-USE-CARTON-CURSORS                 VALUE 'C'.             
               88  PS-USE-AUDIT-CURSORS                  VALUE 'A'.             
           05  PS-UPC-FOUND-SW             PIC  X(01)    VALUE 'Y'.             
               88  PS-UPC-FOUND                          VALUE 'Y'.             
               88  PS-UPC-NOT-FOUND                      VALUE 'N'.             
      *                                                                         
       01  PC-CONSTANTS.                                                        
           05  PC-1                        PIC S9(03)    VALUE +1               
                                                         COMP-3.                
           05  PC-18                       PIC S9(04)    VALUE 18               
                                                         COMP SYNC.             
           05  PC-A                        PIC  X(01)    VALUE 'A'.             
           05  PC-Y                        PIC  X(01)    VALUE 'Y'.             
           05  PC-E                        PIC  X(01)    VALUE 'E'.             
           05  PC-RCV                      PIC  X(03)    VALUE 'RCV'.           
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
               'SUKUT009'.                                                      
      *                                                                         
       01  PV-MISC-FIELDS.                                                      
           05  PV-AUDIT-CATEGORY           PIC  X(02)    VALUE SPACE.           
               88  PV-RFID-AUDIT                         VALUE 'RF'.            
           05  PV-HOLD-PO-NBR              PIC S9(09)    COMP.                  
           05  PV-HOLD-REC-SEQ-NBR         PIC S9(09)    COMP.                  
           05  PV-LINE-SUB                 PIC S9(03)    VALUE ZERO             
                                                         COMP-3.                
           05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
           05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
           05  PV-AUDIT-NBR-X              PIC  X(09).                          
           05  PV-AUDIT-NBR REDEFINES PV-AUDIT-NBR-X                            
                                           PIC  9(09).                          
           05  PV-SKU-COUNT                    PIC S9(09)  VALUE +0             
                                                           COMP-3.              
           05  PV-EXCEPT-CNT                   PIC S9(09)  VALUE +0             
                                                           COMP-3.              
           05  PV-PICK-CNT                     PIC S9(09)  VALUE +0             
                                                           COMP-3.              
           05  PV-WRITE-VEND-REC-COUNTER    PIC S9(09) VALUE ZERO               
                                                           COMP.                
           05  PV-WRITE-RECV-REC-COUNTER    PIC S9(09) VALUE ZERO               
                                                           COMP.                
           05  PV-SCANNED-QTY-TOTAL         PIC S9(09) VALUE ZERO               
                                                           COMP.                
           05  PV-SHIPT-UNT-ID-DISP         PIC 9(18)  VALUE ZERO.              
           05  PV-UPC-NBR-DISP              PIC 9(15)  VALUE ZERO.              
           05  PV-VILN-GP-SEQ-NBR           PIC S9(04) VALUE ZERO               
                                                           COMP.                
       EJECT                                                                    
      *                                                                         
       01  DK-DB-KEYS.                                                          
           05  DK-SHIPT-UNT-ID             PIC S9(18)  COMP-3.                  
           05  DK-SHPUNT-SHIPT-UNT-ID      PIC S9(18)  COMP-3.                  
           05  DK-AUD-CNTL-NBR             PIC S9(09)  COMP.                    
           05  DK-PO-NBR                   PIC S9(09)  COMP.                    
           05  DK-UPC-NBR                  PIC S9(15)V COMP-3.                  
                                                                                
       01  CC-CARDIN.                                                           
           05  CC-AUDIT-CNTL-NBR           PIC 9(09).                           
           05  CC-CARTON-ID                PIC 9(18).                           
           05  CC-SYSTEM-MANUAL-AUDIT      PIC X(01).                           
               88  CC-SYSTEM-GENERATED-AUDIT    VALUE 'S'.                      
               88  CC-MANUAL-GENERATED-AUDIT    VALUE 'M'.                      
           05  CC-RFID-QC-AUDIT-IND        PIC X(01).                           
               88  CC-RFID-AUDIT                VALUE 'R'.                      
               88  CC-QC-AUDIT                  VALUE 'S'.                      
           05  CC-RFID-QC-AUD-CMB-IND      PIC X(01).                           
               88  CC-RFID-AUD-ONLY             VALUE 'R'.                      
               88  CC-QC-AUD-ONLY               VALUE 'S'.                      
               88  CC-QC-AND-RFID-AUD           VALUE 'B'.                      
      *                                                                         
       01  CARTON-EXTRACT-FILE.                                                 
           COPY SURD003.                                                        
      *                                                                         
      *                                                                         
       01  VENDOR-EXTRACT-FILE.                                                 
           COPY SURD004.                                                        
                                                                                
      /                                                                         
      *  DOMAIN ITEM MODULE                                                     
           COPY XLWS001.                                                        
      *                                                                         
      ******************************************************************        
      *  DB2 FORMATTED MESSAGE AREA                                             
      ******************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *  NUMERIC EDIT ROUTINE COMMAREA                                          
      ******************************************************************        
           COPY DPWS010I.                                                       
      *                                                                         
      *                                                                         
      *  COPY BOOK FOR THE SHIPPING UNIT TABLE                                  
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSHPUNT                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *                                                                         
      *  COPY BOOK FOR THE SHIPPING UNIT AUDIT TRANSACTION TABLE                
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSUATRN                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE RECEIVING TABLE                                      
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TRECEIV                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE SHIPPING UNIT CONTENT AUDIT TABLE                    
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSUAUPC                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE PO AUDIT REQ TABLE                                   
      *                                                                         
           EXEC SQL                                                             
                INCLUDE SUT00015                                                
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE SHIPPING UNIT AUDIT EXCEPTION TABLE                  
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSUEXCP                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE SHIPPING UNIT  AUDIT TABLE                           
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSUNAUD                                                 
           END-EXEC.                                                            
       EJECT                                                                    
      *  DB2 COMMUNICATION AREA                                                 
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       EJECT                                                                    
      *  DB2 COMMUNICATION AREA2                                                
      *                                                                         
           COPY SQLCA2.                                                         
       EJECT                                                                    
                                                                                
                                                                                
           EXEC SQL                                                             
             DECLARE AUDIT-CSR CURSOR FOR                                       
               SELECT A.SHIPT_UNT_ID                                            
                     ,A.TRN_NBR                                                 
                     ,A.REC_SEQ_NBR                                             
                     ,A.DEST_OPER_UNT_ID                                        
                     ,B.AUD_TYP_CDE                                             
                     ,B.OPER_UNT_ID                                             
                FROM  TSUATRN  A                                                
                    , TSUNAUD  B                                                
                WHERE A.AUD_CNTL_NBR    = :DK-AUD-CNTL-NBR                      
                  AND A.AUD_CNTL_NBR    = B.AUD_CNTL_NBR                        
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
             DECLARE AUDIT-CARTON-CSR CURSOR FOR                                
               SELECT A.SHIPT_UNT_ID                                            
                     ,A.TRN_NBR                                                 
                     ,A.REC_SEQ_NBR                                             
                     ,A.DEST_OPER_UNT_ID                                        
                     ,B.AUD_TYP_CDE                                             
                     ,B.OPER_UNT_ID                                             
                FROM  TSUATRN  A                                                
                    , TSUNAUD  B                                                
                WHERE A.AUD_CNTL_NBR    = :DK-AUD-CNTL-NBR                      
                  AND A.SHIPT_UNT_ID    = :DK-SHIPT-UNT-ID                      
                  AND A.AUD_CNTL_NBR    = B.AUD_CNTL_NBR                        
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
             DECLARE ITEM-CSR CURSOR FOR                                        
               SELECT C.UPC_NBR                                                 
                     ,C.SCN_TMST                                                
      *              ,C.SCND_UNT_QTY + C.RFID_TGD_QTY                           
                     ,C.SCND_UNT_QTY                                            
                     ,1                                                         
                FROM  TSUAUPC C                                                 
                WHERE C.AUD_CNTL_NBR      = :DK-AUD-CNTL-NBR                    
                  AND C.SHIPT_UNT_ID      = :DK-SHIPT-UNT-ID                    
                  AND C.SCN_TYP_CDE      <> :PC-E                               
               UNION                                                            
      *SB 04/10/14 - BEGIN PR#5148D                                             
               SELECT C.UPC_NBR                                                 
                     ,C.SCN_TMST                                                
                     ,1                                                         
                     ,A.VILN_GP_SEQ_NBR                                         
                FROM  TSUAUPC C                                                 
                     ,TSUEXCP A                                                 
                WHERE C.AUD_CNTL_NBR      = :DK-AUD-CNTL-NBR                    
                  AND C.SHIPT_UNT_ID      = :DK-SHIPT-UNT-ID                    
                  AND C.SCN_TYP_CDE      <> :PC-E                               
                  AND A.AUD_CNTL_NBR       = C.AUD_CNTL_NBR                     
                  AND A.SHIPT_UNT_ID       = C.SHIPT_UNT_ID                     
                  AND A.UPC_NBR            = C.UPC_NBR                          
                  AND A.SCN_TMST           = C.SCN_TMST                         
                  AND A.VILN_GP_SEQ_NBR    = 38                                 
                  AND A.VILN_GP_CDE        = 'VA'                               
               UNION                                                            
      *SB 04/10/14 - END   PR#5148D                                             
               SELECT C.UPC_NBR                                                 
                     ,C.SCN_TMST                                                
      *              ,C.SCND_UNT_QTY + C.RFID_TGD_QTY                           
                     ,C.SCND_UNT_QTY                                            
                     ,A.VILN_GP_SEQ_NBR                                         
               FROM   TSUAUPC C                                                 
                     ,TSUEXCP A                                                 
               WHERE C.AUD_CNTL_NBR       = :DK-AUD-CNTL-NBR                    
                 AND C.SHIPT_UNT_ID       = :DK-SHIPT-UNT-ID                    
                 AND C.SCN_TYP_CDE        = :PC-E                               
                 AND A.AUD_CNTL_NBR       = C.AUD_CNTL_NBR                      
                 AND A.SHIPT_UNT_ID       = C.SHIPT_UNT_ID                      
                 AND A.UPC_NBR            = C.UPC_NBR                           
                 AND A.SCN_TMST           = C.SCN_TMST                          
                 AND A.VILN_GP_SEQ_NBR    = 42                                  
                 AND A.VILN_GP_CDE        = 'VA'                                
      *        ORDER BY C.UPC_NBR                                               
      *                ,C.SCN_TMST                                              
               ORDER BY 1, 2                                                    
           END-EXEC.                                                            
                                                                                
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
       0000-MAINLINE-PROCESSING.                                                
      ******************************************************************        
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
      *                                                                         
           IF PS-NOT-END-OF-CARDIN-FILE                                         
               PERFORM 2000-PROCESS-CARTON                                      
                  UNTIL PS-END-OF-CARDIN-FILE                                   
           END-IF.                                                              
           PERFORM 9000-EOJ-ROUTINE.                                            
      *                                                                         
           GOBACK.                                                              
      *                                                                         
      *                                                                         
       1000-INITIALIZATION.                                                     
      ******************************************************************        
      **  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
      ******************************************************************        
                                                                                
           OPEN INPUT  CARDIN-AUDIT-NBR-FILE                                    
                OUTPUT SU-VENDOR-EXTRACT-FILE                                   
                       SU-RECEIVER-EXTRACT-FILE.                                
                                                                                
           PERFORM 7000-READ-CARDIN-FILE.                                       
                                                                                
           IF PS-END-OF-CARDIN-FILE                                             
               DISPLAY '** SUKUT009 ABEND **'                                   
               DISPLAY '** CARDIN (AUDIT NUMBER) FILE IS EMPTY **'              
               MOVE +4019                  TO PV-ABEND-CODE                     
               CALL 'ILBOABN0' USING PV-ABEND-CODE                              
           END-IF.                                                              
                                                                                
           MOVE ZERO                TO SU004-QTY-RECV.                          
      *                                                                         
       2000-PROCESS-CARTON.                                                     
           SET PS-NOT-END-OF-CARTON-CURSOR TO TRUE                              
           PERFORM 2100-OPEN-CARTON-CURSOR.                                     
           PERFORM 2200-FETCH-CARTON-CURSOR.                                    
           PERFORM 2300-PROCESS-CARTON-CURSOR                                   
               UNTIL PS-END-OF-CARTON-CURSOR.                                   
           PERFORM 2400-CLOSE-CARTON-CURSOR.                                    
           PERFORM 7000-READ-CARDIN-FILE.                                       
       2100-OPEN-CARTON-CURSOR.                                                 
           MOVE '2100-OPEN-CARTON-CURSOR   '                                    
                                       TO PV-ABEND-PARAGRAPH.                   
           IF PS-USE-AUDIT-CURSORS                                              
               EXEC SQL                                                         
                   OPEN AUDIT-CSR                                               
               END-EXEC                                                         
           ELSE                                                                 
               EXEC SQL                                                         
                   OPEN AUDIT-CARTON-CSR                                        
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES ENDS                               
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2100-OPEN-CARTON-CURSOR         '                      
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'OPEN THE CARTON_CUROSR         '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       2200-FETCH-CARTON-CURSOR.                                                
           MOVE '2200-FETCH-CARTON-CURSOR   '                                   
                                       TO PV-ABEND-PARAGRAPH.                   
           IF PS-USE-AUDIT-CURSORS                                              
               EXEC SQL                                                         
                    FETCH AUDIT-CSR                                             
                    INTO  :SUATRN-SHIPT-UNT-ID                                  
                        , :SUATRN-TRN-NBR                                       
                        , :SUATRN-REC-SEQ-NBR                                   
                        , :SUATRN-DEST-OPER-UNT-ID                              
                        , :SUNAUD-AUD-TYP-CDE                                   
                        , :SUNAUD-OPER-UNT-ID                                   
               END-EXEC                                                         
           ELSE                                                                 
               EXEC SQL                                                         
                    FETCH AUDIT-CARTON-CSR                                      
                    INTO  :SUATRN-SHIPT-UNT-ID                                  
                        , :SUATRN-TRN-NBR                                       
                        , :SUATRN-REC-SEQ-NBR                                   
                        , :SUATRN-DEST-OPER-UNT-ID                              
                        , :SUNAUD-AUD-TYP-CDE                                   
                        , :SUNAUD-OPER-UNT-ID                                   
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               CONTINUE                                                         
             WHEN SQLCODE = +100                                                
                 SET PS-END-OF-CARTON-CURSOR TO TRUE                            
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE < ZERO                                                
             WHEN SQLCODE > ZERO                                                
                 MOVE '2200-FETCH-CARTON-CURSOR'                                
                                     TO PV-PARAGRAPH-NAME                       
                 MOVE 'FETCH CARTON CURSOR '                                    
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
       2300-PROCESS-CARTON-CURSOR.                                              
           INITIALIZE SU003-EXTRACT-RECORD.                                     
           INITIALIZE SU004-EXTRACT-RECORD.                                     
           IF SUATRN-TRN-NBR = PV-HOLD-PO-NBR AND                               
              SUATRN-REC-SEQ-NBR = PV-HOLD-REC-SEQ-NBR                          
               CONTINUE                                                         
           ELSE                                                                 
               MOVE SUATRN-TRN-NBR        TO PV-HOLD-PO-NBR                     
                                             DK-PO-NBR                          
               MOVE SUATRN-REC-SEQ-NBR    TO PV-HOLD-REC-SEQ-NBR                
               PERFORM 2310-GET-ORDER-TYPE                                      
           END-IF.                                                              
                                                                                
           MOVE SUATRN-SHIPT-UNT-ID                                             
                                          TO DK-SHIPT-UNT-ID.                   
           PERFORM 3000-PROCESS-ITEM                                            
           PERFORM 2200-FETCH-CARTON-CURSOR.                                    
                                                                                
                                                                                
      *****************************************************************         
      *  2310-GET-ORDER-TYPE.                                         *         
      * THIS PARAGRAPH WILL PERFORM A SELECT ON THE RECEIVING TABLE   *         
      * TO DETERMINE IF THE PO IS A PACK BY STORE ORDER OR NOT        *         
      *****************************************************************         
       2310-GET-ORDER-TYPE.                                                     
                                                                                
           EXEC SQL                                                             
             SELECT  PACK_BY_ST_IND                                             
               INTO  :RECEIV-PACK-BY-ST-IND                                     
               FROM  TRECEIV                                                    
              WHERE  ORD_NBR        = :SUATRN-TRN-NBR                           
                AND  REC_SEQ_NBR    = :SUATRN-REC-SEQ-NBR                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
                  CONTINUE                                                      
             WHEN SQLCODE = -305                                                
             WHEN SQLCODE = +100                                                
                  MOVE 'N'                     TO RECEIV-PACK-BY-ST-IND         
                                                                                
             WHEN OTHER                                                         
                   MOVE '2310-GET-ORDER-TYPE'                                   
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'SELECT THE PBS INDICATOR FROM TPOAGMT'                 
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   DISPLAY 'PO NUMBER    : ' SUATRN-TRN-NBR                     
                   DISPLAY 'REC SEQ NBR  : ' SUATRN-REC-SEQ-NBR                 
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2400-CLOSE-CARTON-CURSOR.                                                
           MOVE '2400-CLOSE-CARTON-CURSOR   '                                   
                                       TO PV-ABEND-PARAGRAPH.                   
           IF PS-USE-AUDIT-CURSORS                                              
               EXEC SQL                                                         
                   CLOSE AUDIT-CSR                                              
               END-EXEC                                                         
           ELSE                                                                 
               EXEC SQL                                                         
                   CLOSE AUDIT-CARTON-CSR                                       
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2400-CLOSE-CURSOR '                                    
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'CLOSE THE CARTON_CURSOR        '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
                WHEN SQLCODE = ZERO                                             
                    CONTINUE                                                    
           END-EVALUATE.                                                        
      *                                                                         
       3000-PROCESS-ITEM.                                                       
           SET PS-NOT-END-OF-ITEM-CURSOR TO TRUE                                
           PERFORM 3100-OPEN-ITEM-CURSOR.                                       
           PERFORM 3200-FETCH-ITEM-CURSOR.                                      
           PERFORM 3300-PROCESS-ITEM-CURSOR                                     
               UNTIL PS-END-OF-ITEM-CURSOR.                                     
           PERFORM 3400-CLOSE-ITEM-CURSOR.                                      
       3100-OPEN-ITEM-CURSOR.                                                   
           MOVE '3100-OPEN-ITEM-CURSOR   '                                      
                                       TO PV-ABEND-PARAGRAPH.                   
           EXEC SQL                                                             
               OPEN ITEM-CSR                                                    
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '3100-OPEN-ITEM-CURSOR           '                      
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'OPEN THE ITEM_CURSOR           '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
             WHEN SQLCODE = ZEROS                                               
               CONTINUE                                                         
           END-EVALUATE.                                                        
                                                                                
       3200-FETCH-ITEM-CURSOR.                                                  
           MOVE '3200-FETCH-ITEM-CURSOR   '                                     
                                       TO PV-ABEND-PARAGRAPH.                   
           INITIALIZE PV-VILN-GP-SEQ-NBR.                                       
                                                                                
           EXEC SQL                                                             
                FETCH ITEM-CSR                                                  
                INTO  :SUAUPC-UPC-NBR                                           
                    , :SUAUPC-SCN-TMST                                          
                    , :SUAUPC-SCND-UNT-QTY                                      
                    , :PV-VILN-GP-SEQ-NBR                                       
      * RLK                                                                     
                    , :SUEXCP-VILN-GP-SEQ-NBR                                   
      * RLK                                                                     
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SUAUPC-UPC-NBR: ' SUAUPC-UPC-NBR                            
           DISPLAY 'SUAUPC-SCN-TMST: ' SUAUPC-SCN-TMST                          
           DISPLAY 'SUAUPC-SCND-UNT-QTY: ' SUAUPC-SCND-UNT-QTY                  
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
                  IF CC-MANUAL-GENERATED-AUDIT AND                              
                     CC-QC-AND-RFID-AUD        AND                              
                     PV-VILN-GP-SEQ-NBR = 42                                    
      *              MOVE 0 TO SUAUPC-SCND-UNT-QTY                              
                     PERFORM 3200-FETCH-ITEM-CURSOR                             
                  END-IF                                                        
      *        CONTINUE                                                         
             WHEN SQLCODE = +100                                                
                 SET PS-END-OF-ITEM-CURSOR TO TRUE                              
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE < ZERO                                                
             WHEN SQLCODE > ZERO                                                
                 MOVE '3200-FETCH-ITEM-CURSOR  '                                
                                     TO PV-PARAGRAPH-NAME                       
                 MOVE 'FETCH ITEM CURSOR   '                                    
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
       3300-PROCESS-ITEM-CURSOR.                                                
           PERFORM 3500-CALL-XLKUT001.                                          
                                                                                
           MOVE DK-AUD-CNTL-NBR           TO SU003-AUD-CNTL-NBR                 
                                             SU004-AUD-CNTL-NBR.                
           MOVE SUATRN-SHIPT-UNT-ID       TO SU003-SHIPT-UNT-ID                 
                                             DK-SHPUNT-SHIPT-UNT-ID             
                                             DK-SHIPT-UNT-ID.                   
                                                                                
           MOVE SUAUPC-UPC-NBR            TO DK-UPC-NBR                         
                                             SU003-UPC.                         
           MOVE XL001-INTR-UPC-ID         TO SU003-INTR-UPC-ID.                 
                                                                                
           MOVE SUAUPC-SCN-TMST           TO SU003-SCN-TMST                     
                                             SU004-SCN-TMST.                    
                                                                                
           MOVE XL001-DEPT-NBR-3CH        TO SU003-SKU-DEPT-NBR                 
                                             SU004-SKU-DEPT-NBR.                
           INSPECT SU003-SKU-DEPT-NBR REPLACING ALL SPACES BY ZEROES            
           INSPECT SU004-SKU-DEPT-NBR REPLACING ALL SPACES BY ZEROES            
           MOVE XL001-SUB-CL-NBR-2CH      TO SU003-SKU-CL-NBR                   
                                             SU004-SKU-CL-NBR.                  
           INSPECT SU003-SKU-CL-NBR REPLACING ALL SPACES BY ZEROES              
           INSPECT SU004-SKU-CL-NBR REPLACING ALL SPACES BY ZEROES              
           MOVE XL001-SKU-NBR             TO SU003-SKU-NMBR                     
                                             SU004-SKU-NMBR.                    
           INSPECT SU003-SKU-NMBR REPLACING ALL SPACES BY ZEROES                
           INSPECT SU004-SKU-NMBR REPLACING ALL SPACES BY ZEROES                
                                                                                
           MOVE SUATRN-TRN-NBR            TO SU003-PO-NBR                       
                                             SU004-PO-NBR.                      
           MOVE SUATRN-REC-SEQ-NBR        TO SU003-REC-SEQ-NBR                  
                                             SU004-REC-SEQ-NBR.                 
                                                                                
           IF  RECEIV-PACK-BY-ST-IND = 'Y'                                      
               MOVE SUATRN-DEST-OPER-UNT-ID                                     
                                          TO SU003-DEST-OPER-UNT-ID             
                                             SU004-DEST-OPER-UNT-ID             
           ELSE                                                                 
               PERFORM 3330-GET-DEST-OPER-UNT                                   
               MOVE SHPUNT-DEST-OPER-UNT-ID                                     
                                          TO SU003-DEST-OPER-UNT-ID             
                                             SU004-DEST-OPER-UNT-ID             
           END-IF.                                                              
                                                                                
           MOVE SUNAUD-OPER-UNT-ID        TO SU003-AUD-OPER-UNT-ID .            
           MOVE 1                         TO SU003-QTY-AUD                      
                                             SU004-QTY-AUD.                     
                                                                                
           PERFORM 3310-GET-EXCEPT-COUNT.                                       
                                                                                
           MOVE PV-EXCEPT-CNT   TO SU003-TOT-ERR-QTY.                           
                                                                                
           IF PV-EXCEPT-CNT > 0                                                 
              MOVE 1            TO SU003-UNT-ERR-CNT-COMBO                      
                                   SU004-UNT-ERR-CNT-COMBO                      
           ELSE                                                                 
              MOVE 0            TO SU003-UNT-ERR-CNT-COMBO                      
                                   SU004-UNT-ERR-CNT-COMBO                      
           END-IF.                                                              
                                                                                
           PERFORM 3320-DETERMINE-PICK-ERRORS.                                  
                                                                                
           IF PV-PICK-CNT > 0                                                   
              MOVE 1            TO SU003-PICK-ERR-CNT                           
                                   SU004-PICK-ERR-CNT                           
           ELSE                                                                 
              MOVE 0            TO SU003-PICK-ERR-CNT                           
                                   SU004-PICK-ERR-CNT                           
           END-IF.                                                              
                                                                                
           IF RECEIV-PACK-BY-ST-IND = 'Y'                                       
               SET SU003-PBS-ORDER                                              
                   SU004-PBS-ORDER           TO TRUE                            
           ELSE                                                                 
               SET SU003-CASEPACK-ORDER                                         
                   SU004-CASEPACK-ORDER      TO TRUE                            
           END-IF                                                               
                                                                                
      *    IF THE FLAG IS SET TO USE THE AUDIT CURSOR, SET THE FLAG ON          
      *    THE EXTRACT TO SAY IT IS AN AUDIT EXTRACT                            
           IF PS-USE-AUDIT-CURSORS                                              
               SET SU003-AUDIT-EXTRACT    TO TRUE                               
           ELSE                                                                 
               SET SU003-CARTON-EXTRACT   TO TRUE                               
           END-IF.                                                              
           DISPLAY 'PV-AUDIT-CATEGORY-BEF ' PV-AUDIT-CATEGORY                   
           IF PV-RFID-AUDIT                                                     
               MOVE 'RFID'                TO SU003-AUD-CAT-CDE                  
           ELSE                                                                 
               MOVE 'QC  '                TO SU003-AUD-CAT-CDE                  
           END-IF                                                               
                                                                                
           DISPLAY 'PV-AUDIT-CATEGORY-AFT ' PV-AUDIT-CATEGORY                   
      **CHANGED THE WRITE VENDOR EXTRACT TO WRITE THE NUMBER OF ROWS            
      * IN THE SCND-UNT-QTY. AN RF AUDIT AND TABLE CHANGE NOW ALLOWS THE        
      * ASSOCIATE TO KEY THE QUANTITY OF ITEMS IN THE CARTON RATHER THAN        
      * SCANNING EACH INDIVIDUAL UPC. SO ONE ROW ON THE SUAUPC TABLE            
      * MAY REPRESENT SEVERAL UPC'S. IN ORDER TO AVOID INVALID                  
      * OVER/SHORT ERRORS, THE CORRECT NUMBER OF ROWS WILL NOW BE               
      * WRITTEN TO THE VENDOR EXTRACT GB 2/26/02                                
      **                                                                        
                                                                                
            INITIALIZE PV-SCANNED-QTY-TOTAL                                     
            MOVE SUAUPC-SCND-UNT-QTY TO PV-SCANNED-QTY-TOTAL                    
            DISPLAY 'PV-SCANNED-QTY-TOTAL: ' PV-SCANNED-QTY-TOTAL               
            PERFORM                                                             
                VARYING PV-WRITE-VEND-REC-COUNTER                               
                FROM 1 BY 1                                                     
                UNTIL PV-WRITE-VEND-REC-COUNTER >  PV-SCANNED-QTY-TOTAL         
                   PERFORM 8000-WRITE-VENDOR-EXTRACT-REC                        
            END-PERFORM                                                         
      ***************                                                           
      **GB PERFORM 8000-WRITE-VENDOR-EXTRACT-REC.                               
      ***************                                                           
                                                                                
      *    DONT WRITE THE RECEIVER EXTRACT IF THE TYPE IS SPOT CHECK,           
      *    OR IF THIS IS A CARTON EXTRACT                                       
              DISPLAY 'BEFORE CHECK- ' SUAUPC-UPC-NBR                           
                       ' - '           SUAUPC-SCN-TMST                          
                       ' - '           SUAUPC-SCND-UNT-QTY                      
                       ' - '           SUNAUD-AUD-TYP-CDE                       
                       ' - '           SU003-EXTRACT-TYPE                       
                       ' - '           SUEXCP-VILN-GP-SEQ-NBR.                  
           IF SUNAUD-AUD-TYP-CDE = '02'                                         
           OR SU003-CARTON-EXTRACT                                              
               CONTINUE                                                         
           ELSE                                                                 
      ***GB  4/5/02***   PERFORM 8100-WRITE-RECEIV-EXTRACT-REC                  
      *** ADDED LOGIC TO EXPAND OUT THE SUAUPC QTY TO THE RECEIV EXTRACT        
              INITIALIZE PV-SCANNED-QTY-TOTAL                                   
              MOVE SUAUPC-SCND-UNT-QTY TO PV-SCANNED-QTY-TOTAL                  
              PERFORM                                                           
                  VARYING PV-WRITE-RECV-REC-COUNTER                             
                  FROM 1 BY 1                                                   
                  UNTIL PV-WRITE-RECV-REC-COUNTER > PV-SCANNED-QTY-TOTAL        
                     PERFORM 8100-WRITE-RECEIV-EXTRACT-REC                      
              END-PERFORM                                                       
      * RLK                                                                     
              DISPLAY 'BEFORE WRITE- ' SUAUPC-UPC-NBR                           
                       ' - '           SUAUPC-SCN-TMST                          
                       ' - '           SUAUPC-SCND-UNT-QTY                      
                       ' - '           PV-SCANNED-QTY-TOTAL                     
                       ' - '           PV-VILN-GP-SEQ-NBR                       
                       ' - '           SUEXCP-VILN-GP-SEQ-NBR                   
              IF SUEXCP-VILN-GP-SEQ-NBR = 38                                    
                AND PV-SCANNED-QTY-TOTAL = 0                                    
                     PERFORM 8100-WRITE-RECEIV-EXTRACT-REC                      
              END-IF                                                            
      * RLK                                                                     
           END-IF.                                                              
           PERFORM 3200-FETCH-ITEM-CURSOR.                                      
                                                                                
                                                                                
       3310-GET-EXCEPT-COUNT.                                                   
#          DISPLAY 'SHIPT-UNT-ID: ' SUATRN-SHIPT-UNT-ID.                        
           MOVE '3310-GET-EXCEPT-COUNT    '                                     
                                       TO PV-ABEND-PARAGRAPH.                   
      *    OMITTING SEQ 12 BECAUSE THAT IS UPC MISPACK.  THESE ARE              
      *    COUNTED AS VENDOR OVER/SHORT AND SHOULD NOT BE INCLUDED              
      *    IN THE ERROR COUNTS                                                  
           EXEC SQL                                                             
               SELECT COUNT(*)                                                  
                 INTO :PV-EXCEPT-CNT                                            
                 FROM TSUEXCP                                                   
                WHERE AUD_CNTL_NBR = :DK-AUD-CNTL-NBR                           
                  AND SHIPT_UNT_ID = :SUATRN-SHIPT-UNT-ID                       
                  AND UPC_NBR      = :SUAUPC-UPC-NBR                            
                  AND SCN_TMST     = :SUAUPC-SCN-TMST                           
                  AND VILN_GP_SEQ_NBR <> 12                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                 CONTINUE                                                       
               WHEN SQLCODE = +100                                              
                 MOVE 0 TO PV-EXCEPT-CNT                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '3310-GET-EXCEPT-COUNT'                                 
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'CLOSE THE ITEM_CURSOR          '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   DISPLAY 'SHIPT UNT ID: ' SUATRN-SHIPT-UNT-ID                 
                   DISPLAY 'UPD NUMBER:   ' SUAUPC-UPC-NBR                      
                   DISPLAY 'SCN TMST:     ' SUAUPC-SCN-TMST                     
                   PERFORM 9100-SQL-ABEND                                       
                WHEN SQLCODE = ZERO                                             
                    CONTINUE                                                    
           END-EVALUATE.                                                        
                                                                                
       3320-DETERMINE-PICK-ERRORS.                                              
           MOVE '3320-DETERMINE-PICK-ERRORS'                                    
                                       TO PV-ABEND-PARAGRAPH.                   
           EXEC SQL                                                             
               SELECT COUNT(*)                                                  
                 INTO :PV-PICK-CNT                                              
                 FROM TSUEXCP                                                   
                WHERE AUD_CNTL_NBR = :DK-AUD-CNTL-NBR                           
                  AND SHIPT_UNT_ID = :SUATRN-SHIPT-UNT-ID                       
                  AND UPC_NBR      = :SUAUPC-UPC-NBR                            
                  AND SCN_TMST     = :SUAUPC-SCN-TMST                           
                  AND VILN_GP_SEQ_NBR                                           
                      IN (9, 10, 11, 20, 38, 39, 40, 41, 42)                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                 CONTINUE                                                       
               WHEN SQLCODE = +100                                              
                 MOVE 0 TO PV-PICK-CNT                                          
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '3320-DETERMINE-PICK-ERRORS'                            
                                   TO PV-PARAGRAPH-NAME                         
                   DISPLAY 'SHIPT UNT ID: ' SUATRN-SHIPT-UNT-ID                 
                   DISPLAY 'UPD NUMBER:   ' SUAUPC-UPC-NBR                      
                   DISPLAY 'SCN TMST:     ' SUAUPC-SCN-TMST                     
                   PERFORM 9100-SQL-ABEND                                       
                WHEN SQLCODE = ZERO                                             
                    CONTINUE                                                    
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      *  FOR NON PACK-BY-STORE ORDERS, THIS CURSOR WILL GO GET THE              
      *  DEST OPER UNIT ID FROM THE TSHPUNT TABLE.  THIS WAS ADDED              
      *  BECAUSE WHEN THE UCC-128 AUDIT ENHANCEMENTS WERE DONE IN               
      *  MARCH OF 2002, A CHANGE WAS MADE TO BRING FORWARD THE STORE            
      *  NUMBER FROM THE CROSS REFERENCE TABLE TO THE AUDIT APPLICATION.        
      *  THE STORE NUMBER WAS THEN USED THROUGH THE EXISTING REPORTING          
      *  INSTEAD OF THE DESTINATION OPER UNIT ID AS IT DID PRIOR TO THE         
      *  CHANGES, THIS CAUSED RECEIVER LEVEL OVER AND SHORT PROBLEMS            
      *  WHEN IT SHOULD NOT HAVE.                                               
      *                                                                         
       3330-GET-DEST-OPER-UNT.                                                  
                                                                                
           MOVE '3330-GET-DEST-OPER-UNT'                                        
                                       TO PV-ABEND-PARAGRAPH.                   
           EXEC SQL                                                             
               SELECT DEST_OPER_UNT_ID                                          
                 INTO :SHPUNT-DEST-OPER-UNT-ID                                  
                 FROM TSHPUNT                                                   
                WHERE SHIPT_UNT_ID = :DK-SHPUNT-SHIPT-UNT-ID                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                 CONTINUE                                                       
               WHEN SQLCODE = +100                                              
                 MOVE 0 TO SHPUNT-DEST-OPER-UNT-ID                              
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '3330-GET-DEST-OPER-UNT'                                
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE DK-SHPUNT-SHIPT-UNT-ID                                  
                                   TO PV-SHIPT-UNT-ID-DISP                      
                   DISPLAY 'SHIPT UNT ID: ' PV-SHIPT-UNT-ID-DISP                
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      *  FOR NON PACK-BY-STORE ORDERS, THIS CURSOR WILL GO GET THE              
      *  DEST OPER UNIT ID FROM THE TSHPUNT TABLE.  THIS WAS ADDED              
      *  BECAUSE WHEN THE UCC-128 AUDIT ENHANCEMENTS WERE DONE IN               
      *  MARCH OF 2002, A CHANGE WAS MADE TO BRING FORWARD THE STORE            
      *  NUMBER FROM THE CROSS REFERENCE TABLE TO THE AUDIT APPLICATION.        
      *  THE STORE NUMBER WAS THEN USED THROUGH THE EXISTING REPORTING          
      *  INSTEAD OF THE DESTINATION OPER UNIT ID AS IT DID PRIOR TO THE         
      *  CHANGES, THIS CAUSED RECEIVER LEVEL OVER AND SHORT PROBLEMS            
      *  WHEN IT SHOULD NOT HAVE.                                               
      *                                                                         
                                                                                
       3400-CLOSE-ITEM-CURSOR.                                                  
           MOVE '3400-CLOSE-ITEM-CURSOR   '                                     
                                       TO PV-ABEND-PARAGRAPH.                   
           EXEC SQL                                                             
               CLOSE ITEM-CSR                                                   
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
      *        WHEN SQLCODE = -904                                              
      *        WHEN SQLCODE = -911                                              
      *            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '3400-CLOSE-CURSOR '                                    
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'CLOSE THE ITEM_CURSOR          '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
                WHEN SQLCODE = ZERO                                             
                    CONTINUE                                                    
             WHEN SQLCODE = ZEROS                                               
               CONTINUE                                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3500-CALL-XLKUT001.                                                      
                                                                                
           MOVE '3500-CALL-XLKUT001'   TO PV-ABEND-PARAGRAPH.                   
           MOVE 'CALL XLUT0001'        TO PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
                                                                                
           INITIALIZE XL001-ITEM-COMMAREA                                       
           SET XL001-SELECT-SKU                                                 
               XL001-SELECT-VND-STYL                                            
               XL001-BY-UPC-NBR             TO TRUE.                            
                                                                                
           MOVE SUAUPC-UPC-NBR              TO XL001-KEY-UPC-NBR.               
                                                                                
           CALL XL001-VSM-PGM USING XL001-ITEM-COMMAREA.                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN XL001-SUCCESSFUL                                            
               WHEN XL001-SKU-NOT-FOUND                                         
               WHEN XL001-UPC-NOT-FOUND                                         
               WHEN XL001-VND-STYL-NOT-FOUND                                    
                   CONTINUE                                                     
               WHEN XL001-SQL-ERROR                                             
               WHEN XL001-INVALID-ACCESS                                        
                   PERFORM 9200-XL-ABEND                                        
               WHEN OTHER                                                       
                   PERFORM 9200-XL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       7000-READ-CARDIN-FILE.                                                   
      ******************************************************************        
                                                                                
           READ CARDIN-AUDIT-NBR-FILE INTO CC-CARDIN                            
              AT END                                                            
                 SET PS-END-OF-CARDIN-FILE TO TRUE.                             
                                                                                
           IF PS-END-OF-CARDIN-FILE                                             
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY 'AUDIT NBR IS : ' CC-AUDIT-CNTL-NBR                      
      *        DISPLAY 'CARTON ID IS : ' CC-CTN-LAST-17                         
               DISPLAY 'CARTON ID IS : ' CC-CARTON-ID                           
               DISPLAY 'SYS-MAN-AUDIT: '  CC-SYSTEM-MANUAL-AUDIT                
               DISPLAY 'CC-RFID-QC-AUD: ' CC-RFID-QC-AUDIT-IND                  
               DISPLAY 'CC-RFID-QC-AUD-CMB: ' CC-RFID-QC-AUD-CMB-IND            
               PERFORM 7100-VALIDATE-CONTROL-CARD                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 7100-VALIDATE-CONTROL-CARD - VERIFIES THAT THE AUDIT CONTROL            
      * NUMBER AND THE SHIPPING UNIT ID ARE BOTH NUMBERIC                       
      ******************************************************************        
       7100-VALIDATE-CONTROL-CARD.                                              
                                                                                
      *NUMERIC EDIT OF AUDIT CONTROL NBR                                        
           MOVE CC-AUDIT-CNTL-NBR          TO DP010I-UNEDITED-FIELD.            
           MOVE +9                         TO DP010I-MAXIMUM-DIGITS.            
           MOVE 0                          TO DP010I-MAXIMUM-DECIMALS.          
           SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE.                             
           CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
               USING DP010I-NUMERIC-EDIT-AREA.                                  
                                                                                
           IF DP010I-NO-ERROR-DETECTED                                          
               MOVE DP010I-NUMERIC-FIELD  TO DK-AUD-CNTL-NBR                    
           ELSE                                                                 
               DISPLAY '** SUKUT009 ABEND **'                                   
               DISPLAY '** AUDIT NUMBER IS NOT NUMERIC **'                      
               MOVE +4019                  TO PV-ABEND-CODE                     
               CALL 'ILBOABN0' USING PV-ABEND-CODE                              
           END-IF.                                                              
      *                                                                         
                                                                                
      *NUMERIC EDIT OF CARTON ID                                                
      *    IF CC-CTN-FIRST-DIGIT = 0                                            
      *    MOVE CC-CTN-LAST-17         TO DP010I-UNEDITED-FIELD.                
      *    MOVE +17                    TO DP010I-MAXIMUM-DIGITS.                
      *    MOVE 0                      TO DP010I-MAXIMUM-DECIMALS.              
      *    SET DP010I-NEGATIVE-NOT-ALLOWED                                      
      *                                TO TRUE.                                 
      *    CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
      *        USING DP010I-NUMERIC-EDIT-AREA.                                  
      *                                                                         
      *    IF DP010I-NO-ERROR-DETECTED                                          
      *        MOVE DP010I-NUM-FIELD-17-DIG-0-DEC                               
      *                                TO DK-SHIPT-UNT-ID                       
      *        CONTINUE                                                         
      *    ELSE                                                                 
      *        DISPLAY '** SUKUT009 ABEND **'                                   
      *        DISPLAY '** CARTON NUMBER MUST BE NUMERIC **'                    
      *        MOVE +4019              TO PV-ABEND-CODE                         
      *        CALL 'ILBOABN0' USING PV-ABEND-CODE                              
      *    END-IF.                                                              
      *    ELSE                                                                 
      *        DISPLAY '** SUKUT009 ABEND **'                                   
      *        DISPLAY '** FIRST DIGIT OF CARTON NBR MUST BE 0 OR 4 ***'        
      *        MOVE +4019              TO PV-ABEND-CODE                         
      *        CALL 'ILBOABN0' USING PV-ABEND-CODE                              
      *    END-IF.                                                              
                                                                                
           MOVE CC-CARTON-ID           TO DK-SHIPT-UNT-ID.                      
           IF DK-SHIPT-UNT-ID > 0                                               
              SET PS-USE-CARTON-CURSORS TO TRUE                                 
              DISPLAY 'USING CARTON CURSOR'                                     
           ELSE                                                                 
              SET PS-USE-AUDIT-CURSORS  TO TRUE                                 
              DISPLAY 'USING AUDIT CURSOR'                                      
           END-IF.                                                              
           IF CC-RFID-AUDIT                                                     
               MOVE 'RF'               TO PV-AUDIT-CATEGORY                     
           ELSE                                                                 
               MOVE 'QC'               TO PV-AUDIT-CATEGORY                     
           END-IF.                                                              
      *                                                                         
      *                                                                         
       8000-WRITE-VENDOR-EXTRACT-REC.                                           
           WRITE SU-VENDOR-EXTRACT-RECORD                                       
             FROM SU003-EXTRACT-RECORD.                                         
       8100-WRITE-RECEIV-EXTRACT-REC.                                           
           WRITE SU-RECEIVER-EXTRACT-RECORD                                     
             FROM SU004-EXTRACT-RECORD.                                         
      *                                                                         
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CARDIN-AUDIT-NBR-FILE                                          
                 SU-VENDOR-EXTRACT-FILE                                         
                 SU-RECEIVER-EXTRACT-FILE.                                      
           DISPLAY 'FILE CLOSED '.                                              
                                                                                
      * SND 10/2009  DATA CONTENTION CHANGES START                              
       9100-SQL-ABEND.                                                          
      *                                                                         
           IF SQLCODE                  = -904 OR -911 OR -913                   
              MOVE +4050               TO RETURN-CODE                           
              STOP RUN                                                          
           ELSE                                                                 
              PERFORM 9110-ABEND-PROGRAM                                        
           END-IF.                                                              
      *                                                                         
      *9100-EXIT.                                                               
      *                                                                         
       9110-ABEND-PROGRAM.                                                      
      ******************************************************************        
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
      *  ERROR OR WARNING CODE OCCURS.                                          
      ******************************************************************        
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9200-XL-ABEND.                                                           
           PERFORM 9000-EOJ-ROUTINE                                             
           IF SQLCODE                  = -904 OR -911 OR -913                   
              MOVE +4050               TO RETURN-CODE                           
              STOP RUN                                                          
           ELSE                                                                 
              PERFORM 9210-ABEND-PROGRAM                                        
           END-IF.                                                              
      *                                                                         
      *9200-EXIT.                                                               
      *                                                                         
      * SND 10/2009  DATA CONTENTION CHANGES END                                
      *                                                                         
       9210-ABEND-PROGRAM.                                                      
           DISPLAY '9200-XL-ABEND'.                                             
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' XL001-ERROR-PARAGRAPH-NBR               
           DISPLAY '** OPERATION ** =  ERROR INSIDE XLKUT001'.                  
           MOVE XL001-SQLCA         TO SQLCA                                    
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
