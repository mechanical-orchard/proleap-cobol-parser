      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.   PXKUT109.                                          00050001
       AUTHOR.       COGNIZANT.                                         00060000
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070000
       DATE-WRITTEN. APRIL 2011.                                        00080000
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ***************************************************************** 00110000
      * THIS PROGRAM LOOKS UP STORE INVENTORY THAT HAPPENS 2-DAYS      *00130001
      * PRIOR TO EVENT AND ITS ELIMINATE ON HAND ADJUSTMET FILE        *00140001
      ***************************************************************** 00170000
      ******************** HISTORY OF CHANGES **************************00180000
      * CHG ID/                                                        *00190000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00200000
      * -------  ----------  ----------------------------------------  *00210000
      *          04/26/2011  INITIAL IMPLEMENTATION                    *00220001
      ******************************************************************00230000
      ******************************************************************00240000
       ENVIRONMENT DIVISION.                                            00250000
      ******************************************************************00260000
      *                                                                 00270000
       CONFIGURATION SECTION.                                           00280000
       SOURCE-COMPUTER.   IBM-3090.                                     00290000
       OBJECT-COMPUTER.   IBM-3090.                                     00300000
      *                                                                 00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
           SELECT DATE-FILE                                             00340000
             ASSIGN                       TO INP01.                     00350000
           SELECT YLLW-TCK-ADJ-FILE                                     00351003
             ASSIGN                       TO INP02.                     00352002
                                                                        00360000
            SELECT YLLW-TCK-ADJ-ELM-FILE                                00370003
              ASSIGN                        TO OUT01.                   00380000
                                                                        00399000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
      *                                                                 00430000
       FILE SECTION.                                                    00440000
       FD  DATE-FILE                                                    00450000
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           RECORD CONTAINS 080 CHARACTERS                               00480000
           LABEL RECORDS ARE STANDARD                                   00490000
           DATA RECORD IS INPUT-RECORD.                                 00500000
       01  INPUT-RECORD                           PIC X(080).           00510000
      *                                                                 00520000
       FD YLLW-TCK-ADJ-FILE                                             00530003
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS                                     00550000
           RECORD CONTAINS 026 CHARACTERS                               00551003
           LABEL RECORDS ARE STANDARD                                   00552000
           DATA RECORD IS YLLW-TCK-ADJ-FILE-REC.                        00560003
      *                                                                 00570000
       01 YLLW-TCK-ADJ-FILE-REC                  PIC X(026).            00580003
      *                                                                 00580103
       FD YLLW-TCK-ADJ-ELM-FILE                                         00581003
           RECORDING MODE IS F                                          00582003
           BLOCK CONTAINS 0 RECORDS                                     00583003
           RECORD CONTAINS 026 CHARACTERS                               00584003
           LABEL RECORDS ARE STANDARD                                   00585003
           DATA RECORD IS YLLW-ELM-FILE-REC.                            00586024
      *                                                                 00587003
       01 YLLW-ELM-FILE-REC                  PIC X(026).                00588026
      *                                                                 00590000
      *                                                                 00601200
       WORKING-STORAGE SECTION.                                         00602000
                                                                        00610000
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO      00620000
                                                        COMP SYNC.      00630000
           88  ABEND-4013-DB2-ERROR                     VALUE +4013.    00640000
           88  ABEND-4002-NO-CONTROL-CARD               VALUE +4002.    00641007
           88  ABEND-4003-INVALID-INPUT-DATE            VALUE +4003.    00642016
      *                                                                 00650000
                                                                        00700000
       01 PC-PROGRAM-CONSTANTS.                                         00930000
          05  PC-PXKUT109                 PIC X(10)    VALUE 'PXKUT109'.00940003
          05  PC-CURRENT-PROGRAM-NAME     PIC X(08)    VALUE 'PXKUT109'.00960003
          05  PC-PROGRAM-NAME             PIC X(08)    VALUE 'PXKUT109'.00980003
          05  PC-ONE                      PIC S9(01)   VALUE +1.        00991000
          05  PC-DAYS-FR                  PIC S9(3)    VALUE +3.        00991130
                                                                        00992000
       01 PV-PROGRAM-VARIABLES.                                         01000000
          05  PV-SQLCODE                  PIC +9(04).                   01010000
          05  PV-OPERATION-MSG            PIC X(60)    VALUE SPACES.    01020000
          05  PV-WRITE-CNT                PIC 9(7)     VALUE ZERO       01030000
                                                       COMP-3.          01040000
          05  PV-READ-CNT                 PIC 9(7)     VALUE ZERO       01050028
                                                       COMP-3.          01060028
          05  PV-ELM-CNT                  PIC 9(7)     VALUE ZERO       01070028
                                                       COMP-3.          01080028
          05  PV-STR-ELM-CNT              PIC 9(7)     VALUE ZERO       01090032
                                                       COMP-3.          01100032
          05  PV-ABEND-CODE               PIC S9(04)   VALUE +0         01150000
                                                       COMP SYNC.       01160000
          05  PV-PARA-NAME                PIC X(35).                    01170000
          05  PV-PARAGRAPH-NAME           PIC X(30)    VALUE SPACES.    01180016
          05  PV-DB2-OPERATION-ATTEMPTED  PIC X(30)   VALUE SPACES.     01181000
          05  PV-INV-START-DATE            PIC X(10) VALUE SPACES.      01181112
          05  PV-INV-END-DATE              PIC X(10) VALUE SPACES.      01181216
          05 PV-STR-NBR                   PIC 9(04) VALUE ZERO.         01181320
          05 PV-SELECT                    PIC X(01) VALUE SPACE.        01181417
      ******************************************************************01181516
      **DATE VARIABLES                                                  01182000
      ******************************************************************01183000
        01 PV-INPUT-DATE.                                               01185019
             05  PV-INPUT-DATE-YEAR            PIC X(04) VALUE SPACES.  01186014
             05 FILLER                         PIC X(01) VALUE '-'.     01186103
             05  PV-INPUT-DATE-MONTH           PIC X(02) VALUE SPACES.  01187003
             05 FILLER                         PIC X(01) VALUE '-'.     01187103
             05  PV-INPUT-DATE-DD              PIC X(02) VALUE SPACES.  01188014
             05 FILLER                         PIC X(70) VALUE SPACE.   01188104
                                                                        01190000
      *                                                                 01220000
       01 PS-PROGRAM-SWITCHES.                                          01230000
          05   PS-END-OF-ADJ-INP-FILE-SW       PIC X(1) VALUE 'N'.      01240005
              88  PS-END-OF-ADJ-INP-FILE       VALUE 'Y'.               01250005
          05  PS-END-OF-DATE-FILE-SW           PIC X(1) VALUE 'N'.      01260003
              88  PS-END-OF-DATE-FILE          VALUE 'Y'.               01270003
          05  PS-STR-INVENTORY-DONE-SW         PIC X(1) VALUE 'N'.      01280007
              88  PS-STR-INVENTORY-DONE-YES    VALUE 'Y'.               01281007
              88  PS-STR-INVENTORY-DONE-NO     VALUE 'N'.               01282007
      *                                                                 01290000
      ******************************************************************01300003
      **COPYBOOKS                                                       01310003
      ******************************************************************01320003
      ****ON HAND QTY EXTRACT INPUT RECORD LAYOUT                       01330003
           COPY PXRD124.                                                01340018
      ******************************************************************01480000
      * DB2 ERROR MESSAGES FORMAT AREA.                                 01490000
      ******************************************************************01500000
           COPY DPWS004.                                                01510000
                                                                        01520000
      ******************************************************************01530000
      * KOHLS DATE ROUTINE COPY MEMBERS.                                01540000
      ******************************************************************01550000
           COPY DPWS500.                                                01560000
      *                                                                 01570000
      ******************************************************************01701000
      *  DB2 COMMUNICATION AREA.                                        01710000
      ******************************************************************01711000
           EXEC SQL                                                     01730000
                INCLUDE SQLCA                                           01740000
           END-EXEC.                                                    01750000
                                                                        01760000
           COPY SQLCA2.                                                 01770000
      ******************************************************************01780000
      *  DB2 TABLE TINVPAR                                              01790000
      ******************************************************************01800000
           EXEC SQL                                                     01810000
               INCLUDE TINVPAR                                          01820000
           END-EXEC.                                                    01830000
                                                                        02453700
      ******************************************************************02453800
       PROCEDURE DIVISION.                                              02454000
      ******************************************************************02460000
       0000-MAINLINE-PROCESSING.                                        02490000
      ******************************************************************02500000
      * MAIN PROCESSING ROUTINE                                       * 02510000
      ******************************************************************02520000
                                                                        02530000
            PERFORM 1000-INITIALIZE.                                    02540000
                                                                        02541000
            PERFORM 2000-PRCSS-YLLW-TCK-EXTR-FL                         02542005
              UNTIL  PS-END-OF-ADJ-INP-FILE.                            02560005
                                                                        02570000
            PERFORM 9000-END-OF-JOB.                                    02580000
                                                                        02590000
            GOBACK.                                                     02600000
                                                                        02610000
      ******************************************************************02620000
      * OPEN FILES AND READ FIRST INPUT RECORD.                        *02630000
      ******************************************************************02640000
      *                                                                 02650000
       1000-INITIALIZE.                                                 02660000
            MOVE '*1000-INITIALIZE.        *' TO PV-PARA-NAME.          02670000
            OPEN INPUT  DATE-FILE                                       02680000
                 YLLW-TCK-ADJ-FILE                                      02681003
                 OUTPUT YLLW-TCK-ADJ-ELM-FILE.                          02690003
            READ DATE-FILE                                              02700003
             INTO PV-INPUT-DATE                                         02710004
               AT END                                                   02720003
                 SET PS-END-OF-DATE-FILE TO TRUE                        02730004
            END-READ.                                                   02740003
                                                                        02750003
           IF PS-END-OF-DATE-FILE THEN                                  02760004
                 DISPLAY ' '                                            02770003
                 DISPLAY 'DATE IS MISSING INC CONTROL CARD'             02780004
                 SET ABEND-4002-NO-CONTROL-CARD TO TRUE                 02780107
                 CALL 'ILBOABN0' USING ABEND-CODE                       02780207
           ELSE                                                         02790003
                 PERFORM 1500-VALIDATE-INPUT-DATE                       02824015
                 PERFORM 1700-DECREMENT-INPUT-DATE                      02828431
                 DISPLAY 'START DATE:' PV-INV-START-DATE                02828531
                 DISPLAY 'END DATE:' PV-INV-END-DATE                    02828631
           END-IF.                                                      02828803
           PERFORM 3000-READ-YLLW-TCK-ADJ-FL.                           02828905
           IF  PS-END-OF-ADJ-INP-FILE                                   02829019
               DISPLAY ' '                                              02829107
               DISPLAY ' *** NOTHING TO PROCESS ***'                    02829205
           ELSE                                                         02829307
               INITIALIZE PV-STR-NBR                                    02829419
           END-IF.                                                      02829505
      ******************************************************************02829613
       1500-VALIDATE-INPUT-DATE.                                        02829713
      ******************************************************************02829813
           MOVE PV-INPUT-DATE TO PV-INV-END-DATE.                       02829930
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02830016
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      02830119
           SET DPG52-LK-DTE-ISO-FMT TO TRUE.                            02830216
           MOVE PV-INV-END-DATE  TO DPG52-LK-DATE-INPUT.                02830330
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            02830413
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               02830513
                                                                        02830613
           IF DPG54-DATE-INVALID                                        02830713
               DISPLAY '** ABEND ** IN PXKUT109'                        02830813
               DISPLAY '** PARAGRAPH: 1500-VALIDATE-INPUT-DATE'         02830913
               DISPLAY '** INVALID PROCESS DATE ON INPUT FILE.'         02831013
               DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '           02831113
                           DPG54-ERROR-MESSAGE                          02831213
               DISPLAY '** DPG51 = ' DPG51                              02831313
               DISPLAY '** DPG52 = ' DPG52                              02831413
               DISPLAY '** DPG53 = ' DPG53                              02831513
               DISPLAY '** DPG54 = ' DPG54                              02831613
               DISPLAY '** DPG55 = ' DPG55                              02831713
               DISPLAY '** DPG56 = ' DPG56                              02831813
               SET  ABEND-4003-INVALID-INPUT-DATE TO TRUE               02831916
               CALL 'ILBOABN0' USING ABEND-CODE                         02832013
            END-IF.                                                     02832116
                                                                        02832213
      ******************************************************************02832316
       1700-DECREMENT-INPUT-DATE.                                       02832430
      ******************************************************************02832516
            INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.             02832616
            MOVE PV-INPUT-DATE TO PV-INV-START-DATE.                    02832730
            SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                     02832816
            SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                      02832916
            MOVE PV-INV-START-DATE  TO DPG52-LK-DATE-INPUT.             02833030
            SET DPG51-DECREMENT-DATE      TO TRUE.                      02833130
            MOVE PC-DAYS-FR            TO DPG51-INCR-DECR-DAYS-9.       02833216
            CALL DP500-KOHLS-CALENDAR-ROUTINE                           02833430
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               02833516
                                                                        02833616
            IF DPG54-DATE-INVALID                                       02833730
               DISPLAY '** ABEND ** IN PXKUT109'                        02833816
               DISPLAY '** PARAGRAPH: 1700-INCREMENT-INPUT-DATE'        02833916
               DISPLAY '** INVALID PROCESS DATE ON INPUT FILE.'         02834016
               DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '           02834116
                           DPG54-ERROR-MESSAGE                          02834216
               DISPLAY '** DPG51 = ' DPG51                              02834316
               DISPLAY '** DPG52 = ' DPG52                              02834416
               DISPLAY '** DPG53 = ' DPG53                              02834516
               DISPLAY '** DPG54 = ' DPG54                              02834616
               DISPLAY '** DPG55 = ' DPG55                              02834716
               DISPLAY '** DPG56 = ' DPG56                              02834816
               SET  ABEND-4003-INVALID-INPUT-DATE TO TRUE               02834916
               CALL 'ILBOABN0' USING ABEND-CODE                         02835016
            ELSE                                                        02835130
               MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-INV-START-DATE       02835230
            END-IF.                                                     02835330
      ******************************************************************02835413
      * PROCESS CURSOR RECORD.                                          02836000
      ******************************************************************02840000
       2000-PRCSS-YLLW-TCK-EXTR-FL.                                     02850007
      ******************************************************************02860000
           IF PXRD124-STR-NBR NOT EQUAL TO PV-STR-NBR                   02870019
               SET PS-STR-INVENTORY-DONE-NO TO TRUE                     02870123
               MOVE PXRD124-STR-NBR TO PV-STR-NBR                       02870207
               MOVE PV-STR-NBR TO INVPAR-LOC-NBR                        02870321
               PERFORM 4000-VALIDATE-INVENTORY                          02870407
           END-IF.                                                      02870507
           IF PS-STR-INVENTORY-DONE-NO                                  02870627
              PERFORM 7000-WRITE-ADJ-ELM-FILE                           02870727
           ELSE                                                         02870828
              ADD PC-ONE TO PV-ELM-CNT                                  02871029
           END-IF.                                                      02871107
           ADD PC-ONE TO PV-READ-CNT.                                   02871228
           PERFORM 3000-READ-YLLW-TCK-ADJ-FL.                           02871307
      ******************************************************************02871405
      *  READ ADJUSTMENT INPUT FILE                                     02871505
      ******************************************************************02871605
       3000-READ-YLLW-TCK-ADJ-FL.                                       02871705
                                                                        02871805
           READ YLLW-TCK-ADJ-FILE INTO PXRD124-OH-ADJ-RECORD            02871905
               AT END SET PS-END-OF-ADJ-INP-FILE TO TRUE.               02872005
                                                                        02872105
      ******************************************************************02899900
       4000-VALIDATE-INVENTORY.                                         02900008
      ******************************************************************03966000
           EXEC SQL                                                     03966209
                  SELECT '1'                                            03966410
                  INTO :PV-SELECT                                       03966511
                  FROM TINVPAR                                          03966609
                  WHERE ACTL_UNIT_BK_DTE BETWEEN                        03966809
                  :PV-INV-START-DATE AND :PV-INV-END-DATE               03966909
                  AND LOC_NBR = :INVPAR-LOC-NBR                         03967010
                  WITH UR                                               03967109
           END-EXEC.                                                    03967209
073200                                                                  03967500
074600     EVALUATE TRUE                                                03968900
074700       WHEN SQLCODE = +0                                          03969000
074800         SET PS-STR-INVENTORY-DONE-YES TO TRUE                    03969210
074900       WHEN SQLCODE = +100                                        03969300
074800         SET PS-STR-INVENTORY-DONE-NO TO TRUE                     03969410
075100       WHEN OTHER                                                 03969500
075200         MOVE '4000-VALIDATE-INVENTORY'                           03969610
075300           TO PV-PARAGRAPH-NAME                                   03969700
075400         MOVE 'PERFORM SELECT ON TINVPAR'                         03969810
075500           TO PV-DB2-OPERATION-ATTEMPTED                          03969900
075600         PERFORM 9999-SQL-ERROR                                   03970000
075700     END-EVALUATE.                                                03970100
           IF PS-STR-INVENTORY-DONE-YES                                 03970232
              ADD  PC-ONE TO PV-STR-ELM-CNT                             03970332
           END-IF.                                                      03970432
      ******************************************************************03970500
       7000-WRITE-ADJ-ELM-FILE.                                         03970622
      ******************************************************************03970700
           WRITE YLLW-ELM-FILE-REC FROM PXRD124-OH-ADJ-RECORD.          03970824
           ADD  PC-ONE TO PV-WRITE-CNT.                                 03970900
      ******************************************************************03971000
                                                                        03972600
      ******************************************************************03972700
      *  WRITE COUNTER FILES, CLOSE FILES                              *03973000
      ******************************************************************03980000
       9000-END-OF-JOB.                                                 03990000
           DISPLAY 'NUMBER OF RECORDS READ   :'  PV-READ-CNT.           04000028
           DISPLAY 'NUMBER OF RECORDS WRITTEN:'  PV-WRITE-CNT.          04010028
           DISPLAY 'NUMBER OF RECORDS ELIMINATED:' PV-ELM-CNT.          04020032
           DISPLAY 'NUMBER OF STORES ELIMINATE ADJSUTEMT:'              04030032
                                           PV-STR-ELM-CNT.              04040032
           CLOSE DATE-FILE                                              04060022
                 YLLW-TCK-ADJ-FILE                                      04071228
                 YLLW-TCK-ADJ-ELM-FILE.                                 04071328
      ******************************************************************04091000
       9999-SQL-ERROR.                                                  04100000
      ******************************************************************04101000
                                                                        04120000
           MOVE SQLCODE    TO SQLCODE2.                                 04130000
           MOVE SQLERRD(3) TO SQLERRD3.                                 04140000
           DISPLAY '** PROGRAM        ** = PXKUT109'.                   04160005
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  04170000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  04180000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   04190000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  04200000
                                                                        04210000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04220000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04230000
                                                                        04240000
      *                                                                 04250000
