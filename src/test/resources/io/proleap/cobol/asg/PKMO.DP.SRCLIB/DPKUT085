* ********************************************************************  00001014
*                                                                    *  00002014
*  PROGRAM NAME.   DPKUT085.                                         *  00003014
*  AUTHOR.         RICK TULLOCH.                                     *  00004014
*  DATE-WRITTEN.   07-09-03.                                         *  00005018
*                                                                    *  00006014
*       THIS CALLED SUBROUTINE WILL GET JOB INFORMATION FROM THE     *  00007014
*  TIOT AND MVS GENERAL PURPOSE SVC, AND PASS IT BACK IN A SINGLE    *  00008014
*  PARAMETER LIST.  THE ADDRESS OF THE OUTPUT PARAMETER LIST IS      *  00009014
*  PASSED TO THIS ROUTINE AS A PARAMETER.  COBOL APPLICATIONS        *  00009115
*  CALLING THIS ROUTINE SHOULD USE COPYBOOK DPWS085 AS THE OUTPUT    *  00009215
*  PARAMETER LIST LAYOUT, AND SHOULD CALL THE ROUTINE DYNAMICALLY,   *  00009315
*  AS FOLLOWS:                                                       *  00009515
*                                                                    *  00009615
*       CALL DP085-GET-CURR-JOB-INFO-PGM                             *  00009715
*           USING DP085-GET-CURR-JOB-INFO-PARMS.                     *  00009815
*                                                                    *  00009915
* ********************************************************************  00011114
*                                                                       00012000
*   REGISTER EQUATES AND USAGE                                          00020000
*                                                                       00030000
REG0     EQU   0         TIOT ADDRESSING FROM GPSVC                     00040007
REG1     EQU   1         PARM LIST; GPSVC USES FOR JOB NUMBER           00050000
REG2     EQU   2                                                        00060000
REG3     EQU   3         BASE REGISTER                                  00070000
REG4     EQU   4         ADDRESS OF PARAMETER LIST                      00080000
REG5     EQU   5                                                        00090000
REG6     EQU   6                                                        00100000
REG7     EQU   7                                                        00110000
REG8     EQU   8                                                        00120000
REG9     EQU   9         SAVE TIOT ADDRESSING FROM GPSVC                00130007
REG10    EQU   10                                                       00140000
REG11    EQU   11                                                       00150000
REG12    EQU   12                                                       00160000
REG13    EQU   13                                                       00170000
REG14    EQU   14                                                       00180000
REG15    EQU   15        RETURN CODE; GPSVC REQUEST PARAMETER           00190002
*                                                                       00200000
MVSGPSVC EQU  255        USER DEFINED MVS GP SVC NUMBER                 00210000
DPKUT085 AMODE 31                                                       00211008
DPKUT085 RMODE ANY                                                      00212008
*                                                                       00220000
MYTIOT   DSECT                                                          00230000
         IEFTIOT1        IBM MACRO FOR TASK INPUT/OUTPUT TABLE          00240013
*                             ALL TIO* VARIABLES DEFINED HERE           00241013
DPKUT085 CSECT                                                          00250000
         SAVE  (14,12)               SAVE COBOL REGISTERS               00260000
         BALR  REG3,0                LOAD BASE REGISTER                 00270000
         USING *,REG3                SET BASE REGISTER                  00280000
         LA    REG15,16              SET RETURN CODE DFLT TO ERROR      00290000
         LTR   REG1,REG1             LOAD AND TEST PARM LIST ADDR       00300001
         BZ    EXIT                  ERROR - PARM LIST MISSING (=0)     00310001
         L     REG4,0(0,REG1)        LOAD ADDRESS OF PARM LIST          00330000
*                                        (THIS IS ALSO THE LOCATION     00331002
*                                        OF THE FIRST OUTPUT PARM)      00332002
         LA    REG15,8               REQUEST GPSVC TO GET JOB #         00340002
*                                        AND ADDRESS OF TIOT            00341002
         SVC   MVSGPSVC              EXECUTE GPSVC                      00350000
         LR    REG9,REG0             SAVE TIOT ADDRESSING FROM GPSVC    00351007
         MVC   0(8,REG4),0(REG1)     SAVE JOB NUMBER                    00360001
         LA    REG4,8(0,REG4)        ADVANCE TO NEXT OUTPUT FIELD       00361002
         USING TIOT1,REG9            MAP THE TIOT DEFINITION TO THE     00370007
*                                        ADDRESS RETURNED BY GPSVC      00371002
         MVC   0(8,REG4),TIOCNJOB    SAVE JOB NAME, 0(REG9)             00380007
         LA    REG4,8(0,REG4)        ADVANCE TO NEXT OUTPUT FIELD       00390003
         MVC   0(16,REG4),TIOCSTEP   SAVE JOB STEP/PROC STEP, 8(REG9)   00400007
*                                        PROC STEP IS BLANK IF NO PROC  00410003
*                                        IS USED                        00410103
         LA    REG15,0               SET RETURN CODE FOR SUCCESS        00411001
         L     REG1,SAVEREG1         RESTORE ADDRESS OF PARM LIST       00412005
EXIT     EQU   *                                                        00420000
         RETURN (14,12),RC=00        RETURN TO COBOL PROGRAM            00430000
*                                        RC PROVIDED IN REGISTER 15     00431002
SAVEREG1 DS    F                     SAVE AREA FOR REGISTER 1           00440000
INITPARM DC    100X'40'              INITIALIZATION FOR PARM LIST       00441003
         END DPKUT085                                                   00450000
