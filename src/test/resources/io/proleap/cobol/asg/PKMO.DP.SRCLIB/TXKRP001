       IDENTIFICATION DIVISION.                                         00020001
       PROGRAM-ID.   TXKRP001.                                          00050001
       AUTHOR.       KARL KENDRICK.                                     00060001
       DATE-WRITTEN. MAY 2007.                                          00070001
      ******************************************************************00090001
      *  MODULE TYPE:   COBOL2 DB2 BATCH                                00100001
      *                                                                 00110001
      *  DESCRIPTION: THIS PROGRAM WILL BREAK ON SKU AND STATE. AFTER   00120024
      *               THE HEADINGS, THE FIRST LINE WILL SHOW STORE      00130024
      *               NUMBER, NUMBER OF FEES COLLECTED, AMOUNT OF FEE   00140024
      *               COLLECTED, NUMBER OF FEES RETURNED, AMOUNT OF FEES00150024
      *               RETURNED, NET # OF FEES AND NET AMOUNT OF FEES.   00160024
      * JOB:  TXK510                                                    00180024
      ******************************************************************00230001
       ENVIRONMENT DIVISION.                                            00250001
       CONFIGURATION SECTION.                                           00270001
       SOURCE-COMPUTER.        IBM-3090.                                00280001
       OBJECT-COMPUTER.        IBM-3090.                                00290001
                                                                        00300001
       INPUT-OUTPUT SECTION.                                            00310001
                                                                        00320001
       FILE-CONTROL.                                                    00330001
                                                                        00340001
           SELECT DATE-CONTROL-FILE                                     00350001
             ASSIGN TO INP01.                                           00360001
                                                                        00370001
           SELECT RECYCLE-FEE-FILE                                      00380001
             ASSIGN TO INP02.                                           00390001
                                                                        00400001
           SELECT REPORT-FILE                                           00410001
             ASSIGN TO RPT01.                                           00420001
                                                                        00430001
      ******************************************************************00431017
       DATA DIVISION.                                                   00440001
       FILE SECTION.                                                    00450001
                                                                        00460001
       FD  DATE-CONTROL-FILE                                            00470001
           RECORDING MODE F                                             00480001
           BLOCK CONTAINS 0 RECORDS                                     00490001
           LABEL RECORDS ARE OMITTED.                                   00500001
       01  DATE-CONTROL-RECORD                   PIC  X(80).            00510001
                                                                        00520001
       FD  RECYCLE-FEE-FILE                                             00530001
           RECORDING MODE F                                             00540001
           BLOCK CONTAINS 0 RECORDS                                     00550001
           LABEL RECORDS ARE OMITTED.                                   00560001
       01  RECYCLE-FEE-RECORD                    PIC  X(100).           00570001
                                                                        00580001
       FD  REPORT-FILE                                                  00590001
           RECORDING MODE F                                             00600001
           BLOCK CONTAINS 0 RECORDS                                     00610001
           LABEL RECORDS ARE OMITTED.                                   00620001
       01  REPORT-RECORD                         PIC  X(132).           00630001
                                                                        00640001
       WORKING-STORAGE SECTION.                                         00650001
                                                                        00660001
      ************************************************************      00670001
      * CONTROL CARDS - FISCAL PERIOD END DATE                          00680001
      ************************************************************      00690001
       01  CC-DATE-CONTROL-CARD.                                        00710001
           05  CC-FISCAL-END-DATE          PIC  X(10) VALUE SPACE.      00720017
           05  CC-FISCAL-END-DATE-R REDEFINES CC-FISCAL-END-DATE.       00730017
               10  CC-FISCAL-END-CCYY      PIC  X(04).                  00740017
               10  FILLER                  PIC  X.                      00750017
               10  CC-FISCAL-END-MM        PIC  X(02).                  00760017
               10  FILLER                  PIC  X.                      00770017
               10  CC-FISCAL-END-DD        PIC  X(02).                  00780017
           05  FILLER                      PIC  X(70) VALUE SPACE.      00790017
                                                                        00800001
      ************************************************************      00810001
      * PS PROGRAM SWITCHES                                             00820001
      ************************************************************      00830001
       01  PS-PROGRAM-SWITCHES.                                         00850001
           05  PS-DATE-CONTROL-EOF-SW     PIC  X(001) VALUE 'N'.        00860001
             88  PS-DATE-CONTROL-EOF-YES              VALUE 'Y'.        00870001
             88  PS-DATE-CONTROL-EOF-NO               VALUE 'N'.        00880001
                                                                        00890001
           05  PS-RECYCLE-FEE-EOF-SW      PIC  X(001) VALUE 'N'.        00900001
             88  PS-RECYCLE-FEE-EOF-YES               VALUE 'Y'.        00910001
             88  PS-RECYCLE-FEE-EOF-NO                VALUE 'N'.        00920001
                                                                        00930001
           05  PS-PRINT-STATE-HEADING-SW  PIC  X(001) VALUE 'N'.        00971007
             88  PS-PRINT-STATE-HEADING-YES           VALUE 'Y'.        00972007
             88  PS-PRINT-STATE-HEADING-NO            VALUE 'N'.        00973007
                                                                        00974007
      ************************************************************      00980001
      * PROGRAM CONSTANTS                                               00990001
      ************************************************************      01000001
       01  PC-PROGRAM-CONSTANTS.                                        01020001
           05  PC-CURRENT-PROGRAM-NAME PIC  X(008)  VALUE 'TXKRP001'.   01030017
                                                                        01050001
      ************************************************************      01060001
      * PV PROGRAM VARIABLES                                            01070001
      ************************************************************      01080001
       01  PV-PROGRAM-VARAIBLES.                                        01100001
           05  PV-PAGE-COUNT           PIC S9(04)   VALUE +0     COMP.  01120017
           05  PV-LINE-COUNT           PIC S9(04)   VALUE +0     COMP.  01140017
           05  PV-MAX-LINE-COUNT       PIC S9(04)   VALUE 50     COMP.  01160017
                                                                        01180001
           05  PV-SS-COMPANY-TOTAL     PIC S9(04)   VALUE +1     COMP.  01190001
           05  PV-SS-SKU-TOTAL         PIC S9(04)   VALUE +2     COMP.  01200001
           05  PV-SS-STATE-TOTAL       PIC S9(04)   VALUE +3     COMP.  01210001
                                                                        01220001
           05  PV-HOLD-PREV-SKU-NBR    PIC  9(08)   VALUE ZEROES.       01230001
           05  PV-HOLD-PREV-SKU-DESC   PIC  X(25)   VALUE SPACES.       01240001
           05  PV-HOLD-PREV-ST-CDE     PIC  X(02)   VALUE SPACES.       01250001
           05  PV-HOLD-PREV-STR-NBR    PIC  9(04)   VALUE ZEROES.       01251013
                                                                        01260001
       01  PV-TOT-TABLE.                                                01270001
           05 PV-TOT-CTL-BREAKS OCCURS 3 TIMES                          01280002
              INDEXED BY PV-TOT-NDX1                                    01290001
                        , PV-TOT-NDX2.                                  01300002
               10  PV-TOT-UNITS-SOLD            PIC 9(09).              01310001
               10  PV-TOT-FEES-CHRGD            PIC S9(09)V99.          01320001
               10  PV-TOT-UNITS-RTND            PIC 9(09).              01330001
               10  PV-TOT-FEES-RTND             PIC S9(09)V99.          01340001
               10  PV-TOT-NET-UNITS             PIC S9(09).             01350011
               10  PV-TOT-NET-FEES              PIC S9(09)V99.          01360001
                                                                        01370001
           05  PV-HOLD-PERIOD-START-DTE.                                01380001
               10  PV-HOLD-PERIOD-START-CCYY    PIC X(04).              01390001
               10  FILLER                       PIC X(01).              01400001
               10  PV-HOLD-PERIOD-START-MM      PIC X(02).              01410001
               10  FILLER                       PIC X(01).              01420001
               10  PV-HOLD-PERIOD-START-DD      PIC X(02).              01430001
                                                                        01440001
           05  PV-HOLD-PERIOD-END-DTE.                                  01450001
               10  PV-HOLD-PERIOD-END-CCYY      PIC X(04).              01460001
               10  FILLER                       PIC X(01).              01470001
               10  PV-HOLD-PERIOD-END-MM        PIC X(02).              01480001
               10  FILLER                       PIC X(01).              01490001
               10  PV-HOLD-PERIOD-END-DD        PIC X(02).              01500001
                                                                        01510001
           05  PV-CURRENT-DATE.                                         01520001
               10  PV-CURRENT-CC                PIC X(02).              01530001
               10  PV-CURRENT-YY                PIC X(02).              01540001
               10  PV-CURRENT-MM                PIC X(02).              01550001
               10  PV-CURRENT-DD                PIC X(02).              01560001
               10  PV-CURRENT-HOUR              PIC X(02).              01570001
               10  PV-CURRENT-MIN               PIC X(02).              01580001
               10  PV-CURRENT-SEC               PIC X(02).              01590001
               10  FILLER                       PIC X(08).              01600001
                                                                        01610001
      ************************************************************      01620001
      * THIS IS THE COPYBOOK FOR RECYCLE FEES COLLECTED AND      *      01630001
      * RETURNED EXTRACT USED IN PROGRAM TXKUT001, JOB TXK510.   *      01640001
      ************************************************************      01650001
           COPY TXRD001.                                                01670001
                                                                        01680001
      ************************************************************      01690001
      * DATE ROUTINE COPY MEMBERS                                       01700001
      ************************************************************      01710001
           COPY DPWS500.                                                01730001
                                                                        01740001
      ************************************************************      01750001
      ***  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT               01760001
      ************************************************************      01770001
           COPY DPWS132O.                                               01790001
                                                                        01800001
      ************************************************************      01810001
      * PROGRAM ACCUMULATORS                                            01820001
      ************************************************************      01830001
       01  PA-PROGRAM-ACCUMULATORS.                                     01850001
           05  PA-DATE-CONTROL-RECS-READ    PIC  9(09)   VALUE ZERO.    01860001
           05  PA-RECYCLE-FEE-RECS-READ     PIC  9(09)   VALUE ZERO.    01870001
           05  PA-REPORT-RECS-WRITTEN       PIC  9(09)   VALUE ZERO.    01871002
                                                                        01880001
      ************************************************************      01890001
      * REPORT HEADINGS                                                 01900001
      ************************************************************      01910001
       01  HD00-BLANK-LINE             PIC  X(02)    VALUE SPACES.      01930001
                                                                        01940001
       01  HD02-MAIN-HEADING.                                           01950001
           05  FILLER                  PIC  X(049)   VALUE SPACES.      01960001
           05  FILLER                  PIC  X(035)   VALUE              01970001
               'RECYCLE FEES COLLECTED AND RETURNED'.                   01980001
                                                                        01990001
       01  HD03-MAIN-HEADING.                                           02000001
           05  FILLER                     PIC  X(055)   VALUE SPACES.   02010001
           05  HD03-PERIOD-START-DATE.                                  02020001
               10  HD03-PERIOD-START-MM   PIC X(02)    VALUE 'MM'.      02030001
               10  FILLER                 PIC X(01)    VALUE '/'.       02040001
               10  HD03-PERIOD-START-DD   PIC X(02)    VALUE 'DD'.      02050001
               10  FILLER                 PIC X(01)    VALUE '/'.       02060001
               10  HD03-PERIOD-START-CCYY PIC X(04)    VALUE 'CCYY'.    02070002
               10  FILLER                 PIC X(03)    VALUE ' - '.     02080001
               10  HD03-PERIOD-END-MM     PIC X(02)    VALUE 'MM'.      02090001
               10  FILLER                 PIC X(01)    VALUE '/'.       02100001
               10  HD03-PERIOD-END-DD     PIC X(02)    VALUE 'DD'.      02110001
               10  FILLER                 PIC X(01)    VALUE '/'.       02120001
               10  HD03-PERIOD-END-CCYY   PIC X(04)    VALUE 'CCYY'.    02130002
                                                                        02140001
       01  HD05-SKU-HEADING.                                            02150001
           05  FILLER                  PIC  X(002)   VALUE SPACES.      02160001
           05  FILLER                  PIC  X(005)   VALUE 'SKU:'.      02170010
           05  HD05-SKU-NBR            PIC  9(008)   VALUE ZERO.        02180001
           05  FILLER                  PIC  X(002)   VALUE SPACES.      02190001
           05  HD05-SKU-DESC           PIC  X(025)   VALUE SPACES.      02200001
                                                                        02210001
       01  HD06-STATE-HEADING.                                          02220002
           05  FILLER                  PIC  X(003)   VALUE SPACES.      02230001
           05  FILLER                  PIC  X(007)   VALUE 'STATE: '.   02240001
           05  HD06-ST-CDE             PIC  X(002)   VALUE SPACES.      02250001
                                                                        02260001
       01  HD10-DETAIL-LINE.                                            02270001
           05  HD10-STR-NBR-CELL       PIC  X(024)   VALUE SPACES.      02290010
           05  HD10-UNITS-SOLD-CELL    PIC  X(015)   VALUE              02300009
               '  # OF FEES'.                                           02310009
           05  HD10-FEES-CHRGD-CELL    PIC  X(020)   VALUE              02320009
               '      AMOUNT OF'.                                       02321009
           05  HD10-UNITS-RTND-CELL    PIC  X(015)   VALUE              02330009
               '  # OF FEES'.                                           02340009
           05  HD10-FEES-RTND-CELL     PIC  X(020)   VALUE              02350009
               '      AMOUNT OF'.                                       02351009
           05  HD10-NET-UNITS-CELL     PIC  X(016)   VALUE              02360012
               '            '.                                          02361012
           05  HD10-NET-FEES-CELL      PIC  X(015)   VALUE              02370009
               '               '.                                       02371012
                                                                        02380001
       01  HD11-DETAIL-LINE.                                            02390001
           05  HD11-STR-NBR-CELL       PIC  X(024)   VALUE              02410010
               '    STORE #'.                                           02420004
           05  HD11-UNITS-SOLD-CELL    PIC  X(015)   VALUE              02430009
               '  COLLECTED'.                                           02440009
           05  HD11-FEES-CHRGD-CELL    PIC  X(020)   VALUE              02450009
               ' FEES COLLECTED'.                                       02460009
           05  HD11-UNITS-RTND-CELL    PIC  X(015)   VALUE              02470009
               '   RETURNED'.                                           02480009
           05  HD11-FEES-RTND-CELL     PIC  X(020)   VALUE              02490009
               '  FEES RETURNED'.                                       02500009
           05  HD11-NET-UNITS-CELL     PIC  X(016)   VALUE              02510012
               'NET # OF FEES'.                                         02520012
           05  HD11-NET-FEES-CELL      PIC  X(015)   VALUE              02530001
               'NET AMT OF FEES'.                                       02540012
                                                                        02550001
       01  HD12-DETAIL-LINE.                                            02551005
           05  HD12-STR-NBR-CELL       PIC  X(024)   VALUE              02552010
               '     ----  '.                                           02553005
           05  HD12-UNITS-SOLD-CELL    PIC  X(015)   VALUE              02554009
               '-----------'.                                           02555005
           05  HD12-FEES-CHRGD-CELL    PIC  X(020)   VALUE              02556009
               '---------------'.                                       02557005
           05  HD12-UNITS-RTND-CELL    PIC  X(015)   VALUE              02558009
               '-----------'.                                           02559005
           05  HD12-FEES-RTND-CELL     PIC  X(020)   VALUE              02559109
               '---------------'.                                       02559205
           05  HD12-NET-UNITS-CELL     PIC  X(016)   VALUE              02559312
               '-------------'.                                         02559412
           05  HD12-NET-FEES-CELL      PIC  X(015)   VALUE              02559505
               '---------------'.                                       02559605
                                                                        02559705
       01  DL1-DETAIL-LINE.                                             02560001
           05  DL1-STR-NBR-CELL.                                        02580001
               10  FILLER              PIC  X(005)   VALUE SPACES.      02581005
               10  DL1-STR-NBR         PIC  9(004)   VALUE ZERO.        02590001
               10  FILLER              PIC  X(015)   VALUE SPACES.      02600010
           05  DL1-UNITS-SOLD-CELL.                                     02610002
               10  DL1-UNITS-SOLD      PIC  ZZZ,ZZZ,ZZ9.                02620001
               10  FILLER              PIC  X(004)   VALUE SPACES.      02630009
           05  DL1-FEES-CHRGD-CELL.                                     02640001
               10  DL1-FEES-CHRGD      PIC  ZZZ,ZZZ,ZZ9.99-.            02650001
               10  FILLER              PIC  X(005)   VALUE SPACES.      02660009
           05  DL1-UNITS-RTND-CELL.                                     02670001
               10  DL1-UNITS-RTND      PIC  ZZZ,ZZZ,ZZ9.                02680001
               10  FILLER              PIC  X(004)   VALUE SPACES.      02690009
           05  DL1-FEES-RTND-CELL.                                      02700001
               10  DL1-FEES-RTND       PIC  ZZZ,ZZZ,ZZ9.99-.            02710001
               10  FILLER              PIC  X(005)   VALUE SPACES.      02720009
           05  DL1-NET-UNITS-CELL.                                      02730001
               10  DL1-NET-UNITS       PIC  ZZZZ,ZZZ,ZZ9-.              02740012
               10  FILLER              PIC  X(003)   VALUE SPACES.      02750009
           05  DL1-NET-FEES-CELL.                                       02760001
               10  DL1-NET-FEES        PIC  ZZZ,ZZZ,ZZ9.99-.            02770001
                                                                        02780001
       01  TT1-TOTAL-LINE.                                              02790001
           05  FILLER                  PIC  X(002)   VALUE SPACES.      02800001
           05  TT1-TOTAL-LIT           PIC  X(022)   VALUE SPACES.      02810010
           05  FILLER REDEFINES     TT1-TOTAL-LIT.                      02812009
               10  FILLER              PIC  X(001).                     02813010
               10  TT1-STATE-LIT       PIC  X(007).                     02814010
               10  TT1-ST-CDE          PIC  X(002).                     02815009
               10  TT1-STATE-TOTAL-LIT PIC  X(012).                     02816014
           05  FILLER REDEFINES     TT1-TOTAL-LIT.                      02819009
               10  TT1-SKU-LIT         PIC  X(005).                     02819210
               10  TT1-SKU-NBR         PIC  9(008).                     02819309
               10  TT1-SKU-TOTAL-LIT   PIC  X(009).                     02819514
           05  TT1-UNITS-SOLD-CELL.                                     02820001
               10  TT1-UNITS-SOLD      PIC  ZZZ,ZZZ,ZZ9.                02830001
               10  FILLER              PIC  X(004)   VALUE SPACES.      02840009
           05  TT1-FEES-CHRGD-CELL.                                     02850001
               10  TT1-FEES-CHRGD      PIC  ZZZ,ZZZ,ZZ9.99-.            02860001
               10  FILLER              PIC  X(005)   VALUE SPACES.      02870009
           05  TT1-UNITS-RTND-CELL.                                     02880001
               10  TT1-UNITS-RTND      PIC  ZZZ,ZZZ,ZZ9.                02890001
               10  FILLER              PIC  X(004)   VALUE SPACES.      02900009
           05  TT1-FEES-RTND-CELL.                                      02910001
               10  TT1-FEES-RTND       PIC  ZZZ,ZZZ,ZZ9.99-.            02920001
               10  FILLER              PIC  X(005)   VALUE SPACES.      02930009
           05  TT1-NET-UNITS-CELL.                                      02940001
               10  TT1-NET-UNITS       PIC  ZZZZ,ZZZ,ZZ9-.              02950012
               10  FILLER              PIC  X(003)   VALUE SPACES.      02960009
           05  TT1-NET-FEES-CELL.                                       02970001
               10  TT1-NET-FEES        PIC  ZZZ,ZZZ,ZZ9.99-.            02980001
                                                                        02990001
       01  TRL1-TRAILER-LINE.                                           02991016
           05  FILLER                  PIC  X(053)   VALUE SPACES.      02992016
           05  FILLER                  PIC  X(025)   VALUE              02993016
               '*** NO DATA TO REPORT ***'.                             02993116
                                                                        02994016
      ************************************************************      03000001
      * ERROR HANDLING                                                  03010001
      ************************************************************      03020001
                                                                        03030001
       01  PV-MISC-ABEND-FIELDS.                                        03040001
           05  PV-PARAGRAPH-NAME            PIC  X(030)     VALUE SPACE.03050017
           05  PV-OPERATION-MSG-1           PIC  X(040)     VALUE SPACE.03070017
           05  PV-OPERATION-MSG-2           PIC  X(040)     VALUE SPACE.03090017
           05  PV-DB2-OPERATION             PIC  X(030)     VALUE SPACE.03110017
                                                                        03130001
       01  ABEND-CODE                       PIC S9(004)     VALUE ZEROS 03140017
                                                              COMP SYNC.03160017
           88  ABEND-4003-INVALID-CNTL-CARD       VALUE +4003.          03180023
           88  ABEND-4019-CAL-SUB-ERROR           VALUE +4019.          03200023
                                                                        03210001
      ************************************************************      03220001
       PROCEDURE DIVISION.                                              03230001
      ************************************************************      03240001
       0000-MAINLINE.                                                   03260001
                                                                        03270001
           DISPLAY '************ START OF TXKRP001 ***************'.    03280001
                                                                        03290001
           PERFORM 1000-INITIALIZATION.                                 03300001
                                                                        03310001
           SET PV-TOT-NDX1     TO PV-SS-COMPANY-TOTAL.                  03320001
           PERFORM 3000-INIT-CONTROL-TOTAL.                             03330001
                                                                        03340001
           PERFORM 2000-PROC-RECYCLE-FEE-FILE                           03350001
             UNTIL PS-RECYCLE-FEE-EOF-YES.                              03360001
                                                                        03370001
      ***** PRINT COMPANY TOTALS                                        03380001
                                                                        03390001
           SET PV-TOT-NDX1     TO PV-SS-COMPANY-TOTAL.                  03400001
                                                                        03410001
           MOVE '     COMPANY TOTAL:'  TO TT1-TOTAL-LIT.                03420009
           PERFORM 5000-BUILD-TOTAL-LINE.                               03430001
                                                                        03440001
           PERFORM 5100-PRINT-TOTAL-LINE.                               03450001
                                                                        03460001
           PERFORM 9999-WRAPUP.                                         03470001
                                                                        03480001
           DISPLAY '************  END OF TXKRP001  ***************'.    03490001
                                                                        03500001
           GOBACK.                                                      03510001
                                                                        03520001
      ************************************************************      03530001
      * INITIALIZATION.                                                 03540001
      *     OPENS FILES                                                 03550001
      *     INITIALIZES VARIABLES                                       03560001
      *     PERFORM INITIAL READ                                        03570001
      ************************************************************      03580001
       1000-INITIALIZATION.                                             03600001
                                                                        03610001
           OPEN INPUT  DATE-CONTROL-FILE                                03620001
                       RECYCLE-FEE-FILE                                 03630001
                OUTPUT REPORT-FILE                                      03640001
                                                                        03650001
           MOVE FUNCTION CURRENT-DATE TO PV-CURRENT-DATE                03660001
                                                                        03670001
           PERFORM 8000-READ-DATE-CONTROL-FILE.                         03680001
                                                                        03690001
           IF  PS-DATE-CONTROL-EOF-YES                                  03700001
               MOVE '8000-READ-DATE-CONTROL-FILE  ' TO PV-PARAGRAPH-NAME03710001
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03720001
               SET ABEND-4003-INVALID-CNTL-CARD  TO TRUE                03730023
               PERFORM 9910-ABEND                                       03740001
           END-IF.                                                      03750001
                                                                        03760001
           PERFORM 1100-GET-FISCAL-PERIOD-START.                        03770001
                                                                        03780001
           DISPLAY ' '.                                                 03781021
           DISPLAY 'CONTROL CARD DATE: ' CC-FISCAL-END-DATE.            03782021
                                                                        03830001
           DISPLAY ' '.                                                 03830321
           DISPLAY 'REPORTING FISCAL PERIOD =>'                         03831022
                   ' START: ' PV-HOLD-PERIOD-START-DTE                  03832018
                   ' - '                                                03833018
                   ' END  : ' PV-HOLD-PERIOD-END-DTE.                   03834018
                                                                        03835018
           COMPUTE PV-LINE-COUNT  = PV-MAX-LINE-COUNT  + 1.             03860001
                                                                        03870001
           PERFORM 1200-INITIALIZE-HEADING.                             03880001
                                                                        03890001
           SET PS-RECYCLE-FEE-EOF-NO TO TRUE.                           03900001
           PERFORM 8100-READ-RECYCLE-FEE-FILE.                          03910001
                                                                        03920001
      ******************************************************************03930001
      * GET FISCAL PERIOD START DATE                                    03940001
      ******************************************************************03950001
       1100-GET-FISCAL-PERIOD-START.                                    03970001
                                                                        03980001
           INITIALIZE DPG51 DPG52 DPG53                                 03990001
                      DPG54 DPG55 DPG56.                                04000001
                                                                        04010001
           SET DPG51-FISCAL-454-CALENDAR   TO TRUE.                     04020001
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     04030001
                                                                        04040001
           MOVE CC-FISCAL-END-DATE         TO DPG52-LK-DATE-INPUT.      04050001
                                                                        04060001
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    04070001
                                                   DPG54 DPG55 DPG56.   04080001
                                                                        04090001
           IF  DPG54-SEVERE-ERROR                                       04100001
               DISPLAY '*********************************'              04110001
               DISPLAY '** ABEND FROM CALL SUB         **'              04120001
               DISPLAY '** PROGRAM = TXKRP001          **'              04130025
               DISPLAY '** DATE = ' CC-FISCAL-END-DATE '     **'        04140001
               DISPLAY '*********************************'              04150001
               DISPLAY DPG54-ERROR-MESSAGE                              04160001
               SET ABEND-4019-CAL-SUB-ERROR  TO TRUE                    04170023
               PERFORM 9910-ABEND                                       04180001
           ELSE                                                         04190001
           IF  DPG54-DATE-INVALID                                       04200001
               DISPLAY '*********************************'              04210001
               DISPLAY '** ABEND INVALID END DATE      **'              04220001
               DISPLAY '** PROGRAM = TXKRP001          **'              04230025
               DISPLAY '** DATE = ' CC-FISCAL-END-DATE '         **'    04240001
               DISPLAY '*********************************'              04250001
               DISPLAY DPG54-ERROR-MESSAGE                              04260001
               SET ABEND-4003-INVALID-CNTL-CARD TO TRUE                 04271023
               PERFORM 9910-ABEND                                       04280001
           END-IF                                                       04290001
           END-IF.                                                      04300001
                                                                        04310001
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-HOLD-PERIOD-START-DTE. 04320001
           MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-HOLD-PERIOD-END-DTE.   04330001
                                                                        04341101
      ************************************************************      04353001
      * INITIALIZE HEADING DATE AND TIME                                04360001
      ************************************************************      04370001
       1200-INITIALIZE-HEADING.                                         04390001
                                                                        04400001
           MOVE PV-CURRENT-MM                TO DP132O-RUN-MONTH.       04410001
           MOVE PV-CURRENT-DD                TO DP132O-RUN-DAY.         04420001
           MOVE PV-CURRENT-YY                TO DP132O-RUN-YEAR.        04430001
           MOVE PV-CURRENT-HOUR              TO DP132O-RUN-HOUR.        04440001
           MOVE PV-CURRENT-MIN               TO DP132O-RUN-MINUTE.      04450001
           MOVE 'TXKRP001'                   TO DP132O-PROGRAM-NAME.    04460001
           MOVE 1                            TO DP132O-REPORT-NUMBER.   04470001
                                                                        04480001
           MOVE PV-HOLD-PERIOD-START-MM      TO HD03-PERIOD-START-MM.   04490001
           MOVE PV-HOLD-PERIOD-START-DD      TO HD03-PERIOD-START-DD.   04500001
           MOVE PV-HOLD-PERIOD-START-CCYY    TO HD03-PERIOD-START-CCYY. 04510001
           MOVE PV-HOLD-PERIOD-END-MM        TO HD03-PERIOD-END-MM.     04520001
           MOVE PV-HOLD-PERIOD-END-DD        TO HD03-PERIOD-END-DD.     04530001
           MOVE PV-HOLD-PERIOD-END-CCYY      TO HD03-PERIOD-END-CCYY.   04540001
                                                                        04550001
      ************************************************************      04560001
      * PROCESS RECYCLE FEE FILE                                        04570001
      ************************************************************      04580001
       2000-PROC-RECYCLE-FEE-FILE.                                      04600001
                                                                        04610001
      ***** FORCE PAGE BREAK ON SKU                                     04641009
                                                                        04642007
           COMPUTE PV-LINE-COUNT  = PV-MAX-LINE-COUNT  + 1.             04642109
                                                                        04642209
      ***** INITIALIZE SKU TOTALS                                       04660001
                                                                        04670001
           SET PV-TOT-NDX1     TO PV-SS-SKU-TOTAL.                      04680001
           PERFORM 3000-INIT-CONTROL-TOTAL.                             04690001
                                                                        04700001
      ***** SAVE CURRENT SKU NUMBER AND PROCESS UNTIL IT CHANGES        04710001
                                                                        04720001
           MOVE TX001-SKU-NBR  TO PV-HOLD-PREV-SKU-NBR.                 04730001
           MOVE TX001-SKU-DESC TO PV-HOLD-PREV-SKU-DESC.                04740001
           MOVE TX001-STR-NBR  TO PV-HOLD-PREV-STR-NBR.                 04741013
                                                                        04750001
           PERFORM 2100-PROCESS-SKU                                     04760002
             UNTIL PS-RECYCLE-FEE-EOF-YES                               04770001
                OR TX001-SKU-NBR NOT = PV-HOLD-PREV-SKU-NBR.            04780001
                                                                        04790001
      ***** ADD SKU TOTALS TO COMPANY TOTALS                            04800001
                                                                        04810001
           SET PV-TOT-NDX1     TO PV-SS-SKU-TOTAL.                      04820001
           SET PV-TOT-NDX2     TO PV-SS-COMPANY-TOTAL.                  04830001
           PERFORM 3100-ADD-TO-CONTROL-TOTAL.                           04840001
                                                                        04850001
      ***** PRINT SKU TOTALS                                            04860001
                                                                        04870001
           MOVE SPACES                 TO TT1-TOTAL-LIT.                04871009
           MOVE 'SKU: '                TO TT1-SKU-LIT.                  04880010
           MOVE ' TOTAL: '             TO TT1-SKU-TOTAL-LIT.            04880110
           MOVE  PV-HOLD-PREV-SKU-NBR  TO TT1-SKU-NBR.                  04881009
           PERFORM 5000-BUILD-TOTAL-LINE.                               04890001
                                                                        04900001
      ***** BYPASS SKU TOTAL PRINTING WHEN NO DATA IS FOUND FOR SKU     04900213
                                                                        04900413
           IF  PV-HOLD-PREV-STR-NBR  > ZERO                             04902013
                PERFORM 5100-PRINT-TOTAL-LINE                           04910013
           END-IF.                                                      04920013
                                                                        04921013
      ************************************************************      04930001
      * PROCESS SKU                                                     04940001
      ************************************************************      04950001
       2100-PROCESS-SKU.                                                04970001
                                                                        04980001
      ***** FORCE STATE HEADING PRINT IF WE DONT HAVE A PAGE BREAK.     04990001
                                                                        05000001
           SET PS-PRINT-STATE-HEADING-YES TO TRUE.                      05010001
                                                                        05020001
      ***** INITIALIZE STATE TOTALS                                     05030001
                                                                        05040001
           SET PV-TOT-NDX1     TO PV-SS-STATE-TOTAL.                    05050002
           PERFORM 3000-INIT-CONTROL-TOTAL.                             05060001
                                                                        05070001
      ***** SAVE CURRENT STATE CODE AND PROCESS UNTIL IT CHANGES        05080001
                                                                        05090001
           MOVE TX001-ST-CDE TO PV-HOLD-PREV-ST-CDE.                    05100001
                                                                        05110001
           PERFORM 2200-PROCESS-STATE                                   05120001
             UNTIL PS-RECYCLE-FEE-EOF-YES                               05130001
                OR TX001-SKU-NBR NOT = PV-HOLD-PREV-SKU-NBR             05140002
                OR TX001-ST-CDE  NOT = PV-HOLD-PREV-ST-CDE.             05150002
                                                                        05160001
      ***** ADD STATE TOTALS TO SKU TOTALS                              05170001
                                                                        05180001
           SET PV-TOT-NDX1     TO PV-SS-STATE-TOTAL.                    05190002
           SET PV-TOT-NDX2     TO PV-SS-SKU-TOTAL.                      05200001
           PERFORM 3100-ADD-TO-CONTROL-TOTAL.                           05210001
                                                                        05220001
      ***** STATE TOTAL PRINTING                                        05230001
                                                                        05240001
           MOVE SPACES                 TO TT1-TOTAL-LIT.                05250009
           MOVE 'STATE: '              TO TT1-STATE-LIT.                05250110
           MOVE ' TOTAL:'              TO TT1-STATE-TOTAL-LIT.          05250210
           MOVE PV-HOLD-PREV-ST-CDE    TO TT1-ST-CDE.                   05251009
           PERFORM 5000-BUILD-TOTAL-LINE.                               05260001
                                                                        05270001
      ***** BYPASS STATE TOTAL PRINTING WHEN NO DATA IS FOUND FOR SKU   05270213
                                                                        05270313
           IF  PV-HOLD-PREV-STR-NBR  > ZERO                             05273013
               PERFORM 5100-PRINT-TOTAL-LINE                            05280013
           END-IF.                                                      05300013
                                                                        05301013
      ************************************************************      05310001
      * PROCESS STATE                                                   05320001
      ************************************************************      05330001
       2200-PROCESS-STATE.                                              05350001
                                                                        05360001
           IF  PV-LINE-COUNT > PV-MAX-LINE-COUNT                        05370001
               PERFORM 4000-PRINT-MAIN-HEADING                          05380001
               PERFORM 4100-PRINT-DETAIL-HEADING                        05381009
               PERFORM 4200-PRINT-SKU-HEADING                           05390009
                                                                        05390113
      ***** BYPASS STATE HEADING WHEN NO DATA IS FOUND FOR SKU          05390213
                                                                        05390313
               IF  PV-HOLD-PREV-STR-NBR > ZERO                          05391013
                   PERFORM 4300-PRINT-ST-CDE-HEADING                    05400013
                   SET PS-PRINT-STATE-HEADING-NO TO TRUE                05420013
               END-IF                                                   05430013
           ELSE                                                         05461007
           IF  PS-PRINT-STATE-HEADING-YES                               05462007
               PERFORM 4300-PRINT-ST-CDE-HEADING                        05463009
               SET PS-PRINT-STATE-HEADING-NO TO TRUE                    05464007
           END-IF                                                       05471007
           END-IF.                                                      05480001
                                                                        05490001
           MOVE TX001-STR-NBR     TO DL1-STR-NBR.                       05500001
           MOVE TX001-UNITS-SOLD  TO DL1-UNITS-SOLD.                    05510001
           MOVE TX001-FEES-CHRGD  TO DL1-FEES-CHRGD.                    05520001
           MOVE TX001-UNITS-RTND  TO DL1-UNITS-RTND.                    05530001
           MOVE TX001-FEES-RTND   TO DL1-FEES-RTND.                     05540001
                                                                        05550001
           COMPUTE DL1-NET-UNITS =                                      05560001
                TX001-UNITS-SOLD -  TX001-UNITS-RTND.                   05570001
                                                                        05580001
           COMPUTE DL1-NET-FEES  =                                      05590001
                TX001-FEES-CHRGD +  TX001-FEES-RTND.                    05600001
                                                                        05610001
      ***** BYPASS DETAIL PRINTING WHEN NO DATA IS FOUND FOR SKU        05610313
                                                                        05610513
           IF  PV-HOLD-PREV-STR-NBR  > ZERO                             05612013
               WRITE REPORT-RECORD                                      05620013
                   FROM DL1-DETAIL-LINE                                 05630013
                   AFTER ADVANCING 1                                    05640015
                                                                        05650013
               ADD 1 TO PV-LINE-COUNT                                   05660015
               ADD 1 TO PA-REPORT-RECS-WRITTEN                          05670015
           ELSE                                                         05670116
               WRITE REPORT-RECORD                                      05670216
                   FROM TRL1-TRAILER-LINE                               05670416
                   AFTER ADVANCING 1                                    05670516
                                                                        05670616
               ADD 1 TO PV-LINE-COUNT                                   05670716
               ADD 1 TO PA-REPORT-RECS-WRITTEN                          05670816
           END-IF.                                                      05680016
                                                                        05681013
      * ADD TO STATE TOTAL                                              05690001
                                                                        05700001
           ADD TX001-UNITS-SOLD                                         05710001
            TO PV-TOT-UNITS-SOLD        (PV-TOT-NDX1)                   05720001
               PV-TOT-NET-UNITS         (PV-TOT-NDX1).                  05730002
                                                                        05740001
           ADD TX001-FEES-CHRGD                                         05750001
            TO PV-TOT-FEES-CHRGD        (PV-TOT-NDX1)                   05760001
               PV-TOT-NET-FEES          (PV-TOT-NDX1).                  05770001
                                                                        05780001
           ADD TX001-UNITS-RTND                                         05790001
            TO PV-TOT-UNITS-RTND        (PV-TOT-NDX1).                  05800001
                                                                        05810001
           ADD TX001-FEES-RTND                                          05820001
            TO PV-TOT-FEES-RTND         (PV-TOT-NDX1).                  05830001
                                                                        05840001
           SUBTRACT TX001-UNITS-RTND                                    05850001
               FROM PV-TOT-NET-UNITS         (PV-TOT-NDX1).             05860002
                                                                        05870001
           ADD TX001-FEES-RTND                                          05880001
            TO PV-TOT-NET-FEES          (PV-TOT-NDX1).                  05890001
                                                                        05900001
           PERFORM 8100-READ-RECYCLE-FEE-FILE.                          05910001
                                                                        05920001
      ************************************************************      05930001
      * INITIALIZE CONTROL TOTAL                                        05940001
      ************************************************************      05950001
       3000-INIT-CONTROL-TOTAL.                                         05970001
                                                                        05980001
           MOVE ZERO TO                                                 05990001
                PV-TOT-UNITS-SOLD       (PV-TOT-NDX1)                   06000001
                PV-TOT-FEES-CHRGD       (PV-TOT-NDX1)                   06010001
                PV-TOT-UNITS-RTND       (PV-TOT-NDX1)                   06020001
                PV-TOT-FEES-RTND        (PV-TOT-NDX1)                   06030001
                PV-TOT-NET-UNITS        (PV-TOT-NDX1)                   06040002
                PV-TOT-NET-FEES         (PV-TOT-NDX1).                  06050001
                                                                        06060001
      ************************************************************      06070001
      * ADD CONTROL TOTAL TO HIGHER LEVEL CONTROL TOTAL                 06080001
      ************************************************************      06090001
       3100-ADD-TO-CONTROL-TOTAL.                                       06110001
                                                                        06120001
           ADD PV-TOT-UNITS-SOLD        (PV-TOT-NDX1)                   06130001
            TO PV-TOT-UNITS-SOLD        (PV-TOT-NDX2).                  06140001
           ADD PV-TOT-FEES-CHRGD        (PV-TOT-NDX1)                   06150001
            TO PV-TOT-FEES-CHRGD        (PV-TOT-NDX2).                  06160001
           ADD PV-TOT-UNITS-RTND        (PV-TOT-NDX1)                   06170001
            TO PV-TOT-UNITS-RTND        (PV-TOT-NDX2).                  06180001
           ADD PV-TOT-FEES-RTND         (PV-TOT-NDX1)                   06190001
            TO PV-TOT-FEES-RTND         (PV-TOT-NDX2).                  06200001
           ADD PV-TOT-NET-UNITS         (PV-TOT-NDX1)                   06210001
            TO PV-TOT-NET-UNITS         (PV-TOT-NDX2).                  06220001
           ADD PV-TOT-NET-FEES          (PV-TOT-NDX1)                   06230001
            TO PV-TOT-NET-FEES          (PV-TOT-NDX2).                  06240001
                                                                        06250001
      ******************************************************************06260001
      * PRINT MAIN HEADING                                              06270001
      ******************************************************************06280001
       4000-PRINT-MAIN-HEADING.                                         06300001
                                                                        06310001
           MOVE  ZERO                TO PV-LINE-COUNT.                  06320001
           ADD 1 TO PV-PAGE-COUNT.                                      06330001
           MOVE  PV-PAGE-COUNT       TO DP132O-PAGE-NUMBER.             06340001
           MOVE 1                    TO DP132O-REPORT-NUMBER.           06350001
                                                                        06360001
           WRITE REPORT-RECORD                                          06370001
               FROM DP132O-STANDRD-HEADER-132-COLS                      06380001
               AFTER ADVANCING PAGE.                                    06390001
                                                                        06400001
           WRITE REPORT-RECORD                                          06410001
               FROM HD02-MAIN-HEADING                                   06420001
               AFTER ADVANCING 1 LINE.                                  06430001
                                                                        06440001
           WRITE REPORT-RECORD                                          06450001
               FROM HD03-MAIN-HEADING                                   06460001
               AFTER ADVANCING 1 LINE.                                  06470001
                                                                        06480001
           ADD 3 TO PV-LINE-COUNT.                                      06490001
           ADD 3 TO PA-REPORT-RECS-WRITTEN.                             06500001
                                                                        06510001
      ******************************************************************06511009
      * PRINT DETAIL HEADING                                            06512009
      ******************************************************************06513009
       4100-PRINT-DETAIL-HEADING.                                       06515009
                                                                        06516009
           WRITE REPORT-RECORD                                          06516109
              FROM HD00-BLANK-LINE                                      06516209
              AFTER ADVANCING 1 LINE.                                   06516309
                                                                        06516409
           WRITE REPORT-RECORD                                          06517009
              FROM HD10-DETAIL-LINE                                     06518009
              AFTER ADVANCING 1 LINE.                                   06519009
                                                                        06519109
           WRITE REPORT-RECORD                                          06519209
              FROM HD11-DETAIL-LINE                                     06519309
              AFTER ADVANCING 1 LINE.                                   06519409
                                                                        06519509
           WRITE REPORT-RECORD                                          06519609
              FROM HD12-DETAIL-LINE                                     06519709
              AFTER ADVANCING 1 LINE.                                   06519809
                                                                        06520309
           ADD 4 TO PV-LINE-COUNT.                                      06520409
           ADD 4 TO PA-REPORT-RECS-WRITTEN.                             06520509
                                                                        06520609
      ******************************************************************06521001
      * PRINT SKU HEADING                                               06530001
      ******************************************************************06540001
       4200-PRINT-SKU-HEADING.                                          06560009
                                                                        06570001
           MOVE  PV-HOLD-PREV-SKU-NBR  TO  HD05-SKU-NBR.                06580002
           MOVE  PV-HOLD-PREV-SKU-DESC TO  HD05-SKU-DESC.               06590002
                                                                        06600001
           WRITE REPORT-RECORD                                          06610001
              FROM HD00-BLANK-LINE                                      06620001
              AFTER ADVANCING 1 LINE.                                   06630001
                                                                        06640001
           WRITE REPORT-RECORD                                          06650001
              FROM HD05-SKU-HEADING                                     06660002
              AFTER ADVANCING 1 LINE.                                   06670001
                                                                        06680001
           ADD 2 TO PV-LINE-COUNT.                                      06690001
           ADD 2 TO PA-REPORT-RECS-WRITTEN.                             06700001
                                                                        06710001
      ******************************************************************06720001
      * PRINT STATE HEADING                                             06730001
      ******************************************************************06740001
       4300-PRINT-ST-CDE-HEADING.                                       06760009
                                                                        06770001
           MOVE  PV-HOLD-PREV-ST-CDE  TO  HD06-ST-CDE.                  06780001
                                                                        06790001
           WRITE REPORT-RECORD                                          06800001
              FROM HD00-BLANK-LINE                                      06810001
              AFTER ADVANCING 1 LINE.                                   06820001
                                                                        06830001
           WRITE REPORT-RECORD                                          06840001
              FROM HD06-STATE-HEADING                                   06850002
              AFTER ADVANCING 1 LINE.                                   06860001
                                                                        06860103
           WRITE REPORT-RECORD                                          06861003
              FROM HD00-BLANK-LINE                                      06862003
              AFTER ADVANCING 1 LINE.                                   06863003
                                                                        06870001
           ADD 3 TO PV-LINE-COUNT.                                      06880003
           ADD 3 TO PA-REPORT-RECS-WRITTEN.                             06890003
                                                                        06900001
      ******************************************************************07160001
      * BUILD TOTAL LINE                                                07170001
      ******************************************************************07180001
       5000-BUILD-TOTAL-LINE.                                           07200001
                                                                        07210001
           MOVE PV-TOT-UNITS-SOLD (PV-TOT-NDX1)  TO TT1-UNITS-SOLD.     07220017
           MOVE PV-TOT-FEES-CHRGD (PV-TOT-NDX1)  TO TT1-FEES-CHRGD.     07240017
           MOVE PV-TOT-UNITS-RTND (PV-TOT-NDX1)  TO TT1-UNITS-RTND.     07260017
           MOVE PV-TOT-FEES-RTND  (PV-TOT-NDX1)  TO TT1-FEES-RTND.      07280017
           MOVE PV-TOT-NET-UNITS  (PV-TOT-NDX1)  TO TT1-NET-UNITS.      07300017
           MOVE PV-TOT-NET-FEES   (PV-TOT-NDX1)  TO TT1-NET-FEES.       07320017
                                                                        07340001
      ******************************************************************07350001
      * PRINT TOTAL LINE                                                07360001
      ******************************************************************07370001
       5100-PRINT-TOTAL-LINE.                                           07390001
                                                                        07400001
           WRITE REPORT-RECORD                                          07410001
              FROM HD00-BLANK-LINE                                      07420001
              AFTER ADVANCING 1 LINE.                                   07430001
                                                                        07440001
           WRITE REPORT-RECORD                                          07450001
               FROM TT1-TOTAL-LINE                                      07460001
               AFTER ADVANCING 1.                                       07470001
                                                                        07480001
           ADD 2 TO PV-LINE-COUNT.                                      07490001
           ADD 2 TO PA-REPORT-RECS-WRITTEN.                             07500001
                                                                        07510001
      ******************************************************************07520001
      * READ DATE CONTROL CARD.                                         07530001
      ******************************************************************07540001
       8000-READ-DATE-CONTROL-FILE.                                     07560001
                                                                        07570001
           READ DATE-CONTROL-FILE INTO CC-DATE-CONTROL-CARD             07580001
              AT END                                                    07590001
                 SET PS-DATE-CONTROL-EOF-YES TO TRUE.                   07600001
                                                                        07610001
           IF  PS-DATE-CONTROL-EOF-NO                                   07620001
               ADD 1 TO PA-DATE-CONTROL-RECS-READ                       07630001
           END-IF.                                                      07640001
                                                                        07650001
      ************************************************************      07660001
      * READ RECYCLE FEE FILE                                           07670001
      ************************************************************      07680001
       8100-READ-RECYCLE-FEE-FILE.                                      07700001
                                                                        07710001
           READ RECYCLE-FEE-FILE INTO TX001-RECYCLE-FEE-RECORD          07720002
              AT END                                                    07730001
                 SET PS-RECYCLE-FEE-EOF-YES TO TRUE.                    07740001
                                                                        07750001
           IF PS-RECYCLE-FEE-EOF-NO                                     07760001
               ADD 1 TO PA-RECYCLE-FEE-RECS-READ                        07770001
           END-IF.                                                      07780001
                                                                        07790001
      ************************************************************      07800001
      *  ABEND - PERFORMS A NON-SQL-ABEND                               07810001
      ************************************************************      07820001
       9910-ABEND.                                                      07840001
                                                                        07850001
           DISPLAY '** ABEND           **'.                             07860001
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07870001
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07880001
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07890001
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07900001
           CALL 'ILBOABN0' USING ABEND-CODE.                            07910001
                                                                        07920001
      ************************************************************      07930001
      *     END OF PROGRAM ROUTINE. CLOSES ALL FILES AND DISPLAYS       07940001
      *     ANY DATA FROM THE PROGRAM.                                  07950001
      ************************************************************      07960001
       9999-WRAPUP.                                                     07980001
                                                                        07990001
           CLOSE DATE-CONTROL-FILE                                      08000001
                 RECYCLE-FEE-FILE                                       08010001
                 REPORT-FILE.                                           08020001
                                                                        08030001
           DISPLAY ' '.                                                 08040001
           DISPLAY '*********************************************'.     08050001
           DISPLAY '    TXKRP001 SUMMARY STATISTICS              '.     08060001
           DISPLAY '*********************************************'.     08070001
           DISPLAY ' '.                                                 08080001
           DISPLAY 'DATE CONTROL RECS READ ................: '          08090001
                    PA-DATE-CONTROL-RECS-READ.                          08100001
           DISPLAY ' '.                                                 08110001
           DISPLAY 'RECYCLE FEE RECS READ .................: '          08120006
                    PA-RECYCLE-FEE-RECS-READ.                           08130001
           DISPLAY ' '.                                                 08140001
           DISPLAY 'RECYCLE FEE REPORT RECORDS WRITTEN ....: '          08150001
                    PA-REPORT-RECS-WRITTEN.                             08160001
           DISPLAY ' '.                                                 08170001
           DISPLAY '*********************************************'.     08180001
                                                                        08190001
      ************************************************************      08200001
      *             E N D    O F    P R O G R A M                       08210001
      ************************************************************      08220001
