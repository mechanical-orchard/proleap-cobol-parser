      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    FSKCS055.                                                 
       AUTHOR.        MARC BLAZICH.  GREENBRIER & RUSSEL.                       
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03-19-92.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    F055 - SKU SALES LIST                                      *         
      *                                                               *         
      *    THIS PROGRAM GENERATES A LIST SALES BY DOLLARS AND UNITS   *         
      *    BASED ON CRITERIA ENTERED IN F034.                         *         
      *    THIS TRANSACTION IS ONLY INVOKED WHEN A NON-FULLY QUALIFIED*         
      *    SKU IS ENTERED IN F034.                                    *         
      *                                                               *         
      * 04/19/95 -- M. DEERING :  THE CA-STYLE, CA-TSK-SIZE, AND CA-  *         
      *                           TSK-COLOR FIELDS WERE EXPANDED TO   *         
      *                           20 CHARACTER POSITIONS.             *         
      * 05/23/96 -- A. RAISLER :  ADDED KEY ITEM INDICATOR            *         
      *                                                               *         
      * 08/21/96 -- PATI GREEN :  CHANGED SCREEN TO DISPLAY A TOTAL   *         
      *                           LINE, 13 LIST ROWS INSTEAD OF 14.   *         
      *                           TOTALS WILL DISPLAY '******' IF THE *         
      *                           VALUE IS TOO LARGE TO DISPLAY.      *         
      *                           WR# 4013.                           *         
      *                                                               *         
      * 12/18/96 -- D.BOBROV   :  ADDED LOGIC INTO THE PARAGRAPH      *         
      *                           5210-UPDATE-CRITERIA-QUEU TO MODIFY *         
      *                           TSK-LONG-SIZE-DESC AFTER SELECTION  *         
      *                           FROM TABLE TSK.                     *         
      *                           ALSO WAS MODIFIED WORKING STORAGE   *         
      *                           AND PROCEDURE DIVISION TO EXPEND    *         
      *                           QUANTITY OF STORES FROM 200 TO 500. *         
      *                                                               *         
      * 04/17/97 -- DAN GRUBER :  CHANGED SCREEN TO EXPAND THE ON     *         
      *                           ORDER FIELD FROM 6 TO 7 BYTES.      *         
      *                           WR# 5176.                           *         
      *                                                               *         
      * 05/21/97 -- DAN GRUBER :  CHANGED SCREEN TO ALLOW THE USER TO *         
      *                           DISPLAY COLOR, SIZE, VENDOR AND RPL *         
      *                           BY PRESSING THE F23 KEY (CLR/SIZE)  *         
      *                           AND TO DISPLAY THE ORIGINAL SALES   *         
      *                           AND ONHAND INFORMATION BY PRESSING  *         
      *                           THE F22 KEY (SALES).                *         
      *                           WR# 2866.                           *         
      *                                                               *         
      * 06/05/97 -- DAN GRUBER :  CHANGED THE MAXIMUM NUMBER OF       *         
      *                           STORES FOR THE ONORDER FILE FROM    *         
      *                           500 TO 999.                         *         
      *                                                               *         
      * 03/04/98 -- PAUL KEBER :  INCREASED THE ENROUTE FIELD IN THE  *         
      *                           MAP DETAIL LINE FROM 6 TO 9 DIGITS. *         
      *                           ALSO DID LIKEWISE TO THE MAP TOTALS *         
      *                           LINE AND MADE IT ONE MAP FIELD.     *         
      *                                                               *         
      * 07/29/98 -- P. KEBER   :  UPDATED PROGRAM TO CONVERT THE SKU  *         
      *                           STORE WEEKLY SALES TABLE FROM THE   *         
      *                           OLD NAME (TSKSTWKSL) TO THE NEW NAME*         
      *                           (TSKSTWKS).  REPLACED SALES_INDCT   *         
      *                           FIELD WITH NEW DATE FIELD           *         
      *                           FSCL_BOW_DTE.  USED KY_ITM_IND FROM *         
      *                           TUPC INSTEAD OF TSK IN THE          *         
      *                           TSK-CSR-2 CURSOR.  (WR# 6041)       *         
      *                                                               *         
      * 04/14/99  ROLLIN KRING - REMOVED REFERENCE TO DPG51-SYSTEM-DATE*        
      *       WHICH IS NO LONGER SUPPORTED BY THE CALENDAR ROUTINE.    *        
      *       REMOVED WORKING STORAGE FIELD PC-DPKUT500-CALENDAR TO    *        
      *       STANDARDIZE FIELD IN CALENDAR ROUTINE COPYBOOK.          *        
      *                                           (WR# 10647)          *        
      * 11/01/99 -- M. KLAMIK  :  UPDATED PROGRAM TO DELETE ALL       *         
      *       REFERENCES TO CA-DEPT-CONVRTD-TO-VSM.  DELETED CURSOR   *         
      *       TSK-CSR-1.  ADDED CODE TO HANDLE REGIONAL CLEARANCE     *         
      *       PRICING - TABLE TDISTSM (RCP SUMMARY TABLE ADDED).      *         
      *       ADDED NEW PF18 KEY TO TRANSFER CONTROL TO FSLRCPP -     *         
      *       (FSKCS080).                                             *         
      *                                              (WR# 10760)      *         
      *                                                               *         
      * 05/09/2000  M. KLAMIK  :  CONVERT VSAM ON-ORDER CALL TO DB2   *         
      *                           ON-ORDER CALL (TABLE TSKSTOO).      *         
      *                                                 (WR# 21062)   *         
      *                                                               *         
      * 06/27/2001  M. BANSEMAR:  ADDED INTRANSIT QUANTITY TO THE     *         
      *                           SKU SALES LIST. THE INTRANSIT QTY   *         
      *                           IS FOUND ON TABLE PCT_IN_TRNST_QTY. *         
      *                           THE '2 WKS AGO SALES' COLUMN IS     *         
      *                           REPLACED BY THE INTRANSIT VALUES.   *         
      *                           NEW / CHANGED PARAGRAPHS:           *         
      *                              4309-READ-INTRANSIT-DATA         *         
      *                              4310-SUMM-INTRANSIT-DATA         *         
      *                              4315-FETCH-CSR-2                 *         
      *                                                 (WR# 21546)   *         
      * 05/09/2002  M. COLEMAN :  MAKE CHANGES FOR NON-INTELLEGENT    *         
      *                           SKU.  ADDED ANOTHER VIEW OF MAP     *         
      *                           TO HANDLE OVERFLOW                  *         
      *                                                               *         
      * 09/10/2003  M. KLAMIK  :  CHANGED PARA 4308-EVALUATE-STORE-   *         
      *                           STATUS.  ISSUE WITH STORE NOT FOUND *         
      *                           WHEN RCP SKU IS A STATUS 30.  THIS  *         
      *                           WAS A BUSINESS RULE CHANGE.         *         
      *                                                 (WR# 25651)   *         
      *                                                               *         
      * 02/16/2005  A. KINTOPF :  CHANGED PROGRAM FOR STORE NUMBER    *         
      *                           EXPANSION.  ALSO CHANGED PROGRAM TO *         
      *                           USE XML_LOC TABLE INSTEAD OF TSTR000*         
      *                                                 (WR# 26354)   *         
      * 11/10/2005  M. KLAMIK  :  ADDED CODE TO INITIALIZE THE        *         
      *                           FIELDS TO ZERO (ISSUE WAS NEGATIVE  *         
      *                           STORE EXPECTED).                    *         
      *                           PV-STORE-OH-AMT  (VALUE ZERO)       *         
      *                           PV-STORE-OH-QTY  (VALUE ZERO)       *         
      *                                               (PM00821277)    *         
SR3403* 05/01/2012  J SELWITSCHKA LARGE RETAIL EXPANSION.             *         
SR3403*                           CRQ000000023799                     *         
0518  * 05/18/2018  J ARENDT      FOR MAINFRAME REFACTOR,  BROKE THE  *         
0518  *                           VENDOR STYLE CODE INTO TWO FIELDS   *         
0518  *                           ONE SHORT X(15) AND ONE LONG X(20). *         
0518  *                           CHG0201762                          *         
272945* 08/05/2022  JIM HUNTER    CHANGE CALL OF DPKUT100 TO           *        
272945*                           CALL DP010I-NUMERIC-EDIT-ROUTINE     *        
272945*                           MAKING THE CALL DYNAMIC VS STATIC.   *        
272945*                           CHG0272945                           *        
      *                                                                *        
      * 10/24/22    BOB MCASEY    ADD COPYBOOK DPWS930 AND CHANGED     *        
      *                           CICS ARCHITECTURE CALL TO A DYNAMIC  *        
      *                           CALL.                                *        
      *---------------------------------------------------------------*         
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                 FSKCS036
MC0502     15  DK-CLASS-LOW.                                                    
MC0502         20 DK-MAJOR-CLASS-L            PIC X.                            
MC0502         20 DK-MINOR-CLASS-L            PIC X.                            
MC0502     15  DK-CLASS-LOW-9 REDEFINES DK-CLASS-LOW                            
MC0502                                        PIC 99.                           
MC0502     15  DK-CLASS-HIGH.                                                   
MC0502         20 DK-MAJOR-CLASS-H            PIC X.                            
MC0502         20 DK-MINOR-CLASS-H            PIC X.                            
MC0502     15  DK-CLASS-HIGH-9 REDEFINES DK-CLASS-HIGH                          
MC0502                                        PIC 99.                           
           15  DK-DEPT-NMBR                   PIC  X(03).                       
           15  DK-CLASS-LOW                   PIC  X(02).                       
           15  DK-CLASS-HIGH                  PIC  X(02).                       
MC0502     15  DK-SKU-LOW                     PIC  X(08).                       
MC0502     15  DK-SKU-HIGH                    PIC  X(08).                       
           15  DK-STORE                       PIC  X(05).                       
           15  DK-STORE-9                     PIC  9(05).                       
           15  DK-STORE-COMP                  PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           15  DK-BOW-DTE                     PIC  X(10).                       
           15  DK-GROUP-LOW                   PIC  X(03).                       
           15  DK-GROUP-HIGH                  PIC  X(03).                       
           15  DK-VENDOR-LOW                  PIC  S9(3)  COMP-3.               
           15  DK-VENDOR-HIGH                 PIC  S9(3)  COMP-3.               
0518  *    15  DK-VENDOR-STYLE-LOW            PIC  X(20).                       
0518  *    15  DK-VENDOR-STYLE-HIGH           PIC  X(20).                       
0518       15  DK-VND-STYL-LO                 PIC  X(15).                       
0518       15  DK-VND-STYL-HI                 PIC  X(15).                       
0518       15  DK-LNG-STYL-LO                 PIC  X(20).                       
0518       15  DK-LNG-STYL-HI                 PIC  X(20).                       
           15  DK-LONG-COLOR-LOW              PIC  X(10).                       
           15  DK-LONG-COLOR-HIGH             PIC  X(10).                       
           15  DK-COLOR-LOW                   PIC  X(03).                       
           15  DK-COLOR-HIGH                  PIC  X(03).                       
           15  DK-SIZE-LOW                    PIC  X(05).                       
           15  DK-SIZE-HIGH                   PIC  X(05).                       
           15  DK-STATUS-1                    PIC  S9(3)  COMP-3.               
           15  DK-STATUS-2                    PIC  S9(3)  COMP-3.               
           15  DK-STATUS-3                    PIC  S9(3)  COMP-3.               
           15  DK-STATUS-4                    PIC  S9(3)  COMP-3.               
           15  DK-STATUS-5                    PIC  S9(3)  COMP-3.               
           15  DK-STATUS-6                    PIC  S9(3)  COMP-3.               
           15  DK-KEY-LOW                     PIC  X.                           
           15  DK-KEY-HIGH                    PIC  X.                           
           15  DK-REPLEN-LOW                  PIC  X.                           
           15  DK-REPLEN-HIGH                 PIC  X.                           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-EN-ROUTE-FILE               PIC  X(08)  VALUE                 
                                                          'FSENRTE'.            
           05  PC-CORPORATE-LITERAL           PIC  X(10)  VALUE                 
                                                          'ALL STORES'.         
           05  PC-OPERATING-COMPANY           PIC  X(02)  VALUE '03'.           
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'FS055A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'FSKM055 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'FSKCS055'.           
           05  PC-DPKCS030                    PIC  X(08)  VALUE                 
                                                          'DPKCS030'.           
           05  PC-COLOR-000                   PIC  X(03)  VALUE                 
                                                          '000'.                
           05  PC-COLOR-999                   PIC  X(03)  VALUE                 
                                                          '999'.                
           05  PC-ALL-SIZES                   PIC  X(10)  VALUE                 
                                                          'ALL SIZES'.          
           05  PC-ALL-COLS                    PIC  X(10)  VALUE                 
                                                          'ALL COLORS'.         
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
           05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
           05  PC-CRITERIA-QUEUE-KEY          PIC  9(01)  VALUE 3.              
           05  PC-KEY-QUEUE-SEQ               PIC  9(01)  VALUE 4.              
           05  PC-SCRN-QUEUE-SEQ              PIC  9(01)  VALUE 5.              
           05  PC-ONE                         PIC S9(04)  COMP VALUE +1.        
           05  PC-SCRN-ONE                    PIC S9(04)  COMP VALUE +1.        
           05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
                                                          +1000000              
                                                          COMP-3.               
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-MAX-NUM-WEEKS               PIC S9(03)  VALUE +2              
                                                          COMP-3.               
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
                                                          COMP-3.               
           05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
           05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
           05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
           05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
           05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
           05  PC-YEAR-99                     PIC  9(2)   VALUE 99.             
           05  PC-ABSTIME              PIC S9(18)   VALUE ZERO                  
                                       COMP SYNC.                               
           05  PC-SYSTEM-DATE          PIC  X(06).                              
                                                                                
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
               10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
               10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
               10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
               10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
               10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
               10  PC-TSYMSG-00370            PIC  9(05)  VALUE 00370.          
               10  PC-TSYMSG-00989            PIC 9(5)    VALUE 00989.          
               10  PC-TSYMSG-01056            PIC 9(5)    VALUE 01056.          
               10  PC-TSYMSG-01300            PIC  9(05)  VALUE 01300.          
               10  PC-TSYMSG-02136            PIC  9(05)  VALUE 02136.          
               10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
               88  PS-NO-ERROR                            VALUE 'Y'.            
               88  PS-ERROR                               VALUE 'N'.            
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-COMMAREA-VALID                      VALUE 'Y'.            
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
           05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
               88  PS-DONE                                VALUE 'Y'.            
               88  PS-NOT-DONE                            VALUE 'N'.            
           05  PS-CLR-STORE-SW                PIC  X(01)  VALUE 'N'.            
               88  PS-CLR-STORE-FOUND                     VALUE 'Y'.            
               88  PS-CLR-STORE-NOT-FOUND                 VALUE 'N'.            
           05  PS-SKIP-SKU-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-SKIP-SKU-DISPLAY                    VALUE 'Y'.            
               88  PS-KEEP-SKU-DISPLAY                    VALUE 'N'.            
           05  PS-INT-FOUND-IND               PIC X(1)    VALUE 'N'.            
               88  PS-INT-FOUND                           VALUE 'Y'.            
               88  PS-INT-NOT-FOUND                       VALUE 'N'.            
                                                                                
       01  HL-HEADING-LINES.                                            PMKCS239
MC0502     05  HL-SALES-LN-1                  PIC X(41)   VALUE            CL*34
MC0502         '    CURR  CURR WK  LST WK   ON          '.                 CL*34
MC0502     05  HL-SALES-LN-2                  PIC X(41)   VALUE            CL*34
MC0502         '  ONHAND    SALES   SALES   ORDER       '.                 CL*35
MC0502     05  HL-SALES-LN-2-DOL              PIC X(41)   VALUE            CL*34
MC0502         'ONHAND $  SALES $  SALES $  ORDER $     '.                 CL*35
MC0502     05  HL-COLOR-SIZE-LN-1             PIC X(41)   VALUE            CL*34
MC0502         ' COLOR      SIZE       VENDOR NAME   RPL'.                 CL*34
MC0502     05  HL-SALES-RT-LN-1               PIC X(41)   VALUE            CL*34
MC0502         '    IN-           STORE                 '.                 CL*34
MC0502     05  HL-SALES-RT-LN-2               PIC X(41)   VALUE            CL*34
MC0502         '    TRANSIT       ONHAND                '.                 CL*34
MC0502     05  HL-SALES-RT-LN-2-DOL           PIC X(41)   VALUE            CL*34
MC0502         '    TRANSIT $     ONHAND $              '.                 CL*34
                                                                        PMKCS239
       01  DL-DETAIL-LINE-AREA.                                         PMKCS239
           05  DL-DETAIL-LINE                 PIC X(41)   VALUE SPACES. PMKCL*35
           05  FILLER REDEFINES DL-DETAIL-LINE.                         PMKCS239
               10  DL-CURR-ONHAND             PIC ZZZZZZZ9-.            PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-CURR-SALES              PIC ZZZZZZ9-.             PMKCS239
MC0502         10  FILLER                     PIC X(01).                        
               10  DL-LAST-WK-SALES           PIC ZZZZZZ9-.             PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-ON-ORDER                PIC ZZZZZZ9.              PMKCS239
MC0502         10  FILLER                     PIC X(06).                        
           05  FILLER REDEFINES DL-DETAIL-LINE.                         PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-COLOR-DESC              PIC X(10).                PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-SIZE-DESC               PIC X(10).                PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-VENDOR-NAME             PIC X(13).                PMKCS239
               10  FILLER                     PIC X(01).                PMKCS239
               10  DL-REPLEN-IND              PIC X(01).                PMKCL*35
               10  FILLER                     PIC X(03).                        
                                                                        PMKCS239
MC0502     05  FILLER REDEFINES DL-DETAIL-LINE.                         PMKCS239
MC0502         10  FILLER                     PIC X(05).                PMKCS239
MC0502         10  DL-INTRANSIT               PIC ZZZZZ9-.              PMKCS239
MC0502         10  FILLER                     PIC X(04).                PMKCS239
MC0502         10  DL-STORE-OH                PIC ZZZZZZZ9-.            PMKCS239
MC0502         10  FILLER                     PIC X(16).                        
                                                                                
       01  TL-TOTALS-LINE-AREA.                                         PMKCS239
           05  TL-TOTALS-LINE                 PIC X(41)   VALUE SPACES. PMKCL*35
           05  FILLER REDEFINES TL-TOTALS-LINE.                         PMKCS239
               10  TL-CURR-ONHAND.                                      PMKCS239
                   15  TL-CURR-ONHAND-9       PIC ZZZZZZZ9-.                    
                   15  TL-CURR-ONHAND-C REDEFINES TL-CURR-ONHAND-9              
                                              PIC X(09).                        
               10  FILLER                     PIC X(01).                PMKCS239
               10  TL-CURR-SALES.                                       PMKCS239
                   15  TL-CURR-SALES-9        PIC ZZZZZZ9-.                     
                   15  TL-CURR-SALES-C REDEFINES TL-CURR-SALES-9                
                                              PIC X(08).                        
               10  FILLER                     PIC X(01).                PMKCS239
               10  TL-LAST-WK-SALES.                                    PMKCS239
                   15  TL-LAST-WK-SALES-9     PIC ZZZZZZ9-.                     
                   15  TL-LAST-WK-SALES-C REDEFINES TL-LAST-WK-SALES-9          
                                              PIC X(08).                        
               10  FILLER                     PIC X(01).                PMKCS239
               10  TL-ON-ORDER.                                         PMKCS239
                   15  TL-ON-ORDER-9          PIC ZZZZZZ9.                      
                   15  TL-ON-ORDER-C REDEFINES TL-ON-ORDER-9                    
                                              PIC X(07).                        
MC0502         10  FILLER                     PIC X(06).                        
MC0502     05  FILLER REDEFINES TL-TOTALS-LINE.                         PMKCS239
MC0502         10  TL-INTRANSIT.                                        PMKCS239
MC0502             15  FILLER                 PIC X(05).                        
MC0502             15  TL-INTRANSIT-9         PIC ZZZZZ9-.                      
MC0502             15  TL-INTRANSIT-C   REDEFINES TL-INTRANSIT-9                
MC0502                                        PIC X(07).                        
MC0502         10  FILLER                     PIC X(03).                        
MC0502         10  TL-STORE-OH.                                         PMKCS239
MC0502             15  TL-STORE-OH-9          PIC ZZZZZZZZ9.                    
MC0502             15  TL-STORE-OH-C REDEFINES TL-STORE-OH-9                    
MC0502                                        PIC X(09).                        
MC0502         10  FILLER                     PIC X(17).                        
                                                                        PMKCS239
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-STORE-OH-AMT                PIC S9(9)   COMP-3                
MK1110                                                VALUE ZERO.               
           05  PV-STORE-OH-QTY                PIC S9(9)   COMP-3                
MK1110                                                VALUE ZERO.               
           05  PV-ON-ORDER-QTY                PIC S9(7)   COMP-3.               
           05  PV-ON-ORDER-AMT                PIC S9(7)   COMP-3.               
           05  PV-ON-ORDER-DLR                PIC S9(7)   COMP-3.               
           05  PV-SALES-FIGURES.                                                
               10  PV-WKLY-SALES     OCCURS  2 TIMES.                           
                   15  PV-SALES-QTY           PIC S9(7)   COMP-3.               
                   15  PV-SALES-AMT           PIC S9(7)   COMP-3.               
           05  PV-TODAYS-DATE                 PIC 9(7)    VALUE ZERO.           
           05  FILLER REDEFINES PV-TODAYS-DATE.                                 
               10  FILLER                     PIC X(2).                         
               10  PV-TODAYS-YYDDD            PIC 9(5).                         
           05  PV-WEEK-IND                    PIC X(4)    VALUE SPACES.         
           05  PV-WEEKS-THIS-YEAR             PIC 9(2)    VALUE ZERO.           
               88  PV-52-WEEKS-THIS-YEAR                  VALUE 52.             
               88  PV-53-WEEKS-THIS-YEAR                  VALUE 53.             
           05  PV-WK                          PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-WKLY-DATE-KEYS.                                               
               10  PV-WKLY-DATE-KEY  OCCURS  3 TIMES.                           
                   15  PV-WKLY-BOW-DATE       PIC X(10).                        
           05  PV-WORK-YEAR                   PIC 9(2)    VALUE ZERO.           
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-ITEM                        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CUR-RTL-AMT            PIC S9(07)V9(02) VALUE +0              
                                                          COMP-3.               
SR3403*    05  PV-CUR-RTL-FMT                 PIC ZZZZ.99 VALUE ZERO.           
SR3403     05  PV-CUR-RTL-FMT                 PIC ZZZZZ.99 VALUE ZERO.          
           05  PV-SUM-LVL-ALL                 PIC X(01)   VALUE 'A'.            
           05  PV-BLANK-DISTRICT              PIC X(02)   VALUE SPACES.         
           05  PV-STATUS-CODE                 PIC X(02)   VALUE SPACES.         
           05  PV-STATUS-CODE-20              PIC X(02)   VALUE '20'.           
           05  PV-STATUS-CODE-30              PIC X(02)   VALUE '30'.           
           05  PV-ONHAND-QTY                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ONHAND-AMT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-STORE-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-STORE-NBR-COMP              PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-INTRANSIT-QTY               PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-INTRANSIT-AMT               PIC S9(09)  VALUE +0              
                                                          COMP-3.               
                                                                                
      ******************************************************************        
      **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
      **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
      **  APPLICATION-SPECIFIC COMMAREA.                              **        
      ******************************************************************        
       01  LQW-LIST-QUEUE-WORK-AREA.                                            
           05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
           05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES                   
                                              INDEXED BY LQW-IDX.               
               10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
               10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
               10  LQW-SELECTED-SW            PIC  X(01).                       
                   88  LQW-SELECT                         VALUE 'S'.            
                   88  LQW-DESELECT                       VALUE ' '.            
               10  LQW-ACTION                 PIC  X(03).                       
               10  LQW-CLASS-NMBR             PIC  X(02).                       
MC0502         10  LQW-SKU-NMBR               PIC  X(08).                       
SR3403*        10  LQW-SKU-DESC               PIC  X(15).                       
SR3403         10  LQW-SKU-DESC               PIC  X(14).                       
SR3403*        10  LQW-CUR-RTL                PIC  X(07).                       
SR3403         10  LQW-CUR-RTL                PIC  X(08).                       
               10  LQW-CUR-ONHAND-AMT         PIC  S9(7)    COMP-3.             
               10  LQW-CUR-ONHAND-QTY         PIC  S9(7)    COMP-3.             
               10  LQW-CUR-SALES-AMT          PIC  S9(7)    COMP-3.             
               10  LQW-CUR-SALES-QTY          PIC  S9(7)    COMP-3.             
               10  LQW-LAST-WK-SALES-AMT      PIC  S9(7)    COMP-3.             
               10  LQW-LAST-WK-SALES-QTY      PIC  S9(7)    COMP-3.             
               10  LQW-INTRN-SALES-AMT        PIC  S9(7)    COMP-3.             
               10  LQW-INTRN-SALES-QTY        PIC  S9(7)    COMP-3.             
               10  LQW-ON-ORDER-AMT           PIC  S9(7)    COMP-3.             
               10  LQW-ON-ORDER-QTY           PIC  S9(7)    COMP-3.             
               10  LQW-STORE-OH-AMT           PIC  S9(9)    COMP-3.             
               10  LQW-STORE-OH-QTY           PIC  S9(9)    COMP-3.             
               10  LQW-ITEM-NMBR              PIC  S9(15)   COMP-3.             
               10  LQW-STORE-NMBR             PIC  X(05).                       
               10  LQW-COLOR-DESC             PIC  X(20).                       
               10  LQW-SIZE-DESC              PIC  X(10).                       
               10  LQW-VENDOR-NAME            PIC  X(30).                       
               10  LQW-REPLEN-IND             PIC  X(01).                       
                                                                                
       01  SQW-SCRN-QUEUE-WORK-AREA.                                            
           15  SQW-BLOCK-ENTRIES OCCURS 2 TIMES.                                
               20  SQW-BLOCK-MODIFIED-SW           PIC  X(01).                  
                   88  SQW-BLOCK-NOT-MODIFIED      VALUE 'N'.                   
                   88  SQW-BLOCK-MODIFIED          VALUE 'Y'.                   
                                                                                
               20  SQW-ITEM-ARRAY.                                              
                   25  SQW-ITEM-ENTRIES OCCURS 13 TIMES.                        
                       30  SQW-LIST-ITEM-NUMBER                                 
                                                   PIC S9(09)                   
                                                   COMP-3.                      
                       30  SQW-TEMP-SELECTED-SW    PIC  X(01).                  
                           88  SQW-VALID-SELECT    VALUE ' '                    
                                                         'S'                    
                                                         'B'                    
                                                         'E'.                   
                           88  SQW-TEMP-NO-SELECT  VALUE ' '.                   
                           88  SQW-TEMP-SELECT     VALUE 'S'.                   
                           88  SQW-TEMP-BEGIN      VALUE 'B'.                   
                           88  SQW-TEMP-END        VALUE 'E'.                   
                       30  SQW-SELECTED-SW         PIC  X(01).                  
                           88  SQW-NO-SELECT       VALUE ' '.                   
                           88  SQW-SELECT          VALUE 'S'.                   
                           88  SQW-BEGIN           VALUE 'B'.                   
                           88  SQW-END             VALUE 'E'.                   
                       30  SQW-ACTION              PIC  X(03).                  
                       30  SQW-CLASS-NMBR          PIC  X(02).                  
MC0502                 30  SQW-SKU-NMBR            PIC  X(08).                  
SR3403*                30  SQW-SKU-DESC            PIC  X(15).                  
SR3403                 30  SQW-SKU-DESC            PIC  X(14).                  
SR3403*                30  SQW-CUR-RTL             PIC  X(07).                  
SR3403                 30  SQW-CUR-RTL             PIC  X(08).                  
                       30  SQW-CUR-ONHAND-AMT      PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-CUR-ONHAND-QTY      PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-CUR-SALES-AMT       PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-CUR-SALES-QTY       PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-LAST-WK-SALES-AMT   PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-LAST-WK-SALES-QTY   PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-INTRANSIT-AMT       PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-INTRANSIT-QTY       PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-ON-ORDER-AMT        PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-ON-ORDER-QTY        PIC  S9(7)                   
                                                        COMP-3.                 
                       30  SQW-STORE-OH-AMT        PIC  S9(9)                   
                                                        COMP-3.                 
                       30  SQW-STORE-OH-QTY        PIC  S9(9)                   
                                                        COMP-3.                 
                       30  SQW-ITEM-NMBR           PIC  S9(15)                  
                                                        COMP-3.                 
                       30  SQW-STORE-NMBR          PIC  X(05).                  
                       30  SQW-COLOR-DESC          PIC  X(20).                  
                       30  SQW-SIZE-DESC           PIC  X(10).                  
                       30  SQW-VENDOR-NAME         PIC  X(30).                  
                       30  SQW-REPLEN-IND          PIC  X(01).                  
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE SKU SELECTION TEMP STORAGE QUEUE.      *         
      *---------------------------------------------------------------*         
                                                                                
           COPY FSWS002.                                                        
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      *    INTRANSIT COPYBOOK.                                         *        
      ******************************************************************        
           COPY XLWS074.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY FSKM055.                                                        
      *                                                                         
       01  MR-OCCURS-LAYOUT                   REDEFINES  FS055AO.               
           05  FILLER                         PIC X(258).                       
           05  MR-SCROLL-AREA                 OCCURS 13 TIMES.                  
               10  MR-SELECTL                 PIC S9(04) COMP.                  
               10  MR-SELECTA                 PIC  X(01).                       
               10  MR-SELECTC                 PIC  X(01).                       
               10  MR-SELECTH                 PIC  X(01).                       
               10  MR-SELECTI                 PIC  X(01).                       
                                                                                
               10  MR-CLASSL                  PIC S9(04) COMP.                  
               10  MR-CLASSA                  PIC  X(01).                       
               10  MR-CLASSC                  PIC  X(01).                       
               10  MR-CLASSH                  PIC  X(01).                       
               10  MR-CLASS                   PIC  X(02).                       
                                                                                
MC0502         10  MR-SKUL                    PIC S9(04) COMP.                  
MC0502         10  MR-SKUA                    PIC  X(01).                       
MC0502         10  MR-SKUC                    PIC  X(01).                       
MC0502         10  MR-SKUH                    PIC  X(01).                       
MC0502         10  MR-SKU                     PIC  X(08).                       
                                                                                
               10  MR-SKU-DESCL               PIC S9(04) COMP.                  
               10  MR-SKU-DESCA               PIC  X(01).                       
               10  MR-SKU-DESCC               PIC  X(01).                       
               10  MR-SKU-DESCH               PIC  X(01).                       
SR3403*        10  MR-SKU-DESC                PIC  X(15).                       
SR3403         10  MR-SKU-DESC                PIC  X(14).                       
               10  MR-CUR-RTLL                PIC S9(04) COMP.                  
               10  MR-CUR-RTLA                PIC  X(01).                       
               10  MR-CUR-RTLC                PIC  X(01).                       
               10  MR-CUR-RTLH                PIC  X(01).                       
SR3403*        10  MR-CUR-RTL                 PIC  X(07).                       
SR3403         10  MR-CUR-RTL                 PIC  X(08).                       
               10  MR-SCROLLABLE-AREAL        PIC S9(04) COMP.          PMKCS239
               10  MR-SCROLLABLE-AREAA        PIC  X(01).               PMKCS239
               10  MR-SCROLLABLE-AREAC        PIC  X(01).               PMKCS239
               10  MR-SCROLLABLE-AREAH        PIC  X(01).               PMKCS239
MC0502         10  MR-SCROLLABLE-AREA         PIC  X(41).                  CL*37
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      * CALENDAR ROUTINE                                              *         
      *---------------------------------------------------------------*         
           COPY DPWS500.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           COPY FSWS000.                                                        
           COPY FSWS006.                                                        
                                                                                
                                                                                
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-DISPLAY-TYPE                    PIC  X.              
                       88  ASC-DISPLAY-DOLLARS             VALUE 'D'.           
                       88  ASC-DISPLAY-UNITS               VALUE 'U'.           
                   15  ASC-TOT-ONHAND-AMT                  PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-ONHAND-QTY                  PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-SALES-AMT                   PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-SALES-QTY                   PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-LAST-WK-SALES-AMT           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-LAST-WK-SALES-QTY           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-INTRANSIT-AMT               PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-INTRANSIT-QTY               PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-ON-ORDER-AMT                PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-ON-ORDER-QTY                PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-STORE-OH-AMT                PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-TOT-STORE-OH-QTY                PIC S9(09)           
                                                                COMP-3.         
                                                                                
                   15  ASC-VIEW-IND                        PIC X(01).   PMKCS239
                       88  ASC-SALES-VIEW                  VALUE '1'.   PMKCS239
                       88  ASC-COLOR-SIZE-VIEW             VALUE '2'.   PMKCS239
MC0502                 88  ASC-RIGHT-VIEW                  VALUE '3'.   PMKCS239
                                                                                
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
                   15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
                       88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
                       88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-LIST-SEQ                    PIC  9(01).          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                   15  ASC-CRIT-QUEUE-NAME.                                     
                       20  ASC-CRIT-TERM-ID                PIC  X(04).          
                       20  ASC-CRIT-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-CRIT-SEQ                    PIC  9(01).          
                   15  ASC-KEY-QUEUE-NAME.                                      
                       20  ASC-KEY-TERM-ID                 PIC  X(04).          
                       20  ASC-KEY-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-KEY-SEQ                     PIC  9(01).          
                   15  ASC-SCRN-QUEUE-NAME.                                     
                       20  ASC-SCRN-TERM-ID                PIC  X(04).          
                       20  ASC-SCRN-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SCRN-SEQ                    PIC  9(01).          
                                                                                
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
                                                                                
MC0502*---------------------------------------------------------------*         
MC0502*    NON-INTELLEGENT SKU WORK AREAS                             *         
MC0502*---------------------------------------------------------------*         
MC0502     COPY ISWS006.                                                        
MC0502   05 NI-CLASS-L-X2             PIC X(02).                                
MC0502   05 NI-CLASS-L-92 REDEFINES NI-CLASS-L-X2                               
MC0502                                PIC 9(02).                                
MC0502   05 NI-CLASS-H-X2             PIC X(02).                                
MC0502   05 NI-CLASS-H-92 REDEFINES NI-CLASS-H-X2                               
MC0502                                PIC 9(02).                                
MC0502   05 NI-SKU-L-X8               PIC X(08).                                
MC0502   05 NI-SKU-L-98 REDEFINES NI-SKU-L-X8                                   
MC0502                                PIC 9(08).                                
MC0502   05 NI-SKU-H-X8               PIC X(08).                                
MC0502   05 NI-SKU-H-98 REDEFINES NI-SKU-H-X8                                   
MC0502                                PIC 9(08).                                
MC0502   05 NI-CLASS-L-C3             PIC S9(04)V COMP-3.                       
MC0502   05 NI-CLASS-H-C3             PIC S9(04)V COMP-3.                       
MC0502   05 NI-SKU-L-C3               PIC S9(08)V COMP-3.                       
MC0502   05 NI-SKU-H-C3               PIC S9(08)V COMP-3.                       
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSK                                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSK                                                     
           END-EXEC.                                                            
MC0502*---------------------------------------------------------------*         
MC0502*    DB2 AREA FOR TSKXREF                                       *         
MC0502*---------------------------------------------------------------*         
MC0502     EXEC SQL                                                             
MC0502          INCLUDE TSKXREF                                                 
MC0502     END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSKST                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSKST                                                   
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE UPC SKU TABLE TO INCLUDE  *         
      *  REPLENISHMENT SKUS                                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE UPC TABLE TO INCLUDE      *         
      *  REPLENISHMENT SKUS                                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSIZE                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSIZE                                                   
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR XMT_LOC                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE XMT00001                                                
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    TSKSTWKS IS THE DCLGEN MEMBER FOR THE SKU WEEKLY SALES TBL *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TSKSTWKS                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TUPCPLS                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TDISTSM                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TDISTSM                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE TENTNME TABLE TO RETRIEVE *         
      *  THE VENDOR NAME                                              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE TSKSTOO TABLE TO RETRIEVE *         
      *  THE ON-ORDER QUANTITY                                        *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TSKSTOO                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    GET ALL SKU'S WHICH MATCH THE CRITERIA AND HAVE BEEN       *         
      *    CONVERTED TO VSM                                           *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE TSK-CSR-2 CURSOR                                        
                FOR SELECT                                                      
MC0502                X.DEPT                                                    
MC0502               ,X.SUBCLASS                                                
MC0502               ,X.SKU                                                     
MK                   ,X.INTR_UPC_ID                                             
                     ,A.CUR_UNIT_RTL_AMT                                        
                     ,A.STATUS_CODE                                             
                     ,A.SKU_DESC                                                
                     ,A.VNDR_IN_HSE_NMBR                                        
                     ,A.ITEM_NMBR                                               
                     ,A.SIZE_CDE                                                
                     ,A.LONG_SIZE_DESC                                          
                     ,A.LONG_COLOR_DESC                                         
                     ,A.ENT_ID                                                  
                     ,B.ONHAND_QTY                                              
                     ,D.RPLNMT_IND                                              
                     ,A.REGN_PRIC_IND                                           
                FROM  TSK     A                                                 
                     ,TSKST   B                                                 
                     ,TUPC    D                                                 
MC0502               ,TSKXREF X                                                 
MC0502         WHERE   X.DEPT                    = :NI-DEPT-9-4                 
MC0502           AND ( X.SUBCLASS               >= :NI-CLASS-L-C3               
MC0502           AND   X.SUBCLASS               <= :NI-CLASS-H-C3)              
MC0502           AND ( X.SKU                    >= :NI-SKU-L-C3                 
MC0502           AND   X.SKU                    <= :NI-SKU-H-C3)                
                 AND   A.GROUP_CODE        BETWEEN :DK-GROUP-LOW AND            
                                                   :DK-GROUP-HIGH               
                 AND   A.VNDR_IN_HSE_NMBR  BETWEEN :DK-VENDOR-LOW AND           
                                                   :DK-VENDOR-HIGH              
                 AND   D.KY_ITM_IND      BETWEEN :DK-KEY-LOW AND                
                                                 :DK-KEY-HIGH                   
                 AND   A.COLOR_CDE       BETWEEN :DK-COLOR-LOW AND              
                                                 :DK-COLOR-HIGH                 
                 AND  A.LONG_COLOR_DESC  BETWEEN :DK-LONG-COLOR-LOW AND         
                                                 :DK-LONG-COLOR-HIGH            
                 AND   A.SIZE_CDE        BETWEEN :DK-SIZE-LOW AND               
                                                 :DK-SIZE-HIGH                  
0518             AND ((A.VENDOR_STYLE_CODE BETWEEN :DK-VND-STYL-LO              
0518                                           AND :DK-VND-STYL-HI)             
0518               OR (A.LONG_VENDOR_STYLE BETWEEN :DK-LNG-STYL-LO              
0518                                           AND :DK-LNG-STYL-HI))            
0518  *          AND (A.VENDOR_STYLE_CODE BETWEEN :DK-VENDOR-STYLE-LOW          
0518  *                                     AND :DK-VENDOR-STYLE-HIGH OR        
0518  *               A.LONG_VENDOR_STYLE BETWEEN :DK-VENDOR-STYLE-LOW          
0518  *                                      AND :DK-VENDOR-STYLE-HIGH)         
                 AND   A.STATUS_CODE         IN (:DK-STATUS-1,                  
                                                 :DK-STATUS-2,                  
                                                 :DK-STATUS-3,                  
                                                 :DK-STATUS-4,                  
                                                 :DK-STATUS-5,                  
                                                 :DK-STATUS-6)                  
                 AND   B.LOC_NBR               = :DK-STORE-COMP                 
MC0502           AND   X.SKU_STAT_CDE         ^= '00'                           
                 AND   A.ITEM_NMBR             =  B.ITEM_NMBR                   
                 AND   A.ITEM_NMBR             =  X.ITM_NBR                     
                 AND   D.INTR_UPC_ID = X.INTR_UPC_ID                            
                 AND   D.UPC_TYP_CDE = 'I'                                      
                 AND   D.RPLNMT_IND BETWEEN                                     
                          :DK-REPLEN-LOW AND :DK-REPLEN-HIGH                    
                 AND   ( CURRENT DATE BETWEEN                                   
                          D.EFF_BEG_DTE  AND  D.EFF_END_DTE  OR                 
                        (CURRENT DATE > D.EFF_BEG_DTE        AND                
                          D.EFF_END_DTE IS NULL))                               
                ORDER BY DEPT                                                   
                        ,SUBCLASS                                               
                        ,SKU                                                    
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF FS055AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
      ******************************************************************        
       0001-CALL-CICS-ARCH-API.                                                 
           CALL DP930-CICS-ARCH-API  USING DFHEIBLK                             
                                           DFHCOMMAREA                          
                                           DP030-CICS-API-FIELDS                
                                           DP020-STANDARD-COMMAREA              
                                           FS055AI.                             
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE '0001-CALL-CICS-ARCH-API'                                   
                                         TO DP013-PARAGRAPH                     
1022RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1022RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EXEC CICS                                                            
               ASKTIME                                                          
                   ABSTIME(PC-ABSTIME)                                          
           END-EXEC.                                                            
                                                                                
           EXEC CICS                                                            
               FORMATTIME                                                       
                   ABSTIME(PC-ABSTIME)                                          
                   MMDDYY(PC-SYSTEM-DATE)                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   SET ASC-SALES-VIEW    TO TRUE                        PMKCS239
                   MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
                                            ASC-SEL-TERM-ID                     
                                            ASC-SCRN-TERM-ID                    
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                         TO ASC-LIST-TASK-NUMBER                
                                            ASC-SEL-TASK-NUMBER                 
                                            ASC-SCRN-TASK-NUMBER                
                   MOVE PC-LIST-QUEUE-SEQ                                       
                                         TO ASC-LIST-SEQ                        
                                                                                
                   MOVE PC-SEL-QUEUE-SEQ    TO ASC-SEL-SEQ                      
                   MOVE CA-SEL-QUEUE-NAME   TO ASC-CRIT-QUEUE-NAME              
                   MOVE CA-SEL-QUEUE-NAME   TO ASC-KEY-QUEUE-NAME               
                   MOVE PC-KEY-QUEUE-SEQ    TO ASC-KEY-SEQ                      
                   MOVE PC-SCRN-QUEUE-SEQ   TO ASC-SCRN-SEQ                     
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  PS-COMMAREA-NOT-VALID                                    
      *-->             INVALID SKU PASSED TO PROGRAM                            
                       MOVE PC-TSYMSG-00370                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                         TO TRUE                                
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
      *-->         SINCE THE ASC WAS TOO LARGE, A TS QUEUE HAD TO               
      *-->         BE USED IN ITS PLACE TO HOLD ALL OF THE SCREEN               
      *-->         INFORMATION.  ONCE THE (WHAT USED TO BE ASC) DATA            
      *-->         IS FILLED INITIALLY, IT IS WRITTEN TO THE TS QUEUE.          
      *-->         IT IS THEN READ BACK INTO WORKING STORAGE ANY OTHER          
      *-->         TIME EXECUTION IS RETURNED TO THE PROGRAM.  A                
      *-->         REWRITE IS DONE TO UPDATE THE TS QUEUE AS THE LAST           
      *-->         STEP OF THE PROGRAM.                                         
                   PERFORM 6200-WRITE-SCRN-QUEUE                                
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 6210-READ-SCRN-QUEUE                                 
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   PERFORM 6210-READ-SCRN-QUEUE                                 
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                           TO PV-TOP-ITEM                       
                   MOVE ZERO               TO ASC-ITEM-AT-TOP-OF-PANEL          
                   SET ASC-DISPLAY-UNITS   TO TRUE                              
                   PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
                   PERFORM 4050-READ-KEY-QUEUE                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                         TO DP013-MESSAGE-TEXT(1)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
           PERFORM 6220-REWRITE-SCRN-QUEUE.                                     
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID         TO TRUE.                               
           MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
                                                                                
           IF DP020-NAV-KEY-DATA > SPACES                                       
               MOVE DP020-NAV-KEY-DATA TO CA-SKU-STORE.                         
                                                                                
           IF CA-DEPT-NUMBER-X NOT NUMERIC                                      
               SET  PS-COMMAREA-NOT-VALID TO TRUE                               
               MOVE SPACES     TO  CA-DEPT-NUMBER-X                             
                                   CA-DEPT-NAME.                                
                                                                                
           IF CA-CLASS-NUMBER-X NOT NUMERIC                                     
               IF CA-MAJOR-CLASS NOT NUMERIC                                    
                   MOVE SPACES TO  CA-CLASS-NUMBER-X                            
                                   CA-CLASS-DESCRIPTION.                        
                                                                                
           IF CA-SKU-NUMBER-X NOT NUMERIC                                       
               MOVE SPACES     TO  CA-SKU-NUMBER-X                              
                                   CA-SKU-DESCRIPTION.                          
                                                                                
           IF CA-STORE-NUMBER-X NOT NUMERIC                                     
               MOVE SPACES     TO  CA-STORE-NUMBER-X                            
                                   CA-STORE-NAME                                
                                   CA-STORE-SHORT-NAME.                         
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   SET ASC-SALES-VIEW    TO TRUE                        PMKCS239
                   PERFORM 2050-DELETE-LIST-QUEUE                               
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
                   PERFORM 2140-DESELECT-ALL-ITEMS                              
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
                   PERFORM 2150-SELECT-ALL-ITEMS                                
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
      *----------------------------------------------------------------*        
       2050-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
                                                                                
               MOVE '2050-DELETE-LIST-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
                   IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
                       INITIALIZE CA-TS-SELECTION-DATA                          
                       MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
                                      TO CA-NUMBER-OF-SELECTED-ITEMS            
                       PERFORM 5000-PREPARE-SEL-QUEUE                           
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO                                    
                                         TO TRUE                                
                       MOVE PC-TSYMSG-00057                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-WARNING                                    
                                         TO TRUE                                
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                   MOVE +1               TO PV-TOP-ITEM                         
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                   PERFORM 2110-BROWSE-BACKWARD                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                   PERFORM 2120-BROWSE-FORWARD                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
                   IF  PS-ERROR                                                 
                       CONTINUE                                                 
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
      *-->     TOGGLE BETWEEN DOLLARS AND UNITS                                 
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                    IF ASC-DISPLAY-UNITS                                        
                        SET ASC-DISPLAY-DOLLARS  TO TRUE                        
                    ELSE                                                        
                        SET ASC-DISPLAY-UNITS    TO TRUE                        
                    END-IF                                                      
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        
                                                                                
      *-->     DISPLAY SALES INFORMATION (DEFAULT)                              
               WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
                   SET ASC-SALES-VIEW TO TRUE                           PMKCS239
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 PMKCS239
                                                                        PMKCS239
MC0502*-->     DISPLAY INTRANSIT & STORE-OH INFORMATION                         
MC0502         WHEN DP020-FK-RIGHT(DP020-SRC-AID)                               
MC0502             SET ASC-RIGHT-VIEW TO TRUE                           PMKCS239
MC0502             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 PMKCS239
                                                                        PMKCS239
MC0502*-->     RETURN TO SALES INFORMATION                                      
MC0502         WHEN DP020-FK-LEFT(DP020-SRC-AID)                                
MC0502             SET ASC-SALES-VIEW TO TRUE                           PMKCS239
MC0502             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 PMKCS239
                                                                        PMKCS239
      *-->     DISPLAY COLOR, SIZE, VENDOR, AND RPL INDICATOR                   
               WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
                   SET ASC-COLOR-SIZE-VIEW TO TRUE                      PMKCS239
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 PMKCS239
                                                                        PMKCS239
      *-->     DISPLAY REGIONAL CLEARANCE PRICE INFORMATION                     
               WHEN DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)               FSKCS036
                   IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              FSKCS036
                       IF CA-STORE-NUMBER-X =                           FSKCS036
                                  (SPACES OR LOW-VALUES OR '00000')     FSKCS036
                          INITIALIZE CA-TS-SELECTION-DATA               FSKCS036
                          MOVE ASC-NUMBER-OF-SELECTED-ITEMS             FSKCS036
                                      TO CA-NUMBER-OF-SELECTED-ITEMS    FSKCS036
                          PERFORM 5000-PREPARE-SEL-QUEUE                FSKCS036
                       ELSE                                             FSKCS036
                          SET DP030-OVERRIDE-GO                         FSKCS036
                                            TO TRUE                     FSKCS036
                          MOVE PC-TSYMSG-02136                          FSKCS036
                                            TO DP020-MSG-NUMBER         FSKCS036
                          SET DP020-MSG-WARNING                         FSKCS036
                                            TO TRUE                     FSKCS036
                       END-IF                                           FSKCS036
                   ELSE                                                 FSKCS036
                       SET DP030-OVERRIDE-GO                            FSKCS036
                                         TO TRUE                        FSKCS036
                       MOVE PC-TSYMSG-00057                             FSKCS036
                                         TO DP020-MSG-NUMBER            FSKCS036
                       SET DP020-MSG-WARNING                            FSKCS036
                                         TO TRUE                        FSKCS036
                   END-IF                                               FSKCS036
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
      *    MESSAGE.                                                    *        
      *----------------------------------------------------------------*        
                                                                                
       2110-BROWSE-BACKWARD.                                                    
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
               GIVING PV-TOP-ITEM.                                              
                                                                                
           IF  ((PV-TOP-ITEM < +1)                                              
             OR (PV-TOP-ITEM = +1))                                             
               MOVE +1                   TO PV-TOP-ITEM                         
      *        -- TOP OF LIST --                                                
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
      *----------------------------------------------------------------*        
                                                                                
       2120-BROWSE-FORWARD.                                                     
                                                                                
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
               GIVING PV-TOP-ITEM.                                              
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
      *        -- BOTTOM OF LIST --                                             
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           ELSE                                                                 
               COMPUTE PV-BOTTOM-ITEM =                                         
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
      *            -- BOTTOM OF LIST --                                         
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *----------------------------------------------------------------*        
                                                                                
       2130-CHECK-FOR-JUMP-BROWSE.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           IF (ASC-START-ITEM-REQUEST = SPACE)                                  
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
272945         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
272945                         USING DP010I-NUMERIC-EDIT-AREA                   
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO  ASC-START-ITEM-REQUEST-N           
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *                        -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *                --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-MDT                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-MDT                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
      *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
      *----------------------------------------------------------------*        
       2140-DESELECT-ALL-ITEMS.                                                 
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE SQW-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB FROM 1 BY 1                         
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
           MOVE +0                     TO ASC-START-OF-SELECTED-RANGE.          
           MOVE +0                     TO ASC-END-OF-SELECTED-RANGE.            
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2141-CLEAR-SELECTED-INDS.                                                
                                                                                
           IF  SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT SQW-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                   SET SQW-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       SQW-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
                       SQW-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECT SWITCH    *        
      *  FOR ALL ITEMS.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2150-SELECT-ALL-ITEMS.                                                   
                                                                                
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE SQW-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
                             ASC-NUMBER-OF-SELECTED-ITEMS.                      
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *----------------------------------------------------------------*        
                                                                                
       2151-SET-SELECTED-INDS.                                                  
                                                                                
           IF  SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT SQW-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                   SET SQW-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                       SQW-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
                       SQW-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
      ******************************************************************        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
           MOVE +1                       TO PV-SUB.                             
                                                                                
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           IF  ASTRTITL > ZERO                                                  
               MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
           ELSE                                                                 
               IF  ASTRTITF = DP015-ERASE-EOF                                   
                   INITIALIZE ASC-START-ITEM-REQUEST                            
               END-IF                                                           
           END-IF.                                                              
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
                                                                                
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
       2210-MOVE-LINES-TO-COMMAREA.                                             
           IF  MR-SELECTL (PV-SUB) > ZERO                                       
               SET SQW-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
               MOVE MR-SELECTI (PV-SUB)                                         
                                         TO SQW-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
           ELSE                                                                 
               IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
                       SET SQW-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
                       MOVE SPACE        TO SQW-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
               END-IF                                                           
           END-IF.                                                              
           ADD +1                        TO ASC-ITEM-SUB.                       
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               MOVE +1                   TO ASC-ITEM-SUB                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
      ******************************************************************        
       3000-EDIT-DATA-IN-COMMAREA.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
      **                                                                        
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
      * EDITTING IS DONE FROM THE BOTTOM UP.                                    
      **                                                                        
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
               ASC-ITEMS-DISPLAYED - 1.                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           PERFORM 3300-EDIT-SELECTIONS                                         
               VARYING PV-SUB                                                   
               FROM PV-SUB BY -1                                                
               UNTIL PV-SUB < +1                                                
                                                                                
           IF  PS-NO-ERROR                                                      
               PERFORM 3400-CHECK-RANGE-SEQUENCE                                
           END-IF.                                                              
                                                                                
           IF  ((PS-NO-ERROR)                                                   
             AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
             AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
                   PERFORM 3500-MARK-RANGE-AS-SELECTED                          
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               (PV-ITEM-IN-USE - SQW-LIST-ITEM-NUMBER(1 1)) + 1.                
                                                                                
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
           ELSE                                                                 
               MOVE +1                     TO ASC-BLK-SUB                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
      ******************************************************************        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-SELECTA (PV-SUB)                 
               MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
               MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      ** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
      ** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
      ** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
      ** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
      ******************************************************************        
       3300-EDIT-SELECTIONS.                                                    
           EVALUATE TRUE                                                        
               WHEN SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                   = SQW-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
                       CONTINUE                                                 
               WHEN SQW-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                   IF  NOT SQW-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
                       ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
                       MOVE SQW-TEMP-SELECTED-SW                                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                                      TO SQW-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
               WHEN SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                       = SPACE                                                  
                   EVALUATE TRUE                                                
                       WHEN SQW-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
                           SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
                           MOVE SPACE TO SQW-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN SQW-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
                           MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
                           MOVE SPACE TO SQW-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN SQW-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
                           MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
                           MOVE SPACE TO SQW-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-EVALUATE                                                 
               WHEN SQW-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
                   IF  SQW-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN SQW-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
                   IF  SQW-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
                   SET DP020-MSG-FATAL    TO TRUE                               
                   MOVE -1                TO MR-SELECTL (PV-SUB)                
                   SET DP030-SET-CURSOR-APPL-1                                  
                                          TO TRUE                               
                   MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
                   MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
                   SET PS-ERROR TO TRUE                                         
                   IF  SQW-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
                       MOVE SPACE         TO SQW-SELECTED-SW                    
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
           SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < 1                                                 
               SUBTRACT +1 FROM ASC-BLK-SUB                                     
               MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
      ** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
      ** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
      ** THE ENDING.                                                  **        
      ******************************************************************        
       3310-EDIT-RANGE.                                                         
           IF  SQW-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
               IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
                   MOVE SQW-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
                                         TO SQW-SELECTED-SW                     
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
               ELSE                                                             
                   IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
                       MOVE PC-TSYMSG-00054                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
                       MOVE DP015-UNDERLINE                                     
                                         TO MR-SELECTH (PV-SUB)                 
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
                   MOVE SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO SQW-SELECTED-SW                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
                   MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
               ELSE                                                             
                   IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE END-OF-RANGE ALLOWED --                      
                       MOVE PC-TSYMSG-00055                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
      ** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
      ******************************************************************        
       3400-CHECK-RANGE-SEQUENCE.                                               
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
             AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
             AND  ASC-START-OF-SELECTED-RANGE >                                 
                                               ASC-END-OF-SELECTED-RANGE        
      *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
               MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-FATAL TO TRUE                                      
               SET PS-ERROR TO TRUE                                             
               PERFORM 3410-POSITION-AT-ERROR                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
      ** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
      ** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
      ******************************************************************        
       3410-POSITION-AT-ERROR.                                                  
           SET DP030-SET-CURSOR-APPL-1                                          
                                         TO TRUE.                               
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
      *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
      *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
      *----------------------------------------------------------------*        
                                                                                
       3500-MARK-RANGE-AS-SELECTED.                                             
           MOVE +1                       TO  ASC-BLK-SUB.                       
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE SQW-LIST-ITEM-NUMBER(1 1)                                       
                                         TO  PV-LIST-ITEM-NUMBER.               
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-START-OF-SELECTED-RANGE                                     
                                         TO PV-START-OF-SELECTED-RANGE.         
           COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
               1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-END-OF-SELECTED-RANGE                                       
                                         TO  PV-END-OF-SELECTED-RANGE.          
           COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
               1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
                     PC-ITEMS-PER-PANEL).                                       
           COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
               PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
                                                                                
           IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
              (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
               MOVE +1                   TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
                         PV-PREV-BLOCK-IN-ARRAY                                 
                   MOVE PV-FIRST-BLOCK-IN-RANGE                                 
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         PV-LAST-BLOCK-IN-RANGE                                 
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
                                            ASC-END-OF-SELECTED-RANGE.          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
      *----------------------------------------------------------------*        
                                                                                
       3510-MARK-SELECTED-INDS.                                                 
                                                                                
           IF (SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) >                           
               ASC-START-OF-SELECTED-RANGE)                                     
           OR (SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) =                           
               ASC-START-OF-SELECTED-RANGE)                                     
                                                                                
               IF (SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) <                       
                   ASC-END-OF-SELECTED-RANGE)                                   
               OR (SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) =                       
                   ASC-END-OF-SELECTED-RANGE)                                   
                                                                                
                   IF NOT SQW-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       SET SQW-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
                           SQW-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
                                     TO TRUE                                    
                       SET SQW-BLOCK-MODIFIED(ASC-BLK-SUB)                      
                                     TO TRUE                                    
                       ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                  SQW-BLOCK-ENTRIES (1)                    
                                       SQW-BLOCK-ENTRIES (2).                   
           MOVE +1                 TO ASC-BLK-SUB.                              
           SET ASC-ITEMS-LEFT-ON-DB                                             
                                   TO TRUE.                                     
                                                                                
           MOVE +0                 TO ASC-ITEM-SUB                              
                                      ASC-NUMBER-OF-ITEMS-READ                  
                                      ASC-NUMBER-OF-SELECTED-ITEMS              
                                      ASC-HIGHEST-BLOCK-WRITTEN                 
                                      ASC-START-OF-SELECTED-RANGE               
                                      ASC-END-OF-SELECTED-RANGE                 
                                      ASC-ITEM-AT-TOP-OF-PANEL                  
                                      ASC-TOT-ONHAND-AMT                        
                                      ASC-TOT-ONHAND-QTY                        
                                      ASC-TOT-SALES-AMT                         
                                      ASC-TOT-SALES-QTY                         
                                      ASC-TOT-LAST-WK-SALES-AMT                 
                                      ASC-TOT-LAST-WK-SALES-QTY                 
                                      ASC-TOT-INTRANSIT-AMT                     
                                      ASC-TOT-INTRANSIT-QTY                     
                                      ASC-TOT-ON-ORDER-AMT                      
                                      ASC-TOT-ON-ORDER-QTY                      
                                      ASC-TOT-STORE-OH-AMT                      
                                      ASC-TOT-STORE-OH-QTY.                     
                                                                                
           SET ASC-DISPLAY-UNITS TO TRUE.                                       
                                                                                
           PERFORM 4100-PREPARE-CSRS.                                           
                                                                                
           PERFORM 4015-OPEN-CSR-2.                                             
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB.                                     
                                                                                
           PERFORM 4025-CLOSE-CSR-2.                                            
                                                                                
           PERFORM 4030-DELETE-KEY-QUEUE.                                       
                                                                                
           PERFORM 4040-WRITE-KEY-QUEUE.                                        
                                                                                
           MOVE +1                   TO  PV-TOP-ITEM.                           
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01300  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP030-SET-CURSOR-APPL-1                                      
                                     TO TRUE                                    
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       4015-OPEN-CSR-2.                                                         
                                                                                
           EXEC SQL                                                             
                OPEN TSK-CSR-2                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4015-OPEN-CSR-2'                                      
                                     TO DP013-PARAGRAPH                         
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       EJECT                                                                    
       4025-CLOSE-CSR-2.                                                        
                                                                                
           EXEC SQL                                                             
               CLOSE TSK-CSR-2                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4025-CLOSE-CSR-2'                                     
                                     TO DP013-PARAGRAPH                         
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE KEY TEMP STORAGE QUEUE.                          *        
      *----------------------------------------------------------------*        
       4030-DELETE-KEY-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '4030-DELETE-KEY-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       4040-WRITE-KEY-QUEUE.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   FROM  (CA-SKU-STORE)                                         
                   ITEM  (PC-ONE)                                               
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '4040-WRITE-KEY-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF REFRESHING, PUT THE CA-SKU-STORE KEY FIELD BACK TO THE   *        
      *    WAY IT WAS WHEN THIS TRANSACTION WAS ORIGINALLY INVOKED.    *        
      *----------------------------------------------------------------*        
                                                                                
       4050-READ-KEY-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-KEY-QUEUE-NAME)                                   
                   INTO  (CA-SKU-STORE)                                         
                   ITEM  (PC-ONE)                                               
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4050-READ-KEY-QUEUE'                                       
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF CA-STORE-NUMBER-X = (SPACES OR LOW-VALUES OR '00000')             
               MOVE '00000'             TO DK-STORE                             
               MOVE 0                 TO DK-STORE-9                             
               MOVE DK-STORE-9        TO DK-STORE-COMP                          
           ELSE                                                                 
               MOVE CA-STORE-NUMBER-X TO DK-STORE                               
           END-IF.                                                              
                                                                                
           IF CA-STORE-NUMBER-X  NUMERIC                                        
              MOVE CA-STORE-NUMBER-9 TO DK-STORE-9                              
              MOVE DK-STORE-9        TO DK-STORE-COMP                           
           END-IF.                                                              
                                                                                
           PERFORM 4330-GET-STORE-NAME.                                         
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4100-PREPARE-CSRS.                                                       
                                                                                
           INITIALIZE DK-DB2-KEYS                                               
                                                                                
           IF DP020-FK-REFRESH(DP020-SRC-AID)                                   
                                                                                
      *-->     THIS TRANSACTION WAS ORIGINALLY INVOKED W/ A PARTIAL             
      *-->     KEY.  THE LOWER LEVEL (SELECTED) TRANSACTION (F024)              
      *-->     FULLY QUALIFIES THE CA-SKU-STORE KEY FIELD.  A REFRESH           
      *-->     WOULD RESULT IN A SINGLE SKU BEING RETURNED (THE LAST            
      *-->     ONE VIEWED IN F024).                                             
      *-->                                                                      
      *-->     THE ORIGINAL CA-SKU-STORE IS STORED IN THE TS-KEY-QUEUE.         
      *-->     PLACE IT'S CONTENTS INTO CA-SKU-STORE AND PROCEED AS             
      *-->     NORMAL.                                                          
      *-->                                                                      
               PERFORM 4050-READ-KEY-QUEUE                                      
           END-IF.                                                              
                                                                                
MC0502     MOVE CA-DEPT-NUMBER-9      TO NI-DEPT-9-4.                           
                                                                                
           IF CA-CLASS-NUMBER-X = (SPACES OR LOW-VALUES)                        
MC0502         MOVE ZERO              TO NI-CLASS-L-C3                          
MC0502         MOVE 99                TO NI-CLASS-H-C3                          
           ELSE                                                                 
MC0502         IF (CA-MAJOR-CLASS > '0') AND                                    
MC0502            (CA-MINOR-CLASS = (SPACES OR LOW-VALUES))                     
MC0502            MOVE CA-MAJOR-CLASS  TO DK-MAJOR-CLASS-L                      
MC0502                                    DK-MAJOR-CLASS-H                      
MC0502            MOVE '0'             TO DK-MINOR-CLASS-L                      
MC0502            MOVE '9'             TO DK-MINOR-CLASS-H                      
MC0502            MOVE DK-CLASS-LOW-9  TO NI-CLASS-L-C3                         
MC0502            MOVE DK-CLASS-HIGH-9 TO NI-CLASS-H-C3                         
MC0502         ELSE                                                             
MC0502             IF CA-MAJOR-CLASS > 0 AND                                    
                      CA-MINOR-CLASS NUMERIC                                    
MC0502                MOVE CA-CLASS-NUMBER-9 TO NI-CLASS-L-C3                   
MC0502                                          NI-CLASS-H-C3                   
MC0502             END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF CA-SKU-NUMBER-X = (SPACES OR LOW-VALUES)                          
               MOVE LOW-VALUES        TO DK-SKU-LOW                             
MC0502                                   NI-SKU-L-X8                            
MC0502         MOVE NI-SKU-L-98       TO NI-SKU-L-C3                            
               MOVE HIGH-VALUES       TO DK-SKU-HIGH                            
MC0502                                   NI-SKU-H-X8                            
MC0502*        MOVE NI-SKU-H-98       TO NI-SKU-H-C3                            
MC0502         MOVE 99999999          TO NI-SKU-H-C3                            
           ELSE                                                                 
               MOVE CA-SKU-NUMBER-X   TO DK-SKU-LOW                             
                                         DK-SKU-HIGH                            
MC0502         MOVE CA-SKU-NUMBER-9   TO NI-SKU-L-C3                            
MC0502                                   NI-SKU-H-C3                            
           END-IF.                                                              
                                                                                
           IF CA-STORE-NUMBER-X = (SPACES OR LOW-VALUES OR '00000')             
               MOVE '00000'             TO DK-STORE                             
               MOVE 0                 TO DK-STORE-9                             
               MOVE DK-STORE-9        TO DK-STORE-COMP                          
           ELSE                                                                 
               MOVE CA-STORE-NUMBER-X TO DK-STORE                               
           END-IF.                                                              
                                                                                
           IF CA-STORE-NUMBER-X  NUMERIC                                        
              MOVE CA-STORE-NUMBER-9 TO DK-STORE-9                              
              MOVE DK-STORE-9        TO DK-STORE-COMP                           
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-CRITERIA-QUEUE.                                    
                                                                                
           IF CA-GROUP-CODE     = (SPACES OR LOW-VALUES)                        
               MOVE LOW-VALUES        TO DK-GROUP-LOW                           
               MOVE HIGH-VALUES       TO DK-GROUP-HIGH                          
           ELSE                                                                 
               MOVE CA-GROUP-CODE     TO DK-GROUP-LOW                           
                                         DK-GROUP-HIGH                          
           END-IF.                                                              
                                                                                
           IF CA-SIZE-CDE       = (SPACES OR LOW-VALUES)                        
               MOVE LOW-VALUES        TO DK-SIZE-LOW                            
               MOVE HIGH-VALUES       TO DK-SIZE-HIGH                           
           ELSE                                                                 
               MOVE CA-SIZE-CDE       TO DK-SIZE-LOW                            
                                         DK-SIZE-HIGH                           
           END-IF.                                                              
                                                                                
                                                                                
           IF CA-TSK-LONG-COLOR = SPACES                                        
              MOVE LOW-VALUES        TO DK-LONG-COLOR-LOW                       
              MOVE HIGH-VALUES       TO DK-LONG-COLOR-HIGH                      
           ELSE                                                                 
              MOVE CA-TSK-LONG-COLOR TO DK-LONG-COLOR-LOW                       
                                        DK-LONG-COLOR-HIGH                      
           END-IF.                                                              
                                                                                
           IF CA-COLOR-1-X      = SPACES                                        
               MOVE LOW-VALUES        TO DK-COLOR-LOW                           
               MOVE HIGH-VALUES       TO DK-COLOR-HIGH                          
           ELSE                                                                 
               MOVE CA-COLOR-1-NUM    TO DK-COLOR-LOW                           
               IF CA-COLOR-2-X = SPACES                                         
                  MOVE CA-COLOR-1-NUM TO DK-COLOR-HIGH                          
               ELSE                                                             
                  MOVE CA-COLOR-2-NUM TO DK-COLOR-HIGH                          
           END-IF.                                                              
                                                                                
                                                                                
           IF CA-VSM-REPLENISH = 'Y' OR                                         
              CA-VSM-REPLENISH = 'N'                                            
               MOVE CA-VSM-REPLENISH  TO DK-REPLEN-LOW                          
                                         DK-REPLEN-HIGH                         
           ELSE                                                                 
               MOVE LOW-VALUES       TO DK-REPLEN-LOW                           
               MOVE HIGH-VALUES      TO DK-REPLEN-HIGH                          
           END-IF.                                                              
                                                                                
                                                                                
           IF CA-KEY-ITEM = 'Y' OR                                              
              CA-KEY-ITEM = 'N'                                                 
              MOVE CA-KEY-ITEM TO DK-KEY-LOW                                    
                                  DK-KEY-HIGH                                   
           ELSE                                                                 
              MOVE LOW-VALUES  TO DK-KEY-LOW                                    
              MOVE HIGH-VALUES TO DK-KEY-HIGH                                   
           END-IF.                                                              
                                                                                
           IF CA-INHOUSE-NUMBER   = (SPACES OR LOW-VALUES)                      
               MOVE -999                TO DK-VENDOR-LOW                        
               MOVE +999                TO DK-VENDOR-HIGH                       
           ELSE                                                                 
               MOVE CA-INHOUSE-NUMBER-9 TO DK-VENDOR-LOW                        
                                           DK-VENDOR-HIGH                       
           END-IF.                                                              
                                                                                
           IF CA-STYLE            = (SPACES OR LOW-VALUES)                      
0518           MOVE LOW-VALUES     TO DK-VND-STYL-LO                            
0518                                  DK-LNG-STYL-LO                            
0518           MOVE HIGH-VALUES    TO DK-VND-STYL-HI                            
0518                                  DK-LNG-STYL-HI                            
0518  *        MOVE LOW-VALUES          TO DK-VENDOR-STYLE-LOW                  
0518  *        MOVE HIGH-VALUES         TO DK-VENDOR-STYLE-HIGH                 
           ELSE                                                                 
0518           MOVE CA-STYLE       TO DK-VND-STYL-LO                            
0518                                  DK-VND-STYL-HI                            
0518                                  DK-LNG-STYL-LO                            
0518                                  DK-LNG-STYL-HI                            
0518  *        MOVE CA-STYLE            TO DK-VENDOR-STYLE-LOW                  
0518  *                                    DK-VENDOR-STYLE-HIGH                 
           END-IF.                                                              
                                                                                
           IF CA-STAT-10-ENTERED                                                
               MOVE +10    TO  DK-STATUS-1                                      
           END-IF                                                               
           IF CA-STAT-20-ENTERED                                                
               MOVE +20    TO  DK-STATUS-2                                      
           END-IF                                                               
           IF CA-STAT-30-ENTERED                                                
               MOVE +30    TO  DK-STATUS-3                                      
           END-IF                                                               
           IF CA-STAT-40-ENTERED                                                
               MOVE +40    TO  DK-STATUS-4                                      
           END-IF                                                               
           IF CA-STAT-90-ENTERED                                                
               MOVE +90    TO  DK-STATUS-5                                      
           END-IF.                                                              
      * MUST EVALUATE STATUS 25 SKUS FOR REGIONAL CLEARANCE PRICING.            
      * STATUS 20-WILL NEED NON-CLEARANCE STORE DATA.                           
      * STATUS 25-WILL NEED NON-CLEARANCE STORE DATA AND CLEARANCE STORE DATA.  
      * STATUS 30-WILL NEED CLEARANCE STORE DATA.                               
           IF CA-STAT-20-ENTERED OR                                             
              CA-STAT-25-ENTERED OR                                             
              CA-STAT-30-ENTERED                                                
               MOVE +25    TO  DK-STATUS-6                                      
           END-IF.                                                              
                                                                                
      * PV-WKLY-DATE-KEYS CONTAINS THE ACTUAL WEEKLY DATE FORMATS               
      * REQUIRED FOR THE DB2 WEEKLY INFORMATION CURSORS                         
           MOVE SPACES TO PV-WKLY-DATE-KEYS.                                    
                                                                                
      * THIS WILL USE THE STANDARD SYSTEM ROUTINE TO FIND THE CURRENT           
      * FIRST WEEKDAY DATE, GIVEN TODAYS DATE.  THIS WEEK'S FIRST               
      * WEEKDAY DATE WILL BE PLACED IN THE FIRST ENTRY OF THE 4                 
      * ENTRY WEEKLY DATE TABLE (THIS TABLE CONTAINS THIS WEEK AND THE          
      * PREVIOUS 3 WEEKS)                                                       
                                                                                
           MOVE EIBDATE TO PV-TODAYS-DATE.                                      
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2                                          
               DPG51-DO-NOT-INCR-DECR-DATE                                      
               DPG52-LK-DTE-GREG        TO TRUE.                                
           MOVE PC-SYSTEM-DATE TO DPG52-GREG-DATE-N.                            
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52                  
                                                   DPG53 DPG54                  
                                                   DPG55 DPG56.                 
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PV-WKLY-BOW-DATE(1).               
                                                                                
      * BY WORKING FROM THE WEEKLY DATE IN THE FIRST ENTRY, FIGURE THE          
      * PREVIOUS 2 WEEKS DATE VALUES AND PLACE THEM IN THE 2 REMAINING          
      * TABLE ENTRIES.                                                          
      * CALCUATE THE DISPLAY DATE FORMAT (MMDDYY) FOR THE MAP.  THIS            
      * (MMDDYY) DATE WILL ALWAYS BE THE FIRST DAY OF THE WEEK.                 
                                                                                
           PERFORM VARYING PV-WK                                                
             FROM 2 BY 1                                                        
               UNTIL PV-WK > PC-MAX-NUM-WEEKS                                   
                 SET DPG51-FISCAL-JAN-DEC-F2                                    
                     DPG51-DECREMENT-DATE                                       
                     DPG52-LK-DTE-GREG        TO TRUE                           
                 MOVE 7 TO DPG51-INCR-DECR-DAYS-9                               
                 MOVE DPG55-1ST-WKDY-GREG-N TO DPG52-GREG-DATE-N                
                 CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52            
                                                         DPG53 DPG54            
                                                         DPG55 DPG56            
                 MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                              
                                     PV-WKLY-BOW-DATE(PV-WK)                    
           END-PERFORM.                                                         
                                                                                
       4200-READ-CRITERIA-QUEUE.                                                
                                                                                
           SET CA-TS-SHARED-DATA      TO TRUE                                   
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (CA-SEL-QUEUE-NAME)                                   
                    INTO  (CA-FSKCS034-CRITERIA-DATA)                           
                    ITEM  (PC-ONE)                                              
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP     = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '4200-READ-CRITERIA-QUEUE'                                  
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTION CRITERIA QUEUE'               
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
                                                                                
           PERFORM                                                              
               VARYING PV-READ-COUNT                                            
               FROM 1 BY 1                                                      
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
                                                                                
               PERFORM 4315-FETCH-CSR-2                                         
                                                                                
               IF SQLCODE = +0                                                  
                  INITIALIZE PV-SALES-FIGURES                                   
                  PERFORM 4320-GET-WEEKLY-SALES VARYING PV-WK                   
                     FROM +1 BY +1                                              
                     UNTIL PV-WK > PC-MAX-NUM-WEEKS                             
                                                                                
      *  WILL GET THE ON-ORDER AND STORE-OH DATA FROM TDISTSM                   
      *  WHEN REGIONALLY CLEARANCE PRICED AND REQUEST IS AT A CORPORATE         
      *  LEVEL (STORE = 000)                                                    
                  SET PS-CLR-STORE-NOT-FOUND TO TRUE                            
                  SET PS-KEEP-SKU-DISPLAY TO TRUE                               
                  IF (TSK-REGN-PRIC-IND = 'Y') AND                              
                     (CA-STORE-NUMBER-X = '00000')                              
                     PERFORM 4307-EVALUATE-RCP-SKU                              
                  ELSE                                                          
                     IF TSK-REGN-PRIC-IND = 'Y'                                 
                        PERFORM 4328-SELECT-TUPCPLS-DATA                        
                     ELSE                                                       
                        MOVE TSK-CUR-UNIT-RTL-AMT TO                            
                             PV-CUR-RTL-AMT                                     
                     END-IF                                                     
                     PERFORM 4305-READ-ON-ORDER-DATA                            
                     PERFORM 4306-READ-STORE-OH-FILE                            
                     INITIALIZE PV-STORE-NBR-COMP                               
                     MOVE DK-STORE-9           TO PV-STORE-NBR-COMP             
      *              IF  CA-STORE-NUMBER-X = '00000'                            
      *                  PERFORM 4310-SUMM-INTRANSIT-DATA                       
      *              ELSE                                                       
      *                  PERFORM 4309-READ-INTRANSIT-DATA                       
      *              END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
                                                                                
      *  WHEN REGIONALLY CLEARANCE PRICED AND AT A STORE LEVEL - CHECK TO       
      *  SEE IF THE STORE IS DISPLAYED WITH FOR THE CORRECT STATUS CHOSEN       
               IF TSK-REGN-PRIC-IND = 'Y'                                       
                  PERFORM 4308-EVALUATE-STORE-STATUS                            
               END-IF                                                           
                                                                                
      *  SKIP DISPLAYING STORES WITH NO ACTIVITY.                               
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                 IF (PV-ONHAND-QTY    = ZERO  AND                               
                     PV-SALES-QTY (1) = ZERO  AND                               
                     PV-SALES-QTY (2) = ZERO  AND                               
      *              PV-SALES-QTY (3) = ZERO  AND                               
                     PV-INTRANSIT-QTY = ZERO  AND                               
                     PV-ON-ORDER-QTY  = ZERO  AND                               
                     PV-STORE-OH-QTY  = ZERO)                                   
                    OR                                                          
                     (PS-SKIP-SKU-DISPLAY)                                      
                      CONTINUE                                                  
                 ELSE                                                           
                   IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
                       ADD +1            TO ASC-ITEM-SUB                        
                   ELSE                                                         
                       ADD +1            TO ASC-BLK-SUB                         
                       MOVE +1           TO ASC-ITEM-SUB                        
                                                                                
                       IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
                           MOVE +1       TO ASC-BLK-SUB                         
                           SET SQW-BLOCK-MODIFIED (1)                           
                                         TO TRUE                                
                           PERFORM 5100-WRITE-TEMP-STORAGE                      
                           MOVE SQW-BLOCK-ENTRIES                               
                                     (PC-MAX-BLOCKS-IN-ARRAY)                   
                                      TO SQW-BLOCK-ENTRIES(ASC-BLK-SUB)         
                           ADD +1        TO ASC-BLK-SUB                         
                           INITIALIZE SQW-BLOCK-ENTRIES (ASC-BLK-SUB)           
                           SET SQW-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
                                         TO TRUE                                
                       END-IF                                                   
                   END-IF                                                       
                   ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
                   MOVE ASC-NUMBER-OF-ITEMS-READ                                
                                         TO SQW-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
MC0502             MOVE SKXREF-SUBCLASS  TO NI-CLASS-9-2                        
MC0502             MOVE NI-CLASS-X-2     TO SQW-CLASS-NMBR                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
MC0502             MOVE SKXREF-SKU       TO NI-SKU-9                            
MC0502             MOVE NI-SKU           TO SQW-SKU-NMBR                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE TSK-SKU-DESC     TO SQW-SKU-DESC                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-ONHAND-QTY    TO SQW-CUR-ONHAND-QTY                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                                                                
                   IF TSK-REGN-PRIC-IND = 'Y'                                   
                      IF CA-STORE-NUMBER-X = '00000'                            
                         MOVE PV-ONHAND-AMT TO                                  
                            SQW-CUR-ONHAND-AMT                                  
                                    (ASC-BLK-SUB ASC-ITEM-SUB)                  
                         MOVE 'MIXED' TO SQW-CUR-RTL                            
                                        (ASC-BLK-SUB ASC-ITEM-SUB)              
                      ELSE                                                      
                         COMPUTE SQW-CUR-ONHAND-AMT                             
                             (ASC-BLK-SUB ASC-ITEM-SUB)                         
                             ROUNDED                                            
                           = PV-CUR-RTL-AMT     * PV-ONHAND-QTY                 
                                                                                
                         MOVE PV-CUR-RTL-FMT     TO SQW-CUR-RTL                 
                                         (ASC-BLK-SUB ASC-ITEM-SUB)             
                      END-IF                                                    
                   ELSE                                                         
                      COMPUTE SQW-CUR-ONHAND-AMT                                
                          (ASC-BLK-SUB ASC-ITEM-SUB)                            
                          ROUNDED                                               
                        = TSK-CUR-UNIT-RTL-AMT * PV-ONHAND-QTY                  
                                                                                
                      MOVE TSK-CUR-UNIT-RTL-AMT TO PV-CUR-RTL-FMT               
                      MOVE PV-CUR-RTL-FMT       TO SQW-CUR-RTL                  
                                         (ASC-BLK-SUB ASC-ITEM-SUB)             
                   END-IF                                                       
                   MOVE PV-SALES-QTY(1)  TO SQW-CUR-SALES-QTY                   
                                         (ASC-BLK-SUB ASC-ITEM-SUB)             
                   MOVE PV-SALES-AMT(1)  TO SQW-CUR-SALES-AMT                   
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-SALES-QTY(2)  TO SQW-LAST-WK-SALES-QTY               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-SALES-AMT(2)  TO SQW-LAST-WK-SALES-AMT               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-INTRANSIT-QTY TO SQW-INTRANSIT-QTY                   
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-INTRANSIT-AMT TO SQW-INTRANSIT-AMT                   
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-ON-ORDER-QTY  TO SQW-ON-ORDER-QTY                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-ON-ORDER-AMT  TO SQW-ON-ORDER-AMT                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-STORE-OH-QTY  TO SQW-STORE-OH-QTY                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE PV-STORE-OH-AMT  TO SQW-STORE-OH-AMT                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                                                                
      * EXCLUDE GWP (GIFT WITH PURCHASE) ITEMS FROM TOTALS                      
                   IF (TSK-CUR-UNIT-RTL-AMT = 0)                                
                     CONTINUE                                                   
                   ELSE                                                         
                     ADD PV-ONHAND-QTY    TO ASC-TOT-ONHAND-QTY                 
                     ADD SQW-CUR-ONHAND-AMT(ASC-BLK-SUB ASC-ITEM-SUB)           
                                          TO ASC-TOT-ONHAND-AMT                 
                     ADD PV-SALES-QTY(1)  TO ASC-TOT-SALES-QTY                  
                     ADD PV-SALES-AMT(1)  TO ASC-TOT-SALES-AMT                  
                     ADD PV-SALES-QTY(2)  TO ASC-TOT-LAST-WK-SALES-QTY          
                     ADD PV-SALES-AMT(2)  TO ASC-TOT-LAST-WK-SALES-AMT          
                     ADD PV-INTRANSIT-QTY TO ASC-TOT-INTRANSIT-QTY              
                     ADD PV-INTRANSIT-AMT TO ASC-TOT-INTRANSIT-AMT              
                     ADD PV-ON-ORDER-QTY  TO ASC-TOT-ON-ORDER-QTY               
                     ADD PV-ON-ORDER-AMT  TO ASC-TOT-ON-ORDER-AMT               
                     ADD PV-STORE-OH-QTY  TO ASC-TOT-STORE-OH-QTY               
                     ADD PV-STORE-OH-AMT  TO ASC-TOT-STORE-OH-AMT               
                   END-IF                                                       
                                                                                
                   MOVE TSK-ITEM-NMBR    TO SQW-ITEM-NMBR                       
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE DK-STORE         TO SQW-STORE-NMBR                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                                                                
                   MOVE TSK-LONG-COLOR-DESC   TO SQW-COLOR-DESC                 
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   IF TSK-SIZE-CDE NOT EQUAL SPACES                             
                       MOVE TSK-SIZE-CDE      TO SIZE-NRF-SIZE-CDE              
                       PERFORM 5300-RETRIEVE-KOHLS-SIZE-DESC                    
                       MOVE SIZE-KOHLS-DESC   TO SQW-SIZE-DESC                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ELSE                                                         
                       IF TSK-LONG-SIZE-DESC = SPACES                           
                           MOVE TSK-SIZE-DESC TO SQW-SIZE-DESC                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                       ELSE                                                     
                           MOVE TSK-LONG-SIZE-DESC                              
                                              TO SQW-SIZE-DESC                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 5400-RETRIEVE-VENDOR-NAME                            
                   MOVE ENTNME-ENT-NAME-DESC TO SQW-VENDOR-NAME                 
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE UPC-RPLNMT-IND       TO SQW-REPLEN-IND                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                 END-IF                                                         
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
               MOVE +1               TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE 1                TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
               PERFORM 4410-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2            TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
                   PERFORM 4410-READ-TEMP-STORAGE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4330-GET-STORE-NAME.                                         
                                                                                
      *****************************************************************         
      *  WILL GET THE ON-ORDER DATA FROM TSKSTOO.                               
      *  WHEN REGIONALLY CLEARANCE PRICED AND REQUEST IS AT A CORPORATE         
      *  LEVEL (STORE = 000) THIS PROCESS IS SKIPPED - A SEPARATE CALL          
      *  IS MADE TO TDISTSM.  IF THE SKU IS REGIONALLY CLEARANCE PRICED         
      *  AND FOR A SPECIFIC STORE - A SUM IS PERFORMED AGAINST TSKSTOO.         
      *  NON-REGIONALLY CLEARANCED PRICED SKUS AT EITHER A CORPORATE OR         
      *  A SPECIFIC STORE LEVEL ARE SUMMED IN THIS PARAGRAPH.                   
      *****************************************************************         
       4305-READ-ON-ORDER-DATA.                                                 
                                                                                
           MOVE +0 TO PV-ON-ORDER-AMT                                           
                      PV-ON-ORDER-QTY.                                          
                                                                                
           IF CA-STORE-NUMBER-X = '00000'                                       
              PERFORM 4340-SUM-TSKSTOO-CORP                                     
              IF SQLCODE = +0                                                   
                  MOVE SKSTOO-ONORD-UNT-QTY TO PV-ON-ORDER-QTY                  
                  COMPUTE PV-ON-ORDER-DLR = (PV-ON-ORDER-QTY                    
                     * TSK-CUR-UNIT-RTL-AMT)                                    
                  MOVE PV-ON-ORDER-DLR TO PV-ON-ORDER-AMT                       
              ELSE                                                              
                  MOVE +0                   TO PV-ON-ORDER-QTY                  
                                               PV-ON-ORDER-AMT                  
              END-IF                                                            
           ELSE                                                                 
               MOVE CA-STORE-NUMBER-9 TO PV-STORE-NBR-COMP                      
               PERFORM 4345-SUM-TSKSTOO-STORE                                   
               IF TSK-REGN-PRIC-IND = 'Y'                                       
                  IF SQLCODE = +0                                               
                      MOVE SKSTOO-ONORD-UNT-QTY TO PV-ON-ORDER-QTY              
                      COMPUTE PV-ON-ORDER-DLR = (PV-ON-ORDER-QTY                
                         * PV-CUR-RTL-AMT)                                      
                      MOVE PV-ON-ORDER-DLR TO PV-ON-ORDER-AMT                   
                  ELSE                                                          
                      MOVE +0                   TO PV-ON-ORDER-QTY              
                                                   PV-ON-ORDER-AMT              
                  END-IF                                                        
               ELSE                                                             
                  IF SQLCODE = +0                                               
                      MOVE SKSTOO-ONORD-UNT-QTY TO PV-ON-ORDER-QTY              
                      COMPUTE PV-ON-ORDER-DLR = (PV-ON-ORDER-QTY                
                         * TSK-CUR-UNIT-RTL-AMT)                                
                      MOVE PV-ON-ORDER-DLR TO PV-ON-ORDER-AMT                   
                  ELSE                                                          
                      MOVE +0                   TO PV-ON-ORDER-QTY              
                                                   PV-ON-ORDER-AMT              
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE RECEIPT STORE-OH FILE TO GET THE NUMBER OF UNITS   *        
      *    STORE-OH TO THE STORE(S).                                   *        
      *----------------------------------------------------------------*        
       4306-READ-STORE-OH-FILE.                                                 
                                                                                
                                                                                
           INITIALIZE XL074-ITEM-COMMAREA.                                      
                                                                                
           MOVE SKXREF-SKU   TO   NI-SKU-9                                      
           SET XL074-BY-SKU               TO TRUE.                              
           MOVE NI-SKU                    TO XL074-KEY-SKU.                     
                                                                                
           IF CA-ALL-STORES                                                     
              SET XL074-BY-ALL-LOCATIONS  TO TRUE                               
              MOVE ZERO                   TO XL074-KEY-LOC-NBR                  
           ELSE                                                                 
              SET XL074-BY-STORE          TO TRUE                               
              MOVE CA-STORE-NUMBER-9      TO PV-STORE-NBR-COMP                  
              MOVE PV-STORE-NBR-COMP      TO XL074-KEY-LOC-NBR                  
           END-IF.                                                              
                                                                                
           CALL XL074-ONLINE-PGM                                                
               USING DFHEIBLK                                                   
                     XL074-COMMAREA                                             
                     XL074-ITEM-COMMAREA.                                       
                                                                                
           EVALUATE XL074-RETURN-CODE                                           
              WHEN '00'                                                         
      *----------> SUCCESSFUL                                                   
                                                                                
                IF XL074-SE-QTY            IS NEGATIVE                          
                   CONTINUE                                                     
                ELSE                                                            
                   IF CA-STORE-NUMBER-X = '00000'                               
                       COMPUTE PV-STORE-OH-QTY =                                
                           PV-ONHAND-QTY - XL074-SE-QTY                         
                       COMPUTE PV-STORE-OH-AMT =                                
                        (PV-STORE-OH-QTY * TSK-CUR-UNIT-RTL-AMT)                
                   ELSE                                                         
                       COMPUTE PV-STORE-OH-QTY =                                
                           PV-ONHAND-QTY  - XL074-SE-QTY                        
                       IF TSK-REGN-PRIC-IND = 'Y'                               
                            COMPUTE PV-STORE-OH-AMT =                           
                             (PV-STORE-OH-QTY * PV-CUR-RTL-AMT)                 
                       ELSE                                                     
                            COMPUTE PV-STORE-OH-AMT =                           
                             (PV-STORE-OH-QTY * TSK-CUR-UNIT-RTL-AMT)           
                       END-IF                                                   
                  END-IF                                                        
               END-IF                                                           
                                                                                
               MOVE XL074-IT-QTY               TO PV-INTRANSIT-QTY              
               COMPUTE PV-INTRANSIT-AMT ROUNDED =                               
                               PV-INTRANSIT-QTY * PV-CUR-RTL-AMT                
                                                                                
              WHEN OTHER                                                        
      *----------> SOME TYPE OF ERROR                                           
      *----------> ONLY ACCEPT +100 ERROR (NOT FOUND)                           
                 MOVE +0 TO PV-STORE-OH-AMT                                     
                            PV-STORE-OH-QTY                                     
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      * ---------------------------------------------------------------*        
      *  EVALUATE WHAT STATUSES WERE CHOSEN IN THE CRITERIA SCREEN.    *        
      *  IF STATUS 20 CHOSEN - CALL TDISTSM FOR THE CORPORATE '20'     *        
      *  RECORD.  IF STATUS 30 CHOSEN - CALL TDISTSM FOR THE CORPORATE *        
      *  '30' RECORD.  IF STATUS 25 CHOSEN - CALL TDISTSM FOR BOTH     *        
      *  THE '20' AND '30' STATUS RECORDS.                             *        
      *----------------------------------------------------------------*        
       4307-EVALUATE-RCP-SKU.                                                   
                                                                                
           IF (CA-STAT-20-ENTERED AND CA-STAT-30-ENTERED) OR                    
              (CA-STAT-25-ENTERED)                                              
              PERFORM 4325-SUM-TDISTSM-DATA                                     
           ELSE                                                                 
              IF CA-STAT-20-ENTERED                                             
                 MOVE PV-STATUS-CODE-20 TO PV-STATUS-CODE                       
                 PERFORM 4326-SELECT-TDISTSM-DATA                               
              ELSE                                                              
                 IF CA-STAT-30-ENTERED                                          
                    MOVE PV-STATUS-CODE-30 TO PV-STATUS-CODE                    
                    PERFORM 4326-SELECT-TDISTSM-DATA                            
                 ELSE                                                           
                    MOVE ZEROS TO      PV-ONHAND-QTY                            
                                       PV-ONHAND-AMT                            
                                       PV-ON-ORDER-QTY                          
                                       PV-ON-ORDER-AMT                          
                                       PV-STORE-OH-QTY                          
                                       PV-STORE-OH-AMT                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  EVALUATE WHAT STATUS OR STATUSES WERE CHOSEN IN THE CRITERIA  *        
      *  SCREEN - WHEN A SPECIFIC STORE IS CHOSEN AND ---->            *        
      *  -ONLY STATUS 20 CHOSEN - IF THE SKU/STORE IS ON TUPCPLS,      *        
      *                           IT IS REALLY IN A CLEARANCE STATUS.  *        
      *                           SKIP ADDING IN THIS SKU.             *        
      *  -ONLY STATUS 30 CHOSEN - IF THE SKU/STORE IS NOT ON TUPCPLS,  *        
MK    *          WRONG ---------> IT IS REALLY NOT IN A CLEARANCE      *        
MK    *          WRONG ---------> STATUS. SKIP ADDING IN THIS SKU.     *        
MK    *   BUSINESS RULE CHANGE - CANNOT ASSUME THAT THE SKU IS NOT     *        
MK    *   IN A CLEARANCE STATUS.  ADDED ADDITIONAL CHECK TO VALIDATE   *        
MK    *   THE SPECIFIC SKU STATUS.  IF THE STATUS IS A 25 THE ABOVE    *        
MK    *   ASSUMPTION IS CORRECT.  IF THE STATUS IS A 30 INCLUDE THE    *        
MK    *   SKU FOR PROCESSING.                                          *        
      *----------------------------------------------------------------*        
       4308-EVALUATE-STORE-STATUS.                                              
                                                                                
           MOVE 'N' TO PS-SKIP-SKU-SW.                                          
                                                                                
           IF CA-STORE-NUMBER-X = '00000'                                       
              CONTINUE                                                          
           ELSE                                                                 
              IF ((CA-STAT-20 = 'S') AND (CA-STAT-30 NOT = 'S')                 
                  AND (PS-CLR-STORE-FOUND))                                     
                  SET PS-SKIP-SKU-DISPLAY TO TRUE                               
              END-IF                                                            
              IF ((CA-STAT-20 NOT = 'S') AND (CA-STAT-30 = 'S')                 
                  AND (PS-CLR-STORE-NOT-FOUND))                                 
MK                IF TSK-STATUS-CODE = 25                                       
MK                   SET PS-SKIP-SKU-DISPLAY TO TRUE                            
MK                END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE TSK CURSOR.  LOOKUP THE VENDOR NAME WHEN THE      *        
      *    TSK-VNDR-IN-HSE-NMBR CHANGES.                               *        
      *----------------------------------------------------------------*        
                                                                                
       4315-FETCH-CSR-2.                                                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = ZERO                                             
                  OR SQLCODE = +100                                             
                                                                                
                        EXEC SQL                                                
                             FETCH TSK-CSR-2                                    
                             INTO   :SKXREF-DEPT                                
                                   ,:SKXREF-SUBCLASS                            
                                   ,:SKXREF-SKU                                 
                                   ,:SKXREF-INTR-UPC-ID                         
                                   ,:TSK-CUR-UNIT-RTL-AMT                       
                                   ,:TSK-STATUS-CODE                            
                                   ,:TSK-SKU-DESC                               
                                   ,:TSK-VNDR-IN-HSE-NMBR                       
                                   ,:TSK-ITEM-NMBR                              
                                   ,:TSK-SIZE-CDE                               
                                   ,:TSK-LONG-SIZE-DESC                         
                                   ,:TSK-LONG-COLOR-DESC                        
                                   ,:TSK-ENT-ID                                 
                                   ,:TSKST-ONHAND-QTY                           
                                   ,:UPC-RPLNMT-IND                             
                                   ,:TSK-REGN-PRIC-IND                          
                        END-EXEC                                                
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        MOVE TSKST-ONHAND-QTY TO PV-ONHAND-QTY                  
                   WHEN SQLCODE = +100                                          
                        MOVE ZERO             TO PV-ONHAND-QTY                  
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4315-FETCH-CSR-2'                                 
                                         TO DP013-PARAGRAPH                     
                        MOVE 'FETCH MESSAGE CURSOR'                             
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4315-FETCH-CSR-2'   TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                     
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET ASC-NO-ITEMS-LEFT-ON-DB                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
                                                                                
       EJECT                                                                    
       4320-GET-WEEKLY-SALES.                                                   
                                                                                
           MOVE PV-WKLY-DATE-KEY(PV-WK) TO DK-BOW-DTE                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR SQLCODE = -305                                             
                                                                                
                   INITIALIZE SKSTWKS-SALES-UNIT-QTY                            
                              SKSTWKS-SALES-AMT                                 
                                                                                
                   EXEC SQL                                                     
                           SELECT  SUM(SALES_UNIT_QTY)                          
                                  ,SUM(SALES_AMT)                               
                           INTO   :SKSTWKS-SALES-UNIT-QTY                       
                                 ,:SKSTWKS-SALES-AMT                            
                           FROM    TSKSTWKS                                     
                           WHERE  :TSK-ITEM-NMBR = ITEM_NMBR                    
                             AND  :DK-STORE-COMP     = STR_NBR                  
                             AND  :DK-BOW-DTE    = FSCL_BOW_DTE                 
                   END-EXEC                                                     
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
      *-->         A "-305" OCCURS WHEN DOING A DB2 BUILT-IN FUNCTION           
      *-->         (I.E. "SUM") AND A NOT-FOUND OCCURS.                         
                   WHEN SQLCODE = -305                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4320-GET-WEEKLY-SALES'                            
                                         TO DP013-PARAGRAPH                     
                        MOVE 'SELECT TSKSTWKS'                                  
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4320-GET-WEEKLY-SALES'                                     
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                     
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               COMPUTE PV-SALES-QTY(PV-WK) ROUNDED =                            
                   SKSTWKS-SALES-UNIT-QTY * 1                                   
               COMPUTE PV-SALES-AMT(PV-WK) ROUNDED =                            
                   SKSTWKS-SALES-AMT * 1                                        
           ELSE                                                                 
               MOVE +0             TO PV-SALES-QTY(PV-WK)                       
               MOVE +0             TO PV-SALES-AMT(PV-WK)                       
           END-IF.                                                              
                                                                                
           EJECT                                                                
       4325-SUM-TDISTSM-DATA.                                                   
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR SQLCODE = -305                                             
                                                                                
                   INITIALIZE DCLTDISTSM                                        
                                                                                
                   EXEC SQL                                                     
                           SELECT  SUM(OH_QTY)                                  
                                  ,SUM(OH_AMT)                                  
                                  ,SUM(ONORD_QTY)                               
                                  ,SUM(ONORD_AMT)                               
                                  ,SUM(ENTRE_QTY)                               
                                  ,SUM(ENTRE_AMT)                               
                                  ,SUM(IN_TRNST_QTY)                            
                                  ,SUM(IN_TRNST_AMT)                            
                           INTO   :DISTSM-OH-QTY                                
                                 ,:DISTSM-OH-AMT                                
                                 ,:DISTSM-ONORD-QTY                             
                                 ,:DISTSM-ONORD-AMT                             
                                 ,:DISTSM-ENTRE-QTY                             
                                 ,:DISTSM-ENTRE-AMT                             
                                 ,:DISTSM-IN-TRNST-QTY                          
                                 ,:DISTSM-IN-TRNST-AMT                          
                           FROM    TDISTSM                                      
MC0502                     WHERE  ITM_NBR     = :TSK-ITEM-NMBR                  
                             AND  STAT_CDE     IN ('20','30')                   
                             AND  SUM_LVL_CDE  = :PV-SUM-LVL-ALL                
                             AND  DIST_NBR     = :PV-BLANK-DISTRICT             
                   END-EXEC                                                     
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
      *-->         A "-305" OCCURS WHEN DOING A DB2 BUILT-IN FUNCTION           
      *-->         (I.E. "SUM") AND A NOT-FOUND OCCURS.                         
                   WHEN SQLCODE = -305                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4325-SUM-TDISTSM-DATA'                            
                                         TO DP013-PARAGRAPH                     
                        MOVE 'SELECT TDISTSM'                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4325-SUM-TDISTSM-DATA'                                     
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               MOVE DISTSM-OH-QTY     TO  PV-ONHAND-QTY                         
               MOVE DISTSM-OH-AMT     TO  PV-ONHAND-AMT                         
               MOVE DISTSM-ONORD-QTY  TO  PV-ON-ORDER-QTY                       
               MOVE DISTSM-ONORD-AMT  TO  PV-ON-ORDER-AMT                       
               MOVE DISTSM-ENTRE-QTY  TO  PV-STORE-OH-QTY                       
               MOVE DISTSM-ENTRE-AMT  TO  PV-STORE-OH-AMT                       
               MOVE DISTSM-IN-TRNST-QTY                                         
                                      TO  PV-INTRANSIT-QTY                      
               MOVE DISTSM-IN-TRNST-AMT                                         
                                      TO  PV-INTRANSIT-AMT                      
           ELSE                                                                 
               MOVE +0                TO  PV-ONHAND-QTY                         
                                          PV-ONHAND-AMT                         
                                          PV-ON-ORDER-QTY                       
                                          PV-ON-ORDER-AMT                       
                                          PV-STORE-OH-QTY                       
                                          PV-STORE-OH-AMT                       
                                          PV-INTRANSIT-QTY                      
                                          PV-INTRANSIT-AMT                      
           END-IF.                                                              
                                                                                
           EJECT                                                                
       4326-SELECT-TDISTSM-DATA.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                                                                                
                   INITIALIZE DCLTDISTSM                                        
                                                                                
                   EXEC SQL                                                     
                           SELECT  OH_QTY                                       
                                  ,OH_AMT                                       
                                  ,ONORD_QTY                                    
                                  ,ONORD_AMT                                    
                                  ,ENTRE_QTY                                    
                                  ,ENTRE_AMT                                    
                                  ,IN_TRNST_QTY                                 
                                  ,IN_TRNST_AMT                                 
                           INTO   :DISTSM-OH-QTY                                
                                 ,:DISTSM-OH-AMT                                
                                 ,:DISTSM-ONORD-QTY                             
                                 ,:DISTSM-ONORD-AMT                             
                                 ,:DISTSM-ENTRE-QTY                             
                                 ,:DISTSM-ENTRE-AMT                             
                                 ,:DISTSM-IN-TRNST-QTY                          
                                 ,:DISTSM-IN-TRNST-AMT                          
                           FROM    TDISTSM                                      
MC0502                     WHERE  ITM_NBR      = :TSK-ITEM-NMBR                 
                             AND  STAT_CDE     = :PV-STATUS-CODE                
                             AND  SUM_LVL_CDE  = :PV-SUM-LVL-ALL                
                             AND  DIST_NBR     = :PV-BLANK-DISTRICT             
                   END-EXEC                                                     
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4326-SELECT-TDISTSM-DATA'                         
                                         TO DP013-PARAGRAPH                     
                        MOVE 'SELECT TDISTSM'                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4326-SELECT-TDISTSM-DATA'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               MOVE DISTSM-OH-QTY     TO  PV-ONHAND-QTY                         
               MOVE DISTSM-OH-AMT     TO  PV-ONHAND-AMT                         
               MOVE DISTSM-ONORD-QTY  TO  PV-ON-ORDER-QTY                       
               MOVE DISTSM-ONORD-AMT  TO  PV-ON-ORDER-AMT                       
               MOVE DISTSM-ENTRE-QTY  TO  PV-STORE-OH-QTY                       
               MOVE DISTSM-ENTRE-AMT  TO  PV-STORE-OH-AMT                       
               MOVE DISTSM-IN-TRNST-QTY                                         
                                      TO  PV-INTRANSIT-QTY                      
               MOVE DISTSM-IN-TRNST-AMT                                         
                                      TO  PV-INTRANSIT-AMT                      
           ELSE                                                                 
               MOVE +0                TO  PV-ONHAND-QTY                         
                                          PV-ONHAND-AMT                         
                                          PV-ON-ORDER-QTY                       
                                          PV-ON-ORDER-AMT                       
                                          PV-STORE-OH-QTY                       
                                          PV-STORE-OH-AMT                       
                                          PV-INTRANSIT-QTY                      
                                          PV-INTRANSIT-AMT                      
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
       4328-SELECT-TUPCPLS-DATA.                                                
                                                                                
           MOVE 'N' TO PS-CLR-STORE-SW.                                         
           MOVE CA-STORE-NUMBER-9 TO PV-STORE-NUMBER.                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                                                                                
                   INITIALIZE DCLTUPCPLS                                        
                                                                                
                   EXEC SQL                                                     
                           SELECT  UNT_RTL_AMT                                  
                           INTO   :UPCPLS-UNT-RTL-AMT                           
                           FROM    TUPCPLS                                      
                           WHERE  OPERCO_NBR   = '03'                           
                             AND  ITM_NBR      = :TSK-ITEM-NMBR                 
                             AND  STR_NBR      = :PV-STORE-NUMBER               
                             AND ((EFF_BEG_TMST <= CURRENT TIMESTAMP            
                             AND EFF_END_TMST >= CURRENT TIMESTAMP)             
                              OR (EFF_BEG_TMST <= CURRENT TIMESTAMP             
                             AND EFF_END_TMST IS NULL))                         
                   END-EXEC                                                     
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '4328-SELECT-TUPCPLS-DATA'                         
                                         TO DP013-PARAGRAPH                     
                        MOVE 'SELECT TUPCPLS'                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4328-SELECT-TUPCPLS-DATA'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               SET PS-CLR-STORE-FOUND TO TRUE                                   
               MOVE UPCPLS-UNT-RTL-AMT TO PV-CUR-RTL-AMT                        
               MOVE PV-CUR-RTL-AMT     TO PV-CUR-RTL-FMT                        
           ELSE                                                                 
               SET PS-CLR-STORE-NOT-FOUND TO TRUE                               
               MOVE TSK-CUR-UNIT-RTL-AMT TO PV-CUR-RTL-AMT                      
               MOVE PV-CUR-RTL-AMT     TO PV-CUR-RTL-FMT                        
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
       4330-GET-STORE-NAME.                                                     
                                                                                
           IF  DK-STORE NOT = ZERO                                              
               PERFORM 4335-GET-STORE                                           
           ELSE                                                                 
               MOVE PC-CORPORATE-LITERAL TO ASTDESCO                            
                                            CA-STORE-NAME                       
                                            CA-STORE-SHORT-NAME                 
               MOVE '00000'              TO CA-STORE-NUMBER-X                   
               MOVE DP015-HL-OFF         TO ASTDESCH                            
               MOVE DP015-BLUE           TO ASTDESCC                            
               MOVE DP015-ASK-NOR-OFF    TO ASTDESCA                            
           END-IF.                                                              
                                                                                
           EJECT                                                                
       4335-GET-STORE.                                                          
      ******************************************************************        
      * ACCESS THE STORE DB2 TABLE TO GET THE NAME OF THE ENTERED STORE         
      * NUMBER                                                                  
      ******************************************************************        
                                                                                
           MOVE DP015-HL-OFF               TO ASTDESCH.                         
           MOVE DP015-BLUE                 TO ASTDESCC.                         
           MOVE DP015-ASK-NOR-OFF          TO ASTDESCA.                         
           MOVE SPACES                     TO ASTDESCI                          
                                              CA-STORE-NAME                     
                                              CA-STORE-SHORT-NAME               
                                                                                
           MOVE DK-STORE             TO DK-STORE-9                              
           MOVE DK-STORE-9           TO PV-STORE-NBR-COMP                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE NOT = +0                                           
                                                                                
                   EXEC SQL                                                     
                       SELECT LOC_NM                                            
                             ,LOC_10_ABBR_NM                                    
                       INTO  :XMT00001-LOC-NM                                   
                            ,:XMT00001-LOC-10-ABBR-NM                           
                       FROM   XMT_LOC                                           
                       WHERE  LOC_NBR    = :PV-STORE-NBR-COMP                   
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                          MOVE XMT00001-LOC-10-ABBR-NM                          
                                                  TO CA-STORE-SHORT-NAME        
                          MOVE XMT00001-LOC-NM    TO CA-STORE-NAME              
                                                     ASTDESCO                   
                       WHEN SQLCODE  NOT = 0                                    
                       WHEN SQLWARN0 NOT = SPACES                               
                            MOVE '4330-GET-STORE-NAME'                          
                                             TO DP013-PARAGRAPH                 
                            MOVE 'SELECT XMT_LOC'                               
                                             TO DP013-MESSAGE-TEXT (1)          
                            MOVE SQLCA       TO DP013-SQLCA                     
                            SET DP013-DB2-ABEND                                 
                                             TO TRUE                            
                            PERFORM DP013-0000-PROCESS-ABEND                    
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4335-GET-STORE-NAME'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * ACCESS THE TSKSTOO DB2 TABLE TO GET THE ON-ORDER QUANTITY               
      ******************************************************************        
       4340-SUM-TSKSTOO-CORP.                                                   
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR SQLCODE = -305                                             
                                                                                
                   INITIALIZE DCLTSKSTOO                                        
                                                                                
                   EXEC SQL                                                     
                        SELECT SUM(ONORD_UNT_QTY)                               
                          INTO :SKSTOO-ONORD-UNT-QTY                            
                          FROM TSKSTOO                                          
                         WHERE ITM_NBR = :TSK-ITEM-NMBR                         
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                       WHEN SQLCODE = +100                                      
                       WHEN SQLCODE = -305                                      
                            CONTINUE                                            
                       WHEN SQLWARN0 NOT EQUAL SPACE                            
                       WHEN SQLCODE NOT EQUAL ZERO                              
                            MOVE '4340-SUM-TSKSTOO-CORP'                        
                                             TO DP013-PARAGRAPH                 
                            MOVE 'SELECT TSKSTOO'                               
                                             TO DP013-MESSAGE-TEXT (1)          
                            MOVE SQLCA       TO DP013-SQLCA                     
                            SET DP013-DB2-ABEND                                 
                                             TO TRUE                            
                            PERFORM DP013-0000-PROCESS-ABEND                    
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4340-SUM-TSKSTOO-CORP'                                     
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SUM'                              
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
           EJECT                                                                
      ******************************************************************        
      * ACCESS THE TSKSTOO DB2 TABLE TO GET THE ON-ORDER QUANTITY               
      ******************************************************************        
       4345-SUM-TSKSTOO-STORE.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR SQLCODE = -305                                             
                                                                                
                   INITIALIZE DCLTSKSTOO                                        
                                                                                
                   EXEC SQL                                                     
                        SELECT SUM(ONORD_UNT_QTY)                               
                          INTO :SKSTOO-ONORD-UNT-QTY                            
                          FROM TSKSTOO                                          
                         WHERE ITM_NBR = :TSK-ITEM-NMBR                         
                           AND STR_NBR = :PV-STORE-NBR-COMP                     
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                       WHEN SQLCODE = +100                                      
                       WHEN SQLCODE = -305                                      
                            CONTINUE                                            
                       WHEN SQLWARN0 NOT EQUAL SPACE                            
                       WHEN SQLCODE NOT EQUAL ZERO                              
                            MOVE '4345-SUM-TSKSTOO-STORE'                       
                                             TO DP013-PARAGRAPH                 
                            MOVE 'SELECT TSKSTOO'                               
                                             TO DP013-MESSAGE-TEXT (1)          
                            MOVE SQLCA       TO DP013-SQLCA                     
                            SET DP013-DB2-ABEND                                 
                                             TO TRUE                            
                            PERFORM DP013-0000-PROCESS-ABEND                    
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '4345-SUM-TSKSTOO-STORE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SUM'                              
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
MC0502*--> DISPLAY THE HEADINGS FOR THE VARIOUS VIEWS                           
                                                                                
MC0502     EVALUATE TRUE                                                        
MC0502         WHEN ASC-SALES-VIEW                                      PMKCS239
                  IF ASC-DISPLAY-UNITS                                          
                      MOVE HL-SALES-LN-1       TO AHEADR1O                 CL**3
                      MOVE HL-SALES-LN-2       TO AHEADR2O                 CL**3
                  ELSE                                                          
                      MOVE HL-SALES-LN-1       TO AHEADR1O                      
                      MOVE HL-SALES-LN-2-DOL   TO AHEADR2O                      
                   END-IF                                                       
MC0502          WHEN ASC-RIGHT-VIEW                                             
MC0502            IF ASC-DISPLAY-UNITS                                          
MC0502                MOVE HL-SALES-RT-LN-1       TO AHEADR1O              CL**3
MC0502                MOVE HL-SALES-RT-LN-2       TO AHEADR2O              CL**3
MC0502            ELSE                                                          
MC0502                MOVE HL-SALES-RT-LN-1       TO AHEADR1O                   
MC0502                MOVE HL-SALES-RT-LN-2-DOL   TO AHEADR2O                   
MC0502            END-IF                                                        
MC0502          WHEN ASC-COLOR-SIZE-VIEW                                        
                  MOVE SPACES                  TO AHEADR1O                 CL**3
                  MOVE HL-COLOR-SIZE-LN-1      TO AHEADR2O                 CL**3
MC0502     END-EVALUATE.                                                PMKCS239
                                                                        PMKCS239
           MOVE CA-DEPT-NUMBER-X            TO ADEPTO.                          
           MOVE CA-DEPT-NAME                TO ADPTDSCO.                        
           MOVE CA-STORE-NUMBER-X           TO ASTOREO.                         
           MOVE CA-STORE-NAME               TO ASTDESCO.                        
                                                                                
           IF CA-COLOR-1-X = SPACES AND                                         
              CA-COLOR-2-X = SPACES AND                                         
              CA-TSK-LONG-COLOR =  SPACES                                       
              MOVE PC-ALL-COLS TO ACOLORO                                       
              MOVE PC-COLOR-000 TO ACOLOR1O                                     
              MOVE PC-COLOR-999 TO ACOLOR2O                                     
           ELSE                                                                 
              IF CA-TSK-LONG-COLOR =  SPACES                                    
                 MOVE CA-COLOR-1-X          TO ACOLOR1O                         
                 MOVE CA-COLOR-2-X          TO ACOLOR2O                         
                 MOVE CA-COLOR-DESC         TO ACOLORO                          
              ELSE                                                              
                 MOVE CA-TSK-LONG-COLOR     TO ACOLORO                          
              END-IF                                                            
           END-IF.                                                              
           IF CA-SIZE-DESC = SPACES                                             
              MOVE PC-ALL-SIZES             TO ASIZEO                           
           ELSE                                                                 
              MOVE CA-SIZE-DESC             TO ASIZEO.                          
                                                                                
           IF  (((PV-TOP-ITEM > SQW-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > SQW-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < SQW-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1     TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER                                   
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1 TO PV-BLOCK-NUMBER                                
                   END-IF                                                       
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   ADD +1     TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
           END-IF.                                                              
           MOVE +1            TO ASC-BLK-SUB.                                   
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - SQW-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
                                                                                
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1         TO ASC-BLK-SUB                                    
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
                                                                                
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
           PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
           PERFORM 4420-FORMAT-LIST-LINES                                       
               VARYING PV-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PV-SUB < +1.                                               
                                                                                
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
                                            ASTRTITO.                           
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO ASELITO.                            
                                                                                
           IF ASC-SALES-VIEW                                            PMKCS239
               IF ASC-DISPLAY-UNITS                                             
                   IF ASC-TOT-ONHAND-QTY > +99999999 OR                         
                      ASC-TOT-ONHAND-QTY < -99999999                            
                       MOVE '#########'           TO TL-CURR-ONHAND-C           
                   ELSE                                                         
                       MOVE ASC-TOT-ONHAND-QTY    TO TL-CURR-ONHAND-9           
                   END-IF                                                       
                   IF ASC-TOT-SALES-QTY > +9999999 OR                           
                      ASC-TOT-SALES-QTY < -9999999                              
                       MOVE '########'            TO TL-CURR-SALES-C            
                   ELSE                                                         
                       MOVE ASC-TOT-SALES-QTY     TO TL-CURR-SALES-9            
                   END-IF                                                       
                   IF ASC-TOT-LAST-WK-SALES-QTY > +9999999 OR                   
                      ASC-TOT-LAST-WK-SALES-QTY < -9999999                      
                       MOVE '########'            TO TL-LAST-WK-SALES-C         
                   ELSE                                                         
                       MOVE ASC-TOT-LAST-WK-SALES-QTY                           
                                                  TO TL-LAST-WK-SALES-9         
                   END-IF                                                       
                   IF ASC-TOT-ON-ORDER-QTY > 9999999                            
                       MOVE '#######'             TO TL-ON-ORDER-C              
                   ELSE                                                         
                       MOVE ASC-TOT-ON-ORDER-QTY  TO TL-ON-ORDER-9              
                   END-IF                                                       
               ELSE                                                             
                   IF ASC-TOT-ONHAND-AMT > +99999999 OR                         
                      ASC-TOT-ONHAND-AMT < -99999999                            
                       MOVE '#########'           TO TL-CURR-ONHAND-C           
                   ELSE                                                         
                       MOVE ASC-TOT-ONHAND-AMT    TO TL-CURR-ONHAND-9           
                   END-IF                                                       
                   IF ASC-TOT-SALES-AMT > +9999999 OR                           
                      ASC-TOT-SALES-AMT < -9999999                              
                       MOVE '########'            TO TL-CURR-SALES-C            
                   ELSE                                                         
                       MOVE ASC-TOT-SALES-AMT     TO TL-CURR-SALES-9            
                   END-IF                                                       
                   IF ASC-TOT-LAST-WK-SALES-AMT > +9999999 OR                   
                      ASC-TOT-LAST-WK-SALES-AMT < -9999999                      
                       MOVE '########'            TO TL-LAST-WK-SALES-C         
                   ELSE                                                         
                       MOVE ASC-TOT-LAST-WK-SALES-AMT                           
                                                  TO TL-LAST-WK-SALES-9         
                   END-IF                                                       
                   IF ASC-TOT-ON-ORDER-AMT > 9999999                            
                       MOVE '#######'             TO TL-ON-ORDER-C              
                   ELSE                                                         
                       MOVE ASC-TOT-ON-ORDER-AMT  TO TL-ON-ORDER-9              
                   END-IF                                                       
               END-IF                                                           
               MOVE TL-TOTALS-LINE                TO ATOTLINO                   
MC0502     ELSE                                                                 
MC0502        IF ASC-RIGHT-VIEW                                         PMKCS239
MC0502            IF ASC-DISPLAY-UNITS                                          
                      IF ASC-TOT-INTRANSIT-QTY > +999999 OR                     
                         ASC-TOT-INTRANSIT-QTY < -999999                        
                          MOVE '#######'          TO TL-INTRANSIT-C             
                      ELSE                                                      
                          MOVE ASC-TOT-INTRANSIT-QTY                            
                                                  TO TL-INTRANSIT-9             
                      END-IF                                                    
                      MOVE ASC-TOT-STORE-OH-QTY   TO TL-STORE-OH-9              
MC0502            ELSE                                                          
                     IF ASC-TOT-INTRANSIT-AMT > +999999 OR                      
                        ASC-TOT-INTRANSIT-AMT < -999999                         
                        MOVE '#######'            TO TL-INTRANSIT-C             
                     ELSE                                                       
                        MOVE ASC-TOT-INTRANSIT-AMT                              
                                                  TO TL-INTRANSIT-9             
MC0502                  MOVE ASC-TOT-STORE-OH-AMT TO TL-STORE-OH-9              
                     END-IF                                                     
MC0502        END-IF                                                            
MC0502        MOVE TL-TOTALS-LINE                 TO ATOTLINO                   
           ELSE                                                                 
               MOVE SPACES                        TO ATOTLINO                   
           END-IF.                                                              
                                                                                
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
      *            --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
       4410-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (SQW-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4410-READ-TEMP-STORAGE'                                    
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET SQW-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE SQW-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *----------------------------------------------------------------*        
                                                                                
       4420-FORMAT-LIST-LINES.                                                  
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
               MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
                                          MR-SKU    (PV-SUB)                    
                                          MR-CLASS  (PV-SUB)                    
                                          MR-SKU-DESC  (PV-SUB)                 
                                          MR-CUR-RTL  (PV-SUB)                  
                                          MR-SCROLLABLE-AREA(PV-SUB)            
           ELSE                                                                 
               IF  SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                                         > SPACE                
                   MOVE SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-SELECTI (PV-SUB)                   
               ELSE                                                             
                   MOVE SQW-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                       TO MR-SELECTI (PV-SUB)                   
               END-IF                                                           
                                                                                
               IF  DP030-SET-CURSOR-TRAILER                                     
                   IF  ((SQW-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
                       > SPACE)                                                 
                     OR (PV-SUB = 1))                                           
                      SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
                      MOVE -1          TO MR-SELECTL (PV-SUB)                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF (PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL) OR               
      *-->         UNIT/DOLLAR KEY HIT                                          
                   DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID) OR                     
      *-->         SALES KEY HIT                                                
                   DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID) OR                     
MC0502*-->         LEFT KEY HIT                                                 
                   DP020-FK-LEFT(DP020-SRC-AID)          OR                     
MC0502*-->         RIGHT KEY HIT                                                
                   DP020-FK-RIGHT(DP020-SRC-AID)         OR                     
      *-->         CLR/SIZE KEY HIT                                             
                   DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                        
                   MOVE SQW-CLASS-NMBR (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-CLASS (PV-SUB)                     
                   MOVE SQW-SKU-NMBR   (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-SKU   (PV-SUB)                     
                   MOVE SQW-SKU-DESC   (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-SKU-DESC (PV-SUB)                  
                   MOVE SQW-CUR-RTL    (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-CUR-RTL   (PV-SUB)                 
                   MOVE SPACES         TO DL-DETAIL-LINE                        
                   IF ASC-SALES-VIEW                                    PMKCS239
                       IF ASC-DISPLAY-UNITS                                     
                           MOVE SQW-CUR-ONHAND-QTY                              
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-CURR-ONHAND                      
                           MOVE SQW-CUR-SALES-QTY                               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-CURR-SALES                       
                           MOVE SQW-LAST-WK-SALES-QTY                           
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-LAST-WK-SALES                    
                           MOVE SQW-ON-ORDER-QTY                                
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-ON-ORDER                         
                   ELSE                                                         
                           MOVE SQW-CUR-ONHAND-AMT                              
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-CURR-ONHAND                      
                           MOVE SQW-CUR-SALES-AMT                               
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-CURR-SALES                       
                           MOVE SQW-LAST-WK-SALES-AMT                           
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-LAST-WK-SALES                    
                           MOVE SQW-ON-ORDER-AMT                                
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-ON-ORDER                         
                   END-IF                                                       
MC0502          ELSE                                                            
MC0502             IF ASC-RIGHT-VIEW                                    PMKCS239
MC0502                 IF ASC-DISPLAY-UNITS                                     
MC0502                     MOVE SQW-INTRANSIT-QTY                               
MC0502                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
MC0502                                   TO DL-INTRANSIT                        
MC0502                     MOVE SQW-STORE-OH-QTY                                
MC0502                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
MC0502                                   TO DL-STORE-OH                         
MC0502                 ELSE                                                     
MC0502                     MOVE SQW-INTRANSIT-AMT                               
MC0502                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
MC0502                                   TO DL-INTRANSIT                        
MC0502                     MOVE SQW-STORE-OH-AMT                                
MC0502                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
MC0502                                   TO DL-STORE-OH                         
MC0502                 END-IF                                                   
                   ELSE                                                         
                       MOVE SQW-COLOR-DESC   (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-COLOR-DESC                       
                       MOVE SQW-SIZE-DESC    (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-SIZE-DESC                        
                       MOVE SQW-VENDOR-NAME  (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-VENDOR-NAME                      
                       MOVE SQW-REPLEN-IND   (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO DL-REPLEN-IND                       
                   END-IF                                                       
               END-IF                                                           
               MOVE DL-DETAIL-LINE   TO MR-SCROLLABLE-AREA (PV-SUB)     PMKCS239
           END-IF.                                                              
                                                                                
           SUBTRACT +1 FROM ASC-ITEM-SUB                                        
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
      ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
      ** ISSUE A WARNING.                                             **        
      ******************************************************************        
       4430-CHECK-FOR-BEGIN-AND-END.                                            
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
      *            -- NO END-OF-RANGE SPECFIED ---                              
                   MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
      *            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
                   MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
      ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
      ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
      ** POSITION THE CURSOR THERE.                                   **        
      ******************************************************************        
       4431-POSITION-AT-WARNING.                                                
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
      * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
      * FOR THE SELECTED ITEMS QUEUE.                                  *        
      *                                                                *        
      * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
      * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
      * THE ARRAY IS FULL.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
                                                                                
           PERFORM 6010-DELETE-SEL-QUEUE.                                       
                                                                                
           SET ASC-UPDATE-TSQ        TO TRUE.                                   
                                                                                
           MOVE ZERO                 TO CA-NUMBER-OF-TS-ITEMS.                  
           INITIALIZE FS002-TS-SEL-REC.                                         
                                                                                
           SET FS002-IDX             TO 1.                                      
           MOVE 20                   TO FS002-MAX-ITEMS                         
                                                                                
           PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
                                                                                
           IF  FS002-IDX > 1                                                    
               PERFORM 5205-WRITE-SEL-QUEUE                                     
               ADD +1                TO CA-NUMBER-OF-TS-ITEMS                   
           END-IF.                                                              
                                                                                
           IF DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)                             
              MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                              
              MOVE ASC-SEL-QUEUE-NAME TO CA-SEL-QUEUE-NAME                      
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       5100-WRITE-TEMP-STORAGE.                                                 
           MOVE SQW-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  SQW-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 5105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 5110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET SQW-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5105-REWRITE-ASC-LISTQ.                                                  
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (SQW-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5110-WRITE-ASC-LISTQ.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (SQW-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       5200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 6112-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM                                                          
               VARYING LQW-IDX                                                  
               FROM 1 BY 1                                                      
               UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
                 OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
                                         TO FS002-ITEM (FS002-IDX)              
                       MOVE CA-DEPT-NUMBER-X                                    
                                         TO FS002-DEPT-NMBR (FS002-IDX)         
                       MOVE LQW-CLASS-NMBR     (LQW-IDX)                        
                                         TO FS002-CLASS-NMBR                    
                                              (FS002-IDX)                       
                       MOVE LQW-SKU-NMBR       (LQW-IDX)                        
                                         TO FS002-SKU-NMBR                      
                                              (FS002-IDX)                       
                       MOVE LQW-STORE-NMBR     (LQW-IDX)                        
                                         TO FS002-STORE-NMBR                    
                                              (FS002-IDX)                       
                       MOVE LQW-ITEM-NMBR      (LQW-IDX)                        
                                         TO FS002-ITEM-NMBR                     
                                              (FS002-IDX)                       
                       IF  FS002-IDX = 1                                        
      *-->                 UPDATE CRITERA QUEUE W/ THE 1ST ITEM'S INFO          
                           PERFORM 5210-UPDATE-CRITERIA-QUEUE                   
                       END-IF                                                   
                       SET FS002-IDX UP BY 1                                    
                       IF  FS002-IDX > FS002-MAX-ITEMS                          
                           PERFORM 5205-WRITE-SEL-QUEUE                         
                           ADD +1        TO CA-NUMBER-OF-TS-ITEMS               
                           INITIALIZE FS002-SELECTED-ITEM-ARRAY                 
                           SET FS002-IDX TO  1                                  
                       END-IF                                                   
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
      *----------------------------------------------------------------*        
                                                                                
       5205-WRITE-SEL-QUEUE.                                                    
                                                                                
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO FS002-SELECTED-ITEMS.               
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    FROM  (FS002-TS-SEL-REC)                                    
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5205-WRITE-SEL-QUEUE'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       5210-UPDATE-CRITERIA-QUEUE.                                              
                                                                                
      *--> FOR THE FIRST SELECTED ITEM, MOVE IT'S DATA TO THE                   
      *--> CA-TSK FIELDS FOR DISPLAY IN FSKCS032'S HEADER INFO                  
                                                                                
           MOVE LQW-CLASS-NMBR (LQW-IDX) TO CA-CLASS-NUMBER-X                   
MC0502     MOVE CA-CLASS-NUMBER-9        TO NI-CLASS-9-4                        
           MOVE LQW-SKU-NMBR   (LQW-IDX) TO CA-SKU-NUMBER-X                     
MC0502     MOVE CA-SKU-NUMBER-9          TO NI-SKU-9-8                          
           MOVE LQW-SKU-DESC   (LQW-IDX) TO CA-SKU-DESCRIPTION                  
                                                                                
           INITIALIZE CA-TSK-DATA                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = +0                                               
                  OR SQLCODE NOT = +0                                           
                                                                                
               EXEC SQL                                                         
MC0502             SELECT  DEPT                                                 
MC0502                    ,SUBCLASS                                             
MC0502                    ,SKU                                                  
                          ,CUR_UNIT_RTL_AMT                                     
                          ,GP_RTL_AMT                                           
                          ,GP_QTY                                               
                          ,UNIT_COST_AMT                                        
                          ,ORIG_UNIT_RTL_AMT                                    
                          ,SIZE_DESC                                            
                          ,COLOR_DESC                                           
                          ,SIZE_CDE                                             
                          ,LONG_SIZE_DESC                                       
                          ,LONG_COLOR_DESC                                      
                          ,ITEM_NMBR                                            
                          ,REGN_PRIC_IND                                        
MC0502             INTO   :SKXREF-DEPT                                          
MC0502                   ,:SKXREF-SUBCLASS                                      
MC0502                   ,:SKXREF-SKU                                           
                         ,:TSK-CUR-UNIT-RTL-AMT                                 
                         ,:TSK-GP-RTL-AMT                                       
                         ,:TSK-GP-QTY                                           
                         ,:TSK-UNIT-COST-AMT                                    
                         ,:TSK-ORIG-UNIT-RTL-AMT                                
                         ,:TSK-SIZE-DESC                                        
                         ,:TSK-COLOR-DESC                                       
                         ,:TSK-SIZE-CDE                                         
                         ,:TSK-LONG-SIZE-DESC                                   
                         ,:TSK-LONG-COLOR-DESC                                  
                         ,:TSK-ITEM-NMBR                                        
                         ,:TSK-REGN-PRIC-IND                                    
MC0502             FROM    TSK T                                                
MC0502             JOIN    TSKXREF X                                            
MC0502               ON    T.ITEM_NMBR = X.ITM_NBR                              
MC0502             WHERE   SKU              = :NI-SKU-9-8                       
MC0502               AND   SKU_STAT_CDE    ^= '00'                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       IF TSK-REGN-PRIC-IND = 'Y'                               
                          IF CA-STORE-NUMBER-X = '00000'                        
                             MOVE 'MIXED' TO CA-TSK-CUR-RTL-X                   
                          ELSE                                                  
                             PERFORM 4328-SELECT-TUPCPLS-DATA                   
                             MOVE PV-CUR-RTL-FMT TO CA-TSK-CUR-RTL              
                          END-IF                                                
                       ELSE                                                     
                          MOVE TSK-CUR-UNIT-RTL-AMT  TO CA-TSK-CUR-RTL          
                       END-IF                                                   
                                                                                
                       MOVE TSK-GP-RTL-AMT        TO CA-TSK-GP-RTL              
                       MOVE TSK-GP-QTY            TO CA-TSK-GP-QTY              
                       MOVE TSK-UNIT-COST-AMT     TO CA-TSK-COST                
                       MOVE TSK-ORIG-UNIT-RTL-AMT TO CA-TSK-ORG-RTL             
                                                                                
                       IF TSK-SIZE-CDE NOT EQUAL SPACES                         
                           MOVE TSK-SIZE-CDE      TO SIZE-NRF-SIZE-CDE          
                           PERFORM 5300-RETRIEVE-KOHLS-SIZE-DESC                
                           MOVE SIZE-KOHLS-DESC   TO   CA-TSK-SIZE              
                       ELSE                                                     
                           IF TSK-LONG-SIZE-DESC = SPACES                       
                               MOVE TSK-SIZE-DESC TO   CA-TSK-SIZE              
                           ELSE                                                 
                               MOVE TSK-LONG-SIZE-DESC TO   CA-TSK-SIZE         
                           END-IF                                               
                       END-IF                                                   
                                                                                
                       IF TSK-LONG-COLOR-DESC = SPACES                          
                          MOVE TSK-COLOR-DESC        TO   CA-TSK-COLOR          
                       ELSE                                                     
                          MOVE TSK-LONG-COLOR-DESC   TO   CA-TSK-COLOR          
                       END-IF                                                   
                                                                                
                   WHEN SQLCODE  NOT = +0                                       
                   WHEN SQLWARN0 NOT = SPACES                                   
                        MOVE '5210-UPDATE-CRITERIA'                             
                                         TO DP013-PARAGRAPH                     
                        MOVE 'SELECT TSK'                                       
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '5210-UPDATE-CRITERIA'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED FOR SELECT'                           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               SET DP013-DB2-ABEND       TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE '/'                   TO CA-TSK-GP-SLASH                        
                                                                                
      **   USE F034'S QUEUE NAME FOR THE DELETEQ                                
                                                                                
           MOVE ASC-CRIT-QUEUE-NAME   TO CA-SEL-QUEUE-NAME.                     
                                                                                
      **   DELETE THE QUEUE ASSOCIATED W/ F034'S TASK NUMBER, AND               
      **   WRITE A NEW ONE.  THAT WILL ASSURE THAT ALL QUEUES READ              
      **   BY THE DETAIL PROGRAM (F032) WILL HAVE THE SAME TASK NUMBER          
      **   MAKEUP IN THEIR QUEUE NAMES.                                         
                                                                                
      *    EXEC CICS                                                            
      *         DELETEQ TS                                                      
      *             QUEUE (CA-SEL-QUEUE-NAME)                                   
      *             RESP  (PV-CICS-RESP)                                        
      *    END-EXEC.                                                            
      *                                                                         
      *    IF  PV-CICS-RESP = DFHRESP (NORMAL) OR                               
      *                       DFHRESP (QIDERR)                                  
      *        CONTINUE                                                         
      *    ELSE                                                                 
      *        SET DP013-CICS-ABEND      TO  TRUE                               
      *        SET DP013-NO-ROLLBACK     TO  TRUE                               
      *        MOVE '5210-WRITE-TS-QUEUE' TO  DP013-PARAGRAPH                   
      *        MOVE 'ATTEMPTING TO DELETE CRITERIA QUEUE'                       
      *                                  TO  DP013-MESSAGE-TEXT(1)              
      *        PERFORM DP013-0000-PROCESS-ABEND                                 
      *    END-IF.                                                              
                                                                                
      **   NOW LOAD THE NEW QUEUE NAME TO REFLECT THE MOST RECENT               
      **   DP020-SRC-TASK-NUMBER                                                
                                                                                
           MOVE ASC-SEL-QUEUE-NAME    TO CA-SEL-QUEUE-NAME.                     
           SET CA-TS-SHARED-DATA      TO TRUE                                   
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (CA-SEL-QUEUE-NAME)                                   
                    FROM  (CA-FSKCS034-CRITERIA-DATA)                           
                    LENGTH(LENGTH OF CA-FSKCS034-CRITERIA-DATA)                 
                    ITEM  (PC-ONE)                                              
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '5210-WRITE-TS-QUEUE' TO  DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READ SELECTION CRITERIA QUEUE'               
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  RETRIEVE THE KOHLS SIZE DESC FROM THE TSIZE TABLE.  IF THERE  *        
      *  ISN'T AN ACTIVE SIZE DESCRIPTION FOR THE GIVEN NRFCODE, THE   *        
      *  MOST RECENTLY EXPIRED SIZE DESCRIPTION WILL BE RETURNED.      *        
      ******************************************************************        
                                                                                
       5300-RETRIEVE-KOHLS-SIZE-DESC.                                           
                                                                                
           INITIALIZE PV-SQL-SUB.                                               
           MOVE SPACES TO SIZE-KOHLS-DESC.                                      
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PV-SQL-SUB > PC-MAX-RETRIES))                        
             EXEC SQL                                                           
                 SELECT KOHLS_DESC                                              
                 INTO  :SIZE-KOHLS-DESC                                         
                 FROM   TSIZE                                                   
                 WHERE  NRF_SIZE_CDE  = :SIZE-NRF-SIZE-CDE                      
                   AND  (EFF_END_DTE IS NULL                                    
                           OR                                                   
                         EFF_END_DTE > CURRENT DATE)                            
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                 WHEN SQLCODE = +100                                            
                      CONTINUE                                                  
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                      ADD +1 TO PV-SQL-SUB                                      
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                      MOVE '5300-RETRIEVE-KOHLS-SIZE-DESC'                      
                                       TO DP013-PARAGRAPH                       
                      MOVE 'SELECT TSIZE'                                       
                                       TO DP013-MESSAGE-TEXT (1)                
                      MOVE SQLCA       TO DP013-SQLCA                           
                      SET DP013-DB2-ABEND                                       
                                       TO TRUE                                  
                      PERFORM DP013-0000-PROCESS-ABEND                          
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
      *    RETRIEVE THE MOST RECENTLY EXPIRED SIZE DESCRIPTION                  
      *    IF THERE ARE NOT ANY ACTIVE SIZES FOUND.                             
                                                                                
           IF  SQLCODE = +100                                                   
               INITIALIZE PV-SQL-SUB                                            
                                                                                
               PERFORM WITH TEST AFTER                                          
                       UNTIL ((SQLCODE = ZERO)                                  
                                    OR                                          
                              (SQLCODE = +100)                                  
                                    OR                                          
                              (SQLCODE = -811)                                  
                                    OR                                          
                              (PV-SQL-SUB > PC-MAX-RETRIES))                    
                 EXEC SQL                                                       
                     SELECT KOHLS_DESC                                          
                     INTO  :SIZE-KOHLS-DESC                                     
                     FROM   TSIZE                                               
                     WHERE  NRF_SIZE_CDE = :SIZE-NRF-SIZE-CDE                   
                       AND  EFF_END_DTE  =                                      
                            ( SELECT MAX (EFF_END_DTE)                          
                              FROM   TSIZE                                      
                              WHERE  NRF_SIZE_CDE = :SIZE-NRF-SIZE-CDE )        
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = 0                                           
                     WHEN SQLCODE = +100                                        
                     WHEN SQLCODE = -811                                        
                          CONTINUE                                              
                     WHEN SQLCODE = -911                                        
                     WHEN SQLCODE = -913                                        
                          ADD +1 TO PV-SQL-SUB                                  
                     WHEN SQLWARN0 NOT = SPACES                                 
                     WHEN SQLCODE < ZERO                                        
                     WHEN SQLCODE > ZERO                                        
                         MOVE '5300-RETRIEVE-KOHLS-SIZE-DESC'                   
                                          TO DP013-PARAGRAPH                    
                         MOVE 'SELECT TSIZE'                                    
                                          TO DP013-MESSAGE-TEXT (1)             
                         MOVE SQLCA       TO DP013-SQLCA                        
                         SET DP013-DB2-ABEND                                    
                                          TO TRUE                               
                         PERFORM DP013-0000-PROCESS-ABEND                       
                 END-EVALUATE                                                   
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '5300-RETRIEVE-KOHLS-SIZE-DESC'                             
                                TO DP013-PARAGRAPH                              
               MOVE 'SELECT TSIZE'                                              
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE SQLCA       TO DP013-SQLCA                                  
               SET DP013-DB2-ABEND                                              
                                TO TRUE                                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  RETRIEVE THE VENDOR NAME FOR THE SKU FROM THE TENTNME TABLE.  *        
      *  IF A NOT FOUND CONDITION IS RETURNED, BLANKS WILL BE DISPLAYED*        
      *  ON THE SCREEN IN THE VENDOR FIELD FOR THE SKU.                *        
      ******************************************************************        
                                                                                
       5400-RETRIEVE-VENDOR-NAME.                                               
                                                                                
           INITIALIZE PV-SQL-SUB.                                               
           MOVE SPACES TO ENTNME-ENT-NAME-DESC.                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PV-SQL-SUB > PC-MAX-RETRIES))                        
             EXEC SQL                                                           
                 SELECT ENT_NAME_DESC                                           
                 INTO  :ENTNME-ENT-NAME-DESC                                    
                 FROM   TENTNME                                                 
                 WHERE  ENT_ID    = :TSK-ENT-ID                                 
                   AND  TYPE_CDE  = '04'                                        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                      CONTINUE                                                  
                 WHEN SQLCODE = +100                                            
                      MOVE SPACES TO ENTNME-ENT-NAME-DESC                       
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                      ADD +1 TO PV-SQL-SUB                                      
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                      MOVE '5400-RETRIEVE-VENDOR-NAME'                          
                                       TO DP013-PARAGRAPH                       
                      MOVE 'SELECT TENTNME'                                     
                                       TO DP013-MESSAGE-TEXT (1)                
                      MOVE SQLCA       TO DP013-SQLCA                           
                      SET DP013-DB2-ABEND                                       
                                       TO TRUE                                  
                      PERFORM DP013-0000-PROCESS-ABEND                          
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '5400-RETRIEVE-VENDOR-NAME'                                 
                                TO DP013-PARAGRAPH                              
               MOVE 'SELECT TENTNME'                                            
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE SQLCA       TO DP013-SQLCA                                  
               SET DP013-DB2-ABEND                                              
                                TO TRUE                                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      ** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
      ** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
      ** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
      ** THE USER.                                                    **        
      ******************************************************************        
       6000-REFRESH-PANEL-FROM-TEMP.                                            
           IF  ASC-UPDATE-TSQ                                                   
               SET ASC-NO-TSQ-UPDATE TO TRUE                                    
               MOVE ZERO                 TO PV-SAVE-ITEM                        
                                            PV-SEL-QUEUE-ITEM                   
               MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
                                         TO PV-HOLD-SELECTED-ITEMS              
               PERFORM 6100-UPDATE-TSQ                                          
                   UNTIL PV-ITEMS-PROCESSED >                                   
                                           ASC-NUMBER-OF-SELECTED-ITEMS         
               PERFORM 6010-DELETE-SEL-QUEUE                                    
               INITIALIZE SQW-BLOCK-ENTRIES (1)                                 
                          SQW-BLOCK-ENTRIES (2)                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
      *----------------------------------------------------------------*        
       6010-DELETE-SEL-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '6010-DELETE-SEL-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       6100-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
           PERFORM 6105-READ-SEL-QUEUE.                                         
           PERFORM                                                              
               VARYING FS002-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL ((PV-ITEMS-PROCESSED >                                     
                                    PV-HOLD-SELECTED-ITEMS)                     
               OR (FS002-IDX GREATER THAN FS002-MAX-ITEMS))                     
               IF  FS002-ACTION-SW (FS002-IDX) > SPACE                          
                   PERFORM 6110-PROCESS-ITEMS                                   
               END-IF                                                           
               ADD +1                TO PV-ITEMS-PROCESSED                      
           END-PERFORM.                                                         
           PERFORM 6111-WRITE-LIST-QUEUE.                                       
                                                                                
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *----------------------------------------------------------------*        
                                                                                
       6105-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    INTO  (FS002-TS-SEL-REC)                                    
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '6105-READ-SEL-QUEUE'                                       
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *----------------------------------------------------------------*        
                                                                                
       6110-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM = 1 + ((FS002-ITEM (FS002-IDX) - 1)                  
                           / PC-ITEMS-PER-PANEL).                               
                                                                                
           IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
               IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
                   PERFORM 6111-WRITE-LIST-QUEUE                                
               END-IF                                                           
               MOVE PV-ITEM          TO PV-SAVE-ITEM                            
                                                                                
               PERFORM 6112-READ-LIST-QUEUE                                     
               IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
                   CONTINUE                                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = (FS002-ITEM (FS002-IDX)                             
                          - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
           SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
           MOVE LQW-SELECTED-SW (PV-SUB)                                        
                                     TO LQW-TEMP-SELECTED-SW (PV-SUB).          
           EVALUATE TRUE                                                        
               WHEN FS002-INQ (FS002-IDX)                                       
                    MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN FS002-UPD (FS002-IDX)                                       
                    MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN FS002-DEL (FS002-IDX)                                       
                    MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN FS002-ERR (FS002-IDX)                                       
                    MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *----------------------------------------------------------------*        
                                                                                
       6111-WRITE-LIST-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-SAVE-ITEM)                                         
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6111-WRITE-LIST-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       6112-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SCRN TS QUEUE RECORD                              *        
      *----------------------------------------------------------------*        
                                                                                
       6200-WRITE-SCRN-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-SCRN-QUEUE-NAME)                                  
                   FROM  (SQW-SCRN-QUEUE-WORK-AREA)                             
                   ITEM  (PC-SCRN-ONE)                                          
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6200-WRITE-SCRN-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO WRITE SCRN QUEUE'                            
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    READ THE SCRN TS QUEUE RECORD                               *        
      *----------------------------------------------------------------*        
                                                                                
       6210-READ-SCRN-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SCRN-QUEUE-NAME)                                  
                   INTO  (SQW-SCRN-QUEUE-WORK-AREA)                             
                   ITEM  (PC-SCRN-ONE)                                          
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6210-READ-SCRN-QUEUE'                                      
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO READ SCRN QUEUE'                             
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    RE-WRITE THE SCRN TS QUEUE RECORD                           *        
      *----------------------------------------------------------------*        
                                                                                
       6220-REWRITE-SCRN-QUEUE.                                                 
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-SCRN-QUEUE-NAME)                                  
                   FROM  (SQW-SCRN-QUEUE-WORK-AREA)                             
                   ITEM  (PC-SCRN-ONE)                                          
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6220-REWRITE-SCRN-QUEUE'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE SCRN QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
