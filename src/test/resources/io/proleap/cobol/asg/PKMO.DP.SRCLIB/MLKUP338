       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUP338                                                  
       AUTHOR.        JULIE SEIDLER.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02-2-00.                                                  
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUP338 - M/L RECALC MONTH-TO-DATE MAJ CL STR TABLE (TMNMCLS)  *       
      ******************************************************************        
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TMNMCLS  *       
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *        
      *    END_INV_RTL_AMT  - RETAIL ENDING INVENTORY                  *        
      *    CUM_MKUP_PCT     - CUM-TO-DATE MARKUP PERCENT               *        
      *    END_INV_COST_AMT - COST ENDING INVENTORY                    *        
      *    SLS_COST_AMT     - COST AS A BASIS OF SALES                 *        
      *    GRO_MRGN_AMT     - GROSS MARGIN DOLLAR AMOUNT               *        
      ******************************************************************        
      *                                                                         
      * THIS PGM WILL USE CHECKPOINT RESTART LOGIC TO DETERMINE IF AND *        
      * WHEN A CHECKPOINT IS TO BE TAKEN. A MINIMUM OF 10 SECONDS WILL *        
      * EXPIRE BEFORE A CHECKPOINT / COMMIT WILL BE PERFORMED.         *        
      * CONDITIONS ....                                                *        
      * NORMAL START - INITAL CURSOR WILL SELECT ALL DEPT/MCL ROWS WITH*        
      *   ACTIVITY - CHECK IF TIME FOR A COMMIT ONLY AFTER AN ENTIRE   *        
      *   DEPT/MCL COMBINATION HAS BEEN RECALCED FOR ENTIRE DATE RANGE.*        
      * RESTART - SELECT CHECKPOINT RESTART KEY FROM TCKRSTINF, USE    *        
      *   DEPT/MCL KEY TO DETERMINE WHICH ROWS TO SELECT FOR RECALC -  *        
      *   WHICH IS ALL ROWS >= TO LAST COMMITED DEPT/MCL.              *        
W31475*  06/01/09 - COGNIZANT - INTRODUCE MULTI-ROW FETCH - CHANGED AS *        
W31475*                          PART OF ML BATCH PERF TUNING PROJECT  *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
JS0100     SELECT MNMCLS-EXTR-FILE                                              
               ASSIGN TO INP01.                                                 
      **                                                                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      **                                                                        
JS0100 FD  MNMCLS-EXTR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MNMCLS-EXTR-REC             PIC X(18).                               
      **                                                                        
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-COMMIT-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-INSERT-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-UPDATE-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-CUM-MKUP-MAX-LIMIT           PIC  9(05) VALUE ZEROES.         
JS0100     05  PA-EXTRACT-RECS-READ            PIC  9(09) VALUE ZEROES.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
JS0100     05  PS-EOF-EXTR-FILE-SW             PIC X(01) VALUE 'N'.             
JS0100         88  PS-EOF-EXTR-FILE                      VALUE 'Y'.             
           05  PS-CONTROL-TABLE-EMPTY-SW       PIC X     VALUE 'N'.             
               88  PS-EMPTY-CONTROL-TABLE                VALUE 'Y'.             
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.             
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.             
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
           05  PS-LOAD-DATE-TABLE-SW           PIC X     VALUE 'N'.             
               88  PS-DATE-TABLE-LOADED                  VALUE 'Y'.             
JS0200     05  PS-SEA-LOAD-DATE-TABLE-SW       PIC X     VALUE 'N'.             
JS0200         88  PS-SEA-DATE-TABLE-LOADED              VALUE 'Y'.             
JS0200     05  PS-SEASON-DATE-TABLE-SW         PIC X     VALUE 'N'.             
JS0200         88  PS-SEASON-DATE-TABLE-LOADED           VALUE 'Y'.             
JS0200         88  PS-SEASON-DATE-TAB-NOT-LOADED         VALUE 'N'.             
           05  PS-TABLE-PROCESSING-SW          PIC X     VALUE 'N'.             
               88  PS-TABLE-PROCESSED                    VALUE 'Y'.             
           05  PS-CURR-MONTH-SW                PIC X     VALUE 'N'.             
               88  PS-CURR-MONTH-ROW-NOT-FOUND           VALUE 'Y'.             
           05  PS-PREV-MONTH-ROW-SW            PIC X     VALUE 'N'.             
               88  PS-PREV-MONTH-ROW-NOT-FOUND           VALUE 'Y'.             
           05  PS-CARRY-FORWARD-SW             PIC X     VALUE 'N'.             
               88  PS-PFM-INV-DOLLARS-FOUND              VALUE 'Y'.             
           05  PS-PREV-SEASON-INV-SW           PIC X     VALUE 'N'.             
               88  PS-NO-PREV-SEASONAL-INV               VALUE 'Y'.             
           05  PS-CURRENT-PURCH-COST-SW        PIC X     VALUE 'N'.             
               88  PS-NO-CURRENT-PURCH-COSTS             VALUE 'Y'.             
           05  PS-RESTART-ROW-SW               PIC X     VALUE 'N'.             
               88  PS-RESTART-KEY-ESTABLISHED            VALUE 'Y'.             
           05  PS-COMMIT-TAKEN-SW              PIC X     VALUE 'N'.             
               88  PS-COMMIT-TAKEN                       VALUE 'Y'.             
           05  PS-CURR-SEASON-MTHS-SW          PIC X     VALUE 'N'.             
               88  PS-END-OF-CURR-SEASON-MTHS            VALUE 'Y'.             
               88  PS-NOT-END-OF-CURR-SEASON-MTHS        VALUE 'N'.             
           05  PS-SEASON-MTH-SW                PIC X     VALUE 'N'.             
               88  PS-SEASON-MTH-FOUND                   VALUE 'Y'.             
               88  PS-SEASON-MTH-NOT-FOUND               VALUE 'N'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK283'.            
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP338'.          
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                'INSPECT WHERE CLAUSE VARIABLE'.                                
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-06                       PIC X(30) VALUE                  
                'ERROR ON UPDATE OF TMNMCLS    '.                               
           05  PE-MSG-07                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-08                       PIC X(30) VALUE                  
                'UPDATE WORK_STRG - TCKRSTINF '.                                
           05  PE-MSG-09                       PIC X(30) VALUE                  
                'PGM ABEND DURING A COMMIT    '.                                
           05  PE-MSG-10                       PIC X(30) VALUE                  
                'UPDATE OF TCKRSTCNTL FAILED  '.                                
           05  PE-MSG-11                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTINF  '.                                
           05  PE-MSG-12                       PIC X(30) VALUE                  
                'UNABLE TO OBTAIN TIMESTAMP   '.                                
           05  PE-MSG-13                       PIC X(30) VALUE                  
                'SHRINK NOT FND FOR DEPT/DATE '.                                
           05  PE-MSG-14                       PIC X(30) VALUE                  
                'ERROR ON ''INSERT'' TO TMNMCLS'.                               
           05  PE-MSG-15                       PIC X(30) VALUE                  
                'CURRENT DATE LIMIT IS 24 MOS.'.                                
           05  PE-MSG-16                       PIC X(30) VALUE                  
                'ERROR IN SEA MN NBR - DTATTR.'.                                
           05  PE-MSG-17                       PIC X(30) VALUE                  
                'ERROR IN PFM MN NBR - DTATTR.'.                                
           05  PE-MSG-18                       PIC X(30) VALUE                  
                'ERR PREV SEA MN NBR - DTATTR.'.                                
           05  PE-MSG-19                       PIC X(30) VALUE                  
                'ERROR IN OPN MN NBR - DTATTR.'.                                
           05  PE-MSG-20                       PIC X(30) VALUE                  
                'ERR IN OPN SEA MTH - TDTATTR.'.                                
           05  PE-MSG-21                       PIC X(30) VALUE                  
                'ERR IN FETCH SEA MTH - DTATTR'.                                
           05  PE-MSG-22                       PIC X(30) VALUE                  
                'ERROR IN NBR OF SEAONS      .'.                                
           05  PE-MSG-23                       PIC X(30) VALUE                  
                'ERROR IN CLOS SEA MTH - DTAT.'.                                
       01  MISC.                                                                
           05  MISC-HOLD-DEPT                  PIC X(03)  VALUE SPACES.         
      *                                                                         
W31475 01  WS-SHNK-RTL-AMT-NULL-IND     PIC S9(04) COMP VALUE 0.                
  |    01  WS-COMMIT-TIME.                                                      
  |        05  WS-COMMIT-HH             PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MIN            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-SEC            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MSEC           PIC  9(02) VALUE 0.                     
  |    01  WS-HCOMMIT-TIME.                                                     
  |        05  WS-HCOMMIT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-SEC           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MSEC          PIC  9(02) VALUE 0.                     
  |    01  WS-CURRENT-TIME.                                                     
  |        05  WS-CURRENT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-SEC           PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-MSEC          PIC  9(02) VALUE 0.                     
W31475*                                                                         
       01  OUTPUT-DETAIL-LINE.                                                  
           05  FILLER                   PIC  X(12) VALUE ' DEPT NBR : '.        
           05  DL-DEPT-NBR              PIC  X(03) VALUE SPACES.                
           05  FILLER                   PIC  X(01) VALUE '-'.                   
           05  DL-MCL-NBR               PIC  X(02) VALUE SPACES.                
           05  FILLER                   PIC  X(01) VALUE SPACES.                
           05  DL-STR-NBR               PIC  X(04) VALUE SPACES.                
           05  DL-CUM-MKUP-PCT          PIC  ZZZ,ZZ9,999.9999999-.              
           05  FILLER                   PIC  X(09) VALUE SPACES.                
           05  FILLER                   PIC  X(14) VALUE                        
                                                   'ML MKUP USED: '.            
           05  DL-CUM-MKUP-USED         PIC  9,999.9999999-.                    
           05  FILLER                   PIC  X(05) VALUE SPACES.                
      *                                                                         
       01  PROGRAM-VARIABLES.                                                   
           05  PV-TMNMCLS-UPDATE-VARIABLES.                                     
      * THESE VARIABLES WILL UPDATE CURRENT TMNMCLS ROW *****************       
               10  PV-SHNK-RTL-AMT             PIC  S9(11)V9(2) COMP-3.         
               10  PV-END-INV-RTL-AMT          PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-MKUP-PCT             PIC  S9(4)V9(7)  COMP-3.         
               10  PV-CUM-MKUP-PCT-MAX         PIC  S9(9)V9(7) COMP-3.          
               10  PV-END-INV-COST-AMT         PIC  S9(11)V9(2) COMP-3.         
               10  PV-SLS-COST-AMT             PIC  S9(11)V9(2) COMP-3.         
               10  PV-GRO-MRGN-AMT             PIC  S9(11)V9(2) COMP-3.         
      ******************************************************************        
           05  PV-SEASON-TO-DATE-CSR-VAR.                                       
               10  PV-MNMCLS-RCPT-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-MNMCLS-RCPT-NET-COST-AMT PIC  S9(11)V9(2) COMP-3.         
               10  PV-MNMCLS-PADJ-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-MNMCLS-PADJ-NET-COST-AMT PIC  S9(11)V9(2) COMP-3.         
               10  PV-MNMCLS-MKUP-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-MNMCLS-XFER-NET-AMT      PIC  S9(11)V9(2) COMP-3.         
           05  PV-CURR-MONTH-COST-VAR.                                          
               10  PV-CURR-MONTH-RCPT-NET-COST PIC  S9(11)V9(2) COMP-3.         
               10  PV-CURR-MONTH-PADJ-NET-COST PIC  S9(11)V9(2) COMP-3.         
           05  PV-PREV-MONTH-VAR.                                               
               10  PV-PREV-MONTH-END-INV-RTL   PIC  S9(11)V9(2) COMP-3.         
               10  PV-PREV-MONTH-END-INV-COST  PIC  S9(11)V9(2) COMP-3.         
           05  PV-PREV-SEASON-VAR.                                              
               10  PV-PREV-SEASON-PURCH-RETAIL PIC  S9(11)V9(2) COMP-3.         
               10  PV-PREV-SEASON-PURCH-COST   PIC  S9(11)V9(2) COMP-3.         
           05  PV-CUM-TO-DATE-VARIABLES.                                        
               10  PV-CUM-TO-DATE-PURCH-RETAIL PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-TO-DATE-PURCH-COST   PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-SEASON-MKUP          PIC  S9(11)V9(2) COMP-3.         
      *                                                                         
      *                                                                         
       01  PV-VARIABLE-FIELDS.                                                  
           05  PV-TEST-STR-NUM                 PIC  9(04) VALUE ZEROES.         
           05  PV-TEST-STR-X REDEFINES PV-TEST-STR-NUM.                         
               10  PV-TEST-STR-FIRST2          PIC  X(02).                      
               10  PV-TEST-STR-LAST2           PIC  X(02).                      
           05  PV-SUB1                         PIC  9(02) VALUE ZEROES.         
           05  PV-SEA-SUB                      PIC  9(02) VALUE ZEROES.         
           05  PV-MAX-DATE-SUB                 PIC  9(02) VALUE ZEROES.         
           05  PV-HOLD-ROW.                                                     
               10  PV-HOLD-DEPT-NBR            PIC  X(03) VALUE SPACES.         
               10  PV-HOLD-MAJ-CL-NBR          PIC  X(02) VALUE SPACES.         
               10  PV-HOLD-STR-NBR       COMP  PIC S9(04) VALUE ZEROES.         
           05  PV-HOLD-DATE                    PIC  X(10) VALUE SPACES.         
JS0200     05  PV-HOLD-PFM-MN-NBR              PIC  X(02) VALUE SPACES.         
JS0200     05  PV-TEMP-PFM-MN-NBR              PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-PFM-END-DTE             PIC  X(10) VALUE SPACES.         
JS0200     05  PV-TEMP-PFM-END-DTE             PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-SEA-BEG-DTE             PIC  X(10) VALUE SPACES.         
JS0200     05  PV-TEMP-SEA-BEG-DTE             PIC  X(10) VALUE SPACES.         
JS0200     05  PV-HOLD-SEA-BEG-MN-NBR          PIC  X(02) VALUE SPACES.         
JS0200     05  PV-TEMP-SEA-BEG-MN-NBR          PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-OPN-MN-END-DTE          PIC  X(10) VALUE SPACES.         
JS0200     05  PV-TEMP-OPN-MN-END-DTE          PIC  X(10) VALUE SPACES.         
JS0200     05  PV-HOLD-OPN-MN-NBR              PIC  X(02) VALUE SPACES.         
JS0200     05  PV-TEMP-OPN-MN-NBR              PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-CAL-MN-NBR              PIC  9(02) VALUE ZEROES.         
JS0200     05  PV-HOLD-PREV-SEA-MN-NBR         PIC  X(02) VALUE SPACES.         
JS0200     05  PV-TEMP-PREV-SEA-MN-NBR         PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES.         
JS0200     05  PV-TEMP-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-FSCL-MN-NBR             PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-FSCL-YR-NBR             PIC  X(04) VALUE SPACES.         
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES.         
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES.         
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES.         
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES.         
           05  PV-XFER-NET                     PIC  S9(11)V99   COMP-3.         
           05  PV-MAJ-CL-NBR                   PIC  9(02) VALUE ZEROS.          
           05  PV-HOLD-MAX-MAJ-CL-NBR          PIC  X(02) VALUE SPACES.         
JS0200     05  PV-SEA-MTH-DTE-DB2              PIC  X(10) VALUE SPACES.         
JS0200     05  PV-SEA-MTH-NBR-DB2              PIC  X(02) VALUE SPACES.         
JS0200     05  PV-SEASON-MN-DTE                PIC  X(10) VALUE SPACES.         
JS0200     05  PV-SEASON-MN-NBR                PIC  X(02) VALUE SPACES.         
JS0200     05  PV-MAJ-CL-DIGITS.                                                
JS0200         10  PV-MAJ-CL-DIGIT-1          PIC  X(01) VALUE SPACES.          
JS0200         10  FILLER                     PIC  X(01) VALUE ZEROES.          
                                                                                
                                                                                
      *                                                                         
      *** TMNMCLS EXTRACT FILE                                                  
JS0100 01  EXTR-RECORD.                                                         
JS0100     05  EXTR-DEPT-NBR    PIC X(03).                                      
JS0100     05  EXTR-MAJ-CL-NBR  PIC X(02).                                      
JS0100     05  EXTR-STR-NBR     PIC S9(04) COMP.                                
JS0100     05  FILLER           PIC X(11).                                      
                                                                                
                                                                                
      * REFORMAT CALENDAR NUMBER TO PIC 9 FIELD FOR USE AS A SUBSCRIPT          
       01  REFORMAT-ALPHA-CALENDAR.                                             
           05  FMT-CALENDAR-MO-NUMERIC          PIC 9(2)  VALUE ZEROES.         
      *                                                                         
      *                                                                         
      * TABLE TO CONTAIN DATE INFORMATION FOR ALL MONTHS RE-CALCULATED          
       01  DATE-TABLE.                                                          
           05  CONTROL-DATES OCCURS 24 TIMES.                                   
               10  DT-OPN-MN-END-DTE            PIC X(10) VALUE SPACES.         
JS0200         10  DT-OPN-MN-NBR                PIC X(02) VALUE SPACES.         
               10  DT-CAL-MN-NBR                PIC X(02) VALUE SPACES.         
               10  DT-PFM-END-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-PFM-MN-NBR                PIC X(02) VALUE SPACES.         
               10  DT-SEA-BEG-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-BEG-MN-NBR            PIC X(02) VALUE SPACES.         
               10  DT-PREV-SEA-END-DTE          PIC X(10) VALUE SPACES.         
JS0200         10  DT-PREV-SEA-MN-NBR           PIC X(02) VALUE SPACES.         
               10  DT-FSCL-MN-NBR               PIC X(02) VALUE SPACES.         
               10  DT-FSCL-YR-NBR               PIC X(04) VALUE SPACES.         
JS0200         10  DT-SEA-MN1-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN1-NBR               PIC X(02) VALUE SPACES.         
JS0200         10  DT-SEA-MN2-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN2-NBR               PIC X(02) VALUE SPACES.         
JS0200         10  DT-SEA-MN3-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN3-NBR               PIC X(02) VALUE SPACES.         
JS0200         10  DT-SEA-MN4-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN4-NBR               PIC X(02) VALUE SPACES.         
JS0200         10  DT-SEA-MN5-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN5-NBR               PIC X(02) VALUE SPACES.         
JS0200         10  DT-SEA-MN6-DTE               PIC X(10) VALUE SPACES.         
JS0200         10  DT-SEA-MN6-NBR               PIC X(02) VALUE SPACES.         
                                                                                
                                                                                
      ****************************************************                      
      *  WEEKLY SHRINKAGE TABLE                          *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TWKSHNK                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *  MNMCL TABLE                                                            
      ****************************************************                      
TEMP       EXEC SQL                                                             
TEMP           INCLUDE TMNMCL                                                   
TEMP       END-EXEC.                                                            
      ****************************************************                      
      *  MNSCL TABLE                                                            
      ****************************************************                      
TEMP       EXEC SQL                                                             
TEMP           INCLUDE TMNSCL                                                   
TEMP       END-EXEC.                                                            
      ****************************************************                      
      *  MNSCLS TABLE                                                           
      ****************************************************                      
TEMP       EXEC SQL                                                             
TEMP           INCLUDE TMNSCLS                                                  
TEMP       END-EXEC.                                                            
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ****************************************************                      
      *  M/L MONTH-TO-DATE MAJ CLASS STR TABLE                                  
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TMNMCLS                                                  
           END-EXEC.                                                            
      ****************************************************                      
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      ****************************************************                      
      * MONTHS TO BE PROCESSED - ALL DATES BETWEEN OPEN AND MAX DATES           
      *                                    --------------------------           
      ****************************************************                      
           EXEC SQL                                                             
               DECLARE FISCAL_DATE_CSR CURSOR FOR                               
                SELECT  DISTINCT FSCL_MN_END_DTE                                
                  FROM  TDTATTR                                                 
                 WHERE FSCL_MN_END_DTE BETWEEN                                  
                                            :MLCNTL-OPN-FSCL-MN-DTE             
                                        AND :MLCNTL-MXPSTG-FSCL-MN-DTE          
                 ORDER BY FSCL_MN_END_DTE                                       
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                         
      *************************************************************             
      * MONTHS TO BE PROCESSED - ALL DATES IN THE CURRENT SEASON                
      *                                         ------------------              
      **************************************************************            
           EXEC SQL                                                             
               DECLARE SEASON_DATE_CSR CURSOR FOR                               
                SELECT  DISTINCT FSCL_MN_END_DTE                                
                                ,FSCL_MN_NBR                                    
                  FROM  TDTATTR                                                 
                 WHERE FSCL_MN_END_DTE BETWEEN                                  
                                            :PV-TEMP-SEA-BEG-DTE                
                                        AND :PV-HOLD-DATE                       
                 ORDER BY FSCL_MN_END_DTE                                       
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04) VALUE +61  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-PREV-DTL-KEY          PIC  X(21)  VALUE SPACES.           
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                         
                   15  FILLER               PIC  X(01).                         
                   15  CR-FISCAL-MN-NBR     PIC  X(02).                         
                   15  FILLER               PIC  X(01).                         
                   15  CR-DEPT-MCL-STR-KEY.                                     
                       20  CR-DEPT-NBR      PIC  X(03).                         
                       20  CR-MAJ-CL-NBR    PIC  X(02).                         
                       20  CR-STR-NBR       PIC  S9(04) COMP.                   
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TMNMCLS-UPDATE-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TMNMCLS-INSERT-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TMNMCLS-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TMNMCLS-INPUT-COUNT   PIC  9(09) VALUE ZEROES.            
           EJECT                                                                
      *                                                                         
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
      *                                                                         
      *                                                                         
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-EOF-EXTR-FILE.                                         
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
                                                                                
           IF PA-CUM-MKUP-MAX-LIMIT > 0                                         
              MOVE 4095 TO RETURN-CODE                                          
           ELSE                                                                 
              MOVE 0    TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 1000-INITIALIZATION.                                                    
      * LOAD TABLE WITH MONTHS TO BE RECALCULATED - OPN_MN THRU MXPSTG          
      * DETERMINE IF THIS IS A 'RESTART' (TCKRSTINF-CKPT-STAT-CODE = 9)         
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
      *                                                                         
           PERFORM 7000-READ-CONTROL-TABLE.                                     
           PERFORM 7025-PROCESS-CNTL-TBL.                                       
      *                                                                         
                                                                                
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
      *                                                                         
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
                PERFORM 7700-SELECT-RESTART-INFO                                
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                           
                                              CR-CHECKPOINT-RESTART-VAR         
                MOVE CR-DEPT-MCL-STR-KEY  TO PV-HOLD-ROW                        
                DISPLAY 'RESTART-LAST DEPT/MCL COMMIT: ',                       
                         CR-DEPT-NBR '-' CR-MAJ-CL-NBR                          
JS0100                   ' STR: ' CR-STR-NBR                                    
                 DISPLAY '          '                                           
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '          '                                           
JS0100          MOVE CR-DEPT-NBR   TO PV-HOLD-DEPT-NBR                          
JS0100          MOVE CR-MAJ-CL-NBR TO PV-HOLD-MAJ-CL-NBR                        
JS0100          MOVE CR-STR-NBR    TO PV-HOLD-STR-NBR                           
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
      *                                                                         
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'        
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
      *                                                                         
JS0100     OPEN INPUT MNMCLS-EXTR-FILE.                                         
                                                                                
JS0200*    **************************************************                   
JS0200*    **  THE INPUT EXTRACT FILE IS THE DRIVING FORCE                      
JS0200*    **  IN THIS PROGRAM.                                                 
JS0200*    **************************************************                   
JS0100     IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
JS0100         PERFORM 4000-READ-EXTR-FILE                                      
JS0100             UNTIL                                                        
JS0100             (PA-EXTRACT-RECS-READ = CR-TMNMCLS-INPUT-COUNT)              
JS0100              OR (PS-EOF-EXTR-FILE)                                       
JS0100     ELSE                                                                 
JS0100         PERFORM 4000-READ-EXTR-FILE                                      
JS0100     END-IF.                                                              
                                                                                
JS0100     MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT.                                
JS0100     DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT.                          
      *                                                                         
                                                                                
           IF  PS-EOF-EXTR-FILE                                                 
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESSED AFTER RESTART'              
               ELSE                                                             
                   IF TCKRSTCNTL-CKPT-STAT-CODE = '0'                           
                       DISPLAY 'NO ROWS SELECTED FOR RECALCULATION'             
                       DISPLAY 'CHECK IF INPUT FILE IS EMPTY      '             
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ZERO TO PA-CUM-MKUP-MAX-LIMIT.                                  
      *                                                                         
      *                                                                         
      ******************************************************************        
      * MAIN PROCESSING PARAGRAPH-PERFORMED UNTIL END OF CSR PROCESSING         
      * WHEN PV-SUB1 > PV-MAX-SUB - THEN ALL DATES FOR THIS DEPT/MCL/STR        
      *                 HAVE BEEN PROCESSED, CHECK IF COMMIT REQUIRED.          
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE 1 TO PV-SUB1.                                                   
      *                                                                         
           PERFORM 2020-PROCESS-MCLS                                            
               UNTIL PV-SUB1 > PV-MAX-DATE-SUB.                                 
                                                                                
           PERFORM 7800-COMMIT-PROCESSING.                                      
                                                                                
JS0100     PERFORM 4000-READ-EXTR-FILE.                                         
                                                                                
           IF PS-EOF-EXTR-FILE                                                  
               DISPLAY 'END OF INPUT FILE'                                      
TEMP  *        CONTINUE                                                         
           ELSE                                                                 
JS0100         IF EXTR-DEPT-NBR NOT = MISC-HOLD-DEPT                            
JS0100             MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT                         
JS0100             DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT                   
JS0100         END-IF                                                           
JS0100         IF PS-COMMIT-TAKEN                                               
JS0100             MOVE 'N' TO PS-COMMIT-TAKEN-SW                               
JS0100         END-IF                                                           
JS0100     END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * PROCESS ALL STORES FOR A MAJ-CLASS                                      
      ******************************************************************        
       2020-PROCESS-MCLS.                                                       
                                                                                
           PERFORM 2025-PROCESS-MCL.                                            
                                                                                
JS0299     ADD 1 TO PV-SUB1.                                                    
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DEPT LEVEL PROCESSING PARAGRAPH                                         
      * PROCESS ALL DATES FOR THIS DEPARTMENT                                   
      ******************************************************************        
       2025-PROCESS-MCL.                                                        
           INITIALIZE  PROGRAM-VARIABLES.                                       
           MOVE 'N' TO PS-CURR-MONTH-SW                                         
                       PS-PREV-MONTH-ROW-SW                                     
                       PS-RESTART-ROW-SW                                        
                       PS-CARRY-FORWARD-SW.                                     
                                                                                
JS0200     MOVE EXTR-DEPT-NBR   TO PV-HOLD-DEPT-NBR.                            
JS0200     MOVE EXTR-MAJ-CL-NBR TO PV-HOLD-MAJ-CL-NBR.                          
JS0200     MOVE EXTR-STR-NBR    TO PV-HOLD-STR-NBR.                             
                                                                                
           PERFORM 2800-FORMAT-DATES.                                           
                                                                                
      *                                                                         
      * DETERMINE IF CURRENT DOLLARS AND/OR PRIOR INVENTORY DOLLARS             
           PERFORM 7350-SELECT-PREV-MONTH-END-INV.                              
           PERFORM 7300-SELECT-CURRENT-DOLLARS.                                 
      *                                                                         
      ******************************************************************        
      *****                CARRY FORWARD LOGIC                     *****        
      ******************************************************************        
      *    CURRENT $ FOUND FOR THIS MONTH  - RECALC ROW                *        
      * NO CURRENT $ FOUND/NO PREV $ FOUND - BYPASS RECALC THIS MONTH  *        
      * NO CURRENT $ FOUND/   PREV $ FOUND - CARRY FORWARD INVENTORY $ *        
      ******************************************************************        
           IF PS-CURR-MONTH-ROW-NOT-FOUND                                       
               IF PS-PREV-MONTH-ROW-NOT-FOUND                                   
                   CONTINUE                                                     
               ELSE                                                             
                   IF PS-PFM-INV-DOLLARS-FOUND                                  
                       PERFORM 7950-INSERT-CARRY-FWD-ROW                        
                       PERFORM 7300-SELECT-CURRENT-DOLLARS                      
                       PERFORM 2035-RECALC-PROCESSING                           
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               PERFORM 2035-RECALC-PROCESSING                                   
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                *        
      *            ********* RECALC PROCESSING **********              *        
      *                                                                *        
      ******************************************************************        
       2035-RECALC-PROCESSING.                                                  
      *                                                                         
JS0200     MOVE EXTR-DEPT-NBR     TO PV-HOLD-DEPT-NBR.                          
JS0200     MOVE EXTR-MAJ-CL-NBR   TO PV-HOLD-MAJ-CL-NBR                         
                                     PV-MAJ-CL-NBR.                             
           ADD +9                 TO PV-MAJ-CL-NBR.                             
           MOVE PV-MAJ-CL-NBR     TO PV-HOLD-MAX-MAJ-CL-NBR.                    
JS0200     MOVE EXTR-STR-NBR      TO PV-HOLD-STR-NBR.                           
      *                                                                         
           PERFORM 7200-OBTAIN-SHRINK-DLRS.                                     
           PERFORM 2150-CALC-ENDINV-RETAIL.                                     
                                                                                
           PERFORM 7400-PREV-SEASON-END-INV.                                    
      *                                                                         
           PERFORM 2200-CALC-PREV-SEASN-END-PURCH.                              
                                                                                
JS0200     SET PS-NOT-END-OF-CURR-SEASON-MTHS TO TRUE.                          
JS0200     PERFORM 2250-CALC-SEASON-TO-DATE-PURCH                               
               VARYING PV-SEA-SUB FROM 1 BY 1                                   
               UNTIL PS-END-OF-CURR-SEASON-MTHS.                                
                                                                                
      *                                                                         
           PERFORM 2300-CALC-CUMM-TO-DATE-PURCH.                                
           PERFORM 2400-CALC-CUMM-MARKUP-PCT.                                   
                                                                                
      * CURRENT MONTH CUMM                                                      
           PERFORM 2500-CALC-END-INV-COST.                                      
                                                                                
      * SALES COST AMOUNT                                                       
W31475*    PERFORM 7475-CURR-SEASON-COST.                                       
           PERFORM 2600-CALC-SALES-COST-AMT.                                    
                                                                                
      * GROSS MARGIN AMOUNT                                                     
           PERFORM 2700-CALC-GROSS-MARGIN-AMT.                                  
           PERFORM 7500-UPDATE-PROCESSING.                                      
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      **************************************************                        
      *                                                *                        
      * FOLLOWING PARAGRAPHS PERFORM ALL CALCULATIONS  *                        
      *                                                *                        
      ******************************************************************        
      * CALCULATE THE ENDING INVENTORY OF CURRENT MONTH BASED ON THE            
      *       ENDING INVENTORY FROM THE PRIOR MONTH                             
      ******************************************************************        
       2150-CALC-ENDINV-RETAIL.                                                 
      *                                                                         
           COMPUTE PV-END-INV-RTL-AMT = (  PV-PREV-MONTH-END-INV-RTL            
                                         + MNMCLS-RCPT-RTL-AMT                  
                                         + MNMCLS-PADJ-RTL-AMT                  
                                         + MNMCLS-MKUP-RTL-AMT                  
                                         - MNMCLS-SLS-RTL-AMT                   
                                         + MNMCLS-XFER-IN-RTL-AMT               
                                         - MNMCLS-XFER-OUT-RTL-AMT              
                                         - MNMCLS-MKDN-RTL-AMT                  
                                         - MNMCLS-INV-ADJ-RTL-AMT               
                                         - PV-SHNK-RTL-AMT    ).                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * CALCULATE CUMULATIVE MARKUP PERCENTAGE FOR UPDATE                       
      ******************************************************************        
       2200-CALC-PREV-SEASN-END-PURCH.                                          
      *                                                                         
           IF PS-NO-PREV-SEASONAL-INV                                           
                MOVE 0 TO PV-PREV-SEASON-PURCH-RETAIL                           
                MOVE 0 TO PV-PREV-SEASON-PURCH-COST                             
           ELSE                                                                 
                MOVE MNMCLS-END-INV-RTL-AMT TO                                  
                                         PV-PREV-SEASON-PURCH-RETAIL            
                MOVE MNMCLS-END-INV-COST-AMT TO                                 
                                         PV-PREV-SEASON-PURCH-COST              
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * ACCUMULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING        
      ******************************************************************        
       2250-CALC-SEASON-TO-DATE-PURCH.                                          
      *                                                                         
           PERFORM 7450-GET-SEASONAL-MTH.                                       
                                                                                
           IF PS-SEASON-MTH-FOUND                                               
               COMPUTE PV-XFER-NET =                                            
                  (MNMCLS-XFER-IN-RTL-AMT -                                     
                   MNMCLS-XFER-OUT-RTL-AMT)                                     
               ADD PV-XFER-NET             TO  PV-MNMCLS-XFER-NET-AMT           
               ADD MNMCLS-RCPT-RTL-AMT     TO  PV-MNMCLS-RCPT-RTL-AMT           
               ADD MNMCLS-RCPT-NET-COST-AMT                                     
                   TO PV-MNMCLS-RCPT-NET-COST-AMT                               
               ADD MNMCLS-PADJ-RTL-AMT     TO  PV-MNMCLS-PADJ-RTL-AMT           
               ADD MNMCLS-PADJ-NET-COST-AMT                                     
                   TO PV-MNMCLS-PADJ-NET-COST-AMT                               
               ADD MNMCLS-MKUP-RTL-AMT     TO  PV-MNMCLS-MKUP-RTL-AMT           
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DETERMINE CUMULATIVE RETAIL AND COST                                    
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS            
      ******************************************************************        
       2300-CALC-CUMM-TO-DATE-PURCH.                                            
      *                                                                         
           COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                                
                                         (  PV-PREV-SEASON-PURCH-COST           
                                          + PV-MNMCLS-RCPT-NET-COST-AMT         
                                          + PV-MNMCLS-PADJ-NET-COST-AMT).       
                                                                                
           COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                                
                                          (  PV-PREV-SEASON-PURCH-RETAIL        
                                           + PV-MNMCLS-XFER-NET-AMT             
                                           + PV-MNMCLS-RCPT-RTL-AMT             
                                           + PV-MNMCLS-PADJ-RTL-AMT             
                                           + PV-MNMCLS-MKUP-RTL-AMT).           
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNMCLS - CUM_MKUP_PCT           
      ******************************************************************        
       2400-CALC-CUMM-MARKUP-PCT.                                               
      *                                                                         
           COMPUTE PV-CUM-SEASON-MKUP = (  PV-CUM-TO-DATE-PURCH-RETAIL          
                                         - PV-CUM-TO-DATE-PURCH-COST  ).        
      * CUM SEASON RETAIL CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')        
           IF PV-CUM-TO-DATE-PURCH-RETAIL = 0                                   
                MOVE 100 TO PV-CUM-MKUP-PCT                                     
           ELSE                                                                 
                COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED =                           
      *         COMPUTE PV-CUM-MKUP-PCT ROUNDED =                               
               (PV-CUM-SEASON-MKUP * 100) / PV-CUM-TO-DATE-PURCH-RETAIL         
                IF ( PV-CUM-MKUP-PCT-MAX > 9999.9999999 OR                      
                     PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                     
                    MOVE PV-HOLD-DEPT-NBR     TO DL-DEPT-NBR                    
                    MOVE PV-HOLD-MAJ-CL-NBR   TO DL-MCL-NBR                     
                    MOVE PV-HOLD-STR-NBR      TO DL-STR-NBR                     
                    MOVE PV-CUM-MKUP-PCT-MAX  TO DL-CUM-MKUP-PCT                
                                                 DL-CUM-MKUP-USED               
                    ADD +1 TO PA-CUM-MKUP-MAX-LIMIT                             
                END-IF                                                          
                MOVE PV-CUM-MKUP-PCT-MAX TO PV-CUM-MKUP-PCT                     
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNMCLS - END_INV_COST_AMT       
      ******************************************************************        
       2500-CALC-END-INV-COST.                                                  
      *                                                                         
           COMPUTE PV-END-INV-COST-AMT ROUNDED =                                
                        ( (100 - PV-CUM-MKUP-PCT)  *                            
                               ( PV-END-INV-RTL-AMT) ) / 100.                   
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNMCLS - SALES_COST_AMT *       
      ******************************************************************        
       2600-CALC-SALES-COST-AMT.                                                
      *                                                                         
           IF PS-NO-CURRENT-PURCH-COSTS                                         
               MOVE ZEROES            TO PV-CURR-MONTH-RCPT-NET-COST            
                                         PV-CURR-MONTH-PADJ-NET-COST            
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-SLS-COST-AMT =                                            
                 (  PV-PREV-MONTH-END-INV-COST                                  
                  + PV-CURR-MONTH-RCPT-NET-COST                                 
                  + PV-CURR-MONTH-PADJ-NET-COST                                 
                  - PV-END-INV-COST-AMT         ).                              
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNMCLS - GRO_MRGN_AMT  *        
      ******************************************************************        
       2700-CALC-GROSS-MARGIN-AMT.                                              
      *                                                                         
           IF MNMCLS-SLS-RTL-AMT = 0                                            
               MOVE 0 TO PV-GRO-MRGN-AMT                                        
           ELSE                                                                 
               COMPUTE PV-GRO-MRGN-AMT =                                        
               ( ( MNMCLS-SLS-RTL-AMT ) - ( PV-SLS-COST-AMT ) )                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * MOVE SUBSCRIPTED VARIABLE TO FORMAT USABLE BY DB2                       
      ******************************************************************        
       2800-FORMAT-DATES.                                                       
           MOVE DT-SEA-BEG-DTE(PV-SUB1)    TO PV-HOLD-SEA-BEG-DTE.              
JS0200     MOVE DT-SEA-BEG-MN-NBR(PV-SUB1) TO PV-HOLD-SEA-BEG-MN-NBR.           
           MOVE DT-OPN-MN-END-DTE(PV-SUB1) TO PV-HOLD-OPN-MN-END-DTE.           
JS0200     MOVE DT-OPN-MN-NBR(PV-SUB1)     TO PV-HOLD-OPN-MN-NBR.               
           MOVE DT-PFM-END-DTE(PV-SUB1)    TO PV-HOLD-PFM-END-DTE.              
JS0200     MOVE DT-PFM-MN-NBR(PV-SUB1)     TO PV-HOLD-PFM-MN-NBR.               
           MOVE DT-CAL-MN-NBR(PV-SUB1)     TO PV-HOLD-CAL-MN-NBR.               
           MOVE DT-FSCL-MN-NBR(PV-SUB1)    TO PV-HOLD-FSCL-MN-NBR.              
           MOVE DT-FSCL-YR-NBR(PV-SUB1)    TO PV-HOLD-FSCL-YR-NBR.              
           MOVE DT-PREV-SEA-END-DTE(PV-SUB1)                                    
                                           TO PV-HOLD-PREV-SEA-END-DTE.         
JS0200     MOVE DT-PREV-SEA-MN-NBR(PV-SUB1)                                     
                                           TO PV-HOLD-PREV-SEA-MN-NBR.          
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       4000-READ-EXTR-FILE.                                                     
                                                                                
           READ MNMCLS-EXTR-FILE INTO EXTR-RECORD                               
               AT END                                                           
                  SET PS-EOF-EXTR-FILE TO TRUE.                                 
                                                                                
           IF PS-EOF-EXTR-FILE                                                  
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-EXTRACT-RECS-READ                                    
           END-IF.                                                              
         EJECT                                                                  
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       7000-READ-CONTROL-TABLE.                                                 
      *                                                                         
            EXEC SQL                                                            
                SELECT OPN_FSCL_MN_DTE                                          
                      ,OPN_FSCL_MN_NBR                                          
                      ,MXPSTG_FSCL_MN_DTE                                       
                      ,MXPSTG_FSCL_MN_NBR                                       
                 INTO  :MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                      ,:MLCNTL-MXPSTG-FSCL-MN-DTE                               
                      ,:MLCNTL-MXPSTG-FSCL-MN-NBR                               
                 FROM  TMLCNTL                                                  
            END-EXEC.                                                           
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE MLCNTL-OPN-FSCL-MN-DTE TO CR-FISCAL-MN-END-DTE          
                   MOVE MLCNTL-OPN-FSCL-MN-NBR TO CR-FISCAL-MN-NBR              
                 DISPLAY '--------------------------------------------'         
                   DISPLAY 'CONTROL DATES (TMLCNTL)'                            
                   DISPLAY 'FISCAL OPEN END DATE  : ',                          
                                                 MLCNTL-OPN-FSCL-MN-DTE         
                   DISPLAY '       OPEN MONTH NBR : ',                          
                                                 MLCNTL-OPN-FSCL-MN-NBR         
                   DISPLAY 'MAXPSTG  MONTH END DTE: ',                          
                                              MLCNTL-MXPSTG-FSCL-MN-DTE         
                   DISPLAY '     MAX MONTH NUMBER : ',                          
                                              MLCNTL-MXPSTG-FSCL-MN-NBR         
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '     '                                                
               WHEN OTHER                                                       
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * LOAD ALL DATES TO BE PROCESSED TO INTERNAL TABLE                        
      ******************************************************************        
       7025-PROCESS-CNTL-TBL.                                                   
      *                                                                         
            PERFORM 7050-OPEN-DATE-CURSOR.                                      
            PERFORM 7075-PROCESS-DATE-CURSOR                                    
                UNTIL PS-DATE-TABLE-LOADED.                                     
      *                                                                         
      *                                                                         
      *****************************************************************         
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT            
      *****************************************************************         
       7050-OPEN-DATE-CURSOR.                                                   
      *                                                                         
           EXEC SQL                                                             
               OPEN FISCAL_DATE_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7050-OPEN-DATE-CSR' TO PV-PARAGRAPH-NAME                
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE                 
      *****************************************************************         
       7075-PROCESS-DATE-CURSOR.                                                
      *                                                                         
           EXEC SQL                                                             
                  FETCH FISCAL_DATE_CSR                                         
                   INTO :PV-HOLD-DATE                                           
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                 DISPLAY '--------------------------------------------'         
                   ADD 1 TO PV-SUB1                                             
                   DISPLAY 'MONTH # ', PV-SUB1                                  
                   DISPLAY 'PROCESS DATE: ', PV-HOLD-DATE                       
                   PERFORM 7100-SELECT-DB2-DATES                                
                   PERFORM 7125-LOAD-DATE-TABLE                                 
               WHEN +100                                                        
                 DISPLAY '     '                                                
                 DISPLAY '--------------------------------------------'         
                 DISPLAY 'NBR OF MONTHS PROCESSED THIS RUN: ', PV-SUB1          
                   IF PV-SUB1 > 1                                               
                       DISPLAY '   ', MLCNTL-OPN-FSCL-MN-DTE, ' THRU ' ,        
                                      MLCNTL-MXPSTG-FSCL-MN-DTE                 
                   END-IF                                                       
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '     '                                                
                   MOVE PV-SUB1 TO PV-MAX-DATE-SUB                              
                   MOVE ZEROES  TO PV-SUB1                                      
                   SET PS-DATE-TABLE-LOADED TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME            
                   MOVE PE-MSG-04 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
           IF PV-SUB1 > 24                                                      
               MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME                
               MOVE PE-MSG-15 TO PV-OPERATION-ATTEMPTED                         
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7100-SELECT-DB2-DATES.                                                   
      *                                                                         
           EXEC SQL                                                             
               SELECT  CAL_MN_NBR                                               
                      ,PFM_END_DTE                                              
                      ,SEA_BEG_DTE                                              
                      ,PREV_SEA_END_DTE                                         
                      ,FSCL_MN_NBR                                              
                      ,FSCL_YR_NBR                                              
                INTO  :DTATTR-CAL-MN-NBR                                        
                     ,:DTATTR-PFM-END-DTE                                       
                     ,:DTATTR-SEA-BEG-DTE                                       
                     ,:DTATTR-PREV-SEA-END-DTE                                  
                     ,:DTATTR-FSCL-MN-NBR                                       
                     ,:DTATTR-FSCL-YR-NBR                                       
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-HOLD-DATE                                   
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN +100                                                        
                  DISPLAY '**************************************'              
                  DISPLAY 'TDTATTR-KEY ' PV-HOLD-DATE                           
                  DISPLAY 'NO ROW FOUND ON TDTATTR TABLE'                       
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
               WHEN OTHER                                                       
                  DISPLAY 'TDTATTR-KEY ' PV-HOLD-DATE                           
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       7110-SELECT-SEA-MN-NBR.                                                  
      *                                                                         
           EXEC SQL                                                             
               SELECT  FSCL_MN_NBR                                              
                INTO  :PV-TEMP-SEA-BEG-MN-NBR                                   
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-TEMP-SEA-BEG-DTE                            
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  DISPLAY '****************************************'            
                  DISPLAY 'SEASON BEGIN DATE:  ' PV-TEMP-SEA-BEG-DTE            
                  DISPLAY ' '                                                   
                  MOVE '7110-SELECT-SEA-MN-NBR' TO PV-PARAGRAPH-NAME            
                  MOVE PE-MSG-16 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       7112-SELECT-PREV-MN-NBR.                                                 
      *                                                                         
           EXEC SQL                                                             
               SELECT  FSCL_MN_NBR                                              
                INTO  :PV-TEMP-PFM-MN-NBR                                       
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-TEMP-PFM-END-DTE                            
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  DISPLAY '******************************************'          
                  DISPLAY 'PREV FSCL MN DTE :  ' PV-TEMP-PFM-END-DTE            
                  DISPLAY ' '                                                   
                  MOVE '7112-SELECT-PREV-MN-NBR' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-17 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       7114-SELECT-PREV-SEA-MN-NBR.                                             
      *                                                                         
           EXEC SQL                                                             
               SELECT  FSCL_MN_NBR                                              
                INTO  :PV-TEMP-PREV-SEA-MN-NBR                                  
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-TEMP-PREV-SEA-END-DTE                       
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  DISPLAY '********************************************'        
                  DISPLAY 'PREV SEA END DTE:  ' PV-TEMP-PREV-SEA-END-DTE        
                  DISPLAY ' '                                                   
                  MOVE '7114-SELECT-PREV-SEA-MN-NBR'                            
                        TO PV-PARAGRAPH-NAME                                    
                  MOVE PE-MSG-18 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * POPULATE INTERNAL TABLE WITH DATES RELATED EACH MONTH                   
      * TO BE RE-CALCULATED                                                     
      *****************************************************************         
       7125-LOAD-DATE-TABLE.                                                    
           MOVE PV-HOLD-DATE            TO DT-OPN-MN-END-DTE(PV-SUB1).          
JS0200     MOVE DTATTR-FSCL-MN-NBR      TO DT-OPN-MN-NBR(PV-SUB1).              
                                                                                
           MOVE DTATTR-CAL-MN-NBR       TO DT-CAL-MN-NBR(PV-SUB1).              
           MOVE DTATTR-PFM-END-DTE      TO DT-PFM-END-DTE(PV-SUB1)              
           MOVE DTATTR-SEA-BEG-DTE      TO DT-SEA-BEG-DTE(PV-SUB1).             
           MOVE DTATTR-PREV-SEA-END-DTE TO DT-PREV-SEA-END-DTE(PV-SUB1).        
           MOVE DTATTR-FSCL-MN-NBR      TO DT-FSCL-MN-NBR(PV-SUB1).             
           MOVE DTATTR-FSCL-YR-NBR      TO DT-FSCL-YR-NBR(PV-SUB1).             
                                                                                
           MOVE DTATTR-SEA-BEG-DTE      TO PV-TEMP-SEA-BEG-DTE.                 
JS0200     PERFORM 7110-SELECT-SEA-MN-NBR.                                      
           MOVE PV-TEMP-SEA-BEG-MN-NBR TO DT-SEA-BEG-MN-NBR(PV-SUB1)            
                                                                                
           MOVE DTATTR-PFM-END-DTE      TO PV-TEMP-PFM-END-DTE.                 
JS0200     PERFORM 7112-SELECT-PREV-MN-NBR.                                     
           MOVE PV-TEMP-PFM-MN-NBR      TO DT-PFM-MN-NBR(PV-SUB1).              
                                                                                
           MOVE DTATTR-PREV-SEA-END-DTE TO PV-TEMP-PREV-SEA-END-DTE.            
JS0200     PERFORM 7114-SELECT-PREV-SEA-MN-NBR.                                 
           MOVE PV-TEMP-PREV-SEA-MN-NBR TO DT-PREV-SEA-MN-NBR(PV-SUB1).         
                                                                                
JS0200     PERFORM 7150-GET-SEASON-DATE-RANGE.                                  
                                                                                
                                                                                
                                                                                
      *    ********************                                                 
      *    **  DISPLAY FIELDS                                                   
      *    ********************                                                 
                                                                                
           DISPLAY '--------------------------------------------'               
           DISPLAY '    OPN MN END DTE    ' DT-OPN-MN-END-DTE(PV-SUB1).         
JS0200     DISPLAY '    OPN MN END NBR    ' DT-OPN-MN-NBR(PV-SUB1).             
                                                                                
           DISPLAY '    CAL MN NBR        ' DT-CAL-MN-NBR(PV-SUB1).             
                                                                                
           DISPLAY '    PREV FSCL END DTE ' DT-PFM-END-DTE(PV-SUB1)             
           DISPLAY '    PREV FSCL MN NBR  ' DT-PFM-MN-NBR(PV-SUB1).             
                                                                                
           DISPLAY '    SEA BEG DTE       ' DT-SEA-BEG-DTE(PV-SUB1).            
           DISPLAY '    SEA BEG MN NBR    ' DT-SEA-BEG-MN-NBR(PV-SUB1)          
                                                                                
           DISPLAY '    PREV SEA END DTE  '                                     
                   DT-PREV-SEA-END-DTE(PV-SUB1).                                
           DISPLAY '    PREV SEA MN NBR   '                                     
                   DT-PREV-SEA-MN-NBR(PV-SUB1).                                 
                                                                                
           DISPLAY '    FSCL MN NBR       ' DT-FSCL-MN-NBR(PV-SUB1).            
           DISPLAY '    FSCL YR NBR       ' DT-FSCL-YR-NBR(PV-SUB1).            
                                                                                
                                                                                
           IF PS-SEASON-DATE-TABLE-LOADED                                       
               SUBTRACT 2 FROM PV-SEA-SUB                                       
           ELSE                                                                 
               SUBTRACT 1 FROM PV-SEA-SUB                                       
           END-IF.                                                              
                                                                                
           DISPLAY ' '                                                          
           DISPLAY '    NBR OF SEA MTHS PROCESSED FOR THIS DATE: ',             
                            PV-SEA-SUB                                          
           IF PV-SEA-SUB > 0                                                    
               DISPLAY '        ' PV-TEMP-SEA-BEG-DTE ' THRU ' ,                
                             PV-HOLD-DATE                                       
               DISPLAY ' '                                                      
               DISPLAY '       1ST MTH DTE '  DT-SEA-MN1-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN1-NBR(PV-SUB1)               
               DISPLAY '       2ND MTH DTE '  DT-SEA-MN2-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN2-NBR(PV-SUB1)               
               DISPLAY '       3RD MTH DTE '  DT-SEA-MN3-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN3-NBR(PV-SUB1)               
               DISPLAY '       4TH MTH DTE '  DT-SEA-MN4-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN4-NBR(PV-SUB1)               
               DISPLAY '       5TH MTH DTE '  DT-SEA-MN5-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN5-NBR(PV-SUB1)               
               DISPLAY '       6TH MTH DTE '  DT-SEA-MN6-DTE(PV-SUB1)           
                       '   MTH NBR '      DT-SEA-MN6-NBR(PV-SUB1)               
           ELSE                                                                 
               DISPLAY ' ************************'                              
               DISPLAY '   NO SEASONAL DATA EXISTS '                            
               MOVE '7125-LOAD-DATE-TABLE' TO PV-PARAGRAPH-NAME                 
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
                                                                                
JS0200 7150-GET-SEASON-DATE-RANGE.                                              
                                                                                
           SET PS-SEASON-DATE-TAB-NOT-LOADED TO TRUE.                           
           PERFORM 7152-OPN-SEA-DTE-RANGE-CUR.                                  
           PERFORM 7154-PROCESS-SEA-DATE-CURSOR                                 
               VARYING PV-SEA-SUB FROM 1 BY 1 UNTIL PV-SEA-SUB > 6              
               OR                                                               
               PS-SEASON-DATE-TABLE-LOADED.                                     
           PERFORM 7158-CLOSE-SEA-DTE-RANGE-CUR.                                
                                                                                
      *                                                                         
      *****************************************************************         
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT            
      *****************************************************************         
       7152-OPN-SEA-DTE-RANGE-CUR.                                              
      *                                                                         
           EXEC SQL                                                             
               OPEN SEASON_DATE_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7152-OPEN-SEASON-CSR' TO PV-PARAGRAPH-NAME              
                  MOVE PE-MSG-20 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       7154-PROCESS-SEA-DATE-CURSOR.                                            
      *                                                                         
           EXEC SQL                                                             
                  FETCH SEASON_DATE_CSR                                         
                   INTO :PV-SEASON-MN-DTE                                       
                       ,:PV-SEASON-MN-NBR                                       
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   PERFORM 7156-LOAD-SEA-DATE-RANGE-TABLE                       
               WHEN +100                                                        
                   SET PS-SEASON-DATE-TABLE-LOADED TO TRUE                      
               WHEN OTHER                                                       
                   MOVE '7154-PROCESS-SEA-DATE-CSR'                             
                        TO PV-PARAGRAPH-NAME                                    
                   MOVE PE-MSG-21 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       7156-LOAD-SEA-DATE-RANGE-TABLE.                                          
                                                                                
           EVALUATE PV-SEA-SUB                                                  
           WHEN 1                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN1-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN1-NBR(PV-SUB1)             
           WHEN 2                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN2-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN2-NBR(PV-SUB1)             
           WHEN 3                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN3-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN3-NBR(PV-SUB1)             
           WHEN 4                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN4-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN4-NBR(PV-SUB1)             
           WHEN 5                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN5-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN5-NBR(PV-SUB1)             
           WHEN 6                                                               
JS0200         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN6-DTE(PV-SUB1)             
JS0200         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN6-NBR(PV-SUB1)             
           WHEN OTHER                                                           
               DISPLAY '**********************************************'         
               DISPLAY 'SEASONS PROCESSED > 6'                                  
               DISPLAY 'SEASONS PROCESSED = ' PV-SEA-SUB                        
               MOVE '7156-LOAD-SEA-DATE-RANGE-TABLE'                            
                   TO PV-PARAGRAPH-NAME                                         
               PERFORM 9999-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      *****************************************************************         
      * CLOSE CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT           
      *****************************************************************         
       7158-CLOSE-SEA-DTE-RANGE-CUR.                                            
      *                                                                         
           EXEC SQL                                                             
               CLOSE SEASON_DATE_CSR                                            
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7158-CLOSE-SEASON-CSR' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-23 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * RETRIEVES CURRENT MONTH SHRINK DOLLARS FROM TWKSHNK                     
      *****************************************************************         
       7200-OBTAIN-SHRINK-DLRS.                                                 
                                                                                
                                                                                
                                                                                
      ***   DISPLAY 'PV-TEST-STR-NUM: ' PV-TEST-STR-NUM                         
      ***           ' PV-TEST-STR-X ' PV-TEST-STR-X                             
      ***           ' PV-TEST-STR-FIRST2 ' PV-TEST-STR-FIRST2.                  
      ***   DISPLAY 'TMNSCL: ' PV-HOLD-DEPT-NBR ' ' PV-TEST-STR-LAST2           
      ***           ' ' PV-HOLD-OPN-MN-END-DTE.                                 
      ***                                                                       
                                                                                
TEMP1 ***   MOVE PV-HOLD-STR-NBR TO PV-TEST-STR-NUM.                            
TEMP1 ***   EXEC SQL                                                            
TEMP1 ***       SELECT SUM(SHNK_RTL_AMT)                                        
TEMP1 ***        INTO  :PV-SHNK-RTL-AMT                                         
TEMP1 ***        FROM  TMNSCL                                                   
TEMP1 ***       WHERE DEPT_NBR        = :PV-HOLD-DEPT-NBR                       
TEMP1 ***         AND SUB_CL_NBR      = :PV-TEST-STR-LAST2                      
TEMP1 ***         AND FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE                 
TEMP1 ***   END-EXEC.                                                           
TEMP1 ***                                                                       
                                                                                
                                                                                
TEMP2 ***   EXEC SQL                                                            
TEMP2 ***       SELECT SUM(SHNK_RTL_AMT)                                        
TEMP2 ***        INTO  :PV-SHNK-RTL-AMT                                         
TEMP2 ***        FROM  TMNSCLS                                                  
TEMP2 ***       WHERE FSCL_MN_NBR     = :PV-HOLD-FSCL-MN-NBR                    
TEMP2 ***         AND FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE                 
TEMP2 ***         AND DEPT_NBR        = :PV-HOLD-DEPT-NBR                       
TEMP2 ***         AND SUB_CL_NBR BETWEEN :PV-HOLD-MAJ-CL-NBR                    
TEMP2 ***                            AND :PV-HOLD-MAX-MAJ-CL-NBR                
TEMP2 ***         AND STR_NBR         = :PV-HOLD-STR-NBR                        
TEMP2 ***   END-EXEC.                                                           
                                                                                
                                                                                
                                                                                
PROD                                                                            
PROD        EXEC SQL                                                            
PROD            SELECT SUM(SHNK_RTL_AMT)                                        
PROD             INTO  :PV-SHNK-RTL-AMT                                         
W31475                 :WS-SHNK-RTL-AMT-NULL-IND                                
PROD             FROM  TWKSHNK                                                  
PROD           WHERE  FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE                 
PROD             AND  DEPT_NBR        = :PV-HOLD-DEPT-NBR                       
PROD              AND SUB_CL_NBR BETWEEN :PV-HOLD-MAJ-CL-NBR                    
PROD                                 AND :PV-HOLD-MAX-MAJ-CL-NBR                
PROD              AND STR_NBR         = :PV-HOLD-STR-NBR                        
PROD        END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
W31475             IF WS-SHNK-RTL-AMT-NULL-IND = -1                             
  |                   MOVE ZERO TO PV-SHNK-RTL-AMT                              
  |                END-IF                                                       
  |   *            CONTINUE                                                     
  |   *        WHEN -305                                                        
W31475*            MOVE +0            TO PV-SHNK-RTL-AMT                        
               WHEN OTHER                                                       
                  MOVE '7200-OBTAIN-SHRINK-DLRS' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * SELECT A ROW FROM DB2 TO BE RECALCED & UPDATED (OR INSERT/UPD)          
      *****************************************************************         
       7300-SELECT-CURRENT-DOLLARS.                                             
      *                                                                         
W31475     MOVE 'N'    TO PS-CURRENT-PURCH-COST-SW.                             
W31475*                                                                         
           EXEC SQL                                                             
                SELECT  SLS_RTL_AMT                                             
                       ,MKDN_RTL_AMT                                            
                       ,MKUP_RTL_AMT                                            
                       ,XFER_IN_RTL_AMT                                         
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,PADJ_RTL_AMT                                            
                       ,INV_ADJ_RTL_AMT                                         
W31475                 ,RCPT_NET_COST_AMT                                       
W31475                 ,PADJ_NET_COST_AMT                                       
                  INTO :MNMCLS-SLS-RTL-AMT                                      
                      ,:MNMCLS-MKDN-RTL-AMT                                     
                      ,:MNMCLS-MKUP-RTL-AMT                                     
                      ,:MNMCLS-XFER-IN-RTL-AMT                                  
                      ,:MNMCLS-XFER-OUT-RTL-AMT                                 
                      ,:MNMCLS-RCPT-RTL-AMT                                     
                      ,:MNMCLS-PADJ-RTL-AMT                                     
                      ,:MNMCLS-INV-ADJ-RTL-AMT                                  
W31475                ,:PV-CURR-MONTH-RCPT-NET-COST                             
W31475                ,:PV-CURR-MONTH-PADJ-NET-COST                             
                  FROM TMNMCLS                                                  
                 WHERE FSCL_MN_NBR     = :PV-HOLD-FSCL-MN-NBR                   
                   AND FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE                
                   AND DEPT_NBR        = :PV-HOLD-DEPT-NBR                      
                   AND MAJ_CL_NBR      = :PV-HOLD-MAJ-CL-NBR                    
                   AND STR_NBR         = :PV-HOLD-STR-NBR                       
           END-EXEC                                                             
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                 SET PS-CURR-MONTH-ROW-NOT-FOUND   TO TRUE                      
W31475           SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE                      
               WHEN OTHER                                                       
                   MOVE '7300-SELECT-CURRENT'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
             END-EVALUATE.                                                      
      *                                                                         
      *                                                                         
      ******************************************************************        
      * RETRIEVE LAST MONTH'S ENDING INVENTORY RETAIL/COST                      
      *     THIS IS ALSO THE BEGINNING INVENTORY FOR THIS MONTH                 
      ******************************************************************        
       7350-SELECT-PREV-MONTH-END-INV.                                          
      *                                                                         
           EXEC SQL                                                             
                SELECT  END_INV_RTL_AMT                                         
                      , END_INV_COST_AMT                                        
                  INTO  :PV-PREV-MONTH-END-INV-RTL                              
                       ,:PV-PREV-MONTH-END-INV-COST                             
                  FROM  TMNMCLS                                                 
JS0200           WHERE (FSCL_MN_NBR = :PV-HOLD-PFM-MN-NBR)                      
JS0200             AND (FSCL_MN_END_DTE = :PV-HOLD-PFM-END-DTE)                 
                   AND (       DEPT_NBR = :PV-HOLD-DEPT-NBR)                    
                   AND (     MAJ_CL_NBR = :PV-HOLD-MAJ-CL-NBR)                  
                   AND (     STR_NBR    = :PV-HOLD-STR-NBR)                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   IF ((PV-PREV-MONTH-END-INV-RTL  NOT = 0) OR                  
                       (PV-PREV-MONTH-END-INV-COST NOT = 0))                    
                       SET PS-PFM-INV-DOLLARS-FOUND TO TRUE                     
                   END-IF                                                       
               WHEN +100                                                        
                   SET PS-PREV-MONTH-ROW-NOT-FOUND TO TRUE                      
                   MOVE ZEROES TO PV-PREV-MONTH-END-INV-RTL                     
                                  PV-PREV-MONTH-END-INV-COST                    
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7350-SELECT-PREV-MONTH-END-INV'                       
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7400-PREV-SEASON-END-INV SELECTS THE ENDING INVENTORY COST &   *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
       7400-PREV-SEASON-END-INV.                                                
      *                                                                         
           MOVE 'N'    TO PS-PREV-SEASON-INV-SW.                                
      *                                                                         
           EXEC SQL                                                             
                SELECT  END_INV_RTL_AMT                                         
                       ,END_INV_COST_AMT                                        
                  INTO  :MNMCLS-END-INV-RTL-AMT                                 
                       ,:MNMCLS-END-INV-COST-AMT                                
                  FROM  TMNMCLS                                                 
                 WHERE (FSCL_MN_NBR = :PV-HOLD-PREV-SEA-MN-NBR)                 
                   AND (FSCL_MN_END_DTE = :PV-HOLD-PREV-SEA-END-DTE)            
                   AND (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                    
                   AND (MAJ_CL_NBR =      :PV-HOLD-MAJ-CL-NBR)                  
                   AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET PS-NO-PREV-SEASONAL-INV       TO TRUE                    
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7400-PREV-SEASON-END-INV'                             
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      * 7450-PROCESS-SEASON-CURSOR WILL RETRIEVE ALL ROWS WITH AN ENDING        
      * INVENTORY AMOUNT FOR EACH MONTH END DATE FOR THE CURRENT SEASON.        
      ******************************************************************        
       7450-GET-SEASONAL-MTH.                                                   
                                                                                
           IF PV-SEA-SUB = 1                                                    
               AND DT-SEA-MN1-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN1-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN1-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
           IF PV-SEA-SUB = 2                                                    
               AND DT-SEA-MN2-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN2-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN2-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
           IF PV-SEA-SUB = 3                                                    
               AND DT-SEA-MN3-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN3-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN3-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
           IF PV-SEA-SUB = 4                                                    
               AND DT-SEA-MN4-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN4-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN4-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
           IF PV-SEA-SUB = 5                                                    
               AND DT-SEA-MN5-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN5-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN5-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
           IF PV-SEA-SUB = 6                                                    
               AND DT-SEA-MN6-DTE (PV-SUB1) NOT EQUAL SPACES                    
               MOVE DT-SEA-MN6-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2              
               MOVE DT-SEA-MN6-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2              
           ELSE                                                                 
               SET PS-SEASON-MTH-NOT-FOUND TO TRUE                              
               SET PS-END-OF-CURR-SEASON-MTHS TO TRUE.                          
                                                                                
                                                                                
           IF PS-NOT-END-OF-CURR-SEASON-MTHS                                    
               EXEC SQL                                                         
               SELECT  XFER_IN_RTL_AMT                                          
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,RCPT_NET_COST_AMT                                       
                       ,PADJ_RTL_AMT                                            
                       ,PADJ_NET_COST_AMT                                       
                       ,MKUP_RTL_AMT                                            
                 INTO  :MNMCLS-XFER-IN-RTL-AMT                                  
                      ,:MNMCLS-XFER-OUT-RTL-AMT                                 
                      ,:MNMCLS-RCPT-RTL-AMT                                     
                      ,:MNMCLS-RCPT-NET-COST-AMT                                
                      ,:MNMCLS-PADJ-RTL-AMT                                     
                      ,:MNMCLS-PADJ-NET-COST-AMT                                
                      ,:MNMCLS-MKUP-RTL-AMT                                     
                 FROM  TMNMCLS                                                  
                 WHERE (FSCL_MN_NBR =         :PV-SEA-MTH-NBR-DB2)              
                       AND (FSCL_MN_END_DTE = :PV-SEA-MTH-DTE-DB2)              
                       AND (DEPT_NBR   =      :PV-HOLD-DEPT-NBR)                
                       AND (MAJ_CL_NBR =      :PV-HOLD-MAJ-CL-NBR)              
                       AND (STR_NBR    =      :PV-HOLD-STR-NBR)                 
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                    WHEN 0                                                      
                        SET PS-SEASON-MTH-FOUND TO TRUE                         
                    WHEN +100                                                   
                        SET PS-SEASON-MTH-NOT-FOUND TO TRUE                     
                    WHEN OTHER                                                  
                        MOVE PE-MSG-04      TO PV-OPERATION-ATTEMPTED           
                        MOVE '7450-GET-SEASONAL-MTH'                            
                                                   TO PV-PARAGRAPH-NAME         
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7475-CURR-SEASON-COST SELECTS THE ENDING INVENTORY COST &      *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
W31475*7475-CURR-SEASON-COST.                                                   
  |   *                                                                         
  |   *    MOVE 'N'    TO PS-CURRENT-PURCH-COST-SW.                             
  |   *                                                                         
  |   *    EXEC SQL                                                             
  |   *         SELECT  RCPT_NET_COST_AMT                                       
  |   *                ,PADJ_NET_COST_AMT                                       
  |   *           INTO  :PV-CURR-MONTH-RCPT-NET-COST                            
  |   *                ,:PV-CURR-MONTH-PADJ-NET-COST                            
  |   *           FROM  TMNMCLS                                                 
  |   *          WHERE (FSCL_MN_NBR     = :PV-HOLD-FSCL-MN-NBR)                 
  |   *            AND (FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE)              
  |   *            AND (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                    
  |   *            AND (MAJ_CL_NBR =      :PV-HOLD-MAJ-CL-NBR)                  
  |   *            AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    EVALUATE SQLCODE                                                     
  |   *        WHEN 0                                                           
  |   *            CONTINUE                                                     
  |   *        WHEN +100                                                        
  |   *            SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE                    
  |   *        WHEN OTHER                                                       
  |   *            MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
  |   *            MOVE  '7475-CURRENT-SEASON-COST'                             
  |   *                                      TO PV-PARAGRAPH-NAME               
  |   *            PERFORM 9999-SQL-ABEND                                       
W31475*    END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * UPDATE COLUMNS WITH VALUES JUST CALCULATED FOR THIS DEPARTMENT          
      ******************************************************************        
       7500-UPDATE-PROCESSING.                                                  
      *                                                                         
           EXEC SQL                                                             
               UPDATE TMNMCLS                                                   
                  SET  SHNK_RTL_AMT     = :PV-SHNK-RTL-AMT                      
                      ,END_INV_RTL_AMT  = :PV-END-INV-RTL-AMT                   
                      ,CUM_MKUP_PCT     = :PV-CUM-MKUP-PCT                      
                      ,END_INV_COST_AMT = :PV-END-INV-COST-AMT                  
                      ,SLS_COST_AMT     = :PV-SLS-COST-AMT                      
                      ,GRO_MRGN_AMT     = :PV-GRO-MRGN-AMT                      
                WHERE FSCL_MN_NBR       = :PV-HOLD-FSCL-MN-NBR                  
                  AND FSCL_MN_END_DTE   = :PV-HOLD-OPN-MN-END-DTE               
                  AND DEPT_NBR          = :PV-HOLD-DEPT-NBR                     
                  AND MAJ_CL_NBR        = :PV-HOLD-MAJ-CL-NBR                   
                  AND STR_NBR           = :PV-HOLD-STR-NBR                      
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    ADD 1                TO PA-UPDATE-COUNT                     
               WHEN OTHER                                                       
                    DISPLAY '*************************************'             
                    DISPLAY 'ROW IN PROCESS  : ', PV-HOLD-ROW                   
                    DISPLAY 'MONTH-END-DATE  : ',                               
                                              PV-HOLD-OPN-MN-END-DTE            
                    DISPLAY 'FISCAL MONTH NBR: ',                               
                                                 PV-HOLD-FSCL-MN-NBR            
                    MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED             
                    MOVE '7500-UPDATE-PROCESSING'                               
                                          TO PV-PARAGRAPH-NAME                  
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      *   GET THE TIMESTAMP FROM THE CHECKPOINT TABLE FOR NEXT COMMIT  *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
      *                                                                         
W31475*    EXEC SQL                                                             
  |   *      SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
  |   *        INTO :PV-COMMIT-TIMESTAMP                                        
  |   *        FROM TCKRSTCNTL                                                  
  |   *       WHERE JOB_NAME  = :PC-JOB-NAME                                    
  |   *         AND PLAN_NAME = :PC-PROGRAM-NAME                                
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    EVALUATE TRUE                                                        
  |   *        WHEN SQLCODE = 0                                                 
  |   *            CONTINUE                                                     
  |   *        WHEN SQLCODE NOT EQUAL ZERO                                      
  |   *            MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
  |   *            MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED         
  |   *            PERFORM 9999-SQL-ABEND                                       
  |   *    END-EVALUATE.                                                        
  |        ACCEPT WS-COMMIT-TIME FROM TIME.                                     
  |        MOVE WS-COMMIT-TIME   TO WS-HCOMMIT-TIME.                            
  |        ADD TCKRSTCNTL-CKPT-FRQNCY-QTY                                       
W31475                           TO WS-HCOMMIT-SEC.                             
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * CHECK STATUS CODE TO SEE IF THIS IS A RESTART CONDITION        *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
      *                                                                         
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * OBTAIN INFORMATION REGARDING WHERE TO RESTART TABLE PROCESSING *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
      *                                                                         
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * CURRENT TIMESTAMP SET TO BE COMPARED TO COMMIT TIMESTAMP       *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
      *                                                                         
W31475*    EXEC SQL                                                             
  |   *         SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    IF SQLCODE = 0                                                       
  |   *        CONTINUE                                                         
  |   *    ELSE                                                                 
  |   *        MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED             
  |   *        MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
  |   *        PERFORM 9999-SQL-ABEND                                           
  |   *    END-IF.                                                              
  |        ACCEPT WS-CURRENT-TIME FROM TIME.                                    
W31475*                                                                         
      *                                                                         
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * COMPARE COMMIT TIMESTAMP TO CURRENT TO DETERMINE IF COMMIT REQD*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
      *                                                                         
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
      *                                                                         
W31475*    IF (PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP)  OR                  
W31475     IF ((WS-CURRENT-TIME > WS-HCOMMIT-TIME) OR                           
W31475         (WS-CURRENT-TIME < WS-COMMIT-TIME)) OR                           
              (PS-EOF-EXTR-FILE)                                                
      * PREPARE COMMIT RECORD FOR UPDATE                                        
               ADD 1                          TO PA-COMMIT-COUNT                
               MOVE PV-HOLD-OPN-MN-END-DTE    TO CR-FISCAL-MN-END-DTE           
               MOVE PV-HOLD-FSCL-MN-NBR       TO CR-FISCAL-MN-NBR               
               MOVE PV-HOLD-DEPT-NBR          TO CR-DEPT-NBR                    
               MOVE PV-HOLD-MAJ-CL-NBR        TO CR-MAJ-CL-NBR                  
               MOVE PV-HOLD-STR-NBR           TO CR-STR-NBR                     
               MOVE PA-EXTRACT-RECS-READ      TO CR-TMNMCLS-INPUT-COUNT         
               MOVE PA-UPDATE-COUNT           TO CR-TMNMCLS-UPDATE-COUNT        
               MOVE PA-INSERT-COUNT           TO CR-TMNMCLS-INSERT-COUNT        
               MOVE PA-COMMIT-COUNT           TO CR-TMNMCLS-COMMIT-COUNT        
      * MOVE COMMIT INFO TO WORKING STORAGE ROW                                 
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                                TCKRSTINF-WORK-STRG-DESC        
               MOVE 'Y' TO PS-COMMIT-TAKEN-SW                                   
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
      *                                                                         
               PERFORM 7850-COMMIT-CHANGES                                      
      *                                                                         
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.                     
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
      *                                                                         
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * UPDATE CHECKPOINT STATUS                                                
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
      *                                                                         
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED        
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           EJECT                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7950-INSERT-CARRY-FWD-ROW                                               
      * PERFORMED IF NO ROW IS FOUND ON DB2 (FOR THIS PROCESSING MONTH)         
      * BUT INVENTORY DOLLARS (COST AND/OR RETAIL) FOUND FOR LAST MONTH         
      ******************************************************************        
       7950-INSERT-CARRY-FWD-ROW.                                               
      *                                                                         
      ***  DISPLAY 'INSERTED ROW : ', PV-HOLD-DEPT-NBR, '-',                    
      ***    PV-HOLD-MAJ-CL-NBR, ' ', PV-HOLD-OPN-MN-END-DTE                    
      ***    ' STR: ' PV-HOLD-STR-NBR                                           
      *                                                                         
           EXEC SQL                                                             
               INSERT INTO TMNMCLS                                              
                   ( FSCL_MN_NBR                                                
                    ,FSCL_MN_END_DTE                                            
                    ,DEPT_NBR                                                   
                    ,MAJ_CL_NBR                                                 
                    ,STR_NBR                                                    
                    ,SLS_RTL_AMT                                                
                    ,MKDN_RTL_AMT                                               
                    ,MKUP_RTL_AMT                                               
                    ,XFER_IN_RTL_AMT                                            
                    ,XFER_OUT_RTL_AMT                                           
                    ,RCPT_RTL_AMT                                               
                    ,RCPT_NET_COST_AMT                                          
                    ,PADJ_RTL_AMT                                               
                    ,PADJ_NET_COST_AMT                                          
                    ,INV_ADJ_RTL_AMT                                            
                    ,SHNK_RTL_AMT                                               
                    ,END_INV_RTL_AMT                                            
                    ,CUM_MKUP_PCT                                               
                    ,END_INV_COST_AMT                                           
                    ,SLS_COST_AMT                                               
                    ,GRO_MRGN_AMT )                                             
               VALUES (:PV-HOLD-FSCL-MN-NBR                                     
                      ,:PV-HOLD-OPN-MN-END-DTE                                  
                      ,:PV-HOLD-DEPT-NBR                                        
                      ,:PV-HOLD-MAJ-CL-NBR                                      
                      ,:PV-HOLD-STR-NBR                                         
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,:PV-PREV-MONTH-END-INV-RTL                               
                      ,0                                                        
                      ,:PV-PREV-MONTH-END-INV-COST                              
                      ,0                                                        
                      ,0  )                                                     
           END-EXEC                                                             
      *                                                                         
           IF SQLCODE = 0                                                       
               ADD 1                      TO PA-INSERT-COUNT                    
           ELSE                                                                 
               MOVE '7950-INSERT-CARRY-FWD-ROW' TO PV-PARAGRAPH-NAME            
               MOVE PE-MSG-14             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT. *        
      ******************************************************************        
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
      *                                                                         
      *    COMMIT ANY FINAL CHANGES THAT MAY HAVE BEEN MADE                     
                                                                                
           PERFORM 7800-COMMIT-PROCESSING                                       
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
           CLOSE MNMCLS-EXTR-FILE.                                              
      *                                                                         
           DISPLAY '                                             '.             
           DISPLAY '******* END OF JOB STATISTICS ********'.                    
           DISPLAY '                                             '.             
JS0100     DISPLAY 'INPUT EXTRACT RECORDS READ ' PA-EXTRACT-RECS-READ.          
           DISPLAY '  TMNMCLS ROWS UPDATED   ', PA-UPDATE-COUNT.                
           DISPLAY '  TMNMCLS ROWS INSERTED  ', PA-INSERT-COUNT.                
           DISPLAY '                                             '.             
           DISPLAY '  COMMITS TAKEN         ', PA-COMMIT-COUNT.                 
           DISPLAY '                                             '.             
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
      *                                                                         
      ******************************************************************00000100
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM. 00000200
      ******************************************************************00000300
       9999-ABEND.                                                      00000400
                                                                        00000500
           DISPLAY '**  ABEND     **'.                                  00000700
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            00000900
           DISPLAY '**'                                                 00001100
           DISPLAY '**'                                                 00001200
           DISPLAY '**********************************'                 00001300
           CALL 'ILBOABN0'  USING ABEND-CODE.                           00002000
                                                                        00010000
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
