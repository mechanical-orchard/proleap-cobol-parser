       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUT631.                                                 
       AUTHOR.        JOHN SELWITSCHKA.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02-06-11.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *   THIS IS A CLONE OF MLKUP613 WITH CHANGES FOR BRAND VENDOR             
      *                                                                         
      *   MLKUT631 - FORMAT THE CONSOLIDATION FILE FOR DETAIL LEDGER            
      *                                                                         
      * THIS PROGRAM READS 4 TRANSACTION EXTRACT FILES LISTED BELOW.            
      *                                                                         
      *    1) PURCHASES (INCLUDING TRANSFER FROM / TRANSFER TO)                 
      *    2) PRICE CHANGES (INCLUDING MARKDOWNS / MARKUPS) PRORATES            
      *          DOLLARS ACROSS ALL VENDORS IN A DEPT/SUBCLASS WHEN             
      *          WHEN A VALID SKU IS NOT AVAILABLE TO GET ENT-ID                
      *          PRORATION IS BASED ON CURRENT OH INVENTORY DOLLARS.            
      *    3) INVENTORY ADJUSTMENTS (ONLY HAS RECS AT YR END AFTER INV)         
      *    4) SALES                                                             
      *                                                                         
      * EACH EXTRACT FILE WILL BE OPENED SEPARATELY, AND READ UNTIL ALL         
      * RECORDS ON THE FILE HAVE BEEN PROCESSED AND REFORMATTED TO THE          
      * MLRD154 LAYOUT.                                                         
      *                                                                         
      * EACH EXTRACT FILE WILL PROCESSED IN THE SAME FASHION, FORMATTING        
      * THE DATA FIELDS AS EACH TRAN CODE TYPE/EXTRACT FILE IS PROCESSED        
      *                                                                         
      * ALL RECORD TYPES (EXCEPT SALES) WILL UTILIZE THE PROCESSING DATE        
      * TO APPEND FISCAL CALENDAR INFO TO OUTPUT. SO THE SALES FILE WILL        
      * BE PROCESSED LAST SINCE CALENDAR ROUTINE VARIABLES WILL CHANGE          
      * DEPENDING ON THE FINANCIAL LIABILITY DATE READ ON THE INPUT FILE        
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W37965*W37965  02-06-2011 J SELWITSCHKA                                         
W37965*                               REMOVE LOGIC TO ASSIGN MARKDOWNS.         
W37965*                               REMOVE LOGIC TO ASSIGN RTV AMOUNTS        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PURCHASE-FILE                                                 
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PRICE-CHANGE-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ADJUSTMENT-FILE                                               
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT SALES-FILE                                                    
               ASSIGN TO INP04.                                                 
                                                                                
           SELECT OPTIONAL CONTROL-CARD-FILE                                    
               ASSIGN TO INP05.                                                 
                                                                                
           SELECT CONDENSED-OUTPUT-FILE                                         
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PURCHASE-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W28582 01  PURCHASE-RECORD                 PIC X(200).                          
                                                                                
       FD  PRICE-CHANGE-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W28582 01  PRICE-CHANGE-RECORD             PIC X(200).                          
                                                                                
       FD  ADJUSTMENT-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ADJUSTMENT-RECORD               PIC X(90).                           
                                                                                
       FD  SALES-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SALES-RECORD                    PIC X(80).                           
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-RECORD             PIC X(80).                           
                                                                                
       FD  CONDENSED-OUTPUT-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W28582 01  CONDENSED-OUTPUT-RECORD         PIC X(450).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                      PIC S9(04) VALUE ZERO                
                                                      COMP SYNC.                
                                                                                
       01  CC-CONTROL-CARD-REC.                                                 
           05  CC-BACKPOST-SALES-IND       PIC  X(01)      VALUE SPACES.        
               88  CC-BACKPOST-SALES                       VALUE 'Y'.           
               88  CC-DO-NOT-BACKPOST-SALES                VALUE 'N'.           
           05  FILLER                      PIC  X(01)      VALUE SPACES.        
           05  CC-PROCESS-DATE             PIC  X(10)      VALUE SPACES.        
           05  FILLER REDEFINES CC-PROCESS-DATE.                                
               10  CC-PROCESS-DATE-CCYY    PIC  X(04).                          
               10  FILLER                  PIC  X(01).                          
                   88  CC-VALID-PROCESS-DATE-DASH-1        VALUE '-'.           
               10  CC-PROCESS-DATE-MM      PIC  X(02).                          
               10  FILLER                  PIC  X(01).                          
                   88  CC-VALID-PROCESS-DATE-DASH-2        VALUE '-'.           
               10  CC-PROCESS-DATE-DD      PIC  X(02).                          
           05  FILLER                      PIC  X(68)      VALUE SPACES.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'MLKUT631'.         
           05  PC-MAX-LINES                PIC 9(02)  VALUE 60.                 
           05  PC-YES                      PIC X(01)  VALUE 'Y'.                
           05  PC-CONTINUED-CLEARANCE      PIC X(03)  VALUE '110'.              
           05  PC-UNKNOWN-VENDOR           PIC 9(09)  VALUE 5161.               
           05  PC-STORE-ZERO               PIC X(03)  VALUE '000'.              
           05  PC-CLEARANCE-STATUS         PIC S9(3)V VALUE 030                 
                                                      COMP-3.                   
           05  PC-CLEARANCE-STAT-CHAR      PIC X(03)  VALUE '30'.               
           05  PC-MIXED-STATUS             PIC S9(3)V VALUE 025                 
                                                      COMP-3.                   
           05  PC-DEBIT-ALLOW              PIC X(03)  VALUE '999'.              
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                   PIC X(30)  VALUE                     
                'INSPECT WHERE CLAUSE VARIABLE'.                                
           05  PE-MSG-02                   PIC X(40)  VALUE                     
                'SALES FINANCIAL LIABILITY DATE INVALID'.                       
           05  PE-MSG-03                   PIC X(40)  VALUE                     
                '                                      '.                       
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-EOF-SW         PIC X(01) VALUE 'N'.              
               88  PS-END-OF-CONTROL-CARD-FILE          VALUE 'Y'.              
                                                                                
           05  PS-PURCHASE-EOF-SW             PIC X(01) VALUE 'N'.              
               88  PS-NO-MORE-PURCHASES                 VALUE 'Y'.              
                                                                                
           05  PS-PCHNG-EOF-SW                PIC X(01) VALUE 'N'.              
               88  PS-NO-MORE-PRICE-CHANGES             VALUE 'Y'.              
                                                                                
           05  PS-ADJ-EOF-SW                  PIC X(01) VALUE 'N'.              
               88  PS-NO-MORE-ADJUSTMENTS               VALUE 'Y'.              
                                                                                
           05  PS-SALE-EOF-SW                 PIC X(01) VALUE 'N'.              
               88  PS-NO-MORE-SALES                     VALUE 'Y'.              
                                                                                
           05  PS-SALE-FIRST-TIME-SW          PIC X(01) VALUE 'N'.              
               88  PS-FIRST-SALES-REC                   VALUE 'Y'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACES.             
           05  PV-ABEND-MESSAGE            PIC X(75)  VALUE SPACES.             
           05  PV-OPERATION-ATTEMPTED      PIC X(40)  VALUE SPACES.             
           05  PV-PFW-WK-NBR               PIC X(02).                           
           05  PV-SAVE-SKU                 PIC X(08)  VALUE SPACES.             
           05  PV-SAVE-ENT-ID              PIC 9(09)  VALUE ZEROS.              
           05  PV-SAVE-DEPT-NBR            PIC 9(03)  VALUE ZEROS.              
           05  PV-SAVE-VENDOR-NBR          PIC X(05)  VALUE SPACES.             
           05  PV-SAVE-VENDOR-NBR-9 REDEFINES PV-SAVE-VENDOR-NBR                
                                           PIC 9(05).                           
           05  FILLER REDEFINES PV-SAVE-VENDOR-NBR.                             
               10  FILLER                  PIC X(02).                           
               10  PV-SAVE-INHSE-NBR       PIC X(03).                           
               10  PV-SAVE-INHSE-NBR-9 REDEFINES PV-SAVE-INHSE-NBR              
                                           PIC 9(03).                           
           05  PV-SAVE-CLASS-NBR           PIC 9(02)  VALUE ZEROS.              
           05  PV-SAVE-WK-PARTN-NBR        PIC S9(4)  COMP                      
                                                      VALUE ZEROS.              
           05  PV-SAVE-WK-DEPT-GP-CDE      PIC X(01)  VALUE SPACE.              
           05  PV-SAVE-MN-PARTN-NBR        PIC S9(4)  COMP                      
                                                      VALUE ZEROS.              
           05  PV-SAVE-MN-DEPT-GP-CDE      PIC X(01)  VALUE SPACE.              
                                                                                
           05  PV-SAVE-SALE-FIN-LIAB-DTE   PIC X(10)       VALUE SPACES.        
                                                                                
           05  PV-PURCH-CALC-FIELDS.                                            
               10  PV-PURCH-RTL-AMT       PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-PURCH-EXT-COST-AMT  PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-PURCH-GRO-COST-AMT  PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-PURCH-FRGT-COST-AMT PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-PURCH-DISC-COST-AMT PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-PURCH-NET-COST-AMT  PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-TRX-OUT-RTL-AMT     PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-TRX-IN-RTL-AMT      PIC S9(11)V99                         
                                              COMP-3  VALUE ZEROES.             
               10  PV-MKDN-RTL-AMT         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
               10  PV-MKUP-RTL-AMT         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PRORATE-MKDN         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PRORATE-MKUP         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
               10  PV-DISPLAY-VALUE        PIC ZZ,ZZZ,ZZZ,ZZ9.99-               
                                                           VALUE ZEROES.        
           05  PV-MAJ-CL-NBR-WORK.                                              
               10  PV-MAJ-CL-NBR-1        PIC 9(01)   VALUE 0.                  
               10  PV-MAJ-CL-NBR-2        PIC 9(01)   VALUE 0.                  
                                                                                
           05  PV-MAJ-CL-NBR              REDEFINES                             
               PV-MAJ-CL-NBR-WORK         PIC 9(02).                            
                                                                                
           05  PV-LOC-NBR                 PIC X(05).                            
           05  PV-LOC-NBR-2               REDEFINES                             
               PV-LOC-NBR                 PIC 9(05).                            
                                                                                
           05  PV-DEPT-NBR                PIC S9(4)V COMP-3                     
                                                      VALUE ZEROS.              
           05  PV-FSCL-MN-END-DTE         PIC X(10).                            
           05  PV-FSCL-WK-END-DTE         PIC X(10).                            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-COUNTS.                                                 
               10  PA-INPUT-COUNT-IN      PIC 9(11)   VALUE ZEROES.             
               10  PA-PURCHASE-COUNT-IN   PIC 9(11)   VALUE ZEROES.             
               10  PA-PURCH-EXTR-CNT-IN   PIC 9(11)   VALUE ZEROES.             
               10  PA-ADJUST-COUNT-IN     PIC 9(11)   VALUE ZEROES.             
               10  PA-PRICE-CHG-COUNT-IN  PIC 9(11)   VALUE ZEROES.             
               10  PA-SALES-COUNT-IN      PIC 9(11)   VALUE ZEROES.             
               10  PA-PARTN-REUSE         PIC 9(11)   VALUE ZEROES.             
               10  PA-PARTN-LOOKUP        PIC 9(11)   VALUE ZEROES.             
               10  PA-VENDOR-LOOKUP       PIC 9(11)   VALUE ZEROES.             
                                                                                
           05  PA-OUTPUT-COUNTS.                                                
               10  PA-OUTPUT-COUNT-OUT     PIC 9(11)       VALUE ZEROES.        
               10  PA-PURCHASE-COUNT-OUT   PIC 9(11)       VALUE ZEROES.        
               10  PA-PURCH-EXTR-CNT-OUT   PIC 9(11)       VALUE ZEROES.        
               10  PA-TRX-FROM-COUNT-OUT   PIC 9(11)       VALUE ZEROES.        
               10  PA-TRX-TO-COUNT-OUT     PIC 9(11)       VALUE ZEROES.        
               10  PA-PCHANGE-COUNT-OUT    PIC 9(11)       VALUE ZEROES.        
               10  PA-PCHANGE-PRORATED-OUT PIC 9(11)       VALUE ZEROES.        
               10  PA-UNK-VND-OUT          PIC 9(11)       VALUE ZEROES.        
               10  PA-MARKDOWN-COUNT-OUT   PIC 9(11)       VALUE ZEROES.        
               10  PA-MARKON-COUNT-OUT     PIC 9(11)       VALUE ZEROES.        
               10  PA-ADJUST-COUNT-OUT     PIC 9(11)       VALUE ZEROES.        
               10  PA-SALES-COUNT-OUT      PIC 9(11)       VALUE ZEROES.        
                                                                                
      ************************************************************              
      * PARTITION.                                                              
      ************************************************************              
           COPY MLWS500.                                                        
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
                                                                                
      ****************************************************                      
      * M/L OPEN FISCAL DATE INFORMATION TABLE           *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  M/L DATE TABLE                                  *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
26006 *01  ML501-PV-PROGRAM-VARIABLE.                                           
26006 *    TSKXREF                                                              
26006 *    TUPC                                                                 
26006 *    TVNDSTL                                                              
26006 *    TVNDDPT                                                              
26006      COPY MLWS501.                                                        
                                                                                
      ******************************************************************        
W28582* MLRD030 - PURCHASE AND TRANSFER EXTRACT RECORD LAYOUT                   
      ******************************************************************        
W28582     COPY MLRD030.                                                        
      *                                                                         
      ******************************************************************        
      * MLRD022 - SALES EXTRACT RECORD LAYOUT                                   
      ******************************************************************        
           COPY MLRD022.                                                        
      *                                                                         
      ******************************************************************        
W28582* MLRD029 - PRICE CHANGE EXTRACT RECORD LAYOUT (MARKDOWN/MARKUPS)         
      ******************************************************************        
W28582     COPY MLRD029.                                                        
      *                                                                         
      ******************************************************************        
      * MLRD024 - ADJUSTMENT EXTRACT                                            
      ******************************************************************        
           COPY MLRD024.                                                        
      *                                                                         
      ******************************************************************        
      * MLRD250 - INVENTORY / BOOKING ADJUSTMENT RECORD LAYOUT                  
      ******************************************************************        
           COPY MLRD250.                                                        
      *                                                                         
      ******************************************************************        
W28582* MLRD254 - DETAIL MERCH LEDGER EXTRACT RECORD LAYOUT                     
      ******************************************************************        
W28582     COPY MLRD254.                                                        
      *                                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * PROGRAM MAINLINE                                                        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2100-PROCESS-PURCHASE-EXTRACT.                               
           PERFORM 2200-PROCESS-PRICE-CHG-EXTRACT.                              
           PERFORM 2300-PROCESS-INV-ADJ-EXTRACT.                                
           PERFORM 2400-PROCESS-SALES-EXTRACT.                                  
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * PROCESS CONTROL CARD, INITIALIZE VARIABLES                              
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
                OUTPUT CONDENSED-OUTPUT-FILE.                                   
                                                                                
           PERFORM 4050-READ-CONTROL-CARD-FILE.                                 
           DISPLAY 'CONTROL CARD: ' CC-CONTROL-CARD-REC.                        
                                                                                
           IF CC-PROCESS-DATE-CCYY IS NUMERIC                                   
               AND CC-VALID-PROCESS-DATE-DASH-1                                 
               AND CC-PROCESS-DATE-MM IS NUMERIC                                
               AND CC-VALID-PROCESS-DATE-DASH-2                                 
               AND CC-PROCESS-DATE-DD IS NUMERIC                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE SPACES TO CC-PROCESS-DATE                                   
           END-IF.                                                              
           PERFORM 7000-READ-CONTROL-TABLE.                                     
                                                                                
                                                                                
           MOVE SPACES  TO CONDENSED-OUTPUT-RECORD.                             
           MOVE ZEROES  TO PA-PROGRAM-ACCUMULATORS.                             
                                                                                
           MOVE MLCNTL-PSTG-FSCL-MN-DTE TO ML500-PV-REQ-FSCL-END-DTE.           
           MOVE DTATTR-FSCL-MN-BEG-DTE  TO ML500-PV-REQ-FSCL-BEG-DTE.           
           MOVE MLCNTL-PSTG-FSCL-WK-DTE TO PV-FSCL-WK-END-DTE.                  
           MOVE MLCNTL-PSTG-FSCL-MN-DTE TO PV-FSCL-MN-END-DTE.                  
           PERFORM ML500-0100-LOAD-PARTN-TABLE.                                 
           IF ML500-PV-PARAGRAPH-NAME NOT EQUAL TO SPACES                       
             MOVE ML500-PV-PARAGRAPH-NAME TO PV-PARAGRAPH-NAME                  
             MOVE ML500-PV-OPERATION-ATTEMPTED TO PV-OPERATION-ATTEMPTED        
             PERFORM 9999-SQL-ABEND                                             
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * PROCESS PURCHASE FILE (INC. PURCHASE AND TRANSFER TRAN CODES)           
      ******************************************************************        
       2100-PROCESS-PURCHASE-EXTRACT.                                           
                                                                                
           OPEN INPUT PURCHASE-FILE.                                            
           PERFORM 4100-READ-PURCHASE-FILE.                                     
                                                                                
           MOVE ZEROES      TO PA-PARTN-LOOKUP                                  
                               PA-PARTN-REUSE                                   
                               PA-UNK-VND-OUT                                   
                               PA-VENDOR-LOOKUP.                                
           PERFORM 3100-PROCESS-PURCHASE-FILE                                   
               UNTIL PS-NO-MORE-PURCHASES.                                      
                                                                                
           COMPUTE PA-PURCH-EXTR-CNT-OUT = (  PA-PURCHASE-COUNT-OUT             
                                            + PA-TRX-FROM-COUNT-OUT             
                                            + PA-TRX-TO-COUNT-OUT  ).           
                                                                                
           DISPLAY ' ' FUNCTION CURRENT-DATE.                                   
           DISPLAY '********* PURCHASE STATS **********'.                       
           DISPLAY 'PURCHASE RECORDS READ = ', PA-PURCH-EXTR-CNT-IN.            
           DISPLAY 'PARTITION CNTL LOOKUP = ' PA-PARTN-LOOKUP.                  
           DISPLAY 'PARTITION CNTL REUSE  = ' PA-PARTN-REUSE.                   
           DISPLAY 'PARTITION CNTL ADDED  = ' ML500-PV-BAL-ROW-INSERTED.        
           DISPLAY 'VENDOR INFO LOOKUP    = ' PA-VENDOR-LOOKUP.                 
           DISPLAY '                                              '.            
           DISPLAY 'PURCHASE OUTPUT COUNT = ', PA-PURCHASE-COUNT-OUT.           
           DISPLAY 'TRX FROM OUTPUT COUNT = ', PA-TRX-FROM-COUNT-OUT.           
           DISPLAY 'TRX  TO  OUTPUT COUNT = ', PA-TRX-TO-COUNT-OUT.             
           DISPLAY '                        ---------'                          
           DISPLAY 'PURCHASE RECS WRITTEN = ', PA-PURCH-EXTR-CNT-OUT.           
           DISPLAY 'PURCHASE UNKN VENDORS = ' PA-UNK-VND-OUT.                   
                                                                                
           CLOSE PURCHASE-FILE.                                                 
                                                                                
      ******************************************************************        
      * PROCESS PRICE CHANGE FILE                                               
      ******************************************************************        
       2200-PROCESS-PRICE-CHG-EXTRACT.                                          
                                                                                
           OPEN INPUT PRICE-CHANGE-FILE.                                        
           PERFORM 4200-READ-PRICE-CHANGE-FILE.                                 
                                                                                
           MOVE ZEROES      TO PA-PARTN-LOOKUP                                  
                               PA-PARTN-REUSE                                   
                               PA-VENDOR-LOOKUP                                 
                               PA-UNK-VND-OUT.                                  
                                                                                
           PERFORM 3200-PROCESS-PRICE-CHANGE-FILE                               
               UNTIL PS-NO-MORE-PRICE-CHANGES.                                  
                                                                                
           COMPUTE PA-PCHANGE-COUNT-OUT  = (  PA-MARKDOWN-COUNT-OUT             
                                            + PA-MARKON-COUNT-OUT  ).           
                                                                                
           DISPLAY '  ' FUNCTION CURRENT-DATE.                                  
           DISPLAY '**** PRICE CHG STATS ***********'.                          
           DISPLAY 'EXTRACT  RECORDS READ = ' PA-PRICE-CHG-COUNT-IN.            
           DISPLAY 'PARTITION CNTL LOOKUP = ' PA-PARTN-LOOKUP.                  
           DISPLAY 'PARTITION CNTL REUSE  = ' PA-PARTN-REUSE.                   
           DISPLAY 'PARTITION CNTL ADDED  = ' ML500-PV-BAL-ROW-INSERTED.        
           DISPLAY 'VENDOR INFO LOOKUP    = ' PA-VENDOR-LOOKUP.                 
           DISPLAY '                                              '.            
           DISPLAY 'MARKDOWN OUTPUT COUNT = ' PA-MARKDOWN-COUNT-OUT.            
           DISPLAY 'MARKUPS  OUTPUT COUNT = ' PA-MARKON-COUNT-OUT.              
           DISPLAY '                       ----------'                          
           DISPLAY 'EXTRACT RECS WRITTEN  = ' PA-PCHANGE-COUNT-OUT.             
                                                                                
           DISPLAY '  '.                                                        
           MOVE PV-PRORATE-MKDN    TO PV-DISPLAY-VALUE.                         
           DISPLAY 'POTENTIAL MARKDOWN PRORATION = ' PV-DISPLAY-VALUE.          
           MOVE PV-PRORATE-MKUP    TO PV-DISPLAY-VALUE.                         
           DISPLAY 'POTENTIAL MARKUP PRORATION   = ' PV-DISPLAY-VALUE.          
           DISPLAY 'PRC CHGS UNKN VENDORS = ' PA-UNK-VND-OUT.                   
                                                                                
           CLOSE PRICE-CHANGE-FILE.                                             
                                                                                
      ************************************************************              
      * PROCESS INVENTORY ADJUSTMENT FILE                                       
      ************************************************************              
       2300-PROCESS-INV-ADJ-EXTRACT.                                            
                                                                                
           OPEN INPUT ADJUSTMENT-FILE.                                          
           PERFORM 4300-READ-ADJUSTMENT-FILE.                                   
                                                                                
           MOVE ZEROES      TO PA-PARTN-LOOKUP                                  
                               PA-PARTN-REUSE                                   
                               PA-UNK-VND-OUT                                   
                               PA-VENDOR-LOOKUP.                                
           PERFORM 3400-PROCESS-ADJUSTMENT-FILE                                 
               UNTIL PS-NO-MORE-ADJUSTMENTS.                                    
                                                                                
           DISPLAY ' ' FUNCTION CURRENT-DATE.                                   
           DISPLAY '**** INV ADJ STATS *************'.                          
           DISPLAY 'ADJUSTMENT INPUT CNT  = ', PA-ADJUST-COUNT-IN.              
           DISPLAY 'PARTITION CNTL LOOKUP = ' PA-PARTN-LOOKUP.                  
           DISPLAY 'PARTITION CNTL REUSE  = ' PA-PARTN-REUSE.                   
           DISPLAY 'PARTITION CNTL ADDED  = ' ML500-PV-BAL-ROW-INSERTED.        
           DISPLAY 'VENDOR INFO LOOKUP    = ' PA-VENDOR-LOOKUP.                 
           DISPLAY 'ADJUSTMENT OUTPUT CNT = ', PA-ADJUST-COUNT-OUT              
           DISPLAY 'ADJUSTMENT UNKN VENDOR= ' PA-UNK-VND-OUT.                   
                                                                                
           CLOSE ADJUSTMENT-FILE.                                               
                                                                                
      ******************************************************************        
      * PROCESS SALES FILE                                                      
      ******************************************************************        
       2400-PROCESS-SALES-EXTRACT.                                              
                                                                                
           OPEN INPUT SALES-FILE.                                               
           PERFORM 4400-READ-SALES-FILE.                                        
                                                                                
           MOVE ZEROES      TO PA-PARTN-LOOKUP                                  
                               PA-PARTN-REUSE                                   
                               PA-VENDOR-LOOKUP                                 
                               PA-UNK-VND-OUT.                                  
           MOVE 'Y' TO PS-SALE-FIRST-TIME-SW.                                   
                                                                                
           PERFORM 3500-PROCESS-SALES-FILE                                      
               UNTIL PS-NO-MORE-SALES.                                          
                                                                                
           DISPLAY ' ' FUNCTION CURRENT-DATE.                                   
           DISPLAY '**** SALES FILE STATS **********'.                          
           DISPLAY 'SALES INPUT  COUNT    = ', PA-SALES-COUNT-IN.               
           DISPLAY 'PARTITION CNTL LOOKUP = ' PA-PARTN-LOOKUP.                  
           DISPLAY 'PARTITION CNTL REUSE  = ' PA-PARTN-REUSE.                   
           DISPLAY 'PARTITION CNTL ADDED  = ' ML500-PV-BAL-ROW-INSERTED.        
           DISPLAY 'VENDOR INFO LOOKUP    = ' PA-VENDOR-LOOKUP.                 
           DISPLAY 'SALES OUTPUT COUNT    = ', PA-SALES-COUNT-OUT.              
           DISPLAY ' '                                                          
           DISPLAY 'SALES UNKN VENDORS    = ' PA-UNK-VND-OUT.                   
                                                                                
           CLOSE SALES-FILE.                                                    
                                                                                
      ******************************************************************        
      * DETERMINE WHAT RECORD TYPE TO PROCESS (PURCHASES OR TRANSFERS)          
      ******************************************************************        
       3100-PROCESS-PURCHASE-FILE.                                              
                                                                                
           ADD 1 TO PA-INPUT-COUNT-IN                                           
                    PA-PURCH-EXTR-CNT-IN.                                       
W28582     INITIALIZE ML254-UPDATE-REC.                                         
W28582     MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML254-FSCL-WK-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML254-FSCL-WK-END-DTE.            
W28582     MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML254-FSCL-MN-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML254-FSCL-MN-END-DTE.            
      * STRAIGHT MOVES FROM INPUT FORMAT TO OUTPUT FIELDS                       
W28582     MOVE ML030-PURCH-DEPT-NBR       TO ML254-DEPT-NBR                    
                                              PV-DEPT-NBR.                      
W28582     MOVE ML030-PURCH-CLASS-NBR      TO ML254-SUB-CL-NBR.                 
W28582     MOVE ML030-PURCH-SKU            TO ML254-SKU-NBR.                    
                                                                                
W28582     MOVE ML030-PURCH-STORE-NBR      TO PV-LOC-NBR-2                      
W28582     MOVE PV-LOC-NBR-2               TO ML254-LOC-NBR.                    
                                                                                
      * DERIVE MAJOR CLASS NUMBER FROM SUB CLASS NUMBER                         
W28582     MOVE ML030-PURCH-CLASS-NBR      TO PV-MAJ-CL-NBR-WORK.               
           MOVE 0 TO PV-MAJ-CL-NBR-2.                                           
W28582     MOVE PV-MAJ-CL-NBR              TO ML254-MAJ-CL-NBR.                 
                                                                                
W28582     MOVE ML030-PURCH-ENT-ID         TO ML254-ENT-ID.                     
                                                                                
W28582     IF ML254-ENT-ID = PC-UNKNOWN-VENDOR                                  
26006          ADD +1                      TO PA-UNK-VND-OUT                    
26006      END-IF.                                                              
                                                                                
W28582     MOVE ML030-PURCH-LBL-SEQ-NBR    TO ML254-LBL-SEQ-NBR.                
W28582     MOVE ML030-PURCH-ML-LWST-ID     TO ML254-ML-LWST-ID.                 
W28582     MOVE ML030-PURCH-AP-TRANS-CDE   TO ML254-AP-TRANS-CDE.               
W28582     MOVE ML030-PURCH-FIN-LIAB-DTE   TO ML254-FIN-LIAB-DTE.               
W28582     MOVE ML030-PURCH-TRANS-CDE      TO ML254-TRN-TYP-CDE.                
      ***INTRANSIT?                                                             
           EVALUATE TRUE                                                        
W28582         WHEN ML030-PURCH-PURCHASE                                        
                    PERFORM 3125-FORMAT-PURCHASE-RECORDS                        
W28582         WHEN ML030-PURCH-TRANSFER-FRM-STORE                              
                    PERFORM 3150-FORMAT-TRX-FROM-RECORDS                        
W28582         WHEN ML030-PURCH-TRANSFER-TO-STORE                               
                    PERFORM 3175-FORMAT-TRX-TO-RECORDS                          
               WHEN OTHER                                                       
                    DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME              
                    DISPLAY 'ERROR PROCESSING PURCHASE EXTRACT FILE'            
W28582              DISPLAY  ML030-PURCH-RECORD                                 
                    MOVE +4008 TO ABEND-CODE                                    
W28582              DISPLAY 'TRANS CODE : ', ML030-PURCH-TRANS-CDE              
                    PERFORM 9999-ABEND-ROUTINE                                  
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4100-READ-PURCHASE-FILE.                                     
                                                                                
      ******************************************************************        
      * FORMAT PURCHASE RECORDS (CODE '10') TO OUTPUT FORMAT                    
      ******************************************************************        
       3125-FORMAT-PURCHASE-RECORDS.                                            
                                                                                
W28582     MOVE ML030-PURCH-EXTD-RTL-AMT   TO PV-PURCH-RTL-AMT.                 
W28582     MOVE ML030-PURCH-EXTD-NET-COST-AMT                                   
                                           TO PV-PURCH-NET-COST-AMT.            
W28582     MOVE ML030-PURCH-EXTD-GRO-COST-AMT                                   
                                           TO PV-PURCH-GRO-COST-AMT.            
W28582     MOVE ML030-PURCH-FRGT-AMT       TO PV-PURCH-FRGT-COST-AMT.           
W28582     MOVE ML030-PURCH-DISC-AMT       TO PV-PURCH-DISC-COST-AMT.           
                                                                                
      * FOLLOWING FIELDS REQUIRE CALCULATION PRIOR TO MOVING VALUES             
W28582     IF (ML030-PURCH-DR-CR-CDE = '02' OR '08')                            
               COMPUTE PV-PURCH-RTL-AMT =                                       
                                 (PV-PURCH-RTL-AMT * -1)                        
               COMPUTE PV-PURCH-NET-COST-AMT =                                  
                                 (PV-PURCH-NET-COST-AMT * -1)                   
               COMPUTE PV-PURCH-GRO-COST-AMT =                                  
                                 (PV-PURCH-GRO-COST-AMT * -1)                   
               COMPUTE PV-PURCH-FRGT-COST-AMT =                                 
                                 (PV-PURCH-FRGT-COST-AMT * -1)                  
P28582***      COMPUTE PV-PURCH-DISC-COST-AMT =                                 
P28582***                        (PV-PURCH-DISC-COST-AMT * -1)                  
           END-IF.                                                              
                                                                                
W28582     IF ML030-PURCH-DR-CR-CDE = '01'                                      
                COMPUTE PV-PURCH-DISC-COST-AMT =                                
                                 (PV-PURCH-DISC-COST-AMT * -1)                  
           END-IF.                                                              
W28582     MOVE ZEROES      TO ML254-RCPT-RTL-AMT                               
W28582                         ML254-RCPT-FRGT-COST-AMT                         
W28582                         ML254-RCPT-NET-COST-AMT                          
W28582                         ML254-PADJ-RTL-AMT                               
W28582                         ML254-PADJ-FRGT-COST-AMT                         
W28582                         ML254-PADJ-NET-COST-AMT                          
W28582                         ML254-IN-TRNST-RTL-AMT                           
W28582                         ML254-STR-EXPTED-RTL-AMT.                        
                                                                                
      *IF THE SOURCE SYSTEM IS RECEIVING, MOVE THE DOLLARS TO RECEIPT           
27808 *IF THE SOURCE SYSTEM IS PHYS INV, MOVE THE DOLLARS TO IN-TRANSIT         
27808 *IF THE SOURCE SYSTEM IS PHYS INV, MOVE ZEROES TO THE IN-TRANSIT          
27808 *BUCKET AND MOVE THE DOLLARS TO THE STORE-EXPECTED BUCKET                 
      *OTHERWISE, MOVE THE DOLLARS TO PURCHASE ADJ                              
                                                                                
W28582*    MOVE ML030-PURCH-AP-TRANS-CDE   TO PV-AP-TRANS-TYPE.                 
                                                                                
           EVALUATE TRUE                                                        
W28582         WHEN ML030-PURCH-RECEIPT                                         
                   MOVE PV-PURCH-RTL-AMT                                        
W28582                 TO ML254-RCPT-RTL-AMT                                    
                   MOVE PV-PURCH-NET-COST-AMT                                   
W28582                 TO ML254-RCPT-NET-COST-AMT                               
                   MOVE PV-PURCH-GRO-COST-AMT                                   
W28582                 TO ML254-RCPT-GRO-COST-AMT                               
                   MOVE PV-PURCH-FRGT-COST-AMT                                  
W28582                 TO ML254-RCPT-FRGT-COST-AMT                              
                   MOVE PV-PURCH-DISC-COST-AMT                                  
W28582                 TO ML254-RCPT-DISC-COST-AMT                              
W28582         WHEN ML030-PHYSICAL-INVENTORY                                    
27808              MOVE ZEROES                                                  
W28582                 TO ML254-IN-TRNST-RTL-AMT                                
27808              MOVE PV-PURCH-RTL-AMT                                        
W28582                 TO ML254-STR-EXPTED-RTL-AMT                              
               WHEN OTHER                                                       
                   MOVE PV-PURCH-RTL-AMT                                        
W28582                 TO ML254-PADJ-RTL-AMT                                    
                   MOVE PV-PURCH-NET-COST-AMT                                   
W28582                 TO ML254-PADJ-NET-COST-AMT                               
                   MOVE PV-PURCH-GRO-COST-AMT                                   
W28582                 TO ML254-PADJ-GRO-COST-AMT                               
                   MOVE PV-PURCH-FRGT-COST-AMT                                  
W28582                 TO ML254-PADJ-FRGT-COST-AMT                              
                   MOVE PV-PURCH-DISC-COST-AMT                                  
W28582                 TO ML254-PADJ-DISC-COST-AMT                              
                   EVALUATE TRUE                                                
                       WHEN ML030-AP-RETURN-TO-VENDOR                           
W37965*                     MOVE PV-PURCH-RTL-AMT                               
W37965*                             TO ML254-PADJ-RTV-RTL-AMT                   
W37965*                     MOVE PV-PURCH-DISC-COST-AMT                         
W37965*                             TO ML254-PADJ-RTV-COST-AMT                  
W37965*                     ADD PV-PURCH-GRO-COST-AMT                           
W37965*                               TO ML254-PADJ-RTV-COST-AMT                
W37965                      CONTINUE                                            
      *                     MOVE PV-PURCH-GRO-COST-AMT                          
W28582*                               TO ML254-RTV-RTL-AMT                      
                       WHEN ML030-AP-DEBIT-ALLOWANCE                            
W28582                      MOVE PV-PURCH-RTL-AMT                               
W28582                                TO ML254-PADJ-DA-RTL-AMT                  
                            MOVE PV-PURCH-DISC-COST-AMT                         
W28582                                   TO ML254-PADJ-DA-COST-AMT              
                            ADD PV-PURCH-GRO-COST-AMT                           
W28582                                   TO ML254-PADJ-DA-COST-AMT              
      * MAP OF COST FIELD TO RETAIL FIELD:                                      
      * RTV-RTL-AMT IS IN THE VA, ML DETAIL AND DW TABLES.  THE                 
      * FIELD IS NOT NAMED CORRECTLY.  WHEN DW REPORTS ON THIS FIELD,           
      * THE LABEL IS RETURN TO VENDOR, AND IN VA REPORTS IT IS JUST             
      * LABELED RETURN TO VENDOR.  THE AMOUNT OF EFFORT TO CORRECT              
      * IT IN THE DATABASES IS NOT EASILY JUSTIFIED, SO AT THIS TIME            
      * THIS COMMENT IN THE PROGRAM IS THE SOLUTION SO THAT IN THE              
      * FUTURE, WE KNOW WHY A COST FIELD IS MAPPING TO A RETAIL FIELD.          
                            MOVE PV-PURCH-GRO-COST-AMT                          
W28582                                   TO ML254-DR-ALW-RTL-AMT                
                       WHEN OTHER                                               
                            MOVE PV-PURCH-RTL-AMT                               
W28582                                   TO ML254-PADJ-OTH-RTL-AMT              
                            MOVE PV-PURCH-DISC-COST-AMT                         
W28582                                   TO ML254-PADJ-OTH-COST-AMT             
                            ADD PV-PURCH-GRO-COST-AMT                           
W28582                                   TO ML254-PADJ-OTH-COST-AMT             
                   END-EVALUATE                                                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
      * MAP OF COST FIELD TO RETAIL FIELD:                                      
      * RTV-RTL-AMT IS IN THE VA, ML DETAIL AND DW TABLES.  THE                 
      * FIELD IS NOT NAMED CORRECTLY.  WHEN DW REPORTS ON THIS FIELD,           
      * THE LABEL IS RETURN TO VENDOR, AND IN VA REPORTS IT IS JUST             
      * LABELED RETURN TO VENDOR.  THE AMOUNT OF EFFORT TO CORRECT              
      * IT IN THE DATABASES IS NOT EASILY JUSTIFIED, SO AT THIS TIME            
      * THIS COMMENT IN THE PROGRAM IS THE SOLUTION SO THAT IN THE              
      * FUTURE, WE KNOW WHY A COST FIELD IS MAPPING TO A RETAIL FIELD.          
                                                                                
W37965*    IF ML030-PURCH-AP-TRANS-CDE IS EQUAL TO '21' OR '22'                 
W37965*        MOVE PV-PURCH-GRO-COST-AMT TO ML254-RTV-RTL-AMT                  
W37965*    END-IF.                                                              
                                                                                
                                                                                
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
                                                                                
           ADD 1 TO PA-PURCHASE-COUNT-OUT                                       
                    PA-OUTPUT-COUNT-OUT.                                        
                                                                                
      ******************************************************************        
      * FORMAT TRANSFER FROM/OUT RECORDS (CODE '30') TO OUTPUT FORMAT           
      ******************************************************************        
       3150-FORMAT-TRX-FROM-RECORDS.                                            
                                                                                
W28582     MOVE ML030-PURCH-EXTD-RTL-AMT   TO ML254-XFER-OUT-RTL-AMT.           
                                                                                
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
                                                                                
           ADD 1 TO PA-TRX-FROM-COUNT-OUT                                       
                    PA-OUTPUT-COUNT-OUT.                                        
                                                                                
      ******************************************************************        
      * FORMAT TRANSFER TO/IN RECORDS (CODE '31') TO OUTPUT FORMAT.             
      ******************************************************************        
       3175-FORMAT-TRX-TO-RECORDS.                                              
                                                                                
W28582     MOVE ML030-PURCH-EXTD-RTL-AMT   TO ML254-XFER-IN-RTL-AMT.            
                                                                                
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
                                                                                
           ADD 1 TO PA-TRX-TO-COUNT-OUT                                         
                    PA-OUTPUT-COUNT-OUT.                                        
                                                                                
      ******************************************************************        
      * PROCESS ALL RECORDS ON THE PRICE CHANGE FILE                            
      ******************************************************************        
       3200-PROCESS-PRICE-CHANGE-FILE.                                          
                                                                                
           ADD 1 TO PA-INPUT-COUNT-IN                                           
                    PA-PRICE-CHG-COUNT-IN.                                      
                                                                                
W28582     INITIALIZE ML254-UPDATE-REC.                                         
                                                                                
      * MOVE TMLCNTL VALUES TO OUTPUT FIELDS (BASED ON CC DATE)                 
W28582     MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML254-FSCL-WK-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML254-FSCL-WK-END-DTE.            
W28582     MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML254-FSCL-MN-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML254-FSCL-MN-END-DTE.            
      * STRAIGHT MOVES FROM INPUT FORMAT TO OUTPUT FIELDS                       
W28582     MOVE ML029-PC-STORE-NBR         TO ML254-LOC-NBR.                    
W28582     MOVE ML029-PC-LBL-SEQ-NBR       TO ML254-LBL-SEQ-NBR.                
W28582     MOVE ML029-PC-ML-LWST-ID        TO ML254-ML-LWST-ID.                 
W28582     MOVE ML029-PC-SKU               TO ML254-SKU-NBR.                    
W28582     MOVE ML029-PC-DEPT-NBR          TO ML254-DEPT-NBR                    
                                              PV-DEPT-NBR.                      
W28582     MOVE ML029-PC-CLASS-NBR         TO ML254-SUB-CL-NBR.                 
W28582     MOVE ML029-PC-FIN-LIAB-DTE      TO ML254-FIN-LIAB-DTE.               
W28582     MOVE ML029-PC-TRANS-CDE         TO ML254-TRN-TYP-CDE.                
W37965     MOVE 0                          TO ML254-PRCHG-RSN-CDE (1:1)         
W37965     MOVE ML029-PC-MKDN-MKUP-RSN-CDE TO ML254-PRCHG-RSN-CDE (2:3)         
W37965     MOVE 0                          TO ML254-PRCHG-CTG-CDE (1:1)         
W37965     MOVE ML029-PC-CATEGORY          TO ML254-PRCHG-CTG-CDE (2:1)         
W37695*ML254-PC-SYST-ORIG-CDE IS NOT INCLUDED IN MLRD254                        
W37965*    MOVE ML029-PC-SYST-ORIG-CDE     TO ML254-PC-SYST-ORIG-CDE            
      * DERIVE MAJOR CLASS NUMBER FROM SUB CLASS NUMBER                         
W28582     MOVE ML029-PC-CLASS-NBR         TO PV-MAJ-CL-NBR-WORK.               
           MOVE 0 TO PV-MAJ-CL-NBR-2.                                           
W28582     MOVE PV-MAJ-CL-NBR              TO ML254-MAJ-CL-NBR.                 
                                                                                
26006      PERFORM 3280-MOVE-PC-TO-VENDORS.                                     
                                                                                
           PERFORM 4200-READ-PRICE-CHANGE-FILE.                                 
                                                                                
      ******************************************************************        
      * CHECK THE TYPE OF PRICE CHANGE THAT IS BEING PROCESSED SO THAT          
      * THE CORRECT BUCKETS CAN BE LOADED                                       
      ******************************************************************        
       3280-MOVE-PC-TO-VENDORS.                                                 
                                                                                
      *  IF THE TRANSACTION IS A VENDOR ALLOWANCE, USE THE INHOUSE              
      *  NUMBER TO GET THE ENT-ID                                               
      *  IF THE VENDOR ENT ID IS NOT FOUND, THE ENT ID WILL DEFAULT             
      *  TO THE DEPT SUBCLASS UNKNOWN VENDOR ENTITY ID (5161)                   
26006R                                                                          
                                                                                
W28582     MOVE ML029-PC-ENT-ID TO ML254-ENT-ID.                                
                                                                                
W28582     IF ML254-ENT-ID = PC-UNKNOWN-VENDOR                                  
26006          ADD +1                      TO PA-UNK-VND-OUT                    
26006      END-IF.                                                              
                                                                                
W28582     IF ML254-ENT-ID = PC-UNKNOWN-VENDOR                                  
W28582         IF ML029-PC-MARKDOWN                                             
W28582             ADD ML029-PC-EXTD-RTL-AMT  TO PV-PRORATE-MKDN                
26006R         ELSE                                                             
W28582             ADD ML029-PC-EXTD-RTL-AMT  TO PV-PRORATE-MKUP                
26006R         END-IF                                                           
26006R     END-IF.                                                              
26006R                                                                          
           PERFORM 3290-CHECK-PRICE-CHG-TYPE                                    
                                                                                
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
                                                                                
W28582     IF  ML029-PC-MARKDOWN                                                
               ADD 1 TO PA-MARKDOWN-COUNT-OUT                                   
                        PA-OUTPUT-COUNT-OUT                                     
           ELSE                                                                 
               ADD 1 TO PA-MARKON-COUNT-OUT                                     
                        PA-OUTPUT-COUNT-OUT                                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * CHECK THE TYPE OF PRICE CHANGE THAT IS BEING PROCESSED SO THAT          
      * THE CORRECT BUCKETS CAN BE LOADED                                       
      ******************************************************************        
       3290-CHECK-PRICE-CHG-TYPE.                                               
                                                                                
W28582     IF ML029-PC-MARKDOWN                                                 
W28582        MOVE ML029-PC-EXTD-RTL-AMT    TO PV-MKDN-RTL-AMT                  
              PERFORM 3350-DETERMINE-MARKDOWN-TYPE                              
           ELSE                                                                 
W28582        IF ML029-PC-MARKUP                                                
W28582           MOVE ML029-PC-EXTD-RTL-AMT TO ML254-MKUP-RTL-AMT               
              ELSE                                                              
                 DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME                 
                 DISPLAY 'ERROR PROCESSING PRICE CHANGE EXTRACT FILE'           
W28582           DISPLAY  ML029-PC-RECORD                                       
                 MOVE +4008 TO ABEND-CODE                                       
W28582           DISPLAY 'TRANS CODE : ', ML029-PC-TRANS-CDE                    
                 PERFORM 9999-ABEND-ROUTINE                                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * CHECK THE TYPE OF MARKDOWN AND MOVE IT TO THE CORRECT                   
      * OUTPUT FIELD.                                                           
      ******************************************************************        
       3350-DETERMINE-MARKDOWN-TYPE.                                            
                                                                                
W28582*    MOVE ML029-PC-CATEGORY          TO PV-MARKDOWN-TYPE.                 
W28582*    MOVE ML029-PC-MKDN-MKUP-RSN-CDE TO PV-MKDN-RSN-CODE.                 
                                                                                
W28582     MOVE PV-MKDN-RTL-AMT            TO ML254-MKDN-RTL-AMT.               
R21764                                                                          
W37965*    EVALUATE TRUE                                                        
W37965*      WHEN ML029-MARKDOWN-PLU                                            
W37965*          MOVE PV-MKDN-RTL-AMT TO ML254-MKDN-PLU-RTL-AMT                 
W37965*          IF ML029-MKDN-RSN-PROMO                                        
W37965*              MOVE PV-MKDN-RTL-AMT                                       
W37965*                               TO ML254-MKDN-PROMO-RTL-AMT               
W37965*          ELSE                                                           
W37965*              MOVE PV-MKDN-RTL-AMT                                       
W37965*                               TO ML254-MKDN-OTH-RTL-AMT                 
W37965*          END-IF                                                         
W37965*      WHEN ML029-MARKDOWN-PERM                                           
W37965*          MOVE PV-MKDN-RTL-AMT TO ML254-MKDN-RGST-RTL-AMT                
W37965*          IF ML029-MKDN-RSN-GOLDSTAR                                     
W37965*              MOVE PV-MKDN-RTL-AMT                                       
W37965*                               TO ML254-MKDN-GLDST-RTL-AMT               
W37965*          ELSE                                                           
W37965*              MOVE PV-MKDN-RTL-AMT                                       
W37965*                               TO ML254-MKDN-OTH-RTL-AMT                 
W37965*          END-IF                                                         
W37965*      WHEN ML029-MARKDOWN-UNMATCHED                                      
W37965*         MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-UMTCH-RTL-AMT               
W37965*         MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-OTH-RTL-AMT                 
W37965*      WHEN ML029-MARKDOWN-BUYER                                          
W37965*         MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-BYR-RTL-AMT                 
W37965*      WHEN ML029-MARKDOWN-STORE                                          
W37965*         IF ML029-MKDN-RSN-MOS-SALVAGE                                   
W37965*            MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-BYR-RTL-AMT              
W37965*         ELSE                                                            
W37965*            MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-STR-RTL-AMT              
W37965*                                                                         
W37965*            IF ML029-MKDN-RSN-PRICE-POINT                                
W37965*               MOVE PV-MKDN-RTL-AMT TO ML254-MKDN-PRPT-RTL-AMT           
W37965*            ELSE                                                         
W37965*                MOVE PV-MKDN-RTL-AMT TO ML254-MKDN-OTH-RTL-AMT           
W37965*            END-IF                                                       
W37965*                                                                         
W37965*         END-IF                                                          
W37965*      WHEN OTHER                                                         
W37965*         MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-UMTCH-RTL-AMT               
W37965*         MOVE PV-MKDN-RTL-AMT  TO ML254-MKDN-OTH-RTL-AMT                 
W37965*    END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * PROCESS THE INVENTORY ADJUSTMENT FILE                                   
      ************************************************************              
       3400-PROCESS-ADJUSTMENT-FILE.                                            
                                                                                
           ADD 1 TO PA-INPUT-COUNT-IN                                           
                    PA-ADJUST-COUNT-IN.                                         
                                                                                
W28582     INITIALIZE ML254-UPDATE-REC.                                         
                                                                                
           IF ML024-ADJ-ADJUSTMENT                                              
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME                   
               DISPLAY '** ERROR PROCESSING ADJUSTMENT EXTRACT FILE.'           
               DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'                 
               DISPLAY  ML024-ADJ-RECORD                                        
               MOVE +4008 TO ABEND-CODE                                         
               DISPLAY 'TRANS CODE : ', ML024-ADJ-TRANS-CDE                     
               PERFORM 9999-ABEND-ROUTINE                                       
           END-IF.                                                              
                                                                                
      * MOVE TMLCNTL VALUES TO OUTPUT FIELDS (BASED ON CC DATE)                 
      * STRAIGHT MOVES FROM INPUT FORMAT TO OUTPUT FIELDS                       
      *ML CONTROL VALUES                                                        
W28582     MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML254-FSCL-WK-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML254-FSCL-WK-END-DTE.            
W28582     MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML254-FSCL-MN-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML254-FSCL-MN-END-DTE.            
      *                                                                         
W28582     MOVE ML024-ADJ-STORE-NBR        TO ML254-LOC-NBR.                    
W28582     MOVE ML024-ADJ-LBL-SEQ-NBR      TO ML254-LBL-SEQ-NBR.                
W28582     MOVE ML024-ADJ-ML-LWST-ID       TO ML254-ML-LWST-ID.                 
W28582     MOVE ML024-ADJ-SKU              TO ML254-SKU-NBR.                    
W28582     MOVE ML024-ADJ-DEPT-NBR         TO ML254-DEPT-NBR                    
                                              PV-DEPT-NBR.                      
W28582     MOVE ML024-ADJ-CLASS-NBR        TO ML254-SUB-CL-NBR.                 
                                                                                
W28582     MOVE ML024-ADJ-ENT-ID          TO ML254-ENT-ID.                      
                                                                                
W28582     IF ML254-ENT-ID = PC-UNKNOWN-VENDOR                                  
26006          ADD +1                      TO PA-UNK-VND-OUT                    
26006      END-IF.                                                              
                                                                                
W28582     MOVE ML024-ADJ-FIN-LIAB-DTE     TO ML254-FIN-LIAB-DTE.               
W28582     MOVE ML024-ADJ-TRANS-CDE        TO ML254-TRN-TYP-CDE.                
W28582     MOVE ML024-ADJ-AT-RTL-AMT       TO ML254-INV-ADJ-RTL-AMT.            
      *                                                                         
      * DERIVE MAJOR CLASS NUMBER FROM SUB CLASS NUMBER                         
           MOVE ML024-ADJ-CLASS-NBR        TO PV-MAJ-CL-NBR-WORK.               
           MOVE 0 TO PV-MAJ-CL-NBR-2.                                           
W28582     MOVE PV-MAJ-CL-NBR              TO ML254-MAJ-CL-NBR.                 
                                                                                
W28582     MOVE ZEROES                     TO ML254-SLS-REG-RTL-AMT             
W28582                                        ML254-XFER-IN-RTL-AMT             
W28582                                        ML254-XFER-OUT-RTL-AMT            
W28582                                        ML254-RCPT-RTL-AMT                
W28582                                        ML254-RCPT-FRGT-COST-AMT          
W28582                                        ML254-RCPT-NET-COST-AMT           
W28582                                        ML254-PADJ-RTL-AMT                
W28582                                        ML254-PADJ-FRGT-COST-AMT          
W28582                                        ML254-PADJ-NET-COST-AMT           
W28582                                        ML254-MKDN-RTL-AMT                
W28582                                        ML254-MKUP-RTL-AMT.               
      *                                                                         
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
      *                                                                         
           ADD +1 TO PA-ADJUST-COUNT-OUT                                        
                     PA-OUTPUT-COUNT-OUT.                                       
      *                                                                         
           PERFORM 4300-READ-ADJUSTMENT-FILE.                                   
      *                                                                         
      ******************************************************************        
      * PUT THE ML024 LOGIC FROM MLKUT400 HERE                                  
      ******************************************************************        
      ******************************************************************        
      * PROCESS THE SALES FILE                                                  
      ******************************************************************        
       3500-PROCESS-SALES-FILE.                                                 
                                                                                
           ADD 1 TO PA-INPUT-COUNT-IN                                           
                    PA-SALES-COUNT-IN.                                          
                                                                                
W28582     INITIALIZE ML254-UPDATE-REC.                                         
                                                                                
           IF ML022-SALE                                                        
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME                   
               DISPLAY 'ERROR PROCESSING SALES EXTRACT FILE'                    
               DISPLAY  ML022-SALE-RECORD                                       
               MOVE +4008 TO ABEND-CODE                                         
               DISPLAY 'TRANS CODE : ', ML022-SALE-TRANS-CDE                    
               PERFORM 9999-ABEND-ROUTINE                                       
           END-IF.                                                              
                                                                                
                                                                                
W28582     MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML254-FSCL-MN-NBR.                
W28582     MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML254-FSCL-MN-END-DTE.            
                                                                                
W28582     MOVE MLCNTL-PSTG-FSCL-WK-NBR  TO ML254-FSCL-WK-NBR.                  
W28582     MOVE MLCNTL-PSTG-FSCL-WK-DTE  TO ML254-FSCL-WK-END-DTE.              
                                                                                
      * STRAIGHT MOVES FROM INPUT FORMAT TO OUTPUT FIELDS                       
W28582     MOVE ML022-SALE-STORE-NBR         TO ML254-LOC-NBR.                  
W28582     MOVE ML022-SALE-ENT-ID            TO ML254-ENT-ID.                   
W28582     MOVE ML022-SALE-LBL-SEQ-NBR       TO ML254-LBL-SEQ-NBR.              
W28582     MOVE ML022-SALE-ML-LWST-ID        TO ML254-ML-LWST-ID.               
W28582     MOVE ML022-SALE-SKU               TO ML254-SKU-NBR.                  
W28582     MOVE ML022-SALE-DEPT-NBR          TO ML254-DEPT-NBR                  
                                                PV-DEPT-NBR.                    
W28582     MOVE ML022-SALE-CLASS-NBR         TO ML254-SUB-CL-NBR.               
W28582     MOVE ML022-SALE-FIN-LIAB-DTE      TO ML254-FIN-LIAB-DTE.             
W28582     MOVE ML022-SALE-TRANS-CDE         TO ML254-TRN-TYP-CDE.              
                                                                                
      * DERIVE MAJOR CLASS NUMBER FROM SUB CLASS NUMBER                         
           MOVE ML022-SALE-CLASS-NBR         TO PV-MAJ-CL-NBR-WORK.             
           MOVE 0 TO PV-MAJ-CL-NBR-2.                                           
W28582     MOVE PV-MAJ-CL-NBR                TO ML254-MAJ-CL-NBR.               
                                                                                
      *  IF THE VENDOR ENT ID IS NOT FOUND, THE ENT ID WILL DEFAULT             
      *  TO THE DEPT SUBCLASS UNKNOWN VENDOR ENTITY ID (5161)                   
      *    MOVE ML022-SALE-DEPT-NBR          TO SKXREF-DEPT.                    
      *    MOVE ML022-SALE-CLASS-NBR         TO SKXREF-SUBCLASS.                
                                                                                
W28582     MOVE ML022-SALE-ENT-ID            TO ML254-ENT-ID.                   
                                                                                
W28582     IF ML254-ENT-ID = PC-UNKNOWN-VENDOR                                  
26006          ADD +1                      TO PA-UNK-VND-OUT                    
26006      END-IF.                                                              
                                                                                
      *    MOVE ML022-SALE-TYPE-2            TO PV-SALE-TYPE.                   
W37965     MOVE ML022-SALE-TYPE-2           TO ML254-SLS-TYP-CDE.               
                                                                                
           EVALUATE TRUE                                                        
             WHEN ML022-SALE-REGULAR                                            
W28582         MOVE ML022-SALE-EXTD-RTL-AMT TO ML254-SLS-REG-RTL-AMT            
             WHEN ML022-SALE-PROMO                                              
W28582         MOVE ML022-SALE-EXTD-RTL-AMT TO ML254-SLS-PROMO-RTL-AMT          
                                                                                
             WHEN ML022-SALE-PROMO-GOLDSTAR                                     
               MOVE ML022-SALE-EXTD-RTL-AMT TO                                  
W28582                                      ML254-SLS-PROCLR-RTL-AMT            
                                                                                
             WHEN ML022-SALE-CLEARANCE                                          
W28582         MOVE ML022-SALE-EXTD-RTL-AMT TO ML254-SLS-CLR-RTL-AMT            
             WHEN OTHER                                                         
W28582         MOVE ML022-SALE-EXTD-RTL-AMT TO ML254-SLS-OTH-RTL-AMT            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 7800-FIND-BOTH-PARTN.                                        
                                                                                
           PERFORM 5000-WRITE-CNDSD-OUTPUT-RECORD.                              
                                                                                
           ADD 1 TO PA-SALES-COUNT-OUT                                          
                     PA-OUTPUT-COUNT-OUT.                                       
                                                                                
           PERFORM 4400-READ-SALES-FILE.                                        
                                                                                
      ******************************************************************        
       4050-READ-CONTROL-CARD-FILE.                                             
                                                                                
            READ CONTROL-CARD-FILE  INTO CC-CONTROL-CARD-REC                    
                AT END SET PS-END-OF-CONTROL-CARD-FILE TO TRUE.                 
                                                                                
                                                                                
      ******************************************************************        
      * READ FILE CONTAINING PURCHASE AND TRANSFER TRANSACTIONS                 
      ******************************************************************        
       4100-READ-PURCHASE-FILE.                                                 
                                                                                
W28582      READ PURCHASE-FILE  INTO ML030-PURCH-RECORD                         
                AT END SET PS-NO-MORE-PURCHASES TO TRUE.                        
                                                                                
      ******************************************************************        
      * READ FILE CONTAINING MARKDOWN AND MARKUP TRANSACTIONS                   
      ******************************************************************        
       4200-READ-PRICE-CHANGE-FILE.                                             
                                                                                
W28582      READ PRICE-CHANGE-FILE  INTO ML029-PC-RECORD                        
                AT END SET PS-NO-MORE-PRICE-CHANGES TO TRUE.                    
                                                                                
      ************************************************************              
      * READ FILE CONTAINING INVENTORY ADJUSTMENT TRANSACTIONS                  
      ************************************************************              
       4300-READ-ADJUSTMENT-FILE.                                               
                                                                                
      *     READ ADJUSTMENT-FILE INTO ML250-UPDATE-REC                          
            READ ADJUSTMENT-FILE INTO ML024-ADJ-RECORD                          
                AT END SET PS-NO-MORE-ADJUSTMENTS TO TRUE.                      
                                                                                
      ******************************************************************        
      * READ SALES TRANSACTION FILE                                             
      ******************************************************************        
       4400-READ-SALES-FILE.                                                    
                                                                                
            READ SALES-FILE INTO ML022-SALE-RECORD                              
                AT END SET PS-NO-MORE-SALES TO TRUE.                            
                                                                                
      ******************************************************************        
      * CONTROL WRITING OF ALL RECORDS TO THE OUTPUT FILE                       
      ******************************************************************        
       5000-WRITE-CNDSD-OUTPUT-RECORD.                                          
                                                                                
W28582     WRITE CONDENSED-OUTPUT-RECORD FROM ML254-UPDATE-REC.                 
                                                                                
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       7000-READ-CONTROL-TABLE.                                                 
                                                                                
           IF CC-PROCESS-DATE = SPACES                                          
               DISPLAY ' USING ML CONTROL DATE '                                
               EXEC SQL                                                         
                   SELECT A.PSTG_FSCL_MN_DTE                                    
                         ,A.PSTG_FSCL_MN_NBR                                    
                         ,A.PSTG_FSCL_WK_DTE                                    
                         ,A.PSTG_FSCL_WK_NBR                                    
                         ,B.FSCL_MN_BEG_DTE                                     
                    INTO  :MLCNTL-PSTG-FSCL-MN-DTE                              
                         ,:MLCNTL-PSTG-FSCL-MN-NBR                              
                         ,:MLCNTL-PSTG-FSCL-WK-DTE                              
                         ,:MLCNTL-PSTG-FSCL-WK-NBR                              
                         ,:DTATTR-FSCL-MN-BEG-DTE                               
                    FROM  TMLCNTL A                                             
                         ,TDTATTR B                                             
                    WHERE KY_DTE = OPN_FSCL_MN_DTE                              
W21764              WITH UR                                                     
               END-EXEC                                                         
           ELSE                                                                 
               DISPLAY ' USING CONTROL CARD DATE OF: ' CC-PROCESS-DATE          
               EXEC SQL                                                         
                   SELECT B.FSCL_MN_END_DTE                                     
                         ,B.FSCL_MN_NBR                                         
                         ,B.FSCL_WK_END_DTE                                     
                         ,B.FSCL_WK_NBR                                         
                         ,B.FSCL_MN_BEG_DTE                                     
                    INTO  :MLCNTL-PSTG-FSCL-MN-DTE                              
                         ,:MLCNTL-PSTG-FSCL-MN-NBR                              
                         ,:MLCNTL-PSTG-FSCL-WK-DTE                              
                         ,:MLCNTL-PSTG-FSCL-WK-NBR                              
                         ,:DTATTR-FSCL-MN-BEG-DTE                               
                    FROM                                                        
                          TDTATTR B                                             
                    WHERE KY_DTE = :CC-PROCESS-DATE                             
                    WITH UR                                                     
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   DISPLAY 'CTRL TABLE POSTING MONTH END DATE : ',              
                                              MLCNTL-PSTG-FSCL-MN-DTE           
                   DISPLAY 'CTRL TABLE POSTING MONTH NUMBER   : ',              
                                              MLCNTL-PSTG-FSCL-MN-NBR           
                   DISPLAY 'CTRL TABLE POSTING WEEK DATE      : '               
                                              MLCNTL-PSTG-FSCL-WK-DTE           
                   DISPLAY 'CTRL TABLE POSTING WEEK NUMBER    : '               
                                              MLCNTL-PSTG-FSCL-WK-NBR           
                   DISPLAY 'BEGINNING FISCAL MONTH DATE       : '               
                                              DTATTR-FSCL-MN-BEG-DTE            
               WHEN OTHER                                                       
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * FIND PARTITION NUMBER FOR BOTH WEEKLY AND MONTHLY FISCAL DATE           
      ******************************************************************        
       7800-FIND-BOTH-PARTN.                                                    
                                                                                
           IF PV-DEPT-NBR = ML500-PV-DEPT-NBR                                   
               ADD +1                      TO PA-PARTN-REUSE                    
W28582         MOVE PV-SAVE-WK-PARTN-NBR   TO ML254-WK-PARTN-NBR                
W28582         MOVE PV-SAVE-WK-DEPT-GP-CDE TO ML254-WK-DEPT-GP-CDE              
W28582         MOVE PV-SAVE-MN-PARTN-NBR   TO ML254-MTH-PARTN-NBR               
W28582         MOVE PV-SAVE-MN-DEPT-GP-CDE TO ML254-MTH-DEPT-GP-CDE             
           ELSE                                                                 
               ADD +1                   TO PA-PARTN-LOOKUP                      
               MOVE PV-DEPT-NBR         TO ML500-PV-DEPT-NBR                    
                                                                                
               MOVE PV-FSCL-WK-END-DTE  TO ML500-PV-FSCL-END-DTE                
               MOVE 'W'                 TO ML500-PV-TM-LVL-CDE                  
                                                                                
               PERFORM 7850-FIND-PARTITION                                      
                                                                                
W28582         MOVE ML500-PV-PARTN-NBR-OUT TO ML254-WK-PARTN-NBR                
                                              PV-SAVE-WK-PARTN-NBR              
W28582         MOVE ML500-PV-DEPT-GP-CDE-OUT TO ML254-WK-DEPT-GP-CDE            
                                              PV-SAVE-WK-DEPT-GP-CDE            
                                                                                
               MOVE PV-FSCL-MN-END-DTE  TO ML500-PV-FSCL-END-DTE                
               MOVE 'M'                 TO ML500-PV-TM-LVL-CDE                  
                                                                                
               PERFORM 7850-FIND-PARTITION                                      
                                                                                
W28582         MOVE ML500-PV-PARTN-NBR-OUT TO ML254-MTH-PARTN-NBR               
                                              PV-SAVE-MN-PARTN-NBR              
W28582         MOVE ML500-PV-DEPT-GP-CDE-OUT TO ML254-MTH-DEPT-GP-CDE           
                                              PV-SAVE-MN-DEPT-GP-CDE            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FIND PARTITION NUMBER                                                   
      ******************************************************************        
       7850-FIND-PARTITION.                                                     
                                                                                
           PERFORM ML500-0500-FIND-PARTITION.                                   
           IF ML500-PV-PARAGRAPH-NAME NOT EQUAL TO SPACES                       
             MOVE ML500-PV-PARAGRAPH-NAME TO PV-PARAGRAPH-NAME                  
             MOVE ML500-PV-OPERATION-ATTEMPTED TO PV-OPERATION-ATTEMPTED        
             PERFORM 9999-SQL-ABEND                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS FINAL COUNTS FOR INPUT AND OUTPUT COUNTS                        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
                 CONDENSED-OUTPUT-FILE.                                         
                                                                                
           DISPLAY '                                           '.               
           DISPLAY 'TOTAL INPUT  RECORDS  = ', PA-INPUT-COUNT-IN.               
           DISPLAY 'TOTAL OUTPUT RECORDS  = ', PA-OUTPUT-COUNT-OUT.             
           DISPLAY '                                           '.               
           DISPLAY '***** GOOD EOJ *****'.                                      
                                                                                
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND-ROUTINE.                                          
                                                                                
      ******************************************************************        
      * COMMON ABEND MODULE                                                     
      ******************************************************************        
       9999-ABEND-ROUTINE.                                                      
                                                                                
               CALL 'ILBOABN0' USING ABEND-CODE.                                
                                                                                
      ******************************************************************        
      * PARTITION NUMBER LOOKUP                                                 
      ******************************************************************        
      *ML500-0100-LOAD-PARTN-TABLE.                                             
           COPY MLPD500.                                                        
                                                                                
                                                                                
