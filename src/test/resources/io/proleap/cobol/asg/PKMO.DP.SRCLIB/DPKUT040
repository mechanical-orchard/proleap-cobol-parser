00001 ******************************************************************04/27/90
00002  IDENTIFICATION DIVISION.                                         DPKUT040
00003 ******************************************************************   LV001
00004                                                                   DPKUT040
00005  PROGRAM-ID.                DPKUT040.                             DPKUT040
00006  AUTHOR.                    RICK TULLOCH.                         DPKUT040
00007  INSTALLATION.              KOHLS DEPARTMENT STORES.              DPKUT040
00008  DATE-WRITTEN.              4-20-90.                              DPKUT040
00009  DATE-COMPILED.                                                   DPKUT040
00010                                                                   DPKUT040
00011 ******************************************************************DPKUT040
00012 *       THIS SUBROUTINE WILL COMPUTE UPC CHECK DIGITS BASED ON   *DPKUT040
00013 *  A PASSED PARAMETER LIST.  TWO OPTIONS ARE AVAILABLE:          *DPKUT040
00014 *  CHECK DIGIT COMPUTATION/VERIFICATION, AND INTERNAL UPC CODE   *DPKUT040
00015 *  GENERATION.  TO USE THE CHECK DIGIT COMPUTATION/VERIFICATION  *DPKUT040
00016 *  OPTION, SET THE OPTION INDICATOR, AND PROVIDE A FULL UPC      *DPKUT040
00017 *  (MARK THE CHECK DIGIT POSITION WITH A ZERO, IF YOU ARE JUST   *DPKUT040
00018 *  COMPUTING A CHECK DIGIT).  AN ERROR INDICATOR, AND A COMPUTED *DPKUT040
00019 *  CHECK DIGIT WILL BE PASSED BACK TO THE CALLING PROGRAM.  TO   *DPKUT040
00020 *  USE THE INTERNAL UPC CODE GENERATION OPTION, SET THE OPTION   *DPKUT040
00021 *  INDICATOR, AND PROVIDE THE SKU NUMBER ON WHICH THE INTERNAL   *DPKUT040
00022 *  UPC CODE IS TO BE GENERATED.  AN ERROR INDICATOR, THE FULL    *DPKUT040
00023 *  INTERAL UPC CODE, AND THE COMPUTED CHECK DIGIT WILL BE PASSED *DPKUT040
00024 *  BACK TO THE CALLING PROGRAM.  USE COPY MEMBER DPWS040I FOR    *DPKUT040
00025 *  THE PARAMETER LIST.                                           *DPKUT040
00026 *                                                                *DPKUT040
00027 *       THE SUBROUTINE BEGINS BY VALIDATING THE PARAMETERS       *DPKUT040
00028 *  PASSED IN THE PARAMETER LIST (OPTION, UPC, SKU).  IF THE      *DPKUT040
00029 *  GENERATE INTERNAL UPC OPTION WAS SELECTED, BUILD THE INTERNAL *DPKUT040
00030 *  UPC CODE, USING THE SKU NUMBER, WITH ZERO AS THE CHECK DIGIT. *DPKUT040
00031 *  COMPUTE THE CHECK DIGIT FOR THE UPC THAT WAS PASSED OR BUILT. *DPKUT040
00032 *  TO COMPUTE THE CHECK DIGIT, SUM ONE OR THREE TIMES EACH UPC   *DPKUT040
00033 *  DIGIT, IN AN ALTERNATING SEQUENCE, FOR ALL NON-CHECK DIGIT    *DPKUT040
00034 *  NUMBERS.  SINCE WE ARE ASSUMING A 15 DIGIT UPC, THE           *DPKUT040
00035 *  MULTIPLICATION SEQUENCE BEGINS WITH ONE TIMES THE DIGIT, AS   *DPKUT040
00036 *  OPPOSED TO THREE TIMES THE DIGIT, FOR A STANDARD 12 DIGIT     *DPKUT040
00037 *  UPC.  THE CHECK DIGIT IS THEN TEN MINUS THE LAST DIGIT OF     *DPKUT040
00038 *  THAT SUM.  FINALLY, IF THE GENERATE INTERNAL UPC OPTION WAS   *DPKUT040
00039 *  SELECTED, MOVE THE COMPUTED CHECK DIGIT TO THE CHECK DIGIT    *DPKUT040
00040 *  POSITION OF THE UPC, TO COMPLETE THE GENERATION OF THE UPC.   *DPKUT040
00041 *                                                                *DPKUT040
00042 *       **  NOTE  **                                             *DPKUT040
00043 *                                                                *DPKUT040
00044 *       WHENEVER THIS ROUTINE IS MOVED TO PRODUCTION, THE LOAD   *DPKUT040
00045 *   MODULE MUST BE COPIED INTO THE CICS LOADLIB, TO MAKE IT      *DPKUT040
00046 *   AVAILABLE TO CICS, AS WELL AS BATCH.                         *DPKUT040
00047 ******************************************************************DPKUT040
00048                                                                   DPKUT040
00049 ******************************************************************DPKUT040
00050  ENVIRONMENT DIVISION.                                            DPKUT040
00051 ******************************************************************DPKUT040
00052                                                                   DPKUT040
00053  CONFIGURATION SECTION.                                           DPKUT040
00054                                                                   DPKUT040
00055  SOURCE-COMPUTER.        IBM-3090.                                DPKUT040
00056  OBJECT-COMPUTER.        IBM-3090.                                DPKUT040
00057                                                                   DPKUT040
00058  INPUT-OUTPUT SECTION.                                            DPKUT040
00059                                                                   DPKUT040
00060  FILE-CONTROL.                                                    DPKUT040
00061                                                                   DPKUT040
00062 ******************************************************************DPKUT040
00063  DATA DIVISION.                                                   DPKUT040
00064 ******************************************************************DPKUT040
00065                                                                   DPKUT040
00066  WORKING-STORAGE SECTION.                                         DPKUT040
00067                                                                   DPKUT040
00068 *                                                                 DPKUT040
00069 *  UPC CHECK DIGIT ROUTINE PARAMETER LIST LAYOUT.                 DPKUT040
00070 *                                                                 DPKUT040
00071                                                                   DPKUT040
00072      COPY DPWS040I.                                               DPKUT040
00073                                                                   DPKUT040
00074  01  PC-PROGRAM-CONSTANTS.                                        DPKUT040
00075      05  PC-UPC-CHECK-DIGIT-BASE PIC S9(2)    VALUE +10.          DPKUT040
00076      05  PC-CHECK-DIGIT-LOCATION PIC S9(4)    VALUE +15           DPKUT040
00077                                  COMP SYNC.                       DPKUT040
00078                                                                   DPKUT040
00079  01  PV-PROGRAM-VARIABLES.                                        DPKUT040
00080      05  PV-CHECK-DIGIT-ACCUMULATOR                               DPKUT040
00081                                  PIC S9(3)    VALUE ZERO.         DPKUT040
00082      05  FILLER                  REDEFINES                        DPKUT040
00083                                      PV-CHECK-DIGIT-ACCUMULATOR.  DPKUT040
00084          10  FILLER              PIC X(2).                        DPKUT040
00085          10  PV-UPC-CHECK-DIGIT-MODIFIER                          DPKUT040
00086                                  PIC S9(1).                       DPKUT040
00087      05  PV-CHECK-DIGIT-MULTIPLIER                                DPKUT040
00088                                  PIC S9(1)    VALUE ZERO.         DPKUT040
00089          88  PV-MULTIPLY-BY-1                 VALUE +1.           DPKUT040
00090          88  PV-MULTIPLY-BY-3                 VALUE +3.           DPKUT040
00091                                                                   DPKUT040
00092  01  IU-INTERNAL-UPC-CODE-AREA.                                   DPKUT040
00093      05  IU-INTERNAL-UPC-CODE-X.                                  DPKUT040
00094          10  FILLER              PIC 9(6)     VALUE 000400.       DPKUT040
00095          10  IU-SKU-NUMBER       PIC 9(8)     VALUE ZERO.         DPKUT040
00096          10  FILLER              PIC 9(1)     VALUE ZERO.         DPKUT040
00097      05  IU-INTERNAL-UPC-CODE    REDEFINES IU-INTERNAL-UPC-CODE-X DPKUT040
00098                                  PIC 9(15).                       DPKUT040
00099                                                                   DPKUT040
00100  LINKAGE SECTION.                                                 DPKUT040
00101                                                                   DPKUT040
00102  01  LS-UPC-CHECK-DIGIT-PARMS    PIC X(26).                       DPKUT040
00103                                                                   DPKUT040
00104 ******************************************************************DPKUT040
00105  PROCEDURE DIVISION USING LS-UPC-CHECK-DIGIT-PARMS.               DPKUT040
00106 ******************************************************************DPKUT040
00107                                                                   DPKUT040
00108  0000-MAINLINE.                                                   DPKUT040
00109      MOVE LS-UPC-CHECK-DIGIT-PARMS TO                             DPKUT040
00110                DP040I-UPC-CHECK-DIGIT-PARMS.                      DPKUT040
00111                                                                   DPKUT040
00112 *                                                                 DPKUT040
00113 *  EDIT THE PARAMETERS THAT WERE PASSED                           DPKUT040
00114 *                                                                 DPKUT040
00115                                                                   DPKUT040
00116      EVALUATE TRUE  ALSO  TRUE                                    DPKUT040
00117          WHEN (NOT DP040I-VALID-CHK-DIGIT-OPTION)  ALSO  ANY      DPKUT040
00118              SET DP040I-INV-CHECK-DIGIT-OPTION TO TRUE            DPKUT040
00119          WHEN (DP040I-COMP-CHECK-DIGIT-OPTION)     ALSO           DPKUT040
00120                    (DP040I-UPC-CODE NOT NUMERIC)                  DPKUT040
00121          WHEN (DP040I-GEN-INTERNAL-UPC-OPTION)     ALSO           DPKUT040
00122                    (DP040I-SKU-NUMBER NOT NUMERIC)                DPKUT040
00123              SET DP040I-UPC-SKU-NOT-NUMERIC TO TRUE               DPKUT040
00124 *        WHEN (DP040I-COMP-CHECK-DIGIT-OPTION)     ALSO           DPKUT040
00125 *                  (DP040I-UPC-CODE = ZERO)                       DPKUT040
00126          WHEN (DP040I-GEN-INTERNAL-UPC-OPTION)     ALSO           DPKUT040
00127                    (DP040I-SKU-NUMBER = ZERO)                     DPKUT040
00128              SET DP040I-UPC-SKU-NOT-PROVIDED TO TRUE              DPKUT040
00129          WHEN OTHER                                               DPKUT040
00130              SET DP040I-NO-ERROR-DETECTED TO TRUE                 DPKUT040
00131      END-EVALUATE.                                                DPKUT040
00132                                                                   DPKUT040
00133 *                                                                 DPKUT040
00134 *  GENERATE THE INTERNAL UPC CODE, IF THAT WAS THE REQUESTED      DPKUT040
00135 *  OPTION.  NO CHECK DIGIT IS ASSIGNED AT THIS POINT.             DPKUT040
00136 *                                                                 DPKUT040
00137                                                                   DPKUT040
00138      IF ((DP040I-NO-ERROR-DETECTED)                               DPKUT040
00139              AND (DP040I-GEN-INTERNAL-UPC-OPTION))                DPKUT040
00140          MOVE DP040I-SKU-NUMBER TO IU-SKU-NUMBER                  DPKUT040
00141          MOVE IU-INTERNAL-UPC-CODE TO DP040I-UPC-CODE             DPKUT040
00142      END-IF.                                                      DPKUT040
00143                                                                   DPKUT040
00144                                                                   DPKUT040
00145      INITIALIZE PV-PROGRAM-VARIABLES                              DPKUT040
00146                 DP040I-COMPUTED-UPC-CHK-DIGIT.                    DPKUT040
00147                                                                   DPKUT040
00148 *                                                                 DPKUT040
00149 *  COMPUTE THE CHECK DIGIT.  SINCE WE ARE ASSUMING A 15 DIGIT UPC,DPKUT040
00150 *  THE MULTIPLICATION SEQUENCE BEGINS WITH ONE TIMES THE DIGIT, ASDPKUT040
00151 *  OPPOSED TO THREE TIMES THE DIGIT FOR A STANDARD 12 DIGIT UPC.  DPKUT040
00152 *                                                                 DPKUT040
00153                                                                   DPKUT040
00154      IF DP040I-NO-ERROR-DETECTED                                  DPKUT040
00155          SET PV-MULTIPLY-BY-1 TO TRUE                             DPKUT040
00156          PERFORM                                                  DPKUT040
00157                  VARYING DP040I-UPC-INDEX                         DPKUT040
00158                  FROM 1 BY 1                                      DPKUT040
00159                  UNTIL DP040I-UPC-INDEX = PC-CHECK-DIGIT-LOCATION DPKUT040
00160              COMPUTE PV-CHECK-DIGIT-ACCUMULATOR =                 DPKUT040
00161                        (PV-CHECK-DIGIT-ACCUMULATOR +              DPKUT040
00162                        (DP040I-UPC-DIGIT (DP040I-UPC-INDEX) *     DPKUT040
00163                        PV-CHECK-DIGIT-MULTIPLIER))                DPKUT040
00164              IF PV-MULTIPLY-BY-1                                  DPKUT040
00165                  SET PV-MULTIPLY-BY-3 TO TRUE                     DPKUT040
00166              ELSE                                                 DPKUT040
00167                  SET PV-MULTIPLY-BY-1 TO TRUE                     DPKUT040
00168              END-IF                                               DPKUT040
00169          END-PERFORM                                              DPKUT040
00170          COMPUTE DP040I-COMPUTED-UPC-CHK-DIGIT =                  DPKUT040
00171                    (PC-UPC-CHECK-DIGIT-BASE -                     DPKUT040
00172                    PV-UPC-CHECK-DIGIT-MODIFIER)                   DPKUT040
00173      END-IF.                                                      DPKUT040
00174                                                                   DPKUT040
00175 *                                                                 DPKUT040
00176 *  ASSIGN THE CHECK DIGIT TO THE UPC CODE FOR REQUESTS TO         DPKUT040
00177 *  GENERATE THE INTERNAL UPC CODE.                                DPKUT040
00178 *                                                                 DPKUT040
00179                                                                   DPKUT040
00180      IF ((DP040I-NO-ERROR-DETECTED)                               DPKUT040
00181              AND (DP040I-GEN-INTERNAL-UPC-OPTION))                DPKUT040
00182          MOVE DP040I-COMPUTED-UPC-CHK-DIGIT TO                    DPKUT040
00183                    DP040I-UPC-CHECK-DIGIT                         DPKUT040
00184      END-IF.                                                      DPKUT040
00185                                                                   DPKUT040
00186 *                                                                 DPKUT040
00187 *  MOVE THE PARAMETERS LIST, WITH RESPONSES INCLUDED, TO THE      DPKUT040
00188 *  LINKAGE SECTION, FOR PASSING BACK TO THE CALLING PROGRAM.      DPKUT040
00189 *                                                                 DPKUT040
00190                                                                   DPKUT040
00191      MOVE DP040I-UPC-CHECK-DIGIT-PARMS TO                         DPKUT040
00192                LS-UPC-CHECK-DIGIT-PARMS.                          DPKUT040
00193                                                                   DPKUT040
00194  1000-EXIT-PROGRAM.                                               DPKUT040
00195      EXIT PROGRAM.                                                DPKUT040
