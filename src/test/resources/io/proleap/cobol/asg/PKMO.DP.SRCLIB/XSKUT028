      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.    XSKUT028.                                                 
       AUTHOR.        JOSEPH MENYAH.                                            
       DATE-WRITTEN.  OCTOBER , 2002.                                           
                                                                                
      *----------------------------------------------------------               
      *            ADHOC STORE LIST EXTRACT                                     
      *----------------------------------------------------------               
      * THIS PROGRAM WILL PERFORM THE NECESSARY BUSINESS EVENT                  
      * EXTRACT FOR THE ADHOC STORE LISTE BUSINESS EVENT                        
      *                                                                         
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         7/15/2004                                                 
      * WR/IR#:       P000671132                                                
      * PROGRAMMER:   NANCY MARTIN                                              
      * DESCRIPTION:  IMPROVE SQL - PROGRAM WAS FAILING DUE TO JOIN             
      *               WITH TABLE THAT HAS GROWN SIGNIFICANTLY SINCE             
      *               ORIGINAL INSTALLATION.                                    
      *                                                                         
PK8471*----------------------------------------------------------               
PK8471* DATE:         7/14/2011                                                 
PK8471* WR/IR#:       PKE#????                                                  
PK8471* PROGRAMMER:   COGNIZANT                                                 
PK8471* DESCRIPTION:  CHANGED PROGRAM TO IMPROVE SQL PERFORMANCE.               
PK8471*                                                                         
      *----------------------------------------------------------               
      * DATE:                                                                   
      * WR/IR#:                                                                 
      * PROGRAMMER:                                                             
      * DESCRIPTION:                                                            
      *                                                                         
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT EXTRACT-FILE                                                  
               ASSIGN TO OUT01.                                                 
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
       FD  EXTRACT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 125 CHARACTERS                                       
           DATA RECORD IS ADHC-STORE-LST-REC.                                   
       01  ADHC-STORE-LST-REC    PIC X(125).                                    
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-RANGE-LINE             PIC  X(80).                              
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
                                                                                
      *---------------------------------------------------------                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *---------------------------------------------------------                
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08) VALUE 'XSKUT028'.         
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +5 COMP.           
                                                                                
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-EXTRACT-SW           PIC X(01)  VALUE SPACE.              
               88  PS-EOF-EXTRACT                     VALUE 'Y'.                
               88  PS-NOT-EOF-EXTRACT                 VALUE 'N'.                
           05  PS-EOF-EXTRACT1-SW          PIC X(01)  VALUE SPACE.              
               88  PS-EOF-EXTRACT1                    VALUE 'Y'.                
               88  PS-NOT-EOF-EXTRACT1                VALUE 'N'.                
           05  PS-DATE-RANGE-FILE-SW        PIC  X(01)  VALUE 'N'.              
               88  PS-DATE-RANGE-FILE-END               VALUE 'Y'.              
               88  PS-DATE-RANGE-FILE-START             VALUE 'N'.              
           05  PS-TMST-FILE-SW              PIC  X(01)  VALUE 'N'.              
               88  PS-TMST-FILE-END                     VALUE 'Y'.              
               88  PS-TMST-FILE-START                   VALUE 'N'.              
                                                                                
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01 PV-PROGRAM-VARIABLES.                                                 
          05  PV-RECORD-COUNT               PIC 9(07) VALUE ZEROS.              
          05  PV-EXTRACT-CNT                PIC 9(07) VALUE ZEROS.              
          05  PV-EXTRACT1-CNT               PIC 9(07) VALUE ZEROS.              
          05  ABEND-CODE              PIC S9(4)  VALUE ZEROS COMP SYNC.         
          05  PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.           
          05  PV-MAX-OFFSET-DTE             PIC 9(08)  VALUE 0.                 
          05  PV-LAST-UPD-TMST.                                                 
              10  PV-AD-EVNT-END-DTE.                                           
                  15  PV-LAST-UPD-CCYY          PIC X(04).                      
                  15  FILLER                    PIC X(01) VALUE '-'.            
                  15  PV-AD-EVNT-DTE-TM2-MM     PIC X(02).                      
                  15  FILLER                    PIC X(01) VALUE '-'.            
                  15  PV-AD-EVNT-DTE-TM2-DD     PIC X(02).                      
                  15  PV-TIME-FILLER            PIC X(01) VALUE '.'.            
              10  PV-AD-EVNT-END-TM             PIC X(15).                      
          05  PV-AD-EVNT-ID                 PIC S9(09) VALUE +0.                
                                                                                
          05  PV-START-CC-TMST-X.                                               
              10  PV-START-CC-DTE           PIC X(10).                          
              10  PV-START-DEFAULT          PIC X(16) VALUE                     
                                  '-00.00.00.000000'.                           
          05  PV-START-CC-TMST REDEFINES PV-START-CC-TMST-X PIC X(26).          
          05  PV-END-CC-TMST-X.                                                 
              10  PV-END-CC-DTE             PIC X(10).                          
              10  PV-END-DEFAULT            PIC X(16) VALUE                     
                                  '-23.59.59.999999'.                           
          05  PV-END-CC-TMST REDEFINES PV-END-CC-TMST-X PIC X(26).              
      *N  05  PV-STRT-PARM.                                                     
      *N      10  PV-STRT-DTE-CC            PIC X(02).                          
      *N      10  PV-STRT-DTE-YY            PIC X(02).                          
      *N      10  PV-STRT-DTE-MM            PIC X(02).                          
      *N      10  PV-STRT-DTE-DD            PIC X(02).                          
      *N      10  FILLER                    PIC X.                              
      *N      10  PV-DECREMENT              PIC X(02).                          
      *N      10  FILLER                    PIC X(69).                          
                                                                                
          05  PV-EVNT-STRT-DTE              PIC X(10).                          
                                                                                
                                                                                
          05  PV-EVNT-END-DTE-TM.                                               
              10  PV-EVNT-END-DTE.                                              
                  15  PV-EVNT-DTE-TM2-CCYY      PIC X(04).                      
                  15  FILLER                    PIC X(01).                      
                  15  PV-EVNT-DTE-TM2-MM        PIC X(02).                      
                  15  FILLER                    PIC X(01).                      
                  15  PV-EVNT-DTE-TM2-DD        PIC X(02).                      
              10  PV-EVNT-END-TM                PIC X(16).                      
                                                                                
          05  PV-NULLS-INDICATORS.                                              
              10  PV-EFF-END-TMST          PIC S9(4) VALUE +0 COMP.             
          05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.            
          05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.            
                                                                                
       01 PV-TOTAL-RECORD.                                                      
          05 PV-BUSINESS-EVNT-ID              PIC 9(03) VALUE ZERO.             
          05 PV-ACTION-CDE                    PIC X(01) VALUE 'T'.              
          05 PV-TOTAL-NBR-OF-RECS             PIC 9(11) VALUE ZERO.             
      *----------------------------------------------------------               
      * ADHOC STORE LIST EVENT RECORD LAYOUT                                    
      *----------------------------------------------------------               
           COPY XSRD024.                                                        
                                                                                
      *----------------------------------------------------------               
      * DATE ROUTINE COPY MEMBERS                                               
      *----------------------------------------------------------               
           COPY DPWS500.                                                        
                                                                                
      *----------------------------------------------------------               
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                 
      *----------------------------------------------------------               
           COPY DPWS004.                                                        
                                                                                
      *---------------------------------------------                            
      *  DB2 COMMUNICATION AREA.                                                
      *---------------------------------------------                            
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
      *---------------------------------------------                            
      *  DB2 TABLE XST00033(XST_PROMO_ADHC_STR)                                 
      *---------------------------------------------                            
           EXEC SQL                                                             
                INCLUDE XST00033                                                
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------                            
      *  DB2 TABLE XST00036(XST_ADHC_STR_GP)                                    
      *---------------------------------------------                            
           EXEC SQL                                                             
                INCLUDE XST00036                                                
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------                            
      *  DB2 TABLE XST00031(XST_PROMO_ADHC_RLT)                                 
      *---------------------------------------------                            
           EXEC SQL                                                             
                INCLUDE XST00031                                                
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------                            
      *   EXTRACT CURSOR  DB2 SELECT                                            
      *---------------------------------------------                            
           EXEC SQL                                                             
             DECLARE EXTRACT CURSOR FOR                                         
              SELECT DISTINCT A.ADHC_STR_GP_ID,                                 
                     A.STR_NBR,                                                 
                     A.STAT_CDE,                                                
                     A.EFF_STRT_TMST,                                           
                     A.EFF_END_TMST,                                            
                     A.SOR_LAST_UPD_TMST                                        
              FROM XST_PROMO_ADHC_STR A,                                        
                   XST_ADHC_STR_GP C                                            
              WHERE  C.ADHC_STR_GP_ID = A.ADHC_STR_GP_ID                        
              AND    C.RUSBL_IND = 'N'                                          
              AND    A.SOR_LAST_UPD_TMST <=:CC-LAST-UPD-TMST                    
PK8471        AND  EXISTS                                                       
PK8471             (SELECT 1                                                    
                      FROM XST_PROMO_ADHC_RLT B                                 
                     WHERE B.ADHC_STR_GP_ID  =   C.ADHC_STR_GP_ID               
                       AND B.PROMO_END_TMST  >= :PV-START-CC-TMST               
                       AND B.PROMO_STRT_TMST <= :PV-END-CC-TMST)                
               WITH UR                                                          
           END-EXEC.                                                            
      ******  FROM XST_PROMO_ADHC_STR A,                                        
      ******       XST_PROMO_ADHC_RLT B,                                        
      ******       XST_ADHC_STR_GP C                                            
      ******  WHERE  B.ADHC_STR_GP_ID = A.ADHC_STR_GP_ID                        
      ******  AND    B.ADHC_STR_GP_ID = C.ADHC_STR_GP_ID                        
      ******  AND    C.RUSBL_IND = 'N'                                          
      ******  AND    B.PROMO_END_TMST    >= :PV-START-CC-TMST                   
      ******  AND    B.PROMO_STRT_TMST   <= :PV-END-CC-TMST                     
      ******  AND    A.SOR_LAST_UPD_TMST <=:CC-LAST-UPD-TMST                    
      ****    AND    DATE(B.PROMO_END_TMST)                                     
      ****                >= :CC-START-DATE                                     
      ****    AND    DATE(B.PROMO_STRT_TMST)                                    
      ****                <= :CC-END-DATE                                       
      *---------------------------------------------                            
      *   EXTRACT1 CURSOR  DB2 SELECT                                           
      *---------------------------------------------                            
           EXEC SQL                                                             
             DECLARE EXTRACT1 CURSOR FOR                                        
              SELECT DISTINCT B.ADHC_STR_GP_ID,                                 
                     B.STR_NBR,                                                 
                     B.STAT_CDE,                                                
                     B.EFF_STRT_TMST,                                           
                     B.EFF_END_TMST,                                            
                     B.SOR_LAST_UPD_TMST                                        
              FROM XST_ADHC_STR_GP A,                                           
                   XST_PROMO_ADHC_STR B                                         
              WHERE  A.ADHC_STR_GP_ID = B.ADHC_STR_GP_ID                        
              AND    A.RUSBL_IND = 'Y'                                          
              AND    ((B.EFF_END_TMST >= :PV-START-CC-TMST)                     
               OR     (B.EFF_END_TMST IS NULL))                                 
              AND    B.EFF_STRT_TMST  <= :PV-END-CC-TMST                        
               WITH UR                                                          
           END-EXEC.                                                            
      ****    AND    ((DATE(B.EFF_END_TMST)                                     
      ****                >= :CC-START-DATE)                                    
      ****     OR     (B.EFF_END_TMST IS NULL))                                 
      ****    AND    DATE(B.EFF_STRT_TMST)                                      
      ****                <= :CC-END-DATE                                       
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-EXTRACT-DATA                                            
             UNTIL PS-EOF-EXTRACT                                               
               AND PS-EOF-EXTRACT1.                                             
                                                                                
           PERFORM 9990-WRITE-RECORD-CNT.                                       
                                                                                
           PERFORM 9999-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
      *0000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *     INTIALIZES OR MOVES ANY VARIABLES BEFORE IT WILL                    
      *     UPDATE OR INSERT A RECORD.                                          
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
                                                                                
           SET PS-NOT-EOF-EXTRACT TO TRUE.                                      
           SET PS-NOT-EOF-EXTRACT1 TO TRUE.                                     
                                                                                
           OPEN INPUT DATE-RANGE-FILE                                           
                      TMST-FILE                                                 
                OUTPUT EXTRACT-FILE.                                            
                                                                                
           PERFORM 2010-COMPUTE-STRT-DTE                                        
              THRU 2010-EXIT.                                                   
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0                                        
                         OR  SQLCODE = +100)                                    
                                                                                
             EXEC SQL                                                           
                 OPEN EXTRACT                                                   
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
               WHEN SQLCODE = ZERO                                              
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 DISPLAY 'OPEN  FAILED FOR CURSOR EXTRACT'                      
                 PERFORM 9900-SQL-ABEND-ROUTINE                                 
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
             DISPLAY 'OPEN RETRIES EXCEEDED ON EXTRACT CURSOR'                  
             PERFORM 9900-SQL-ABEND-ROUTINE                                     
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0                                        
                         OR  SQLCODE = +100)                                    
                                                                                
             EXEC SQL                                                           
                 OPEN EXTRACT1                                                  
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
               WHEN SQLCODE = ZERO                                              
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 DISPLAY 'OPEN  FAILED FOR CURSOR EXTRACT1'                     
                 PERFORM 9900-SQL-ABEND-ROUTINE                                 
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
             DISPLAY 'OPEN RETRIES EXCEEDED ON EXTRACT1 CURSOR'                 
             PERFORM 9900-SQL-ABEND-ROUTINE                                     
           END-IF.                                                              
                                                                                
           INITIALIZE DCLXST00033.                                              
                                                                                
      *1100-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2000-EXTRACT-DATA.                                                      
      *     THIS CHECKS THE ACTION TO SEE WHETHER THE RECORD                    
      *     SHOULD BE INSERTED INTO THE AD EVENT TABLE OR UPDATED.              
      *     IF SOMETHING IS WRONG WITH THE ACTION CODE IT WILL                  
      *     PROCESS AN ERROR AND EXIT THE PROGRAM.                              
      *----------------------------------------------------------               
       2000-EXTRACT-DATA.                                                       
                                                                                
           IF PS-NOT-EOF-EXTRACT                                                
             PERFORM 2100-FETCH-EXTRACT                                         
                THRU 2100-EXIT                                                  
           END-IF.                                                              
                                                                                
           IF PS-NOT-EOF-EXTRACT                                                
               PERFORM 2200-FORMAT-EXTRACT-FILE                                 
                  THRU 2200-EXIT                                                
                                                                                
               PERFORM 3000-WRITE-ADHC-STR-LST-EXTRCT                           
                  THRU 3000-EXIT                                                
           END-IF.                                                              
                                                                                
           IF PS-NOT-EOF-EXTRACT1                                               
             PERFORM 2110-FETCH-EXTRACT1                                        
                THRU 2110-EXIT                                                  
           END-IF.                                                              
                                                                                
           IF PS-NOT-EOF-EXTRACT1                                               
               PERFORM 2200-FORMAT-EXTRACT-FILE                                 
                  THRU 2200-EXIT                                                
                                                                                
               PERFORM 3000-WRITE-ADHC-STR-LST-EXTRCT                           
                  THRU 3000-EXIT                                                
           END-IF.                                                              
                                                                                
      *2000-EXIT.                                                               
       EJECT.                                                                   
       2010-COMPUTE-STRT-DTE.                                                   
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
               MOVE CC-START-DATE TO      PV-START-CC-DTE                       
               MOVE CC-END-DATE   TO      PV-END-CC-DTE                         
               DISPLAY 'PV-START-TMST = ' PV-START-CC-TMST                      
               DISPLAY 'PV-END-TMST   = ' PV-END-CC-TMST                        
           END-IF.                                                              
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
       2010-EXIT.                                                               
           EXIT.                                                                
       EJECT.                                                                   
      *------------------------------------------------------                   
      * 2100-FETCH-EXTRACT                                                      
      *     EXTRACTS THE AD EVENT RECORD.                                       
      *------------------------------------------------------                   
       2100-FETCH-EXTRACT.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0                                        
                         OR  SQLCODE = +100)                                    
                                                                                
             EXEC SQL                                                           
                 FETCH EXTRACT                                                  
                     INTO :XST00033-ADHC-STR-GP-ID,                             
                          :XST00033-STR-NBR,                                    
                          :XST00033-STAT-CDE,                                   
                          :XST00033-EFF-STRT-TMST,                              
                          :XST00033-EFF-END-TMST:PV-EFF-END-TMST,               
                          :XST00033-SOR-LAST-UPD-TMST                           
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                 CONTINUE                                                       
               WHEN SQLCODE = ZERO                                              
                    ADD 1 TO PV-EXTRACT-CNT                                     
               WHEN SQLCODE = +100                                              
                 MOVE 'Y' TO PS-EOF-EXTRACT-SW                                  
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE  NOT EQUAL ZERO                                     
                 DISPLAY '2100-FETCH-EXTRACT - FETCH FAILED'                    
                 PERFORM U9980-SQL-ERROR                                        
                   THRU U9980-EXIT                                              
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
             DISPLAY '2100-FETCH-EXTRACT - FETCH RETRIES EXCEEDED'              
             PERFORM U9980-SQL-ERROR                                            
               THRU U9980-EXIT                                                  
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-FETCH-EXTRACT1.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0                                        
                         OR  SQLCODE = +100)                                    
             EXEC SQL                                                           
                 FETCH EXTRACT1                                                 
                     INTO :XST00033-ADHC-STR-GP-ID,                             
                          :XST00033-STR-NBR,                                    
                          :XST00033-STAT-CDE,                                   
                          :XST00033-EFF-STRT-TMST,                              
                          :XST00033-EFF-END-TMST:PV-EFF-END-TMST,               
                          :XST00033-SOR-LAST-UPD-TMST                           
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                 CONTINUE                                                       
               WHEN SQLCODE = ZERO                                              
                 ADD 1 TO PV-EXTRACT1-CNT                                       
               WHEN SQLCODE = +100                                              
                 MOVE 'Y' TO PS-EOF-EXTRACT1-SW                                 
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE  NOT EQUAL ZERO                                     
                 DISPLAY '2110-FETCH-EXTRACT1 - FETCH FAILED'                   
                 PERFORM U9980-SQL-ERROR                                        
                   THRU U9980-EXIT                                              
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
             DISPLAY '2110-FETCH-EXTRACT1 - FETCH RETRIES EXCEEDED'             
             PERFORM U9980-SQL-ERROR                                            
               THRU U9980-EXIT                                                  
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------                   
      * 2200-FORMAT-EXTRACT-FILE                                                
      *     FORMATS THE ADHOC STORE GROUP COPY BOOK.                            
      *------------------------------------------------------                   
       2200-FORMAT-EXTRACT-FILE.                                                
           MOVE XST00033-ADHC-STR-GP-ID TO XS024-ADHC-STR-GP-ID                 
           MOVE XST00033-STR-NBR        TO XS024-STR-NBR                        
           MOVE 'U'                     TO XS024-ACTION-CDE                     
           MOVE XST00033-STAT-CDE       TO XS024-STAT-CDE                       
           MOVE XST00033-EFF-STRT-TMST  TO XS024-STRT-TMST                      
           IF PV-EFF-END-TMST = -1                                              
              MOVE SPACES TO XS024-END-TMST                                     
           ELSE                                                                 
              MOVE XST00033-EFF-END-TMST   TO XS024-END-TMST                    
           END-IF.                                                              
           MOVE XST00033-SOR-LAST-UPD-TMST   TO PV-LAST-UPD-TMST.               
           MOVE '.'                          TO PV-TIME-FILLER.                 
           MOVE PV-LAST-UPD-TMST             TO XS024-LAST-UPD-TMST.            
           MOVE 050                     TO XS024-BUSINESS-EVNT-ID.              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
       EJECT.                                                                   
                                                                                
      *------------------------------------------------------                   
      * 3000-WRITE-ADHC-STR-LST-EXTRCT                                          
      *     WRITE EXTRACT FILE.                                                 
      *------------------------------------------------------                   
       3000-WRITE-ADHC-STR-LST-EXTRCT.                                          
           ADD 1 TO PV-RECORD-COUNT.                                            
                                                                                
           WRITE ADHC-STORE-LST-REC FROM XS024-ADHOC-STR-LST-RECORD.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
       EJECT.                                                                   
                                                                                
                                                                                
                                                                                
       9990-WRITE-RECORD-CNT.                                                   
           INITIALIZE XS024-ADHOC-STR-LST-RECORD.                               
           MOVE 050 TO PV-BUSINESS-EVNT-ID.                                     
           MOVE PV-RECORD-COUNT TO PV-TOTAL-NBR-OF-RECS.                        
           MOVE 'T' TO PV-ACTION-CDE.                                           
           WRITE ADHC-STORE-LST-REC FROM PV-TOTAL-RECORD.                       
           DISPLAY '** EXTRACT  FETCH COUNT-', PV-EXTRACT-CNT                   
           DISPLAY '** EXTRACT1 FETCH COUNT-', PV-EXTRACT1-CNT                  
           DISPLAY '** OUTPUT RECORDS COUNT-', PV-RECORD-COUNT.                 
      *9990-EXIT.                                                               
                                                                                
      *----------------------------------------------------------               
      * 9999-WRAPUP.                                                            
      *     MOVES ALL ERROR CODES TO THE LINKAGE VARIABLES TO                   
      *     BE PASSED BACK TO THE CALLING PROGRAM.  EXITS THE                   
      *     PROGRAM.                                                            
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
           CLOSE DATE-RANGE-FILE                                                
                 TMST-FILE                                                      
                 EXTRACT-FILE.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE EXTRACT                                                    
           END-EXEC.                                                            
           IF SQLCODE NOT = +0                                                  
              DISPLAY 'CLOSE FAILED FOR CURSOR EXTRACT'                         
               PERFORM U9980-SQL-ERROR                                          
                  THRU U9980-EXIT                                               
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE EXTRACT1                                                   
           END-EXEC.                                                            
           IF SQLCODE NOT = +0                                                  
             DISPLAY 'CLOSE FAILED FOR CURSOR EXTRACT1'                         
             PERFORM U9980-SQL-ERROR                                            
               THRU U9980-EXIT                                                  
           END-IF.                                                              
                                                                                
      *9999-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
       U9980-SQL-ERROR.                                                         
      *----------------------------------------------------------               
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLWARNING'.                         
           DISPLAY '** PROGRAM        ** = XSKUT028'.                           
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
                                                                                
           MOVE 4013 TO ABEND-CODE.                                             
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       U9980-EXIT.                                                              
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------               
       U9990-SQL-WARNING.                                                       
      *----------------------------------------------------------               
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLWARNING'.                         
           DISPLAY '** PROGRAM        ** = XSKUT028'.                           
                                                                                
           MOVE 4013 TO ABEND-CODE.                                             
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       U9990-EXIT.                                                              
           EXIT.                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *----------------------------------------------------------               
      * 9900-SQL-ABEND-ROUTINE.                                                 
      *     SQL ABEND ROUTINE.                                                  
      *----------------------------------------------------------               
       9900-SQL-ABEND-ROUTINE.                                                  
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** =   XSKUT028'.                              
           DISPLAY '** PARAGRAPH ** = '.                                        
           DISPLAY '** OPERATION ** = '.                                        
                                                                                
           COPY DPPD004.                                                        
           MOVE 4013 TO ABEND-CODE.                                             
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *9900-EXIT.                                                               
       EJECT.                                                                   
                                                                                
