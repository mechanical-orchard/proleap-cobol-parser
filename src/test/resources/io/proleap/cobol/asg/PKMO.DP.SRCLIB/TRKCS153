000200 IDENTIFICATION DIVISION.                                                 
000300                                                                          
000400 PROGRAM-ID.    TRKCS153.                                                 
000500 AUTHOR.        MARGARET MORRISON.                                        
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  12-18-97.                                                 
000800 DATE-COMPILED.                                                           
000900******************************************************************        
001000*    Y153 - PORT OF ENTRY CODE CRITERIA SPECIFICATIONS           *        
001100*                                                                *        
001200*    THIS PROGRAM ALLOWS THE USER ENTER DATA TO BE USED BY       *        
001300*    SUBSEQUENT PROGRAMS TO CREATE A LIST OR TO BRING UP A       *        
001400*    DETAIL SCREEN.  THE POE CODE, IF ENTERED, IS EDITTED        *        
001500*    TO BE A VALID CODE.  ALL OTHER EDITS ARE DONE IN THE        *        
001600*    DESTINATION PROGRAMS WHEN THE 'GO-TO' TYPE FUNCTION IS USED *        
001700*    (LOCAL-FUNC-01 THRU LOCAL-FUNC-12, WITH DISPOSITION 'G').   *        
001800******************************************************************        
CICS  * CHANGED 11/21/2022    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001900                                                                          
002000 ENVIRONMENT DIVISION.                                                    
002100                                                                          
002200 DATA DIVISION.                                                           
002300                                                                          
002400 WORKING-STORAGE SECTION.                                                 
002500                                                                          
002600 01  DK-DB2-CONSTANTS.                                                    
002700     05  DK-POE-CDE                PIC  X(03).                            
002800     05  DK-ENT-ID                 PIC S9(09) COMP.                       
002810                                                                          
002900 01  PV-PROGRAM-VARIABLES.                                                
003000     05  PV-ASTRK-CNT              PIC S9(04)  VALUE +0                   
003100                                               COMP SYNC.                 
003110     05  PV-POE-CDE                PIC  X(03)  VALUE SPACE.               
003111     05  PV-RETRY-COUNTER          PIC S9(04)  VALUE +0                   
003120                                               COMP SYNC.                 
003200 01  PC-PROGRAM-CONSTANTS.                                                
003300     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR153A  '.         
003400     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM153 '.         
003500     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS153'.         
003600     05  PC-ASTRK                  PIC  X(01)  VALUE '*'.                 
003700     05  PC-A                      PIC  X(01)  VALUE 'A'.                 
003800     05  PC-I                      PIC  X(01)  VALUE 'I'.                 
005610     05  PC-PRIMARY                PIC XX      VALUE '01'.                
005611     05  PC-MAX-RETRIES            PIC S9(03)  VALUE 3                    
005612                                               COMP-3.                    
005613                                                                          
005614     05  PC-TSYMSG-NUMBERS.                                               
005615         10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.              
005616*             -  REQUIRED ENTRY                                           
005617         10  PC-TSYMSG-01452       PIC  9(05)  VALUE  01452.              
005618*             -  THE PORT CODE IS INVALID                                 
005619         10  PC-TSYMSG-01644       PIC  9(05)  VALUE  01644.              
005620*             -  CODE IS INACTIVE                                         
005621                                                                          
005622 01  PS-PROGRAM-SWITCHES.                                                 
005623     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
005624         88  PS-ERROR                          VALUE  'Y'.                
005625         88  PS-NO-ERROR                       VALUE  'N'.                
005626                                                                          
005627 01  PM-PROGRAM-MESSAGES.                                                 
005628     05  PM-ABEND-INFO-A.                                                 
005629         10  FILLER                PIC X(15)   VALUE                      
005630                                   'DP020-SRC-AID: '.                     
005631         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
005632         10  FILLER                PIC X(19)   VALUE                      
005640                                   '. DP020-FK-ACTION: '.                 
005700         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
005800         10  FILLER                PIC X(22)   VALUE                      
005900                                   '. DP020-FK-DISPN-IND: '.              
006000         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
006100         10  FILLER                PIC X(01)   VALUE '.'.                 
006200         10  FILLER                PIC X(19)   VALUE SPACE.               
006400                                                                          
006500******************************************************************        
006600*    MAP LAYOUT                                                  *        
006700******************************************************************        
006900     COPY TRKM153.                                                        
007000 EJECT                                                                    
007100******************************************************************        
007200*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
007300******************************************************************        
007500     COPY DPWS015.                                                        
007600                                                                          
007700******************************************************************        
007800*    FUNCTION KEYS COPYBOOK                                      *        
007900******************************************************************        
008100     COPY DPWS016.                                                        
008200 EJECT                                                                    
008300******************************************************************        
008400** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
008500** SUB-ROUTINE (DPKUT100).                                      **        
008600******************************************************************        
008700     COPY DPWS010I.                                                       
008800 EJECT                                                                    
008900******************************************************************        
009000** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **        
009100** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **        
009200******************************************************************        
009300     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
009400 EJECT                                                                    
009500*****************************************************************         
009600*    STANDARD COMMAREA.                                                   
009700*****************************************************************         
009800                                                                          
009900     COPY DPWS020.                                                        
010000     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
010100                                                                          
010200*****************************************************************         
010300*    INTER-APPLICATION COMMAREA.                                          
010400*****************************************************************         
010500                                                                          
010600         10  INTER-APPL-COMM-AREA PIC X(200).                             
010700     COPY TRWS022.                                                        
010800*****************************************************************         
010900*    SPECIFIC COMMMAREA FOR TRKCS153.                                     
011000*****************************************************************         
011100         10  ASC-SPECIFIC-COMMAREA.                                       
011200             15  ASC-POE-CDE                    PIC  X(03).               
028100             15  ASC-POE-STD-EDI-CDE            PIC  X(04).               
028101             15  ASC-POE-NM                     PIC  X(30).               
028102             15  ASC-POE-CTY-NM                 PIC  X(30).               
028103             15  ASC-POE-CDE-IND                PIC  X(01).               
028104                 88  ASC-VALID-POE-CDE               VALUE 'Y'.           
028105                 88  ASC-INVALID-POE-CDE             VALUE 'N'.           
028106                 88  ASC-INACTIVE-POE-CDE            VALUE 'I'.           
028110             15  ASC-DCONSLR-NM                 PIC  X(30).               
028120             15  ASC-DCONSLR-ENT-ID             PIC S9(9).                
028300             15  ASC-POE-ST-CDE                 PIC  X(02).               
028601             15  ASC-POE-STAT-CDE               PIC  X(01).               
028602     EJECT                                                                
028603******************************************************************        
028604*    ABEND PROCESSING COPYBOOK.                                  *        
028605******************************************************************        
028607     COPY DPWS013.                                                        
028608                                                                          
028609*****************************************************************         
028610*    DB2 AREA FOR PORT OF ENTRY TABLE                           *         
028611*****************************************************************         
028612     EXEC SQL                                                             
028613          INCLUDE TIMPOE                                                  
028614     END-EXEC.                                                            
028615                                                                          
035900*****************************************************************         
036000*    DB2 AREA FOR TENTNME                                       *         
036100*****************************************************************         
036200     EXEC SQL                                                             
036300          INCLUDE TENTNME                                                 
036400     END-EXEC.                                                            
036401                                                                          
036402*****************************************************************         
036403*    DB2 AREA FOR TEXTENT                                       *         
036404*****************************************************************         
036405     EXEC SQL                                                             
036406          INCLUDE TEXTENT                                                 
036407     END-EXEC.                                                            
036408                                                                          
036409*****************************************************************         
036410*    DB2 AREA FOR COMMUNICATIONS                                *         
036411*****************************************************************         
036412     EXEC SQL                                                             
036413          INCLUDE SQLCA                                                   
036414     END-EXEC.                                                            
036415                                                                          
036416 EJECT                                                                    
036417                                                                          
036418*---------------------------------------------------------------* 03880099
036419*    CURSOR DECLARATION FOR SELECTING BOTH ACTIVE AND INACTIVE  *         
036420*    POE CODES                                                  *         
036421*---------------------------------------------------------------* 03900099
036422                                                                  03910099
036423     EXEC SQL                                                     03920099
036424          DECLARE  POE-CSR1 CURSOR                                03930099
036425              FOR  SELECT DISTINCT                                03940099
036426                   A.POE_NM                                               
036427                  ,A.POE_CTY_NM                                           
036428                  ,A.STAT_CDE                                             
036429             FROM  TIMPOE  A                                              
036430            WHERE  A.POE_CDE = :DK-POE-CDE                                
036431              AND (A.STAT_CDE = :PC-A                                     
036432              OR   A.STAT_CDE = :PC-I)                                    
036434     END-EXEC.                                                            
036446                                                                          
036447*---------------------------------------------------------------* 03880099
036448*    CURSOR DECLARATION FOR SELECTING POE CODE INFORMATION      *         
036450*---------------------------------------------------------------* 03900099
036451                                                                  03910099
036452     EXEC SQL                                                     03920099
036453          DECLARE  POE-CSR2 CURSOR                                03930099
036454              FOR  SELECT DISTINCT                                03940099
036457                   A.POE_CDE                                              
036458                 , A.DCONSLR_ENT_ID                                       
036459                 , A.POE_STD_EDI_CDE                                      
036460                 , A.POE_NM                                               
036461                 , A.POE_CTY_NM                                           
036462                 , A.POE_ST_CDE                                           
036463             FROM  TIMPOE  A                                              
036464            WHERE  A.POE_CDE  = :PV-POE-CDE                               
036465              AND  A.STAT_CDE = :PC-A                                     
036469     END-EXEC.                                                            
036485                                                                          
036486 EJECT                                                            04110099
036487 LINKAGE SECTION.                                                         
036488                                                                          
036489 01  DFHCOMMAREA.                                                         
036490     05  FILLER                         OCCURS  1 TO 4072 TIMES           
036491                                        DEPENDING ON EIBCALEN.            
036492         10  FILLER                     PIC X.                            
036493                                                                          
036494                                                                          
036495 PROCEDURE DIVISION.                                                      
036496*****************************************************************         
036497** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
036498** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
036499** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
036500**                                                             **         
036501** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
036502** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
036503*****************************************************************         
036504 0000-MAIN-MODULE.                                                        
036505     INITIALIZE DP030-CICS-API-FIELDS.                                    
036506     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
036507     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
036508     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
036509     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
036510     MOVE LENGTH OF TR153AI        TO DP030-MAP-LENGTH (1).               
036511     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
036512     PERFORM 0001-CALL-CICS-ARCH-API.                                     
036513                                                                          
036514     PERFORM 1000-CONTROL-PROCESSING.                                     
036515                                                                          
036516     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
036517     PERFORM 0001-CALL-CICS-ARCH-API.                                     
036518                                                                          
036519 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
036521                                    DFHCOMMAREA                           
036522                                    DP030-CICS-API-FIELDS                 
036523                                    DP020-STANDARD-COMMAREA               
036524                                    TR153AI.                              
036525                                                                          
036526     IF  DP030-RC-CALL-SUCCESSFUL                                         
036527         CONTINUE                                                         
036528     ELSE                                                                 
036529         SET DP013-NO-ROLLBACK                                            
036530             DP013-XCTL-DISPLAY-RESTART                                   
036531             DP013-CICS-ABEND      TO TRUE                                
036532         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
036533                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
036536         MOVE DP030-RETURN-CODE                                           
036537                                   TO DP013-MESSAGE-TEXT (3)              
036538         PERFORM DP013-0000-PROCESS-ABEND                                 
036539     END-IF.                                                              
036540 EJECT                                                                    
036541******************************************************************        
036542*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
036543*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
036544******************************************************************        
036545 1000-CONTROL-PROCESSING.                                                 
036546                                                                          
036547     EVALUATE TRUE                                                        
036548         WHEN DP020-NEXT-ACT-INITIAL                                      
036549             INITIALIZE ASC-SPECIFIC-COMMAREA                             
036550             PERFORM 4000-BUILD-INITIAL-PANEL                             
036551                                                                          
036552         WHEN DP020-NEXT-ACT-READ-MAP                                     
036553             PERFORM 2000-FKEY-PROCESSING                                 
036554                                                                          
036555         WHEN DP020-NEXT-ACT-RETURN                                       
036556             MOVE SPACES  TO ASC-POE-STAT-CDE                             
036557             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
036558             MOVE -1      TO APOECODL                                     
036559             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
036560             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036561                                                                          
036562         WHEN OTHER                                                       
036563             SET DP013-LOGIC-ABEND                                        
036564                 DP013-NO-ROLLBACK TO TRUE                                
036565             MOVE '1000-CONTROL-PROCESSING'                               
036566                                   TO  DP013-PARAGRAPH                    
036567             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
036568                                   TO  DP013-MESSAGE-TEXT(1)              
036569             MOVE DP020-NEXT-APPL-ACTIVITY                                
036570                                   TO DP013-MESSAGE-TEXT(2)               
036571             PERFORM DP013-0000-PROCESS-ABEND                             
036572     END-EVALUATE.                                                        
036573                                                                          
036574******************************************************************        
036575* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
036576* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
036577* EDITS.                                                         *        
036578******************************************************************        
036579 2000-FKEY-PROCESSING.                                                    
036580                                                                          
036581     EVALUATE TRUE                                                        
036582                                                                          
036583         WHEN DP020-SRC-AID = DP016-CLEAR                                 
036584             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036585             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
036586                                                                          
036587         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
036588             INITIALIZE ASC-SPECIFIC-COMMAREA                             
036589             PERFORM 4000-BUILD-INITIAL-PANEL                             
036590             PERFORM 3200-RESET-ATTRIBUTES                                
036591                                                                          
036592         WHEN OTHER                                                       
036593             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
036594             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036595             IF PS-NO-ERROR OR                                            
036596                DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                    
036597                 SET DP030-CONTINUE-GO TO TRUE                            
036598                 PERFORM 2100-CHECK-FUNCTION-KEY                          
036599             ELSE                                                         
036600                 SET DP030-OVERRIDE-GO TO TRUE                            
036601             END-IF                                                       
036602     END-EVALUATE.                                                        
036603                                                                          
036604******************************************************************        
036605** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
036606** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
036607** FROM THE CICS ARCHITECTURE API.                              **        
036608******************************************************************        
036609 2100-CHECK-FUNCTION-KEY.                                                 
036610                                                                          
036611     EVALUATE TRUE                                                        
036621         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
036622             MOVE 'I' TO ASC-POE-STAT-CDE                                 
036623             MOVE SPACE TO ASC-POE-CDE                                    
036624             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
036625         WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
036626             IF ASC-POE-STAT-CDE = PC-I                                   
036627                 SET DP030-OVERRIDE-GO TO TRUE                            
036628                 MOVE PC-TSYMSG-01644  TO DP020-MSG-NUMBER                
036629                 SET DP020-MSG-FATAL   TO TRUE                            
036630             ELSE                                                         
036631                 MOVE 'A' TO ASC-POE-STAT-CDE                             
036632                 PERFORM 2110-PREPARE-INTER-APPL-COMM                     
036633             END-IF                                                       
036634                                                                          
036635         WHEN DP020-SRC-AID = DP016-ENTER                                 
036636             CONTINUE                                                     
036637                                                                          
036638         WHEN OTHER                                                       
036639             SET DP013-NO-ROLLBACK                                        
036640                 DP013-XCTL-DISPLAY-RESTART                               
036641                 DP013-CICS-ABEND  TO TRUE                                
036642             MOVE '2100-CHECK-FUNCTION-KEY'                               
036643                                   TO DP013-PARAGRAPH                     
036644             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
036645                                   TO DP013-MESSAGE-TEXT (1)              
036646             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
036647                                   TO DP013-MESSAGE-TEXT (2)              
036648             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
036649             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
036650                                   TO PM-A-FK-ACTION                      
036651             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
036652                                   TO PM-A-FK-DISPN                       
036653             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
036654             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
036655                                   TO DP013-MESSAGE-TEXT (4)              
036656             PERFORM DP013-0000-PROCESS-ABEND                             
036657     END-EVALUATE.                                                        
036658                                                                          
036659******************************************************************        
036660** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
036661** INTER-APPLICATION COMMAREA.                                  **        
036662******************************************************************        
036663 2110-PREPARE-INTER-APPL-COMM.                                            
036670                                                                          
036700     INITIALIZE TR022-INTER-APPL-COMMAREA.                                
040800     MOVE ASC-POE-CDE              TO PV-POE-CDE.                         
040900                                                                          
161601     PERFORM 4100-OPEN-POE-CURSOR2.                                       
161602     PERFORM 4120-FETCH-POE-CURSOR2.                                      
161603     PERFORM 4110-CLOSE-POE-CURSOR2.                                      
161604                                                                          
161609     MOVE IMPOE-DCONSLR-ENT-ID     TO ASC-DCONSLR-ENT-ID.                 
161610     MOVE IMPOE-POE-STD-EDI-CDE    TO ASC-POE-STD-EDI-CDE.                
166000     MOVE IMPOE-POE-NM             TO ASC-POE-NM.                         
166100     MOVE IMPOE-POE-CTY-NM         TO ASC-POE-CTY-NM.                     
166200     MOVE IMPOE-POE-ST-CDE         TO ASC-POE-ST-CDE.                     
166400     MOVE ASC-POE-NM               TO TR022-POE-CODE-DESC.                
166500     MOVE ASC-POE-CDE              TO TR022-POE-CODE.                     
166600     MOVE ASC-POE-STD-EDI-CDE      TO TR022-POE-STD-EDI-CDE.              
166700     MOVE ASC-POE-CTY-NM           TO TR022-POE-CITY-NAME.                
166800     MOVE ASC-POE-ST-CDE           TO TR022-POE-STATE-CODE.               
166810     MOVE ASC-POE-STAT-CDE         TO TR022-STAT-CDE.                     
166900     MOVE ASC-DCONSLR-ENT-ID       TO TR022-DCONSLR-ENT-ID                
167000                                      DK-ENT-ID.                          
167300                                                                          
167401     PERFORM 4332-SELECT-SCAC.                                            
167403     MOVE EXTENT-SCAC-CDE          TO TR022-DCONSLR-SCAC.                 
167410                                                                          
167420     MOVE IMPOE-DCONSLR-ENT-ID     TO DK-ENT-ID.                          
167500     PERFORM 4331-SELECT-ENTNME.                                          
167600     MOVE ENTNME-ENT-NAME-DESC     TO TR022-DCONSLR-NAME.                 
168001                                                                          
168002 EJECT                                                                    
168003******************************************************************        
168004* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
168005* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
168006* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
168007******************************************************************        
168009 2200-MOVE-SCREEN-TO-COMMAREA.                                            
168010                                                                          
168011     IF  APOECODL > ZERO                                                  
168012         MOVE APOECODI             TO ASC-POE-CDE                         
168013     ELSE                                                                 
168014         IF  APOECODF = DP015-ERASE-EOF                                   
168015             MOVE SPACE            TO ASC-POE-CDE                         
168016                                      ASC-POE-CTY-NM                      
168017         END-IF                                                           
168018     END-IF.                                                              
168019                                                                          
168020******************************************************************        
168021* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
168022* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
168023* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
168024* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
168025******************************************************************        
168027 3000-EDIT-DATA-IN-COMMAREA.                                              
168028                                                                          
168029     PERFORM 3200-RESET-ATTRIBUTES.                                       
168030                                                                          
168031     IF  ASC-POE-CDE > SPACE OR                                           
168032         ASC-POE-CDE = SPACE                                              
168033         MOVE ASC-POE-CDE         TO DK-POE-CDE                           
168035         INSPECT DK-POE-CDE TALLYING PV-ASTRK-CNT FOR ALL                 
168036                                           PC-ASTRK                       
168037         IF PV-ASTRK-CNT > 0                                              
168038             MOVE SPACES               TO ASC-POE-CTY-NM                  
168039                                          APOECTYO                        
168040             MOVE PC-A                 TO ASC-POE-STAT-CDE                
168041         ELSE                                                             
168044             PERFORM 3100-OPEN-POE-CURSOR1                                
168045             PERFORM 3120-FETCH-POE-CURSOR1                               
168046             PERFORM 3110-CLOSE-POE-CURSOR1                               
168047             IF ASC-INVALID-POE-CDE AND                                   
168048                ASC-POE-STAT-CDE = PC-A                                   
168049                 MOVE SPACES           TO ASC-POE-CTY-NM                  
168050                                          APOECTYO                        
168051                 SET PS-ERROR                                             
168052                     DP020-MSG-FATAL   TO TRUE                            
168053                 MOVE PC-TSYMSG-01452  TO DP020-MSG-NUMBER                
168054                 MOVE DP015-UNP-ALP-BRT-OFF                               
168055                                       TO APOECODA                        
168056                 MOVE DP015-RED        TO APOECODC                        
168057                 MOVE DP015-REVERSE    TO APOECODH                        
168058                 MOVE -1               TO APOECODL                        
168059                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
168060             ELSE                                                         
168069                 MOVE IMPOE-POE-CTY-NM TO ASC-POE-CTY-NM                  
168070                                          APOECTYO                        
168073                                                                          
168074                 IF (DP020-SRC-AID = DP016-PF15 OR                        
168075                    DP020-SRC-AID = DP016-PF16 OR                         
168076                    DP020-SRC-AID = DP016-PF17) AND                       
168077                    IMPOE-STAT-CDE = PC-I                                 
168078                     SET PS-ERROR                                         
168079                         DP020-MSG-FATAL   TO TRUE                        
168082                     MOVE PC-TSYMSG-01644  TO DP020-MSG-NUMBER            
168083                END-IF                                                    
168084             END-IF                                                       
168085         END-IF                                                           
168088     END-IF.                                                              
168089                                                                          
168091     IF  PS-NO-ERROR                                                      
168092         MOVE -1                     TO APOECODL                          
168093         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
168095     END-IF.                                                              
168096                                                                          
168155*----------------------------------------------------------------*        
168156*   OPEN THE POE CODE CURSOR1                                    *        
168157*----------------------------------------------------------------*        
168158 3100-OPEN-POE-CURSOR1.                                                   
168159                                                                          
168160     PERFORM                                                              
168161         WITH TEST AFTER                                                  
168162         VARYING PV-RETRY-COUNTER                                         
168163         FROM 1 BY 1                                                      
168164         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
168165                   OR (SQLCODE = ZERO))                                   
168166                                                                          
168167         MOVE 'CURSOR:  POE-CSR1'    TO DP013-MESSAGE-TEXT (2)            
168168                                                                          
168169         EXEC SQL                                                         
168170              OPEN POE-CSR1                                               
168171         END-EXEC                                                         
168172                                                                          
168173         EVALUATE TRUE                                                    
168174             WHEN SQLCODE = ZERO                                          
168175             WHEN SQLCODE = -904                                          
168176             WHEN SQLCODE = -913                                          
168177                 CONTINUE                                                 
168178             WHEN SQLWARN0 NOT = SPACES                                   
168179             WHEN SQLCODE < ZERO                                          
168180             WHEN SQLCODE > ZERO                                          
168181                 MOVE '3100-OPEN-POE-CURSOR1'                             
168182                                   TO DP013-PARAGRAPH                     
168183                 MOVE 'OPEN POE CURSOR1 - '                               
168184                                   TO DP013-MESSAGE-TEXT (1)              
168185                 MOVE SQLCA        TO DP013-SQLCA                         
168186                 MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)            
168187                 SET DP013-DB2-ABEND                                      
168188                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
168189                 PERFORM DP013-0000-PROCESS-ABEND                         
168190         END-EVALUATE                                                     
168191     END-PERFORM.                                                         
168192*                                                                         
168193     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
168194         MOVE '3100-OPEN-POE-CURSOR1' TO DP013-PARAGRAPH                  
168195         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
168196                                   TO DP013-MESSAGE-TEXT (1)              
168197         MOVE SQLCA                TO DP013-SQLCA                         
168198         MOVE 'TIMPOE  '           TO DP013-DB2-TABLE-NAME (1)            
168199         SET DP013-DB2-ABEND                                              
168200             DP013-XCTL-DISPLAY-RESTART                                   
168201                                   TO TRUE                                
168202         PERFORM DP013-0000-PROCESS-ABEND                                 
168203     END-IF.                                                              
168204                                                                          
168205 EJECT                                                                    
168206*----------------------------------------------------------------*        
168207*   CLOSE THE POE CODE CURSOR1                                   *        
168208*----------------------------------------------------------------*        
168209 3110-CLOSE-POE-CURSOR1.                                                  
168210                                                                          
168211     EXEC SQL                                                             
168212          CLOSE POE-CSR1                                                  
168213     END-EXEC.                                                            
168214                                                                          
168215     EVALUATE TRUE                                                        
168216         WHEN SQLCODE = ZERO                                              
168217             CONTINUE                                                     
168218         WHEN SQLWARN0 NOT = SPACES                                       
168219         WHEN SQLCODE < ZERO                                              
168220         WHEN SQLCODE > ZERO                                              
168221             MOVE '3110-CLOSE-POE-CURSOR1'                                
168222                               TO DP013-PARAGRAPH                         
168223             MOVE 'CLOSE POE CURSOR1'                                     
168224                               TO DP013-MESSAGE-TEXT (1)                  
168225             MOVE SQLCA        TO DP013-SQLCA                             
168226             MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)                
168227             SET DP013-DB2-ABEND                                          
168228                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
168229             PERFORM DP013-0000-PROCESS-ABEND                             
168230     END-EVALUATE.                                                        
168231                                                                          
168232 EJECT                                                                    
168233*----------------------------------------------------------------*        
168234*    FETCH THE POE CODE CURSOR1                                  *        
168235*----------------------------------------------------------------*        
168236 3120-FETCH-POE-CURSOR1.                                                  
168237                                                                          
168238     EXEC SQL                                                             
168239         FETCH POE-CSR1                                                   
168240          INTO :IMPOE-POE-NM                                              
168241             , :IMPOE-POE-CTY-NM                                          
168242             , :IMPOE-STAT-CDE                                            
168243     END-EXEC.                                                            
168244                                                                          
168245     EVALUATE TRUE                                                        
168246         WHEN SQLCODE = ZERO                                              
168247         WHEN SQLCODE = -811                                              
168248             SET ASC-VALID-POE-CDE TO TRUE                                
168250         WHEN SQLCODE = +100                                              
168251             SET ASC-INVALID-POE-CDE TO TRUE                              
168252             MOVE SPACES TO IMPOE-POE-NM                                  
168253             MOVE SPACES TO IMPOE-POE-CTY-NM                              
168257         WHEN SQLWARN0 NOT = SPACE                                        
168258         WHEN SQLCODE NOT = ZERO                                          
168259              MOVE '3120-FETCH-POE-CURSOR1'                               
168260                               TO DP013-PARAGRAPH                         
168261              MOVE 'FETCH POE CURSOR1'                                    
168262                               TO DP013-MESSAGE-TEXT (1)                  
168263              MOVE SQLCA       TO DP013-SQLCA                             
168264              SET DP013-DB2-ABEND                                         
168265                               TO TRUE                                    
168266              PERFORM DP013-0000-PROCESS-ABEND                            
168267     END-EVALUATE.                                                        
168268                                                                          
168269 EJECT                                                                    
168270******************************************************************        
168271** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
168272** MAP.                                                         **        
168273******************************************************************        
168274 3200-RESET-ATTRIBUTES.                                                   
168275                                                                          
168276     MOVE DP015-UNP-ALP-NOR-OFF    TO APOECODA.                           
168277     MOVE DP015-GREEN              TO APOECODC.                           
168278     MOVE DP015-UNDERLINE          TO APOECODH.                           
168279                                                                          
168280     MOVE DP015-PRO-NOR-OFF        TO APOECTYA.                           
168281     MOVE DP015-BLUE               TO APOECTYC.                           
168282     MOVE DP015-HL-OFF             TO APOECTYH.                           
168283                                                                          
168284*----------------------------------------------------------------*        
168285*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
168286*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
168287*  IT HERE.                                                      *        
168288*----------------------------------------------------------------*        
168289 4000-BUILD-INITIAL-PANEL.                                                
168290     SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
168300     MOVE -1                       TO APOECODL.                           
172500                                                                          
172510*----------------------------------------------------------------*        
172520*   OPEN THE POE CODE CURSOR2                                    *        
172530*----------------------------------------------------------------*        
172540 4100-OPEN-POE-CURSOR2.                                                   
172550                                                                          
172560     PERFORM                                                              
172570         WITH TEST AFTER                                                  
172580         VARYING PV-RETRY-COUNTER                                         
172590         FROM 1 BY 1                                                      
172591         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
172592                   OR (SQLCODE = ZERO))                                   
172593                                                                          
172594         MOVE 'CURSOR:  POE-CSR2'    TO DP013-MESSAGE-TEXT (2)            
172595                                                                          
172596         EXEC SQL                                                         
172597              OPEN POE-CSR2                                               
172598         END-EXEC                                                         
172599                                                                          
172600         EVALUATE TRUE                                                    
172601             WHEN SQLCODE = ZERO                                          
172602             WHEN SQLCODE = -904                                          
172603             WHEN SQLCODE = -913                                          
172604                 CONTINUE                                                 
172605             WHEN SQLWARN0 NOT = SPACES                                   
172606             WHEN SQLCODE < ZERO                                          
172607             WHEN SQLCODE > ZERO                                          
172608                 MOVE '4100-OPEN-POE-CURSOR2'                             
172609                                   TO DP013-PARAGRAPH                     
172610                 MOVE 'OPEN POE CURSOR2 - '                               
172611                                   TO DP013-MESSAGE-TEXT (1)              
172612                 MOVE SQLCA        TO DP013-SQLCA                         
172613                 MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)            
172614                 SET DP013-DB2-ABEND                                      
172615                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
172616                 PERFORM DP013-0000-PROCESS-ABEND                         
172617         END-EVALUATE                                                     
172618     END-PERFORM.                                                         
172619*                                                                         
172620     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
172621         MOVE '4100-OPEN-POE-CURSOR2' TO DP013-PARAGRAPH                  
172622         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
172623                                   TO DP013-MESSAGE-TEXT (1)              
172624         MOVE SQLCA                TO DP013-SQLCA                         
172625         MOVE 'TIMPOE  '           TO DP013-DB2-TABLE-NAME (1)            
172626         SET DP013-DB2-ABEND                                              
172627             DP013-XCTL-DISPLAY-RESTART                                   
172628                                   TO TRUE                                
172629         PERFORM DP013-0000-PROCESS-ABEND                                 
172630     END-IF.                                                              
172631                                                                          
172632 EJECT                                                                    
172633*----------------------------------------------------------------*        
172634*   CLOSE THE POE CODE CURSOR2                                   *        
172635*----------------------------------------------------------------*        
172636 4110-CLOSE-POE-CURSOR2.                                                  
172637                                                                          
172638     EXEC SQL                                                             
172639          CLOSE POE-CSR2                                                  
172640     END-EXEC.                                                            
172641                                                                          
172642     EVALUATE TRUE                                                        
172643         WHEN SQLCODE = ZERO                                              
172644             CONTINUE                                                     
172645         WHEN SQLWARN0 NOT = SPACES                                       
172646         WHEN SQLCODE < ZERO                                              
172647         WHEN SQLCODE > ZERO                                              
172648             MOVE '4110-CLOSE-POE-CURSOR2'                                
172649                               TO DP013-PARAGRAPH                         
172650             MOVE 'CLOSE POE CURSOR2'                                     
172651                               TO DP013-MESSAGE-TEXT (1)                  
172652             MOVE SQLCA        TO DP013-SQLCA                             
172653             MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)                
172654             SET DP013-DB2-ABEND                                          
172655                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
172656             PERFORM DP013-0000-PROCESS-ABEND                             
172657     END-EVALUATE.                                                        
172658                                                                          
172659 EJECT                                                                    
172660*----------------------------------------------------------------*        
172661*    FETCH THE POE CODE CURSOR2                                  *        
172662*----------------------------------------------------------------*        
172663 4120-FETCH-POE-CURSOR2.                                                  
172664                                                                          
172665     EXEC SQL                                                             
172666         FETCH POE-CSR2                                                   
172667          INTO  :IMPOE-POE-CDE                                            
172668              , :IMPOE-DCONSLR-ENT-ID                                     
172669              , :IMPOE-POE-STD-EDI-CDE                                    
172670              , :IMPOE-POE-NM                                             
172671              , :IMPOE-POE-CTY-NM                                         
172672              , :IMPOE-POE-ST-CDE                                         
172675     END-EXEC.                                                            
172676                                                                          
172677     EVALUATE TRUE                                                        
172678         WHEN SQLCODE = ZERO                                              
172679         WHEN SQLCODE = -811                                              
172680             CONTINUE                                                     
172681         WHEN SQLCODE = +100                                              
172682             MOVE ZEROES TO IMPOE-DCONSLR-ENT-ID                          
172683             MOVE SPACES TO IMPOE-POE-STD-EDI-CDE                         
172684             MOVE SPACES TO IMPOE-POE-NM                                  
172685             MOVE SPACES TO IMPOE-POE-CTY-NM                              
172686             MOVE SPACES TO IMPOE-POE-ST-CDE                              
172697         WHEN SQLWARN0 NOT = SPACE                                        
172698         WHEN SQLCODE NOT = ZERO                                          
172699              MOVE '4120-FETCH-POE-CURSOR2'                               
172700                               TO DP013-PARAGRAPH                         
172701              MOVE 'FETCH POE CURSOR1'                                    
172702                               TO DP013-MESSAGE-TEXT (1)                  
172703              MOVE SQLCA       TO DP013-SQLCA                             
172704              SET DP013-DB2-ABEND                                         
172705                               TO TRUE                                    
172706              PERFORM DP013-0000-PROCESS-ABEND                            
172707     END-EVALUATE.                                                        
172708                                                                          
172709 EJECT                                                                    
174901                                                                          
174902 4331-SELECT-ENTNME.                                                      
174903                                                                          
174904     EXEC SQL                                                             
174905         SELECT A.ENT_NAME_DESC                                           
174906            INTO :ENTNME-ENT-NAME-DESC                                    
174907             FROM TENTNME A                                               
174908             WHERE A.ENT_ID    = :DK-ENT-ID                               
174909             AND SEQ_NBR =                                                
174910                 (SELECT MAX(SEQ_NBR)                                     
174911                    FROM TENTNME                                          
174912                   WHERE ENT_ID   = :DK-ENT-ID                            
174913                     AND TYPE_CDE = :PC-PRIMARY)                          
174914             AND TYPE_CDE = :PC-PRIMARY                                   
174915     END-EXEC                                                             
174916                                                                          
174917         EVALUATE TRUE                                                    
174918             WHEN SQLCODE = ZERO                                          
174919                 CONTINUE                                                 
174920             WHEN SQLCODE = +100                                          
174921                 MOVE SPACES TO ENTNME-ENT-NAME-DESC                      
174923             WHEN OTHER                                                   
174924                 MOVE '4331-SELECT-ENTNME'                                
174925                                   TO DP013-PARAGRAPH                     
174926                 MOVE 'SQL ERROR SELECTING ENTNME NAME'                   
174927                                   TO DP013-MESSAGE-TEXT (1)              
174928                 MOVE SQLCA        TO DP013-SQLCA                         
174929                 MOVE 'TENTNME '   TO DP013-DB2-TABLE-NAME (1)            
174930                 SET DP013-DB2-ABEND                                      
174931                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
174932                 PERFORM DP013-0000-PROCESS-ABEND                         
174933         END-EVALUATE.                                                    
174934                                                                          
174935                                                                          
174936 4332-SELECT-SCAC.                                                        
174937                                                                          
174938     EXEC SQL                                                             
174939         SELECT A.ENT_ID,                                                 
174940                B.SCAC_CDE                                                
174941            INTO :ENTNME-ENT-ID,                                          
174942                 :EXTENT-SCAC-CDE                                         
174943             FROM TENTNME A,                                              
174944                  TEXTENT B                                               
174945             WHERE A.ENT_ID = B.ENT_ID                                    
174946               AND A.ENT_ID = :DK-ENT-ID                                  
174947               AND A.TYPE_CDE = :PC-PRIMARY                               
174948     END-EXEC                                                             
174949                                                                          
174950         EVALUATE TRUE                                                    
174951             WHEN SQLCODE = ZERO                                          
174952                 CONTINUE                                                 
174953             WHEN SQLCODE = +100                                          
174954                 MOVE SPACES TO EXTENT-SCAC-CDE                           
174955                 CONTINUE                                                 
174956             WHEN OTHER                                                   
174957                 MOVE '4332-SELECT-SCAC  '                                
174958                                   TO DP013-PARAGRAPH                     
174959                 MOVE 'SQL ERROR SELECTING ENTNME NAME'                   
174960                                   TO DP013-MESSAGE-TEXT (1)              
174961                 MOVE SQLCA        TO DP013-SQLCA                         
174962                 MOVE 'TENTNME '   TO DP013-DB2-TABLE-NAME (1)            
174963                 SET DP013-DB2-ABEND                                      
174964                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
174965                 PERFORM DP013-0000-PROCESS-ABEND                         
174966         END-EVALUATE.                                                    
174967                                                                          
174968******************************************************************        
174969*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
174970******************************************************************        
174971 4400-MOVE-COMMAREA-TO-SCREEN.                                            
174972                                                                          
174973     MOVE ASC-POE-CDE              TO APOECODO.                           
174974     MOVE ASC-POE-CTY-NM           TO APOECTYO.                           
174975                                                                          
174976******************************************************************        
174977*    ABEND PROCESSOR MODULE                                      *        
174978******************************************************************        
174980     COPY DPPD013.                                                        
