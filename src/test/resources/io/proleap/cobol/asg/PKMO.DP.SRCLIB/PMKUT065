      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT065.                                        
       AUTHOR.                 DARYL STEWART.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           JULY 2010.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT065 - REBATE FORM BUSINESS EVENT INTEGRITY CHECK.      *        
      *                                                                *        
      *    THIS PROGRAM CREATES THE REBATE FORM EXTRACT FILE USED      *        
      *    IN THE DATA INTEGRITY PROCESS.  FIRST OPEN THE THREE        *        
      *    INPUT FILES AND ONE OUTPUT FILE.  ONCE OPEN READ THE        *        
      *    TWO INPUT FILES.  IF NOTHING IS IN EITHER OF THESE FILES    *        
      *    ABEND THE PROGRAM. NEXT LOG ON TO ORACLE.  IF SUCCESSFUL    *        
      *    THEN FIND THE CURRENT SYS DATE USING A SELECT STATMENT.     *        
      *    ONCE THE SYSDATE HAS BEEN FOUND THEN SELECT THE REBATE FORM *        
      *    RECORDS FROM THE REBATE TABLES THAT FALLS WITHIN THE        *        
      *    DATE RANGE AND HAVE NOT BEEN UPDATED SINCE THE TIMESTAMP    *        
      *    FILE TIME.  MOVE THE SELECTED DATA TO A COPYBOOK. THEN      *        
      *    WRITE THE RECORD TO THE OUTPUT FILE. AFTER THE LAST RECORD  *        
      *    HAS BEEN WRITTEN TO THE FILE WRITE A TOTAL LINE TO THE      *        
      *    FILE.  FINALLY CLOSE THE THREE INPUT FILES AND THE ONE      *        
      *    OUTPUT FILE. THE NEW EVENT NUMBER IS 130.                   *        
      *                                                                *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *    07/14/2010 - DARYL STEWART - REBATE PROJECT 2419A           *        
      *    - INITIAL DEPLOYMENT                                        *        
      *    09/15/2010 - CHERI PARMLEY - PKE#000000005095               *        
      *    - EXCLUDE 'D' STAT CODE ROWS IN CURSOR                      *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941*    SELECT IN-ORACLE-SRC-CTL                                             
070941*        ASSIGN                       TO INP03.                           
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT REBATE-FILE                                                   
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941*FD  IN-ORACLE-SRC-CTL                                                    
070941 FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941*01  IN-ORA-SRC-CTL-RECORD           PIC  X(80).                          
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  REBATE-FILE                                                          
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  REBT-LINE                        PIC X(160).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)    VALUE 'N'.             
070941         88  PS-END-OF-LOGON-FILE                  VALUE 'Y'.             
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)    VALUE 'N'.             
070941         88  PS-ORA-SRC-EMPTY                      VALUE 'Y'.             
           05  PS-DATE-RANGE-FILE-SW        PIC X(01)    VALUE 'N'.             
               88  PS-DATE-RANGE-FILE-END                VALUE 'Y'.             
               88  PS-DATE-RANGE-FILE-START              VALUE 'N'.             
           05  PS-TMST-FILE-SW              PIC X(01)    VALUE 'N'.             
               88  PS-TMST-FILE-END                      VALUE 'Y'.             
               88  PS-TMST-FILE-START                    VALUE 'N'.             
           05  PS-END-OF-REBT-CSR-SWITCH    PIC X(1)     VALUE 'N'.             
               88  PS-END-OF-REBT-CSR                    VALUE 'Y'.             
               88  PS-NOT-END-OF-REBT-CSR                VALUE 'N'.             
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                          VALUE '/'.             
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT065'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 130.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PC-AUTO-LOGON               PIC  X(01)  VALUE '/'.               
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SYS-DATE                  PIC  X(26)  VALUE SPACES.           
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-NEW-STRT-DATE             PIC  X(26)  VALUE SPACES.           
           05  PV-NEW-END-DATE              PIC  X(26)  VALUE SPACES.           
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-REBT-PROMO-ID             PIC S9(10)  VALUE ZEROS.            
           05  PV-REBT-FM-ID                PIC S9(10)  VALUE ZEROS.            
           05  PV-MDSE-SELN-SEQ-NBR         PIC S9(10)  VALUE ZEROS.            
           05  PV-CUST-INCTV-PROMO-INTFC-ID PIC S9(10)  VALUE ZEROS.            
           05  PV-FM-SECT-TYP-CDE           PIC  X(03)  VALUE SPACES.           
           05  PV-FM-LNE-TYP-CDE            PIC  X(03)  VALUE SPACES.           
           05  PV-FM-LNE-NBR                PIC S9(03)  VALUE ZEROS.            
           05  PV-EFF-END-TMST              PIC  X(19)  VALUE SPACES.           
           05  PV-FM-TXT                    PIC  X(42)  VALUE SPACES.           
           05  PV-STAT-CDE                  PIC  X(01)  VALUE SPACES.           
           05  PV-LAST-UPD-TMST             PIC  X(19)  VALUE SPACES.           
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-REBT-CNT                  PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE ADHOC STORE GROUP BUSINESS EVENT.                   
      ******************************************************************        
           COPY PMRD065.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
070941*    COPY PMWS800.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE FILE                                   *         
      *  - READ THE TIMESTAMP FILE                                    *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RETRIEVE REBATE INFO FROM PMT_REBT_FM                      *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM800-A000-ORACLE-SETUP.                                     
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C200-DECLARE-REBT-CURSOR.                                    
           PERFORM C300-OPEN-REBT-CURSOR.                                       
           PERFORM C400-FETCH-REBT-CURSOR.                                      
           PERFORM C500-PROCESS-REBT-CURSOR                                     
               UNTIL PS-END-OF-REBT-CSR                                         
           PERFORM C600-CLOSE-REBT-CURSOR.                                      
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
070941*         INPUT  IN-ORACLE-SRC-CTL                                        
                OUTPUT REBATE-FILE.                                             
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-TMST-FILE.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
070941*           IN-ORACLE-SRC-CTL                                             
                  REBATE-FILE.                                                  
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-REBT-CURSOR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES REBT INFORMATION FROM           *         
      *    PMT_CUST_INCTV                                             *         
      *    PMT_CUST_INCTV_MDSE_LNE                                    *         
      *    PMT_CUST_INCTV_PRICG                                       *         
      *    PMT_REBT_FM                                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-REBT-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
                DECLARE REBTCSR                                                 
                CURSOR FOR                                                      
                    SELECT                                                      
                         B.REBT_PROMO_ID                                        
                        ,B.REBT_FM_ID                                           
                        ,C.MDSE_SELN_SEQ_NBR                                    
                        ,D.CUST_INCTV_PROMO_INTFC_ID                            
                        ,B.FM_SECT_TYP_CDE                                      
                        ,B.FM_LNE_TYP_CDE                                       
                        ,B.FM_LNE_NBR                                           
                        ,TO_CHAR(B.EFF_END_DTE,                                 
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,B.FM_TXT                                               
                        ,B.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                           'YYYY-MM-DD-HH24.MI.SS')             
                    FROM PMT_CUST_INCTV A,                                      
                         PMT_REBT_FM B,                                         
                         PMT_CUST_INCTV_MDSE_LNE C,                             
                         PMT_CUST_INCTV_PRICG D                                 
                    WHERE A.CUST_INCTV_ID = B.REBT_PROMO_ID                     
                      AND A.CUST_INCTV_ID = C.CUST_INCTV_ID                     
                      AND C.CUST_INCTV_ID = D.CUST_INCTV_ID                     
                      AND C.MDSE_SELN_SEQ_NBR = D.MDSE_SELN_SEQ_NBR             
                      AND (A.END_TMST >= TO_DATE(                               
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                          OR A.END_TMST IS NULL)                                
                      AND A.STRT_TMST <= TO_DATE(                               
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
091510                AND B.STAT_CDE <> 'D'                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      * C300-OPEN-REBT-CURSOR.                                        *         
      * ==> PROCESSES:                                                *         
      *  - OPEN REBTCSR                                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-REBT-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
                 OPEN REBTCSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-REBT-CURSOR      '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON REBT-CURSOR '                            
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * C400-FETCH-REBT-CURSOR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-REBT-CURSOR   *         
      *****************************************************************         
       C400-FETCH-REBT-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH REBTCSR                                                    
                INTO  :PV-REBT-PROMO-ID                                         
                    , :PV-REBT-FM-ID                                            
                    , :PV-MDSE-SELN-SEQ-NBR                                     
                    , :PV-CUST-INCTV-PROMO-INTFC-ID                             
                    , :PV-FM-SECT-TYP-CDE                                       
                    , :PV-FM-LNE-TYP-CDE                                        
                    , :PV-FM-LNE-NBR                                            
                    , :PV-EFF-END-TMST                                          
                    , :PV-FM-TXT                                                
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-REBT-CNT                                         
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'REBT CNT FORM FETCH = ' PA-REBT-CNT                 
                   DISPLAY 'END OF REBT CSR'                                    
                   SET PS-END-OF-REBT-CSR TO TRUE                               
               WHEN OTHER                                                       
                   DISPLAY 'REBT PROMO ID = ' PV-REBT-PROMO-ID                  
                   MOVE 'C400-FETCH-REBT-CURSOR        '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON REBT-CURSOR   '                         
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-REBT-CURSOR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-REBT-CURSOR.                                                
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-REBT-CURSOR.                                      
                                                                                
      *****************************************************************         
      * C600-CLOSE-REBT-CURSOR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE REBTCSR                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-REBT-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
                CLOSE REBTCSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-REBT-CURSOR      '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON REBT-CSR     '                           
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK.                                           *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM:                                           *         
      *   C400-FETCH-REBT-CURSOR                                      *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM065-REBT-FORM-RECORD.                                   
           MOVE PC-BUSINESS-EVNT-ID  TO PM065-BUSINESS-EVNT-ID.                 
           MOVE PC-ACTION-CDE        TO PM065-ACTION-CDE.                       
           MOVE PV-REBT-PROMO-ID     TO PM065-REBT-PROMO-ID.                    
           MOVE PV-REBT-FM-ID        TO PM065-REBT-FM-ID.                       
           MOVE PV-MDSE-SELN-SEQ-NBR TO PM065-MDSE-SELN-SEQ-NBR.                
           MOVE PV-CUST-INCTV-PROMO-INTFC-ID                                    
                                     TO PM065-INCTV-PROMO-INTFC-ID.             
           MOVE PV-FM-SECT-TYP-CDE   TO PM065-FM-SECT-TYP-CDE.                  
           MOVE PV-FM-LNE-TYP-CDE    TO PM065-FM-LNE-TYP-CDE.                   
           MOVE PV-FM-LNE-NBR        TO PM065-FM-LNE-NBR.                       
           STRING PV-EFF-END-TMST DELIMITED BY SPACE                            
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM065-EFF-END-TMST.                                      
           MOVE PV-FM-TXT            TO PM065-FM-TXT.                           
           MOVE PV-STAT-CDE          TO PM065-STAT-CDE.                         
           STRING PV-LAST-UPD-TMST DELIMITED BY SPACE                           
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM065-LAST-UPD-TMST.                                     
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE ADHOC STORE GP OUTPUT FILE.   *         
      * ==> PERFORMED FROM: C500-PROCESS-REBT-CURSOR                  *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE REBT-LINE FROM PM065-REBT-FORM-RECORD.                         
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE ADHOC OUTPUT FILE.             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE REBT-LINE FROM PM700-TOTAL-RECORD.                             
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD800.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
