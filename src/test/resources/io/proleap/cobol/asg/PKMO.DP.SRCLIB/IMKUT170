       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.    IMKUT170                                                  
       AUTHOR.        COGNIZANT.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  12-04-2012.                                               
       DATE-COMPILED.                                                           
      *                                                                         
      *                                                                         
      *****************************************************************         
      *     IMKUT170 -  FETCH ADDITIONAL FIELDS FOR THE EXTRACT OF              
      *                 VS & SKU DESCRIPTION HAVING SPECIAL CHARACTERS:         
      *                    COPYRIGHT, REGISTERED, TRADEMARK SYMBOLS             
      *                                                                         
      *                 DATA IS EXTRACTED AND FTPED TO SHARED DRIVE.            
      *                 AN EMAIL WILL BE SENT TO MS IF THERE IS DATA.           
      *                                                                         
      *                 INPUT FILE TO THIS PROGRAM IS A TRIMMED FILE            
      *                 OF IMK066(VS) & IMK067(SKU) OUTPUT WHICH ARE OF         
      *                 LAYOUTS IMRD058 & IMRD053 RESPECTIVELY.                 
      *                 ANY CHANGE IN THESE LAYOUTS WILL HAVE IMPACT TO         
      *                 THE SORT CONTROL CARDS: IMSR170 & IMSR171               
      *****************************************************************         
      *                                                                         
      *****************************************************************         
      * CHANGE LOG:                                                             
      *     12/04/12 - PBI 20047 INTIAL RELEASE                                 
      *              - COGNIZANT(KARTHIK KRISHNA K)                             
      **                                                                        
      *     MM/DD/YY - PBI XXXXX               <<< OVER WRITE HERE              
      *              - CHANGE MADE BY          <<< OVER WRITE HERE              
      *              - CHANGE DETAILS          <<< OVER WRITE HERE              
      **                                                                        
      *     MM/DD/YY - PBI XXXXX                                                
      *              - CHANGE MADE BY                                           
      *              - CHANGE DETAILS                                           
      *****************************************************************         
      *                                                                         
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT        VS-SPLCH-FILE-IN                                       
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT        SKU-SPLCH-FILE-IN                                      
               ASSIGN TO INP02.                                                 
      *                                                                         
           SELECT        VS-SPLCH-CSV-OUT                                       
               ASSIGN TO OUT01.                                                 
      *                                                                         
           SELECT        SKU-SPLCH-CSV-OUT                                      
               ASSIGN TO OUT02.                                                 
      *                                                                         
      *                                                                         
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
      *                                                                         
       FD  VS-SPLCH-FILE-IN                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-SPLCH-FILE-IN-REC        PIC X(080).                              
      *                                                                         
       FD  SKU-SPLCH-FILE-IN                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-SPLCH-FILE-IN-REC       PIC X(038).                              
      *                                                                         
       FD  VS-SPLCH-CSV-OUT                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-SPLCH-CSV-OUT-REC        PIC X(114).                              
      *                                                                         
       FD  SKU-SPLCH-CSV-OUT                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-SPLCH-CSV-OUT-REC       PIC X(113).                              
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME             PIC  X(06)  VALUE 'IMK000'.              
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUT170'.            
           05  PC-MAX-RETRIES          PIC S9(03)  VALUE +3  COMP-3.            
           05  PC-MORE-THAN-MAX-RETRY  PIC S9(03)  VALUE +4  COMP-3.            
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-VS-SW                    PIC  X     VALUE 'N'.            
               88  PS-MORE-VS-RECORDS                     VALUE 'N'.            
               88  PS-NO-MORE-VS-RECORDS                  VALUE 'Y'.            
           05  PS-EOF-SKU-SW                   PIC  X     VALUE 'N'.            
               88  PS-MORE-SKU-RECORDS                    VALUE 'N'.            
               88  PS-NO-MORE-SKU-RECORDS                 VALUE 'Y'.            
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-DB2-OPER-ATTEMPTED           PIC  X(50) VALUE SPACES.         
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.           
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.             
           05  PV-PREVIOUS-ENT-ID              PIC  9(09) VALUE ZERO.           
           05  PV-PREVIOUS-ENT-DESC            PIC  X(30) VALUE SPACE.          
      *                                                                         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORD-COUNTS.                                                
               10  PA-VS-IN-CNT                PIC  9(04) VALUE ZEROES.         
               10  PA-SKU-IN-CNT               PIC  9(04) VALUE ZEROES.         
               10  PA-VS-OUT-CNT               PIC  9(04) VALUE ZEROES.         
               10  PA-SKU-OUT-CNT              PIC  9(04) VALUE ZEROES.         
      *                                                                         
      *****************************************************************         
      *IN01 - VENDOR STYLE  INPUT RECORD LAYOUT                                 
      *****************************************************************         
       01  PV-VS-IN-RECORD.                                                     
           10  PV-VS-IN-VS-ID                  PIC  9(09) VALUE 0.              
           10  PV-VS-IN-VS-DESC                PIC  X(30) VALUE SPACE.          
           10  PV-VS-IN-DEPT-NBR               PIC  9(04) VALUE 0.              
           10  PV-VS-IN-MAJ-CL-NBR             PIC  9(04) VALUE 0.              
           10  PV-VS-IN-SUB-CL-NBR             PIC  9(04) VALUE 0.              
           10  PV-VS-IN-ENT-ID                 PIC  9(09) VALUE ZERO.           
           10  PV-VS-IN-VS-NBR                 PIC  X(20) VALUE SPACE.          
      *                                                                         
      *****************************************************************         
      *IN02 - SKU INPUT RECORD LAYOUT                                           
      *****************************************************************         
       01  PV-SKU-IN-RECORD.                                                    
           10  PV-SKU-IN-SKU                   PIC  9(08) VALUE 0.              
           10  PV-SKU-IN-INTR-UPC-DESC         PIC  X(30) VALUE SPACE.          
      *                                                                         
      *****************************************************************         
      *OUT01 - VENDOR STYLE DETAIL RECORD LAYOUT                                
      *****************************************************************         
       01  PV-VS-OUT-RECORD.                                                    
           10  PV-VS-OUT-DEPT-NBR              PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(01) VALUE ','.            
           10  PV-VS-OUT-MAJ-CL-NBR            PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(01) VALUE ','.            
           10  PV-VS-OUT-SUB-CL-NBR            PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(02) VALUE ',"'.           
           10  PV-VS-OUT-ENT-NAME-DESC         PIC  X(30) VALUE SPACE.          
           10  FILLER                          PIC  X(03) VALUE '","'.          
           10  PV-VS-OUT-VS-NBR                PIC  X(20) VALUE SPACE.          
           10  FILLER                          PIC  X(03) VALUE '","'.          
           10  PV-VS-OUT-VS-DESC               PIC  X(30) VALUE SPACE.          
           10  FILLER                          PIC  X(02) VALUE '",'.           
           10  PV-VS-OUT-VS-ID                 PIC  9(09) VALUE 0.              
           10  FILLER                          PIC  X(01) VALUE ','.            
      *                                                                         
      *****************************************************************         
      *OUT02 - SKU DETAIL RECORD LAYOUT                                         
      *****************************************************************         
       01  PV-SKU-OUT-RECORD.                                                   
           10  PV-SKU-OUT-DEPT-NBR             PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(01) VALUE ','.            
           10  PV-SKU-OUT-MAJ-CL-NBR           PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(01) VALUE ','.            
           10  PV-SKU-OUT-SUB-CL-NBR           PIC  9(04) VALUE 0.              
           10  FILLER                          PIC  X(02) VALUE ',"'.           
           10  PV-SKU-OUT-ENT-NAME-DESC        PIC  X(30) VALUE SPACE.          
           10  FILLER                          PIC  X(03) VALUE '","'.          
           10  PV-SKU-OUT-VS-NBR               PIC  X(20) VALUE SPACE.          
           10  FILLER                          PIC  X(02) VALUE '",'.           
           10  PV-SKU-OUT-SKU                  PIC  9(08) VALUE 0.              
           10  FILLER                          PIC  X(02) VALUE ',"'.           
           10  PV-SKU-OUT-INTR-UPC-DESC        PIC  X(30) VALUE SPACE.          
           10  FILLER                          PIC  X(02) VALUE '",'.           
      *                                                                         
      *****************************************************************         
      *OUT01 - VENDOR STYLE HEADER RECORD LAYOUT                                
      *****************************************************************         
       01  PV-VS-OUT-HEADER.                                                    
           10  FILLER                          PIC  X(44) VALUE                 
               'DEPARTMENT,MAJOR CLASS,SUB CLASS,VENDOR NAME'.                  
           10  FILLER                          PIC  X(32) VALUE                 
               ',VS NUMBER,VS DESCRIPTION,VS ID,'.                              
           10  FILLER                          PIC  X(38) VALUE SPACES.         
      *                                                                         
      *****************************************************************         
      *OUT02 - SKU HEADER RECORD LAYOUT                                         
      *****************************************************************         
       01  PV-SKU-OUT-HEADER.                                                   
           10  FILLER                          PIC  X(44) VALUE                 
               'DEPARTMENT,MAJOR CLASS,SUB CLASS,VENDOR NAME'.                  
           10  FILLER                          PIC  X(32) VALUE                 
               ',VS NUMBER,SKU#,SKU DESCRIPTION,'.                              
           10  FILLER                          PIC  X(37) VALUE SPACES.         
      *                                                                         
      *****************************************************************         
      * DCLGEN FOR TENTNME - ENTITY NAME TABLE                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DCLGEN FOR TSKXREF - SKU CROSS REFERENCE TABLE                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DCLGEN FOR TUPC - UPC TABLE                                             
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DCLGEN FOR TVNDSTL - VENDOR STYLE TABLE                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      *****************************************************************         
           COPY DPWS004.                                                        
      *                                                                         
      *****************************************************************         
      *  USED FOR DB2 ERRORS                                                    
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      *                                                                         
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
      *                                                                         
       0000-MAIN-MODULE.                                                        
      *                                                                         
           PERFORM 1000-INIT-PARA.                                              
      *                                                                         
           PERFORM 3000-PROCESS-PARA.                                           
      *                                                                         
           PERFORM 9000-END-PARA.                                               
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
      *                                                                         
      *****************************************************************         
      * INITIALIZATION - OPEN INPUT/OUTPUT FILES                                
      *****************************************************************         
      *                                                                         
       1000-INIT-PARA.                                                          
      *                                                                         
           DISPLAY ' '                                                          
           DISPLAY ' ****  PROGRAM IMKUT170 BEGINS   ****'                      
           DISPLAY ' '                                                          
      *                                                                         
           OPEN INPUT  VS-SPLCH-FILE-IN                                         
                       SKU-SPLCH-FILE-IN.                                       
           OPEN OUTPUT VS-SPLCH-CSV-OUT                                         
                       SKU-SPLCH-CSV-OUT.                                       
      *                                                                         
           WRITE VS-SPLCH-CSV-OUT-REC   FROM PV-VS-OUT-HEADER.                  
      *                                                                         
           WRITE SKU-SPLCH-CSV-OUT-REC  FROM PV-SKU-OUT-HEADER.                 
      *                                                                         
      *                                                                         
      *****************************************************************         
      * PROCESS THE INPUT FILES                                                 
      *****************************************************************         
       3000-PROCESS-PARA.                                                       
      *                                                                         
           PERFORM  3100-PROCESS-VS-FILE                                        
             UNTIL  PS-NO-MORE-VS-RECORDS.                                      
      *                                                                         
           PERFORM  3500-PROCESS-SKU-FILE                                       
             UNTIL  PS-NO-MORE-SKU-RECORDS.                                     
      *                                                                         
      *                                                                         
      *****************************************************************         
      * PROCESS THE VENDOR STYLE FILE                                           
      *****************************************************************         
       3100-PROCESS-VS-FILE.                                                    
      *                                                                         
           PERFORM  8000-READ-VS-FILE-IN.                                       
      *                                                                         
           IF PS-MORE-VS-RECORDS                                                
               PERFORM 3200-INIT-VS-OUT-FIELDS                                  
               IF PV-VS-IN-ENT-ID = PV-PREVIOUS-ENT-ID                          
                  MOVE PV-PREVIOUS-ENT-DESC TO ENTNME-ENT-NAME-DESC             
               ELSE                                                             
                  PERFORM 5000-GET-VENDOR-NAME                                  
               END-IF                                                           
               PERFORM 3300-POPULATE-VS-OUT-FIELDS                              
               PERFORM 7000-WRITE-VS-FILE-OUT                                   
           ELSE                                                                 
               DISPLAY ' **** END OF VS FILE PROCESSING  ****'                  
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *****************************************************************         
      * INITIALIZE WORKING FIELDS AND MOVE ENT-ID TO HOST VARIABLE              
      *****************************************************************         
       3200-INIT-VS-OUT-FIELDS.                                                 
      *                                                                         
           MOVE ZEROES                    TO PV-VS-OUT-VS-ID                    
                                             PV-VS-OUT-DEPT-NBR                 
                                             PV-VS-OUT-MAJ-CL-NBR               
                                             PV-VS-OUT-SUB-CL-NBR.              
           MOVE SPACES                    TO PV-VS-OUT-VS-DESC                  
                                             PV-VS-OUT-ENT-NAME-DESC            
                                             PV-VS-OUT-VS-NBR.                  
      *                                                                         
           MOVE PV-VS-IN-ENT-ID           TO ENTNME-ENT-ID.                     
      *                                                                         
      *                                                                         
      *****************************************************************         
      * POPULATE THE OUTPUT VENDOR STYLE FILE FIELDS                            
      *****************************************************************         
       3300-POPULATE-VS-OUT-FIELDS.                                             
      *                                                                         
           MOVE PV-VS-IN-VS-ID            TO PV-VS-OUT-VS-ID.                   
           MOVE PV-VS-IN-VS-DESC          TO PV-VS-OUT-VS-DESC.                 
           MOVE PV-VS-IN-DEPT-NBR         TO PV-VS-OUT-DEPT-NBR.                
           MOVE PV-VS-IN-MAJ-CL-NBR       TO PV-VS-OUT-MAJ-CL-NBR.              
           MOVE PV-VS-IN-SUB-CL-NBR       TO PV-VS-OUT-SUB-CL-NBR.              
           MOVE ENTNME-ENT-NAME-DESC      TO PV-VS-OUT-ENT-NAME-DESC.           
           MOVE PV-VS-IN-VS-NBR           TO PV-VS-OUT-VS-NBR.                  
      *                                                                         
      *                                                                         
      *****************************************************************         
      * PROCESS THE SKU FILE                                                    
      *****************************************************************         
       3500-PROCESS-SKU-FILE.                                                   
      *                                                                         
           PERFORM 8100-READ-SKU-FILE-IN.                                       
      *                                                                         
           IF PS-MORE-SKU-RECORDS                                               
               PERFORM 3600-INIT-SKU-OUT-FIELDS                                 
               PERFORM 5100-GET-MORE-SKU-VS-INFO                                
               IF SQLCODE = 0                                                   
                  IF VNDSTL-ENT-ID = PV-PREVIOUS-ENT-ID                         
                     MOVE PV-PREVIOUS-ENT-DESC TO ENTNME-ENT-NAME-DESC          
                  ELSE                                                          
                     PERFORM 5000-GET-VENDOR-NAME                               
                  END-IF                                                        
               END-IF                                                           
               PERFORM 3700-POPULATE-SKU-OUT-FIELDS                             
               PERFORM 7100-WRITE-SKU-FILE-OUT                                  
           ELSE                                                                 
               DISPLAY ' **** END OF SKU FILE PROCESSING ****'                  
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *****************************************************************         
      * INITIALIZE WORKING FIELDS AND MOVE SKU# TO HOST VARIABLE                
      *****************************************************************         
       3600-INIT-SKU-OUT-FIELDS.                                                
      *                                                                         
           MOVE ZEROES                    TO PV-SKU-OUT-SKU                     
                                             PV-SKU-OUT-DEPT-NBR                
                                             PV-SKU-OUT-MAJ-CL-NBR              
                                             PV-SKU-OUT-SUB-CL-NBR.             
           MOVE SPACES                    TO PV-SKU-OUT-INTR-UPC-DESC           
                                             PV-SKU-OUT-ENT-NAME-DESC           
                                             PV-SKU-OUT-VS-NBR.                 
      *                                                                         
           MOVE PV-SKU-IN-SKU             TO SKXREF-SKU.                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * POPULATE THE OUTPUT SKU FILE FIELDS                                     
      *****************************************************************         
       3700-POPULATE-SKU-OUT-FIELDS.                                            
      *                                                                         
           MOVE PV-SKU-IN-SKU             TO PV-SKU-OUT-SKU.                    
           MOVE PV-SKU-IN-INTR-UPC-DESC   TO PV-SKU-OUT-INTR-UPC-DESC.          
           MOVE SKXREF-DEPT               TO PV-SKU-OUT-DEPT-NBR.               
           MOVE SKXREF-CLASS              TO PV-SKU-OUT-MAJ-CL-NBR.             
           MOVE SKXREF-SUBCLASS           TO PV-SKU-OUT-SUB-CL-NBR.             
           MOVE ENTNME-ENT-NAME-DESC      TO PV-SKU-OUT-ENT-NAME-DESC.          
           MOVE VNDSTL-VS-NBR             TO PV-SKU-OUT-VS-NBR.                 
      *                                                                         
      *                                                                         
      *****************************************************************         
      * GET VENDOR NAME DETAILS                                                 
      *****************************************************************         
       5000-GET-VENDOR-NAME.                                                    
      *                                                                         
           PERFORM VARYING PV-RETRY-COUNT FROM 1 BY 1                           
                     UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                      
      *                                                                         
             EXEC SQL                                                           
                SELECT  ENT_NAME_DESC                                           
                  INTO  :ENTNME-ENT-NAME-DESC                                   
                  FROM  TENTNME                                                 
                 WHERE  ENT_ID   = :ENTNME-ENT-ID                               
                   AND  TYPE_CDE = '01'                                         
                  WITH  UR                                                      
             END-EXEC                                                           
      *                                                                         
             EVALUATE SQLCODE                                                   
               WHEN 0                                                           
                   MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT                
                   MOVE ENTNME-ENT-NAME-DESC   TO PV-PREVIOUS-ENT-DESC          
                   MOVE ENTNME-ENT-ID          TO PV-PREVIOUS-ENT-ID            
               WHEN +100                                                        
                   MOVE ' '                    TO ENTNME-ENT-NAME-DESC          
                   MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT                
               WHEN -911                                                        
               WHEN -913                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '5000-GET-VENDOR-NAME'                                  
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED                  
                   PERFORM 9998-SQL-ABEND                                       
             END-EVALUATE                                                       
           END-PERFORM.                                                         
      *                                                                         
           IF (SQLWARN0 NOT = SPACES  OR                                        
              (SQLCODE > 0 AND SQLCODE NOT = +100) OR                           
               SQLCODE < 0)                                                     
               DISPLAY 'MAX RETIES EXCEEDED'                                    
               DISPLAY 'ABENDING THE PROGRAM'                                   
               MOVE '5000-GET-VENDOR-NAME'                                      
                 TO PV-PARAGRAPH-NAME                                           
               MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED                      
               PERFORM 9998-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *****************************************************************         
      * GET SKU CROSS REFERENCE & VENDOR STYLE TABLE DETAILS                    
      *****************************************************************         
       5100-GET-MORE-SKU-VS-INFO.                                               
      *                                                                         
           PERFORM VARYING PV-RETRY-COUNT FROM 1 BY 1                           
                     UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                      
      *                                                                         
             EXEC SQL                                                           
                SELECT  A.DEPT,                                                 
                        A.CLASS,                                                
                        A.SUBCLASS,                                             
                        C.ENT_ID,                                               
                        C.VS_NBR                                                
                  INTO  :SKXREF-DEPT,                                           
                        :SKXREF-CLASS,                                          
                        :SKXREF-SUBCLASS,                                       
                        :VNDSTL-ENT-ID,                                         
                        :VNDSTL-VS-NBR                                          
                  FROM  TSKXREF A,                                              
                        TUPC    B,                                              
                        TVNDSTL C                                               
                 WHERE  SKU            = :SKXREF-SKU                            
                   AND  B.INTR_UPC_ID  = A.INTR_UPC_ID                          
                   AND  B.UPC_TYP_CDE  = 'I'                                    
                   AND  C.VS_ID        = B.VS_ID                                
                  WITH  UR                                                      
             END-EXEC                                                           
      *                                                                         
             EVALUATE SQLCODE                                                   
               WHEN 0                                                           
                   MOVE VNDSTL-ENT-ID          TO ENTNME-ENT-ID                 
                   MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT                
               WHEN +100                                                        
                   MOVE 0   TO SKXREF-DEPT                                      
                               SKXREF-CLASS                                     
                               SKXREF-SUBCLASS                                  
                               ENTNME-ENT-ID                                    
                   MOVE ' ' TO VNDSTL-VS-NBR                                    
                               ENTNME-ENT-NAME-DESC                             
                   MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT                
               WHEN -911                                                        
               WHEN -913                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '5100-GET-MORE-SKU-VS-INFO'                             
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED                  
                   PERFORM 9998-SQL-ABEND                                       
             END-EVALUATE                                                       
           END-PERFORM.                                                         
      *                                                                         
           IF (SQLWARN0 NOT = SPACES  OR                                        
              (SQLCODE > 0 AND SQLCODE NOT = +100) OR                           
               SQLCODE < 0)                                                     
               DISPLAY 'MAX RETIES EXCEEDED'                                    
               DISPLAY 'ABENDING THE PROGRAM'                                   
               MOVE '5100-GET-MORE-SKU-VS-INFO'                                 
                 TO PV-PARAGRAPH-NAME                                           
               MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED                      
               PERFORM 9998-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  WRITE VENDOR STYLE SPECIAL CHARACTERS CSV FILE                         
      *****************************************************************         
       7000-WRITE-VS-FILE-OUT.                                                  
      *                                                                         
           WRITE VS-SPLCH-CSV-OUT-REC  FROM  PV-VS-OUT-RECORD.                  
           ADD   1                       TO  PA-VS-OUT-CNT.                     
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  WRITE SKU SPECIAL CHARACTERS CSV FILE                                  
      *****************************************************************         
       7100-WRITE-SKU-FILE-OUT.                                                 
      *                                                                         
           WRITE SKU-SPLCH-CSV-OUT-REC FROM  PV-SKU-OUT-RECORD.                 
           ADD   1                       TO  PA-SKU-OUT-CNT.                    
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  READ THE VENDOR STYLE INPUT FILE.                                      
      *****************************************************************         
       8000-READ-VS-FILE-IN.                                                    
      *                                                                         
           READ VS-SPLCH-FILE-IN                                                
               INTO PV-VS-IN-RECORD                                             
               AT END                                                           
                   SET PS-NO-MORE-VS-RECORDS TO TRUE                            
               NOT AT END                                                       
                   ADD 1  TO  PA-VS-IN-CNT                                      
           END-READ.                                                            
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  READ THE SKU INPUT FILE.                                               
      *****************************************************************         
       8100-READ-SKU-FILE-IN.                                                   
      *                                                                         
           READ SKU-SPLCH-FILE-IN                                               
               INTO PV-SKU-IN-RECORD                                            
               AT END                                                           
                   SET PS-NO-MORE-SKU-RECORDS TO TRUE                           
               NOT AT END                                                       
                   ADD 1  TO  PA-SKU-IN-CNT                                     
           END-READ.                                                            
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  CLOSE FILES AND DISPLAY PROGRAM RUN STATISTICS IN SYSOUT               
      *****************************************************************         
       9000-END-PARA.                                                           
      *                                                                         
           CLOSE  VS-SPLCH-FILE-IN                                              
                  SKU-SPLCH-FILE-IN                                             
                  VS-SPLCH-CSV-OUT                                              
                  SKU-SPLCH-CSV-OUT.                                            
      *                                                                         
           DISPLAY ' ************************************************'.         
           DISPLAY ' END OF PROGRAM : IMKUT170 '.                               
           DISPLAY ' TOTAL INPUT VS  RECORD READ: '  PA-VS-IN-CNT.              
           DISPLAY ' TOTAL INPUT SKU RECORD READ: '  PA-SKU-IN-CNT.             
           DISPLAY ' TOTAL INPUT VS  RECORD OUT:  '  PA-VS-OUT-CNT.             
           DISPLAY ' TOTAL INPUT SKU RECORD OUT:  '  PA-SKU-OUT-CNT.            
           DISPLAY ' ************************************************'.         
      *                                                                         
           IF PA-VS-OUT-CNT  > 0 AND                                            
              PA-SKU-OUT-CNT > 0                                                
              MOVE 4095                   TO RETURN-CODE                        
           ELSE                                                                 
              IF PA-VS-OUT-CNT  > 0                                             
                 MOVE 4091                TO RETURN-CODE                        
              ELSE                                                              
                 IF PA-SKU-OUT-CNT  > 0                                         
                    MOVE 4092             TO RETURN-CODE                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *****************************************************************         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      *****************************************************************         
       9998-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.                    
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED .               
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.                   
      *                                                                         
           EXEC SQL                                                             
                ROLLBACK                                                        
           END-EXEC.                                                            
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBIAGE.               
      *                                                                         
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
      *                                                                         
      ********************** END OF PROGRAM ***************************         
