      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    IMKUT232.                                                 
       AUTHOR.        DAVID FELLENZ.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/17/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    IM DELIMIT THE STYLE PARTNER EXTRACT FILE.                           
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A DELIMITED, FILE FROM THE                  
      *    ALREADY EXTRACTED STYL PRTNR TABLE.                                  
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT IM-SP-EXT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IM-SP-DELIM-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT IM-SP-DELIM-COUNT                                             
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IM-SP-EXT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-SP-EXT-REC                    PIC X(14).                          
                                                                                
       FD  IM-SP-DELIM-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-SP-DELIM-REC                  PIC X(33).                          
                                                                                
       FD  IM-SP-DELIM-COUNT                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-SP-DELIM-CNT                  PIC X(80).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      ******************************************************************        
      * INPUT FILE LAYOUT FOR IM SP DAILY EXTRACT FILE                          
      ******************************************************************        
       01  IM-SP-EXTRACT-RECORD.                                                
           05 IM-SUB-CL-MDSEAR-ID           PIC  S9(6)V COMP-3.                 
           05 IM-VS-ID                      PIC  S9(9) BINARY.                  
           05 IM-BUS-PRTNR-ROL-CDE          PIC  X(02).                         
           05 IM-ENT-ID                     PIC  S9(9) BINARY.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)   VALUE 'IMKUT232'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC X      VALUE 'N'.               
               88  PS-END-OF-FILE                      VALUE 'Y'.               
               88  PS-NOT-END-OF-FILE                  VALUE 'N'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT        PIC 9(09)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT1      PIC 9(12)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT2      PIC 9(09)  VALUE 0.                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1               PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1              PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR IM SP DAILY DELIM FILE                           
      ******************************************************************        
           COPY IMRD124.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 8200-WRITE-SP-DELIM-CNT.                                     
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  IM-SP-EXT-FILE                                           
                OUTPUT IM-SP-DELIM-FILE                                         
                       IM-SP-DELIM-COUNT.                                       
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-SP-EXT-FILE.                                       
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE THE OUTPUT RECORDS.                                   
      *  READ THE NEXT INPUT RECORD.                                            
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE IM124-SP-DELIM-RECORD.                                    
                                                                                
           MOVE IM-SUB-CL-MDSEAR-ID       TO  IM124-SUB-CL-MDSEAR-ID            
                                                                                
           MOVE IM-VS-ID                  TO  IM124-VS-ID                       
                                                                                
           MOVE IM-BUS-PRTNR-ROL-CDE      TO  IM124-BUS-PRTNR-ROL-CDE           
                                                                                
           MOVE IM-ENT-ID                 TO  IM124-ENT-ID                      
                                                                                
           PERFORM 8100-WRITE-SP-DELIM-REC.                                     
                                                                                
           PERFORM 8000-READ-SP-EXT-FILE.                                       
                                                                                
      ************************************************************              
      *  READ THE SP EXTRACT FILE                                               
      ************************************************************              
       8000-READ-SP-EXT-FILE.                                                   
                                                                                
           READ IM-SP-EXT-FILE INTO IM-SP-EXTRACT-RECORD                        
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A SP DELIM RECORD.                                               
      ******************************************************************        
       8100-WRITE-SP-DELIM-REC.                                                 
                                                                                
           WRITE IM-SP-DELIM-REC                                                
               FROM IM124-SP-DELIM-RECORD.                                      
                                                                                
           ADD 1 TO PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
      ******************************************************************        
      *  WRITE A SP DELIM COUNT RECORD                                          
      ******************************************************************        
       8200-WRITE-SP-DELIM-CNT.                                                 
                                                                                
           WRITE IM-SP-DELIM-CNT                                                
               FROM PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   IM SP RECORDS INPUT         = '                          
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   IM SP DELIM RECS OUTPUT      = '                         
                    PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
                                                                                
           CLOSE IM-SP-EXT-FILE                                                 
                 IM-SP-DELIM-FILE                                               
                 IM-SP-DELIM-COUNT.                                             
                                                                                
