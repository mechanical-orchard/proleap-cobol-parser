       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP059.                                         00030000
       AUTHOR.        SURENDAR BALAKRISHNAN.                            00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  03/18/2010.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP059 - INVENTORY ADJUSTMENT AUDIT                *00100000
      *                      TABLE UPDATE (MLT_INV_ADJ_TRN_DTL)        *00110000
      ******************************************************************00120000
      ******************************************************************00130000
      *    WR/PITS#     DATE        DESCRIPTION                        *00140000
      *    --------   --------     ---------------------------------   *00150000
      *    WR37965    03/18/10     INITIAL CREATION                    *00160000
      ******************************************************************00170000
                                                                        00180000
       ENVIRONMENT DIVISION.                                            00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
       INPUT-OUTPUT SECTION.                                            00240000
       FILE-CONTROL.                                                    00250000
           SELECT INV-ADJ-DTL-FILE                                      00260000
               ASSIGN TO INP01.                                         00270000
                                                                        00280000
           SELECT CNTL-DATE-FILE                                        00290000
               ASSIGN TO INP02.                                         00300000
                                                                        00310000
       DATA DIVISION.                                                   00320000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  INV-ADJ-DTL-FILE                                             00350000
           LABEL RECORDS ARE STANDARD                                   00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS.                                    00380000
       01  INV-ADJ-DTL-REC            PIC X(200).                       00390000
                                                                        00400000
       FD  CNTL-DATE-FILE                                               00410000
           LABEL RECORDS ARE STANDARD                                   00420000
           RECORDING MODE IS F                                          00430000
           BLOCK CONTAINS 0 RECORDS.                                    00440000
       01  CNTL-DATE-REC               PIC X(80).                       00450000
      *                                                                 00460000
                                                                        00470000
       WORKING-STORAGE SECTION.                                         00480000
                                                                        00490000
       01  FILLER                      PIC X(28)   VALUE                00500000
                'BEGINNING OF WORKING STORAGE'.                         00510000
                                                                        00520000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00530000
                                                                        00540000
       01  PC-PROGRAM-CONSTANTS.                                        00550000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK059'.    00560000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP059'.  00570000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00580000
                                                                        00590000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00600000
           05  PE-MSG-01                       PIC X(50) VALUE          00610000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00620000
           05  PE-MSG-02                       PIC X(52) VALUE          00630000
                'ATTEMPT TO UPDATE ROW ON MLT_INV_ADJ_TRN_DTL FAILED'.  00640000
           05  PE-MSG-03                       PIC X(52) VALUE          00650000
                'ATTEMPT TO INSERT ROW ON MLT_INV_ADJ_TRN_DTL FAILED'.  00660000
           05  PE-MSG-04                       PIC X(50) VALUE          00670000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00680000
           05  PE-MSG-05                       PIC X(50) VALUE          00690000
                'UPDATE TO TCKRSTINF FAILED                      '.     00700000
           05  PE-MSG-06                       PIC X(50) VALUE          00710000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00720000
           05  PE-MSG-07                       PIC X(50) VALUE          00730000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00740000
           05  PE-MSG-08                       PIC X(50) VALUE          00750000
                'ERROR WHILE VALIDATING CONTROL DATE'.                  00760000
      *                                                                 00770000
       01  PS-PROGRAM-SWITCHES.                                         00780000
           05  PS-EOF-INV-DTL-FILE-SW       PIC X(01)     VALUE 'N'.    00790000
               88  PS-EOF-INV-DTL-FILE                    VALUE 'Y'.    00800000
           05  PS-MLT00037-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00810000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00820000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00830000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00840000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00850000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00860000
           05  PS-CNTL-DATE-FILE-SW         PIC X(01)     VALUE 'N'.    00870000
               88  PS-END-OF-CNTL-DTE-FILE                VALUE 'Y'.    00880000
      *                                                                 00890000
       01  CC-CONTROL-CARD.                                             00900000
           05  CC-CNTL-DATE                 PIC X(10)     VALUE SPACES. 00910000
           05  FILLER                       PIC X(70)     VALUE SPACES. 00920000
      *                                                                 00930000
       01  PROGRAM-VARIABLES.                                           00940000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00950000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00960000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00970000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00980000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00990000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       01000000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       01010000
           05  PV-DISPLAY-FIELD          PIC ZZZZZZZZ9  VALUE ZERO.     01020000
           05  PV-DB2-DATE-EXISTS        PIC   X(1) VALUE SPACES.       01030000
      *                                                                 01040000
       01  PA-PROGRAM-ACCUMULATORS.                                     01050000
           05  PA-RECORD-COUNTS.                                        01060000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01070000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01080000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01090000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01100000
      *                                                                 01110000
       01  PD-PROGRAM-DISPLAYS.                                         01120000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01130000
      *                                                                 01140000
      ******************************************************************01150000
      * MLRD090 - AUDIT DETAIL TRANSACTIONS                             01160000
      ******************************************************************01170000
           COPY MLRD090.                                                01180010
      *                                                                 01190000
      ******************************************************************01200000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01210000
      ******************************************************************01220000
           COPY DPWS004.                                                01230000
      *                                                                 01240000
      ******************************************************************01250000
      *  USED FOR DB2 ERRORS                                            01260000
      ******************************************************************01270000
           EXEC SQL                                                     01280000
               INCLUDE SQLCA                                            01290000
           END-EXEC.                                                    01300000
      *                                                                 01310000
      ******************************************************************01320000
      * DATE TABLE                                                      01330000
      ******************************************************************01340000
           EXEC SQL                                                     01350000
             INCLUDE TDTATTR                                            01360000
           END-EXEC.                                                    01370000
      *                                                                 01380000
      ******************************************************************01390000
      *  MERCHANDISE LEDGER SALES TRANSACTION AUDIT DETAIL              01400000
      ******************************************************************01410000
           EXEC SQL                                                     01420000
               INCLUDE MLT00037                                         01430000
           END-EXEC.                                                    01440000
      *                                                                 01450000
      ***************************************************************** 01460000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01470000
      ***************************************************************** 01480000
           EXEC SQL                                                     01490000
               INCLUDE TCKRSTCN                                         01500000
           END-EXEC.                                                    01510000
      *                                                                 01520000
           EXEC SQL                                                     01530000
               INCLUDE TCKRSTIN                                         01540000
           END-EXEC.                                                    01550000
      *                                                                 01560000
      ******************************************************************01570000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01580000
      ******************************************************************01590000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01600000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +76 COMP.  01610000
           05  CR-CHECKPOINT-RESTART-VAR.                               01620000
               10  CR-PREV-DTL-KEY.                                     01630000
                   15  CR-FSCL-WK-END-DTE   PIC X(10)  VALUE SPACES.    01640000
                   15  CR-PRCG-DTE          PIC X(10)  VALUE SPACES.    01650000
                   15  CR-DEPT-NBR          PIC S9(4)V COMP-3 VALUE +0. 01660000
                   15  CR-MAJ-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01670000
                   15  CR-SUB-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01680000
                   15  CR-LOC-NBR           PIC S9(4)  COMP   VALUE +0. 01690000
                   15  CR-ML-LO-MDSE-LVL-ID PIC S9(9)  COMP   VALUE +0. 01700000
                   15  CR-FINCL-PRC-GP-CDE  PIC X(06) VALUE SPACES.     01710000
                   15  CR-SRC-SYS-ID        PIC S9(4)  COMP   VALUE +0. 01720000
                   15  CR-TRN-TYP-CDE       PIC X(03) VALUE SPACES.     01730000
                   15  CR-ENT-ID            PIC S9(9)  COMP   VALUE +0. 01740000
                   15  CR-LBL-SEQ-NBR       PIC S9(4)  COMP   VALUE +0. 01750000
               10  CR-INPUT-READ-COUNT      PIC 9(09) VALUE ZEROES.     01760000
      *                                                                 01770000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01780000
           05  CK-SAVE-PREV-KEY.                                        01790000
               10  CK-FSCL-WK-END-DTE     PIC X(10)  VALUE SPACES.      01800000
               10  CK-PRCG-DTE            PIC X(10)  VALUE SPACES.      01810000
               10  CK-DEPT-NBR            PIC S9(4)V COMP-3 VALUE +0.   01820000
               10  CK-MAJ-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01830000
               10  CK-SUB-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01840000
               10  CK-LOC-NBR             PIC S9(4)  COMP   VALUE +0.   01850000
               10  CK-ML-LO-MDSE-LVL-ID   PIC S9(9)  COMP   VALUE +0.   01860000
               10  CK-FINCL-PRC-GP-CDE    PIC X(06) VALUE SPACES.       01870000
               10  CK-SRC-SYS-ID          PIC S9(4)  COMP   VALUE +0.   01880000
               10  CK-TRN-TYP-CDE         PIC X(03) VALUE SPACES.       01890000
               10  CK-ENT-ID              PIC S9(9)  COMP   VALUE +0.   01900000
               10  CK-LBL-SEQ-NBR         PIC S9(4)  COMP   VALUE +0.   01910000
                                                                        01920000
      *-------------------*                                             01930000
       PROCEDURE DIVISION.                                              01940000
      *-------------------*                                             01950000
                                                                        01960000
       0000-MLKUP059.                                                   01970002
           PERFORM 1000-INITIALIZATION.                                 01980000
      *                                                                 01990000
           PERFORM 2000-PROCESS-INPUT                                   02000000
                UNTIL PS-EOF-INV-DTL-FILE.                              02010000
      *                                                                 02020000
           PERFORM 8000-EOJ-ROUTINE.                                    02030000
                                                                        02040000
           STOP RUN.                                                    02050000
                                                                        02060000
      ******************************************************************02070000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02080000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02090000
      ******************************************************************02100000
      *                                                                 02110000
       1000-INITIALIZATION.                                             02120000
      *                                                                 02130007
           OPEN INPUT INV-ADJ-DTL-FILE                                  02140000
                      CNTL-DATE-FILE.                                   02150000
                                                                        02160000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02170000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02180000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02190000
               PERFORM 7500-SELECT-RESTART-INFO                         02200000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02210000
                                            CR-CHECKPOINT-RESTART-VAR   02220000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02230000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02240000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            02250000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   02260000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           02270000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              02280000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           02290000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              02300000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           02310000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              02320000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           02330000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              02340000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           02350000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              02360000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           02370000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           02380000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              02390000
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                02400000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           02410000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              02420000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           02430000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              02440000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02450000
           ELSE                                                         02460000
               SET PS-FIRST-COMMIT TO TRUE                              02470000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02480000
           END-IF.                                                      02490000
      *                                                                 02500000
           PERFORM 5000-READ-DATE-CNTL-FILE                             02510000
           PERFORM 4000-READ-INV-DTL-FILE UNTIL                         02520000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02530000
            OR PS-EOF-INV-DTL-FILE.                                     02540000
      *                                                                 02550000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02560000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02570000
                        PA-INPUT-COUNT                                  02580000
               DISPLAY 'FSCL WK END DTE ' ML090-FSCL-WK-END-DTE         02590000
               DISPLAY 'PRCG DATE       ' ML090-PRCG-DTE                02600000
               MOVE ML090-DEPT-NBR        TO PV-DISPLAY-FIELD           02610000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              02620000
               MOVE ML090-MAJ-CL-NBR      TO PV-DISPLAY-FIELD           02630000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              02640000
               MOVE ML090-SUB-CL-NBR      TO PV-DISPLAY-FIELD           02650000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              02660000
               MOVE ML090-LOC-NBR         TO PV-DISPLAY-FIELD           02670000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              02680000
               MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD         02690000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              02700000
               DISPLAY 'FINCL PRC CDE   ' ML090-FINCL-PRC-GP-CDE        02710000
               MOVE ML090-SRC-SYS-ID      TO PV-DISPLAY-FIELD           02720000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              02730000
               DISPLAY 'TRN TYP CDE     ' ML090-TRN-TYP-CDE             02740000
               MOVE ML090-ENT-ID          TO PV-DISPLAY-FIELD           02750000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              02760000
               MOVE ML090-LBL-SEQ-NBR     TO PV-DISPLAY-FIELD           02770000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              02780000
           END-IF.                                                      02790000
                                                                        02800000
           IF PS-EOF-INV-DTL-FILE                                       02810000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02820000
                   CONTINUE                                             02830000
               ELSE                                                     02840000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02850000
               END-IF                                                   02860000
           ELSE                                                         02870000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02880000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02890000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02900000
           END-IF.                                                      02910000
                                                                        02920000
      ******************************************************************02930000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02940000
      ******************************************************************02950000
       2000-PROCESS-INPUT.                                              02960000
      *                                                                 02970000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02980000
      *                                                                 02990000
           IF CC-CNTL-DATE = SPACES                                     03000000
             MOVE ML090-PRCG-DTE        TO MLT00037-PRCG-DTE            03010000
           ELSE                                                         03020000
             MOVE CC-CNTL-DATE          TO MLT00037-PRCG-DTE            03030000
           END-IF                                                       03040000
      *                                                                 03050002
           PERFORM 7200-INSERT-MLT00037-ROW                             03060009
      *                                                                 03070000
           IF PS-PROGRAM-DEADLOCKED                                     03080000
               CONTINUE                                                 03090000
           ELSE                                                         03100000
               PERFORM 7700-COMMIT-PROCESSING                           03110000
               PERFORM 4000-READ-INV-DTL-FILE                           03120000
           END-IF.                                                      03130000
      *                                                                 03140000
      ******************************************************************03150000
      * READS THE CONDENSED FILE INTO THE MLT_INV_ADJ_TRN_DTL LAYOUT    03160000
      ******************************************************************03170000
       4000-READ-INV-DTL-FILE.                                          03180000
           READ INV-ADJ-DTL-FILE INTO ML090-AUDIT-REC                   03190000
              AT END                                                    03200000
                MOVE 'Y' TO PS-EOF-INV-DTL-FILE-SW.                     03210000
      *                                                                 03220000
           IF PS-EOF-INV-DTL-FILE                                       03230000
               IF PA-INPUT-COUNT = 0                                    03240006
                    INITIALIZE ML090-AUDIT-REC                          03250006
               END-IF                                                   03260006
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     03270000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            03280000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            03290000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03300000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          03310000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             03320000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   03330000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    03340000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          03350000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         03360000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              03370000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         03380000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03390000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03400000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03410000
                                             TCKRSTINF-WORK-STRG-DESC   03420000
               EXEC SQL                                                 03430000
                 UPDATE TCKRSTINF                                       03440000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03450000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03460000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03470000
               END-EXEC                                                 03480000
               IF SQLCODE = 0                                           03490000
                   CONTINUE                                             03500000
               ELSE                                                     03510000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03520000
                   MOVE '* 4000-READ-INV-DTL  *' TO PV-PARAGRAPH-NAME   03530000
                   PERFORM 9999-SQL-ABEND                               03540000
               END-IF                                                   03550000
            ELSE                                                        03560000
                ADD 1                        TO PA-INPUT-COUNT          03570000
            END-IF.                                                     03580000
      *                                                                 03590000
      ******************************************************************03600000
      * READ DATE CONTROL FILE                                          03610000
      ******************************************************************03620000
       5000-READ-DATE-CNTL-FILE.                                        03630000
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                     03640000
               AT END                                                   03650000
                   MOVE SPACES TO CC-CONTROL-CARD                       03660000
                   SET PS-END-OF-CNTL-DTE-FILE TO TRUE                  03670000
                   DISPLAY  'PROCESSING DATE CONTROL CARD EMPTY'.       03680001
           IF NOT PS-END-OF-CNTL-DTE-FILE                               03690000
               PERFORM 5001-VALIDATE-CNTL-DATE                          03700000
           END-IF.                                                      03710000
      *                                                                 03720000
      ******************************************************************03730000
      * VALIDATE CONTROL DATE                                           03740000
      ******************************************************************03750000
       5001-VALIDATE-CNTL-DATE.                                         03760000
                  EXEC SQL                                              03770000
                     SELECT 'Y'                                         03780000
                       INTO :PV-DB2-DATE-EXISTS                         03790000
                       FROM TDTATTR                                     03800000
                      WHERE KY_DTE = :CC-CNTL-DATE                      03810000
                  END-EXEC                                              03820000
                                                                        03830000
                  EVALUATE TRUE                                         03840000
                      WHEN SQLCODE = 0                                  03850000
                           CONTINUE                                     03860000
                      WHEN SQLCODE = 100                                03870000
                           DISPLAY 'INVALID DATE IN CONTROL CARD'       03880000
                           MOVE SPACES TO CC-CNTL-DATE                  03890000
                      WHEN OTHER                                        03900000
                           CONTINUE                                     03910000
                           MOVE '5001-VALIDATE-CNTL-DATE' TO            03920000
                                              PV-PARAGRAPH-NAME         03930000
                           MOVE PE-MSG-08 TO PV-OPERATION-ATTEMPTED     03940000
                           PERFORM 9999-SQL-ABEND                       03950000
                  END-EVALUATE.                                         03960000
      *                                                                 03970000
      ***************************************************************** 03980004
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON KEY. CURRENT ROW 03990004
      * AMOUNT FIELDS UPDATED TO ADD VALUES FROM INPUT REC.             04000004
      ***************************************************************** 04010004
       7100-UPDATE-MLT00037-ROW.                                        04020000
      *                                                                 04030000
           EXEC SQL                                                     04040000
               UPDATE MLT_INV_ADJ_TRN_DTL                               04050000
                   SET INV_RTL_ADJ_AMT    = INV_RTL_ADJ_AMT  +          04060002
                                            :ML090-INV-ADJ-RTL-AMT      04070002
                      ,INV_COST_ADJ_AMT   = INV_COST_ADJ_AMT +          04080002
                                            :ML090-INV-ADJ-COST-AMT     04090002
              WHERE   FSCL_WK_END_DTE     = :ML090-FSCL-WK-END-DTE      04100000
                AND   PRCG_DTE            = :MLT00037-PRCG-DTE          04110000
                AND   DEPT_NBR            = :ML090-DEPT-NBR             04120000
                AND   MAJ_CL_NBR          = :ML090-MAJ-CL-NBR           04130000
                AND   SUB_CL_NBR          = :ML090-SUB-CL-NBR           04140000
                AND   LOC_NBR             = :ML090-LOC-NBR              04150000
                AND   ML_LO_MDSE_LVL_ID   = :ML090-ML-LO-MDSE-LVL-ID    04160000
                AND   FINCL_PRC_GP_CDE    = :ML090-FINCL-PRC-GP-CDE     04170000
                AND   SRC_SYS_ID          = :ML090-SRC-SYS-ID           04180000
                AND   TRN_TYP_CDE         = :ML090-TRN-TYP-CDE          04190000
                AND   ENT_ID              = :ML090-ENT-ID               04200000
                AND   LBL_SEQ_NBR         = :ML090-LBL-SEQ-NBR          04210000
           END-EXEC                                                     04220000
      *                                                                 04230000
           EVALUATE SQLCODE                                             04240000
               WHEN 0                                                   04250000
                   ADD 1 TO PA-UPDATE-COUNT                             04260002
               WHEN -911                                                04270000
                   ADD +1             TO PV-DEADLOCK-COUNT              04280000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04290000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04300000
                       MOVE '7100-UPDATE-MLT00037-ROW' TO               04310000
                                                    PV-PARAGRAPH-NAME   04320000
                       PERFORM 9000-DEADLOCK-ERROR                      04330000
                   ELSE                                                 04340000
                       PERFORM 7999-RESTART-PROCESS                     04350000
                   END-IF                                               04360000
               WHEN OTHER                                               04370000
                   MOVE '7100-UPDATE-MLT00037-ROW' TO PV-PARAGRAPH-NAME 04380000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04390000
                   PERFORM 9999-SQL-ABEND                               04400000
           END-EVALUATE.                                                04410000
      *                                                                 04420000
      ***************************************************************** 04430000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 04440000
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 04450000
      ***************************************************************** 04460000
       7200-INSERT-MLT00037-ROW.                                        04470000
      *                                                                 04480000
           EXEC SQL                                                     04490000
               INSERT INTO MLT_INV_ADJ_TRN_DTL                          04500000
                    (  FSCL_WK_END_DTE                                  04510000
                     , PRCG_DTE                                         04520000
                     , DEPT_NBR                                         04530000
                     , MAJ_CL_NBR                                       04540000
                     , SUB_CL_NBR                                       04550000
                     , LOC_NBR                                          04560000
                     , ML_LO_MDSE_LVL_ID                                04570000
                     , FINCL_PRC_GP_CDE                                 04580000
                     , SRC_SYS_ID                                       04590000
                     , TRN_TYP_CDE                                      04600000
                     , ENT_ID                                           04610000
                     , LBL_SEQ_NBR                                      04620000
                     , INV_RTL_ADJ_AMT                                  04630000
                     , INV_COST_ADJ_AMT)                                04640000
               VALUES                                                   04650000
                   (   :ML090-FSCL-WK-END-DTE                           04660000
                     , :MLT00037-PRCG-DTE                               04670000
                     , :ML090-DEPT-NBR                                  04680000
                     , :ML090-MAJ-CL-NBR                                04690000
                     , :ML090-SUB-CL-NBR                                04700000
                     , :ML090-LOC-NBR                                   04710000
                     , :ML090-ML-LO-MDSE-LVL-ID                         04720000
                     , :ML090-FINCL-PRC-GP-CDE                          04730000
                     , :ML090-SRC-SYS-ID                                04740000
                     , :ML090-TRN-TYP-CDE                               04750000
                     , :ML090-ENT-ID                                    04760000
                     , :ML090-LBL-SEQ-NBR                               04770000
                     , :ML090-INV-ADJ-RTL-AMT                           04780000
                     , :ML090-INV-ADJ-COST-AMT)                         04790000
           END-EXEC                                                     04800000
      *                                                                 04810000
           EVALUATE SQLCODE                                             04820000
               WHEN 0                                                   04830000
                   ADD 1 TO PA-INSERT-COUNT                             04840002
               WHEN -803                                                04850002
                   PERFORM 7100-UPDATE-MLT00037-ROW                     04860002
               WHEN -911                                                04870000
                   ADD +1             TO PV-DEADLOCK-COUNT              04880000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            04890000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04900000
                       MOVE '7200-INSERT-MLT00037-ROW' TO               04910000
                                                    PV-PARAGRAPH-NAME   04920000
                       PERFORM 9000-DEADLOCK-ERROR                      04930000
                   ELSE                                                 04940000
                       PERFORM 7999-RESTART-PROCESS                     04950000
                   END-IF                                               04960000
               WHEN OTHER                                               04970000
                   MOVE '7200-INSERT-MLT00037-ROW' TO PV-PARAGRAPH-NAME 04980000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED04990000
                   PERFORM 9999-SQL-ABEND                               05000000
           END-EVALUATE.                                                05010000
                                                                        05020000
      ******************************************************************05030000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05040000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05050000
      *            CONTROL TABLE                                       *05060000
      ******************************************************************05070000
       7300-GET-COMMIT-TIMESTAMP.                                       05080000
                                                                        05090000
           EXEC SQL                                                     05100000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05110000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05120000
               INTO :PV-COMMIT-TIMESTAMP                                05130000
               FROM TCKRSTCNTL                                          05140000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05150000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05160000
           END-EXEC.                                                    05170000
                                                                        05180000
           EVALUATE TRUE                                                05190000
               WHEN SQLCODE = 0                                         05200000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05210000
                   CONTINUE                                             05220000
               WHEN SQLCODE NOT EQUAL ZERO                              05230000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05240000
                   PERFORM 9999-SQL-ABEND                               05250000
           END-EVALUATE.                                                05260000
      *                                                                 05270000
      *                                                                 05280000
      ******************************************************************05290000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05300000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05310000
      *            IF WE ARE IN A RESTART CONDITION.                   *05320000
      ******************************************************************05330000
       7400-INIT-CHECKPOINT-SELECT.                                     05340000
           EXEC SQL                                                     05350000
             SELECT  CKPT_STAT_CODE                                     05360000
                    ,CKPT_FRQNCY_QTY                                    05370000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05380000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05390000
               FROM  TCKRSTCNTL                                         05400000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05410000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05420000
           END-EXEC.                                                    05430000
                                                                        05440000
           IF SQLCODE = 0                                               05450000
               CONTINUE                                                 05460000
           ELSE                                                         05470000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05480000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05490000
               PERFORM 9999-SQL-ABEND                                   05500000
           END-IF.                                                      05510000
      *                                                                 05520000
      *                                                                 05530000
      ******************************************************************05540000
      * 7500-SELECT-RESTART-INFO                                       *05550000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05560000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05570000
      ******************************************************************05580000
       7500-SELECT-RESTART-INFO.                                        05590000
           EXEC SQL                                                     05600000
             SELECT  WORK_STRG_DESC                                     05610000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05620000
               FROM  TCKRSTINF                                          05630000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05640000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05650000
           END-EXEC.                                                    05660000
                                                                        05670000
           IF SQLCODE = 0                                               05680000
               CONTINUE                                                 05690000
           ELSE                                                         05700000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05710000
               PERFORM 9999-SQL-ABEND                                   05720000
           END-IF.                                                      05730000
                                                                        05740000
      ******************************************************************05750000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05760000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05770000
      ******************************************************************05780000
       7600-SET-CURRENT-TIMESTAMP.                                      05790000
                                                                        05800000
           EXEC SQL                                                     05810000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05820000
           END-EXEC.                                                    05830000
                                                                        05840000
           IF SQLCODE = 0                                               05850000
               CONTINUE                                                 05860000
           ELSE                                                         05870000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05880000
               PERFORM 9999-SQL-ABEND                                   05890000
           END-IF.                                                      05900000
      *                                                                 05910000
      *                                                                 05920000
      ******************************************************************05930000
      * 7700-COMMIT-PROCESSING.                                        *05940000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *05950000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*05960000
      ******************************************************************05970000
       7700-COMMIT-PROCESSING.                                          05980000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     05990000
                                                                        06000000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06010000
                                                                        06020000
      * PREPARE COMMIT RECORD FOR UPDATE                                06030000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06040000
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     06050000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            06060000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            06070000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06080000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          06090000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             06100000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   06110000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    06120000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          06130000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         06140000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              06150000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         06160000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06170000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06180000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06190000
                                             TCKRSTINF-WORK-STRG-DESC   06200000
               IF PS-FIRST-COMMIT                                       06210000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06220000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06230000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06240000
               END-IF                                                   06250000
               PERFORM 7800-COMMIT-CHANGES                              06260000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06270000
           END-IF.                                                      06280000
      *                                                                 06290000
      *                                                                 06300000
      ******************************************************************06310000
      * 7800-COMMIT-CHANGES.                                            06320000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06330000
      *            TAKE A COMMIT.                                       06340000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06350000
      ******************************************************************06360000
       7800-COMMIT-CHANGES.                                             06370000
                                                                        06380000
           EXEC SQL                                                     06390000
             UPDATE TCKRSTINF                                           06400000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06410000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06420000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06430000
           END-EXEC.                                                    06440000
                                                                        06450000
           IF SQLCODE = 0                                               06460000
               CONTINUE                                                 06470000
           ELSE                                                         06480000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06490000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06500000
               PERFORM 9999-SQL-ABEND                                   06510000
           END-IF.                                                      06520000
                                                                        06530000
           EXEC SQL                                                     06540000
               COMMIT                                                   06550000
           END-EXEC.                                                    06560000
                                                                        06570000
           IF SQLCODE = 0                                               06580000
               ADD 1 TO PA-COMMIT-COUNT                                 06590000
           ELSE                                                         06600000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06610000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06620000
               PERFORM 9999-SQL-ABEND                                   06630000
           END-IF.                                                      06640000
      *                                                                 06650000
      ******************************************************************06660000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06670000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06680000
      ******************************************************************06690000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06700000
                                                                        06710000
           EXEC SQL                                                     06720000
             UPDATE  TCKRSTCNTL                                         06730000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06740000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06750000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06760000
           END-EXEC.                                                    06770000
                                                                        06780000
           IF SQLCODE = 0                                               06790000
               CONTINUE                                                 06800000
           ELSE                                                         06810000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06820000
               PERFORM 9999-SQL-ABEND                                   06830000
           END-IF.                                                      06840000
                                                                        06850000
           EJECT                                                        06860000
      *                                                                 06870000
      *                                                                 06880000
      ******************************************************************06890000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_INV_ADJ_TRN_DTL   06900000
      * ROW FOR UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT  06910000
      * OF LAST COMMIT.                                                 06920000
      ******************************************************************06930000
       7999-RESTART-PROCESS.                                            06940000
      *                                                                 06950000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           06960000
      *                                                                 06970000
           CLOSE INV-ADJ-DTL-FILE.                                      06980000
      *                                                                 06990000
           PERFORM 7500-SELECT-RESTART-INFO.                            07000000
      *                                                                 07010000
           DISPLAY '**** RESTART **** '.                                07020000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07030000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07040000
                                                                        07050000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07060000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07070000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07080000
           IF PS-FIRST-COMMIT                                           07090000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:76)               07100000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07110000
                      ' BEGINNING'                                      07120000
           ELSE                                                         07130000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07140000
                        TCKRSTINF-WORK-STRG-DESC (1:76)                 07150000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07160000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07170000
                    CR-CHECKPOINT-RESTART-AREA                          07180000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07190000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            07200000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   07210000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           07220000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              07230000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           07240000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              07250000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           07260000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              07270000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           07280000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              07290000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           07300000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              07310000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           07320000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           07330000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              07340000
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                07350000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           07360000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              07370000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           07380000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              07390000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07400000
           END-IF.                                                      07410000
                                                                        07420000
           OPEN INPUT INV-ADJ-DTL-FILE.                                 07430000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07440000
      *                                                                 07450000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07460000
           PERFORM 4000-READ-INV-DTL-FILE                               07470000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07480000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  07490000
                    PA-INPUT-COUNT.                                     07500000
           DISPLAY 'FSCL WK END DTE '     ML090-FSCL-WK-END-DTE.        07510000
           DISPLAY 'PRCG DATE       '     ML090-PRCG-DTE.               07520000
           MOVE ML090-DEPT-NBR          TO PV-DISPLAY-FIELD.            07530000
           DISPLAY 'DEPARTMENT NBR  '      PV-DISPLAY-FIELD.            07540000
           MOVE ML090-MAJ-CL-NBR        TO PV-DISPLAY-FIELD.            07550000
           DISPLAY 'MAJOR CLASS     '      PV-DISPLAY-FIELD.            07560000
           MOVE ML090-SUB-CL-NBR        TO PV-DISPLAY-FIELD.            07570000
           DISPLAY 'SUB CLASS       '      PV-DISPLAY-FIELD.            07580000
           MOVE ML090-LOC-NBR           TO PV-DISPLAY-FIELD.            07590000
           DISPLAY 'LOC NBR         '      PV-DISPLAY-FIELD.            07600000
           MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD.            07610000
           DISPLAY 'ML LWST ID      '      PV-DISPLAY-FIELD.            07620000
           DISPLAY 'FINCL PRC CDE   '      ML090-FINCL-PRC-GP-CDE.      07630000
           MOVE ML090-SRC-SYS-ID        TO PV-DISPLAY-FIELD.            07640000
           DISPLAY 'SRC SYS ID      '      PV-DISPLAY-FIELD.            07650000
           DISPLAY 'TRN TYP CDE     '      ML090-TRN-TYP-CDE.           07660000
           MOVE ML090-ENT-ID            TO PV-DISPLAY-FIELD.            07670000
           DISPLAY 'ENT-ID          '      PV-DISPLAY-FIELD.            07680000
           MOVE ML090-LBL-SEQ-NBR       TO PV-DISPLAY-FIELD.            07690000
           DISPLAY 'LBL SEQ NBR     '      PV-DISPLAY-FIELD.            07700000
      ******************************************************************07710000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07720000
      ******************************************************************07730000
       8000-EOJ-ROUTINE.                                                07740000
      *                                                                 07750000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07760000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07770000
                                                                        07780000
      *                                                                 07790000
           DISPLAY SPACES.                                              07800000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07810000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07820000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07830000
           DISPLAY SPACES.                                              07840000
      *                                                                 07850000
           CLOSE INV-ADJ-DTL-FILE.                                      07860000
      *                                                                 07870000
      *                                                                 07880000
      ******************************************************************07890000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       07900000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07910000
      ******************************************************************07920000
       9000-DEADLOCK-ERROR.                                             07930000
      *                                                                 07940000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07950000
               PERFORM 9999-SQL-ABEND.                                  07960000
      *                                                                 07970000
      ******************************************************************07980000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07990000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08000000
      ******************************************************************08010000
       9999-SQL-ABEND.                                                  08020000
                                                                        08030000
           MOVE +4013                 TO ABEND-CODE.                    08040000
           DISPLAY '**  ABEND     **'.                                  08050000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08060000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08070000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08080000
                                                                        08090000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08100000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08110000
                                                                        08120000
           COPY DPPD004.                                                08130000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08140000
