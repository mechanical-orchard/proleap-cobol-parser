00001 *                                                                 06/04/92
00002 *                                                                 MBKCS136
00003 *                                                                    LV002
00004  IDENTIFICATION DIVISION.                                         MBKCS136
00005                                                                   MBKCS136
00006  PROGRAM-ID.    MBKCS136.                                         MBKCS136
00007  AUTHOR.        JO ANN  M. SCHMIDT.                               MBKCS136
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MBKCS136
00009  DATE-WRITTEN.  03-12-92.                                         MBKCS136
00010  DATE-COMPILED.                                                   MBKCS136
00011                                                                   MBKCS136
00012 ***************************************************************** MBKCS136
00013 *    WORKER SELECTION CRITERIA                                    MBKCS136
00014 *                                                                 MBKCS136
00015 *    THIS PROGRAM ALLOWS THE USER TO SPECIFY THE CRITERIA         MBKCS136
00016 *    THAT IS TO BE USED IN GENERATING THE WORKER SELECTION        MBKCS136
00017 *    LIST. THE CRITERIA IS SPECIFIED BY ENTERING ALL OR PART      MBKCS136
00018 *    OF A WORKER LAST NAME, '*' FOR ALL WORKERS, OR A SPECIFIC    MBKCS136
00019 *    WORKER USER ID TO SELECT A SPECIFIC WORKER.                  MBKCS136
00020 *                                                                 MBKCS136
00021 ***************************************************************** MBKCS136
00022  EJECT                                                            MBKCS136
00023 *                                                                 MBKCS136
00024 *                                                                 MBKCS136
00025  ENVIRONMENT DIVISION.                                            MBKCS136
00026 *                                                                 MBKCS136
00027 *                                                                 MBKCS136
00028  DATA DIVISION.                                                   MBKCS136
00029 *                                                                 MBKCS136
00030 *                                                                 MBKCS136
00031  WORKING-STORAGE SECTION.                                         MBKCS136
00032                                                                   MBKCS136
00033  01  PC-PROGRAM-CONSTANTS.                                        MBKCS136
00034      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.MBKCS136
00035      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072     CL**2
00036                                                      COMP SYNC.   MBKCS136
00037      05  PC-CURRENT-MAP-NAME            PIC X(08)                 MBKCS136
00038                                             VALUE 'MB136A  '.     MBKCS136
00039      05  PC-CURRENT-MAPSET-NAME         PIC X(08)                 MBKCS136
00040                                             VALUE 'MBKM136 '.     MBKCS136
00041      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)                 MBKCS136
00042                                             VALUE 'MBKCS136'.     MBKCS136
00043      05  PC-WKR-LIST-MNEMONIC           PIC X(08)                    CL**2
00044                                             VALUE 'MBLWORKR'.     MBKCS136
00045      05  PC-WKR-UPD-MNEMONIC            PIC X(08)                    CL**2
00046                                             VALUE 'MBUWORKR'.     MBKCS136
00047      05  PC-WKR-ADD-MNEMONIC            PIC X(08)                    CL**2
00048                                             VALUE 'MBAWORKR'.     MBKCS136
00049      05  PC-WKR-DEL-MNEMONIC            PIC X(08)                    CL**2
00050                                             VALUE 'MBDWORKR'.     MBKCS136
00051      05  PC-TSYMSG-NUMBERS.                                       MBKCS136
00052          10  PC-TSYMSG-00005           PIC 9(05)    VALUE 00005.  MBKCS136
00053          10  PC-TSYMSG-00007           PIC 9(05)    VALUE 00007.  MBKCS136
00054 *            ** EITHER A LAST NAME (OR PARTIAL LAST NAME STRING)  MBKCS136
00055 *            ** OR A USERID MUST BE ENTERED                       MBKCS136
00056          10  PC-TSYMSG-00137           PIC 9(05)    VALUE 00137.     CL**2
00057                                                                   MBKCS136
00058  01  PM-PROGRAM-MESSAGES.                                         MBKCS136
00059      05  PM-PROGRAM-MESSAGE-1           PIC X(78)    VALUE        MBKCS136
00060      'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                MBKCS136
00061                                                                   MBKCS136
00062  01  PS-PROGRAM-SWITCHES.                                         MBKCS136
00063      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   MBKCS136
00064          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   MBKCS136
00065          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   MBKCS136
00066          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   MBKCS136
00067          88  PS-SEND-DATA-ONLY                       VALUE '4'.   MBKCS136
00068          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   MBKCS136
00069          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   MBKCS136
00070      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   MBKCS136
00071          88  PS-ERROR                                VALUE 'Y'.   MBKCS136
00072          88  PS-NO-ERROR                             VALUE 'N'.   MBKCS136
00073                                                                   MBKCS136
00074  01  PV-PROGRAM-VARIABLES.                                        MBKCS136
00075      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     MBKCS136
00076                                                      COMP SYNC.   MBKCS136
00077      05  PV-MSG-NUMBER-X                PIC X(05)    VALUE SPACES.MBKCS136
00078      05  PV-INSPECT-CTR                 PIC S9(03)   VALUE +0.       CL**2
00079                                                                   MBKCS136
00080  EJECT                                                            MBKCS136
00081 ***************************************************************** MBKCS136
00082 *    STANDARD COMMAREA.                                           MBKCS136
00083 ***************************************************************** MBKCS136
00084                                                                   MBKCS136
00085      COPY DPWS020.                                                   CL**3
00086      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                MBKCS136
00087                                                                   MBKCS136
00088  EJECT                                                            MBKCS136
00089 ***************************************************************** MBKCS136
00090 *    INTER-APPLICATION COMMAREA.                                  MBKCS136
00091 *     ( FOR MODULES MBKCS136 THRU MBKCS138 )                      MBKCS136
00092 ***************************************************************** MBKCS136
00093                                                                   MBKCS136
00094          10  INTER-APPL-COMM-AREA           PIC X(200).           MBKCS136
00095      COPY MBWS007.                                                   CL**3
00096                                                                   MBKCS136
00097  EJECT                                                            MBKCS136
00098 ***************************************************************** MBKCS136
00099 *    SPECIFIC COMMMAREA FOR MBKCS136.                             MBKCS136
00100 ***************************************************************** MBKCS136
00101                                                                   MBKCS136
00102          10  APPLICATION-SPECIFIC-COMMAREA.                       MBKCS136
00103              15  MB136-SAVE-COMMAREA        PIC X(200).              CL**2
00104                                                                   MBKCS136
00105  EJECT                                                            MBKCS136
00106 ***************************************************************** MBKCS136
00107 *    MAP LAYOUT                                                   MBKCS136
00108 ***************************************************************** MBKCS136
00109                                                                   MBKCS136
00110      COPY MBKM136.                                                MBKCS136
00111                                                                   MBKCS136
00112  01  MD-MAP-DATA-AREA REDEFINES MB136AO.                          MBKCS136
00113      05  FILLER                         PIC X(432).               MBKCS136
00114                                                                   MBKCS136
00115  EJECT                                                            MBKCS136
00116 ***************************************************************** MBKCS136
00117 *    STANDARD ATTENTION IDENTIFIER LIST                           MBKCS136
00118 ***************************************************************** MBKCS136
00119                                                                   MBKCS136
00120      COPY DFHAID.                                                 MBKCS136
00121                                                                   MBKCS136
00122  EJECT                                                            MBKCS136
00123 ***************************************************************** MBKCS136
00124 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          MBKCS136
00125 ***************************************************************** MBKCS136
00126                                                                   MBKCS136
00127      COPY DPWS009.                                                MBKCS136
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00129  EJECT                                                            MBKCS136
00130 ***************************************************************** MBKCS136
00131 *    FUNCTION KEYS COPYBOOK.                                      MBKCS136
00132 ***************************************************************** MBKCS136
00133                                                                   MBKCS136
00134      COPY DPWS016.                                                MBKCS136
00135                                                                   MBKCS136
00136  EJECT                                                            MBKCS136
00137 ***************************************************************** MBKCS136
00138 * ?? STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST        MBKCS136
00139 ***************************************************************** MBKCS136
00140                                                                   MBKCS136
00141      COPY DFHBMSCA.                                               MBKCS136
00142                                                                   MBKCS136
00143  EJECT                                                            MBKCS136
00144 ***************************************************************** MBKCS136
00145 *    ATTRIBUTE SETTINGS COPYBOOK.                                 MBKCS136
00146 ***************************************************************** MBKCS136
00147                                                                   MBKCS136
00148      COPY DPWS015.                                                MBKCS136
00149                                                                   MBKCS136
00150  EJECT                                                            MBKCS136
00151 ***************************************************************** MBKCS136
00152 *    CURRENT DATE AND TIME COPYBOOK.                              MBKCS136
00153 ***************************************************************** MBKCS136
00154                                                                   MBKCS136
00155      COPY DPWS017.                                                MBKCS136
00156                                                                   MBKCS136
00157  EJECT                                                            MBKCS136
00158 ***************************************************************** MBKCS136
00159 *    NUMERIC DATE EDIT ROUTINE WORKING STORAGE                    MBKCS136
00160 ***************************************************************** MBKCS136
00161                                                                   MBKCS136
00162      COPY DPWS010I.                                               MBKCS136
00163                                                                   MBKCS136
00164  EJECT                                                            MBKCS136
00165 ***************************************************************** MBKCS136
00166 *    ALPHA NUMERIC FIELD EDIT ROUTINE WORKING STORAGE             MBKCS136
00167 ***************************************************************** MBKCS136
00168                                                                   MBKCS136
00169      COPY DPWS025.                                                   CL**2
00170                                                                   MBKCS136
00171  EJECT                                                            MBKCS136
00172 ***************************************************************** MBKCS136
00173 *    DPKUT045 COPYBOOK.                                           MBKCS136
00174 *    IT IS USED TO CONVERT EIBAID CHARACTERS RECEIVED FROM A      MBKCS136
00175 *    TERMINAL TO AN INTEGER VALUE OF 1 THRU 29. THIS VALUE IS     MBKCS136
00176 *    STORED IN 'DP020-SRC-AID' AND IS USED TO INTEROGATE THE      MBKCS136
00177 *    'DP020-FUNCTION-KEY-ARRAY' TABLE OF THE STANDARD COMMAREA.   MBKCS136
00178 *    THIS DETERMINES WHAT IS TO BE DONE NEXT.                     MBKCS136
00179 ***************************************************************** MBKCS136
00180                                                                   MBKCS136
00181      COPY DPWS045.                                                MBKCS136
00182                                                                   MBKCS136
00183  EJECT                                                            MBKCS136
00184 ***************************************************************** MBKCS136
00185 *    SYSTEM MESSAGES RECORD LAYOUT                                MBKCS136
00186 ***************************************************************** MBKCS136
00187                                                                   MBKCS136
00188      COPY DPRD507I.                                               MBKCS136
00189                                                                   MBKCS136
00190  EJECT                                                            MBKCS136
00191 ***************************************************************** MBKCS136
00192 *    ABEND PROCESSING COPYBOOK.                                   MBKCS136
00193 ***************************************************************** MBKCS136
00194                                                                   MBKCS136
00195      COPY DPWS013.                                                MBKCS136
00196                                                                   MBKCS136
00197  EJECT                                                            MBKCS136
00198 ***************************************************************** MBKCS136
00199 *    LINKAGE SECTION                                              MBKCS136
00200 ***************************************************************** MBKCS136
00201 *                                                                 MBKCS136
00202  LINKAGE SECTION.                                                 MBKCS136
00203                                                                   MBKCS136
00204  01  DFHCOMMAREA.                                                 MBKCS136
00205      05  FILLER                         OCCURS  1 TO 4072 TIMES   MBKCS136
00206                                         DEPENDING ON EIBCALEN.    MBKCS136
00207          10  FILLER                     PIC X.                    MBKCS136
00208                                                                   MBKCS136
00209  EJECT                                                            MBKCS136
00210 *                                                                 MBKCS136
00211 *                                                                 MBKCS136
00212 *                                                                 MBKCS136
00213  PROCEDURE DIVISION.                                              MBKCS136
00214                                                                   MBKCS136
00215  A0000-MAIN-MODULE.                                               MBKCS136
00216 ******************************************************************MBKCS136
00217 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           MBKCS136
00218 *                                                                 MBKCS136
00219 *    'DP020-SRC-RETURN-W-TRAN-ID'                                 MBKCS136
00220 *    WILL BE SET TO A VALUE OF TRUE IF THIS PROGRAM WAS SENT      MBKCS136
00221 *    A STANDARD COMMAREA. IF IT IS NOT SET TRUE (MEANING OTHER    MBKCS136
00222 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM) THIS      MBKCS136
00223 *    PROGRAM WILL RETURN TO CICS.                                 MBKCS136
00224 ******************************************************************MBKCS136
00225                                                                   MBKCS136
00226      PERFORM DP015-0000-CHECK-COMMAREA.                           MBKCS136
00227                                                                   MBKCS136
00228      IF DP020-SRC-RETURN-W-TRAN-ID                                MBKCS136
00229          PERFORM B1000-CONTROL-PROCESSING                         MBKCS136
00230      END-IF.                                                      MBKCS136
00231                                                                   MBKCS136
00232      PERFORM DP003-0000-RETURN-TO-CICS.                           MBKCS136
00233                                                                   MBKCS136
00234  EJECT                                                            MBKCS136
00235                                                                   MBKCS136
00236  B1000-CONTROL-PROCESSING.                                        MBKCS136
00237 ******************************************************************MBKCS136
00238 *    EVAULATE THE ACTION TO BE TAKEN WITHIN THIS MODULE.          MBKCS136
00239 *                                                                 MBKCS136
00240 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHAT THE NEXT     MBKCS136
00241 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    MBKCS136
00242 *                                                                 MBKCS136
00243 ******************************************************************MBKCS136
00244                                                                   MBKCS136
00245      EVALUATE TRUE                                                MBKCS136
00246                                                                   MBKCS136
00247          WHEN DP020-NEXT-ACT-INITIAL                              MBKCS136
00248              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS136
00249              PERFORM S1000-INITIALIZE-COMMAREA                    MBKCS136
00250              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00251                                                                   MBKCS136
00252          WHEN DP020-NEXT-ACT-READ-MAP                             MBKCS136
00253              SET PS-SEND-DATA-ONLY-FRSET TO TRUE                  MBKCS136
00254              PERFORM C1000-PROCESS-PANEL                          MBKCS136
00255                                                                   MBKCS136
00256          WHEN DP020-NEXT-ACT-RETURN                               MBKCS136
00257              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS136
00258              PERFORM S2000-UPDATE-INTER-APPL                      MBKCS136
00259              PERFORM D2000-EDIT-DATA-IN-COMMAREA                     CL**2
00260              IF DP020-MSG-NUMBER = PC-TSYMSG-00007                   CL**2
00261                  SET PS-NO-ERROR TO TRUE                             CL**2
00262                  MOVE ZEROES TO DP020-MSG-NUMBER                     CL**2
00263              END-IF                                                  CL**2
00264              PERFORM S6000-MOVE-COMMAREA-TO-SCREEN                   CL**2
00265              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00266                                                                   MBKCS136
00267          WHEN DP020-NEXT-ACT-ERROR                                MBKCS136
00268 ******************************************************************MBKCS136
00269 *    THE FOLLOWING CODE HAS BEEN INSERTED IN ORDER TO PRESERVE    MBKCS136
00270 *    THE ERROR MESSAGE BEING RETURNED FROM THE NAVIGATOR. SINCE   MBKCS136
00271 *    THE ERROR BEING RETURNED WILL TYPICALLY BE A SECURITY        MBKCS136
00272 *    VIOLATION OR THE UNAVAILABILITY OF THE REQUESTED TRANSACTION MBKCS136
00273 *    THIS CODE ALSO CHECKS TO SEE HOW THE NAVIGATION REQUEST WAS  MBKCS136
00274 *    INVOKED. IF 'DP020-NAV-MNEMONIC' FIELD IN THE STANDARD       MBKCS136
00275 *    COMMAREA IS SPACES, THE NAVIGATION REQUEST WAS VIA A FUNCTIONMBKCS136
00276 *    KEY. IF NOT, IT WAS VIA A COMMAND ENTERED IN THE COMMAND LINEMBKCS136
00277 *    COMMAND FIELD AND THIS LOGIC SETS THE ATTRIBUTE FOR THIS FIELMBKCS136
00278 *    TO RED, REVERSE VIDEO.                                       MBKCS136
00279 ******************************************************************MBKCS136
00280              MOVE DP020-MSG-NUMBER-X  TO PV-MSG-NUMBER-X          MBKCS136
00281                                                                   MBKCS136
00282              IF DP020-NAV-MNEMONIC = SPACES                       MBKCS136
00283                  MOVE SPACES              TO ANAVCMDO             MBKCS136
00284                  MOVE DP015-GREEN         TO ANAVCMDC             MBKCS136
00285                  MOVE DP015-UNDERLINE     TO ANAVCMDH             MBKCS136
00286              ELSE                                                 MBKCS136
00287                  MOVE DP015-RED           TO ANAVCMDC             MBKCS136
00288                  MOVE DP015-REVERSE       TO ANAVCMDH             MBKCS136
00289              END-IF                                               MBKCS136
00290                                                                   MBKCS136
00291              MOVE LOW-VALUES TO DP020-INTER-APPL-DATA-AREA        MBKCS136
00292                                                                   MBKCS136
00293              SET PS-ERROR                 TO TRUE                 MBKCS136
00294              SET PS-SEND-DATA-ONLY-FRSET  TO TRUE                 MBKCS136
00295                                                                   MBKCS136
00296              MOVE -1                      TO ANAVCMDL             MBKCS136
00297              PERFORM D2000-EDIT-DATA-IN-COMMAREA                     CL**2
00298              PERFORM S6000-MOVE-COMMAREA-TO-SCREEN                   CL**2
00299              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00300                                                                   MBKCS136
00301          WHEN OTHER                                               MBKCS136
00302              SET DP013-LOGIC-ABEND        TO TRUE                 MBKCS136
00303              SET DP013-NO-ROLLBACK        TO TRUE                 MBKCS136
00304                                                                   MBKCS136
00305              MOVE 'B1000-CONTROL-PROCESSING' TO                   MBKCS136
00306                   DP013-PARAGRAPH                                 MBKCS136
00307                                                                   MBKCS136
00308              MOVE PM-PROGRAM-MESSAGE-1      TO                    MBKCS136
00309                   DP013-MESSAGE-TEXT(1)                           MBKCS136
00310                                                                   MBKCS136
00311              PERFORM DP013-0000-PROCESS-ABEND                     MBKCS136
00312                                                                   MBKCS136
00313      END-EVALUATE.                                                MBKCS136
00314                                                                   MBKCS136
00315  EJECT                                                            MBKCS136
00316                                                                   MBKCS136
00317  C1000-PROCESS-PANEL.                                             MBKCS136
00318 ******************************************************************MBKCS136
00319 *    RECEIVE THE MAP                                              MBKCS136
00320 *    PERFORM APPROPRIATE PARAGRAPH BASED ON THE                   MBKCS136
00321 *       FUNCTION KEY SELECTED                                     MBKCS136
00322 *                                                                 MBKCS136
00323 ******************************************************************MBKCS136
00324                                                                   MBKCS136
00325      PERFORM DP007-0000-GET-PANEL.                                MBKCS136
00326                                                                   MBKCS136
00327      IF (DP020-SRC-AID NOT = DP016-PF5)  AND                      MBKCS136
00328         (DP020-SRC-AID NOT = DP016-CLEAR)                         MBKCS136
00329          PERFORM D1000-MOVE-SCREEN-TO-COMMAREA                    MBKCS136
00330      END-IF.                                                      MBKCS136
00331                                                                   MBKCS136
00332      EVALUATE TRUE                                                MBKCS136
00333                                                                   MBKCS136
00334          WHEN DP020-SRC-AID = DP016-CLEAR                         MBKCS136
00335              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS136
00336              PERFORM D2000-EDIT-DATA-IN-COMMAREA                  MBKCS136
00337              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00338                                                                   MBKCS136
00339          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     MBKCS136
00340              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS136
00341              INITIALIZE ANAVCMDO ANAVKEYO                         MBKCS136
00342              MOVE SPACES TO DP020-NAV-KEY-DATA                    MBKCS136
00343              PERFORM S1000-INITIALIZE-COMMAREA                    MBKCS136
00344              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00345                                                                   MBKCS136
00346          WHEN DP020-SRC-AID = DP016-PA2                           MBKCS136
00347              PERFORM DP009-0000-LOAD-FK-LINES                     MBKCS136
00348              PERFORM DP017-0000-GET-CURR-DATE-TIME                MBKCS136
00349              SET PS-SEND-DATA-ONLY TO TRUE                        MBKCS136
00350              MOVE -1               TO ANAVCMDL                    MBKCS136
00351              PERFORM DP008-0000-WRITE-PANEL                       MBKCS136
00352                                                                   MBKCS136
00353          WHEN DP020-SRC-AID = DP016-ENTER                         MBKCS136
00354              IF (DP020-NAV-MNEMONIC = SPACES)     OR              MBKCS136
00355                 (DP020-NAV-MNEMONIC = LOW-VALUES)                 MBKCS136
00356                  PERFORM D2000-EDIT-DATA-IN-COMMAREA              MBKCS136
00357                  PERFORM S6000-MOVE-COMMAREA-TO-SCREEN            MBKCS136
00358                  PERFORM S5000-DISPLAY-PANEL                      MBKCS136
00359              ELSE                                                 MBKCS136
00360                  MOVE MB007-INTER-APPL-COMMAREA                      CL**2
00361                    TO MB136-SAVE-COMMAREA                            CL**2
00362                  MOVE LOW-VALUES TO DP020-INTER-APPL-DATA-AREA       CL**2
00363                  PERFORM DP015-1000-START-NAVIGATOR                  CL**2
00364              END-IF                                                  CL**2
00365                                                                   MBKCS136
00366          WHEN DP020-FK-RETURN(DP020-SRC-AID)                         CL**2
00367              PERFORM S3000-SETUP-INTER-APPL                       MBKCS136
00368              PERFORM DP015-1000-START-NAVIGATOR                   MBKCS136
00369                                                                   MBKCS136
00370          WHEN (DP020-SRC-AID = DP016-PF4) OR                         CL**2
00371               (DP020-SRC-AID = DP016-PF1) OR                         CL**2
00372               (DP020-SRC-AID = DP016-PF12)                           CL**2
00373              PERFORM S3000-SETUP-INTER-APPL                          CL**2
00374              PERFORM DP015-1000-START-NAVIGATOR                      CL**2
00375                                                                      CL**2
00376          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID) OR                  CL**2
00377               DP020-FK-EDIT-GOTO(DP020-SRC-AID)                      CL**2
00378                                                                      CL**2
00379              PERFORM D2000-EDIT-DATA-IN-COMMAREA                  MBKCS136
00380                                                                   MBKCS136
00381              IF DP020-SRC-AID = DP016-PF13 OR                        CL**2
00382 *                 ** WORKER ADD (PF13) **                            CL**2
00383                 DP020-SRC-AID = DP016-PF15 OR                        CL**2
00384 *                 ** WORKER UPDATE (PF15) **                         CL**2
00385                 DP020-SRC-AID = DP016-PF16                           CL**2
00386 *                 ** WORKER DELETE (PF16) **                         CL**2
00387                                                                      CL**2
00388                  PERFORM D3000-ADD-DEL-UPDT-EDITS                    CL**2
00389                                                                      CL**2
00390              END-IF                                               MBKCS136
00391                                                                   MBKCS136
00392              IF DP020-SRC-AID = DP016-PF20                        MBKCS136
00393 *            ** WORKER LIST (PF20)   **                           MBKCS136
00394 *            ** ALSO ACTS AS INQUIRY **                           MBKCS136
00395                  MOVE 'L'  TO  MB007-ACTION                          CL**2
00396                  PERFORM D4000-INQ-EDITS                             CL**2
00397              END-IF                                               MBKCS136
00398                                                                   MBKCS136
00399              IF PS-NO-ERROR                                       MBKCS136
00400                  PERFORM S3000-SETUP-INTER-APPL                   MBKCS136
00401                  PERFORM DP015-1000-START-NAVIGATOR               MBKCS136
00402              ELSE                                                 MBKCS136
00403                  PERFORM S6000-MOVE-COMMAREA-TO-SCREEN            MBKCS136
00404                  PERFORM S5000-DISPLAY-PANEL                      MBKCS136
00405              END-IF                                               MBKCS136
00406                                                                   MBKCS136
00407          WHEN OTHER                                                  CL**2
00408              PERFORM D2000-EDIT-DATA-IN-COMMAREA                  MBKCS136
00409              MOVE 45             TO DP020-MSG-NUMBER              MBKCS136
00410              SET DP020-MSG-FATAL TO TRUE                          MBKCS136
00411              PERFORM S5000-DISPLAY-PANEL                          MBKCS136
00412                                                                   MBKCS136
00413      END-EVALUATE.                                                MBKCS136
00414                                                                   MBKCS136
00415  EJECT                                                            MBKCS136
00416                                                                   MBKCS136
00417  D1000-MOVE-SCREEN-TO-COMMAREA.                                   MBKCS136
00418 ******************************************************************MBKCS136
00419 *    SET THE APPLICATION COMMAREA WITH EITHER                     MBKCS136
00420 *    THE MAP FIELD (IF DATA ENTERED AT SCREEN)                    MBKCS136
00421 *    OR SPACES                                                    MBKCS136
00422 *                                                                 MBKCS136
00423 ******************************************************************MBKCS136
00424                                                                   MBKCS136
00425      MOVE SPACES                 TO MB007-ACTION.                    CL**2
00426      MOVE SPACES                 TO MB007-FIR-NAME.                  CL**2
00427                                                                   MBKCS136
00428      IF ALNAMEL > 0                                               MBKCS136
00429         MOVE ALNAMEI             TO MB007-LAST-NAME               MBKCS136
00430      ELSE                                                         MBKCS136
00431         IF ALNAMEF = DP015-ERASE-EOF                              MBKCS136
00432            MOVE SPACES           TO MB007-LAST-NAME               MBKCS136
00433         END-IF                                                    MBKCS136
00434      END-IF.                                                      MBKCS136
00435                                                                   MBKCS136
00436      IF AUSERIDL   > 0                                            MBKCS136
00437         MOVE AUSERIDI            TO MB007-USERID                  MBKCS136
00438      ELSE                                                         MBKCS136
00439         IF AUSERIDF = DP015-ERASE-EOF                             MBKCS136
00440            MOVE SPACES           TO MB007-USERID                  MBKCS136
00441         END-IF                                                    MBKCS136
00442      END-IF.                                                      MBKCS136
00443                                                                   MBKCS136
00444  EJECT                                                            MBKCS136
00445                                                                   MBKCS136
00446  D2000-EDIT-DATA-IN-COMMAREA.                                     MBKCS136
00447 ******************************************************************MBKCS136
00448 *    THIS ROUTINE IS CALLED WHENEVER A USER PRESSES THE           MBKCS136
00449 *                                                                 MBKCS136
00450 *    ENTER KEY                                                    MBKCS136
00451 *    THE ADD FUNCTION KEY (PF13 - MBAWORKR)                       MBKCS136
00452 *    THE UPDATE FUNCTION KEY (PF15 - MBUWORKR)                       CL**2
00453 *    THE DELETE FUNCTION KEY (PF16 - MBDWORKR)                    MBKCS136
00454 *    LIST FUNCTION KEY (PF20 - MBLWORKR)                          MBKCS136
00455 *    ( NOTE: THE INQUIRE FUNCTION, PF14, NOT CURRENTLY AVAILABLE) MBKCS136
00456 *                                                                 MBKCS136
00457 *    THIS PARAGRAPH WILL USE THE ALPHA NUMERIC EDIT ROUTINE       MBKCS136
00458 *    DPPD019 TO VALIDATE THE ENTRIES ON THE SCREEN:               MBKCS136
00459 *                                                                 MBKCS136
00460 *      VALIDATE THE WORKER LAST NAME ENTERED (IF ANY)             MBKCS136
00461 *         VALID IF THE FIELD CONTAINS:                            MBKCS136
00462 *           '*' SIGNIFYING SELECT ALL ROWS                        MBKCS136
00463 *           SPACES, SIGNIFYING THAT A WORKER USERID WILL BE USED  MBKCS136
00464 *           (') SINGLE QUOTE FOR ACCENTED LAST NAMES              MBKCS136
00465 *           (-) HYPHEN FOR JOINED/MULTIPLE LAST NAMES             MBKCS136
00466 *           '*' FOLLOWING A CHARACTER STRING, WILD CARD, MEANS TO MBKCS136
00467 *                 SELECT ALL NAMES BEGINING LIKE THE STRING       MBKCS136
00468 *      VALIDATE THE VALUE ENTERED IN THE USERID FIELD (IF ANY)    MBKCS136
00469 *         VALID IF THE FIELD CONTAINS:                            MBKCS136
00470 *           '*' SIGNIFYING SELECT ALL ROWS                        MBKCS136
00471 *           'XXXXXXX ' A 7 OR 8 CHARACTER STRING WITH NO IMBEDDED MBKCS136
00472 *                 SPACES CONTAINING LETTERS OR NUMBERS            MBKCS136
00473 *                                                                 MBKCS136
00474 ******************************************************************MBKCS136
00475                                                                   MBKCS136
00476      IF NOT DP020-NEXT-ACT-ERROR                                  MBKCS136
00477         SET PS-NO-ERROR TO TRUE                                   MBKCS136
00478         MOVE DP015-GREEN     TO ANAVCMDC                          MBKCS136
00479                                 ANAVKEYC                          MBKCS136
00480         MOVE DP015-UNDERLINE TO ANAVCMDH                          MBKCS136
00481                                 ANAVKEYH                          MBKCS136
00482      END-IF.                                                      MBKCS136
00483                                                                   MBKCS136
00484 *    ***********************************************************  MBKCS136
00485 *    * INITIALIZE THE ENTERABLE FIELDS TO GREEN AND UNDERLINED *  MBKCS136
00486 *    ***********************************************************  MBKCS136
00487                                                                   MBKCS136
00488      MOVE DP015-GREEN     TO ALNAMEC                              MBKCS136
00489                              AUSERIDC.                            MBKCS136
00490                                                                   MBKCS136
00491      MOVE DP015-UNDERLINE TO ALNAMEH                              MBKCS136
00492                              AUSERIDH.                            MBKCS136
00493                                                                   MBKCS136
00494 *    ***********************************************************  MBKCS136
00495 *    * EDIT LAST NAME FOR SPACES, ASTERISKS, ETC.              *  MBKCS136
00496 *    ***********************************************************  MBKCS136
00497      MOVE 'N' TO MB007-LNAME-ERROR-SW.                            MBKCS136
00498                                                                   MBKCS136
00499      MOVE  +18             TO DP019-FLD-LENGTH.                   MBKCS136
00500      MOVE  MB007-LAST-NAME TO DP019-IN-FLD.                       MBKCS136
00501      PERFORM DP019-ALPHA-NUMERIC-EDIT.                            MBKCS136
00502      MOVE  SPACES          TO MB007-LAST-NAME.                    MBKCS136
00503      MOVE  DP019-OUT-FLD   TO MB007-LAST-NAME.                    MBKCS136
00504                                                                   MBKCS136
00505      IF DP019-ERROR                                               MBKCS136
00506 *        ** INVALID WORKER LAST NAME ENTERED **                   MBKCS136
00507          MOVE 'Y' TO MB007-LNAME-ERROR-SW                         MBKCS136
00508      END-IF.                                                      MBKCS136
00509                                                                   MBKCS136
00510 *    ***********************************************************  MBKCS136
00511 *    * EDIT USER-ID FOR SPACES, ASTERISKS, ETC.                *  MBKCS136
00512 *    ***********************************************************  MBKCS136
00513      MOVE 'N' TO MB007-USERID-ERROR-SW.                           MBKCS136
00514                                                                   MBKCS136
00515      MOVE  +8              TO DP019-FLD-LENGTH.                   MBKCS136
00516      MOVE  MB007-USERID    TO DP019-IN-FLD.                       MBKCS136
00517      PERFORM DP019-ALPHA-NUMERIC-EDIT.                            MBKCS136
00518      MOVE  SPACES          TO MB007-USERID.                       MBKCS136
00519      MOVE  DP019-OUT-FLD   TO MB007-USERID.                       MBKCS136
00520                                                                   MBKCS136
00521      IF DP019-ERROR                                               MBKCS136
00522 *        ** INVALID WORKER USERID ENTERED **                      MBKCS136
00523          MOVE 'Y' TO MB007-USERID-ERROR-SW                        MBKCS136
00524      END-IF.                                                      MBKCS136
00525                                                                   MBKCS136
00526      IF MB007-LNAME-ERROR OR                                      MBKCS136
00527         MB007-USERID-ERROR                                        MBKCS136
00528          CONTINUE                                                 MBKCS136
00529      ELSE                                                         MBKCS136
00530          PERFORM D2100-RELATIONSHIP-EDITS                         MBKCS136
00531      END-IF.                                                      MBKCS136
00532                                                                   MBKCS136
00533 *    ***********************************************************  MBKCS136
00534 *    * ASSESS EDIT ERRORS ENCOUNTERED                          *  MBKCS136
00535 *    ***********************************************************  MBKCS136
00536                                                                   MBKCS136
00537      IF MB007-USERID-ERROR                                        MBKCS136
00538          MOVE DP015-RED             TO AUSERIDC                   MBKCS136
00539          MOVE DP015-REVERSE         TO AUSERIDH                   MBKCS136
00540          MOVE -1                    TO AUSERIDL                   MBKCS136
00541      END-IF.                                                      MBKCS136
00542                                                                   MBKCS136
00543      IF MB007-LNAME-ERROR                                         MBKCS136
00544          MOVE DP015-RED             TO ALNAMEC                    MBKCS136
00545          MOVE DP015-REVERSE         TO ALNAMEH                    MBKCS136
00546          MOVE -1                    TO ALNAMEL                    MBKCS136
00547      END-IF.                                                      MBKCS136
00548                                                                   MBKCS136
00549      IF MB007-USERID-ERROR OR MB007-LNAME-ERROR                   MBKCS136
00550          IF  DP020-MSG-NUMBER = PC-TSYMSG-00007                   MBKCS136
00551               CONTINUE                                            MBKCS136
00552          ELSE                                                     MBKCS136
00553              MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER           MBKCS136
00554          END-IF                                                   MBKCS136
00555          SET DP020-MSG-FATAL        TO TRUE                       MBKCS136
00556          SET PS-ERROR               TO TRUE                       MBKCS136
00557      END-IF.                                                      MBKCS136
00558                                                                   MBKCS136
00559  EJECT                                                            MBKCS136
00560                                                                   MBKCS136
00561  D2100-RELATIONSHIP-EDITS.                                        MBKCS136
00562 ******************************************************************MBKCS136
00563 *    PERFORM FIELD RELATIONSHIP EDITS                            *MBKCS136
00564 ******************************************************************MBKCS136
00565                                                                   MBKCS136
00566      IF MB007-LNAME-1ST = SPACE                                   MBKCS136
00567          IF MB007-USERID-1ST = SPACE                              MBKCS136
00568 *         ** EITHER A LAST NAME (OR PARTIAL LAST NAME STRING) OR     CL**2
00569 *         ** A USERID MUST BE ENTERED, UNLESS THE ADD                CL**2
00570 *         ** OR LIST FUNCTION (PF13 OR PF20) ARE SELECTED            CL**2
00571              IF DP020-SRC-AID = DP016-PF13 OR                        CL**2
00572                 DP020-SRC-AID = DP016-PF20                           CL**2
00573                  SET MB007-NO-USERID-ERROR TO TRUE                   CL**2
00574                  SET MB007-NO-LNAME-ERROR  TO TRUE                   CL**2
00575              ELSE                                                    CL**2
00576                  MOVE PC-TSYMSG-00007 TO DP020-MSG-NUMBER            CL**2
00577                  SET MB007-USERID-ERROR    TO TRUE                   CL**2
00578                  SET MB007-LNAME-ERROR     TO TRUE                   CL**2
00579          END-IF                                                   MBKCS136
00580      END-IF.                                                      MBKCS136
00581                                                                   MBKCS136
00582      IF  DP020-MSG-NUMBER = PC-TSYMSG-00007                          CL**2
00583           CONTINUE                                                   CL**2
00584      ELSE                                                            CL**2
00585           IF (MB007-USERID = SPACES) OR                              CL**2
00586              (MB007-USERID-1ST = '*')                                CL**2
00587                MOVE '********'           TO MB007-USERID             CL**2
00588           END-IF                                                     CL**2
00589           IF (MB007-LNAME-1ST = '*')                                 CL**2
00590                MOVE '******************' TO MB007-LAST-NAME          CL**2
00591           END-IF                                                     CL**2
00592      END-IF.                                                      MBKCS136
00593                                                                   MBKCS136
00594  D3000-ADD-DEL-UPDT-EDITS.                                           CL**2
00595 ******************************************************************MBKCS136
00596 *    THIS ROUTINE IS CALLED WHENEVER A USER PRESSES THE           MBKCS136
00597 *    ADD FUNCTION KEY (PF13 - MBAWORKR) OR                           CL**2
00598 *    DELETE FUNCTION KEY (PF16 - MBDWORKR) OR                        CL**2
00599 *    UPDATE FUNCTION KEY (PF15 - MBUWORKR)                           CL**2
00600 *                                                                    CL**2
00601 *    -  NO LAST NAME ENTRY ALLOWED FOR THE ADD/DELETE FUNCTIONS      CL**2
00602 *    -  USER ID IS OPTIONAL FOR THIS FUNCTION                     MBKCS136
00603 *                                                                 MBKCS136
00604 ******************************************************************MBKCS136
00605                                                                   MBKCS136
00606      IF MB007-LAST-NAME = SPACES OR                                  CL**2
00607         MB007-LNAME-1ST = '*'                                        CL**2
00608 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00609          MOVE SPACES             TO MB007-LAST-NAME                  CL**2
00610      ELSE                                                            CL**2
00611 *        ** MUST BE BLANK FOR ADD, DELETE, UPDATE FUNCTIONS **       CL**2
00612          MOVE MB007-LAST-NAME    TO ALNAMEO                          CL**2
00613          MOVE DP015-RED          TO ALNAMEC                          CL**2
00614          MOVE DP015-REVERSE      TO ALNAMEH                          CL**2
00615          MOVE -1                 TO ALNAMEL                          CL**2
00616          SET PS-ERROR            TO TRUE                             CL**2
00617          MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                 CL**2
00618      END-IF.                                                         CL**2
00619                                                                   MBKCS136
00620      IF MB007-USERID = SPACES OR                                     CL**2
00621         MB007-USERID-1ST = '*'                                       CL**2
00622 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00623              MOVE SPACES         TO MB007-USERID                     CL**2
00624      ELSE                                                            CL**2
00625          MOVE 0 TO PV-INSPECT-CTR                                    CL**2
00626          INSPECT MB007-USERID                                        CL**2
00627                  TALLYING PV-INSPECT-CTR FOR ALL '*'                 CL**2
00628          IF PV-INSPECT-CTR > 0                                       CL**2
00629              MOVE MB007-USERID       TO AUSERIDO                     CL**2
00630              MOVE DP015-RED          TO AUSERIDC                     CL**2
00631              MOVE DP015-REVERSE      TO AUSERIDH                     CL**2
00632              MOVE -1                 TO AUSERIDL                     CL**2
00633              SET PS-ERROR            TO TRUE                         CL**2
00634              MOVE PC-TSYMSG-00137    TO DP020-MSG-NUMBER             CL**2
00635 *            ** WILD CARD '*' IS INVALID FOR REQUESTED FUNCTION **   CL**2
00636          END-IF                                                      CL**2
00637      END-IF.                                                      MBKCS136
00638                                                                   MBKCS136
00639      IF MB007-LAST-NAME = SPACES AND                                 CL**2
00640         MB007-USERID = SPACES                                        CL**2
00641          IF DP020-SRC-AID = DP016-PF13                               CL**2
00642 *           ** WORKER ADD (PF13) **                                  CL**2
00643              SET MB007-ADD TO TRUE                                   CL**2
00644              SET PS-NO-ERROR TO TRUE                                 CL**2
00645          END-IF                                                      CL**2
00646          IF DP020-SRC-AID = DP016-PF15 OR                            CL**2
00647 *           ** WORKER UPDATE (PF15) **                               CL**2
00648             DP020-SRC-AID = DP016-PF16                               CL**2
00649 *           ** WORKER DELETE (PF16) **                               CL**2
00650              MOVE MB007-LAST-NAME    TO ALNAMEO                      CL**2
00651              MOVE MB007-USERID       TO AUSERIDO                     CL**2
00652              MOVE DP015-RED          TO AUSERIDC                     CL**2
00653              MOVE DP015-REVERSE      TO AUSERIDH                     CL**2
00654              MOVE -1                 TO AUSERIDL                     CL**2
00655              SET PS-ERROR            TO TRUE                         CL**2
00656              MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER             CL**2
00657          END-IF                                                      CL**2
00658      END-IF.                                                         CL**2
00659                                                                      CL**2
00660      IF PS-NO-ERROR                                                  CL**2
00661          IF DP020-SRC-AID = DP016-PF13                               CL**2
00662 *           ** WORKER ADD (PF13) **                                  CL**2
00663              SET MB007-ADD TO TRUE                                   CL**2
00664          END-IF                                                      CL**2
00665          IF DP020-SRC-AID = DP016-PF15                               CL**2
00666 *           ** WORKER UPDATE (PF15) **                               CL**2
00667              SET MB007-UPDATE TO TRUE                                CL**2
00668          END-IF                                                      CL**2
00669          IF DP020-SRC-AID = DP016-PF16                               CL**2
00670 *           ** WORKER DELETE (PF16) **                               CL**2
00671              SET MB007-DELETE TO TRUE                                CL**2
00672          END-IF                                                      CL**2
00673      END-IF.                                                      MBKCS136
00674                                                                      CL**2
00675      IF PS-ERROR                                                     CL**2
00676          SET DP020-MSG-FATAL  TO TRUE                                CL**2
00677      END-IF.                                                         CL**2
00678                                                                   MBKCS136
00679      EJECT                                                        MBKCS136
00680                                                                   MBKCS136
00681  D4000-INQ-EDITS.                                                    CL**2
00682 ******************************************************************MBKCS136
00683 *    THIS ROUTINE IS CALLED WHENEVER A USER PRESSES THE           MBKCS136
00684 *    LIST FUNCTION KEY (PF20 - MBLWORKR)                          MBKCS136
00685 *                                                                 MBKCS136
00686 *    -  LAST NAME IS OPTIONAL FOR THE LIST FUNCTION               MBKCS136
00687 *    -  USER ID IS OPTIONAL FOR THIS FUNCTION                     MBKCS136
00688 *                                                                 MBKCS136
00689 ******************************************************************MBKCS136
00690                                                                   MBKCS136
00691      IF MB007-LAST-NAME = SPACES OR                                  CL**2
00692         MB007-LNAME-1ST = '*'                                        CL**2
00693 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00694          MOVE '******************' TO MB007-LAST-NAME                CL**2
00695          MOVE MB007-LAST-NAME      TO ALNAMEO                        CL**2
00696      END-IF.                                                      MBKCS136
00697                                                                      CL**2
00698      IF MB007-USERID = SPACES OR                                     CL**2
00699         MB007-USERID-1ST = '*'                                       CL**2
00700 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00701              MOVE '********'         TO MB007-USERID                 CL**2
00702              MOVE MB007-USERID       TO AUSERIDO                     CL**2
00703      END-IF.                                                         CL**2
00704                                                                   MBKCS136
00705      IF PS-ERROR                                                     CL**2
00706          SET DP020-MSG-FATAL TO TRUE                                 CL**2
00707      ELSE                                                            CL**2
00708          MOVE 'I'  TO  MB007-ACTION                                  CL**2
00709      END-IF.                                                         CL**2
00710                                                                      CL**2
00711      EJECT                                                        MBKCS136
00712                                                                   MBKCS136
00713  S1000-INITIALIZE-COMMAREA.                                       MBKCS136
00714 ******************************************************************MBKCS136
00715 *    INITIALIZE THE INTER APPLICATION COMMAREA                    MBKCS136
00716 *    AND THE APPLICATION SPECIFIC COMMAREA.                       MBKCS136
00717 ******************************************************************MBKCS136
00718                                                                   MBKCS136
00719      INITIALIZE DP020-INTER-APPL-DATA-AREA.                          CL**2
00720                                                                      CL**2
00721 *   ** INITIALIZE THE INTER-APPLICATION COMMAREA  **              MBKCS136
00722      INITIALIZE MB007-INTER-APPL-COMMAREA.                           CL**2
00723                                                                      CL**2
00724 *    MOVE LOW-VALUES            TO DP020-INTER-APPL-DATA-AREA.       CL**2
00725                                                                      CL**2
00726 *    MOVE SPACES                TO MB007-ACTION                      CL**2
00727 *                                  MB007-FIR-NAME                    CL**2
00728 *                                  MB007-LAST-NAME                   CL**2
00729 *                                  MB007-USERID.                     CL**2
00730 *                                                                    CL**2
00731 *    MOVE ZEROS           TO MB007-NUMBER-OF-SELECTED-ITEMS          CL**2
00732 *                            MB007-NUMBER-PROCESSED-ITEMS.           CL**2
00733                                                                      CL**2
00734  EJECT                                                            MBKCS136
00735                                                                   MBKCS136
00736  S2000-UPDATE-INTER-APPL.                                         MBKCS136
00737 ******************************************************************MBKCS136
00738 *    WHEN RETURNING FROM ANOTHER TRANSACTION, SAVE THE FIELDS     MBKCS136
00739 *    PASSED IN THE INTER APPLICATION COMMAREA, ONLY IF THE        MBKCS136
00740 *    INTER APPLICATION FORMAT CODE IS VALID FOR THIS TRANSACTION. MBKCS136
00741 ******************************************************************MBKCS136
00742                                                                   MBKCS136
00743      IF DP020-MB-WORKER-LIST OR                                   MBKCS136
00744         DP020-MB-WORKER-MAINT                                     MBKCS136
00745            MOVE MB136-SAVE-COMMAREA TO MB007-INTER-APPL-COMMAREA     CL**2
00746      ELSE                                                         MBKCS136
00747            INITIALIZE MB007-INTER-APPL-COMMAREA                   MBKCS136
00748            INITIALIZE APPLICATION-SPECIFIC-COMMAREA               MBKCS136
00749      END-IF.                                                      MBKCS136
00750                                                                   MBKCS136
00751  EJECT                                                            MBKCS136
00752                                                                   MBKCS136
00753  S3000-SETUP-INTER-APPL.                                          MBKCS136
00754 ******************************************************************MBKCS136
00755 *    SETUP THE INTER APPLICATION COMMAREA WITH DATA FROM THE      MBKCS136
00756 *    SPECIFIC COMMAREA AND SET THE INTER APPLICATION FORMAT CODE. MBKCS136
00757 ******************************************************************MBKCS136
00758                                                                   MBKCS136
00759      SET DP020-MB-WORKER-SELECT TO TRUE.                          MBKCS136
00760                                                                   MBKCS136
00761      MOVE MB007-INTER-APPL-COMMAREA TO MB136-SAVE-COMMAREA.          CL**2
00762                                                                      CL**2
00763  EJECT                                                            MBKCS136
00764                                                                   MBKCS136
00765  S5000-DISPLAY-PANEL.                                             MBKCS136
00766 ******************************************************************MBKCS136
00767 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.       MBKCS136
00768 ******************************************************************MBKCS136
00769                                                                   MBKCS136
00770      PERFORM DP017-0000-GET-CURR-DATE-TIME.                       MBKCS136
00771      MOVE DP017-EDITED-TIME-HMS  TO  ATIMEO.                         CL**2
00772                                                                   MBKCS136
00773      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS136
00774          PERFORM DP002-0000-FORMAT-TITLE-LINES                    MBKCS136
00775 *        ** ALL TITLE LINE FIELDS ARE FORMATED BY DPPD002 **      MBKCS136
00776          PERFORM S6000-MOVE-COMMAREA-TO-SCREEN                    MBKCS136
00777      END-IF.                                                      MBKCS136
00778                                                                   MBKCS136
00779      PERFORM DP005-0000-FORMAT-MSG-LINES.                         MBKCS136
00780                                                                   MBKCS136
00781      IF (DP020-SRC-AID = DP016-PA2) OR                            MBKCS136
00782         (PS-SEND-MAP-N-DATA-FRSET)                                MBKCS136
00783          PERFORM DP009-0000-LOAD-FK-LINES                         MBKCS136
00784      END-IF.                                                      MBKCS136
00785                                                                   MBKCS136
00786      IF PS-NO-ERROR                                               MBKCS136
00787          MOVE -1 TO ALNAMEL                                       MBKCS136
00788      END-IF.                                                      MBKCS136
00789                                                                   MBKCS136
00790      PERFORM DP008-0000-WRITE-PANEL.                              MBKCS136
00791                                                                   MBKCS136
00792  EJECT                                                            MBKCS136
00793                                                                   MBKCS136
00794  S6000-MOVE-COMMAREA-TO-SCREEN.                                   MBKCS136
00795 ******************************************************************MBKCS136
00796 *    MOVE THE FIELDS FROM THE COMMAREA TO THE SCREEN.             MBKCS136
00797 ******************************************************************MBKCS136
00798                                                                   MBKCS136
00799      MOVE '03'                 TO AOPERCOO.                       MBKCS136
00800                                                                   MBKCS136
00801      IF MB007-LNAME-1ST = '*'                                        CL**2
00802 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00803          MOVE SPACES               TO ALNAMEO                        CL**2
00804      ELSE                                                            CL**2
00805          MOVE MB007-LAST-NAME      TO ALNAMEO                        CL**2
00806      END-IF.                                                         CL**2
00807                                                                      CL**2
00808      IF MB007-USERID-1ST = '*'                                       CL**2
00809 *        ** NOTE: '*' IS SET FOR BLANK FIELDS DURING COMMAREA EDIT   CL**2
00810          MOVE SPACES               TO AUSERIDO                       CL**2
00811      ELSE                                                            CL**2
00812          MOVE MB007-USERID         TO AUSERIDO                       CL**2
00813      END-IF.                                                         CL**2
00814                                                                   MBKCS136
00815      IF PS-NO-ERROR                                               MBKCS136
00816          MOVE DP015-GREEN      TO ALNAMEC                         MBKCS136
00817                                   AUSERIDC                        MBKCS136
00818          MOVE DP015-UNDERLINE  TO ALNAMEH                            CL**2
00819                                   AUSERIDH                           CL**2
00820      END-IF.                                                      MBKCS136
00821                                                                   MBKCS136
00822  EJECT                                                            MBKCS136
00823 ******************************************************************MBKCS136
00824 *    SCREEN HEADER PROCESSING MODULE                              MBKCS136
00825 ******************************************************************MBKCS136
00826                                                                   MBKCS136
00827      COPY DPPD002.                                                MBKCS136
00828  EJECT                                                            MBKCS136
00829 ******************************************************************MBKCS136
00830 *    RETURN TO CICS MODULE                                        MBKCS136
00831 ******************************************************************MBKCS136
00832                                                                   MBKCS136
00833      COPY DPPD003.                                                MBKCS136
00834  EJECT                                                            MBKCS136
00835 ******************************************************************MBKCS136
00836 *    FORMAT MESSAGE LINES MODULE                                  MBKCS136
00837 ******************************************************************MBKCS136
00838                                                                   MBKCS136
00839      COPY DPPD005.                                                MBKCS136
00840  EJECT                                                            MBKCS136
00841 ******************************************************************MBKCS136
00842 *    GET PANEL MODULE                                             MBKCS136
00843 ******************************************************************MBKCS136
00844                                                                   MBKCS136
00845      COPY DPPD007.                                                MBKCS136
00846  EJECT                                                            MBKCS136
00847 ******************************************************************MBKCS136
00848 *    WRITE PANEL MODULE                                           MBKCS136
00849 ******************************************************************MBKCS136
00850                                                                   MBKCS136
00851      COPY DPPD008.                                                MBKCS136
00852  EJECT                                                            MBKCS136
00853 ******************************************************************MBKCS136
00854 *    TOGGLE THE FUNCTION KEY LINE MODULE                          MBKCS136
00855 ******************************************************************MBKCS136
00856                                                                   MBKCS136
00857      COPY DPPD009.                                                MBKCS136
00858  EJECT                                                            MBKCS136
00859 ******************************************************************MBKCS136
00860 *    ABEND PROCESSOR MODULE                                       MBKCS136
00861 ******************************************************************MBKCS136
00862                                                                   MBKCS136
00863      COPY DPPD013.                                                MBKCS136
00864  EJECT                                                            MBKCS136
00865 ******************************************************************MBKCS136
00866 *    CHECK COMMUNICATION AREA MODULE                              MBKCS136
00867 ******************************************************************MBKCS136
00868                                                                   MBKCS136
00869      COPY DPPD015.                                                MBKCS136
00870  EJECT                                                            MBKCS136
00871 ******************************************************************MBKCS136
00872 *    UPDATE TIME AND DATE MODULE                                  MBKCS136
00873 ******************************************************************MBKCS136
00874                                                                   MBKCS136
00875      COPY DPPD017.                                                MBKCS136
00876  EJECT                                                            MBKCS136
00877 ***************************************************************** MBKCS136
00878 *    ALPHA NUMERIC FIELD EDIT ROUTINE                             MBKCS136
00879 ***************************************************************** MBKCS136
00880                                                                   MBKCS136
00881      COPY DPPD019.                                                   CL**3
00882  EJECT                                                            MBKCS136
