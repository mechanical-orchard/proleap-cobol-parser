       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP340                                          00030000
       AUTHOR.        JON FIEDLER                                       00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  07-31-00.                                         00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP340 - LEDGER DATABASE UPDATE PROGRAM            *00100000
      *                      FOR WEEKLY DEPARTMENT STORE, TWKDPTS      *00110000
      *  THIS PROGRAM WAS MODELED AFTER MLKUP303                       *00120000
W26603******************************************************************00130000
W26603* CHG ID   DATE        DESCRIPTION                               *00140000
W26603* ------   ----------  ----------------------------------------- *00150000
W26603* W26603   04-19-2004  STORE EXPANSION.                          *00160000
W26603*                      EXPANDED WORKING STORAGE VARIABLES.       *00170000
W26603*                      ADDED 'WITH UR' TO DB2 SELECT.            *00180000
W26603*                      INP01 EXPANDED TO 100 BYTES.              *00190000
W26603*                      - RONNA MCDONALD                          *00200000
      ******************************************************************00210000
                                                                        00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.        IBM-3090.                                00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
           SELECT CONDENSED-FILE                                        00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONDENSED-FILE                                               00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
W26603 01  CONDENSED-RECORD            PIC X(100).                      00420000
      *                                                                 00430000
                                                                        00440000
                                                                        00450000
       WORKING-STORAGE SECTION.                                         00460000
                                                                        00470000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00480000
                                                                        00490000
       01  PC-PROGRAM-CONSTANTS.                                        00500000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK390'.    00510000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP340'.  00520000
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.             00530000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00540000
                                                                        00550000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00560000
           05  PE-MSG-01                       PIC X(50) VALUE          00570000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00580000
           05  PE-MSG-02                       PIC X(50) VALUE          00590000
                'ATTEMPT TO UPDATE ROW ON TABLE TWKDPTS FAILED     '.   00600000
           05  PE-MSG-03                       PIC X(50) VALUE          00610000
                'ATTEMPT TO INSERT ROW ON TABLE TWKDPTS FAILED     '.   00620000
           05  PE-MSG-04                       PIC X(50) VALUE          00630000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00640000
           05  PE-MSG-05                       PIC X(50) VALUE          00650000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00660000
           05  PE-MSG-06                       PIC X(50) VALUE          00670000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00680000
           05  PE-MSG-07                       PIC X(50) VALUE          00690000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00700000
           05  PE-MSG-08                       PIC X(50) VALUE          00710000
                'SELECT OF CHECKPOINT RESTART INFO FROM TCKRSTINF'.     00720000
           05  PE-MSG-09                       PIC X(50) VALUE          00730000
                'SET HOLD TIMESTAMP EQUAL TO CURRENT TIMESTAMP  '.      00740000
           05  PE-MSG-10                       PIC X(50) VALUE          00750000
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00760000
           05  PE-MSG-11                       PIC X(50) VALUE          00770000
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00780000
      *                                                                 00790000
       01  PS-PROGRAM-SWITCHES.                                         00800000
           05  PS-CONDENSED-FILE-EOF-SW     PIC  X        VALUE 'N'.    00810000
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00820000
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00830000
           05  PS-EMPTY-FILE-SW             PIC  X        VALUE 'N'.    00840000
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00850000
           05  PS-MNDPTS-CURSOR-EOF-SW      PIC  X        VALUE 'N'.    00860000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00870000
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00880000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00890000
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00900000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00910000
           05  PS-PROCESSING-OPTIONS        PIC X         VALUE 'P'.    00920000
               88  PS-UPDATE-WKDPTS-ROW                   VALUE 'U'.    00930000
               88  PS-INSERT-WKDPTS-ROW                   VALUE 'I'.    00940000
      *                                                                 00950000
       01  PROGRAM-VARIABLES.                                           00960000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00970000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00980000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00990000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01000000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01010000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01020000
      *                                                                 01030000
       01  PA-PROGRAM-ACCUMULATORS.                                     01040000
           05  PA-RECORD-COUNTS.                                        01050000
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01060000
               10  PA-UPDATE-COUNT             PIC  9(9)  VALUE ZEROES. 01070000
               10  PA-INSERT-COUNT             PIC  9(9)  VALUE ZEROES. 01080000
               10  PA-DEPTS-PROCESSED          PIC  9(9)  VALUE ZEROES. 01090000
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01100000
      *                                                                 01110000
       01  SAVE-AREA.                                                   01120000
           05  SV-PRIMARY-KEY.                                          01130000
               10  SV-FSCL-WK-NBR        PIC X(02)         VALUE SPACES.01140000
               10  SV-FSCL-WK-END-DTE    PIC X(10)         VALUE SPACES.01150000
               10  SV-DEPT-NBR           PIC X(03)         VALUE SPACES.01160000
               10  SV-STR-NBR            PIC S9(04)     COMP   VALUE +0.01170000
           05  SV-UPDATE-FIELDS.                                        01180000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01190000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01200000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01210000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01220000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01230000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01240000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01260000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01270000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01280000
      *                                                                 01290000
      *                                                                 01300000
      ******************************************************************01310000
      * MLRD146 - M/L MAJOR CLS/STR DATABASE UPDATE TRANSACTION LAYOUT  01320000
      ******************************************************************01330000
           COPY MLRD146.                                                01340000
      *                                                                 01350000
      ******************************************************************01360000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01370000
      ******************************************************************01380000
           COPY DPWS004.                                                01390000
      *                                                                 01400000
      ******************************************************************01410000
      *  USED FOR DB2 ERRORS                                            01420000
      ******************************************************************01430000
           EXEC SQL                                                     01440000
               INCLUDE SQLCA                                            01450000
           END-EXEC.                                                    01460000
      *                                                                 01470000
      ******************************************************************01480000
      *  M/L WEEKLY DEPARTMENT/STORE TABLE                              01490000
      ******************************************************************01500000
           EXEC SQL                                                     01510000
               INCLUDE TWKDPTS                                          01520000
           END-EXEC.                                                    01530000
      *                                                                 01540000
      ***************************************************************** 01550000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01560000
      ***************************************************************** 01570000
           EXEC SQL                                                     01580000
               INCLUDE TCKRSTCN                                         01590000
           END-EXEC.                                                    01600000
      *                                                                 01610000
           EXEC SQL                                                     01620000
               INCLUDE TCKRSTIN                                         01630000
           END-EXEC.                                                    01640000
      *                                                                 01650000
      ******************************************************************01660000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01670000
      ******************************************************************01680000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01690000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +62  COMP. 01700000
           05  CR-CHECKPOINT-RESTART-VAR.                               01710000
               10  CR-PREV-DTL-KEY          PIC  X(17) VALUE SPACES.    01720000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01730000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01740000
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                 01750000
                   15  CR-DEPT-NBR          PIC  X(03).                 01760000
                   15  CR-STR-NBR           PIC S9(04) COMP.            01770000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01780000
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    01790000
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01800000
               10  CR-DEPTS-PROCESSED       PIC  9(09) VALUE ZEROES.    01810000
               10  CR-TWKDPTS-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.    01820000
      *                                                                 01830000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01840000
           05  CK-SAVE-PREV-KEY         PIC  X(17) VALUE SPACES.        01850000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01860000
               10  CK-FISCAL-NBR        PIC  X(02).                     01870000
               10  CK-FISCAL-WK-END-DTE PIC  X(10).                     01880000
               10  CK-DEPT-NBR          PIC  X(03).                     01890000
               10  CK-STR-NBR           PIC S9(04) COMP.                01900000
      *                                                                 01910000
      *                                                                 01920000
      *                                                                 01930000
      *                                                                 01940000
      *-------------------*                                             01950000
       PROCEDURE DIVISION.                                              01960000
      *-------------------*                                             01970000
                                                                        01980000
       0000-MAIN-MODULE.                                                01990000
                                                                        02000000
           PERFORM 1000-INITIALIZATION.                                 02010000
      *                                                                 02020000
           PERFORM 2000-PROCESS-INPUT                                   02030000
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02040000
      *                                                                 02050000
           PERFORM 8000-EOJ-ROUTINE.                                    02060000
                                                                        02070000
           STOP RUN.                                                    02080000
      *                                                                 02090000
      *                                                                 02100000
      *                                                                 02110000
      ******************************************************************02120000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02130000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02140000
      *  TCKRSTCNTL-CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION  02150000
      ******************************************************************02160000
      *                                                                 02170000
       1000-INITIALIZATION.                                             02180000
      *                                                                 02190000
           OPEN INPUT CONDENSED-FILE.                                   02200000
      *                                                                 02210000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02220000
      *                                                                 02230000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02240000
                PERFORM 7500-SELECT-RESTART-INFO                        02250000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02260000
                                              CR-CHECKPOINT-RESTART-VAR 02270000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02280000
           ELSE                                                         02290000
               SET PS-FIRST-COMMIT TO TRUE                              02300000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02310000
           END-IF.                                                      02320000
      *                                                                 02330000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02340000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02350000
            OR PS-NO-MORE-INPUT-RECORDS.                                02360000
      *                                                                 02370000
      * IF PROGRAM IN RESTART MODE, BUT NO RECORDS EXIST AFTER LAST CKPT02380000
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02390000
           IF PS-NO-MORE-INPUT-RECORDS                                  02400000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02410000
                   MOVE CR-INPUT-READ-COUNT     TO PA-INPUT-COUNT       02420000
                   MOVE CR-TWKDPTS-COMMIT-COUNT TO PA-COMMIT-COUNT      02430000
                   MOVE CR-UPDATE-COUNT         TO PA-UPDATE-COUNT      02440000
                   MOVE CR-INSERT-COUNT         TO PA-INSERT-COUNT      02450000
                   MOVE CR-DEPTS-PROCESSED      TO PA-DEPTS-PROCESSED   02460000
               ELSE                                                     02470000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02480000
           ELSE                                                         02490000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02500000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02510000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02520000
           END-IF.                                                      02530000
      *                                                                 02540000
      *                                                                 02550000
      ******************************************************************02560000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02570000
      ******************************************************************02580000
       2000-PROCESS-INPUT.                                              02590000
      *                                                                 02600000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02610000
      *                                                                 02620000
           PERFORM 7000-SELECT-DB-ROW.                                  02630000
      *                                                                 02640000
           EVALUATE TRUE                                                02650000
               WHEN PS-UPDATE-WKDPTS-ROW                                02660000
                   PERFORM 7100-UPDATE-WKDPTS-ROW                       02670000
               WHEN PS-INSERT-WKDPTS-ROW                                02680000
                   PERFORM 7200-INSERT-WKDPTS-ROW                       02690000
               WHEN OTHER                                               02700000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     02710000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             02720000
                   PERFORM 9999-SQL-ABEND                               02730000
           END-EVALUATE.                                                02740000
      *                                                                 02750000
           IF PS-PROGRAM-DEADLOCKED                                     02760000
               CONTINUE                                                 02770000
           ELSE                                                         02780000
               PERFORM 7700-COMMIT-PROCESSING                           02790000
               PERFORM 4000-READ-CONDENSED-FILE.                        02800000
      *                                                                 02810000
      *                                                                 02820000
      ******************************************************************02830000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02840000
      ******************************************************************02850000
       2100-CALCULATE-UPDATES.                                          02860000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02870000
      *                                                                 02880000
           COMPUTE SV-SLS-RTL-AMT       =                               02890000
                     WKDPTS-SLS-RTL-AMT       + ML146-SLS-RTL-AMT       02900000
      *                                                                 02910000
           COMPUTE SV-MKDN-RTL-AMT      =                               02920000
                     WKDPTS-MKDN-RTL-AMT      + ML146-MKDN-RTL-AMT      02930000
      *                                                                 02940000
           COMPUTE SV-MKUP-RTL-AMT      =                               02950000
                     WKDPTS-MKUP-RTL-AMT      + ML146-MKUP-RTL-AMT      02960000
      *                                                                 02970000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               02980000
                     WKDPTS-XFER-IN-RTL-AMT   + ML146-XFER-IN-RTL-AMT   02990000
      *                                                                 03000000
           COMPUTE SV-XFER-OUT-RTL      =                               03010000
                     WKDPTS-XFER-OUT-RTL-AMT  + ML146-XFER-OUT-RTL-AMT  03020000
      *                                                                 03030000
           COMPUTE SV-RCPT-RTL-AMT =                                    03040000
                     WKDPTS-RCPT-RTL-AMT      + ML146-RCPT-RTL-COST-AMT 03050000
      *                                                                 03060000
           COMPUTE SV-RCPT-NET-COST     =                               03070000
                     WKDPTS-RCPT-NET-COST-AMT + ML146-RCPT-NET-COST-AMT 03080000
      *                                                                 03090000
           COMPUTE SV-PADJ-RTL-AMT      =                               03100000
                     WKDPTS-PADJ-RTL-AMT      + ML146-PADJ-RTL-AMT      03110000
      *                                                                 03120000
           COMPUTE SV-PADJ-NET-COST     =                               03130000
                     WKDPTS-PADJ-NET-COST-AMT + ML146-PADJ-NET-COST-AMT 03140000
      *                                                                 03150000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03160000
                     WKDPTS-INV-ADJ-RTL-AMT   + ML146-INV-ADJ-RTL-AMT.  03170000
      *                                                                 03180000
      *                                                                 03190000
      ******************************************************************03200000
      * READS THE CONDENSED FILE INTO THE TWKDPTS LAYOUT                03210000
      ******************************************************************03220000
       4000-READ-CONDENSED-FILE.                                        03230000
            READ CONDENSED-FILE INTO ML146-REC                          03240000
               AT END MOVE 'Y' TO PS-CONDENSED-FILE-EOF-SW.             03250000
      *                                                                 03260000
            IF PS-MORE-INPUT-RECORDS                                    03270000
                ADD 1                        TO PA-INPUT-COUNT.         03280000
      *                                                                 03290000
      ******************************************************************03300000
      * SELECT A ROW FROM THE MAJOR CLS/STR TABLE THAT MATCHES INPUT    03310000
      * KEY SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT    03320000
      ******************************************************************03330000
       7000-SELECT-DB-ROW.                                              03340000
      *                                                                 03350000
           EXEC SQL                                                     03360000
               SELECT SLS_RTL_AMT                                       03370000
                     ,MKDN_RTL_AMT                                      03380000
                     ,MKUP_RTL_AMT                                      03390000
                     ,XFER_IN_RTL_AMT                                   03400000
                     ,XFER_OUT_RTL_AMT                                  03410000
                     ,RCPT_RTL_AMT                                      03420000
                     ,RCPT_NET_COST_AMT                                 03430000
                     ,PADJ_RTL_AMT                                      03440000
                     ,PADJ_NET_COST_AMT                                 03450000
                     ,INV_ADJ_RTL_AMT                                   03460000
                 INTO :WKDPTS-SLS-RTL-AMT                               03470000
                     ,:WKDPTS-MKDN-RTL-AMT                              03480000
                     ,:WKDPTS-MKUP-RTL-AMT                              03490000
                     ,:WKDPTS-XFER-IN-RTL-AMT                           03500000
                     ,:WKDPTS-XFER-OUT-RTL-AMT                          03510000
                     ,:WKDPTS-RCPT-RTL-AMT                              03520000
                     ,:WKDPTS-RCPT-NET-COST-AMT                         03530000
                     ,:WKDPTS-PADJ-RTL-AMT                              03540000
                     ,:WKDPTS-PADJ-NET-COST-AMT                         03550000
                     ,:WKDPTS-INV-ADJ-RTL-AMT                           03560000
               FROM TWKDPTS                                             03570000
              WHERE   FSCL_WK_NBR         = :ML146-FSCL-WK-NBR          03580000
                AND   FSCL_WK_END_DTE     = :ML146-FSCL-WK-END-DTE      03590000
                AND   DEPT_NBR            = :ML146-DEPT-NBR             03600000
                AND   STR_NBR             = :ML146-STR-NBR              03610000
W26603        WITH UR                                                   03620000
           END-EXEC.                                                    03630000
      *                                                                 03640000
           EVALUATE SQLCODE                                             03650000
               WHEN 0                                                   03660000
                   SET PS-UPDATE-WKDPTS-ROW       TO TRUE               03670000
               WHEN +100                                                03680000
                   SET PS-INSERT-WKDPTS-ROW       TO TRUE               03690000
               WHEN OTHER                                               03700000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03710000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
      *                                                                 03750000
      *                                                                 03760000
      ***************************************************************** 03770000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  03780000
      *   CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC 03790000
      ***************************************************************** 03800000
       7100-UPDATE-WKDPTS-ROW.                                          03810000
      *                                                                 03820000
      *                                                                 03830000
           PERFORM 2100-CALCULATE-UPDATES.                              03840000
      *                                                                 03850000
           EXEC SQL                                                     03860000
               UPDATE TWKDPTS                                           03870000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           03880000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          03890000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           03900000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          03910000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            03920000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           03930000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           03940000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        03950000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           03960000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        03970000
              WHERE   FSCL_WK_NBR         = :ML146-FSCL-WK-NBR          03980000
                AND   FSCL_WK_END_DTE     = :ML146-FSCL-WK-END-DTE      03990000
                AND   DEPT_NBR            = :ML146-DEPT-NBR             04000000
                AND   STR_NBR             = :ML146-STR-NBR              04010000
           END-EXEC                                                     04020000
      *                                                                 04030000
           EVALUATE SQLCODE                                             04040000
               WHEN 0                                                   04050000
                   CONTINUE                                             04060000
               WHEN -911                                                04070000
                   ADD +1             TO PV-DEADLOCK-COUNT              04080000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04090000
                       MOVE '7100-UPDATE-WKDPTS-ROW' TO                 04100000
                                                    PV-PARAGRAPH-NAME   04110000
                       PERFORM 9000-DEADLOCK-ERROR                      04120000
                   ELSE                                                 04130000
                       PERFORM 7999-RESTART-PROCESS                     04140000
                   END-IF                                               04150000
               WHEN OTHER                                               04160000
                   MOVE '7100-UPDATE-WKDPTS-ROW' TO PV-PARAGRAPH-NAME   04170000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04180000
                   PERFORM 9999-SQL-ABEND                               04190000
           END-EVALUATE.                                                04200000
      *                                                                 04210000
           IF PS-PROGRAM-DEADLOCKED                                     04220000
               CONTINUE                                                 04230000
           ELSE                                                         04240000
               ADD 1 TO PA-UPDATE-COUNT                                 04250000
                        PA-DEPTS-PROCESSED.                             04260000
      *                                                                 04270000
      *                                                                 04280000
      ***************************************************************** 04290000
      * THERE WAS NO MATCHING ROW ON THE MAJOR CLS/STR TABLE.INITIAL  * 04300000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04310000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04320000
      ***************************************************************** 04330000
       7200-INSERT-WKDPTS-ROW.                                          04340000
      *                                                                 04350000
      *                                                                 04360000
           EXEC SQL                                                     04370000
               INSERT INTO TWKDPTS                                      04380000
                   (   FSCL_WK_NBR                                      04390000
                     , FSCL_WK_END_DTE                                  04400000
                     , DEPT_NBR                                         04410000
                     , STR_NBR                                          04420000
                     , RCPT_RTL_AMT                                     04430000
                     , RCPT_NET_COST_AMT                                04440000
                     , PADJ_RTL_AMT                                     04450000
                     , PADJ_NET_COST_AMT                                04460000
                     , SLS_RTL_AMT                                      04470000
                     , MKDN_RTL_AMT                                     04480000
                     , MKUP_RTL_AMT                                     04490000
                     , XFER_IN_RTL_AMT                                  04500000
                     , XFER_OUT_RTL_AMT                                 04510000
                     , INV_ADJ_RTL_AMT                                  04520000
                     , SHNK_RTL_AMT                                     04530000
                     , END_INV_RTL_AMT                                  04540000
                     , CUM_MKUP_PCT                                     04550000
                     , END_INV_COST_AMT                                 04560000
                     , SLS_COST_AMT                                     04570000
                     , GRO_MRGN_AMT      )                              04580000
               VALUES                                                   04590000
                   (   :ML146-FSCL-WK-NBR                               04600000
                     , :ML146-FSCL-WK-END-DTE                           04610000
                     , :ML146-DEPT-NBR                                  04620000
                     , :ML146-STR-NBR                                   04630000
                     , :ML146-RCPT-RTL-COST-AMT                         04640000
                     , :ML146-RCPT-NET-COST-AMT                         04650000
                     , :ML146-PADJ-RTL-AMT                              04660000
                     , :ML146-PADJ-NET-COST-AMT                         04670000
                     , :ML146-SLS-RTL-AMT                               04680000
                     , :ML146-MKDN-RTL-AMT                              04690000
                     , :ML146-MKUP-RTL-AMT                              04700000
                     , :ML146-XFER-IN-RTL-AMT                           04710000
                     , :ML146-XFER-OUT-RTL-AMT                          04720000
                     , :ML146-INV-ADJ-RTL-AMT                           04730000
                     , 0                                                04740000
                     , 0                                                04750000
                     , 0                                                04760000
                     , 0                                                04770000
                     , 0                                                04780000
                     , 0                       )                        04790000
           END-EXEC                                                     04800000
      *                                                                 04810000
           EVALUATE SQLCODE                                             04820000
               WHEN 0                                                   04830000
                   CONTINUE                                             04840000
               WHEN -911                                                04850000
                   ADD +1             TO PV-DEADLOCK-COUNT              04860000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04870000
                       MOVE '7200-INSERT-WKDPTS-ROW' TO                 04880000
                                                    PV-PARAGRAPH-NAME   04890000
                       PERFORM 9000-DEADLOCK-ERROR                      04900000
                   ELSE                                                 04910000
                       PERFORM 7999-RESTART-PROCESS                     04920000
                   END-IF                                               04930000
               WHEN OTHER                                               04940000
                   MOVE '7200-INSERT-WKDPTS-ROW' TO PV-PARAGRAPH-NAME   04950000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED04960000
                   PERFORM 9999-SQL-ABEND                               04970000
           END-EVALUATE                                                 04980000
      *                                                                 04990000
           IF PS-PROGRAM-DEADLOCKED                                     05000000
               CONTINUE                                                 05010000
           ELSE                                                         05020000
               ADD 1 TO PA-INSERT-COUNT                                 05030000
                        PA-DEPTS-PROCESSED.                             05040000
      *                                                                 05050000
      *                                                                 05060000
      ******************************************************************05070000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05080000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05090000
      *            CONTROL TABLE                                       *05100000
      ******************************************************************05110000
       7300-GET-COMMIT-TIMESTAMP.                                       05120000
                                                                        05130000
           EXEC SQL                                                     05140000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05150000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05160000
               INTO :PV-COMMIT-TIMESTAMP                                05170000
               FROM TCKRSTCNTL                                          05180000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05190000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05200000
           END-EXEC.                                                    05210000
                                                                        05220000
           EVALUATE TRUE                                                05230000
               WHEN SQLCODE = 0                                         05240000
                   CONTINUE                                             05250000
               WHEN SQLCODE NOT EQUAL ZERO                              05260000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05270000
                   MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED 05280000
                   PERFORM 9999-SQL-ABEND                               05290000
           END-EVALUATE.                                                05300000
      *                                                                 05310000
      *                                                                 05320000
      ******************************************************************05330000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05340000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05350000
      *            IF WE ARE IN A RESTART CONDITION.                   *05360000
      ******************************************************************05370000
       7400-INIT-CHECKPOINT-SELECT.                                     05380000
                                                                        05390000
           EXEC SQL                                                     05400000
             SELECT  CKPT_STAT_CODE                                     05410000
                    ,CKPT_FRQNCY_QTY                                    05420000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05430000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05440000
               FROM  TCKRSTCNTL                                         05450000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05460000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05470000
           END-EXEC.                                                    05480000
                                                                        05490000
           IF SQLCODE = 0                                               05500000
               CONTINUE                                                 05510000
           ELSE                                                         05520000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05530000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05540000
               PERFORM 9999-SQL-ABEND                                   05550000
           END-IF.                                                      05560000
      *                                                                 05570000
      *                                                                 05580000
      ******************************************************************05590000
      * 7500-SELECT-RESTART-INFO                                       *05600000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05610000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05620000
      ******************************************************************05630000
       7500-SELECT-RESTART-INFO.                                        05640000
                                                                        05650000
           EXEC SQL                                                     05660000
             SELECT  WORK_STRG_DESC                                     05670000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05680000
               FROM  TCKRSTINF                                          05690000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05700000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05710000
           END-EXEC.                                                    05720000
                                                                        05730000
           IF SQLCODE = 0                                               05740000
               CONTINUE                                                 05750000
           ELSE                                                         05760000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05770000
               MOVE PE-MSG-08             TO PV-OPERATION-ATTEMPTED     05780000
               PERFORM 9999-SQL-ABEND                                   05790000
           END-IF.                                                      05800000
      *                                                                 05810000
      *                                                                 05820000
      ******************************************************************05830000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05840000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05850000
      ******************************************************************05860000
       7600-SET-CURRENT-TIMESTAMP.                                      05870000
                                                                        05880000
           EXEC SQL                                                     05890000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05900000
           END-EXEC.                                                    05910000
                                                                        05920000
           IF SQLCODE = 0                                               05930000
               CONTINUE                                                 05940000
           ELSE                                                         05950000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05960000
               MOVE PE-MSG-09             TO PV-OPERATION-ATTEMPTED     05970000
               PERFORM 9999-SQL-ABEND                                   05980000
           END-IF.                                                      05990000
      *                                                                 06000000
      *                                                                 06010000
      ******************************************************************06020000
      * 7700-COMMIT-PROCESSING.                                        *06030000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06040000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06050000
      ******************************************************************06060000
       7700-COMMIT-PROCESSING.                                          06070000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06080000
                                                                        06090000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06100000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06110000
      *        PREPARE COMMIT RECORD FOR UPDATE                         06120000
               ADD 1                          TO PA-COMMIT-COUNT        06130000
               MOVE ML146-FSCL-WK-NBR         TO CR-FISCAL-NBR          06140000
               MOVE ML146-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   06150000
               MOVE ML146-DEPT-NBR            TO CR-DEPT-NBR            06160000
               MOVE ML146-STR-NBR             TO CR-STR-NBR             06170000
               MOVE PA-COMMIT-COUNT           TO CR-TWKDPTS-COMMIT-COUNT06180000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06190000
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        06200000
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        06210000
               MOVE PA-DEPTS-PROCESSED        TO CR-DEPTS-PROCESSED     06220000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06230000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06240000
                                             TCKRSTINF-WORK-STRG-DESC   06250000
               IF PS-FIRST-COMMIT                                       06260000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06270000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06280000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06290000
               END-IF                                                   06300000
               PERFORM 7800-COMMIT-CHANGES                              06310000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06320000
           END-IF.                                                      06330000
      *                                                                 06340000
      *                                                                 06350000
      ******************************************************************06360000
      * 7800-COMMIT-CHANGES.                                            06370000
      *   PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE. TAKE A COMMIT 06380000
      ******************************************************************06390000
       7800-COMMIT-CHANGES.                                             06400000
                                                                        06410000
           EXEC SQL                                                     06420000
             UPDATE TCKRSTINF                                           06430000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06440000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06450000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06460000
           END-EXEC.                                                    06470000
                                                                        06480000
           IF SQLCODE = 0                                               06490000
               CONTINUE                                                 06500000
           ELSE                                                         06510000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06520000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06530000
               PERFORM 9999-SQL-ABEND                                   06540000
           END-IF.                                                      06550000
                                                                        06560000
           EXEC SQL                                                     06570000
               COMMIT                                                   06580000
           END-EXEC.                                                    06590000
                                                                        06600000
           IF SQLCODE = 0                                               06610000
               CONTINUE                                                 06620000
           ELSE                                                         06630000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06640000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06650000
               PERFORM 9999-SQL-ABEND                                   06660000
           END-IF.                                                      06670000
      *                                                                 06680000
      *                                                                 06690000
      ******************************************************************06700000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06710000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06720000
      *                                                                 06730000
      ******************************************************************06740000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06750000
                                                                        06760000
           EXEC SQL                                                     06770000
             UPDATE  TCKRSTCNTL                                         06780000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06790000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06800000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06810000
           END-EXEC.                                                    06820000
                                                                        06830000
           IF SQLCODE = 0                                               06840000
               CONTINUE                                                 06850000
           ELSE                                                         06860000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06870000
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED06880000
               PERFORM 9999-SQL-ABEND                                   06890000
           END-IF.                                                      06900000
                                                                        06910000
                                                                        06920000
           EJECT                                                        06930000
      *                                                                 06940000
      *                                                                 06950000
      ******************************************************************06960000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKDPTS ROW FOR UPDATE06970000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  06980000
      ******************************************************************06990000
       7999-RESTART-PROCESS.                                            07000000
      *                                                                 07010000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07020000
      *                                                                 07030000
           CLOSE CONDENSED-FILE.                                        07040000
      *                                                                 07050000
           PERFORM 7500-SELECT-RESTART-INFO.                            07060000
      *                                                                 07070000
           DISPLAY '**** RESTART **** '.                                07080000
           DISPLAY '** SQLCODE -911 ** ', PV-DEADLOCK-COUNT, 'TIMES'.   07090000
           DISPLAY 'TCKRSTINF-LAST CKPT ', TCKRSTINF-WORK-STRG-DESC.    07100000
      *                                                                 07110000
           MOVE TCKRSTINF-WORK-STRG-DESC TO CR-CHECKPOINT-RESTART-AREA. 07120000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07130000
      *                                                                 07140000
           OPEN INPUT CONDENSED-FILE.                                   07150000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07160000
      *                                                                 07170000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07180000
           PERFORM 4000-READ-CONDENSED-FILE                             07190000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07200000
      *                                                                 07210000
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07220000
           MOVE CR-UPDATE-COUNT        TO PA-UPDATE-COUNT.              07230000
           MOVE CR-INSERT-COUNT        TO PA-INSERT-COUNT.              07240000
           MOVE CR-DEPTS-PROCESSED     TO PA-DEPTS-PROCESSED.           07250000
      *                                                                 07260000
      *                                                                 07270000
      ******************************************************************07280000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07290000
      ******************************************************************07300000
       8000-EOJ-ROUTINE.                                                07310000
      *                                                                 07320000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07330000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07340000
      *                                                                 07350000
           DISPLAY '                                             '.     07360000
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  07370000
           DISPLAY 'UPDATE COUNT    ', PA-UPDATE-COUNT.                 07380000
           DISPLAY 'INSERT COUNT    ', PA-INSERT-COUNT.                 07390000
           DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.              07400000
           DISPLAY '                                             '.     07410000
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 07420000
      *                                                                 07430000
           CLOSE CONDENSED-FILE.                                        07440000
      *                                                                 07450000
      *                                                                 07460000
      ******************************************************************07470000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07480000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07490000
      ******************************************************************07500000
       9000-DEADLOCK-ERROR.                                             07510000
      *                                                                 07520000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07530000
               PERFORM 9999-SQL-ABEND.                                  07540000
      *                                                                 07550000
      ******************************************************************07560000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07570000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07580000
      ******************************************************************07590000
       9999-SQL-ABEND.                                                  07600000
                                                                        07610000
           MOVE +4013                 TO ABEND-CODE.                    07620000
           DISPLAY '**  ABEND     **'.                                  07630000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07640000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07650000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07660000
                                                                        07670000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07680000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07690000
                                                                        07700000
           COPY DPPD004.                                                07710000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07720000
