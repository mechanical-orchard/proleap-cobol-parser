      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.      PMKUT110.                                               
       AUTHOR.          AMANDA NEWHART.                                         
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    SEPTEMBER 2003.                                         
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *    --- GET ORCALE INFO FOR DB2 PROGRAMS ---                   *         
      *                                                               *         
      *   THIS PROGRAM WILL RETRIEVE AD ITEM DESCRIPTION BASED ON     *         
      *   PROMO ITM ID.  FIRST LOG INTO ORACLE.  NEXT READ THE FIRST  *         
      *   RECORD FROM THE FILE.  TAKE THAT PROMO ITM ID AND FIND THE  *         
      *   AD ITEM DESCRIPTION.  THEN MOVE THAT AD ITEM DESCRIPTION    *         
      *   INTO THE INPUT/OUTPUT FILE.  THEN READ THE NEXT LINE IN THE *         
      *   FILE AND REPEAT THIS PROCESS UNTIL THE LAST LINE IN THE     *         
      *   FILE HAS BEEN READ.  WHEN THE FILE IS DONE BEING READ CLOSE *         
      *   THE FILE AND PRINT OUT THE DISPLAYS.                        *         
      *****************************************************************         
      * ORACLE TABLES:                                                *         
      *  1.                                  (PMT_PROMO_ITM)          *         
      *                                                               *         
      *****************************************************************         
      *   CHANGES:                                                    *         
      *    09/25/2003 - AMANDA NEWHART - WR21770                      *         
      *     - CHANGE PMPD70B TO PMPD700 AND ADD A FEW VARIABLES.      *         
      *****************************************************************         
      *    09/26/2003 - MARTIN BRADLEY - WR25600                      *         
      *     - CHANGED TO PROCESS AN ENTIRE DATASET AT ONCE INSTEAD OF *         
      *       JUST A SINGLE CONTROL CARD.                             *         
      *****************************************************************         
WR166 *    09/30/2016 - COGNIZANT      - WR1668677061                 *         
WR166 *     - CHANGED TO SKIP UNADVERTISED PROMO ITEM PRICING         *         
      *****************************************************************         
      *****************************************************************         
070941*    07/30/2018 - COGNIZANT      - CHG0204126                   *         
070941*     - ORACLE LOGON STANDARDS PROGRAM CHANGE                   *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT LPS-EXCEPTIONS-FILE                                           
WR166          ASSIGN                       TO INP01.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
WR166      SELECT LPS-EXCEPTIONS-FILE-UPD                                       
WR166          ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  LPS-EXCEPTIONS-FILE                                                  
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
       01  LPS-EXCEPTIONS-RECORD            PIC X(350).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
WR166  FD  LPS-EXCEPTIONS-FILE-UPD                                              
WR166      RECORDING MODE F                                                     
WR166      BLOCK CONTAINS 0 RECORDS                                             
WR166      LABEL RECORDS ARE STANDARD.                                          
WR166  01  LPS-EXCEPTIONS-RECORD-UPD        PIC X(350).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941     05  PC-AUTO-LOGON                PIC  X(1)    VALUE '/'.             
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE                    
           05  PC-PROGRAM-NAME              PIC  X(08)   VALUE                  
                                                        'PMKUT110'.             
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)    VALUE ZEROS            
070941                                                   COMP SYNC.             
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)    VALUE ZEROS            
070941                                                   COMP SYNC.             
070941     05  PC-AUTO-LOGON                PIC  X(01)   VALUE '/'.             
           05  PV-PROMO-ITM-ID              PIC  9(10)V  VALUE ZEROS.           
           05  PV-PROMO-ITM-DESC            PIC  X(100)  VALUE SPACES.          
WR166      05  PV-PROMO-ITM-DESC1           PIC  X(100)  VALUE SPACES.          
           05  PV-TABLE-NAME                PIC  X(12)   VALUE SPACES.          
           05  PV-OPERATION-MSG             PIC  X(60)   VALUE SPACES.          
           05  PV-OPERATION-MSG-1           PIC  X(40)   VALUE SPACES.          
           05  PV-PARAGRAPH-NAME            PIC  X(30)   VALUE SPACES.          
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                                     
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW        PIC  X(01)   VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW         PIC  X(01)   VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
           05  PS-END-OF-LPS-EXCEP-SW         PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-LPS-EXCEP                     VALUE 'Y'.           
           05  PS-FIRST-TIME-SW               PIC  X(01)   VALUE 'Y'.           
               88  PS-FIRST-TIME-YES                       VALUE 'Y'.           
               88  PS-FIRST-TIME-NO                        VALUE 'N'.           
WR166      05  PS-UNADV-LPS-ITEM-SW           PIC  X(01)   VALUE 'Y'.           
WR166          88  PS-UNADV-LPS-ITEM-YES                   VALUE 'Y'.           
WR166          88  PS-UNADV-LPS-ITEM-NO                    VALUE 'N'.           
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID               PIC  X(40).                       
070941         88 CC-AUTO-LOGON                            VALUE '/'.           
070941     05  CC-ORACLE-PASSWORD             PIC  X(40).                       
                                                                                
      ******************************************************************        
      * PA PROGRAM ACCUMULATORS                                                 
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ                    PIC S9(11) COMP-3                
                                                           VALUE ZEROES.        
           05  PA-RECS-WRITTEN                 PIC S9(11) COMP-3                
                                                           VALUE ZEROES.        
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ******************************************************************        
      *    LPS EXCEPTIONS RECORD                                                
      *      AND                                                                
      *    LPS EXCEPTIONS RECORD WITH PROMO ITEM DESCRIPTION                    
      ******************************************************************        
           COPY PMRD044.                                                        
                                                                                
      ********************                                                      
        LINKAGE SECTION.                                                        
      ********************                                                      
                                                                                
      ***********************                                                   
        PROCEDURE DIVISION.                                                     
      ***********************                                                   
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      *****************************************************************         
       A100-MAIN.                                                               
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
WR166      OPEN INPUT LPS-EXCEPTIONS-FILE                                       
WR166           OUTPUT LPS-EXCEPTIONS-FILE-UPD.                                 
                                                                                
           PERFORM B400-READ-LPS-EXCEP.                                         
                                                                                
           PERFORM B100-PROCESS-EXCEPTIONS                                      
               UNTIL PS-END-OF-LPS-EXCEP.                                       
                                                                                
           PERFORM B300-CLOSE-RTN.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-PROCESS-EXCEPTIONS.                                      *         
      * ==> PROCESSES:                                                *         
      *  - MOVES THE AD ITM ID TO THE CORRECT VARABLE.                *         
      *  - FIND THE AD ITEM DESCRIPTION.                              *         
      *  - WRITE TO THE OUTPUT FILE                                   *         
      *  - READ NEXT LINE                                             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-PROCESS-EXCEPTIONS.                                                 
                                                                                
WR166          MOVE PM044-LPS-AD-ITM-ID TO PV-PROMO-ITM-ID                      
WR166          PERFORM B900-UNADV-LPS-ITEM-CHECK                                
WR166      IF PS-UNADV-LPS-ITEM-NO                                              
      *    RETRIEVE THE PROMO ITEM DESCRIPTION FOR THE UPC IN EXCEPTION         
WR166        IF PM044-PRE-AD-ITM-ID > 0                                         
WR166          MOVE PM044-PRE-AD-ITM-ID TO PV-PROMO-ITM-ID                      
WR166          PERFORM C100-GET-ADITM-DESC                                      
WR166          MOVE PV-PROMO-ITM-DESC   TO PM044-PRE-AD-ITM                     
WR166        END-IF                                                             
                                                                                
      *    RETRIEVE THE PROMO ITEM DESCRIPTION FOR THE UPC IN THE LPS           
WR166        IF PM044-LPS-AD-ITM-ID > 0                                         
               MOVE PM044-LPS-AD-ITM-ID TO PV-PROMO-ITM-ID                      
WR166          PERFORM C100-GET-ADITM-DESC                                      
WR166          MOVE PV-PROMO-ITM-DESC   TO PM044-LPS-AD-ITM                     
WR166        END-IF                                                             
WR166          PERFORM B200-WRITE-LPS-EXCEP-OUT                                 
           END-IF.                                                              
                                                                                
           PERFORM B400-READ-LPS-EXCEP.                                         
                                                                                
      *****************************************************************         
      * B200-WRITE-LPS-EXCEP-OUT.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OVERWRITES THE LPS EXCEPTION RECORD INCLUDING THE PROMO    *         
      *    ITEM DESCRIPTION.                                          *         
      * ==> PERFORMED FROM: B100-PROCESS-EXCEPTIONS                   *         
      *****************************************************************         
       B200-WRITE-LPS-EXCEP-OUT.                                                
                                                                                
WR166        WRITE LPS-EXCEPTIONS-RECORD-UPD                                    
WR166            FROM PM044-PM-LPS-PRE-EXCEP.                                   
                                                                                
           ADD +1 TO PA-RECS-WRITTEN.                                           
                                                                                
      *****************************************************************         
      * B300-CLOSE-RTN                                                *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE FILES                                            *         
      *  - DISPLAY PROGRAM COUNTS                                     *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-CLOSE-RTN.                                                          
                                                                                
WR166      CLOSE  LPS-EXCEPTIONS-FILE                                           
WR166             LPS-EXCEPTIONS-FILE-UPD.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'INPUT RECORDS READ     = '   PA-RECS-READ.                  
           DISPLAY 'OUTPUT RECORDS WRITTEN = '   PA-RECS-WRITTEN.               
           DISPLAY ' '.                                                         
                                                                                
           DISPLAY ' **** END OF PMKUT110 **** '.                               
                                                                                
      *****************************************************************         
      * B400-READ-LPS-EXCEP.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READS A RECORD FROM THE LPS EXCEPTIONS INPUT FILE.         *         
      *  - INCREMENTS THE COUNTER OF RECORDS READ.                    *         
      * ==> PERFORMED FROM: A100-MAINLINE, B100-PROCESS-EXCEPTIONS    *         
      *****************************************************************         
       B400-READ-LPS-EXCEP.                                                     
                                                                                
           READ LPS-EXCEPTIONS-FILE INTO PM044-PM-LPS-PRE-EXCEP                 
               AT END SET PS-END-OF-LPS-EXCEP TO TRUE                           
           END-READ.                                                            
                                                                                
           IF PS-END-OF-LPS-EXCEP                                               
               IF PS-FIRST-TIME-YES                                             
                   DISPLAY 'EMPTY LPS EXCEPTIONS DATA FILE.'                    
               END-IF                                                           
           ELSE                                                                 
               ADD +1 TO PA-RECS-READ                                           
               SET PS-FIRST-TIME-NO TO TRUE                                     
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * C100-GET-ADITM-DESC                                           *         
      * ==> PROCESSES:                                                *         
      *     -FIND THE ADITEM FOR EACH ITEM                            *         
      * ==> PERFORMED FROM: B100-PROCESS-EXCEPTIONS.                  *         
      *****************************************************************         
       C100-GET-ADITM-DESC.                                                     
                                                                                
           MOVE SPACES     TO PV-PROMO-ITM-DESC.                                
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   PROMO_ITM_DESC                                               
               INTO                                                             
                   :PV-PROMO-ITM-DESC                                           
               FROM                                                             
                   PMT_PROMO_ITM                                                
               WHERE                                                            
                     PROMO_ITM_ID = :PV-PROMO-ITM-ID                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
                                                                                
               WHEN SQLCODE = -1403                                             
                   MOVE 'NO DESC FOUND'       TO PV-PROMO-ITM-DESC              
                   MOVE ZERO                  TO SQLCODE                        
                   DISPLAY 'NO DESC FOUND FOR PROMO ITM ID '                    
                                         PV-PROMO-ITM-ID                        
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C100-GET-ADITM-DESC' TO PV-PARAGRAPH-NAME              
                   MOVE 'PMT_PROMO_ITM' TO PV-TABLE-NAME                        
                   MOVE 'SELECT ERROR ON PMT_PROMO_ITM'                         
                       TO PV-OPERATION-MSG-1                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
WR166 *****************************************************************         
WR166 * B900-UNADV-LPS-ITEM-CHECK                                     *         
WR166 * ==> PROCESSES:                                                *         
WR166 *     -CHECKS IF THE PROMO ITEM IS UNADVERTISED OR NOT.SKIP     *         
WR166 *     UNADVERTISED ITEM FROM INPUT FILE.                        *         
WR166 *****************************************************************         
WR166  B900-UNADV-LPS-ITEM-CHECK.                                               
WR166                                                                           
WR166      EXEC SQL                                                             
WR166          SELECT                                                           
WR166              PROMO_ITM_DESC                                               
WR166          INTO                                                             
WR166              :PV-PROMO-ITM-DESC1                                          
WR166          FROM                                                             
WR166              PMT_PROMO_ITM                                                
WR166          WHERE                                                            
WR166                PROMO_ITM_ID = :PV-PROMO-ITM-ID AND                        
WR166                ADV_IND = 'N'                                              
WR166      END-EXEC.                                                            
WR166                                                                           
WR166      EVALUATE TRUE                                                        
WR166          WHEN SQLCODE = +0                                                
WR166            SET PS-UNADV-LPS-ITEM-YES TO TRUE                              
WR166                                                                           
WR166          WHEN SQLCODE = 1403                                              
WR166            SET PS-UNADV-LPS-ITEM-NO TO TRUE                               
WR166                                                                           
WR166          WHEN OTHER                                                       
WR166              MOVE 'B900-UNADV-ITEM-CHECK' TO PV-PARAGRAPH-NAME            
WR166              MOVE 'PMT_PROMO_ITM' TO PV-TABLE-NAME                        
WR166              MOVE 'SELECT ERROR ON PMT_PROMO_ITM'                         
WR166                  TO PV-OPERATION-MSG-1                                    
WR166              PERFORM 9900-ORACLE-ABEND                                    
WR166      END-EVALUATE.                                                        
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      * FOR TESTING CHANGE PMPD700 TO PMPD70C                          *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** =   SQLERROR'.                         
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG-1.                
           DISPLAY '** RECORDS READ   ** = ' PA-RECS-READ.                      
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *9900-EXIT.                                                               
