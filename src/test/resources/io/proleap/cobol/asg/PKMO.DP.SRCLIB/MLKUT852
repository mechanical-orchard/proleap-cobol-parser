       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT852.                                         00020009
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  08-04-2003.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      * PURPOSE - THE PROGRAM BUILDS THE SALES & INVENTORY PERFORMANCE  00080000
      *           BY STORE GRADE .CSV FILE.                             00090000
      *                                                                 00100000
      *CHANGE HISTORY:                                                  00110000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00120000
      * -------  ----------  ----------------------------------------   00130000
      *          08-04-2003  SALES & INVENTORY PERFORMANCE BY STORE     00140000
      *                      GRADE (CREATION...)  - ROD JANSSEN         00150000
DC    * DC       08-27-2003  ADD DC LOCATIONS TO PROCESSING - S JACKSON 00160017
F90903* F90903   09-09-2003  NEW STORE DOLLARS ARE BEING DUMPED IN THE  00170000
F90903*                      GRADE TABLE. WHY?    - ROD JANSSEN         00180017
ALIGN * ALIGN    09-21-2003  ENSURE ALL GRADES TOTAL TO THE STORE COUNT.00190017
ALIGN *                      ANY REMAINING STORES NOT APPLIED TO A GRADE00200017
ALIGN *                      DUE TO ROUNDING, WILL BE ADDED TO THE FINAL00210017
ALIGN *                      GRADE TABLE.         - SCOTT JACKSON       00220017
FMNTH * FMNTH    01-05-2004  THE EXTRACT PRGM FORMATS MONTH FOR 'OPENED'00230022
FMNTH *                      STORES IN FISCAL MONTH FORMAT. CHANGE THIS 00240022
FMNTH *                      PGM AND COPY MLRD041 TO ALSO BE FISCAL FMT 00250022
FMNTH *                      (VS.CALENDAR FMT)    - SCOTT JACKSON       00260022
W30900* W30900   06-27-2006  ENHANCE THE STORE GRADING EXTRACT TO BE    00261032
W30900*                      REPORT AT MONTHLY, QUARTERLY, AND SEASONAL 00262032
W30900*                      FREQUENCY.                                 00263032
      ******************************************************************00270000
MFTCHG******************************************************************00271041
MFTCHG*  DATE: 08/28/2015                                               00272041
MFTCHG*  WR/IR#:                                                        00273041
MFTCHG*  PROGRAMMER: COGNIZANT                                          00274041
MFTCHG*  DESCRIPTION: CHANGED THE FTP TO MFT CONTROL CARD               00275041
MFTCHG*                                                                 00276041
MFTCHG******************************************************************00277041
                                                                        00280000
       ENVIRONMENT DIVISION.                                            00290000
       CONFIGURATION SECTION.                                           00300000
       SOURCE-COMPUTER.        IBM-3090.                                00310000
       OBJECT-COMPUTER.        IBM-3090.                                00320000
       INPUT-OUTPUT SECTION.                                            00330000
       FILE-CONTROL.                                                    00340000
                                                                        00350000
           SELECT STR-GRADE-PCT-GUIDE-IN                                00360000
               ASSIGN TO INP01.                                         00370000
                                                                        00380000
           SELECT STR-STAT-EXTRACT-IN                                   00390000
               ASSIGN TO INP02.                                         00400000
                                                                        00410000
           SELECT STR-MAIN-EXTRACT-IN                                   00420000
               ASSIGN TO INP03.                                         00430000
                                                                        00440000
           SELECT INPUT-CONTROL-FILE                                    00450000
               ASSIGN TO INP04.                                         00460000
                                                                        00470000
                                                                        00500000
W30900     SELECT FILESET-CONTROL-FILE                                  00501023
W30900         ASSIGN TO INP06.                                         00502023
W30900                                                                  00503023
           SELECT STORE-MERGE-FILE-OUT                                  00510000
               ASSIGN TO OUT01.                                         00520000
                                                                        00530000
           SELECT MFT-CONTROL-CARD-OUT                                  00540041
               ASSIGN TO OUT02.                                         00550000
                                                                        00560000
       DATA DIVISION.                                                   00570000
       FILE SECTION.                                                    00580000
                                                                        00590000
       FD  STR-GRADE-PCT-GUIDE-IN                                       00600000
           LABEL RECORDS ARE OMITTED                                    00610000
           RECORDING MODE IS F                                          00620000
           BLOCK CONTAINS 0 RECORDS.                                    00630000
       01  STR-GRADE-PCT-RECORD.                                        00640000
           05  SG-GRADE-NAME        PIC X(01).                          00650000
           05  SG-FILLER1           PIC X(01).                          00660000
           05  SG-GRADE-PERCENT     PIC 99.99.                          00670000
           05  SG-GRADE-PERCENT-REDEF REDEFINES                         00680000
               SG-GRADE-PERCENT.                                        00690000
               10  SG-GP-WHOLE      PIC X(02).                          00700000
               10  SG-GP-PERIOD     PIC X(01).                          00710000
               10  SG-GP-FRACTION   PIC X(02).                          00720000
           05  SG-REST-RECORD       PIC X(73).                          00730000
                                                                        00740000
       FD  STR-STAT-EXTRACT-IN                                          00750000
           LABEL RECORDS ARE OMITTED                                    00760000
           RECORDING MODE IS F                                          00770000
           BLOCK CONTAINS 0 RECORDS.                                    00780000
       01  STR-STAT-EXTR-RECORD     PIC X(100).                         00790000
                                                                        00800000
       FD  STR-MAIN-EXTRACT-IN                                          00810000
           LABEL RECORDS ARE OMITTED                                    00820000
           RECORDING MODE IS F                                          00830000
           BLOCK CONTAINS 0 RECORDS.                                    00840000
       01  STR-MAIN-EXTR-RECORD     PIC X(125).                         00850000
                                                                        00860000
       FD  INPUT-CONTROL-FILE                                           00870000
           RECORDING MODE IS F                                          00880000
           LABEL RECORDS ARE STANDARD                                   00890000
           BLOCK CONTAINS 0 RECORDS.                                    00900000
       01  INPUT-CONTROL-REC        PIC X(80).                          00910000
                                                                        00920000
                                                                        01000000
W30900 FD  FILESET-CONTROL-FILE                                         01001023
W30900     LABEL RECORDS ARE OMITTED                                    01002023
W30900     RECORDING MODE IS F                                          01003023
W30900     BLOCK CONTAINS 0 RECORDS.                                    01004023
W30900 01  FILESET-CONTROL-RECORD   PIC X(80).                          01005023
W30900                                                                  01006023
       FD  STORE-MERGE-FILE-OUT                                         01010000
           LABEL RECORDS ARE OMITTED                                    01020000
           RECORDING MODE IS F                                          01030000
           BLOCK CONTAINS 0 RECORDS.                                    01040000
       01  STORE-MERGE-RECORD       PIC X(140).                         01050000
                                                                        01060000
       FD  MFT-CONTROL-CARD-OUT                                         01070041
           LABEL RECORDS ARE OMITTED                                    01080000
           RECORDING MODE IS F                                          01090000
           BLOCK CONTAINS 0 RECORDS.                                    01100000
       01  MFT-CONTROL-CARD-RECORD  PIC X(80).                          01110041
                                                                        01120000
      /*****************************************************************01130023
       WORKING-STORAGE SECTION.                                         01140000
      ******************************************************************01150000
MFTCHG 01  PV-MFT-REC2.                                                 01151041
MFTCHG        10 PV-MFT-CSV-RF2        PIC X(20) VALUE                  01152043
MFTCHG      'SET %DIR1 = %DIR1 + '.                                     01153043
MFTCHG        10  DTATTR-SEA-DESC1     PIC X(6).                        01154041
MFTCHG        10  PV-MFT-HYPHEN        PIC X(1) VALUE '-'.              01155041
MFTCHG        10  DTATTR-FSCL-YR-NBR1  PIC X(4).                        01156041
MFTCHG        10  CC-FILESET-ID1       PIC X(1) VALUE SPACES.           01157041
MFTCHG        10  PV-CYCLE-DESC1       PIC X(05).                       01158041
                                                                        01160000
      *** CC-CONTROL-CARD IS ALSO USED IN MLKUT101, MLKUT102....        01170000
       01  CC-CONTROL-CARD.                                             01180000
           05  CC-INPUT-FLAG.                                           01190000
               10  CC-PROCESSING-MODE         PIC  X(01)  VALUE SPACE.  01200000
               10  FILLER                     PIC  X(01)  VALUE SPACE.  01210000
W30900     05  CC-DATE-DRIVER                 PIC  X(10)  VALUE SPACE.  01220027
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01240000
           05  CC-SEASONS-TO-PROCESS          PIC  9(02)  VALUE ZERO.   01250000
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01260000
           05  CC-DPT-LEVEL-PROCESS           PIC  X(01).               01270000
               88  CC-DEPT-LEVEL                     VALUES ARE 'Y' 'X'.01280000
               88  CC-NO-DEPT                        VALUES ARE 'N' ' '.01290000
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01300000
           05  CC-BYR-LEVEL-PROCESS           PIC  X(01).               01310000
               88  CC-BUYER-LEVEL                    VALUES ARE 'Y' 'X'.01320000
               88  CC-NO-BUYER                       VALUES ARE 'N' ' '.01330000
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01340000
           05  CC-DMM-LEVEL-PROCESS           PIC  X(01).               01350000
               88  CC-DMM-LEVEL                      VALUES ARE 'Y' 'X'.01360000
               88  CC-NO-DMM                         VALUES ARE 'N' ' '.01370000
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01380000
           05  CC-GMM-LEVEL-PROCESS           PIC  X(01).               01390000
               88  CC-GMM-LEVEL                      VALUES ARE 'Y' 'X'.01400000
               88  CC-NO-GMM                         VALUES ARE 'N' ' '.01410000
           05  FILLER                         PIC  X(01)  VALUE SPACE.  01420000
           05  CC-GRP-LEVEL-PROCESS           PIC  X(01).               01430000
               88  CC-BUS-GRP-LEVEL                  VALUES ARE 'Y' 'X'.01440000
               88  CC-NO-BUS-GRP                     VALUES ARE 'N' ' '.01450000
W30900     05  FILLER                         PIC  X(01)  VALUE SPACE.  01461025
W30900     05  CC-PROCESS-CYCLE-ID            PIC  X(03)  VALUE SPACE.  01462026
W30900         88  CC-SEASONAL                       VALUES ARE 'SEA'.  01463026
W30900         88  CC-QUARTERLY                      VALUES ARE 'QTR'.  01464026
W30900         88  CC-MONTHLY                        VALUES ARE 'MON'.  01465026
W30900     05  FILLER                         PIC  X(51)  VALUE SPACE.  01466026
                                                                        01470000
       01  PC-PROGRAM-CONSTANTS.                                        01480000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'MLKUT852'.     01490009
           05  PC-NBR-GRADE-TABLE-ROWS PIC 9(05)  VALUE 99.             01500000
DC    *    05  PC-MAX-ACCUM-ROWS       PIC 9(05)  VALUE 9997.           01510000
DC         05  PC-MAX-ACCUM-ROWS       PIC 9(05)  VALUE 9996.           01520000
           05  PC-MAX-STORE-GRADES     PIC 9(05)  VALUE 9999.           01530000
           05  PC-EDIT-LABEL           PIC X(06)  VALUE SPACES.         01540000
               88  VALID-NEW-STORE-MONTH          VALUES                01550000
                   '000001' '000002' '000003'                           01560000
                   '000007' '000008' '000009'                           01570000
                   '000005' '000006' '000004'                           01580000
                   '000011' '000012' '000010'.                          01590000
FMNTH * CHANGE MONTH NUMBER TO REPRESENT FISCAL MONTH (NOT CALENDAR)    01600022
  |            88  PC-NEW-JAN-STORE           VALUE '000012'.           01610021
  |            88  PC-NEW-FEB-STORE           VALUE '000001'.           01620021
  |            88  PC-NEW-MAR-STORE           VALUE '000002'.           01630021
  |            88  PC-NEW-APR-STORE           VALUE '000003'.           01640021
  |            88  PC-NEW-MAY-STORE           VALUE '000004'.           01650021
  |            88  PC-NEW-JUN-STORE           VALUE '000005'.           01660021
  |            88  PC-NEW-JUL-STORE           VALUE '000006'.           01670021
  |            88  PC-NEW-AUG-STORE           VALUE '000007'.           01680021
  |            88  PC-NEW-SEP-STORE           VALUE '000008'.           01690021
  |            88  PC-NEW-OCT-STORE           VALUE '000009'.           01700021
  |            88  PC-NEW-NOV-STORE           VALUE '000010'.           01710021
FMNTH          88  PC-NEW-DEC-STORE           VALUE '000011'.           01720022
                                                                        01730000
       01  PV-PROGRAM-VARIABLES.                                        01740000
           05  PV-STORE-STAT-READ      PIC S9(09) COMP VALUE +0.        01750000
           05  PV-STORE-MAIN-READ      PIC S9(09) COMP VALUE +0.        01760000
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) COMP VALUE +0.        01770000
           05  PV-GRADE-TBL-CNT        PIC S9(07) COMP VALUE +0.        01780000
ALIGN      05  PV-GRADED-STORES        PIC S9(07) COMP VALUE +0.        01790017
ALIGN      05  PV-ACCUM-GRADED-STORES  PIC S9(07) COMP VALUE +0.        01800017
ALIGN      05  PV-GRADE-ALIGN          PIC S9(07) COMP VALUE +0.        01810017
ALIGN      05  PV-SAVE-INDX            PIC S9(07) COMP VALUE +0.        01820017
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.         01830000
W30900     05  PV-THIS-CYCL-END-DTE    PIC X(10)  VALUE SPACES.         01831027
W30900     05  PV-CYCLE-DESC           PIC X(05)  VALUE SPACES.         01832138
W30900     05  PV-MONTH-DESC.                                           01832228
W30900         10 PV-MONTH-INITIAL-DESC    PIC X(03) VALUE SPACE.       01833028
W30900         10 FILLER                   PIC X(06) VALUE SPACE.       01834028
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(30) VALUE SPACES.      01840000
           05  PV-DISPLAY-ABEND-CODE   PIC ZZZ9   VALUE ZERO.           01850000
           05  PV-SEASON-SAVE          PIC X(01)  VALUE SPACES.         01860000
FALL       05  PV-SEASON-DESC.                                          01870018
FALL           10  FILLER              PIC X(02)  VALUE SPACES.         01880018
FALL           10  PV-DTATTR-SEA-DESC  PIC X(04)  VALUE SPACES.         01890018
           05  PV-YEAR-SAVE            PIC X(04)  VALUE ZEROES.         01900018
           05  PV-NUM-YEAR REDEFINES                                    01910000
               PV-YEAR-SAVE            PIC 9(04).                       01920000
           05  PV-DEPT-NBR-SAVE        PIC X(03)  VALUE SPACES.         01930000
           05  PV-BUYER-NBR-SAVE       PIC X(03)  VALUE SPACES.         01940000
           05  PV-BUS-GRP-NBR-SAVE     PIC X(03)  VALUE SPACES.         01950000
           05  PV-DMA-NBR-SAVE         PIC X(02)  VALUE SPACES.         01960000
           05  PV-GMA-NBR-SAVE         PIC X(02)  VALUE SPACES.         01970000
           05  PV-PERCENT           PIC SV9999    COMP-3 VALUE ZEROS.   01980000
           05  PV-STORE-COUNT       PIC S9(07)V9  COMP-3 VALUE ZEROS.   01990000
           05  PV-STORE-COUNT-ROUND PIC S9(07)    COMP-3 VALUE ZEROS.   02000000
           05  PV-WORK-AMOUNT       PIC S9(12)V99   COMP-3 VALUE ZEROS. 02010000
           05  PV-WORK-AMOUNT2      PIC S9(12)V9999 COMP-3 VALUE ZEROS. 02020000
           05  PV-WORK-AMOUNT3      PIC S9(12)V9999 COMP-3 VALUE ZEROS. 02030000
           05  PV-WORK-AMOUNT4         PIC 9(02)           VALUE ZEROS. 02040000
           05  PV-GRADE-PCT-REDEFINE   PIC 99V99.                       02050000
           05  PV-GRADE-PERCENT REDEFINES                               02060000
               PV-GRADE-PCT-REDEFINE.                                   02070000
               10  PV-GRADE-WHOLE      PIC X(02).                       02080000
               10  PV-GRADE-PCT        PIC X(02).                       02090000
           05  PV-STORE-TYPE           PIC X(06)  VALUE SPACES.         02100000
           05  PV-STORE-TYPE-RED REDEFINES                              02110000
               PV-STORE-TYPE.                                           02120000
               10  PV-STORE-TYPE-P1    PIC 9(04).                       02130000
               10  PV-STORE-TYPE-NUM   PIC 9(02).                       02140000
W30900     05  PV-DIVIDE-NUMBER        PIC 9(02)       VALUE ZEROS.     02141039
           05  GRADE-INDX              PIC S9(07) COMP VALUE ZEROS.     02150000
           05  SGT-INDX                PIC S9(07) COMP VALUE ZEROS.     02160000
           05  SGST-INDX               PIC S9(07) COMP VALUE ZEROS.     02170000
           05  ABEND-CODE              PIC S9(04) COMP SYNC VALUE 4013. 02180000
W30900 01  FILESET-CONTROL-CC.                                          02191023
W30900*  MFT FILESET ID WILL BE CHANGED UPON WHEN IT IS PROCESSED       02192041
W30900     05  CC-FILESET-ID          PIC  X(01)   VALUE SPACES.        02193024
W30900     05  CC-FILLER              PIC  X(79)   VALUE SPACES.        02194024
                                                                        02195023
       01  PS-PROGRAM-SWITCHES.                                         02200000
           05  PS-INP01-DONE-SW        PIC  X(01)      VALUE 'N'.       02210000
               88  PS-INP01-DONE                       VALUE 'Y'.       02220000
           05  PS-INP02-DONE-SW        PIC  X(01)      VALUE 'N'.       02230000
               88  PS-INP02-DONE                       VALUE 'Y'.       02240000
           05  PS-INP03-DONE-SW        PIC  X(01)      VALUE 'N'.       02250000
               88  PS-INP03-DONE                       VALUE 'Y'.       02260000
           05  PS-INP05-DONE-SW        PIC  X(01)      VALUE 'N'.       02270000
               88  PS-INP05-DONE                       VALUE 'Y'.       02280000
           05  PS-UNLOAD-TBL-SW        PIC  X(01)      VALUE 'N'.       02290000
               88  UNLOAD-TBL-COMPLETE                 VALUE 'Y'.       02300000
           05  PS-GRADE-FOUND-SW       PIC  X(01)      VALUE 'N'.       02310000
               88  GRADE-FOUND                         VALUE 'Y'.       02320000
           05  PS-GRADE-LVL-SW         PIC  X(01)      VALUE 'N'.       02330000
               88  GRADE-LVL-PROCESSED                 VALUE 'Y'.       02340000
           05  PS-NEW-STORE-SW         PIC  X(01)      VALUE 'N'.       02350000
               88  NEW-STORE-PROCESSED                 VALUE 'Y'.       02360000
DC         05  PS-DC-LOC-SW            PIC  X(01)      VALUE 'N'.       02370000
DC             88  DC-LOC-PROCESSED                    VALUE 'Y'.       02380000
DC         05  PS-GRADE-BALANCE-SW     PIC  X(01)      VALUE 'N'.       02390012
DC             88  PS-GRADE-BALANCED                   VALUE 'Y'.       02400012
                                                                        02410000
      ***************************************************************** 02420000
       01  GRADE-TABLE.                                                 02430000
           05  GRADE-ITEM OCCURS 99 TIMES.                              02440000
               10 GT-GRADE-NAME       PIC X(01).                        02450000
               10 GT-GRADE-PERCENT    PIC 99V99.                        02460000
                                                                        02470000
      ******************************************************************02480000
       01  STORE-GRADE-TBL.                                             02490000
           05  SGT-GRADE-ELEM OCCURS 9999 TIMES.                        02500000
               10  SGT-SEASON        PIC X(01)  VALUE SPACES.           02510000
               10  SGT-YEAR          PIC X(04)  VALUE SPACES.           02520000
               10  SGT-DEPT-NBR      PIC X(03)  VALUE SPACES.           02530000
               10  SGT-BUYER-NBR     PIC X(03)  VALUE SPACES.           02540000
               10  SGT-BUS-GRP-NBR   PIC X(03)  VALUE SPACES.           02550000
               10  SGT-DMA-NBR       PIC X(02)  VALUE SPACES.           02560000
               10  SGT-GMA-NBR       PIC X(02)  VALUE SPACES.           02570000
               10  SGT-STORE-GRADE   PIC X(01)  VALUE SPACE.            02580000
               10  SGT-STORE-COUNT   PIC S9(07) COMP-3 VALUE ZEROS.     02590000
               10  SGT-TOTAL-STORE   PIC S9(07) COMP-3 VALUE ZEROS.     02600000
               10  SGT-STORE-ACCUM   PIC S9(07) COMP-3 VALUE ZEROS.     02610000
                                                                        02620000
      ******************************************************************02630000
       01  STORE-GRADE-STORE-TBL.                                       02640000
           05  SGST-STORE-ENTRY OCCURS 9999 TIMES.                      02650000
               10  SGST-SEQUENCE      PIC 9(02)     VALUE ZEROS.        02660000
               10  SGST-SEASON        PIC X(01)     VALUE SPACES.       02670000
               10  SGST-YEAR          PIC X(04)     VALUE SPACES.       02680000
               10  SGST-DEPT-NBR      PIC X(03)     VALUE SPACES.       02690000
               10  SGST-BUYER-NBR     PIC X(03)     VALUE SPACES.       02700000
               10  SGST-BUS-GRP-NBR   PIC X(03)     VALUE SPACES.       02710000
               10  SGST-DMA-NBR       PIC X(02)     VALUE SPACES.       02720000
               10  SGST-GMA-NBR       PIC X(02)     VALUE SPACES.       02730000
               10  SGST-LABEL         PIC X(06)     VALUE SPACES.       02740000
               10  SGST-SLS-RTL-AMT   PIC S9(12)V99 COMP-3 VALUE ZEROS. 02750000
               10  SGST-END-INV-RTL   PIC S9(12)V99 COMP-3 VALUE ZEROS. 02760000
               10  SGST-MKDN-RTL      PIC S9(12)V99 COMP-3 VALUE ZEROS. 02770000
               10  SGST-STORE-ACCUM   PIC S9(07)    COMP-3 VALUE ZEROS. 02780000
                                                                        02790000
      /*****************************************************************02800000
      * COPYBOOK - CALENDAR ROUTINE                                     02810000
      ******************************************************************02820000
           COPY DPWS500.                                                02830000
                                                                        02840000
      /**************************************************************** 02850000
      * COPYBOOK -                                                      02860000
      * MAIN DATA EXTRACT LAYOUT FOR SALES & INVENTORY PERFORMANCE BY   02870000
      * STORE GRADE.                                                    02880000
      ***************************************************************** 02890000
           COPY MLRD041.                                                02900000
                                                                        02910000
      /**************************************************************** 02920000
      * COPYBOOK -                                                      02930000
      * STORE STATISTIC EXTRACT LAYOUT FOR SALES & INVENTORY            02940000
      * PERFORMANCE BY STORE GRADE                                      02950000
      ***************************************************************** 02960000
           COPY MLRD042.                                                02970000
                                                                        02980000
      /**************************************************************** 02990000
      * COPYBOOK - RANKED SALES & INVENTORY PERFORMANCE EXTRACT         03000000
      ***************************************************************** 03010000
           COPY MLRD043.                                                03020000
                                                                        03030000
      /*****************************************************************03040000
      *  SQL ABEND ROUTINE WORKING STORAGE                              03050000
      ******************************************************************03060000
           COPY DPWS004.                                                03070000
                                                                        03080000
      ******************************************************************03090000
      * DB2 TABLE DCLGEN - TDTATTR                                      03100000
      ******************************************************************03110000
           EXEC SQL                                                     03120000
               INCLUDE TDTATTR                                          03130000
           END-EXEC.                                                    03140000
                                                                        03150000
      ******************************************************************03160000
      * DB2 TABLE ERROR HANDLING                                        03170000
      ******************************************************************03180000
           EXEC SQL                                                     03190000
               INCLUDE SQLCA                                            03200000
           END-EXEC.                                                    03210000
                                                                        03220000
      ***************************************************************** 03230000
           COPY SQLCA2.                                                 03240000
                                                                        03250000
      /*****************************************************************03260000
      *FLOW:      THE MODULE GENERATES A COMMA DELIMITED FILE FOR THE   03270000
      *        SALES AND INVENTORY PERFORMANCE BY GRADE REPORTING.      03280000
      *                                                                 03290000
      *           THE MODULE SEQUENTIALLY READS THE CONTROL CARD FSCC00803300000
      *        TO FILL THE GRADE LEVEL TABLE. THE GRADE LEVEL TABLES    03310000
      *        HOLDS INFORMATION REGARDING THE COMP/NON COMP STORE      03320000
      *        GRADE PERCENTS.                                          03330000
      *                                                                 03340000
      *           THE MODULE SEQUENTIALLY READS THE STORE STATISTICS    03350000
      *        FILE DEVLOPING THE NUMBER OF STORES APPLICABLE TO EACH   03360000
      *        GRADE LEVEL. THE STORE GRADE TABLE IS LOADED WITH        03370000
      *        THIS INFORMATION. GRADING IS DONE AT THE SEASON,         03380000
      *        DEPARTMENT, MBS STRUCTURE, TYPE OF STORE LEVELS. A STORE 03390000
      *        IS CATEGORIZED BASED ON ITS RETAIL SALES.                03400000
      *                                                                 03410000
      *           SEQUENTIALLY READ THE STORE MAIN EXTRACT FILE TOTALING03420000
      *        UP SALES, MARKDOWNS, AND DEVELOPING AN AVERAGE ENDING    03430000
      *        INVENTORY RETAIL BY STORE. IF THE STORE IS A COMP/NON    03440000
      *        COMP STORE, GRADE THE STORE. IF THE STORE IS A NEW STORE,03450000
      *        TOTAL UP IN THE APPROPRIATE NEW STORE OPEN MONTH. THE    03460000
      *        STORE GRADING PROCESS IS CONTROLLED VIA THE STORE GRADE  03470000
      *        TABLE. GRADING IS AT THE SEASON, DEPARTMENT, MBS         03480000
      *        STRUCTURE, TYPE OF STORE, AND SALES RETAIL LEVEL.        03490000
      *                                                                 03500000
      *          AT A BREAK, GENERATE A COMMA DELIMITED FILE RECORD IN  03510000
      *        THE MLRD043 RECORD FORMAT.                               03520000
      *                                                                 03530000
      *          AT END OF FILE, THE MODULE CREATES THE MFT CONTROL CARD03540041
      *        DESIGNED TO PASS THE .CSV FILE TO MIO.                   03550000
      *                                                                 03560000
      *        TABLES USED:                                             03570000
      *            TDTATTR                                              03580000
      *                                                                 03590000
      ******************************************************************03600000
       PROCEDURE DIVISION.                                              03610000
      ******************************************************************03620000
                                                                        03630000
       0100-MAINLINE-MLKUT852.                                          03640009
                                                                        03650000
           PERFORM 1000-INITIALIZATION.                                 03660000
                                                                        03670000
           PERFORM 2000-GENERATE-CSV-RECORDS                            03680000
               UNTIL PS-INP03-DONE.                                     03690000
                                                                        03700000
           PERFORM 9000-END-OF-JOB-TASKS.                               03710000
                                                                        03720000
       9999-EXIT-PROGRAM.                                               03730000
           STOP RUN.                                                    03740000
                                                                        03750000
      /*****************************************************************03760000
      * 1000-INITIALIZATION.                                            03770000
      ******************************************************************03780000
       1000-INITIALIZATION.                                             03790000
                                                                        03800000
           DISPLAY '******************************************'.        03810000
           DISPLAY '*  START OF ', PC-PROGRAM-NAME                      03820000
           DISPLAY '******************************************'.        03830000
                                                                        03840000
           OPEN  INPUT STR-GRADE-PCT-GUIDE-IN                           03850000
                 INPUT STR-STAT-EXTRACT-IN                              03860000
                 INPUT STR-MAIN-EXTRACT-IN                              03870000
                 INPUT INPUT-CONTROL-FILE                               03880000
W30900           INPUT FILESET-CONTROL-FILE                             03891023
                OUTPUT STORE-MERGE-FILE-OUT                             03900000
                OUTPUT MFT-CONTROL-CARD-OUT.                            03910041
                                                                        03920000
      *** INITIALIZE FIELDS....                                         03930000
           INITIALIZE GRADE-TABLE.                                      03940000
           INITIALIZE STORE-GRADE-TBL.                                  03950000
           INITIALIZE STORE-GRADE-STORE-TBL.                            03960000
DC    *    INITIALIZE DC-STORE-TBL.                                     03970000
           MOVE ZEROS               TO PV-STORE-STAT-READ.              03980000
           MOVE ZEROS               TO PV-STORE-MAIN-READ.              03990000
           MOVE ZEROS               TO PV-TOTAL-EXTR-WRITTEN.           04000000
           MOVE ZEROS               TO PV-GRADE-TBL-CNT.                04010000
           MOVE 'N'                 TO PS-INP01-DONE-SW.                04020000
           MOVE 'N'                 TO PS-INP02-DONE-SW.                04030000
           MOVE 'N'                 TO PS-INP03-DONE-SW.                04040000
           MOVE 'N'                 TO PS-INP05-DONE-SW.                04050000
           MOVE 'N'                 TO PS-UNLOAD-TBL-SW.                04060000
           MOVE 'N'                 TO PS-GRADE-FOUND-SW.               04070000
           MOVE 'N'                 TO PS-GRADE-LVL-SW.                 04080000
           MOVE 'N'                 TO PS-NEW-STORE-SW.                 04090000
DC         MOVE 'N'                 TO PS-DC-LOC-SW.                    04100000
                                                                        04110000
      *** READ THE CONTROL CARD.....                                    04120000
           PERFORM 4400-READ-CONTROL-FILE.                              04130000
W30900     PERFORM 4700-READ-FILESET-CONTROL-FILE.                      04131023
                                                                        04140000
      *** LOAD THE STORE GRADES FROM THE CONTROL CARD FSCC008...        04150000
           MOVE ZEROS               TO GRADE-INDX.                      04160000
           PERFORM 2100-LOAD-GRADE-TABLE.                               04170000
                                                                        04180000
      *** READ THE STORE STATISTICS EXTRACT FILE...                     04190000
           PERFORM 4000-READ-STAT-EXTRACT-FILE.                         04200000
           IF PS-INP02-DONE                                             04210000
               DISPLAY '*************************************'          04220000
               DISPLAY '**  PROGRAM = ', PC-PROGRAM-NAME                04230000
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             04240000
               DISPLAY '**'                                             04250000
               DISPLAY '**  NO STAT EXTRACT RECORDS FOUND'              04260000
               DISPLAY '**  INP02'                                      04270000
               DISPLAY '*************************************'          04280000
               MOVE 'Y'             TO PS-INP03-DONE-SW                 04290000
           END-IF.                                                      04300000
                                                                        04310000
      ***   CALCULATE THE STORE GRADES BASED ON THE STORE STATISTICS    04320000
      *** EXTRACT FILE. STORE GRADES ARE BASED ON THE STORE STATISTICS  04330000
      *** EXTRACT FILE.                                                 04340000
           IF (NOT PS-INP02-DONE)                                       04350000
               MOVE ZEROS           TO SGT-INDX                         04360000
               PERFORM 2200-LOAD-MBS-GRADE-TABLE                        04370000
                   UNTIL PS-INP02-DONE                                  04380000
           END-IF.                                                      04390000
                                                                        04400000
      *** READ THE MAIN EXTRACT FILE...                                 04410000
           PERFORM 4100-READ-MAIN-EXTRACT-FILE.                         04420000
           IF PS-INP03-DONE                                             04430000
               DISPLAY '*************************************'          04440000
               DISPLAY '**  PROGRAM = ', PC-PROGRAM-NAME                04450000
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             04460000
               DISPLAY '**'                                             04470000
               DISPLAY '**  NO MAIN EXTRACT RECORDS FOUND'              04480000
               DISPLAY '**  INP03'                                      04490000
               DISPLAY '*************************************'          04500000
           END-IF.                                                      04510000
                                                                        04520000
      *** FIRST TIME THROUGH PROCESSING...                              04530000
           IF (NOT PS-INP03-DONE)                                       04540000
               PERFORM 4300-BUILD-HEADER                                04550000
               MOVE ML041-SEASON       TO PV-SEASON-SAVE                04560000
               MOVE ML041-YEAR         TO PV-YEAR-SAVE                  04570000
               MOVE ML041-DEPT-NBR     TO PV-DEPT-NBR-SAVE              04580000
               MOVE ML041-BUYER-NBR    TO PV-BUYER-NBR-SAVE             04590000
               MOVE ML041-BUS-GRP-NBR  TO PV-BUS-GRP-NBR-SAVE           04600000
               MOVE ML041-DMA-NBR      TO PV-DMA-NBR-SAVE               04610000
               MOVE ML041-GMA-NBR      TO PV-GMA-NBR-SAVE               04620000
           END-IF.                                                      04630000
                                                                        04640000
      /*****************************************************************04650000
      * 2000-GENERATE-CSV-RECORDS.                                      04660000
      *     THE ROUTINE'S MAIN FUNCTION IS TO ACCUMULATE, RANK, AND THEN04670000
      * GENERATE CSV RECORDS FOR THE INVENTORY STORE GRADE CSV FILE.    04680000
      ******************************************************************04690000
       2000-GENERATE-CSV-RECORDS.                                       04700000
                                                                        04710000
      *** ACCUMULATE AND BUILD FORMATTED STORE GRADE EXTRACT INFO...    04720000
           IF ML041-SEASON EQUAL PV-SEASON-SAVE           AND           04730000
              ML041-YEAR EQUAL PV-YEAR-SAVE               AND           04740000
              ML041-DEPT-NBR EQUAL PV-DEPT-NBR-SAVE       AND           04750000
              ML041-BUYER-NBR EQUAL PV-BUYER-NBR-SAVE     AND           04760000
              ML041-BUS-GRP-NBR EQUAL PV-BUS-GRP-NBR-SAVE AND           04770000
              ML041-DMA-NBR EQUAL PV-DMA-NBR-SAVE         AND           04780000
              ML041-GMA-NBR EQUAL PV-GMA-NBR-SAVE                       04790000
               PERFORM 3200-BUILD-STORE-RANK                            04800000
           ELSE                                                         04810000
               MOVE ZEROS           TO SGST-INDX                        04820000
               MOVE 'N'             TO PS-UNLOAD-TBL-SW                 04830000
               PERFORM 4200-FORMAT-RECORD                               04840000
                   UNTIL UNLOAD-TBL-COMPLETE                            04850000
                                                                        04860000
               INITIALIZE STORE-GRADE-STORE-TBL                         04870000
                                                                        04880000
               MOVE ML041-SEASON       TO PV-SEASON-SAVE                04890000
               MOVE ML041-YEAR         TO PV-YEAR-SAVE                  04900000
               MOVE ML041-DEPT-NBR     TO PV-DEPT-NBR-SAVE              04910000
               MOVE ML041-BUYER-NBR    TO PV-BUYER-NBR-SAVE             04920000
               MOVE ML041-BUS-GRP-NBR  TO PV-BUS-GRP-NBR-SAVE           04930000
               MOVE ML041-DMA-NBR      TO PV-DMA-NBR-SAVE               04940000
               MOVE ML041-GMA-NBR      TO PV-GMA-NBR-SAVE               04950000
                                                                        04960000
               PERFORM 3200-BUILD-STORE-RANK                            04970000
                                                                        04980000
           END-IF.                                                      04990000
                                                                        05000000
      *** READ THE MAIN EXTRACT FILE...                                 05010000
           PERFORM 4100-READ-MAIN-EXTRACT-FILE.                         05020000
                                                                        05030000
      ******************************************************************05040000
      * 2100-LOAD-GRADE-TABLE.                                          05050000
      *     LOAD THE GRADE LEVELS PASSED INTO THE PROGRAM THROUGH THE   05060000
      * CONTROL CARD FSCC008.                                           05070000
      ******************************************************************05080000
       2100-LOAD-GRADE-TABLE.                                           05090000
                                                                        05100000
           MOVE '2100-LOAD-GRADE-TABLE' TO PV-PARAGRAPH-NAME.           05110000
                                                                        05120000
           DISPLAY '***********  GRADE TABLE PERCENTS  ***********'     05130000
                                                                        05140000
           PERFORM UNTIL PS-INP01-DONE                                  05150000
               READ STR-GRADE-PCT-GUIDE-IN NEXT                         05160000
                   AT END      MOVE 'Y' TO PS-INP01-DONE-SW             05170000
                   NOT AT END  PERFORM 3000-GRADE-TABLE-INIT            05180000
               END-READ                                                 05190000
           END-PERFORM.                                                 05200000
                                                                        05210000
           DISPLAY '** GRADE LEVEL COUNT:  ' PV-GRADE-TBL-CNT.          05220000
                                                                        05230000
      ******************************************************************05240000
      * 2200-LOAD-MBS-GRADE-TABLE.                                      05250000
      *     HERE WE BUILD THE ACTUAL GRADE LEVELS BY SEASON AND MBS     05260000
      * STRUCTURE FROM THE GRADE TABLE AND THE STORE STATISTICS FILE    05270000
      * EXTRACT. THE GRADE TABLE HOLDS THE GRADE LEVEL PERCENTS WHILE   05280000
      * THE STORE STAT EXTRACT FILE HOLDS THE STORES COUNTS AT THE      05290000
      * SEASON AND MBS STRUCTURE LEVEL.                                 05300000
      ******************************************************************05310000
       2200-LOAD-MBS-GRADE-TABLE.                                       05320000
                                                                        05330000
           MOVE '2200-LOAD-MBS-GRADE-TABLE' TO PV-PARAGRAPH-NAME.       05340000
                                                                        05350000
           MOVE ZEROS               TO GRADE-INDX.                      05360000
           PERFORM 3100-GRADE-STORES                                    05370000
             VARYING GRADE-INDX FROM 1 BY 1                             05380013
               UNTIL GRADE-INDX GREATER PC-NBR-GRADE-TABLE-ROWS.        05390000
                                                                        05400000
ALIGN ** MAKE SURE GRADES TOT CALCED AMTS                               05410020
ALIGN      PERFORM 3300-BALANCE-GRADE-LEVEL.                            05420017
                                                                        05430004
           PERFORM 4000-READ-STAT-EXTRACT-FILE.                         05440000
                                                                        05450000
      /*****************************************************************05460000
      * 3000-GRADE-TABLE-INIT.                                          05470000
      *     LOAD THE GRADE LEVELS PASSED INTO THE PROGRAM THROUGH THE   05480000
      * CONTROL CARD FSCC008.                                           05490000
      ******************************************************************05500000
       3000-GRADE-TABLE-INIT.                                           05510000
                                                                        05520000
           IF SG-GP-WHOLE IS NOT NUMERIC         OR                     05530000
              SG-GP-PERIOD IS NOT EQUAL TO '.'   OR                     05540000
              SG-GP-FRACTION IS NOT NUMERIC                             05550000
               DISPLAY '** GRADE CONTROL CARD: ' STR-GRADE-PCT-RECORD   05560000
               DISPLAY '** GRADE PERCENT IS NOT NUMERIC'                05570000
               MOVE '3000-GRADE-TABLE-INIT' TO PV-PARAGRAPH-NAME        05580000
               PERFORM 9900-ABEND                                       05590000
           END-IF.                                                      05600000
                                                                        05610000
           IF SG-GRADE-PERCENT IS EQUAL TO ZERO                         05620000
               DISPLAY '** GRADE CONTROL CARD: ' STR-GRADE-PCT-RECORD   05630000
               DISPLAY '** GRADE PERCENT IS ZERO'                       05640000
               MOVE '3000-GRADE-TABLE-INIT' TO PV-PARAGRAPH-NAME        05650000
               PERFORM 9900-ABEND                                       05660000
           END-IF.                                                      05670000
                                                                        05680000
           ADD +1                   TO PV-GRADE-TBL-CNT.                05690000
                                                                        05700000
           IF PV-GRADE-TBL-CNT > PC-NBR-GRADE-TABLE-ROWS                05710000
               DISPLAY '** GRADE CONTROL FILE HAS TOO MANY ENTRIES'     05720000
               MOVE '3000-GRADE-TABLE-INIT' TO PV-PARAGRAPH-NAME        05730000
               PERFORM 9900-ABEND                                       05740000
           END-IF.                                                      05750000
                                                                        05760000
           ADD 1                    TO GRADE-INDX.                      05770000
           MOVE SG-GRADE-NAME       TO GT-GRADE-NAME(GRADE-INDX).       05780000
           MOVE SG-GRADE-PERCENT    TO GT-GRADE-PERCENT(GRADE-INDX).    05790000
           DISPLAY '**  GRADE: ' SG-GRADE-NAME                          05800000
                   ' IS ' SG-GRADE-PERCENT ' PERCENT'.                  05810000
                                                                        05820000
      /*****************************************************************05830000
      * 3100-GRADE-STORES.                                              05840000
      *     STORE GRADING IS BASED ON THE TOTAL COMP/NON COMP STRS      05850000
      * BY MBS STRUCTURE. WE COUNT THE STORES AND APPLY THE GRADE LEVEL 05860000
      * PERCENT TO DETERMINE THE NUMBER OF STORES AT EACH GRADE LEVEL.  05870000
      *     THE STORE GRADE TABLE IS BUILT OFF THIS INFORMATION.        05880000
      ******************************************************************05890000
       3100-GRADE-STORES.                                               05900000
                                                                        05910000
      *** ONLY PROCESS GRADE TABLE ROWS WHERE WE ACTUALLY HAVE A GRADE  05920000
      *** LEVEL.                                                        05930000
           IF GT-GRADE-NAME (GRADE-INDX) NOT EQUAL SPACES AND           05940006
              GT-GRADE-PERCENT (GRADE-INDX) NOT EQUAL ZEROS             05950000
                                                                        05960000
      *** ABEND IF THE STORE GRADE TABLE IS FULL....                    05970000
             ADD 1                    TO SGT-INDX                       05980000
             IF SGT-INDX GREATER PC-MAX-STORE-GRADES                    05990000
               DISPLAY '** STORE GRADE TBL HAS TOO MANY ENTRIES'        06000000
               MOVE '3100-GRADE-STORES' TO PV-PARAGRAPH-NAME            06010000
               PERFORM 9900-ABEND                                       06020000
             END-IF                                                     06030000
                                                                        06040000
      *** BUILD THE STORE GRADES BASED ON GRADE-TABLE, SEASON, &        06050000
      *** THE MBS STRUCTURE.                                            06060000
             MOVE ML042-SEASON        TO SGT-SEASON (SGT-INDX)          06070000
             MOVE ML042-YEAR          TO SGT-YEAR (SGT-INDX)            06080000
             MOVE ML042-DEPT-NBR      TO SGT-DEPT-NBR (SGT-INDX)        06090000
             MOVE ML042-BUYER-NBR     TO SGT-BUYER-NBR (SGT-INDX)       06100000
             MOVE ML042-BUS-GRP-NBR   TO SGT-BUS-GRP-NBR (SGT-INDX)     06110000
             MOVE ML042-DMA-NBR       TO SGT-DMA-NBR (SGT-INDX)         06120000
             MOVE ML042-GMA-NBR       TO SGT-GMA-NBR (SGT-INDX)         06130000
                                                                        06140000
      *** COUNT THE TOTAL NUMBER OF STORES....                          06150000
             COMPUTE SGT-TOTAL-STORE (SGT-INDX)                         06160000
                                    = ML042-COMP-STORE-COUNT            06170000
                                    + ML042-NON-COMP-STORE              06180000
                                    + ML042-NEW-STORE-MN-1              06190000
                                    + ML042-NEW-STORE-MN-2              06200000
                                    + ML042-NEW-STORE-MN-3              06210000
                                    + ML042-NEW-STORE-MN-4              06220000
                                    + ML042-NEW-STORE-MN-5              06230000
                                    + ML042-NEW-STORE-MN-6              06240000
                                                                        06250000
      *** COMP STORE GRADING PROCESS. BUILD A STORE COUNT BASED ON THE  06260000
      *** GRADE TABLE PERCENT. THIS COUNT TELLS THE APPLICATION HOW     06270000
      *** MANY STORES ARE IN EACH GRADE LEVEL...                        06280000
             COMPUTE PV-PERCENT     = GT-GRADE-PERCENT (GRADE-INDX)     06290000
                                    / 100                               06300000
             COMPUTE PV-STORE-COUNT ROUNDED                             06310000
                                    = ((ML042-COMP-STORE-COUNT          06320000
                                    +  ML042-NON-COMP-STORE)            06330000
                                    *  PV-PERCENT)                      06340000
             COMPUTE PV-STORE-COUNT-ROUND ROUNDED = PV-STORE-COUNT      06350000
             MOVE PV-STORE-COUNT-ROUND TO SGT-STORE-COUNT (SGT-INDX)    06360000
                                                                        06370000
      *** PASS GRADE NAME VERBIAGE TO THE TABLE.                        06380000
             MOVE GT-GRADE-NAME (GRADE-INDX)                            06390000
                                    TO SGT-STORE-GRADE (SGT-INDX)       06400000
             MOVE ZEROS             TO SGT-STORE-ACCUM (SGT-INDX)       06410000
      **                                                                06420004
           END-IF.                                                      06430004
                                                                        06440000
      ******************************************************************06450000
      * 3200-BUILD-STORE-RANK.                                          06460000
      *     THE ROUTINE ACCUMULATES DOLLARS FROM THE MAIN EXTRACT FILE  06470000
      * AND CATEGORIZES THE INFORMATION BASED ON THE TYPE OF STORE.     06480000
      ******************************************************************06490000
       3200-BUILD-STORE-RANK.                                           06500000
                                                                        06510000
           IF ML041-COMP-STORE                                          06520000
               PERFORM 5000-PROCESS-COMP-STORE                          06530000
           ELSE                                                         06540000
           IF ML041-NON-COMP-STORE                                      06550000
               PERFORM 5100-PROCESS-NCOMP-STORE                         06560000
           ELSE                                                         06570000
           IF ML041-NEW-STORE                                           06580000
               PERFORM 5200-PROCESS-NEW-STORE                           06590000
           ELSE                                                         06600000
DC         IF ML041-DC-LOCATION                                         06610000
DC             PERFORM 5250-PROCESS-DC-LOCATION                         06620000
DC         ELSE                                                         06630000
               DISPLAY '** STORE TYPE NOT FOUND'                        06640000
               MOVE '3200-BUILD-STORE-RANK' TO PV-PARAGRAPH-NAME        06650000
               PERFORM 9900-ABEND                                       06660000
           END-IF.                                                      06670000
                                                                        06680000
      /**************************************************************** 06690004
      * 3300-BALANCE-GRADE-LEVEL.                                       06700004
      ***************************************************************** 06710019
      * EACH STORE GRADE IS CALCULATED TO CONTAIN A NUMBER OF STORES    06720019
      * BASED ON THE INPUT 01 PERCENTAGES. DUE TO ROUNDING, THE TOTAL   06730019
      * NUMBER OF GRADED STORES MAY NOT EQUAL THE TOTAL STORE COUNT.    06740019
      * IN THIS CASE, DETERMINE THE OVERAGE/SHORTAGE STORE COUNT AND    06750019
      * ADD TO THE LAST GRADE (CURRENTLY THE 'G' GRADE).                06760019
      ***************************************************************** 06770019
       3300-BALANCE-GRADE-LEVEL.                                        06780007
                                                                        06790004
           MOVE SGT-INDX TO PV-SAVE-INDX.                               06800012
           MOVE ZEROES   TO PV-ACCUM-GRADED-STORES.                     06810012
                                                                        06820012
      *** COUNT THE TOTAL NUMBER OF STORES....                          06830012
           COMPUTE PV-GRADED-STORES = ML042-COMP-STORE-COUNT            06840012
                                    + ML042-NON-COMP-STORE.             06850012
                                                                        06860012
      *** COUNT                                                         06870012
           PERFORM                                                      06880012
               PV-GRADE-TBL-CNT TIMES                                   06890012
                                                                        06900012
                   ADD SGT-STORE-COUNT(PV-SAVE-INDX)                    06910013
                    TO PV-ACCUM-GRADED-STORES                           06920013
                                                                        06930012
                   COMPUTE PV-SAVE-INDX = (PV-SAVE-INDX - 1)            06940013
                                                                        06950012
           END-PERFORM.                                                 06960012
                                                                        06970012
           COMPUTE PV-GRADE-ALIGN =                                     06980012
                   PV-GRADED-STORES - PV-ACCUM-GRADED-STORES            06990016
                                                                        07000012
           IF PV-GRADE-ALIGN NOT EQUAL ZERO                             07010012
               ADD PV-GRADE-ALIGN TO SGT-STORE-COUNT(SGT-INDX)          07020016
               DISPLAY 'ALIGN GRADING FOR DEPT ', ML042-DEPT-NBR        07030012
           END-IF.                                                      07040012
                                                                        07050012
                                                                        07060012
                                                                        07070004
      /**************************************************************** 07080000
      * 4000-READ-STAT-EXTRACT-FILE.                                    07090000
      ***************************************************************** 07100000
       4000-READ-STAT-EXTRACT-FILE.                                     07110000
                                                                        07120000
           READ STR-STAT-EXTRACT-IN NEXT                                07130000
               INTO ML042-STORE-STATISTIC-EXTRACT                       07140000
             AT END      MOVE 'Y'   TO PS-INP02-DONE-SW                 07150000
             NOT AT END  ADD +1     TO PV-STORE-STAT-READ               07160000
           END-READ.                                                    07170000
                                                                        07180000
      /**************************************************************** 07190000
      * 4100-READ-MAIN-EXTRACT-FILE.                                    07200000
      ***************************************************************** 07210000
       4100-READ-MAIN-EXTRACT-FILE.                                     07220000
                                                                        07230000
           READ STR-MAIN-EXTRACT-IN NEXT                                07240000
               INTO ML041-SALES-INVENTORY-EXTRACT                       07250000
             AT END      MOVE 'Y'   TO PS-INP03-DONE-SW                 07260000
             NOT AT END  ADD +1     TO PV-STORE-MAIN-READ               07270000
           END-READ.                                                    07280000
                                                                        07290000
      ***************************************************************** 07300000
      * 4200-FORMAT-RECORD.                                             07310000
      *     FORMAT THE CSV RECORD FROM THE STORE GRADE STORE TABLE. THE 07320000
      * RECORD IS BASED ON THE MLRD043 RECORD.                          07330000
      ***************************************************************** 07340000
       4200-FORMAT-RECORD.                                              07350000
                                                                        07360000
           ADD 1                    TO SGST-INDX.                       07370000
                                                                        07380000
      ***   GENERATE A CSV RECORD FOR EACH ELEMENT IN THE STORE GRADE   07390000
      *** STORE TABLE WHICH HAS VALUE....                               07400000
           IF SGST-INDX IS GREATER THAN PC-MAX-STORE-GRADES             07410000
              MOVE 'Y'              TO PS-UNLOAD-TBL-SW                 07420000
           ELSE                                                         07430000
           IF SGST-SEASON (SGST-INDX) NOT EQUAL SPACE  OR               07440000
              SGST-YEAR (SGST-INDX) NOT EQUAL SPACE  OR                 07450000
              SGST-DEPT-NBR (SGST-INDX) NOT EQUAL SPACE  OR             07460000
              SGST-BUYER-NBR (SGST-INDX) NOT EQUAL SPACE  OR            07470000
              SGST-BUS-GRP-NBR (SGST-INDX) NOT EQUAL SPACE  OR          07480000
              SGST-DMA-NBR (SGST-INDX) NOT EQUAL SPACE  OR              07490000
              SGST-GMA-NBR (SGST-INDX) NOT EQUAL SPACE  OR              07500000
              SGST-LABEL (SGST-INDX) NOT EQUAL SPACE  OR                07510000
              SGST-SLS-RTL-AMT (SGST-INDX) NOT EQUAL ZEROS  OR          07520000
              SGST-END-INV-RTL (SGST-INDX) NOT EQUAL ZEROS  OR          07530000
              SGST-MKDN-RTL (SGST-INDX) NOT EQUAL ZEROS  OR             07540000
              SGST-STORE-ACCUM (SGST-INDX) NOT EQUAL ZEROS              07550000
                                                                        07560000
      *** SEASON AND MBS STRUCTURE SETUP...                             07570000
               MOVE SGST-SEASON (SGST-INDX)      TO ML043-SEASON        07580000
               MOVE SGST-YEAR (SGST-INDX)        TO ML043-YEAR          07590000
               MOVE SGST-DEPT-NBR (SGST-INDX)    TO ML043-DEPT-NBR      07600000
               MOVE SGST-BUYER-NBR (SGST-INDX)   TO ML043-BUYER-NBR     07610000
               MOVE SGST-BUS-GRP-NBR (SGST-INDX) TO ML043-BUS-GRP-NBR   07620000
               MOVE SGST-DMA-NBR (SGST-INDX)     TO ML043-DMA-NBR       07630000
               MOVE SGST-GMA-NBR (SGST-INDX)     TO ML043-GMA-NBR       07640000
                                                                        07650000
      *** BUILD CONTROL LABELS AND OTHER VERBIAGE.                      07660000
               MOVE SPACES            TO ML043-STR-GRADE-PCT            07670000
               MOVE SPACES            TO ML043-STR-GRADE-LTR            07680000
               IF SGST-LABEL (SGST-INDX) IS EQUAL TO 'X     '           07690000
                   MOVE 'TTL C/NC'    TO ML043-CTRL-LABEL               07700000
               END-IF                                                   07710000
               IF SGST-LABEL (SGST-INDX) IS EQUAL TO 'Y     '           07720000
                   MOVE 'TTL NEW '    TO ML043-CTRL-LABEL               07730000
               END-IF                                                   07740000
DC             IF SGST-LABEL (SGST-INDX) IS EQUAL TO 'W     '           07750000
DC                 MOVE 'TTL DC '     TO ML043-CTRL-LABEL               07760020
DC             END-IF                                                   07770020
               IF SGST-LABEL (SGST-INDX) IS EQUAL TO 'Z     '           07780000
                   MOVE 'TOTAL   '    TO ML043-CTRL-LABEL               07790000
               END-IF                                                   07800000
               IF SGST-INDX IS LESS THAN PC-MAX-ACCUM-ROWS              07810000
                   MOVE ZEROS         TO GRADE-INDX                     07820000
                   MOVE 'N'           TO PS-GRADE-FOUND-SW              07830000
                   PERFORM 5400-BUILD-VERBIAGE                          07840000
                       UNTIL GRADE-FOUND                                07850000
               END-IF                                                   07860000
      *                                                                 07870020
      *** FILE SEQUENCING FOR THE FINAL SORT PROCESS.                   07880000
      ***    A.  000        - TOTALS FOR SEASON/MBS STRUCTURE.          07890020
      ***    B.  001 TO 049 - COMP/NON COMP STORE GRADING.              07900020
      ***    C.  050        - TOTALS FOR COMP/NON COMP STORES.          07910020
      ***    D.  060 TO 089 - TOTALS FOR NEW STORES, BY MONTH.          07920020
      ***    E.  090        - TOTAL NEW STORES.                         07930020
      ***    G.  099        - TOTAL DC STORES.                          07940000
               MOVE SGST-SEQUENCE  (SGST-INDX)   TO ML043-DISPLAY-SEQ   07950000
                                                                        07960000
      *** NUMBER OF STORES....                                          07970000
               MOVE SGST-STORE-ACCUM (SGST-INDX)                        07980000
                                    TO ML043-NUMBER-OF-STORES           07990000
                                                                        08000000
      *** SALES RETAIL CALCULATIONS...                                  08010000
               MOVE SGST-SLS-RTL-AMT (SGST-INDX)                        08020000
                                    TO ML043-TOTAL-SLS-RTL              08030000
                                                                        08040000
               IF SGST-STORE-ACCUM (SGST-INDX) IS EQUAL TO ZEROS        08050000
                   MOVE ZEROS       TO PV-WORK-AMOUNT                   08060000
               ELSE                                                     08070000
                   COMPUTE PV-WORK-AMOUNT ROUNDED                       08080000
                                    = SGST-SLS-RTL-AMT (SGST-INDX)      08090000
                                    / SGST-STORE-ACCUM (SGST-INDX)      08100000
               END-IF                                                   08110000
               MOVE PV-WORK-AMOUNT  TO ML043-AVG-SLS-RTL                08120000
                                                                        08130000
      *** INVENTORY RETAIL CALCULATIONS...                              08140000
               MOVE SGST-END-INV-RTL (SGST-INDX)                        08150000
                                    TO ML043-TOTAL-AVG-INV-RTL          08160000
                                                                        08170000
               IF SGST-STORE-ACCUM (SGST-INDX) IS EQUAL TO ZEROS        08180000
                   MOVE ZEROS       TO PV-WORK-AMOUNT                   08190000
               ELSE                                                     08200000
                   COMPUTE PV-WORK-AMOUNT ROUNDED                       08210000
                                    = SGST-END-INV-RTL (SGST-INDX)      08220000
                                    / SGST-STORE-ACCUM (SGST-INDX)      08230000
               END-IF                                                   08240000
               MOVE PV-WORK-AMOUNT  TO ML043-AVG-INV-RTL                08250000
                                                                        08260000
      *** MARKDOWN CALCULATIONS (3 PARTS)...                            08270000
               MOVE SGST-MKDN-RTL (SGST-INDX)                           08280000
                                    TO ML043-TOTAL-MKDN-RTL             08290000
                                                                        08300000
      *   A.  AVERAGE MARKDOWN CALC.....                                08310000
               IF SGST-STORE-ACCUM (SGST-INDX) IS EQUAL TO ZEROS        08320000
                   MOVE ZEROS       TO PV-WORK-AMOUNT2                  08330000
               ELSE                                                     08340000
                   COMPUTE PV-WORK-AMOUNT2 ROUNDED                      08350000
                                    = SGST-MKDN-RTL (SGST-INDX)         08360000
                                    / SGST-STORE-ACCUM (SGST-INDX)      08370000
               END-IF                                                   08380000
                                                                        08390000
      *   B.  AVERAGE SALES CALC.....                                   08400000
               IF SGST-STORE-ACCUM (SGST-INDX) IS EQUAL TO ZEROS        08410000
                   MOVE ZEROS       TO PV-WORK-AMOUNT3                  08420000
               ELSE                                                     08430000
                   COMPUTE PV-WORK-AMOUNT3 ROUNDED                      08440000
                                    = SGST-SLS-RTL-AMT (SGST-INDX)      08450000
                                    / SGST-STORE-ACCUM (SGST-INDX)      08460000
               END-IF                                                   08470000
                                                                        08480000
      *   C.  AVERAGE MARKDOWN PCT CALC...                              08490000
               IF PV-WORK-AMOUNT3 IS EQUAL TO ZEROS                     08500000
                   MOVE ZEROS       TO PV-WORK-AMOUNT                   08510000
               ELSE                                                     08520000
                   COMPUTE PV-WORK-AMOUNT2 ROUNDED                      08530000
                                    = PV-WORK-AMOUNT2                   08540000
                                    / PV-WORK-AMOUNT3                   08550000
                   COMPUTE PV-WORK-AMOUNT ROUNDED                       08560000
                                    = PV-WORK-AMOUNT2 * 100             08570000
               END-IF                                                   08580000
                                                                        08590000
               MOVE PV-WORK-AMOUNT  TO ML043-AVG-MKDN-PCT               08600000
                                                                        08610000
      *** WRITE THE CSV RECORD...                                       08620000
               PERFORM 8000-WRITE-EXTRACT-REC                           08630000
                                                                        08640000
           END-IF.                                                      08650000
                                                                        08660000
      ***************************************************************** 08670000
      * 4300-BUILD-HEADER.                                              08680000
      *     BUILD THE CSV FILE COLUMN HEADERS.                          08690000
      ***************************************************************** 08700000
       4300-BUILD-HEADER.                                               08710000
                                                                        08720000
           MOVE SPACES              TO STORE-MERGE-RECORD.              08730000
                                                                        08740000
           STRING 'SEASN,' DELIMITED BY SIZE                            08750000
                  'DPT,' DELIMITED BY SIZE                              08760000
                  'BYR,' DELIMITED BY SIZE                              08770000
                  'GRP,' DELIMITED BY SIZE                              08780000
                  'DM,' DELIMITED BY SIZE                               08790000
                  'GM,' DELIMITED BY SIZE                               08800000
                  'SEQ' DELIMITED BY SIZE                               08810000
                  'LABEL     ,' DELIMITED BY SIZE                       08820000
                  'STRS  ,' DELIMITED BY SIZE                           08830000
                  'TOT_SLS_RTL    ,' DELIMITED BY SIZE                  08840000
                  'AVG_STR_SLS_RTL,' DELIMITED BY SIZE                  08850000
                  'TOT_AVG_INV    ,' DELIMITED BY SIZE                  08860000
                  'AVG_STR_INV    ,' DELIMITED BY SIZE                  08870000
                  'MKDN           ,' DELIMITED BY SIZE                  08880000
                  'AVG_MKDN_PCT   ' DELIMITED BY SIZE                   08890000
                                    INTO STORE-MERGE-RECORD.            08900000
                                                                        08910000
           WRITE STORE-MERGE-RECORD.                                    08920000
                                                                        08930000
      ******************************************************************08940000
      *4400-READ-CONTROL-FILE.                                          08950000
      ******************************************************************08960000
       4400-READ-CONTROL-FILE.                                          08970000
                                                                        08980000
           READ INPUT-CONTROL-FILE INTO CC-CONTROL-CARD                 08990000
               AT END  DISPLAY '** PROGRAM ABEND **'                    09000000
                       DISPLAY 'MLKUT102  - CONTROL CARD EMPTY'         09010000
                       MOVE +4003              TO ABEND-CODE            09020000
                       PERFORM 9900-ABEND.                              09030000
                                                                        09040000
           DISPLAY '**************************************************'.09050000
           DISPLAY '** ', PC-PROGRAM-NAME, 'CONTROL: ', CC-CONTROL-CARD.09060000
           DISPLAY '**************************************************'.09070000
                                                                        09080000
                                                                        09150000
      *** BUILD THE NEW SEASON DIRECTORY STRUCTURE COMMAND....          09160000
      *    IF FCR-COMMAND IS EQUAL TO 'CD ' or 'cd '                    09170000
      *        INITIALIZE DCLTDTATTR                                    09180000
W30900*        MOVE CC-DATE-DRIVER  TO DTATTR-KY-DTE                    09190027
      *        PERFORM 5500-SELECT-TDTATTR                              09200000
      *                                                                 09210000
      *        PERFORM 5600-DATE-ROUTINE                                09220000
      *                                                                 09230000
      *        STRING MFT-CONTROL-RECORD DELIMITED BY 'yyyy'            09240041
      *            DTATTR-SEA-DESC DELIMITED BY ' '                     09250000
      *            ' ' DELIMITED BY SIZE                                09260000
      *            DTATTR-FSCL-YR-NBR DELIMITED BY SIZE                 09270000
      *            ' Data/' DELIMITED BY SIZE                           09280000
      *                                 INTO MFT-CONTROL-CARD-RECORD    09290041
      *    ELSE                                                         09300000
MFTCHG*** *******************************************************       09301041
MFTCHG*** GENERATE THE FILE FOR MFT                                     09302041
MFTCHG*** *******************************************************       09303041
MFTCHG*** *******************************************************       09304041
               INITIALIZE DCLTDTATTR                                    09305041
W30900         MOVE CC-DATE-DRIVER TO DTATTR-KY-DTE                     09306041
MFTCHG         PERFORM 5500-SELECT-TDTATTR                              09307041
MFTCHG     PERFORM 4700-READ-FILESET-CONTROL-FILE.                      09308041
MFTCHG         MOVE DTATTR-SEA-DESC    TO DTATTR-SEA-DESC1.             09309041
MFTCHG         MOVE DTATTR-FSCL-YR-NBR TO DTATTR-FSCL-YR-NBR1.          09309141
MFTCHG         MOVE PV-CYCLE-DESC      TO PV-CYCLE-DESC1.               09309241
MFTCHG         MOVE CC-FILESET-ID      TO CC-FILESET-ID1.               09309341
MFTCHG         MOVE PV-MFT-REC2        TO MFT-CONTROL-CARD-RECORD.      09309441
MFTCHG         WRITE MFT-CONTROL-CARD-RECORD.                           09309541
                                                                        09510000
                                                                        09580000
                                                                        09610000
W30900******************************************************************09611023
W30900* 4700-READ-FILESET-CONTROL-FILE.                                 09612023
W30900******************************************************************09613023
W30900 4700-READ-FILESET-CONTROL-FILE.                                  09614023
W30900     READ FILESET-CONTROL-FILE                                    09615023
W30900         INTO FILESET-CONTROL-CC                                  09615223
W30900     END-READ.                                                    09619023
W30900                                                                  09619123
      /*****************************************************************09620000
      * 5000-PROCESS-COMP-STORE.                                        09630000
      *     ACCUMULATE GRADE LEVEL INFORMATION INTO THE STORE GRADE     09640000
      * STORE TABLE FROM THE MAIN STATISTICS EXTRACT FILE. THE ROUTINE  09650000
      * ALSO TOTALS UP DOLLARS FOR COMP/NON COMP STORES AND FINAL       09660000
      * TOTALS. THE INFORMATION IS PROCESSED BASED ON THE SEASON AND    09670000
      * MBS STRUCTURE AT A STORE LEVEL.                                 09680000
      ******************************************************************09690000
       5000-PROCESS-COMP-STORE.                                         09700000
                                                                        09710000
      *** COMP STORE TOTALS (BY GRADE & NUMBER OF STORES IN GRADE)....  09720000
           MOVE 'N'               TO PS-GRADE-LVL-SW.                   09730000
           PERFORM 6000-GRADE-LEVEL-BUILD                               09740000
             VARYING SGT-INDX FROM 1 BY 1                               09750000
               UNTIL SGT-INDX IS GREATER THAN PC-MAX-ACCUM-ROWS         09760000
                  OR GRADE-LVL-PROCESSED.                               09770000
                                                                        09780000
      *** COMP/NON COMP STORE TOTALS...                                 09790000
           MOVE 9998              TO SGST-INDX.                         09800000
           MOVE 50                TO SGST-SEQUENCE (SGST-INDX)          09810000
           MOVE 'X     '          TO SGST-LABEL (SGST-INDX).            09820000
           PERFORM 5300-TOTAL-UP.                                       09830000
                                                                        09840000
      *** COMPANY TOTALS...                                             09850000
           MOVE 9999              TO SGST-INDX.                         09860000
           MOVE ZEROS             TO SGST-SEQUENCE (SGST-INDX).         09870000
           MOVE 'Z     '          TO SGST-LABEL (SGST-INDX).            09880000
           PERFORM 5300-TOTAL-UP.                                       09890000
                                                                        09900000
      ******************************************************************09910000
      * 5100-PROCESS-NCOMP-STORE.                                       09920000
      *     ACCUMULATE GRADE LEVEL INFORMATION INTO THE STORE GRADE     09930000
      * STORE TABLE FROM THE MAIN STATISTICS EXTRACT FILE. THE ROUTINE  09940000
      * ALSO TOTALS UP DOLLARS FOR COMP/NON COMP STORES AND FINAL       09950000
      * TOTALS. THE INFORMATION IS PROCESSED BASED ON THE SEASON AND    09960000
      * MBS STRUCTURE AT A STORE LEVEL.                                 09970000
      ******************************************************************09980000
       5100-PROCESS-NCOMP-STORE.                                        09990000
                                                                        10000000
      *** NON-COMP STORE TOTALS (BY GRADE & NUMBER OF STORES IN GRADE). 10010000
           MOVE 'N'               TO PS-GRADE-LVL-SW.                   10020000
           PERFORM 6000-GRADE-LEVEL-BUILD                               10030000
             VARYING SGT-INDX FROM 1 BY 1                               10040000
               UNTIL SGT-INDX IS GREATER THAN PC-MAX-ACCUM-ROWS         10050000
                  OR GRADE-LVL-PROCESSED.                               10060000
                                                                        10070000
      *** COMP/NON COMP STORE TOTALS...                                 10080000
           MOVE 9998              TO SGST-INDX.                         10090000
           MOVE 50                TO SGST-SEQUENCE (SGST-INDX)          10100000
           MOVE 'X     '          TO SGST-LABEL (SGST-INDX).            10110000
           PERFORM 5300-TOTAL-UP.                                       10120000
                                                                        10130000
      *** COMPANY TOTALS...                                             10140000
           MOVE 9999              TO SGST-INDX.                         10150000
           MOVE ZEROS             TO SGST-SEQUENCE (SGST-INDX)          10160000
           MOVE 'Z     '          TO SGST-LABEL (SGST-INDX).            10170000
           PERFORM 5300-TOTAL-UP.                                       10180000
                                                                        10190000
      ******************************************************************10200000
      * 5200-PROCESS-NEW-STORE.                                         10210000
      *     ACCUMULATE NEW STORE OPEN INFORMATION INTO THE STORE GRADE  10220000
      * STORE TABLE FROM THE MAIN STATISTICS EXTRACT FILE. THE ROUTINE  10230000
      * ALSO TOTALS UP DOLLARS FOR NEW STORES; AS WELL AS A FINAL NEW   10240000
      * STORE TOTAL. THE INFORMATION IS PROCESSED BASED ON THE SEASON   10250000
      * AND MBS STRUCTURE AT A STORE LEVEL.                             10260000
      ******************************************************************10270000
       5200-PROCESS-NEW-STORE.                                          10280000
                                                                        10290000
      *** ACCUMULATE NEW STORE TOTALS (BY OPEN MONTH).....              10300000
           MOVE 'N'               TO PS-NEW-STORE-SW.                   10310000
           PERFORM 6100-NEW-STORE-BUILD                                 10320000
             VARYING SGST-INDX FROM 1 BY 1                              10330000
               UNTIL SGST-INDX IS GREATER THAN PC-MAX-ACCUM-ROWS        10340000
                  OR NEW-STORE-PROCESSED.                               10350000
                                                                        10360000
      *** NEW STORE TOTALS...                                           10370000
           MOVE 9997              TO SGST-INDX.                         10380000
DC    *    MOVE 99                TO SGST-SEQUENCE (SGST-INDX).         10390000
DC         MOVE 90                TO SGST-SEQUENCE (SGST-INDX).         10400000
           MOVE 'Y     '          TO SGST-LABEL (SGST-INDX).            10410000
           PERFORM 5300-TOTAL-UP.                                       10420000
                                                                        10430000
      *** COMPANY TOTALS...                                             10440000
           MOVE 9999              TO SGST-INDX.                         10450000
           MOVE ZEROS             TO SGST-SEQUENCE (SGST-INDX).         10460000
           MOVE 'Z     '          TO SGST-LABEL (SGST-INDX).            10470000
           PERFORM 5300-TOTAL-UP.                                       10480000
                                                                        10490000
DC    ******************************************************************10500000
 |    * 5250-PROCESS-DC-LOCATION                                        10510000
 |    *     ACCUMULATE DC STORE OPEN INFORMATION INTO THE STORE GRADE   10520000
 |    * STORE TABLE FROM THE MAIN STATISTICS EXTRACT FILE. THE ROUTINE  10530000
 |    * ALSO TOTALS UP DOLLARS FOR DC STORES; AS WELL AS A FINAL DC     10540000
 |    * STORE TOTAL. THE INFORMATION IS PROCESSED BASED ON THE SEASON   10550000
 |    * AND MBS STRUCTURE AT A STORE LEVEL. DC DOLLARS ARE INCLUDED IN  10560000
 |    * THE COMPANY TOTALS - BUT NOT INCLUDED AS AN ADDITIONAL LOCATION.10570000
 |    *                                                                 10580000
 |    * FOR THIS REPORT - A DC CAN BE ANY LOCATION TYPE DC, RV OR CS    10590000
 |    ******************************************************************10600000
 |     5250-PROCESS-DC-LOCATION.                                        10610000
 |                                                                      10620000
 |    *** DC STORE TOTALS...                                            10630000
 |         MOVE 9996              TO SGST-INDX.                         10640000
 |         MOVE 99                TO SGST-SEQUENCE (SGST-INDX)          10650000
 |         MOVE 'W     '          TO SGST-LABEL (SGST-INDX).            10660000
 |         PERFORM 5300-TOTAL-UP.                                       10670000
 |                                                                      10680000
 |    *** COMPANY TOTALS...                                             10690000
 |         MOVE 9999              TO SGST-INDX.                         10700000
 |         MOVE ZEROS             TO SGST-SEQUENCE (SGST-INDX).         10710000
 |         MOVE 'Z     '          TO SGST-LABEL (SGST-INDX).            10720000
 |         PERFORM 5300-TOTAL-UP.                                       10730000
 |                                                                      10740000
 |    *** COMPANY "TOTAL" STORES CALCULATED   SGST-INDX = 9999        10750000
 |    *** INCLUDE THE DOLLARS IN EACH OF THE APPROPRIATE BUCKETS BUT    10760000
 |    *** OMIT DC-TYPES 'RV','CS','DC' FROM THE TOTAL STORE COUNT       10770000
 |         COMPUTE  SGST-STORE-ACCUM (SGST-INDX) =                      10780000
DC                 (SGST-STORE-ACCUM (SGST-INDX) - 1).                  10790000
                                                                        10800000
      ******************************************************************10810000
      * 5300-TOTAL-UP.                                                  10820000
      *     ACCUMULATE THE STORE INFORMATION BASED ON THE SEASON AND    10830000
      * MBS STRUCTURE. THE ROUTINE TOTALS FOR ALL LEVELS.               10840000
      ******************************************************************10850000
       5300-TOTAL-UP.                                                   10860000
                                                                        10870000
           MOVE ML041-SEASON      TO SGST-SEASON       (SGST-INDX).     10880000
           MOVE ML041-YEAR        TO SGST-YEAR         (SGST-INDX).     10890000
           MOVE ML041-DEPT-NBR    TO SGST-DEPT-NBR     (SGST-INDX).     10900000
           MOVE ML041-BUYER-NBR   TO SGST-BUYER-NBR    (SGST-INDX).     10910000
           MOVE ML041-BUS-GRP-NBR TO SGST-BUS-GRP-NBR  (SGST-INDX).     10920000
           MOVE ML041-DMA-NBR     TO SGST-DMA-NBR      (SGST-INDX).     10930000
           MOVE ML041-GMA-NBR     TO SGST-GMA-NBR      (SGST-INDX).     10940000
           ADD ML041-SLS-RTL      TO SGST-SLS-RTL-AMT  (SGST-INDX).     10950000
                                                                        10960000
W30900     PERFORM 5350-GET-DIVIDE-NUMBER.                              10961039
W30900                                                                  10962039
           COMPUTE PV-WORK-AMOUNT ROUNDED                               10970000
W30900         EQUAL ML041-END-INV-RTL / PV-DIVIDE-NUMBER.              10980039
           ADD PV-WORK-AMOUNT     TO SGST-END-INV-RTL  (SGST-INDX).     10990000
                                                                        11000000
           ADD ML041-MKDN-RTL     TO SGST-MKDN-RTL     (SGST-INDX).     11010000
                                                                        11020000
           ADD 1              TO SGST-STORE-ACCUM  (SGST-INDX).         11030000
                                                                        11040000
W30900******************************************************************11040139
W30900* 5350-GET-DIVIDE-NUMBER.                                         11040239
W30900******************************************************************11040639
W30900 5350-GET-DIVIDE-NUMBER.                                          11040739
W30900     MOVE ZEROES TO PV-DIVIDE-NUMBER.                             11040839
W30900                                                                  11040939
W30900     EXEC SQL                                                     11041039
W30900         SELECT FSCL_WK_NBR                                       11041139
W30900         INTO   :DTATTR-FSCL-WK-NBR                               11041239
W30900         FROM   TDTATTR                                           11041339
W30900         WHERE  KY_DTE = :CC-DATE-DRIVER                          11041439
W30900     END-EXEC.                                                    11041539
W30900                                                                  11041639
W30900     EVALUATE TRUE                                                11041939
W30900         WHEN CC-SEASONAL                                         11042039
W30900             IF DTATTR-FSCL-WK-NBR EQUAL '53'                     11043039
W30900                 MOVE +28 TO PV-DIVIDE-NUMBER                     11044039
W30900             ELSE                                                 11045039
W30900                 MOVE +27 TO PV-DIVIDE-NUMBER                     11045139
W30900             END-IF                                               11046039
W30900         WHEN CC-QUARTERLY                                        11049639
W30900             IF DTATTR-FSCL-WK-NBR                                11049839
W30900                 EQUAL '53'                                       11049939
W30900                 MOVE +15 TO PV-DIVIDE-NUMBER                     11050040
W30900             ELSE                                                 11050139
W30900                 MOVE +14 TO PV-DIVIDE-NUMBER                     11050240
W30900             END-IF                                               11050339
W30900         WHEN CC-MONTHLY                                          11051439
W30900             IF DTATTR-FSCL-WK-NBR                                11051539
W30900                 EQUAL '09' OR '22' OR '35' OR '48' OR '53'       11051639
W30900                 MOVE +6 TO PV-DIVIDE-NUMBER                      11051739
W30900             ELSE                                                 11051839
W30900                 MOVE +5 TO PV-DIVIDE-NUMBER                      11051939
W30900             END-IF                                               11052039
W30900     END-EVALUATE.                                                11052739
      ******************************************************************11053000
      * 5400-BUILD-VERBIAGE.                                            11060000
      *     GENERATE THE COMMA SEPERATED VARIABLE RECORD'S VERBAIGE.    11070000
      * VERBIAGE DEFINES WHAT LEVEL THE CSV RECORD REPRESENTS IN THE    11080000
      * STORE GRADE FILE.                                               11090000
      ******************************************************************11100000
        5400-BUILD-VERBIAGE.                                            11110000
                                                                        11120000
            ADD 1                 TO GRADE-INDX.                        11130000
                                                                        11140000
      *** FOR COMP STORE GRADING VERBIAGE. ERROR IF THE GRADE DOES NOT  11150000
      *** EXIST.                                                        11160000
            IF GRADE-INDX IS GREATER THAN PC-NBR-GRADE-TABLE-ROWS       11170000
               MOVE '*****'       TO ML043-STR-GRADE-PCT                11180000
               MOVE '*****'       TO ML043-STR-GRADE-LTR                11190000
               MOVE 'Y'           TO PS-GRADE-FOUND-SW                  11200000
            ELSE                                                        11210000
      *** BUILD NEW STORE MONTH VERBIAGE...                             11220000
            MOVE SGST-LABEL (SGST-INDX)  TO PC-EDIT-LABEL               11230000
            IF PC-NEW-JAN-STORE                                         11240000
                MOVE 'JAN     '   TO ML043-CTRL-LABEL                   11250000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11260000
            ELSE                                                        11270000
            IF PC-NEW-FEB-STORE                                         11280000
                MOVE 'FEB     '   TO ML043-CTRL-LABEL                   11290000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11300000
            ELSE                                                        11310000
            IF PC-NEW-MAR-STORE                                         11320000
                MOVE 'MAR     '   TO ML043-CTRL-LABEL                   11330000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11340000
            ELSE                                                        11350000
            IF PC-NEW-APR-STORE                                         11360000
                MOVE 'APR     '   TO ML043-CTRL-LABEL                   11370000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11380000
            ELSE                                                        11390000
            IF PC-NEW-MAY-STORE                                         11400000
                MOVE 'MAY     '   TO ML043-CTRL-LABEL                   11410000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11420000
            ELSE                                                        11430000
            IF PC-NEW-JUN-STORE                                         11440000
                MOVE 'JUN     '   TO ML043-CTRL-LABEL                   11450000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11460000
            ELSE                                                        11470000
            IF PC-NEW-JUL-STORE                                         11480000
                MOVE 'JUL     '   TO ML043-CTRL-LABEL                   11490000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11500000
            ELSE                                                        11510000
            IF PC-NEW-AUG-STORE                                         11520000
                MOVE 'AUG     '   TO ML043-CTRL-LABEL                   11530000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11540000
            ELSE                                                        11550000
            IF PC-NEW-SEP-STORE                                         11560000
                MOVE 'SEP     '   TO ML043-CTRL-LABEL                   11570000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11580000
            ELSE                                                        11590000
            IF PC-NEW-OCT-STORE                                         11600000
                MOVE 'OCT     '   TO ML043-CTRL-LABEL                   11610000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11620000
            ELSE                                                        11630000
            IF PC-NEW-NOV-STORE                                         11640000
                MOVE 'NOV     '   TO ML043-CTRL-LABEL                   11650000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11660000
            ELSE                                                        11670000
            IF PC-NEW-DEC-STORE                                         11680000
                MOVE 'DEC     '   TO ML043-CTRL-LABEL                   11690000
                MOVE 'Y'          TO PS-GRADE-FOUND-SW                  11700000
            ELSE                                                        11710000
      *** BUILD VERBIAGE BASED ON THE COMP STORE GRADING STRUCTURE.     11720000
            IF SGST-LABEL (SGST-INDX) EQUAL GT-GRADE-NAME (GRADE-INDX)  11730000
                MOVE GT-GRADE-PERCENT (GRADE-INDX)                      11740000
                                    TO PV-GRADE-PCT-REDEFINE            11750000
                COMPUTE PV-WORK-AMOUNT4 ROUNDED                         11760000
                                    = GT-GRADE-PERCENT (GRADE-INDX)     11770000
                STRING '  ' DELIMITED BY SIZE                           11780000
                       PV-WORK-AMOUNT4 DELIMITED BY SIZE                11790000
                      '%' DELIMITED BY SIZE                             11800000
                                    INTO ML043-STR-GRADE-PCT            11810000
                STRING ' - ' DELIMITED BY SIZE                          11820000
                      GT-GRADE-NAME (GRADE-INDX) DELIMITED BY SIZE      11830000
                                    INTO ML043-STR-GRADE-LTR            11840000
                MOVE GRADE-INDX     TO SGST-SEQUENCE (SGST-INDX)        11850000
                MOVE 'Y'            TO PS-GRADE-FOUND-SW                11860000
            END-IF.                                                     11870000
                                                                        11880000
      ***************************************************************** 11890000
      * 5500-SELECT-TDTATTR.                                            11900000
      *     CAPTURE SEASON INFORMATION FOR THE MFT CONTROL CARD SETUP.  11910041
      ******************************************************************11920000
       5500-SELECT-TDTATTR.                                             11930000
W30900     EVALUATE TRUE                                                11940028
W30900         WHEN CC-SEASONAL                                         11941028
W30900             EXEC SQL                                             11950028
W30900                 SELECT FSCL_YR_NBR                               11960028
W30900                       ,SEA_DESC                                  11970028
W30900                       ,SEA_END_DTE                               11980028
W30900                 INTO :DTATTR-FSCL-YR-NBR                         11990028
W30900                     ,:DTATTR-SEA-DESC                            12000028
W30900                     ,:PV-THIS-CYCL-END-DTE                       12010028
W30900                 FROM TDTATTR                                     12020028
W30900                 WHERE KY_DTE = :DTATTR-KY-DTE                    12030028
W30900             END-EXEC                                             12040028
W30900         WHEN CC-QUARTERLY                                        12040433
W30900             EXEC SQL                                             12040533
W30900                 SELECT MAX(A.KY_DTE)                             12040633
W30900                 INTO   :PV-THIS-CYCL-END-DTE                     12040733
W30900                 FROM   TDTATTR A                                 12040833
W30900                       ,TDTATTR B                                 12040933
W30900                 WHERE B.KY_DTE = :DTATTR-KY-DTE                  12041027
W30900                      AND A.FSCL_YR_NBR = B.FSCL_YR_NBR           12041127
W30900                      AND A.FSCL_QTR_NBR = B.FSCL_QTR_NBR         12041227
W30900             END-EXEC                                             12041327
W30900             EXEC SQL                                             12041428
W30900                 SELECT FSCL_YR_NBR                               12041528
W30900                       ,SEA_DESC                                  12041628
W30900                       ,FSCL_QTR_NBR                              12041728
W30900                 INTO :DTATTR-FSCL-YR-NBR                         12041828
W30900                     ,:DTATTR-SEA-DESC                            12041928
W30900                     ,:DTATTR-FSCL-QTR-NBR                        12042038
W30900                 FROM TDTATTR                                     12042128
W30900                 WHERE KY_DTE = :DTATTR-KY-DTE                    12042227
W30900             END-EXEC                                             12042328
W30900         WHEN CC-MONTHLY                                          12042828
W30900             EXEC SQL                                             12042928
W30900                 SELECT FSCL_YR_NBR                               12043127
W30900                       ,SEA_DESC                                  12043228
W30900                       ,FSCL_MN_NM                                12043327
W30900                       ,FSCL_MN_END_DTE                           12043427
W30900                 INTO :DTATTR-FSCL-YR-NBR                         12043528
W30900                     ,:DTATTR-SEA-DESC                            12043628
W30900                     ,:PV-MONTH-DESC                              12043827
W30900                     ,:PV-THIS-CYCL-END-DTE                       12043928
W30900                 FROM TDTATTR                                     12044828
W30900                 WHERE KY_DTE = :DTATTR-KY-DTE                    12044928
W30900             END-EXEC                                             12045028
W30900     END-EVALUATE.                                                12046028
                                                                        12050028
           EVALUATE TRUE                                                12060028
             WHEN SQLCODE = +0                                          12070028
                 IF DTATTR-SEA-DESC = 'FALL'                            12080028
                     MOVE DTATTR-SEA-DESC TO PV-DTATTR-SEA-DESC         12090028
                     MOVE PV-SEASON-DESC  TO DTATTR-SEA-DESC            12100028
MFTCHG               MOVE PV-SEASON-DESC  TO DTATTR-SEA-DESC1           12101041
                 END-IF                                                 12110028
             WHEN OTHER                                                 12120028
W30900           MOVE '****'         TO DTATTR-FSCL-YR-NBR              12130027
MFTCHG           MOVE '****'         TO DTATTR-FSCL-YR-NBR1             12131042
W30900           MOVE '******'       TO DTATTR-SEA-DESC                 12140027
MFTCHG           MOVE '******'       TO DTATTR-SEA-DESC1                12141042
W30900           MOVE CC-DATE-DRIVER TO PV-THIS-CYCL-END-DTE            12150027
           END-EVALUATE.                                                12160000
                                                                        12170000
W30900     EVALUATE TRUE                                                12170128
W30900         WHEN CC-SEASONAL                                         12170228
W30900             MOVE SPACES TO PV-CYCLE-DESC                         12170328
MFTCHG             MOVE SPACES TO PV-CYCLE-DESC1                        12170441
W30900         WHEN CC-QUARTERLY                                        12170541
W30900             STRING '-QTR' DELIMITED BY SIZE                      12171128
W30900                 DTATTR-FSCL-QTR-NBR DELIMITED BY SIZE            12171338
W30900                     INTO PV-CYCLE-DESC                           12171434
MFTCHG             STRING '-QTR' DELIMITED BY SIZE                      12171541
MFTCHG                 DTATTR-FSCL-QTR-NBR DELIMITED BY SIZE            12171641
MFTCHG                     INTO PV-CYCLE-DESC1                          12171741
W30900         WHEN CC-MONTHLY                                          12177228
W30900             STRING '-' DELIMITED BY SIZE                         12177328
W30900                 PV-MONTH-INITIAL-DESC DELIMITED BY SIZE          12177429
W30900                     INTO PV-CYCLE-DESC                           12177528
MFTCHG             STRING '-' DELIMITED BY SIZE                         12177641
MFTCHG                 PV-MONTH-INITIAL-DESC DELIMITED BY SIZE          12177741
MFTCHG                     INTO PV-CYCLE-DESC1                          12177841
W30900     END-EVALUATE.                                                12178028
      ***************************************************************** 12180000
      * 5600-DATE-ROUTINE.                                              12190000
      *     DETERMINE THE NEXT SEASON AND BUILD THE DIRECTORY STRUCTURE 12200000
      * FOR NEXT SEASON.                                                12210000
      ******************************************************************12220000
       5600-DATE-ROUTINE.                                               12230000
                                                                        12240000
           INITIALIZE DPG51 DPG52                                       12250000
                      DPG53 DPG54                                       12260000
                      DPG55 DPG56.                                      12270000
                                                                        12280000
           SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                     12290000
           SET DPG51-INCREMENT-DATE TO TRUE.                            12300000
W30900     MOVE PV-THIS-CYCL-END-DTE TO DPG52-LK-DATE-INPUT.            12310027
           SET DPG52-LK-DTE-ISO-FMT TO TRUE.                            12320000
           MOVE 1                   TO DPG51-INCR-DECR-DAYS-9.          12330000
                                                                        12340000
           CALL 'DPKUT500'          USING DPG51 DPG52                   12350000
                                          DPG53 DPG54                   12360000
                                          DPG55 DPG56.                  12370000
                                                                        12380000
           IF DPG54-NO-ERROR                                            12390000
               INITIALIZE DCLTDTATTR                                    12400000
               MOVE DPG55-DB2-ISO-DATE-FMT TO DTATTR-KY-DTE             12410000
               PERFORM 5500-SELECT-TDTATTR                              12420000
           ELSE                                                         12430000
               INITIALIZE DCLTDTATTR                                    12440000
W30900         MOVE CC-DATE-DRIVER         TO DTATTR-KY-DTE             12450027
               PERFORM 5500-SELECT-TDTATTR                              12460000
           END-IF.                                                      12470000
                                                                        12480000
      /*****************************************************************12490000
      * 6000-GRADE-LEVEL-BUILD.                                         12500000
      *     GRADE THE COMP STORE DOLLARS BASED ON THE GRADE LEVELS      12510000
      * PASSED INTO THE MODULE FROM THE CONTROL CARD FSCC008.           12520000
      * ACCUMULATE TOTALS FOR THE GRADE. INFORMATION IS PROCESSED AT    12530000
      * THE STORE LEVEL HERE.                                           12540000
      ******************************************************************12550000
       6000-GRADE-LEVEL-BUILD.                                          12560000
                                                                        12570000
           IF ML041-SEASON EQUAL SGT-SEASON (SGT-INDX)           AND    12580000
              ML041-YEAR EQUAL SGT-YEAR (SGT-INDX)               AND    12590000
              ML041-DEPT-NBR EQUAL SGT-DEPT-NBR (SGT-INDX)       AND    12600000
              ML041-BUYER-NBR EQUAL SGT-BUYER-NBR (SGT-INDX)     AND    12610000
              ML041-BUS-GRP-NBR EQUAL SGT-BUS-GRP-NBR (SGT-INDX) AND    12620000
              ML041-DMA-NBR EQUAL SGT-DMA-NBR (SGT-INDX)         AND    12630000
              ML041-GMA-NBR EQUAL SGT-GMA-NBR (SGT-INDX)                12640000
               IF SGT-STORE-ACCUM (SGT-INDX)                            12650000
ALIGN *                         NOT GREATER SGT-STORE-COUNT (SGT-INDX)  12660017
ALIGN                           LESS THAN   SGT-STORE-COUNT (SGT-INDX)  12670017
                 ADD 1              TO SGT-STORE-ACCUM (SGT-INDX)       12680000
F90903           PERFORM 7000-ACCUM-STORE-GRADE-TABLE                   12690000
F90903             VARYING SGST-INDX FROM 1 BY 1                        12700000
F90903               UNTIL GRADE-LVL-PROCESSED                          12710000
               ELSE                                                     12720000
ALIGN              IF SGT-INDX IS EQUAL TO PV-GRADE-TBL-CNT             12730017
                     ADD 1              TO SGT-STORE-ACCUM (SGT-INDX)   12740000
F90903               PERFORM 7000-ACCUM-STORE-GRADE-TABLE               12750000
F90903                 VARYING SGST-INDX FROM 1 BY 1                    12760000
F90903                   UNTIL GRADE-LVL-PROCESSED                      12770000
                   END-IF                                               12780010
ALIGN          END-IF                                                   12790017
           END-IF.                                                      12800000
                                                                        12810000
      ******************************************************************12820000
      * 6100-NEW-STORE-BUILD.                                           12830000
      *     GRADE THE NEW STORE DOLLARS BASED ON THE MONTH THE STORE    12840000
      * HAD BEEN OPENED. THE OPEN DATE IS CONTROLLED BY THE EXTRACT     12850000
      * MODULE WHICH BUILT THE MAIN EXTRACT. ACCUMULATE TOTALS FOR THE  12860000
      * GRADE. INFORMATION IS PROCESSED AT THE STORE LEVEL HERE.        12870000
      ******************************************************************12880000
       6100-NEW-STORE-BUILD.                                            12890000
                                                                        12900000
           IF ML041-SEASON EQUAL SGST-SEASON (SGST-INDX) AND            12910000
              ML041-YEAR EQUAL SGST-YEAR (SGST-INDX) AND                12920000
              ML041-DEPT-NBR EQUAL SGST-DEPT-NBR (SGST-INDX) AND        12930000
              ML041-BUYER-NBR EQUAL SGST-BUYER-NBR (SGST-INDX) AND      12940000
              ML041-BUS-GRP-NBR EQUAL SGST-BUS-GRP-NBR (SGST-INDX) AND  12950000
              ML041-DMA-NBR EQUAL SGST-DMA-NBR (SGST-INDX) AND          12960000
              ML041-GMA-NBR EQUAL SGST-GMA-NBR (SGST-INDX) AND          12970000
              ML041-STORE-TYPE EQUAL SGST-LABEL (SGST-INDX)             12980000
               PERFORM 5300-TOTAL-UP                                    12990000
               MOVE 'Y'              TO PS-NEW-STORE-SW                 13000000
           ELSE                                                         13010000
           IF SGST-SEASON (SGST-INDX) EQUAL SPACES AND                  13020000
              SGST-YEAR (SGST-INDX) EQUAL SPACES AND                    13030000
              SGST-DEPT-NBR (SGST-INDX) EQUAL SPACES AND                13040000
              SGST-BUYER-NBR (SGST-INDX) EQUAL SPACES AND               13050000
              SGST-BUS-GRP-NBR (SGST-INDX) EQUAL SPACES AND             13060000
              SGST-DMA-NBR (SGST-INDX) EQUAL SPACES AND                 13070000
              SGST-GMA-NBR (SGST-INDX) EQUAL SPACES AND                 13080000
              SGST-LABEL (SGST-INDX) EQUAL SPACES                       13090000
               MOVE ML041-STORE-TYPE  TO SGST-LABEL (SGST-INDX)         13100000
               MOVE ML041-STORE-TYPE  TO PV-STORE-TYPE                  13110000
               COMPUTE SGST-SEQUENCE (SGST-INDX)                        13120000
                                      = PV-STORE-TYPE-NUM + 60          13130000
               PERFORM 5300-TOTAL-UP                                    13140000
               MOVE 'Y'               TO PS-NEW-STORE-SW                13150000
           END-IF.                                                      13160000
                                                                        13170000
F90903/*****************************************************************13180000
  |   * 7000-ACCUM-STORE-GRADE-TABLE.                                   13190000
  |   *     ACCUMULATE THE GRADE LEVEL DOLLARS.                         13200000
  |   ******************************************************************13210000
  |    7000-ACCUM-STORE-GRADE-TABLE.                                    13220000
  |                                                                     13230000
  |        IF ML041-SEASON EQUAL SGST-SEASON (SGST-INDX) AND            13240000
  |           ML041-YEAR EQUAL SGST-YEAR (SGST-INDX) AND                13250000
  |           ML041-DEPT-NBR EQUAL SGST-DEPT-NBR (SGST-INDX) AND        13260000
  |           ML041-BUYER-NBR EQUAL SGST-BUYER-NBR (SGST-INDX) AND      13270000
  |           ML041-BUS-GRP-NBR EQUAL SGST-BUS-GRP-NBR (SGST-INDX) AND  13280000
  |           ML041-DMA-NBR EQUAL SGST-DMA-NBR (SGST-INDX) AND          13290000
  |           ML041-GMA-NBR EQUAL SGST-GMA-NBR (SGST-INDX) AND          13300000
  |           SGT-STORE-GRADE (SGT-INDX) EQUAL SGST-LABEL (SGST-INDX)   13310000
  |            PERFORM 5300-TOTAL-UP                                    13320000
  |            MOVE 'Y'           TO PS-GRADE-LVL-SW                    13330000
  |        ELSE                                                         13340000
  |        IF SGST-SEASON (SGST-INDX) EQUAL SPACES AND                  13350000
  |           SGST-YEAR (SGST-INDX) EQUAL SPACES AND                    13360000
  |           SGST-DEPT-NBR (SGST-INDX) EQUAL SPACES AND                13370000
  |           SGST-BUYER-NBR (SGST-INDX) EQUAL SPACES AND               13380000
  |           SGST-BUS-GRP-NBR (SGST-INDX) EQUAL SPACES AND             13390000
  |           SGST-DMA-NBR (SGST-INDX) EQUAL SPACES AND                 13400000
  |           SGST-GMA-NBR (SGST-INDX) EQUAL SPACES AND                 13410000
  |           SGST-LABEL (SGST-INDX) EQUAL SPACES                       13420000
  |            MOVE SGT-STORE-GRADE (SGT-INDX)                          13430000
  |                               TO SGST-LABEL (SGST-INDX)             13440000
  |            PERFORM 5300-TOTAL-UP                                    13450000
  |            MOVE 'Y'           TO PS-GRADE-LVL-SW                    13460000
  |        END-IF.                                                      13470000
F90903                                                                  13480000
      /*****************************************************************13490000
      * 8000-WRITE-EXTRACT-REC.                                         13500000
      ******************************************************************13510000
       8000-WRITE-EXTRACT-REC.                                          13520000
                                                                        13530000
           WRITE STORE-MERGE-RECORD FROM ML043-RANKED-SALES-INV-PERF.   13540000
           ADD +1                   TO PV-TOTAL-EXTR-WRITTEN.           13550000
                                                                        13560000
      /*****************************************************************13570000
      * 9000-END OF JOB TASKS.                                          13580000
      ******************************************************************13590000
       9000-END-OF-JOB-TASKS.                                           13600000
                                                                        13610000
      *** PERFORM ONE FINAL RECORD FORMAT IF WE HAVE PROCESSED INFO..   13620000
           IF PV-STORE-MAIN-READ IS GREATER THAN ZEROS                  13630000
               MOVE ZEROS           TO SGST-INDX                        13640000
               MOVE 'N'             TO PS-UNLOAD-TBL-SW                 13650000
               PERFORM 4200-FORMAT-RECORD                               13660000
                   UNTIL UNLOAD-TBL-COMPLETE                            13670000
           END-IF.                                                      13680000
                                                                        13690000
                                                                        13740000
      *** SUMMARIZE THE EVENT....                                       13750000
           DISPLAY ' '.                                                 13760000
           DISPLAY '******************************************'.        13770000
           DISPLAY '* RECORD COUNT SUMMARY'.                            13780000
           DISPLAY '******************************************'.        13790000
           DISPLAY '* STORE STAT EXTRACT RECORDS READ = '               13800000
                 PV-STORE-STAT-READ.                                    13810000
           DISPLAY '* STORE MAIN EXTRACT RECORDS READ = '               13820000
                 PV-STORE-MAIN-READ.                                    13830000
           DISPLAY '* CSV RECORDS GENERATED           = '               13840000
                 PV-TOTAL-EXTR-WRITTEN.                                 13850000
           DISPLAY '******************************************'.        13860000
           DISPLAY ' '.                                                 13870000
           DISPLAY '******************************************'.        13880000
           DISPLAY '*  END OF ', PC-PROGRAM-NAME.                       13890000
           DISPLAY '******************************************'.        13900000
                                                                        13910000
      *** CLOSE FILES...                                                13920000
           CLOSE STR-GRADE-PCT-GUIDE-IN                                 13930000
                 STR-STAT-EXTRACT-IN                                    13940000
                 STR-MAIN-EXTRACT-IN                                    13950000
                 INPUT-CONTROL-FILE                                     13960000
W30900           FILESET-CONTROL-FILE                                   13971023
                 MFT-CONTROL-CARD-OUT                                   13980041
                 STORE-MERGE-FILE-OUT.                                  13990000
                                                                        14000000
      /*****************************************************************14010000
      * 9900-ABEND.                                                     14020000
      ******************************************************************14030000
       9900-ABEND.                                                      14040000
                                                                        14050000
           MOVE ABEND-CODE          TO PV-DISPLAY-ABEND-CODE.           14060000
           DISPLAY '**  ABEND     **'.                                  14070000
           DISPLAY '**  PROGRAM   **  = ', PC-PROGRAM-NAME.             14080000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            14090000
           DISPLAY '**  OPERATION **  = ' PV-DISPLAY-ABEND-CODE.        14100000
                                                                        14110000
           CALL 'ILBOABN0'          USING ABEND-CODE.                   14120000
                                                                        14130000
