       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT995.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  08/18/98.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  CREATES A FILE TO LOAD TRADING PARTNER INFORMATION TO AI      *        
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  NOTE:  IF ANOTHER VALUE IS EVER ADDED FOR WS-FIELD-17, THE    *        
      *         VALUE ALSO NEEDS TO BE ADDED TO ETCC036.  THE SAME     *        
      *         VALUE SHOULD NEVER BE USED FOR BOTH WS-FIELD-17 AND    *        
      *         WS-FIELD-18.                                           *        
      ******************************************************************        
JF0502*                                                                         
JF0502*   DATE 05/02/2002                                                       
JF0502*     ADDED LOGIC FOR 816 PROCESSING.                                     
JF0502*   MADE BY: JON FIEDLER             WR/IR #: 22778                       
      ******************************************************************        
JF0104*                                                                         
JF0104*   DATE  01/21/2004                                                      
JF0104*       Added logic to move '126' to the segment                          
JF0104*       terminator (FIELD-17) for Russell Athletic.                       
JF0104*    MADE BY: Jon Fiedler            WR/IR #: WR25446                     
      ******************************************************************        
SP0204*                                                                         
SP0204*   DATE  02/06/2004                                                      
SP0204*       Added logic to move '126' to the segment                          
SP0204*       terminator (FIELD-17) for Columbia Sportswear                     
SP0204*       and Capelli of New York.                                          
SP0204*    MADE BY: Sue Pari               WR/IR #: WR25446                     
      ******************************************************************        
JF0204*                                                                         
JF0204*   DATE  02/06/2004                                                      
JF0204*       ADDED LOGIC TO EXTRACT SEGMENT TERMINATOR, ELEMENT                
JF0204*       SEPARATOR, AND SUB-ELEMENT SEPARATOR INFORMATION FROM             
JF0204*       THE TABLE TTRDPRT.                                                
JF0204*    MADE BY: JON FIEDLER            WR/IR #: WR26569                     
      ******************************************************************        
PS1006*                                                                         
PS1006*   DATE  10/06/2009                                                      
PS1006*       ADDED LOGIC TO PARAGRAPH 3600 TO ACCOMMODATE 310 FILES            
PS1006*    MADE BY: PAUL STROEDE           WR/IR #: WR23145                     
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
VF0629     RECORDING MODE IS F                                                  
           RECORD CONTAINS 406 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD        PIC X(406).                                     
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-OUTPUT-RECORD.                                                    
           05  WS-FIELD-01      PIC X(45).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-02      PIC X(2).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-03      PIC X(19).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-04      PIC X(2).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-05      PIC X(15).                                      
           05  FILLER           PIC X(6)   VALUE ',,,,,,'.                      
           05  WS-FIELD-11      PIC X(15).                                      
           05  FILLER           PIC X(4)   VALUE ',,,,'.                        
           05  WS-FIELD-15      PIC X(5).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-16      PIC X(9).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-17      PIC X(3).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-18      PIC X(2).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-19      PIC X(2).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-20      PIC X(3)   VALUE 'Yes'.                         
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-21      PIC X(4)   VALUE 'None'.                        
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-22      PIC X(2)   VALUE 'No'.                          
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-23      PIC X(1)   VALUE 'U'.                           
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-24      PIC X(16)  VALUE 'No Ack Requested'.            
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-25      PIC X(10)  VALUE 'Production'.                  
           05  FILLER           PIC X(5)   VALUE ',,,,,'.                       
           05  WS-FIELD-30      PIC X(33).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-31      PIC X(19).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-32      PIC X(15).                                      
           05  FILLER           PIC X(3)   VALUE ',,,'.                         
           05  WS-FIELD-35      PIC X(12).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-36      PIC X(9).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-37      PIC X(9).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-38      PIC X(2)   VALUE 'No'.                          
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-39      PIC X(16).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-40      PIC X(1)   VALUE 'X'.                           
           05  FILLER           PIC X(10)  VALUE ',,,,,,,,,,'.                  
           05  WS-FIELD-50      PIC X(10).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-51      PIC X(12).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-52      PIC X(3).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-53      PIC X(9).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-54      PIC X(7).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-55      PIC X(7).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-56      PIC X(14).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-57      PIC X(9).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-58      PIC X(15).                                      
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-59      PIC X(2).                                       
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-60      PIC X(6)   VALUE 'KOHL''S'.                     
           05  FILLER           PIC X(2)   VALUE ',,'.                          
           05  WS-FIELD-62      PIC X(3)   VALUE 'Yes'.                         
           05  FILLER           PIC X(1)   VALUE ','.                           
           05  WS-FIELD-63      PIC X(10).                                      
           05  FILLER           PIC X(36)  VALUE                                
               ',,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,'.                          
                                                                                
       01  WS-STRING-RECORD.                                                    
           05  WS-STRING-01     PIC X(45).                                      
           05  WS-STRING-03     PIC X(23).                                      
           05  WS-STRING-05     PIC X(19).                                      
           05  WS-STRING-11     PIC X(21).                                      
           05  WS-STRING-17     PIC X(23).                                      
           05  WS-STRING-25     PIC X(48).                                      
           05  WS-STRING-30     PIC X(38).                                      
           05  WS-STRING-31     PIC X(20).                                      
           05  WS-STRING-32     PIC X(16).                                      
           05  WS-STRING-35     PIC X(15).                                      
           05  WS-STRING-36     PIC X(10).                                      
           05  WS-STRING-37     PIC X(10).                                      
           05  WS-STRING-39     PIC X(20).                                      
           05  WS-STRING-49     PIC X(12).                                      
           05  WS-STRING-50     PIC X(10).                                      
           05  WS-STRING-51     PIC X(13).                                      
           05  WS-STRING-53     PIC X(14).                                      
           05  WS-STRING-54     PIC X(8).                                       
           05  WS-STRING-55     PIC X(8).                                       
           05  WS-STRING-56     PIC X(15).                                      
           05  WS-STRING-57     PIC X(10).                                      
           05  WS-STRING-58     PIC X(16).                                      
           05  WS-STRING-59     PIC X(3).                                       
           05  WS-STRING-62     PIC X(12).                                      
           05  WS-STRING-63     PIC X(11).                                      
           05  WS-STRING-99     PIC X(36).                                      
                                                                                
       01  WS-FINAL-RECORD      PIC X(408).                                     
                                                                                
                                                                                
       01  WS-9-CHAR-ENT-ID          PIC 9(9).                                  
       01  WS-10-CHAR-ENT-ID         PIC 9(10).                                 
       01  WS-ENT-NAME-DESC          PIC X(30).                                 
VF0625*01  WS-SAVE-NETWORK           PIC X(15).                                 
       01  WS-PREV-ENT-ID            PIC S9(9) COMP.                            
       01  WS-DUP-IND                PIC S9(1) VALUE +0.                        
                                                                                
       01  WS-NAME-FIELD-50.                                                    
           05  WS-NAME               PIC X(9).                                  
           05  WS-NAME-TEST          PIC X(1) VALUE 'T'.                        
                                                                                
       01  WS-CONTROL-NUMBER.                                                   
           05  FILLER                PIC 9(1)  VALUE 6.                         
           05  WS-CNTL-NBR           PIC 9(8).                                  
                                                                                
       01  END-OF-TPLOAD-SW          PIC X(1)  VALUE 'N'.                       
           88  END-OF-TPLOAD                   VALUE 'Y'.                       
                                                                                
       01  END-OF-TRAN-SETS-SW       PIC X(1)  VALUE 'N'.                       
           88  END-OF-TRAN-SETS                VALUE 'Y'.                       
                                                                                
       01  VERSION-ERROR-SW          PIC X(1)  VALUE 'N'.                       
           88  VERSION-ERROR                   VALUE 'Y'.                       
                                                                                
       01  DUP-ENT-ID-SW             PIC X(1)  VALUE 'N'.                       
           88  DUP-ENT-ID                      VALUE 'Y'.                       
           88  NOT-DUP-ENT-ID                  VALUE 'N'.                       
                                                                                
SP0506*01  PC-ENT-ID-9425            PIC S9(09) VALUE +9425.                    
SP0506*01  PC-ENT-ID-10593           PIC S9(09) VALUE +10593.                   
SP0506*01  PC-ENT-ID-10594           PIC S9(09) VALUE +10594.                   
SP0506*01  PC-ENT-ID-10759           PIC S9(09) VALUE +10759.                   
SP0506*01  PC-ENT-ID-11440           PIC S9(09) VALUE +11440.                   
SP0506*01  PC-ENT-ID-11905           PIC S9(09) VALUE +11905.                   
SP0506*01  PC-ENT-ID-29128           PIC S9(09) VALUE +29128.                   
SP0506*01  PC-ENT-ID-31520           PIC S9(09) VALUE +31520.                   
SP0506*01  PC-ENT-ID-40425           PIC S9(09) VALUE +40425.                   
SP0506*01  PC-ENT-ID-30692           PIC S9(09) VALUE +30692.                   
                                                                                
      * THIS IS USED TO CHANGE THE TRADING PARTNER ID AT THE GS                 
      * LEVEL FOR JOHNSON & JOHNSON ONLY FROM 9252330000 TO                     
      * 8008332996.                                                             
       01  PC-TP9252330000           PIC X(15) VALUE '9252330000'.              
       01  PC-TP8008332996           PIC X(15) VALUE '8008332996     '.         
                                                                                
      * THIS IS USED TO CHANGE THE TRADING PARTNER ID AT THE GS                 
      * LEVEL FOR MILLIKEN & COMPANY ONLY FROM 002017440CUST TO                 
      * 015961.                                                                 
       01  PC-TP002017440CUST        PIC X(15) VALUE '002017440CUST'.           
       01  PC-TP015961               PIC X(15) VALUE '015961         '.         
                                                                                
      * THIS IS USED TO CHANGE THE TRADING PARTNER ID AT THE GS                 
      * LEVEL FOR VALLEY MEDIA ONLY.                                            
       01  PC-5306612800             PIC X(15) VALUE '5306612800'.              
       01  PC-VRDI---VRDI002         PIC X(15) VALUE 'VRDI   VRDI002'.          
       01  PC-VALLEYSDQ              PIC X(15) VALUE 'VALLEYSDQ      '.         
                                                                                
       01  PV-PARAGRAPH-NAME         PIC X(30).                                 
       01  PV-DB2-OPER-ATTEMPTED     PIC X(30).                                 
                                                                                
       01  ABEND-CODE                PIC S9(04) VALUE +0 COMP SYNC.             
           88 ABEND-4013                        VALUE +4013.                    
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDFNC                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TNETWRK                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              DECLARE TRDPRT_CURSOR CURSOR FOR                                  
                 SELECT TP_DKEY,                                                
                        ENT_ID,                                                 
                        LAST_SEND_CNTL_NBR,                                     
                        OVRRD_SND_CNTL_NBR,                                     
                        OVRRD_SND_CNTL_ID,                                      
                        INACTV_DTE,                                             
                        VEND_INCHG_ID_DESC,                                     
                        VEND_INCHG_ID,                                          
VF0629*                 NETWK_ID,                                               
                        KOHL_INCHG_ID_DESC,                                     
                        KOHL_INCHG_ID,                                          
                        SEND_FMT_VER_CDE,                                       
VF0629*                 FA_COM_MTHD_CDE,                                        
                        SEND_DATA_ELEM_IND,                                     
                        SEND_SEG_TERM_IND,                                      
                        SEND_SUB_ELEM_IND                                       
                 FROM   TTRDPRT                                                 
                 WHERE  INACTV_DTE = '9999-09-09'                               
                 ORDER BY ENT_ID,                                               
                          VEND_INCHG_ID                                         
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              DECLARE TRDTRN_CURSOR CURSOR FOR                                  
                 SELECT VER_NBR,                                                
                        PROD_TEST_IND,                                          
                        TRAN_SET_CDE,                                           
                        FUNC_CDE,                                               
                        DIR_IND                                                 
VF0629*                 COM_MTHD_CDE                                            
                 FROM   TTRDTRN                                                 
                 WHERE  TP_DKEY    = :TRDPRT-TP-DKEY                            
                   AND  INACTV_DTE = '9999-09-09'                               
                   AND  (TRAN_SET_CDE = '810' OR                                
JF0502                   TRAN_SET_CDE = '816' OR                                
                         TRAN_SET_CDE = '840' OR                                
                         TRAN_SET_CDE = '850' OR                                
                         TRAN_SET_CDE = '852' OR                                
                         TRAN_SET_CDE = '855' OR                                
20669                    TRAN_SET_CDE = '856' OR                                
                         TRAN_SET_CDE = '864' OR                                
SP0506                   TRAN_SET_CDE = '860' OR                                
SP0404                   TRAN_SET_CDE = '870' OR                                
                         TRAN_SET_CDE = '820' OR                                
                         TRAN_SET_CDE = '210' OR                                
                         TRAN_SET_CDE = '214' OR                                
SP0506                   TRAN_SET_CDE = '310')                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           EXEC SQL                                                             
              OPEN TRDPRT_CURSOR                                                
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION' TO PV-PARAGRAPH-NAME                    
              MOVE 'OPEN TRDPRT CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           PERFORM 1000-TPLOAD-FILE                                             
              THRU 1000-TPLOAD-FILE-EXIT                                        
                 UNTIL END-OF-TPLOAD.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE TRDPRT_CURSOR                                               
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION' TO PV-PARAGRAPH-NAME                    
              MOVE 'CLOSE TRDPRT CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-TPLOAD-FILE.                                                        
                                                                                
           SET NOT-DUP-ENT-ID TO TRUE.                                          
                                                                                
           EXEC SQL                                                             
               FETCH TRDPRT_CURSOR                                              
               INTO :TRDPRT-TP-DKEY,                                            
                    :TRDPRT-ENT-ID,                                             
                    :TRDPRT-LAST-SEND-CNTL-NBR,                                 
                    :TRDPRT-OVRRD-SND-CNTL-NBR,                                 
                    :TRDPRT-OVRRD-SND-CNTL-ID,                                  
                    :TRDPRT-INACTV-DTE,                                         
                    :TRDPRT-VEND-INCHG-ID-DESC,                                 
                    :TRDPRT-VEND-INCHG-ID,                                      
VF0629*             :TRDPRT-NETWK-ID,                                           
                    :TRDPRT-KOHL-INCHG-ID-DESC,                                 
                    :TRDPRT-KOHL-INCHG-ID,                                      
                    :TRDPRT-SEND-FMT-VER-CDE,                                   
VF0629*             :TRDPRT-FA-COM-MTHD-CDE,                                    
JF0204              :TRDPRT-SEND-DATA-ELEM-IND,                                 
JF0204              :TRDPRT-SEND-SEG-TERM-IND,                                  
JF0204              :TRDPRT-SEND-SUB-ELEM-IND                                   
           END-EXEC.                                                            
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-TPLOAD-SW                                   
                 GO TO 1000-TPLOAD-FILE-EXIT                                    
              ELSE                                                              
                 MOVE '1000-TPLOAD-TABLE' TO PV-PARAGRAPH-NAME                  
                 MOVE 'FETCH TRDPRT ROWS' TO PV-DB2-OPER-ATTEMPTED              
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           IF TRDPRT-ENT-ID = WS-PREV-ENT-ID                                    
              SET DUP-ENT-ID TO TRUE                                            
              ADD +1 TO WS-DUP-IND                                              
           ELSE                                                                 
              MOVE +0 TO WS-DUP-IND                                             
              MOVE TRDPRT-ENT-ID TO WS-PREV-ENT-ID.                             
                                                                                
      ***************************************************************           
      * THE CODE BELOW CHECKS FOR TRADING PARTNER ID'S WHICH HAVE               
      * BEEN SET UP FOR MORE THAN ONE ENT-ID AND IF THE ENT-ID BEING            
      * PROCESSED IS NOT THE ENT-ID OF THE PARENT THEN THIS PARAGRAPH           
      * IS EXITED AND THE NEXT ID IS PROCESSED.                                 
      ***************************************************************           
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID TO TRDMLT-VND-INCHG-ID.                    
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO TRDMLT-VND-INCHG-ID-DESC.          
                                                                                
           EXEC SQL                                                             
             SELECT PRNT_ENT_ID                                                 
               INTO :TRDMLT-PRNT-ENT-ID                                         
               FROM TTRDMLT                                                     
              WHERE VND_INCHG_ID = :TRDMLT-VND-INCHG-ID                         
                AND VND_INCHG_ID_DESC = :TRDMLT-VND-INCHG-ID-DESC               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                   IF TRDMLT-PRNT-ENT-ID NOT = TRDPRT-ENT-ID                    
                       GO TO 1000-TPLOAD-FILE-EXIT                              
                   END-IF                                                       
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1000-TPLOAD-FILE' TO PV-PARAGRAPH-NAME                 
                   MOVE 'SELECT TTRDMLT' TO PV-DB2-OPER-ATTEMPTED               
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-SET-STATIC-FIELDS                                       
              THRU 2000-SET-STATIC-FIELDS-EXIT.                                 
                                                                                
           EXEC SQL                                                             
              OPEN TRDTRN_CURSOR                                                
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-TPLOAD-FILE' TO PV-PARAGRAPH-NAME                      
              MOVE 'OPEN TRDTRN CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-TRAN-SETS-SW.                                     
           PERFORM 2500-GET-TRAN-SETS                                           
              THRU 2500-GET-TRAN-SETS-EXIT                                      
                 UNTIL END-OF-TRAN-SETS.                                        
                                                                                
           EXEC SQL                                                             
              CLOSE TRDTRN_CURSOR                                               
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-TPLOAD-FILE' TO PV-PARAGRAPH-NAME                      
              MOVE 'CLOSE TRDTRN CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       1000-TPLOAD-FILE-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       2000-SET-STATIC-FIELDS.                                                  
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO WS-FIELD-02.                       
           MOVE TRDPRT-VEND-INCHG-ID TO WS-FIELD-03.                            
           MOVE TRDPRT-VEND-INCHG-ID TO WS-FIELD-31.                            
                                                                                
           IF TRDPRT-VEND-INCHG-ID = PC-TP9252330000                            
              MOVE PC-TP8008332996 TO WS-FIELD-31.                              
           IF TRDPRT-VEND-INCHG-ID = PC-TP002017440CUST                         
              MOVE PC-TP015961 TO WS-FIELD-31.                                  
           IF TRDPRT-VEND-INCHG-ID = PC-5306612800                              
              MOVE PC-VALLEYSDQ TO WS-FIELD-31.                                 
           IF TRDPRT-VEND-INCHG-ID = PC-VRDI---VRDI002                          
              MOVE PC-VALLEYSDQ TO WS-FIELD-31.                                 
                                                                                
           IF TRDPRT-OVRRD-SND-CNTL-ID = 'Y'                                    
              MOVE TRDPRT-OVRRD-SND-CNTL-NBR TO WS-CNTL-NBR                     
              MOVE WS-CONTROL-NUMBER TO WS-FIELD-16                             
           ELSE                                                                 
              MOVE TRDPRT-LAST-SEND-CNTL-NBR TO WS-CNTL-NBR                     
              MOVE WS-CONTROL-NUMBER TO WS-FIELD-16.                            
                                                                                
JF0204     IF TRDPRT-SEND-DATA-ELEM-IND = '5C'                                  
              MOVE '42' TO WS-FIELD-18                                          
           ELSE                                                                 
              MOVE '07' TO WS-FIELD-18.                                         
                                                                                
JF0204     IF TRDPRT-SEND-SEG-TERM-IND = 'A1'                                   
              MOVE '126' TO WS-FIELD-17                                         
           ELSE                                                                 
              MOVE '10 ' TO WS-FIELD-17.                                        
                                                                                
JF0204     IF TRDPRT-SEND-SUB-ELEM-IND = '7A'                                   
              MOVE '58' TO WS-FIELD-19                                          
           ELSE                                                                 
              MOVE '62' TO WS-FIELD-19.                                         
                                                                                
           IF TRDPRT-VEND-INCHG-ID = '058020496' OR                             
              TRDPRT-VEND-INCHG-ID = 'HJBT' OR                                  
SP0503        TRDPRT-VEND-INCHG-ID = 'HUBGROUP' OR                              
SP0506        TRDPRT-VEND-INCHG-ID = 'DBKE' OR                                  
              TRDPRT-VEND-INCHG-ID = 'ABFS'                                     
                 MOVE 'None            ' TO WS-FIELD-39                         
           ELSE                                                                 
              MOVE 'Functional Group' TO WS-FIELD-39.                           
                                                                                
VF0625*    PERFORM 3000-SET-NETWORK-INFO                                        
VF0625*       THRU 3000-SET-NETWORK-INFO-EXIT.                                  
VF0V25     MOVE TRDPRT-KOHL-INCHG-ID-DESC TO WS-FIELD-04.                       
VF0V25     MOVE TRDPRT-KOHL-INCHG-ID TO WS-FIELD-05                             
VF0V25                                  WS-FIELD-32.                            
VF0625     MOVE SPACES TO WS-FIELD-11.                                          
VF0V25     STRING 'FA.IPNET'       DELIMITED BY SIZE                            
VF0V25                      INTO WS-FIELD-11.                                   
                                                                                
           MOVE TRDPRT-ENT-ID TO WS-9-CHAR-ENT-ID.                              
           MOVE TRDPRT-ENT-ID TO WS-10-CHAR-ENT-ID.                             
                                                                                
           PERFORM 3200-GET-VENDOR-NAME                                         
              THRU 3200-GET-VENDOR-NAME-EXIT.                                   
                                                                                
       2000-SET-STATIC-FIELDS-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
VF0625*3000-SET-NETWORK-INFO.                                                   
                                                                                
VF0625*    MOVE TRDPRT-KOHL-INCHG-ID-DESC TO WS-FIELD-04.                       
VF0625*    MOVE TRDPRT-KOHL-INCHG-ID TO WS-FIELD-05                             
VF0629*                                 WS-FIELD-32.                            
                                                                                
VF0625*    PERFORM 4000-GET-NETWORK                                             
VF0625*       THRU 4000-GET-NETWORK-EXIT.                                       
                                                                                
VF0625*3000-SET-NETWORK-INFO-EXIT.                                              
VF0625*    EXIT.                                                                
                                                                                
                                                                                
VF0625*4000-GET-NETWORK.                                                        
                                                                                
VF0625*    EXEC SQL                                                             
VF0625*       SELECT NETWK_NME                                                  
VF0625*       INTO   :NETWRK-NETWK-NME                                          
VF0625*       FROM   TNETWRK                                                    
VF0625*       WHERE  NETWK_ID = :TRDPRT-NETWK-ID                                
VF0625*    END-EXEC.                                                            
                                                                                
VF0625*    IF SQLCODE = +000                                                    
VF0625*       CONTINUE                                                          
VF0625*    ELSE                                                                 
VF0625*       MOVE SPACES TO WS-SAVE-NETWORK                                    
VF0625*                      WS-FIELD-11                                        
VF0625*       GO TO 4000-GET-NETWORK-EXIT.                                      
                                                                                
VF0625*    MOVE SPACES TO WS-SAVE-NETWORK                                       
VF0625*                   WS-FIELD-11.                                          
VF0625*    IF NETWRK-NETWK-NME = 'IBM GLOBAL'                                   
VF0625*       MOVE 'IBM' TO WS-SAVE-NETWORK                                     
VF0625*    ELSE                                                                 
VF0625*       IF NETWRK-NETWK-NME = 'GXS'                                       
VF0625*          MOVE 'GEIS' TO WS-SAVE-NETWORK                                 
VF0625*       ELSE                                                              
VF0625*          IF NETWRK-NETWK-NME = 'V.F.'                                   
VF0625*             MOVE 'VF' TO WS-SAVE-NETWORK                                
VF0625*          ELSE                                                           
VF0625*             IF NETWRK-NETWK-NME = 'IPNET'                               
VF0625*                MOVE 'IPNET' TO WS-SAVE-NETWORK                          
VF0625*             ELSE                                                        
VF0625*                MOVE NETWRK-NETWK-NME TO WS-SAVE-NETWORK.                
                                                                                
VF0625*    IF TRDPRT-FA-COM-MTHD-CDE = 'V'                                      
VF0625*       STRING 'FA.'                DELIMITED BY SIZE                     
VF0625*              WS-SAVE-NETWORK      DELIMITED BY ' '                      
VF0625*                 INTO WS-FIELD-11                                        
VF0625*    ELSE                                                                 
VF0625*       STRING 'FA.IPNET'           DELIMITED BY SIZE                     
VF0625*                 INTO WS-FIELD-11.                                       
                                                                                
VF0625*4000-GET-NETWORK-EXIT.                                                   
VF0625*    EXIT.                                                                
                                                                                
                                                                                
       3200-GET-VENDOR-NAME.                                                    
                                                                                
           EXEC SQL                                                             
              SELECT ENT_NAME_DESC                                              
              INTO   :ENTNME-ENT-NAME-DESC                                      
              FROM   TENTNME                                                    
              WHERE  ENT_ID   = :TRDPRT-ENT-ID                                  
                AND  TYPE_CDE = '01'                                            
           END-EXEC.                                                            
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES TO WS-FIELD-01                                        
              MOVE SPACES TO WS-FIELD-30                                        
              GO TO 3200-GET-VENDOR-NAME-EXIT.                                  
                                                                                
           MOVE SPACES TO WS-ENT-NAME-DESC.                                     
           MOVE ENTNME-ENT-NAME-DESC TO WS-ENT-NAME-DESC.                       
           INSPECT WS-ENT-NAME-DESC REPLACING ALL '&' BY '-'.                   
           INSPECT WS-ENT-NAME-DESC REPLACING ALL '(' BY '-'.                   
           INSPECT WS-ENT-NAME-DESC REPLACING ALL ')' BY '-'.                   
           INSPECT WS-ENT-NAME-DESC REPLACING ALL ',' BY '-'.                   
                                                                                
           MOVE SPACES TO WS-FIELD-30.                                          
           STRING ENTNME-ENT-NAME-DESC DELIMITED BY '  '                        
                  '-GS'                DELIMITED BY SIZE                        
                     INTO WS-FIELD-30.                                          
                                                                                
           INSPECT WS-FIELD-30 REPLACING ALL '&' BY '-'.                        
           INSPECT WS-FIELD-30 REPLACING ALL '(' BY '-'.                        
           INSPECT WS-FIELD-30 REPLACING ALL ')' BY '-'.                        
           INSPECT WS-FIELD-30 REPLACING ALL ',' BY '-'.                        
                                                                                
           MOVE SPACES TO WS-FIELD-01.                                          
                                                                                
           IF DUP-ENT-ID                                                        
              STRING WS-ENT-NAME-DESC  DELIMITED BY '  '                        
                     '~'               DELIMITED BY SIZE                        
                     WS-10-CHAR-ENT-ID DELIMITED BY SIZE                        
                     '  '              DELIMITED BY SIZE                        
                     WS-DUP-IND        DELIMITED BY SIZE                        
                        INTO WS-FIELD-01                                        
           ELSE                                                                 
              STRING WS-ENT-NAME-DESC  DELIMITED BY '  '                        
                     '~'               DELIMITED BY SIZE                        
                     WS-10-CHAR-ENT-ID DELIMITED BY SIZE                        
                        INTO WS-FIELD-01.                                       
                                                                                
       3200-GET-VENDOR-NAME-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2500-GET-TRAN-SETS.                                                      
                                                                                
           EXEC SQL                                                             
               FETCH TRDTRN_CURSOR                                              
               INTO :TRDTRN-VER-NBR,                                            
                    :TRDTRN-PROD-TEST-IND,                                      
                    :TRDTRN-TRAN-SET-CDE,                                       
                    :TRDTRN-FUNC-CDE,                                           
                    :TRDTRN-DIR-IND                                             
VF0629*             :TRDTRN-COM-MTHD-CDE                                        
           END-EXEC.                                                            
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-TRAN-SETS-SW                                
                 GO TO 2500-GET-TRAN-SETS-EXIT                                  
              ELSE                                                              
                 MOVE '2500-GET-TRAN-SETS' TO PV-PARAGRAPH-NAME                 
                 MOVE 'FETCH TRDTRN ROWS' TO PV-DB2-OPER-ATTEMPTED              
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           PERFORM 3500-GET-CNTL-NBRS                                           
              THRU 3500-GET-CNTL-NBRS-EXIT.                                     
                                                                                
           IF TRDTRN-DIR-IND = 'R'                                              
              PERFORM 3600-SET-INBOUND-FIELDS                                   
                 THRU 3600-SET-INBOUND-FIELDS-EXIT                              
           ELSE                                                                 
              PERFORM 3700-SET-OUTBOUND-FIELDS                                  
                 THRU 3700-SET-OUTBOUND-FIELDS-EXIT.                            
                                                                                
           PERFORM 3800-SET-REMAINING-FIELDS                                    
              THRU 3800-SET-REMAINING-FIELDS-EXIT.                              
                                                                                
           IF NOT VERSION-ERROR                                                 
              PERFORM 3900-WRITE-OUTPUT-REC                                     
                 THRU 3900-WRITE-OUTPUT-REC-EXIT.                               
                                                                                
       2500-GET-TRAN-SETS-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       3500-GET-CNTL-NBRS.                                                      
                                                                                
           EXEC SQL                                                             
              SELECT LAST_CNTL_NBR,                                             
                     OVRRD_CNTL_NBR,                                            
                     OVRRD_CNTL_NBR_IND                                         
              INTO   :TRDFNC-LAST-CNTL-NBR,                                     
                     :TRDFNC-OVRRD-CNTL-NBR,                                    
                     :TRDFNC-OVRRD-CNTL-NBR-IND                                 
              FROM   TTRDFNC                                                    
              WHERE  TP_DKEY  = :TRDPRT-TP-DKEY                                 
                AND  FUNC_CDE = :TRDTRN-FUNC-CDE                                
                AND  DIR_IND  = :TRDTRN-DIR-IND                                 
           END-EXEC.                                                            
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '0' TO WS-FIELD-36, WS-FIELD-37                              
              GO TO 3500-GET-CNTL-NBRS-EXIT.                                    
                                                                                
           IF TRDTRN-DIR-IND = 'R'                                              
              MOVE SPACES TO WS-FIELD-37                                        
              IF TRDFNC-OVRRD-CNTL-NBR-IND = 'Y'                                
                 MOVE TRDFNC-OVRRD-CNTL-NBR TO WS-CNTL-NBR                      
                 MOVE WS-CONTROL-NUMBER     TO WS-FIELD-36                      
              ELSE                                                              
                 MOVE TRDFNC-LAST-CNTL-NBR  TO WS-CNTL-NBR                      
                 MOVE WS-CONTROL-NUMBER     TO WS-FIELD-36                      
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES TO WS-FIELD-36                                        
              IF TRDFNC-OVRRD-CNTL-NBR-IND = 'Y'                                
                 MOVE TRDFNC-OVRRD-CNTL-NBR TO WS-CNTL-NBR                      
                 MOVE WS-CONTROL-NUMBER     TO WS-FIELD-37                      
              ELSE                                                              
                 MOVE TRDFNC-LAST-CNTL-NBR  TO WS-CNTL-NBR                      
                 MOVE WS-CONTROL-NUMBER     TO WS-FIELD-37                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-GET-CNTL-NBRS-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       3600-SET-INBOUND-FIELDS.                                                 
                                                                                
           IF TRDTRN-VER-NBR = '003040VICS'                                     
              IF TRDTRN-TRAN-SET-CDE = '810'                                    
                 MOVE 'kin34vi' TO WS-FIELD-53                                  
              END-IF                                                            
              IF TRDTRN-TRAN-SET-CDE = '855'                                    
                 MOVE 'kopr34vi' TO WS-FIELD-53                                 
              END-IF                                                            
              IF TRDTRN-TRAN-SET-CDE = '856'                                    
                 MOVE 'kosn34vis' TO WS-FIELD-53                                
              END-IF                                                            
           ELSE                                                                 
              IF TRDTRN-VER-NBR = '003050VICS'                                  
                 IF TRDTRN-TRAN-SET-CDE = '810'                                 
                    MOVE 'kin35vi' TO WS-FIELD-53                               
                 END-IF                                                         
                 IF TRDTRN-TRAN-SET-CDE = '855'                                 
                    MOVE 'kopr35vi' TO WS-FIELD-53                              
                 END-IF                                                         
                 IF TRDTRN-TRAN-SET-CDE = '856'                                 
                    MOVE 'kosn35vis' TO WS-FIELD-53                             
                 END-IF                                                         
              ELSE                                                              
                 IF TRDTRN-VER-NBR = '004010VICS'                               
                    IF TRDTRN-TRAN-SET-CDE = '810'                              
                       MOVE 'kin41vi' TO WS-FIELD-53                            
                    END-IF                                                      
                    IF TRDTRN-TRAN-SET-CDE = '855'                              
                       MOVE 'kopr41vi' TO WS-FIELD-53                           
                    END-IF                                                      
                    IF TRDTRN-TRAN-SET-CDE = '856'                              
                       MOVE 'kosn41vis' TO WS-FIELD-53                          
                    END-IF                                                      
SP0404              IF TRDTRN-TRAN-SET-CDE = '870'                              
SP0404                 MOVE 'krs41vi' TO WS-FIELD-53                            
SP0404              END-IF                                                      
                    IF TRDTRN-TRAN-SET-CDE = '210'                              
                       MOVE 'kim41vi' TO WS-FIELD-53                            
                    END-IF                                                      
                    IF TRDTRN-TRAN-SET-CDE = '214'                              
                       MOVE 'kss41vi' TO WS-FIELD-53                            
                    END-IF                                                      
PS1006              IF TRDTRN-TRAN-SET-CDE = '310'                              
PS1006                 MOVE 'koi41vi' TO WS-FIELD-53                            
PS1006              END-IF                                                      
              ELSE                                                              
                 IF TRDTRN-VER-NBR = '004010'                                   
                    IF TRDTRN-TRAN-SET-CDE = '210'                              
                       MOVE 'kim41vi' TO WS-FIELD-53                            
                    END-IF                                                      
                    IF TRDTRN-TRAN-SET-CDE = '214'                              
                       MOVE 'kss41vi' TO WS-FIELD-53                            
                    END-IF.                                                     
                                                                                
           EVALUATE TRDTRN-TRAN-SET-CDE                                         
              WHEN '810'                                                        
                 MOVE 'prod810' TO WS-FIELD-54                                  
                 MOVE 'test810' TO WS-FIELD-55                                  
              WHEN '855'                                                        
                 MOVE 'prod855' TO WS-FIELD-54                                  
                 MOVE 'test855' TO WS-FIELD-55                                  
              WHEN '856'                                                        
                 MOVE 'prod856' TO WS-FIELD-54                                  
                 MOVE 'test856' TO WS-FIELD-55                                  
SP0404        WHEN '870'                                                        
SP0404           MOVE 'prod870' TO WS-FIELD-54                                  
SP0404           MOVE 'test870' TO WS-FIELD-55                                  
              WHEN '210'                                                        
                 MOVE 'prod210' TO WS-FIELD-54                                  
                 MOVE 'test210' TO WS-FIELD-55                                  
              WHEN '214'                                                        
                 MOVE 'prod214' TO WS-FIELD-54                                  
                 MOVE 'test214' TO WS-FIELD-55                                  
PS1006        WHEN '310'                                                        
PS1006           MOVE 'prod310' TO WS-FIELD-54                                  
PS1006           MOVE 'test310' TO WS-FIELD-55                                  
           END-EVALUATE.                                                        
                                                                                
           MOVE SPACES TO WS-FIELD-56                                           
                          WS-FIELD-57                                           
                          WS-FIELD-58                                           
                          WS-FIELD-59                                           
                          WS-FIELD-63.                                          
                                                                                
           MOVE 'No'   TO WS-FIELD-62.                                          
                                                                                
       3600-SET-INBOUND-FIELDS-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       3700-SET-OUTBOUND-FIELDS.                                                
                                                                                
           MOVE SPACES TO WS-FIELD-53                                           
                          WS-FIELD-54                                           
                          WS-FIELD-55.                                          
                                                                                
           MOVE SPACES TO WS-FIELD-56.                                          
           IF TRDTRN-PROD-TEST-IND = 'P'                                        
              STRING WS-10-CHAR-ENT-ID   DELIMITED BY SIZE                      
                     '~'                 DELIMITED BY SIZE                      
                     TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                      
                        INTO WS-FIELD-56                                        
           ELSE                                                                 
              STRING WS-9-CHAR-ENT-ID    DELIMITED BY SIZE                      
                     'T'                 DELIMITED BY SIZE                      
                     '~'                 DELIMITED BY SIZE                      
                     TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                      
                        INTO WS-FIELD-56.                                       
                                                                                
           MOVE SPACES TO WS-FIELD-57.                                          
                                                                                
           IF TRDTRN-TRAN-SET-CDE = '850' AND                                   
              TRDTRN-VER-NBR = '003040VICS'                                     
                 MOVE 'kpo34ot' TO WS-FIELD-57.                                 
           IF TRDTRN-TRAN-SET-CDE = '850' AND                                   
              TRDTRN-VER-NBR = '003050VICS'                                     
                 MOVE 'kpo35ot' TO WS-FIELD-57.                                 
           IF TRDTRN-TRAN-SET-CDE = '850' AND                                   
              TRDTRN-VER-NBR = '004010VICS'                                     
                 MOVE 'kpo41ot' TO WS-FIELD-57.                                 
                                                                                
SP0506*    IF TRDTRN-TRAN-SET-CDE = '850' AND                                   
SP0506*       TRDTRN-VER-NBR = '004010VICS' AND                                 
SP0506*       (TRDPRT-ENT-ID  = PC-ENT-ID-9425 OR                               
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10593 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10594 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10759 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-11440 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-29128 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-31520 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-40425 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-30692 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-11905)                                
SP0506*          MOVE 'kpo41oti' TO WS-FIELD-57.                                
                                                                                
SP0506*    IF TRDTRN-TRAN-SET-CDE = '850' AND                                   
SP0506*       TRDTRN-VER-NBR = '003040VICS' AND                                 
SP0506*       (TRDPRT-ENT-ID  = PC-ENT-ID-9425 OR                               
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10593 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10594 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-10759 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-11440 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-29128 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-31520 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-40425 OR                              
SP0506*        TRDPRT-ENT-ID  = PC-ENT-ID-11905)                                
SP0506*          MOVE 'kpo34oti' TO WS-FIELD-57.                                
                                                                                
********  The 4010 map is loaded for 852's regardless of version                
********  because there is no 3040 or 3050 map.  This will decrease             
********  the number of problems caused by trading partner relationships        
********  that have been updated on the mainframe, but not yet loaded           
********  on Unix.                                                              
           IF TRDTRN-TRAN-SET-CDE = '852' AND                                   
              TRDTRN-VER-NBR = '003040VICS'                                     
                 MOVE 'kpd41ot' TO WS-FIELD-57.                                 
           IF TRDTRN-TRAN-SET-CDE = '852' AND                                   
              TRDTRN-VER-NBR = '003050VICS'                                     
                 MOVE 'kpd41ot' TO WS-FIELD-57.                                 
           IF TRDTRN-TRAN-SET-CDE = '852' AND                                   
              TRDTRN-VER-NBR = '004010VICS'                                     
                 MOVE 'kpd41ot' TO WS-FIELD-57.                                 
           IF TRDTRN-TRAN-SET-CDE = '820' AND                                   
              TRDTRN-VER-NBR = '004010VICS'                                     
                 MOVE 'kra41ot' TO WS-FIELD-57.                                 
                                                                                
20669 **  Text Messages - Document 864 (WR20669)                                
20669      IF TRDTRN-TRAN-SET-CDE = '864' AND                                   
20669         TRDTRN-VER-NBR = '004010VICS'                                     
20669            MOVE 'ktx41ot' TO WS-FIELD-57.                                 
20669                                                                           
JF0502     IF TRDTRN-TRAN-SET-CDE = '816' AND                                   
JF0502        TRDTRN-VER-NBR = '004010VICS'                                     
JF0502           MOVE 'kor41ot' TO WS-FIELD-57.                                 
JF0502                                                                          
SP0506     IF TRDTRN-TRAN-SET-CDE = '860' AND                                   
SP0506        TRDTRN-VER-NBR = '004010VICS'                                     
SP0506           MOVE 'kpc41ot' TO WS-FIELD-57.                                 
SP0506                                                                          
      ****************************************************************          
      *                                                                         
      *  THE 840 DOCUMENT IS USED AS A DUMMY FOR TRADING PARTNER                
      *  ID'S WHICH DO NOT HAVE AN OUTBOUND DOCUMENT LOADED                     
      *  ON UNIX.  WE NEED TO LOAD THE DUMMY TO ACCOUNT FOR                     
      *  OUTBOUND FA CONTROL NUMBERS.                                           
      *                                                                         
      ****************************************************************          
                                                                                
           IF TRDTRN-TRAN-SET-CDE = '840'                                       
              MOVE 'dummy' TO WS-FIELD-57.                                      
                                                                                
           MOVE SPACES TO WS-FIELD-58.                                          
                                                                                
VF0625     IF TRDTRN-TRAN-SET-CDE = '852'                                       
VF0625           STRING 'IPNET852' DELIMITED BY SIZE                            
VF0625                      INTO WS-FIELD-58                                    
VF0625     ELSE                                                                 
VF0625     IF TRDTRN-TRAN-SET-CDE = '864'                                       
VF0625           STRING 'IPNET864'      DELIMITED BY SIZE                       
VF0625                      INTO WS-FIELD-58                                    
VF0625     ELSE                                                                 
VF0625        IF TRDTRN-TRAN-SET-CDE = '820'                                    
VF0625           STRING 'IPNET820'      DELIMITED BY SIZE                       
VF0625                      INTO WS-FIELD-58                                    
VF0625     ELSE                                                                 
VF0625     IF TRDTRN-TRAN-SET-CDE = '816'                                       
VF0625           STRING 'IPNET816'      DELIMITED BY SIZE                       
VF0625                      INTO WS-FIELD-58                                    
SP0506     ELSE                                                                 
SP0506     IF TRDTRN-TRAN-SET-CDE = '860'                                       
SP0506           STRING 'IPNET860'      DELIMITED BY SIZE                       
SP0506                      INTO WS-FIELD-58                                    
VF0625     ELSE                                                                 
VF0625     IF TRDTRN-TRAN-SET-CDE = '850'                                       
VF0625           STRING 'IPNET'         DELIMITED BY SIZE                       
VF0625                      INTO WS-FIELD-58.                                   
                                                                                
           MOVE TRDTRN-FUNC-CDE TO WS-FIELD-59.                                 
           MOVE 'Yes'           TO WS-FIELD-62.                                 
                                                                                
           IF TRDTRN-PROD-TEST-IND = 'P'                                        
              MOVE 'Production' TO WS-FIELD-63                                  
           ELSE                                                                 
              MOVE 'Test      ' TO WS-FIELD-63.                                 
                                                                                
       3700-SET-OUTBOUND-FIELDS-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
       3800-SET-REMAINING-FIELDS.                                               
                                                                                
           MOVE 'N' TO VERSION-ERROR-SW.                                        
                                                                                
           EVALUATE TRDTRN-VER-NBR                                              
              WHEN '003040VICS'                                                 
                 STRING '00304-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-NAME                                         
                 MOVE '00200' TO WS-FIELD-15                                    
              WHEN '003050VICS'                                                 
                 STRING '00305-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-NAME                                         
                 MOVE '00200' TO WS-FIELD-15                                    
              WHEN '004010VICS'                                                 
                 STRING '00401-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-NAME                                         
                 MOVE '00401' TO WS-FIELD-15                                    
              WHEN '004010'                                                     
                 STRING '00401-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-NAME                                         
                 MOVE '00401' TO WS-FIELD-15                                    
              WHEN OTHER                                                        
                 MOVE 'Y' TO VERSION-ERROR-SW                                   
                 GO TO 3800-SET-REMAINING-FIELDS-EXIT                           
           END-EVALUATE.                                                        
                                                                                
           MOVE TRDTRN-VER-NBR      TO WS-FIELD-35.                             
                                                                                
           IF TRDTRN-TRAN-SET-CDE = '850' AND TRDTRN-PROD-TEST-IND = 'T'        
              MOVE WS-NAME-FIELD-50 TO WS-FIELD-50                              
SP0506     ELSE                                                                 
SP0506     IF TRDTRN-TRAN-SET-CDE = '860' AND TRDTRN-PROD-TEST-IND = 'T'        
SP0506        MOVE WS-NAME-FIELD-50 TO WS-FIELD-50                              
           ELSE                                                                 
              MOVE WS-NAME          TO WS-FIELD-50                              
           END-IF.                                                              
                                                                                
           MOVE TRDTRN-VER-NBR      TO WS-FIELD-51.                             
           MOVE TRDTRN-TRAN-SET-CDE TO WS-FIELD-52.                             
                                                                                
********  The 4010 map is loaded for 852's regardless of version                
********  because there is no 3040 or 3050 map.  This will decrease             
********  the number of problems caused by trading partner relationships        
********  that have been updated on the mainframe, but not yet loaded           
********  on Unix.                                                              
           IF TRDTRN-TRAN-SET-CDE = '852' AND                                   
              TRDTRN-VER-NBR = '003040VICS'                                     
                 MOVE '004010VICS' TO WS-FIELD-35                               
                 MOVE '004010VICS' TO WS-FIELD-51                               
                 STRING '00401-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-FIELD-50                                     
                 MOVE '00401' TO WS-FIELD-15.                                   
           IF TRDTRN-TRAN-SET-CDE = '852' AND                                   
              TRDTRN-VER-NBR = '003050VICS'                                     
                 MOVE '004010VICS' TO WS-FIELD-35                               
                 MOVE '004010VICS' TO WS-FIELD-51                               
                 STRING '00401-'            DELIMITED BY SIZE                   
                        TRDTRN-TRAN-SET-CDE DELIMITED BY SIZE                   
                           INTO WS-FIELD-50                                     
                 MOVE '00401' TO WS-FIELD-15.                                   
                                                                                
       3800-SET-REMAINING-FIELDS-EXIT.                                          
           EXIT.                                                                
                                                                                
                                                                                
       3900-WRITE-OUTPUT-REC.                                                   
                                                                                
           IF DUP-ENT-ID                                                        
              DISPLAY 'ENT-NAME: ' WS-ENT-NAME-DESC                             
              DISPLAY 'ENT-ID:   ' TRDPRT-ENT-ID                                
              DISPLAY 'TP-ID:    ' TRDPRT-VEND-INCHG-ID                         
              DISPLAY 'SET-CDE:  ' TRDTRN-TRAN-SET-CDE.                         
                                                                                
           MOVE SPACES TO WS-FINAL-RECORD.                                      
           MOVE WS-OUTPUT-RECORD TO WS-STRING-RECORD.                           
                                                                                
           STRING WS-STRING-01 DELIMITED BY '   '                               
                  WS-STRING-03 DELIMITED BY '    '                              
                  WS-STRING-05 DELIMITED BY ' '                                 
                  WS-STRING-11 DELIMITED BY ' '                                 
                  WS-STRING-17 DELIMITED BY ' '                                 
                  WS-STRING-25 DELIMITED BY '  '                                
                  WS-STRING-30 DELIMITED BY '  '                                
                  WS-STRING-31 DELIMITED BY '    '                              
                  WS-STRING-32 DELIMITED BY ' '                                 
                  WS-STRING-35 DELIMITED BY ' '                                 
                  WS-STRING-36 DELIMITED BY ' '                                 
                  WS-STRING-37 DELIMITED BY ' '                                 
                  WS-STRING-39 DELIMITED BY '  '                                
                  WS-STRING-49 DELIMITED BY '  '                                
                  WS-STRING-50 DELIMITED BY ' '                                 
                  WS-STRING-51 DELIMITED BY ' '                                 
                  WS-STRING-53 DELIMITED BY ' '                                 
                  WS-STRING-54 DELIMITED BY ' '                                 
                  WS-STRING-55 DELIMITED BY ' '                                 
                  WS-STRING-56 DELIMITED BY ' '                                 
                  WS-STRING-57 DELIMITED BY ' '                                 
                  WS-STRING-58 DELIMITED BY ' '                                 
                  WS-STRING-59 DELIMITED BY ' '                                 
                  WS-STRING-62 DELIMITED BY ' '                                 
                  WS-STRING-63 DELIMITED BY ' '                                 
                  WS-STRING-99 DELIMITED BY SIZE                                
                     INTO WS-FINAL-RECORD.                                      
                                                                                
           WRITE OUTPUT-RECORD FROM WS-FINAL-RECORD.                            
                                                                                
       3900-WRITE-OUTPUT-REC-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       9999-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
