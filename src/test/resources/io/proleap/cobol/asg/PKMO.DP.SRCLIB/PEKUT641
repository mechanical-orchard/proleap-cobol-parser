      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PEKUT641.                                       00050000
       AUTHOR.          TOM BOYD.                                       00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.    OCT. 2005.                                      00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM WILL REBUILD MATERIALIZED VIEWS.                   00120000
      ******************************************************************00121000
      **  HISTORY LOG:                                                 *00122000
      ******************************************************************00123000
      * DATE:         10/19/2005                                       *00124000
      * WR/IR#:       WR28048                                          *00125000
      * PROGRAMMER:   TOM BOYD                                         *00126000
      * DESCRIPTION:  INITIAL CREATION.                                *00127000
      ******************************************************************00128104
      * DATE:         07/30/2018                                       *00128204
      * WR/IR#:       CHG0204126                                       *00128304
      * PROGRAMMER:   COGNIZANT                                        *00128404
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *00128504
      ******************************************************************00128604
      ******************************************************************00129000
                                                                        00130000
      ******************************************************************00140000
       ENVIRONMENT DIVISION.                                            00150000
      ******************************************************************00160000
                                                                        00161000
       CONFIGURATION SECTION.                                           00162000
                                                                        00163000
       SOURCE-COMPUTER. IBM-3090.                                       00164000
       OBJECT-COMPUTER. IBM-3090.                                       00165000
                                                                        00166000
       INPUT-OUTPUT SECTION.                                            00167000
                                                                        00168000
       FILE-CONTROL.                                                    00169000
           SELECT TABLE-FILE                                            00170000
                  ASSIGN                    TO INP01.                   00180000
                                                                        00190000
           SELECT ORACLE-LOGON-FILE                                     00191001
                  ASSIGN                    TO ORALOGON.                00192001
                                                                        00193001
      ******************************************************************00200000
       DATA DIVISION.                                                   00210000
      ******************************************************************00220000
                                                                        00230000
       FILE SECTION.                                                    00240000
       FD  TABLE-FILE                                                   00250000
           RECORDING MODE F                                             00260000
           BLOCK CONTAINS 0 RECORDS                                     00270000
           LABEL RECORDS ARE OMITTED                                    00280000
           DATA RECORD IS DEPT-RECORD.                                  00290000
       01  TABLE-RECORD                     PIC X(80).                  00300000
                                                                        00310000
       FD  ORACLE-LOGON-FILE                                            00311001
           RECORDING MODE IS F                                          00312001
           LABEL RECORDS ARE STANDARD                                   00313001
           BLOCK CONTAINS 0 RECORDS                                     00314001
           RECORD CONTAINS 80 CHARACTERS                                00315001
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                      00316001
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                       00317001
                                                                        00318001
       WORKING-STORAGE SECTION.                                         00320000
                                                                        00330000
      ******************************************************************00340000
      * PC PROGRAM CONSTANTS                                            00350000
      ******************************************************************00360000
       01  PC-PROGRAM-CONSTANTS.                                        00370000
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE        00380001
                                                           'PEKUT641'.  00390000
           05  PC-MESSAGE-OK                PIC X(36) VALUE             00400000
                    'MATERIALIZED VIEW REFRESH SUCCESSFUL'.             00410000
                                                                        00420000
      ******************************************************************00430000
      * PV PROGRAM VARIABLES                                            00440000
      ******************************************************************00450000
       01  PV-PROGRAM-VARIABLES.                                        00460000
           05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS      00461001
                                                       COMP SYNC.       00462001
           05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS      00463001
                                                       COMP SYNC.       00464001
           05  PV-TABLE-NAME                PIC X(30) VALUE SPACES.     00470000
           05  PV-SQL-CODE                  PIC 9(09) VALUE 0.          00480000
           05  PV-PARAGRAPH-NAME            PIC X(40) VALUE SPACES.     00490000
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.    00500000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACES.     00510000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACES.     00520000
           05  PV-CPU-TIME                  PIC  9(08) VALUE 0.         00530000
           05  PV-CURRENT-TIME              PIC X(08) VALUE SPACES.     00540000
           05  PV-RUN-DATE                  PIC X(10) VALUE SPACES.     00550000
           05  PV-SCHEMA                    PIC X(06) VALUE ZEROES.     00560000
           05  PV-TABLE                     PIC X(35) VALUE ZEROES.     00570000
           05  PV-MESSAGE                   PIC X(255) VALUE SPACES.    00580000
           05  PV-MESSAGE-36                PIC X(36) VALUE SPACES.     00590000
                                                                        00600000
       01  CC-CONTROL-CARD-TBL-REC.                                     00610000
           05  CC-SCHEMA                      PIC X(06) VALUE ZEROES.   00620000
           05  FILLER                         PIC X(01) VALUE SPACES.   00630000
           05  CC-TABLE                       PIC X(35) VALUE ZEROES.   00640000
           05  FILLER                         PIC X(38) VALUE SPACES.   00650000
                                                                        00660000
      ******************************************************************00670000
      * PS PROGRAM SWITCHES                                            *00680000
      ******************************************************************00690000
       01  PS-PROGRAM-SWITCHES.                                         00700000
006510     05  PS-END-OF-LOGON-FILE-SW     PIC X(01)   VALUE 'N'.       00701001
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.       00702001
           05  PS-END-OF-PROC-INST-FILE-SW PIC X(01)   VALUE 'N'.       00703001
               88  PS-END-OF-PROC-INST-FILE            VALUE 'Y'.       00704001
               88  PS-NOT-END-OF-PROC-INST-FILE        VALUE 'N'.       00705001
           05  PS-END-OF-TABLE-FILE-SW      PIC X(01)  VALUE 'N'.       00710000
               88  PS-END-OF-TABLE-FILE                VALUE 'Y'.       00720000
               88  PS-NOT-END-OF-TABLE-FILE            VALUE 'N'.       00730000
           05  PS-ORA-SRC-CTL-DONE-SW  PIC  X(01)      VALUE 'N'.       00731002
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.       00732002
                                                                        00740000
       01  CC-ORACLE-CONNECTION.                                        00741003
           05  CC-ORACLE-USERID             PIC X(40).                  00742003
               88 CC-AUTO-LOGON                         VALUE '/'.      00743003
           05  CC-ORACLE-PASSWORD           PIC X(40).                  00744003
                                                                        00745003
      ******************************************************************00750000
      * PA PROGRAM ACCUMULATORS                                        *00760000
      ******************************************************************00770000
       01  PA-PROGRAM-ACCUMULATORS.                                     00780000
           05  PA-RECS-WRITTEN               PIC S9(13) COMP-3 VALUE 0. 00790000
           05  PA-READ-COUNTER               PIC S9(11) COMP-3 VALUE 0. 00800000
                                                                        00810000
      ******************************************************************00820000
      * AC ABEND CODES                                                 *00830000
      ******************************************************************00840000
                                                                        00850000
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0. 00860000
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.   00870000
           88  ABEND-4002-READ-ERROR                     VALUE +4002.   00880000
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.   00890000
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.   00900000
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.   00910000
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.   00920000
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.   00930000
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.   00940000
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.   00950000
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.   00960000
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.   00970000
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.   00980000
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.   00990000
                                                                        01000000
      *----------------------------------------------------------       01010000
      *                      O  R  A  C  L  E                           01020000
      *==========================================================       01030000
      *                                                                 01040000
      * ORACLE DECLARE SECTION                                          01050000
      * ORACLE USED AND RETURNED VARIABLES                              01060000
      *                                                                 01070000
      *----------------------------------------------------------       01080000
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                     01090000
                                                                        01100000
      *----------------------------------------------------------       01110000
      * ORACLE LOGON WORKING STORAGE COPYBOOK                           01120000
      *----------------------------------------------------------       01130000
                                                                        01140000
       01  PC-ENT01-TYPE-CDE                PIC X(02)  VALUE '01'.      01150000
       01  PV-ORACLE-ID                     PIC X(1)   VALUE '/'.       01160000
                                                                        01170000
       01  USERNAME                         PIC X(10)  VARYING.         01180000
       01  PASSWD                           PIC X(10)  VARYING.         01190000
                                                                        01200000
       01  ORACLE-RETURN                    PIC X(200) VALUE SPACES.    01210000
                                                                        01220000
           EXEC SQL END DECLARE SECTION END-EXEC.                       01230000
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                       01240000
                                                                        01250000
       EJECT                                                            01260000
                                                                        01270000
       PROCEDURE DIVISION.                                              01280000
                                                                        01290000
      ******************************************************************01300000
      * MAIN PROCESSING                                                 01310000
      ******************************************************************01320000
       A100-PROGRAM-MAINLINE.                                           01330000
                                                                        01340000
           PERFORM B100-INITIALIZE.                                     01350000
                                                                        01360000
           PERFORM B200-READ-TABLE-FILE.                                01370000
           PERFORM C100-CONTROL                                         01380000
             UNTIL PS-END-OF-TABLE-FILE.                                01390000
                                                                        01400000
           DISPLAY ' '.                                                 01410000
           DISPLAY 'ALL RECORDS SUCCESSFULLY REBUILT'.                  01420000
           DISPLAY ' '.                                                 01430000
                                                                        01440000
           PERFORM B900-EOJ-ROUTINE.                                    01450000
                                                                        01460000
           GOBACK.                                                      01470000
                                                                        01480000
      ******************************************************************01490000
      * OPEN FILE                                                       01500000
      ******************************************************************01510000
       B100-INITIALIZE.                                                 01520000
                                                                        01520102
           OPEN INPUT ORACLE-LOGON-FILE.                                01521001
                                                                        01522001
           PERFORM 7000-READ-ORACLE-LOGON.                              01523001
                                                                        01524001
           CLOSE ORACLE-LOGON-FILE.                                     01525001
                                                                        01526001
           IF ((PS-END-OF-LOGON-FILE)                                   01527001
                   OR (CC-ORACLE-CONNECTION = SPACES))                  01528001
               DISPLAY '** ABEND **'                                    01529001
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '  01529101
                         'NOT PROVIDED'                                 01529201
               SET PS-ORA-SRC-EMPTY TO TRUE                             01529301
               CALL 'ILBOABN0' USING ABEND-CODE                         01529401
           END-IF.                                                      01529501
                                                                        01529601
           IF CC-AUTO-LOGON                                             01529701
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        01529801
               EXEC SQL                                                 01529901
                    CONNECT :USERNAME                                   01530001
               END-EXEC                                                 01530101
           ELSE                                                         01530201
               INITIALIZE PV-ORACLE-USERID-LEN                          01530301
                          PV-ORACLE-PASSWORD-LEN                        01530401
               INSPECT CC-ORACLE-USERID                                 01530501
                   TALLYING PV-ORACLE-USERID-LEN                        01530601
                FOR CHARACTERS BEFORE INITIAL SPACE                     01530701
            INSPECT CC-ORACLE-PASSWORD                                  01530801
                TALLYING PV-ORACLE-PASSWORD-LEN                         01530901
                FOR CHARACTERS BEFORE INITIAL SPACE                     01531001
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO           01531101
                      USERNAME-ARR                                      01531201
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                   01531301
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO       01531401
                      PASSWD-ARR                                        01531501
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                   01531601
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '       01531701
                      USERNAME-ARR                                      01531801
            EXEC SQL                                                    01531901
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                   01532001
            END-EXEC                                                    01532101
            INITIALIZE CC-ORACLE-PASSWORD                               01532201
                       PASSWD                                           01532301
            END-IF.                                                     01532401
                                                                        01532501
020500     IF (SQLCODE NOT = 0)                                         01532601
020900         DISPLAY PV-PARAGRAPH-NAME                                01532701
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'  01532801
021000         PERFORM 9900-ORACLE-ABEND                                01532901
021200     END-IF.                                                      01533001
                                                                        01533101
      ******************************************************************01533201
      * NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG        01533301
      * OTHERWISE I GET A ORA-3113 ERROR                                01533401
      ******************************************************************01533501
           EXEC SQL                                                     01533601
            ALTER SESSION                                               01533701
                  SET CURSOR_SHARING = 'EXACT'                          01533801
           END-EXEC.                                                    01533901
                                                                        01534000
           OPEN INPUT TABLE-FILE.                                       01540000
                                                                        01550000
      ******************************************************************01580000
      * B200-READ-TABLE-FILE.                                           01590000
      *     READS THE TABLE NAME FOR THE RECORD THAT SHOULD BE REBUILT  01600000
      ******************************************************************01610000
       B200-READ-TABLE-FILE.                                            01620000
                                                                        01630000
           READ TABLE-FILE INTO CC-CONTROL-CARD-TBL-REC                 01640000
                AT END SET PS-END-OF-TABLE-FILE TO TRUE.                01650000
                                                                        01660000
           MOVE CC-SCHEMA TO PV-SCHEMA.                                 01670000
           MOVE CC-TABLE  TO PV-TABLE.                                  01680000
                                                                        01690000
      *B200-EXIT.                                                       01700000
       EJECT.                                                           01710000
                                                                        01720000
      *--------------------------------------------------------------   01730000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT      01740000
      * LOGS ON TO ORACLE.                                              01750000
      *---------------------------------------------------------------  01760000
      *    COPY PEPD700.                                                01770001
                                                                        01780000
      ******************************************************************01900000
      * CLEAN UP                                                        01910000
      ******************************************************************01920000
       B900-EOJ-ROUTINE.                                                01930000
                                                                        01940000
           CLOSE TABLE-FILE.                                            01950000
                                                                        01960000
      ******************************************************************01970000
      * CONTROL PROCESSING BASED ON DEPARTMENT NUMBERS.                 01980000
      ******************************************************************01990000
       C100-CONTROL.                                                    02000000
                                                                        02010000
           PERFORM D100-REBUILD-VIEW.                                   02020000
           PERFORM B200-READ-TABLE-FILE.                                02030000
                                                                        02040000
      ******************************************************************02050000
      * REBUILD MATERIALIZED VIEW                                       02060000
      ******************************************************************02070000
       D100-REBUILD-VIEW.                                               02080000
                                                                        02090000
           PERFORM 8000-GET-CURRENT-TIME.                               02100000
           DISPLAY 'REBUILDING VIEW........ ' PV-SCHEMA '.' PV-TABLE.   02110000
                                                                        02120000
           EXEC SQL EXECUTE                                             02130000
             BEGIN                                                      02140000
                :PV-MESSAGE :=                                          02150000
                K_UTIL.ORPG_APPLICATION_UTILITY.ORFN_MVIEW_REFRESH(     02160000
                                            :PV-SCHEMA,:PV-TABLE);      02170000
             END;                                                       02180000
           END-EXEC.                                                    02190000
                                                                        02200000
           EVALUATE TRUE                                                02210000
             WHEN SQLCODE = ZERO                                        02220000
                 MOVE PV-MESSAGE     TO PV-MESSAGE-36                   02230000
                 MOVE FUNCTION UPPER-CASE(PV-MESSAGE-36)                02240000
                                TO PV-MESSAGE-36                        02250000
                                                                        02260000
                 IF PV-MESSAGE-36 EQUAL PC-MESSAGE-OK                   02270000
                    DISPLAY PV-MESSAGE                                  02280000
                 ELSE                                                   02290000
                    DISPLAY 'ERROR OCCURRED WHILE REBUILDING VIEW     ' 02300000
                    DISPLAY PV-MESSAGE                                  02310000
                    MOVE PV-TABLE                   TO PV-TABLE-NAME    02320000
                    MOVE 'D100-REBUILD-VIEW'        TO PV-PARAGRAPH-NAME02330000
                    PERFORM 9900-ORACLE-ABEND                           02340000
                 END-IF                                                 02350000
             WHEN OTHER                                                 02360000
                 MOVE SQLCODE                    TO PV-SQL-CODE         02370000
                 MOVE PV-TABLE                   TO PV-TABLE-NAME       02380000
                 MOVE 'D100-REBUILD-VIEW'        TO PV-PARAGRAPH-NAME   02390000
                 PERFORM 9900-ORACLE-ABEND                              02400000
           END-EVALUATE.                                                02410000
                                                                        02411000
      *D100 END                                                         02411100
                                                                        02411200
034503****************************************************************  02411302
034504** READ THE ORACLE LOGON CONTROL CARD.                        **  02411402
034505****************************************************************  02411502
034506 7000-READ-ORACLE-LOGON.                                          02411602
                                                                        02411702
034510     READ ORACLE-LOGON-FILE                                       02411802
034520         INTO CC-ORACLE-CONNECTION                                02411902
034530         AT END                                                   02412002
034540             SET PS-END-OF-LOGON-FILE TO TRUE                     02412102
034550     END-READ.                                                    02412202
034600*                                                                 02412302
                                                                        02412402
      ******************************************************************02412500
       8000-GET-CURRENT-TIME.                                           02412600
      ******************************************************************02412700
                                                                        02412800
            MOVE SPACES TO PV-CURRENT-TIME.                             02412900
                                                                        02413000
           EXEC SQL                                                     02413100
              SELECT                                                    02413200
                 TO_CHAR(SYSDATE,'HH24:MI:SS')                          02413300
              INTO :PV-CURRENT-TIME                                     02413400
              FROM DUAL                                                 02413500
            END-EXEC.                                                   02413600
                                                                        02413700
           EVALUATE TRUE                                                02413800
             WHEN SQLCODE = ZERO                                        02413900
                 DISPLAY 'CURRENT TIME: ' PV-CURRENT-TIME               02414000
             WHEN OTHER                                                 02414100
                 MOVE SQLCODE                    TO PV-SQL-CODE         02414200
                 MOVE 'DUAL'                      TO PV-TABLE-NAME      02414300
                 MOVE '8000-GET-CURRENT-TIME   ' TO PV-PARAGRAPH-NAME   02414400
                 PERFORM 9900-ORACLE-ABEND                              02414500
           END-EVALUATE.                                                02414600
                                                                        02414700
      *-----------------------------------------------------------      02414800
      * Z100-ABEND                                                      02414900
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE              02415000
      *  - CALLED BY PEPD70B!                                           02415100
      *-----------------------------------------------------------      02415200
       Z100-ABEND.                                                      02415300
                                                                        02416000
           DISPLAY '                       '.                           02417000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   02418000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           02419001
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                   02420000
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                02430000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02440000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02450000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                02460000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         02470000
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.             02480000
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.          02490000
           DISPLAY '                       '.                           02500000
                                                                        02510000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02520000
                                                                        02530000
      *----------------------------------------------------------       02540000
      * 9900-ORACLE-ABEND.                                              02550000
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.            02560000
      *----------------------------------------------------------       02570000
       9900-ORACLE-ABEND.                                               02580000
                                                                        02590000
           DISPLAY '                       '.                           02600000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   02610000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           02620001
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.               02630000
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                02640000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02650000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02660000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                02670000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         02680000
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.             02690000
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.          02700000
           DISPLAY '                       '.                           02710000
                                                                        02720000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02730000
                                                                        02740000
      *9900-EXIT.                                                       02750000
