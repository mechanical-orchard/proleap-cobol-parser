       IDENTIFICATION DIVISION.                                         00010021
       PROGRAM-ID.    TFKUP002.                                         00020060
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040021
       DATE-WRITTEN.  07/29/2005.                                       00050045
       DATE-COMPILED.                                                   00060021
      ******************************************************************00070021
      **THIS PROGRAM WILL UPDATE THE ITEM POSTING TABLE (TTFITPS) WITH  00080052
      **     THE BATCH DKEY ID FOR THIS RUN.                            00090052
      ******************************************************************00100021
      **DATE:08/04/2005 MADE BY:JULIE TEWES INITIAL PROGRAM WRITE       00110052
      ******************************************************************00120021
       ENVIRONMENT DIVISION.                                            00130021
       CONFIGURATION SECTION.                                           00140021
       SOURCE-COMPUTER.    IBM-3090.                                    00150021
       OBJECT-COMPUTER.    IBM-3090.                                    00160021
       INPUT-OUTPUT SECTION.                                            00170021
       FILE-CONTROL.                                                    00180021
                                                                        00190021
           SELECT INPUT-FILE                                            00200021
                  ASSIGN TO INP01.                                      00210021
           SELECT CONTROL-CARD                                          00220052
                  ASSIGN TO INP02.                                      00230052
           SELECT OUTPUT-FILE                                           00240048
                  ASSIGN TO OUT01.                                      00250045
                                                                        00260021
       DATA DIVISION.                                                   00270021
       FILE SECTION.                                                    00280021
                                                                        00290021
       FD  INPUT-FILE                                                   00300021
           RECORDING MODE IS F                                          00310021
           LABEL RECORDS OMITTED                                        00320021
           BLOCK CONTAINS 0 RECORDS                                     00330021
           DATA RECORD IS INPUT-RECORD.                                 00340021
       01  INPUT-RECORD                        PIC X(112).              00350069
                                                                        00360052
       FD  CONTROL-CARD                                                 00370052
           RECORDING MODE IS F                                          00380052
           LABEL RECORDS OMITTED                                        00390052
           BLOCK CONTAINS 0 RECORDS                                     00400052
           DATA RECORD IS CONTROL-CARD-RECORD.                          00410052
       01  CONTROL-CARD-RECORD                 PIC X(080).              00420052
                                                                        00430045
       FD  OUTPUT-FILE                                                  00440045
           RECORDING MODE IS F                                          00450045
           LABEL RECORDS OMITTED                                        00460045
           BLOCK CONTAINS 0 RECORDS                                     00470045
           DATA RECORD IS OUTPUT-RECORD.                                00480045
       01  OUTPUT-RECORD                       PIC X(117).              00490069
                                                                        00500021
       WORKING-STORAGE SECTION.                                         00510021
      ******************************************************************00520052
      **PROGRAM SWITCHES                                                00530052
      ******************************************************************00540052
       01  PS-SWITCH-AREAS.                                             00550052
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00560052
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00570052
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00580052
      ******************************************************************00590045
      **DATE TIME FIELDS                                                00600045
      ******************************************************************00610045
       01 DF-DATE-TIME-FIELDS.                                          00620045
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00630045
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00640045
      ******************************************************************00650023
      **PROGRAM COUNTERS                                                00660023
      ******************************************************************00670023
       01  PC-PROGRAM-COUNTERS.                                         00680023
           05  PC-INPUT-RECORDS-READ           PIC 9(08) VALUE ZEROS.   00690056
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(08) VALUE ZEROS.   00700056
      ****************************************************************  00710021
      **ABEND INFORMATION                                               00720021
      ****************************************************************  00730021
       01  AC-ABEND-FIELDS.                                             00740021
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00750021
                                                           COMP SYNC.   00760021
               88  AC-TABLE-ERROR                        VALUE +4004.   00770021
               88  AC-DB2-ERROR                          VALUE +4013.   00780021
       01  PV-MISC-ABEND-FIELDS.                                        00790021
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00800021
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00810021
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00820021
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00830021
      ******************************************************************00840021
      **COPYBOOKS                                                       00850021
      ******************************************************************00860021
      ****INPUT RECORD LAYOUT                                           00870057
           COPY TFRD055.                                                00880052
      ****OUTPUT RECORD LAYOUT                                          00890057
           COPY TFRD056.                                                00900057
      ****CONTROL CARD RECORD LAYOUT                                    00910052
           COPY TFRD052.                                                00920052
      ****ABEND ROUTINE INFORMATION                                     00930021
           COPY SQLCA2.                                                 00940021
      ****ABEND ROUTINE INFORMATION                                     00950021
           COPY DPWS004.                                                00960021
      ******************************************************************00970021
      **TABLE INCLUSIONS                                                00980021
      ******************************************************************00990021
      ***TRANSFERS BATCH CONTROL TABLE                                  01000045
           EXEC SQL                                                     01010021
                INCLUDE TTFITPS                                         01020052
           END-EXEC.                                                    01030021
      *****DB2 TABLE INCLUSION                                          01040021
           EXEC SQL                                                     01050021
                INCLUDE SQLCA                                           01060021
           END-EXEC.                                                    01070021
                                                                        01080029
       PROCEDURE DIVISION.                                              01090021
      ******************************************************************01100021
       000-MAINLINE-PROCESSING.                                         01110021
                                                                        01120021
           PERFORM 1000-INITIALIZATION.                                 01130021
           PERFORM 2000-PROCESS-RECORDS                                 01140052
                   UNTIL PS-END-OF-INPUT-FILE.                          01150052
           PERFORM 9999-WRAPUP.                                         01160021
           GOBACK.                                                      01170021
                                                                        01180021
       EJECT.                                                           01190021
      ******************************************************************01200021
      **INITIALIZATION ROUTINE                                          01210021
      ******************************************************************01220021
       1000-INITIALIZATION.                                             01230021
                                                                        01240021
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01250021
                                                                        01260021
           OPEN INPUT  INPUT-FILE                                       01270024
                       CONTROL-CARD                                     01280053
                OUTPUT OUTPUT-FILE.                                     01290045
                                                                        01300021
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01310021
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01320021
                                                                        01330021
           PERFORM 9999-READ-CONTROL-CARD.                              01340052
           PERFORM 9999-READ-INPUT-RECORD.                              01350052
                                                                        01360021
       EJECT.                                                           01370021
      ******************************************************************01380021
      **PROCESS INPUT RECORD TO CHECK ROWS                              01390021
      ******************************************************************01400021
       2000-PROCESS-RECORDS.                                            01410021
                                                                        01420021
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           01430021
                                                                        01440021
           MOVE TFRD055-TRANSFER-TO-PROCESS                             01450058
                                        TO TFRD056-TRANSFER-TO-PROCESS. 01460058
           MOVE TFRD052-BATCH-DKEY-ID   TO TFITPS-BTCH-DKEY-ID.         01470056
           MOVE TFRD055-XFER-DKEY-ID    TO TFITPS-XFER-DKEY-ID.         01480056
           MOVE TFRD055-XFER-LNE-NBR    TO TFITPS-XFER-LNE-NBR.         01490056
           MOVE TFRD055-PSTG-SEQ-NBR    TO TFITPS-PSTG-SEQ-NBR.         01500056
           MOVE TFRD052-BATCH-DKEY-ID   TO TFRD056-BTCH-DKEY-ID.        01510057
           MOVE TFRD052-JULIAN-DAY      TO TFRD056-JULIAN-DATE.         01520057
                                                                        01530055
           PERFORM 2100-UPDATE-TTFITPS.                                 01540070
           PERFORM 2200-COMMIT-TTFITPS.                                 01550070
                                                                        01560056
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            01570048
           PERFORM 9999-READ-INPUT-RECORD.                              01580056
                                                                        01590048
       EJECT.                                                           01600021
      ******************************************************************01610023
      **UPDATE BTCH_DKEY_ID ON TTFITPS                                  01620052
      ******************************************************************01630023
       2100-UPDATE-TTFITPS.                                             01640052
                                                                        01650023
           MOVE '2100-UPDATE-TTFITPS'  TO PV-PARAGRAPH-NAME.            01660052
                                                                        01670052
           EXEC SQL                                                     01680052
                UPDATE TTFITPS                                          01690052
                SET    BTCH_DKEY_ID = :TFITPS-BTCH-DKEY-ID              01700052
                WHERE  XFER_DKEY_ID = :TFITPS-XFER-DKEY-ID              01710052
                  AND  XFER_LNE_NBR = :TFITPS-XFER-LNE-NBR              01720052
                  AND  PSTG_SEQ_NBR = :TFITPS-PSTG-SEQ-NBR              01730052
           END-EXEC.                                                    01740052
                                                                        01750052
           EVALUATE TRUE                                                01760052
              WHEN SQLCODE = +0                                         01770052
                   CONTINUE                                             01780052
              WHEN OTHER                                                01790052
                   DISPLAY 'UPDATE TTFITPS....'                         01800052
                   DISPLAY 'BATCH DKEY....' TFITPS-XFER-DKEY-ID         01810055
                   DISPLAY 'LINE NBR......' TFITPS-XFER-LNE-NBR         01820055
                   DISPLAY 'PSTG SEQ NBR..' TFITPS-PSTG-SEQ-NBR         01830055
                   PERFORM 9999-DISPLAY-STATISTICS                      01840052
                   PERFORM 9999-SQL-ERROR                               01850052
           END-EVALUATE.                                                01860052
                                                                        01870052
       EJECT.                                                           01880052
      ****************************************************************  01890056
      **COMMIT DATA TO DATABASE                                         01900056
      ****************************************************************  01910056
       2200-COMMIT-TTFITPS.                                             01920056
                                                                        01930056
           MOVE '2200-COMMIT-TTFITPS' TO PV-PARAGRAPH-NAME.             01940056
                                                                        01950056
           EXEC SQL                                                     01960056
                COMMIT                                                  01970056
           END-EXEC.                                                    01980056
                                                                        01990056
           EVALUATE TRUE                                                02000056
              WHEN SQLCODE = +0                                         02010056
                   CONTINUE                                             02020056
              WHEN OTHER                                                02030056
                   DISPLAY 'COMMIT DATA....'                            02040056
                   PERFORM 9999-DISPLAY-STATISTICS                      02050056
                   PERFORM 9999-SQL-ERROR                               02060056
           END-EVALUATE.                                                02070056
                                                                        02080056
       EJECT.                                                           02090056
      ******************************************************************02100052
      **READ INPUT RECORD                                               02110052
      ******************************************************************02120052
       9999-READ-INPUT-RECORD.                                          02130052
                                                                        02140052
           READ INPUT-FILE INTO TFRD055-TRANSFER-TO-PROCESS             02150052
                AT END                                                  02160052
                SET PS-END-OF-INPUT-FILE TO TRUE                        02170052
           END-READ.                                                    02180052
                                                                        02190052
           IF  PS-NOT-END-OF-INPUT-FILE                                 02200052
               ADD +1                      TO PC-INPUT-RECORDS-READ     02210052
           END-IF.                                                      02220052
                                                                        02230021
       EJECT.                                                           02240021
      ******************************************************************02250052
      **READ CONTROL CARD                                               02260052
      ******************************************************************02270052
       9999-READ-CONTROL-CARD.                                          02280052
                                                                        02290052
           READ CONTROL-CARD INTO TFRD052-BATCH-CONTROL-FILE.           02300052
                                                                        02310052
       EJECT.                                                           02320052
      ****************************************************************  02330021
      **WRITE OUTPUT RECORD                                             02340021
      ****************************************************************  02350021
       9999-WRITE-OUTPUT-RECORD.                                        02360021
                                                                        02370021
           WRITE OUTPUT-RECORD FROM TFRD056-TRANSFER-TO-PROCESS.        02380057
                                                                        02390021
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            02400052
                                                                        02410052
       EJECT.                                                           02420021
      ******************************************************************02430021
      **CLOSE INPUT FILES.                                              02440021
      ******************************************************************02450021
       9999-WRAPUP.                                                     02460021
                                                                        02470021
           CLOSE INPUT-FILE                                             02480024
                 CONTROL-CARD                                           02490053
                 OUTPUT-FILE.                                           02500024
                                                                        02510021
           PERFORM 9999-DISPLAY-STATISTICS.                             02520021
                                                                        02530021
       EJECT.                                                           02540021
      ******************************************************************02550021
      **DISPLAY PROGRAM STATS                                           02560021
      ******************************************************************02570021
       9999-DISPLAY-STATISTICS.                                         02580021
                                                                        02590021
           DISPLAY '*************************************************'. 02600021
           DISPLAY '* PROGRAM...........: TFKUP002'.                    02610060
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02620021
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02630021
           DISPLAY '* BATCH DKEY USED...: ' TFRD052-BATCH-DKEY-ID.      02640056
           DISPLAY '* INPUT RECORDS READ: ' PC-INPUT-RECORDS-READ.      02650056
           DISPLAY '* RECORDS WRITTEN...: ' PC-OUTPUT-RECORDS-WRITTEN.  02660052
           DISPLAY '*************************************************'. 02670052
                                                                        02680021
       EJECT.                                                           02690021
      ******************************************************************02700021
      * ABEND ROUTINE FOR BAD SQL CALLS                                *02710021
      ******************************************************************02720021
       9999-SQL-ERROR.                                                  02730021
                                                                        02740021
           DISPLAY '** ABEND     **'.                                   02750021
           DISPLAY '** PROGRAM   ** = TFKUP002'.                        02760060
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02770021
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02780021
      ***************************************************************** 02790021
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  02800021
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   02810021
      ***************************************************************** 02820021
           COPY DPPD004.                                                02830021
           SET AC-DB2-ERROR TO TRUE.                                    02840021
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02850021
                                                                        02860021
       EJECT.                                                           02870021
