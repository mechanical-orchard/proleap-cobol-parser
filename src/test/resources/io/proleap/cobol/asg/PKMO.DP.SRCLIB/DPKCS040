00001 *-----------------------*                                         03/19/93
00002  IDENTIFICATION DIVISION.                                         DPKCS040
00003 *-----------------------*                                            LV067
00004  PROGRAM-ID.    DPKCS040.                                            CL**1
00005  AUTHOR.        PAT DOYLE - OMNI RESOURCES.                          CL**1
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00007  DATE-WRITTEN.  02-26-93.                                            CL**1
00008  DATE-COMPILED.                                                      CL**1
00009                                                                      CL**1
00010 *---------------------------------------------------------------*    CL**1
00011 *    Z040 - MESSAGE INQUIRY PROGRAM.                            *    CL**1
00012 *                                                               *    CL**1
00013 *    THIS PROGRAM DISPLAYS THE MESSAGE TEXT, LONG DESCRIPTION,  *    CL**1
00014 *    AND CORRECTIVE ACTION FOR A MESSAGE.  THE LONG DESCRIPTION *    CL**1
00015 *    AND CORRECTIVE ACTION AREA IS SCROLLABLE, IN CASE IT       *    CL**1
00016 *    DOESN'T FIT ON ONE SCREEN.                                 *    CL**1
00017 *                                                               *    CL**1
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023      *         
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE  *         
262023*  MAKING THE CALL DYNAMIC VS STATIC                            *         
00018 *---------------------------------------------------------------*    CL**1
00019                                                                      CL**1
00020  ENVIRONMENT DIVISION.                                               CL**1
00021                                                                      CL**1
00022  DATA DIVISION.                                                      CL**1
00023                                                                      CL**1
00024  WORKING-STORAGE SECTION.                                            CL**1
00025                                                                      CL**1
00026  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00027      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE            CL**1
00028                                                     'DP040A  '.      CL**2
00029      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE            CL**1
00030                                                     'DPKM040 '.      CL**2
00031      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE            CL**1
00032                                                     'DPKCS040'.      CL**2
00033      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.         CL**1
00034      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.         CL**1
00035      05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.         CL**2
00036      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.       CL**1
00037      05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.       CL*41
00038      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13        CL**2
00039                                                     COMP SYNC.       CL**1
00040      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2         CL**1
00041                                                     COMP SYNC.       CL**1
00042      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE            CL**1
00043                                                     +1000000         CL**1
00044                                                     COMP-3.          CL**1
00045      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5         CL**1
00046                                                     COMP SYNC.       CL**1
00047      05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3         CL**1
00048                                                      COMP-3.         CL**1
00049                                                                      CL**8
00050  01  PC-TSYMSG-NUMBERS.                                              CL**1
00051      05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.     CL**1
00052      05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.     CL**1
00053      05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.     CL**1
00054      05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.     CL**1
00055      05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.     CL**1
00056      05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.     CL*41
00057      05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.     CL*66
00058      05  PC-TSYMSG-00302                PIC 9(5)    VALUE 00302.     CL*41
00059                                                                      CL**8
00060  01  PS-PROGRAM-SUBSCRIPTS.                                          CL**1
00061      05  PS-SUB                         PIC S9(04)   VALUE +0        CL**1
00062                                                      COMP SYNC.      CL**1
00063                                                                      CL**1
00064  01  PS-PROGRAM-SWITCHES.                                            CL**1
00065      05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.      CL**1
00066          88  PS-NO-ERROR                             VALUE 'Y'.      CL**1
00067          88  PS-ERROR                                VALUE 'N'.      CL**1
00068      05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.      CL**1
00069          88  PS-COMMAREA-VALID                       VALUE 'Y'.      CL**1
00070          88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.      CL**1
00071      05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.      CL*55
00072          88  PS-LIST-END                             VALUE 'Y'.      CL*55
00073          88  PS-NO-LIST-END                          VALUE 'N'.      CL*55
00074      EJECT                                                           CL**1
00075  01  PV-PROGRAM-VARIABLES.                                           CL**1
00076      05  PV-REMAINDER                   PIC S9(04)   VALUE +0        CL**1
00077                                                      COMP SYNC.      CL**1
00078      05  PV-READ-COUNT                  PIC S9(09)   VALUE +0        CL**1
00079                                                      COMP-3.         CL**1
00080      05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0        CL**1
00081                                                      COMP-3.         CL**1
00082      05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0        CL*58
00083                                                      COMP-3.         CL*58
00084      05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0        CL**1
00085                                                      COMP SYNC.      CL**1
00086      05  PV-CICS-RESP                   PIC S9(04)   VALUE +0        CL**1
00087                                                      COMP SYNC.      CL**1
00088      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0        CL**1
00089                                                      COMP SYNC.      CL**1
00090      05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0        CL**1
00091                                                      COMP SYNC.      CL**1
00092      05  PV-SQL-SUB                     PIC S9(4)    COMP.           CL**1
00093                                                                      CL*42
00094  01  PV-DB2-CRITERIA-AREA.                                           CL**1
00095      15  PV-DB2-MESSAGE-NUMBER          PIC S9(05)  VALUE ZERO       CL**1
00096                                                     COMP-3.          CL**1
00097                                                                      CL**1
00098                                                                      CL**1
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
00104                                                                      CL**1
00105 *---------------------------------------------------------------*    CL**1
00106 *    STANDARD COMMAREA.                                         *    CL**1
00107 *---------------------------------------------------------------*    CL**1
00108                                                                      CL**1
00109      COPY DPWS020.                                                   CL**1
00110      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
00111                                                                      CL**1
00112 *---------------------------------------------------------------*    CL**1
00113 *    INTER-APPLICATION COMMAREA.                                *    CL**1
00114 *---------------------------------------------------------------*    CL**1
00115                                                                      CL**1
00116          10  INTER-APPL-COMM-AREA       PIC X(200).                  CL**1
00117      COPY DPWS054.                                                   CL*67
00118      COPY DPWS056.                                                   CL*67
00119 *---------------------------------------------------------------*    CL**1
00120 *    SPECIFIC COMMMAREA FOR DPKCS040.                           *    CL**2
00121 *---------------------------------------------------------------*    CL**1
00122          10  ASC-SPECIFIC-COMMAREA.                                  CL**1
00123              15  ASC-LIST-KEYS.                                      CL**1
00124                  20  ASC-KEY-MESSAGE-NUMBER          PIC S9(05)      CL**1
00125                                                      COMP-3.         CL**1
00126                  20  ASC-KEY-SYSTEM-ID               PIC  X(02).     CL**1
00127                  20  ASC-KEY-MESSAGE-TEXT            PIC  X(70).     CL*11
00128              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                   CL**1
00129                  20  ASC-ITEM-ARRAY.                                 CL**1
00130                                                                      CL*11
00131                      25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.           CL**2
00132                          30  ASC-LIST-ITEM-NUMBER    PIC S9(09)      CL*11
00133                                                      COMP-3.         CL*11
00134                          30  ASC-TEXT                PIC X(70).      CL*18
00135                                                                      CL**1
00136              15  ASC-BLK-SUB                         PIC S9(04)      CL**1
00137                                                          COMP        CL**1
00138                                                          SYNC.       CL**1
00139              15  ASC-ITEM-SUB                        PIC S9(04)      CL**1
00140                                                          COMP        CL**1
00141                                                          SYNC.       CL**1
00142              15  ASC-SEL-SUB                         PIC S9(04)      CL*41
00143                                                          COMP.       CL*41
00144              15  ASC-SEL-ITEM                        PIC S9(04)      CL*41
00145                                                          COMP.       CL*41
00146              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)      CL**1
00147                                                          COMP-3.     CL**1
00148              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)      CL**1
00149                                                          COMP-3.     CL**1
00150              15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)      CL*41
00151                                                          COMP-3.     CL*41
00152              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)      CL**1
00153                                                          COMP-3.     CL**1
00154              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)      CL**1
00155                                                          COMP-3.     CL**1
00156              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)      CL**1
00157                                                          COMP-3.     CL**1
00158              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)      CL**1
00159                                                          COMP        CL**1
00160                                                          SYNC.       CL**1
00161              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)      CL**1
00162                                                          COMP-3.     CL**1
00163              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)      CL**1
00164                                                          COMP-3.     CL**1
00165              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).     CL**1
00166                  88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.    CL**1
00167                  88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.    CL**1
00168              15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).     CL*41
00169                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.    CL*41
00170              15  ASC-DESC-LINE-IND                   PIC  X(01).     CL*45
00171                  88  ASC-DESC-LINE-DONE                VALUE 'Y'.    CL*45
00172              15  ASC-CORR-LINE-IND                   PIC  X(01).     CL*45
00173                  88  ASC-CORR-LINE-DONE                VALUE 'Y'.    CL*45
00174              15  ASC-LIST-QUEUE-NAME.                                CL**1
00175                  20  ASC-LIST-TERM-ID                PIC  X(04).     CL**1
00176                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)      CL**1
00177                                                          COMP-3.     CL**1
00178                  20  ASC-LIST-SEQ                    PIC  9(01).     CL**1
00179              15  ASC-SEL-QUEUE-NAME.                                 CL**1
00180                  20  ASC-SEL-TERM-ID                 PIC  X(04).     CL**1
00181                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)      CL**1
00182                                                           COMP-3.    CL**1
00183                  20  ASC-SEL-SEQ                     PIC  9(01).     CL**1
00184              15  ASC-DTL-QUEUE-NAME.                                 CL**2
00185                  20  ASC-DTL-TERM-ID                 PIC  X(04).     CL**2
00186                  20  ASC-DTL-TASK-NUMBER             PIC S9(05)      CL**2
00187                                                          COMP-3.     CL**2
00188                  20  ASC-DTL-SEQ                     PIC  9(01).     CL**2
00189                                                                      CL**1
00190              15  ASC-START-ITEM-REQUEST.                             CL**1
00191                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).     CL**1
00192                                                                      CL**1
00193      EJECT                                                           CL**1
00194 *---------------------------------------------------------------*    CL**1
00195 *    MESSAGE SELECTION TSQ B QUEUE RECORD LAYOUT.               *    CL**1
00196 *---------------------------------------------------------------*    CL**1
00197                                                                      CL**1
00198      COPY DPWS057.                                                   CL*67
00199                                                                      CL**1
00200      EJECT                                                           CL**1
00201 *---------------------------------------------------------------*    CL**1
00202 *    ABEND PROCESSING COPYBOOK.                                 *    CL**1
00203 *---------------------------------------------------------------*    CL**1
00204                                                                      CL**1
00205      COPY DPWS013.                                                   CL**1
00206                                                                      CL**1
00207      EJECT                                                           CL**1
00208 *---------------------------------------------------------------*    CL**1
00209 *    ATTRIBUTE SETTINGS COPYBOOK.                               *    CL**1
00210 *---------------------------------------------------------------*    CL**1
00211                                                                      CL**1
00212      COPY DPWS015.                                                   CL**1
00213                                                                      CL**1
00214 *---------------------------------------------------------------*    CL**1
00215 *    FUNCTION KEYS COPYBOOK.                                    *    CL**1
00216 *---------------------------------------------------------------*    CL**1
00217                                                                      CL**1
00218      COPY DPWS016.                                                   CL**1
00219                                                                      CL**1
00220 *---------------------------------------------------------------*    CL**1
00221 *    NUMERIC EDIT LAYOUT.                                       *    CL**1
00222 *---------------------------------------------------------------*    CL**1
00223                                                                      CL**1
00224      COPY DPWS010I.                                                  CL**1
00225                                                                      CL**1
00226 *---------------------------------------------------------------*    CL**1
00227 *    MAP LAYOUT                                                 *    CL**1
00228 *---------------------------------------------------------------*    CL**1
00229                                                                      CL**1
00230      COPY DPKM040.                                                   CL**1
00231                                                                      CL**8
00232  01  MR-OCCURS-LAYOUT          REDEFINES  DP040AO.                   CL**1
00233      05  FILLER                PIC X(134).                           CL**1
00234      05  MR-SCROLL-AREA OCCURS 13 TIMES.                             CL**1
00235          10  MR-TEXTL          PIC S9(04) COMP.                      CL**1
00236          10  MR-TEXTA          PIC  X(01).                           CL**1
00237          10  MR-TEXTC          PIC  X(01).                           CL**1
00238          10  MR-TEXTH          PIC  X(01).                           CL**1
00239          10  MR-TEXT           PIC  X(70).                           CL**1
00240                                                                      CL**1
00241  EJECT                                                               CL**1
00242 *---------------------------------------------------------------*    CL**1
00243 *    DB2 AREA FOR TMESSAGE                                      *    CL**1
00244 *---------------------------------------------------------------*    CL**1
00245      EXEC SQL                                                        CL**1
00246           INCLUDE TSYMSG                                             CL**1
00247      END-EXEC.                                                       CL**1
00248 *---------------------------------------------------------------*    CL**1
00249 *    DB2 AREA FOR MESSAGE TEXT AND CORRECTIVE ACTION            *    CL**1
00250 *---------------------------------------------------------------*    CL**1
00251      EXEC SQL                                                        CL**1
00252           INCLUDE TSYMSGT                                            CL**1
00253      END-EXEC.                                                       CL**1
00254 *---------------------------------------------------------------*    CL**1
00255 *    DB2 AREA FOR COMMUNICATIONS                                *    CL**1
00256 *---------------------------------------------------------------*    CL**1
00257      EXEC SQL                                                        CL**1
00258           INCLUDE SQLCA                                              CL**1
00259      END-EXEC.                                                       CL**1
00260                                                                      CL**1
00261 *---------------------------------------------------------------*    CL**1
00262 *    CURSOR TO OBTAIN LONG DESCRIPTION AND CORRECTIVE ACTION    *    CL**1
00263 *---------------------------------------------------------------*    CL**1
00264                                                                      CL**1
00265      EXEC SQL                                                        CL**1
00266           DECLARE TEXT-CSR CURSOR                                    CL**1
00267           FOR SELECT                                                 CL**1
00268                 TYP_CDE                                              CL**1
00269               , LNE_SEQ_NBR                                          CL**1
00270               , LNE_TXT                                              CL**1
00271           FROM                                                       CL**1
00272                 TSYMSGT                                              CL**1
00273           WHERE                                                      CL**1
00274                 SYMSG_NBR      = :PV-DB2-MESSAGE-NUMBER              CL**1
00275           ORDER BY                                                   CL**1
00276                 TYP_CDE                                              CL**1
00277               , LNE_SEQ_NBR                                          CL**1
00278      END-EXEC.                                                       CL**1
00279                                                                      CL**1
00280  EJECT                                                               CL**1
00281  LINKAGE SECTION.                                                    CL**1
00282                                                                      CL**1
00283  01  DFHCOMMAREA.                                                    CL**1
00284      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL**1
00285                                         DEPENDING ON EIBCALEN.       CL**1
00286          10  FILLER                     PIC X.                       CL**1
00287                                                                      CL**1
00288  EJECT                                                               CL**1
00289  PROCEDURE DIVISION.                                                 CL**1
00290                                                                      CL**1
00291  0000-MAIN-MODULE.                                                   CL**1
00292                                                                      CL**8
00293      INITIALIZE DP030-CICS-API-FIELDS.                               CL**1
00294      MOVE +1                      TO DP030-NUMBER-OF-MAPS.           CL**1
00295      MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.              CL**1
00296      MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).             CL**1
00297      SET DP030-RECEIVE-APPL-MAP   TO TRUE.                           CL**1
00298      MOVE LENGTH OF DP040AI       TO DP030-MAP-LENGTH (1).           CL**1
00299      MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).         CL**1
00300      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00301                                                                      CL**8
00302      PERFORM 1000-CONTROL-PROCESSING.                                CL**1
00303                                                                      CL**8
00304      MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).         CL**1
00305      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00306                                                                      CL**1
00307 *----------------------------------------------------------------*   CL*63
00308 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *   CL*63
00309 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *   CL*63
00310 *----------------------------------------------------------------*   CL*63
00311                                                                      CL*63
00312  0001-CALL-CICS-ARCH-API.                                            CL**1
00313                                                                      CL**8
00314      CALL DP930-CICS-ARCH-API USING DFHEIBLK                         CL**1
00315                                     DFHCOMMAREA                      CL**1
00316                                     DP030-CICS-API-FIELDS            CL**1
00317                                     DP020-STANDARD-COMMAREA          CL**1
00318                                     DP040AI.                         CL**1
00319                                                                      CL**8
00320      IF  DP030-RC-CALL-SUCCESSFUL                                    CL**1
00321          CONTINUE                                                    CL**1
00322      ELSE                                                            CL**1
00323          SET DP013-NO-ROLLBACK TO TRUE                               CL**1
00324          SET DP013-XCTL-DISPLAY-RESTART TO TRUE                      CL**1
00325          SET DP013-CICS-ABEND TO TRUE                                CL**1
00326          MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH           CL**1
00327          MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
00328 -             'N NEXT LINE'    TO DP013-MESSAGE-TEXT (2)                  
00329          MOVE DP030-RETURN-CODE                                      CL**1
00330                                TO DP013-MESSAGE-TEXT (3)             CL**1
00331          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00332      END-IF.                                                         CL**1
00333                                                                      CL**8
00334 *----------------------------------------------------------------*   CL**1
00335 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *   CL**1
00336 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *   CL**1
00337 *                                                                *   CL**1
00338 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *   CL**1
00339 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *   CL**1
00340 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *   CL**1
00341 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *   CL**1
00342 *----------------------------------------------------------------*   CL**1
00343                                                                      CL**1
00344  1000-CONTROL-PROCESSING.                                            CL**1
00345                                                                      CL**1
00346      EVALUATE TRUE                                                   CL**1
00347          WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
00348              MOVE EIBTRMID          TO ASC-DTL-TERM-ID               CL**2
00349              MOVE DP020-SRC-TASK-NUMBER                              CL**1
00350                                     TO ASC-DTL-TASK-NUMBER           CL**2
00351              MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                   CL**2
00352              PERFORM 1100-PROCESS-INTER-APPL-COMM                    CL**1
00353 ***-----$TEMPLATE NOTE$-------------------------------------------   CL**1
00354 *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,   CL**1
00355 *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO     CL**1
00356 *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.   CL**1
00357 ***---------------------------------------------------------------   CL**1
00358              IF  PS-COMMAREA-NOT-VALID                               CL**1
00359 ***              MOVE PC-TSYMSG-99901                                CL**1
00360 ***                            TO  DP020-MSG-NUMBER                  CL**1
00361 ***              SET DP020-MSG-FATAL                                 CL**1
00362 ***                            TO  TRUE                              CL**1
00363                  SET DP020-NEXT-ACT-APPL-ERROR                       CL**1
00364                                TO  TRUE                              CL**1
00365              ELSE                                                    CL**1
00366                  PERFORM 2050-DELETE-DETAIL-QUEUE                    CL*56
00367                  PERFORM 4000-BUILD-INITIAL-PANEL                    CL**1
00368              END-IF                                                  CL**1
00369                                                                      CL**1
00370          WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
00371              MOVE ASC-ITEM-AT-TOP-OF-PANEL                           CL**1
00372                                TO PV-TOP-ITEM                        CL**1
00373              PERFORM 2000-PROCESS-PANEL                              CL**1
00374                                                                      CL**1
00375          WHEN DP020-NEXT-ACT-RETURN                                  CL**1
00376              MOVE ASC-ITEM-AT-TOP-OF-PANEL                           CL**1
00377                                TO PV-TOP-ITEM                        CL**1
00378              MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL           CL**1
00379              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00380                                                                      CL**1
00381          WHEN OTHER                                                  CL**1
00382              SET DP013-LOGIC-ABEND TO TRUE                           CL**1
00383              SET DP013-NO-ROLLBACK TO TRUE                           CL**1
00384              MOVE '1000-CONTROL-PROCESSING'                          CL**1
00385                                TO  DP013-PARAGRAPH                   CL**1
00386              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'       CL**1
00387                                TO  DP013-MESSAGE-TEXT(1)             CL**1
00388              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00389      END-EVALUATE.                                                   CL**1
00390                                                                      CL**1
00391  EJECT                                                               CL**1
00392 *----------------------------------------------------------------*   CL**1
00393 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *   CL**1
00394 * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *   CL**1
00395 *----------------------------------------------------------------*   CL**1
00396                                                                      CL**1
00397  1100-PROCESS-INTER-APPL-COMM.                                       CL**1
00398                                                                      CL**1
00399      SET PS-COMMAREA-VALID     TO TRUE.                              CL**1
00400      INITIALIZE ASC-SPECIFIC-COMMAREA.                               CL**1
00401      MOVE SPACE                TO ASC-START-ITEM-REQUEST.            CL**1
00402      EVALUATE DP020-INT-APPL-FORMAT-IND                              CL**4
00403          WHEN PC-CRIT-SPEC-FORMAT                                    CL**4
00404              IF DP054-MESSAGE-NUMBER > ZERO                          CL**4
00405                  MOVE DP054-MESSAGE-NUMBER                           CL**4
00406                                TO ASC-KEY-MESSAGE-NUMBER             CL**1
00407                                   PV-DB2-MESSAGE-NUMBER              CL**1
00408              ELSE                                                    CL**4
00409                  MOVE PC-TSYMSG-00148                                CL**4
00410                                TO DP020-MSG-NUMBER                   CL**4
00411                  SET DP020-MSG-FATAL TO TRUE                         CL**4
00412                  SET DP020-NEXT-ACT-APPL-ERROR TO TRUE               CL**4
00413              END-IF                                                  CL**4
00414          WHEN PC-LIST-FORMAT                                         CL**4
00415              SET ASC-USE-SELECTION-QUEUE TO TRUE                     CL**4
00416              MOVE DP056-NUMBER-OF-TS-ITEMS                           CL**4
00417                                TO ASC-NUMBER-OF-TS-ITEMS             CL**4
00418              MOVE DP056-SEL-QUEUE-NAME                               CL**4
00419                                TO ASC-SEL-QUEUE-NAME                 CL**4
00420              PERFORM 1120-PREPARE-LIST-ENTRY                         CL**4
00421          WHEN OTHER                                                  CL**4
00422              IF  DP020-NAV-KEY-DATA > SPACE                          CL**4
00423                  PERFORM 1110-EDIT-NAV-KEY                           CL**4
00424              ELSE                                                    CL**4
00425                  MOVE PC-TSYMSG-00148                                CL**4
00426                                TO DP020-MSG-NUMBER                   CL**4
00427                  SET DP020-MSG-FATAL TO TRUE                         CL**4
00428                  SET DP020-NEXT-ACT-APPL-ERROR TO TRUE               CL**4
00429              END-IF                                                  CL**4
00430      END-EVALUATE.                                                   CL**4
00431                                                                      CL**1
00432 *----------------------------------------------------------------*   CL**8
00433 *    EDIT THE KEY ENTERED BY THE USER INTO THE KEY FIELD OF THE  *   CL*61
00434 *    COMMAND LINE.  A VALID MESSAGE NUMBER IS THE ONLY KEY       *   CL*61
00435 *    ACCEPTED.                                                   *   CL*61
00436 *----------------------------------------------------------------*   CL**8
00437                                                                      CL**8
00438  1110-EDIT-NAV-KEY.                                                  CL**4
00439                                                                      CL**8
00440      MOVE DP020-NAV-KEY-DATA TO DP010I-UNEDITED-FIELD.               CL**4
00441      MOVE PC-MSG-NUMBER-LENGTH     TO DP010I-MAXIMUM-DIGITS.         CL*38
00442      MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS.            CL**4
00443      SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE.                        CL**4
262023     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
262023          USING DP010I-NUMERIC-EDIT-AREA.                                 
00445      IF  DP010I-ERROR-DETECTED                                       CL**4
00446          SET PS-ERROR TO TRUE                                        CL**4
00447          SET DP020-MSG-FATAL TO TRUE                                 CL**4
00448          MOVE PC-TSYMSG-00302 TO DP020-MSG-NUMBER                    CL**4
00449          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                       CL**4
00450      ELSE                                                            CL**4
00451          MOVE DP010I-NUMERIC-FIELD   TO ASC-KEY-MESSAGE-NUMBER       CL*38
00452                                         PV-DB2-MESSAGE-NUMBER        CL*38
00453      END-IF.                                                         CL**4
00454                                                                      CL**8
00455 *----------------------------------------------------------------*   CL*61
00456 *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *   CL*61
00457 *----------------------------------------------------------------*   CL*61
00458                                                                      CL*61
00459  1120-PREPARE-LIST-ENTRY.                                            CL**5
00460                                                                      CL**8
00461      MOVE DP057-MAX-ITEMS TO ASC-SEL-SUB.                            CL**5
00462      MOVE ZERO            TO ASC-SEL-ITEM.                           CL**5
00463      PERFORM 2140-GET-NEXT-MESSAGE.                                  CL*38
00464  EJECT                                                               CL**1
00465 *----------------------------------------------------------------*   CL**1
00466 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *   CL**1
00467 *    ON WHAT FUNCTION KEY WAS SELECTED.                          *   CL**1
00468 *----------------------------------------------------------------*   CL**1
00469                                                                      CL**1
00470  2000-PROCESS-PANEL.                                                 CL**1
00471                                                                      CL**8
00472      EVALUATE TRUE                                                   CL**1
00473          WHEN DP020-SRC-AID = DP016-CLEAR                            CL**1
00474              MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                   CL**1
00475              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00476              PERFORM 3200-RESET-ATTRIBUTES                           CL*44
00477                                                                      CL**1
00478          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                       CL**1
00479              MOVE ASC-KEY-MESSAGE-NUMBER                             CL**1
00480                                TO  PV-DB2-MESSAGE-NUMBER             CL**1
00481              PERFORM 2050-DELETE-DETAIL-QUEUE                        CL*44
00482              PERFORM 4000-BUILD-INITIAL-PANEL                        CL**1
00483              PERFORM 3200-RESET-ATTRIBUTES                           CL*44
00484                                                                      CL**1
00485          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                CL**5
00486              IF  ASC-USE-SELECTION-QUEUE                             CL**5
00487                  PERFORM 2140-GET-NEXT-MESSAGE                       CL**5
00488                  IF PS-LIST-END                                      CL*55
00489                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            CL*55
00490                      SET PS-NO-LIST-END TO TRUE                      CL*55
00491                  ELSE                                                CL*55
00492                      PERFORM 2050-DELETE-DETAIL-QUEUE                CL*55
00493                      PERFORM 4000-BUILD-INITIAL-PANEL                CL*55
00494                  END-IF                                              CL*55
00495                  PERFORM 3200-RESET-ATTRIBUTES                       CL*55
00496              ELSE                                                    CL**5
00497                  MOVE PC-TSYMSG-00070                                CL**5
00498                                   TO DP020-MSG-NUMBER                CL**5
00499                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL**5
00500              END-IF                                                  CL**5
00501                                                                      CL**5
00502          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                CL**5
00503              IF  ASC-USE-SELECTION-QUEUE                             CL**5
00504                  PERFORM 2150-GET-PREV-MESSAGE                       CL**5
00505                  IF PS-LIST-END                                      CL*55
00506                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            CL*55
00507                      SET PS-NO-LIST-END TO TRUE                      CL*55
00508                  ELSE                                                CL*55
00509                      PERFORM 2050-DELETE-DETAIL-QUEUE                CL*55
00510                      PERFORM 4000-BUILD-INITIAL-PANEL                CL*55
00511                  END-IF                                              CL*55
00512                  PERFORM 3200-RESET-ATTRIBUTES                       CL**5
00513              ELSE                                                    CL**5
00514                  MOVE PC-TSYMSG-00069                                CL**5
00515                                   TO DP020-MSG-NUMBER                CL**5
00516                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL**5
00517              END-IF                                                  CL**5
00518                                                                      CL**5
00519          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                     CL*11
00520              MOVE +1 TO PV-TOP-ITEM                                  CL*11
00521              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
00522              PERFORM 3200-RESET-ATTRIBUTES                           CL*44
00523                                                                      CL*11
00524          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                      CL*11
00525              PERFORM 2110-BROWSE-BACKWARD                            CL*11
00526              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
00527              PERFORM 3200-RESET-ATTRIBUTES                           CL*44
00528                                                                      CL*11
00529          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                      CL*11
00530              PERFORM 2120-BROWSE-FORWARD                             CL*11
00531              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
00532              PERFORM 3200-RESET-ATTRIBUTES                           CL*44
00533                                                                      CL*11
00534          WHEN DP020-SRC-AID = DP016-ENTER                            CL*11
00535              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                      CL*11
00536              IF  PS-ERROR                                            CL*11
00537                  CONTINUE                                            CL*11
00538              ELSE                                                    CL*11
00539                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                CL*11
00540                  PERFORM 3200-RESET-ATTRIBUTES                       CL*44
00541              END-IF                                                  CL*11
00542                                                                      CL*11
00543          WHEN OTHER                                                  CL*11
00544              SET DP013-NO-ROLLBACK TO TRUE                           CL*11
00545              SET DP013-XCTL-DISPLAY-RESTART TO TRUE                  CL*11
00546              SET DP013-CICS-ABEND TO TRUE                            CL*11
00547              MOVE '2100-CHECK-FUNCTION-KEY' TO DP013-PARAGRAPH       CL*11
00548              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL*11
00549                                    TO DP013-MESSAGE-TEXT (1)         CL*11
00550              PERFORM DP013-0000-PROCESS-ABEND                        CL*11
00551                                                                      CL*11
00552      END-EVALUATE.                                                   CL*11
00553  EJECT                                                               CL**1
00554 *----------------------------------------------------------------*   CL**1
00555 *    DELETE THE TEMPORARY STORAGE QUEUE.                         *   CL**1
00556 *----------------------------------------------------------------*   CL**1
00557                                                                      CL**1
00558  2050-DELETE-DETAIL-QUEUE.                                           CL**6
00559                                                                      CL**1
00560      EXEC CICS                                                       CL**1
00561          DELETEQ TS                                                  CL**1
00562              QUEUE (ASC-DTL-QUEUE-NAME)                              CL**6
00563              RESP  (PV-CICS-RESP)                                    CL**1
00564      END-EXEC.                                                       CL**1
00565                                                                      CL**1
00566      IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                          CL**1
00567         (PV-CICS-RESP = DFHRESP(QIDERR))                             CL**1
00568          CONTINUE                                                    CL**1
00569      ELSE                                                            CL**1
00570          SET DP013-CICS-ABEND TO TRUE                                CL**1
00571          SET DP013-NO-ROLLBACK TO TRUE                               CL**1
00572                                                                      CL**1
00573          MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH          CL**6
00574          MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'     CL**6
00575                         TO DP013-MESSAGE-TEXT (1)                    CL**1
00576          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00577      END-IF.                                                         CL**1
00578                                                                      CL**1
00579      MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                           CL**1
00580  EJECT                                                               CL**1
00581 *----------------------------------------------------------------*   CL**1
00582 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *   CL**1
00583 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *   CL**1
00584 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *   CL**1
00585 *    MESSAGE.                                                    *   CL**1
00586 *----------------------------------------------------------------*   CL**1
00587                                                                      CL**1
00588  2110-BROWSE-BACKWARD.                                               CL**1
00589                                                                      CL**8
00590      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL       CL**1
00591          GIVING PV-TOP-ITEM.                                         CL**1
00592                                                                      CL**8
00593      IF  PV-TOP-ITEM < +1                                            CL**1
00594          MOVE +1              TO PV-TOP-ITEM                         CL**1
00595 *        -- TOP OF LIST --                                           CL**1
00596          MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                    CL**1
00597          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL**1
00598      END-IF.                                                         CL**1
00599                                                                      CL**1
00600  EJECT                                                               CL**1
00601                                                                      CL**8
00602 *----------------------------------------------------------------*   CL*58
00603 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *   CL*58
00604 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *   CL*58
00605 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *   CL*58
00606 *----------------------------------------------------------------*   CL*58
00607                                                                      CL*58
00608  2120-BROWSE-FORWARD.                                                CL*58
00609                                                                      CL*58
00610      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                 CL*58
00611          GIVING PV-TOP-ITEM.                                         CL*58
00612      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                      CL*58
00613          MOVE ASC-ITEM-AT-TOP-OF-PANEL                               CL*58
00614                                    TO PV-TOP-ITEM                    CL*58
00615 *        -- BOTTOM OF LIST --                                        CL*58
00616          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER               CL*58
00617          SET DP020-MSG-INFORMATIONAL                                 CL*58
00618                                    TO TRUE                           CL*58
00619      ELSE                                                            CL*58
00620          COMPUTE PV-BOTTOM-ITEM =                                    CL*58
00621              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                    CL*58
00622          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ               CL*58
00623 *            -- BOTTOM OF LIST --                                    CL*58
00624              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER               CL*58
00625              SET DP020-MSG-INFORMATIONAL                             CL*58
00626                                    TO TRUE                           CL*58
00627          END-IF                                                      CL*58
00628      END-IF.                                                         CL*58
00629  EJECT                                                               CL*58
00630 *----------------------------------------------------------------*   CL**1
00631 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *   CL**1
00632 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *   CL**1
00633 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *   CL**1
00634 *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *   CL**1
00635 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *   CL**1
00636 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *   CL**1
00637 *----------------------------------------------------------------*   CL**1
00638                                                                      CL**1
00639  2130-CHECK-FOR-JUMP-BROWSE.                                         CL*58
00640                                                                      CL*58
00641      IF ASTRTITL > ZERO                                              CL*58
00642          MOVE ASTRTITI             TO ASC-START-ITEM-REQUEST         CL*58
00643      ELSE                                                            CL*58
00644          IF ASTRTITF = DP015-ERASE-EOF                               CL*58
00645              INITIALIZE ASC-START-ITEM-REQUEST                       CL*58
00646          END-IF                                                      CL*58
00647      END-IF.                                                         CL*58
00648                                                                      CL*58
00649      SET PS-NO-ERROR               TO TRUE.                          CL*58
00650      IF (ASC-START-ITEM-REQUEST = SPACE)                             CL*58
00651          MOVE ASC-ITEM-AT-TOP-OF-PANEL                               CL*58
00652                                    TO PV-TOP-ITEM                    CL*58
00653      ELSE                                                            CL*58
00654          MOVE ASC-START-ITEM-REQUEST                                 CL*58
00655                                    TO DP010I-UNEDITED-FIELD          CL*58
00656          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS          CL*58
00657          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS        CL*58
00658          SET DP010I-NEGATIVE-NOT-ALLOWED                             CL*58
00659                                    TO TRUE                           CL*58
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
00661                                                                      CL*58
00662          IF  NOT DP010I-ERROR-DETECTED                               CL*58
00663              MOVE DP010I-NUMERIC-FIELD                               CL*58
00664                                    TO  ASC-START-ITEM-REQUEST-N      CL*58
00665              IF  ASC-START-ITEM-REQUEST-N > ZERO                     CL*58
00666                  IF  ASC-START-ITEM-REQUEST-N >                      CL*58
00667                                           ASC-NUMBER-OF-ITEMS-READ   CL*58
00668                      MOVE SPACE TO ASC-START-ITEM-REQUEST            CL*58
00669                      COMPUTE PV-TOP-ITEM =                           CL*58
00670                              ASC-NUMBER-OF-ITEMS-READ                CL*58
00671                              - PC-ITEMS-PER-PANEL + 1                CL*58
00672                      IF  PV-TOP-ITEM < +1                            CL*58
00673                          MOVE +1   TO PV-TOP-ITEM                    CL*58
00674                          MOVE PC-TSYMSG-00070                        CL*58
00675                                    TO DP020-MSG-NUMBER               CL*58
00676                          SET DP020-MSG-INFORMATIONAL                 CL*58
00677                                    TO TRUE                           CL*58
00678                      END-IF                                          CL*58
00679                  ELSE                                                CL*58
00680                      MOVE ASC-START-ITEM-REQUEST-N                   CL*58
00681                                    TO PV-TOP-ITEM                    CL*58
00682                      MOVE SPACE    TO ASC-START-ITEM-REQUEST         CL*58
00683                      COMPUTE PV-BOTTOM-ITEM =                        CL*58
00684                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1        CL*58
00685                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ   CL*58
00686 *                        -- BOTTOM OF LIST --                        CL*58
00687                          MOVE PC-TSYMSG-00070                        CL*58
00688                                    TO DP020-MSG-NUMBER               CL*58
00689                          SET DP020-MSG-INFORMATIONAL                 CL*58
00690                                    TO TRUE                           CL*58
00691                      END-IF                                          CL*58
00692                  END-IF                                              CL*58
00693              ELSE                                                    CL*58
00694 *                --- VALUE OUT OF RANGE ----                         CL*58
00695                  MOVE PC-TSYMSG-00101                                CL*58
00696                                    TO DP020-MSG-NUMBER               CL*58
00697                  MOVE -1           TO ASTRTITL                       CL*58
00698                  SET DP030-SET-CURSOR-APPL-1                         CL*58
00699                                    TO TRUE                           CL*58
00700                  MOVE DP015-REVERSE                                  CL*58
00701                                    TO ASTRTITH                       CL*58
00702                  MOVE DP015-RED    TO ASTRTITC                       CL*58
00703                  MOVE DP015-UNP-NUM-BRT-MDT                          CL*58
00704                                    TO ASTRTITA                       CL*58
00705                  SET DP020-MSG-FATAL                                 CL*58
00706                                    TO TRUE                           CL*58
00707                  SET PS-ERROR      TO TRUE                           CL*58
00708              END-IF                                                  CL*58
00709                                                                      CL*58
00710          ELSE                                                        CL*58
00711              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL*58
00712              MOVE -1               TO ASTRTITL                       CL*58
00713              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*58
00714              MOVE DP015-REVERSE    TO ASTRTITH                       CL*58
00715              MOVE DP015-RED        TO ASTRTITC                       CL*58
00716              MOVE DP015-UNP-NUM-BRT-MDT                              CL*58
00717                                    TO ASTRTITA                       CL*58
00718              SET DP020-MSG-FATAL   TO TRUE                           CL*58
00719              SET PS-ERROR          TO TRUE                           CL*58
00720          END-IF                                                      CL*58
00721      END-IF.                                                         CL*58
00722  EJECT                                                               CL*58
00723                                                                      CL**1
00724 *----------------------------------------------------------------*   CL*61
00725 *    GET THE NEXT MESSAGE IF A LIST OF MESSAGES IS BEING         *   CL*61
00726 *    PROCESSED.                                                  *   CL*61
00727 *----------------------------------------------------------------*   CL*61
00728                                                                      CL*61
00729  2140-GET-NEXT-MESSAGE.                                              CL**5
00730                                                                      CL**8
00731      ADD +1                       TO ASC-SEL-SUB.                    CL**5
00732      IF  ASC-SEL-SUB > DP057-MAX-ITEMS                               CL**5
00733          ADD +1                   TO ASC-SEL-ITEM                    CL**5
00734          MOVE +1                  TO ASC-SEL-SUB                     CL**5
00735      END-IF.                                                         CL**5
00736      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                   CL**5
00737          PERFORM 9100-READ-SEL-QUEUE                                 CL*49
00738          IF  DP057-ITEM (ASC-SEL-SUB) > ZERO                         CL**5
00739              MOVE DP057-MESSAGE-NUMBER (ASC-SEL-SUB)                 CL**5
00740                                   TO ASC-KEY-MESSAGE-NUMBER          CL*38
00741                                      PV-DB2-MESSAGE-NUMBER           CL*38
00742              SET DP057-INQ (ASC-SEL-SUB) TO TRUE                     CL**5
00743              PERFORM 9200-REWRITE-SEL-QUEUE                          CL*49
00744          ELSE                                                        CL**5
00745              SUBTRACT +1 FROM ASC-SEL-SUB                            CL**5
00746              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                CL**5
00747              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL**5
00748              SET PS-LIST-END             TO TRUE                     CL*55
00749          END-IF                                                      CL**5
00750      ELSE                                                            CL**5
00751          SUBTRACT +1 FROM ASC-SEL-ITEM                               CL**5
00752          MOVE DP057-MAX-ITEMS     TO ASC-SEL-SUB                     CL**5
00753          MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                CL**5
00754          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL**5
00755          SET PS-LIST-END             TO TRUE                         CL*55
00756      END-IF.                                                         CL**5
00757                                                                      CL**8
00758 *----------------------------------------------------------------*   CL**8
00759 *    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *   CL*61
00760 *    PROCESSED.                                                  *   CL*61
00761 *----------------------------------------------------------------*   CL**8
00762                                                                      CL**8
00763  2150-GET-PREV-MESSAGE.                                              CL**5
00764                                                                      CL**8
00765      SUBTRACT +1 FROM ASC-SEL-SUB.                                   CL**5
00766      IF  ASC-SEL-SUB < +1                                            CL**5
00767          SUBTRACT +1 FROM ASC-SEL-ITEM                               CL**5
00768          MOVE DP057-MAX-ITEMS     TO ASC-SEL-SUB                     CL**5
00769      END-IF.                                                         CL**5
00770                                                                      CL**8
00771      IF  ASC-SEL-ITEM > ZERO                                         CL**5
00772          PERFORM 9100-READ-SEL-QUEUE                                 CL*49
00773          MOVE DP057-MESSAGE-NUMBER (ASC-SEL-SUB)                     CL**5
00774                                   TO ASC-KEY-MESSAGE-NUMBER          CL*38
00775                                      PV-DB2-MESSAGE-NUMBER           CL*38
00776      ELSE                                                            CL**5
00777          MOVE +1                  TO ASC-SEL-ITEM                    CL**5
00778                                      ASC-SEL-SUB                     CL**5
00779          MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                CL**5
00780          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL**5
00781          SET PS-LIST-END             TO TRUE                         CL*55
00782      END-IF.                                                         CL**5
00783                                                                      CL**8
00784  EJECT                                                               CL**1
00785 *----------------------------------------------------------------*   CL**1
00786 *    RESET ATTRIBUTES.                                           *   CL*61
00787 *----------------------------------------------------------------*   CL**1
00788                                                                      CL**1
00789  3200-RESET-ATTRIBUTES.                                              CL**1
00790                                                                      CL**8
00791      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                      CL**1
00792      MOVE DP015-GREEN              TO ASTRTITC.                      CL**1
00793      MOVE DP015-UNDERLINE          TO ASTRTITH.                      CL**1
00794      MOVE -1                       TO ASTRTITL.                      CL*62
00795      SET DP030-SET-CURSOR-APPL-1   TO TRUE.                          CL*62
00796                                                                      CL**8
00797  EJECT                                                               CL**1
00798                                                                      CL**8
00799 *----------------------------------------------------------------*   CL**1
00800 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *   CL**1
00801 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *   CL**1
00802 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *   CL**1
00803 *    DISPLAY THE PANEL TO USER.                                  *   CL**1
00804 *----------------------------------------------------------------*   CL**1
00805                                                                      CL**1
00806  4000-BUILD-INITIAL-PANEL.                                           CL**1
00807                                                                      CL**1
00808      INITIALIZE                  ASC-BLOCK-ENTRIES (1)               CL**1
00809                                  ASC-BLOCK-ENTRIES (2).              CL**1
00810      MOVE +1                 TO  ASC-BLK-SUB.                        CL**1
00811      SET ASC-ITEMS-LEFT-ON-DB                                        CL**1
00812                              TO  TRUE.                               CL**1
00813                                                                      CL**1
00814      MOVE +0                 TO  ASC-ITEM-SUB                        CL**1
00815                                  ASC-NUMBER-OF-ITEMS-READ            CL**1
00816                                  ASC-NUMBER-OF-SELECTED-ITEMS        CL**1
00817                                  ASC-HIGHEST-BLOCK-WRITTEN           CL**1
00818                                  ASC-START-OF-SELECTED-RANGE         CL**1
00819                                  ASC-END-OF-SELECTED-RANGE           CL**1
00820                                  ASC-ITEM-AT-TOP-OF-PANEL.           CL**1
00821                                                                      CL**1
00822      MOVE SPACE              TO  ASC-DESC-LINE-IND                   CL*52
00823                                  ASC-CORR-LINE-IND.                  CL*52
00824                                                                      CL*52
00825      PERFORM 4010-OPEN-CURSOR.                                       CL**1
00826                                                                      CL**1
00827      PERFORM 4300-READ-ITEMS-FROM-DB.                                CL**1
00828                                                                      CL**1
00829      PERFORM 4020-CLOSE-CURSOR.                                      CL**1
00830                                                                      CL**1
00831      MOVE +1                 TO  PV-TOP-ITEM.                        CL**1
00832      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                             CL**1
00833          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                       CL**1
00834          SET DP020-MSG-FATAL   TO  TRUE                              CL**1
00835          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                  CL**1
00836      ELSE                                                            CL**1
00837          MOVE -1             TO ASTRTITL                             CL**1
00838          SET DP030-SET-CURSOR-APPL-1 TO TRUE                         CL**1
00839          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        CL**1
00840      END-IF.                                                         CL**1
00841  EJECT                                                               CL**1
00842 *----------------------------------------------------------------*   CL**1
00843 * OPEN THE CURSOR.                                               *   CL*12
00844 *----------------------------------------------------------------*   CL**1
00845                                                                      CL**1
00846  4010-OPEN-CURSOR.                                                   CL**1
00847                                                                      CL**1
00848      EXEC SQL                                                        CL*64
00849           OPEN TEXT-CSR                                              CL*64
00850      END-EXEC.                                                       CL*64
00851                                                                      CL*64
00852      EVALUATE TRUE                                                   CL*64
00853          WHEN SQLCODE = ZERO                                         CL*64
00854               CONTINUE                                               CL*64
00855          WHEN SQLWARN0 NOT EQUAL SPACE                               CL*64
00856          WHEN SQLCODE NOT EQUAL ZERO                                 CL*64
00857               MOVE '4010-OPEN-CURSOR'                                CL*64
00858                            TO  DP013-PARAGRAPH                       CL*64
00859               MOVE 'OPEN MESSAGE CURSOR'                             CL*64
00860                            TO  DP013-MESSAGE-TEXT (1)                CL*64
00861               MOVE SQLCA   TO  DP013-SQLCA                           CL*64
00862               SET DP013-DB2-ABEND                                    CL*64
00863                            TO  TRUE                                  CL*64
00864               PERFORM DP013-0000-PROCESS-ABEND                       CL*64
00865      END-EVALUATE.                                                   CL*64
00866                                                                      CL**1
00867  EJECT                                                               CL**1
00868 *----------------------------------------------------------------*   CL**1
00869 * CLOSE THE CURSOR.                                              *   CL*12
00870 *----------------------------------------------------------------*   CL**1
00871                                                                      CL**1
00872  4020-CLOSE-CURSOR.                                                  CL**1
00873                                                                      CL**1
00874      EXEC SQL                                                        CL*64
00875          CLOSE TEXT-CSR                                              CL*64
00876      END-EXEC.                                                       CL*64
00877                                                                      CL*64
00878      EVALUATE TRUE                                                   CL*64
00879          WHEN SQLCODE = ZERO                                         CL*64
00880               CONTINUE                                               CL*64
00881          WHEN SQLWARN0 NOT EQUAL SPACE                               CL*64
00882          WHEN SQLCODE NOT EQUAL ZERO                                 CL*64
00883               MOVE '4020-CLOSE-CURSOR'                               CL*64
00884                            TO  DP013-PARAGRAPH                       CL*64
00885               MOVE 'CLOSE MESSAGE CURSOR'                            CL*64
00886                            TO  DP013-MESSAGE-TEXT (1)                CL*64
00887               MOVE SQLCA   TO  DP013-SQLCA                           CL*64
00888               SET DP013-DB2-ABEND                                    CL*64
00889                            TO  TRUE                                  CL*64
00890               PERFORM DP013-0000-PROCESS-ABEND                       CL*64
00891      END-EVALUATE.                                                   CL*64
00892                                                                      CL**1
00893  EJECT                                                               CL**1
00894 *----------------------------------------------------------------*   CL**1
00895 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *   CL**1
00896 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *   CL**1
00897 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *   CL**1
00898 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *   CL**1
00899 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *   CL**1
00900 *                                                                *   CL**1
00901 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *   CL**1
00902 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *   CL**1
00903 *    UPON INITIALIZATION.                                        *   CL**1
00904 *----------------------------------------------------------------*   CL**1
00905                                                                      CL**1
00906  4300-READ-ITEMS-FROM-DB.                                            CL**1
00907                                                                      CL**1
00908      EXEC SQL                                                        CL*36
00909          SELECT                                                      CL*36
00910               APLSYS_CDE                                             CL*36
00911             , SHT_TXT                                                CL*36
00912          INTO                                                        CL*37
00913               :ASC-KEY-SYSTEM-ID                                     CL*37
00914             , :ASC-KEY-MESSAGE-TEXT                                  CL*48
00915          FROM                                                        CL*36
00916               TSYMSG                                                 CL*36
00917          WHERE                                                       CL*36
00918               SYMSG_NBR = :PV-DB2-MESSAGE-NUMBER                     CL*36
00919      END-EXEC.                                                       CL*36
00920                                                                      CL*36
00921      PERFORM                                                         CL**1
00922          VARYING PV-READ-COUNT                                       CL**1
00923          FROM 1 BY 1                                                 CL**1
00924          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                       CL**1
00925             OR (ASC-NO-ITEMS-LEFT-ON-DB))                            CL**1
00926                                                                      CL**1
00927          PERFORM 4310-FETCH                                          CL**1
00928                                                                      CL**1
00929          IF  ASC-ITEMS-LEFT-ON-DB                                    CL**1
00930              PERFORM 4320-GET-SUB                                    CL*30
00931              IF SYMSGT-TYP-CDE = 'D'                                 CL*30
00932                      AND NOT ASC-DESC-LINE-DONE                      CL*30
00933                  MOVE 'LONG DESCRIPTION:'                            CL*52
00934                                TO  ASC-TEXT                          CL*30
00935                                        (ASC-BLK-SUB                  CL*30
00936                                         ASC-ITEM-SUB)                CL*30
00937                  SET ASC-DESC-LINE-DONE TO TRUE                      CL*30
00938                  PERFORM 4320-GET-SUB                                CL*30
00939              END-IF                                                  CL*30
00940              IF SYMSGT-TYP-CDE = 'R'                                 CL*30
00941                      AND NOT ASC-CORR-LINE-DONE                      CL*30
00942                  MOVE ALL '-'  TO  ASC-TEXT                          CL*30
00943                                        (ASC-BLK-SUB                  CL*30
00944                                         ASC-ITEM-SUB)                CL*30
00945                  PERFORM 4320-GET-SUB                                CL*30
00946                  MOVE 'CORRECTIVE ACTION:'                           CL*52
00947                                TO  ASC-TEXT                          CL*30
00948                                        (ASC-BLK-SUB                  CL*30
00949                                         ASC-ITEM-SUB)                CL*30
00950                  SET ASC-CORR-LINE-DONE TO TRUE                      CL*30
00951                  PERFORM 4320-GET-SUB                                CL*30
00952              END-IF                                                  CL*30
00953              MOVE SYMSGT-LNE-TXT                                     CL*50
00954                                TO  ASC-TEXT                          CL*18
00955                                        (ASC-BLK-SUB                  CL**1
00956                                         ASC-ITEM-SUB)                CL**1
00957          END-IF                                                      CL**1
00958      END-PERFORM.                                                    CL**1
00959                                                                      CL**1
00960      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                             CL*59
00961          PERFORM 4320-GET-SUB                                        CL*59
00962          MOVE                                                        CL*60
00963          '** NO LONG DESCRIPTION OR CORRECTIVE ACTION FOUND **'      CL*60
00964                                    TO  ASC-TEXT                      CL*59
00965                                            (ASC-BLK-SUB              CL*59
00966                                             ASC-ITEM-SUB)            CL*59
00967      END-IF.                                                         CL*59
00968                                                                      CL*59
00969      MOVE +1                       TO  ASC-BLK-SUB.                  CL*59
00970      PERFORM 5100-WRITE-TEMP-STORAGE.                                CL*59
00971      ADD +1                        TO  ASC-BLK-SUB.                  CL*59
00972      PERFORM 5100-WRITE-TEMP-STORAGE.                                CL*59
00973                                                                      CL*59
00974      MOVE 1                        TO  ASC-BLK-SUB                   CL*59
00975                                        PV-BLOCK-NUMBER.              CL*59
00976      PERFORM 4410-READ-TEMP-STORAGE.                                 CL*59
00977      IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                    CL*59
00978          MOVE 2                    TO  ASC-BLK-SUB                   CL*59
00979                                        PV-BLOCK-NUMBER               CL*59
00980          PERFORM 4410-READ-TEMP-STORAGE                              CL*59
00981      END-IF.                                                         CL*59
00982  EJECT                                                               CL**1
00983 *----------------------------------------------------------------*   CL**1
00984 *    FETCH THE TEXT CURSOR.                                      *   CL*61
00985 *----------------------------------------------------------------*   CL**1
00986                                                                      CL**1
00987  4310-FETCH.                                                         CL**1
00988                                                                      CL**1
00989      PERFORM                                                         CL**1
00990          WITH TEST AFTER                                             CL**1
00991          VARYING PV-SQL-SUB                                          CL**1
00992          FROM 1 BY 1                                                 CL**1
00993          UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL**1
00994             OR SQLCODE = 0                                           CL**1
00995             OR SQLCODE = +100                                        CL**1
00996                                                                      CL**1
00997          EXEC SQL                                                    CL*15
00998               FETCH TEXT-CSR                                         CL*15
00999               INTO                                                   CL*15
01000                     :SYMSGT-TYP-CDE                                  CL*15
01001                   , :SYMSGT-LNE-SEQ-NBR                              CL*15
01002                   , :SYMSGT-LNE-TXT                                  CL*15
01003          END-EXEC                                                    CL*15
01004                                                                      CL**1
01005          EVALUATE TRUE                                               CL**1
01006              WHEN SQLCODE = ZERO                                     CL**1
01007              WHEN SQLCODE = +100                                     CL**1
01008              WHEN SQLCODE = -904                                     CL**1
01009              WHEN SQLCODE = -913                                     CL**1
01010                   CONTINUE                                           CL**1
01011              WHEN SQLWARN0 NOT EQUAL SPACE                           CL**1
01012              WHEN SQLCODE NOT EQUAL ZERO                             CL**1
01013                   MOVE '4310-FETCH-CURSOR'                           CL**1
01014                                TO  DP013-PARAGRAPH                   CL**1
01015                   MOVE 'FETCH MESSAGE CURSOR'                        CL**1
01016                                TO  DP013-MESSAGE-TEXT (1)            CL**1
01017                   MOVE SQLCA   TO  DP013-SQLCA                       CL**1
01018                   SET DP013-DB2-ABEND                                CL**1
01019                                TO  TRUE                              CL**1
01020                   PERFORM DP013-0000-PROCESS-ABEND                   CL**1
01021          END-EVALUATE                                                CL**1
01022      END-PERFORM.                                                    CL**1
01023                                                                      CL**1
01024      IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL**1
01025          MOVE '4310-FETCH-CURSOR'                                    CL**1
01026                                TO  DP013-PARAGRAPH                   CL**1
01027          MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                CL**1
01028                                TO  DP013-MESSAGE-TEXT (1)            CL**1
01029          MOVE SQLCA            TO  DP013-SQLCA                       CL**1
01030          SET DP013-DB2-ABEND   TO  TRUE                              CL**1
01031          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
01032      END-IF.                                                         CL**1
01033                                                                      CL**1
01034      IF  SQLCODE EQUAL +100                                          CL**1
01035          SET ASC-NO-ITEMS-LEFT-ON-DB                                 CL**1
01036                                TO  TRUE                              CL**1
01037      END-IF.                                                         CL**1
01038                                                                      CL**1
01039  EJECT                                                               CL**1
01040 *----------------------------------------------------------------*   CL*30
01041 *    GET THE CORRECT SUBSCRIPTS FOR PLACING THE TEXT LINE.       *   CL*32
01042 *----------------------------------------------------------------*   CL*30
01043                                                                      CL*30
01044  4320-GET-SUB.                                                       CL*30
01045                                                                      CL*30
01046      IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                           CL*30
01047          ADD +1            TO  ASC-ITEM-SUB                          CL*30
01048      ELSE                                                            CL*30
01049          ADD +1            TO  ASC-BLK-SUB                           CL*30
01050          MOVE +1           TO  ASC-ITEM-SUB                          CL*30
01051                                                                      CL*30
01052          IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                    CL*30
01053              MOVE +1       TO  ASC-BLK-SUB                           CL*30
01054              PERFORM 5100-WRITE-TEMP-STORAGE                         CL*30
01055              MOVE ASC-BLOCK-ENTRIES                                  CL*30
01056                        (PC-MAX-BLOCKS-IN-ARRAY)                      CL*30
01057              TO   ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                     CL*30
01058              ADD +1        TO  ASC-BLK-SUB                           CL*30
01059              INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)              CL*30
01060          END-IF                                                      CL*30
01061      END-IF.                                                         CL*30
01062                                                                      CL*30
01063      ADD +1            TO ASC-NUMBER-OF-ITEMS-READ.                  CL*30
01064      MOVE ASC-NUMBER-OF-ITEMS-READ TO ASC-LIST-ITEM-NUMBER           CL*33
01065                                       (ASC-BLK-SUB                   CL*33
01066                                        ASC-ITEM-SUB).                CL*50
01067                                                                      CL*33
01068 *----------------------------------------------------------------*   CL**1
01069 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *   CL**1
01070 *----------------------------------------------------------------*   CL**1
01071                                                                      CL**1
01072  4400-MOVE-COMMAREA-TO-SCREEN.                                       CL**1
01073                                                                      CL**8
01074      MOVE ASC-KEY-MESSAGE-NUMBER TO AMSGNBRO.                        CL*53
01075      MOVE ASC-KEY-SYSTEM-ID      TO ASYSIDO.                         CL*53
01076      MOVE ASC-KEY-MESSAGE-TEXT   TO ASHMSGO.                         CL*53
01077                                                                      CL*53
01078      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                CL**1
01079          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))             CL**1
01080          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))             CL**1
01081              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                CL**1
01082                  GIVING PV-BLOCK-NUMBER                              CL**1
01083                  REMAINDER PV-REMAINDER                              CL**1
01084              IF  PV-REMAINDER > ZERO                                 CL**1
01085                  ADD +1 TO PV-BLOCK-NUMBER                           CL**1
01086              END-IF                                                  CL**1
01087              MOVE +1    TO ASC-BLK-SUB                               CL**1
01088              PERFORM 4410-READ-TEMP-STORAGE                          CL**1
01089              ADD +1     TO ASC-BLK-SUB                               CL**1
01090                            PV-BLOCK-NUMBER                           CL**1
01091              PERFORM 4410-READ-TEMP-STORAGE                          CL**1
01092      END-IF.                                                         CL**1
01093      MOVE +1                       TO ASC-BLK-SUB.                   CL**1
01094      COMPUTE ASC-ITEM-SUB =                                          CL**1
01095          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                 CL**1
01096          + PC-ITEMS-PER-PANEL).                                      CL**1
01097                                                                      CL**8
01098      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                           CL**1
01099          ADD +1   TO ASC-BLK-SUB                                     CL**1
01100          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB               CL**1
01101      END-IF.                                                         CL**1
01102                                                                      CL**8
01103      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                              CL**1
01104          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                   CL**1
01105      PERFORM 4420-FORMAT-LIST-LINES                                  CL**1
01106          VARYING PS-SUB                                              CL**1
01107          FROM PC-ITEMS-PER-PANEL BY -1                               CL**1
01108          UNTIL PS-SUB < +1.                                          CL**1
01109                                                                      CL**8
01110      COMPUTE ASC-ITEMS-DISPLAYED =                                   CL**1
01111          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).             CL**1
01112      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL       CL**1
01113                                       ASTRTITO.                      CL**1
01114      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                       CL**1
01115      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                       CL**1
01116                                                                      CL*36
01117      IF  DP020-MSG-NUMBER-X = SPACE                                  CL*65
01118          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ     CL*65
01119 *            --- MORE ITEMS TO DISPLAY ----                          CL*65
01120              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER               CL*65
01121              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL*65
01122          END-IF                                                      CL*65
01123      END-IF.                                                         CL*65
01124                                                                      CL*65
01125 *----------------------------------------------------------------*   CL**1
01126 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *   CL**1
01127 *----------------------------------------------------------------*   CL**1
01128                                                                      CL**1
01129  4410-READ-TEMP-STORAGE.                                             CL**1
01130                                                                      CL**1
01131      EXEC CICS                                                       CL**1
01132          READQ TS                                                    CL**1
01133              QUEUE (ASC-DTL-QUEUE-NAME)                              CL*17
01134              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                  CL**1
01135              ITEM  (PV-BLOCK-NUMBER)                                 CL**1
01136              RESP  (PV-CICS-RESP)                                    CL**1
01137      END-EXEC.                                                       CL**1
01138                                                                      CL**1
01139      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                          CL**1
01140        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                        CL**1
01141          CONTINUE                                                    CL**1
01142      ELSE                                                            CL**1
01143          SET DP013-CICS-ABEND TO  TRUE                               CL**1
01144          SET DP013-NO-ROLLBACK TO  TRUE                              CL**1
01145          MOVE '4410-READ-TEMP-STORAGE'                               CL**1
01146                                TO  DP013-PARAGRAPH                   CL**1
01147          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'         CL**1
01148                                TO  DP013-MESSAGE-TEXT(1)             CL**1
01149          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
01150      END-IF.                                                         CL**1
01151                                                                      CL**1
01152 *    SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                CL*17
01153                                                                      CL**8
01154      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                            CL**1
01155          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                  CL**1
01156      END-IF.                                                         CL**1
01157  EJECT                                                               CL**1
01158 *----------------------------------------------------------------*   CL**1
01159 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *   CL**1
01160 *----------------------------------------------------------------*   CL**1
01161                                                                      CL**1
01162  4420-FORMAT-LIST-LINES.                                             CL**1
01163                                                                      CL**8
01164      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ         CL**1
01165          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                   CL**1
01166          MOVE SPACE              TO MR-TEXT (PS-SUB)                 CL*17
01167      ELSE                                                            CL**1
01168          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL              CL**1
01169              MOVE ASC-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)                CL*18
01170                                  TO MR-TEXT (PS-SUB)                 CL*18
01171          END-IF                                                      CL**1
01172      END-IF.                                                         CL**1
01173      SUBTRACT +1 FROM ASC-ITEM-SUB.                                  CL*25
01174      IF  ASC-ITEM-SUB < + 1                                          CL**1
01175          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                     CL**1
01176          MOVE +1                 TO ASC-BLK-SUB                      CL**1
01177      END-IF.                                                         CL**1
01178                                                                      CL**8
01179 *----------------------------------------------------------------*   CL**1
01180 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *   CL**1
01181 *----------------------------------------------------------------*   CL**1
01182                                                                      CL**1
01183  5100-WRITE-TEMP-STORAGE.                                            CL**1
01184                                                                      CL**8
01185      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                        CL**1
01186                                TO  PV-LIST-ITEM-NUMBER.              CL**1
01187      COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                              CL*23
01188          ASC-HIGHEST-BLOCK-WRITTEN + 1.                              CL*51
01189                                                                      CL*51
01190      EXEC CICS                                                       CL*51
01191          WRITEQ TS                                                   CL*51
01192              QUEUE (ASC-DTL-QUEUE-NAME)                              CL*51
01193              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                  CL*51
01194              ITEM  (PV-HIGHEST-BLOCK-WRITTEN)                        CL*51
01195              RESP  (PV-CICS-RESP)                                    CL*51
01196      END-EXEC.                                                       CL*51
01197                                                                      CL*51
01198      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                         CL*51
01199          SET DP013-CICS-ABEND                                        CL*51
01200                        TO  TRUE                                      CL*51
01201          SET DP013-NO-ROLLBACK                                       CL*51
01202                        TO  TRUE                                      CL*51
01203          MOVE '5100-WRITE-TEMP-STORAGE'                              CL*51
01204                        TO  DP013-PARAGRAPH                           CL*51
01205          MOVE 'ATTEMPTING TO WRITE TEMP STORAGE LIST BLOCK'          CL*51
01206                        TO  DP013-MESSAGE-TEXT(1)                     CL*51
01207          PERFORM DP013-0000-PROCESS-ABEND                            CL*51
01208      END-IF.                                                         CL*51
01209      ADD +1            TO  ASC-HIGHEST-BLOCK-WRITTEN.                CL*51
01210                                                                      CL**1
01211  EJECT                                                               CL**1
01212 *----------------------------------------------------------------*   CL*47
01213 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *   CL*47
01214 *----------------------------------------------------------------*   CL*47
01215                                                                      CL*47
01216  9100-READ-SEL-QUEUE.                                                CL*47
01217      EXEC CICS                                                       CL*47
01218          READQ TS                                                    CL*47
01219              QUEUE (ASC-SEL-QUEUE-NAME)                              CL*47
01220              INTO  (DP057-MESSAGE-SEL-REC)                           CL*47
01221              ITEM  (ASC-SEL-ITEM)                                    CL*47
01222              RESP  (PV-CICS-RESP)                                    CL*50
01223      END-EXEC.                                                       CL*47
01224                                                                      CL*47
01225      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                          CL*50
01226          CONTINUE                                                    CL*47
01227      ELSE                                                            CL*47
01228          SET DP013-LOGIC-ABEND                                       CL*47
01229              DP013-NO-ROLLBACK     TO TRUE                           CL*47
01230          MOVE '9100-READ-SEL-QUEUE'                                  CL*47
01231                                    TO DP013-PARAGRAPH                CL*47
01232          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'      CL*47
01233                                    TO DP013-MESSAGE-TEXT (1)         CL*47
01234          PERFORM DP013-0000-PROCESS-ABEND                            CL*47
01235      END-IF.                                                         CL*47
01236 *----------------------------------------------------------------*   CL*47
01237 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *   CL*47
01238 *----------------------------------------------------------------*   CL*47
01239  9200-REWRITE-SEL-QUEUE.                                             CL*49
01240      EXEC CICS                                                       CL*47
01241           WRITEQ TS                                                  CL*47
01242               QUEUE  (ASC-SEL-QUEUE-NAME)                            CL*47
01243               FROM   (DP057-MESSAGE-SEL-REC)                         CL*47
01244               ITEM   (ASC-SEL-ITEM)                                  CL*47
01245               RESP   (PV-CICS-RESP)                                  CL*50
01246               REWRITE                                                CL*47
01247      END-EXEC.                                                       CL*47
01248                                                                      CL*47
01249      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                      CL*50
01250          SET DP013-CICS-ABEND                                        CL*47
01251              DP013-NO-ROLLBACK     TO TRUE                           CL*47
01252          MOVE '9200-REWRITE-SEL-QUEUE'                               CL*49
01253                                    TO DP013-PARAGRAPH                CL*47
01254          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'      CL*47
01255                                    TO DP013-MESSAGE-TEXT (1)         CL*47
01256          PERFORM DP013-0000-PROCESS-ABEND                            CL*47
01257      END-IF.                                                         CL*47
01258                                                                      CL*47
01259  EJECT                                                               CL*47
01260 *----------------------------------------------------------------*   CL**1
01261 *    ABEND PROCESSOR MODULE                                      *   CL**1
01262 *----------------------------------------------------------------*   CL**1
01263                                                                      CL**1
01264      COPY DPPD013.                                                   CL**1
