       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT009.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  03/27/2006.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM REMOVES UPC RECORD TYPES THAT ARE MISSING DETAIL *        
      *  INFORMATION.                                                  *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           COPY ETRD019.                                                        
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD                    PIC X(150).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-PREVIOUS-RECORD             PIC X(150).                           
       01  FILLER REDEFINES WS-PREVIOUS-RECORD.                                 
           05  WS-PREV-RECORD-TYPE        PIC X(02).                            
           05  FILLER                     PIC X(148).                           
                                                                                
       01  WS-FILE-EMPTY-SW               PIC X(1)    VALUE 'N'.                
           88  FILE-EMPTY                             VALUE 'Y'.                
                                                                                
       01  END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE                      VALUE 'Y'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           IF END-OF-INPUT-FILE-SW = 'N'                                        
              MOVE ET019-PRODUCT-ACTIVITY-RECORD TO WS-PREVIOUS-RECORD          
              PERFORM 2000-READ-INPUT-FILE                                      
                 THRU 2000-READ-INPUT-FILE-EXIT                                 
           ELSE                                                                 
              SET FILE-EMPTY TO TRUE.                                           
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           IF WS-FILE-EMPTY-SW = 'N'                                            
              MOVE WS-PREVIOUS-RECORD TO OUTPUT-RECORD                          
              WRITE OUTPUT-RECORD.                                              
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF ET019-RECORD-TYPE = '02' AND                                      
              WS-PREV-RECORD-TYPE = '02'                                        
                 NEXT SENTENCE                                                  
           ELSE                                                                 
              MOVE WS-PREVIOUS-RECORD TO OUTPUT-RECORD                          
              WRITE OUTPUT-RECORD.                                              
                                                                                
           MOVE ET019-PRODUCT-ACTIVITY-RECORD TO WS-PREVIOUS-RECORD.            
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE.                                 
                                                                                
       2000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
