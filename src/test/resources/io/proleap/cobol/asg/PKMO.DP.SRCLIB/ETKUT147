       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT147.                                       
       AUTHOR.                  SUE PARI.                                       
       DATE-WRITTEN.            07/01/2002.                                     
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM ADDS ENT-ID'S TO THE HEADER RECORD FOR THOSE      *        
      * VENDORS WHO ARE TO RECEIVE A FULL STORE ADDRESS FILE.          *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT VENDOR-FILE                                                   
              ASSIGN TO INP02.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD                PIC X(60).                               
                                                                                
                                                                                
                                                                                
       FD  VENDOR-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 18 CHARACTERS                                        
           DATA RECORD IS VENDOR-RECORD.                                        
                                                                                
       01  VENDOR-RECORD.                                                       
           05  VENDOR-QUALIFIER        PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  VENDOR-ID               PIC X(15).                               
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD               PIC X(60).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           COPY ETRD032.                                                        
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  WS-ENT-ID                   PIC X(10)   VALUE ZERO.              
                                                                                
           05  WS-PROD-ENT-ID              PIC 9(10)   VALUE ZERO.              
                                                                                
           05  WS-TEST-ENT-ID.                                                  
               10  WS-ENT-ID-NUMBER        PIC 9(9)    VALUE ZERO.              
               10  WS-ENT-ID-TEST-FLAG     PIC X(1)    VALUE 'T'.               
                                                                                
           05  WS-VENDOR-ID-DESC           PIC X(2)    VALUE SPACES.            
           05  WS-VENDOR-ID                PIC X(15)   VALUE SPACES.            
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-INPUT-FILE-SW        PIC X(1)    VALUE 'N'.               
               88  END-OF-INPUT-FILE                   VALUE 'Y'.               
           05  END-OF-VENDOR-FILE-SW       PIC X(1)    VALUE 'N'.               
               88  END-OF-VENDOR-FILE                  VALUE 'Y'.               
           05  END-OF-ENT-ID-SW            PIC X(1)    VALUE 'N'.               
               88  END-OF-ENT-ID                       VALUE 'Y'.               
           05  ON-TTRDMLT-TABLE-SW         PIC X(1)    VALUE 'N'.               
               88  ON-TTRDMLT-TABLE                    VALUE 'Y'.               
           05  PARENT-ENT-ID-SW            PIC X(1)    VALUE 'N'.               
               88  PARENT-ENT-ID                       VALUE 'Y'.               
                                                                                
       01  PV-PARAGRAPH-NAME               PIC X(30).                           
       01  PV-DB2-OPER-ATTEMPTED           PIC X(30).                           
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
                                                                                
      ******************************************************************        
      *                SQL ABEND ROUTINE WORKING STORAGE               *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *                      TRADING PARTNER TABLE                     *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *              TRADING PARTNER TRANSACTION SET TABLE             *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *              TRADING PARTNER MULTIPLE ENT-ID TABLE             *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * RETRIEVE THE ENT-ID'S FOR VENDORS WHO ARE ACTIVELY SET UP TO   *        
      * RECEIVE PRODUCTION 816 DATA.                                   *        
      ******************************************************************        
           EXEC SQL                                                             
              DECLARE VENDOR_CURSOR CURSOR FOR                                  
              SELECT  A.ENT_ID,                                                 
                      B.PROD_TEST_IND                                           
              FROM    TTRDPRT A,                                                
                      TTRDTRN B                                                 
              WHERE   A.VEND_INCHG_ID = :WS-VENDOR-ID                           
                AND   A.VEND_INCHG_ID_DESC = :WS-VENDOR-ID-DESC                 
                AND   A.INACTV_DTE = '9999-09-09'                               
                AND   B.TP_DKEY = A.TP_DKEY                                     
                AND   B.TRAN_SET_CDE = '816'                                    
                AND   B.DIR_IND = 'S'                                           
                AND   B.INACTV_DTE = '9999-09-09'                               
             ORDER BY A.ENT_ID                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  VENDOR-FILE.                                             
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 5000-READ-VENDOR-FILE                                        
              THRU 5000-READ-VENDOR-FILE-EXIT.                                  
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-VENDOR-FILE.                                      
                                                                                
           CLOSE VENDOR-FILE.                                                   
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS ALL THE VENDORS WHO ARE LISTED IN THE INPUT FILE.      *        
      ******************************************************************        
       1000-PROCESS-FILE.                                                       
                                                                                
           MOVE SPACES TO WS-ENT-ID.                                            
                                                                                
           PERFORM 2000-GET-ENT-ID                                              
              THRU 2000-GET-ENT-ID-EXIT.                                        
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           MOVE 'N' TO END-OF-INPUT-FILE-SW.                                    
                                                                                
           IF WS-ENT-ID > SPACES                                                
              PERFORM 4000-READ-INPUT-FILE                                      
                 THRU 4000-READ-INPUT-FILE-EXIT                                 
                    UNTIL END-OF-INPUT-FILE.                                    
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           PERFORM 5000-READ-VENDOR-FILE                                        
              THRU 5000-READ-VENDOR-FILE-EXIT.                                  
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * OPEN THE CURSOR THAT IS USED TO RETRIEVE THE ENT-ID'S.         *        
      ******************************************************************        
       2000-GET-ENT-ID.                                                         
                                                                                
           EXEC SQL                                                             
              OPEN VENDOR_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '2000-GET-ENT-ID'    TO PV-PARAGRAPH-NAME                    
              MOVE 'OPEN VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-ENT-ID-SW.                                        
                                                                                
           PERFORM 3000-PROCESS-ENT-ID                                          
              THRU 3000-PROCESS-ENT-ID-EXIT                                     
                 UNTIL END-OF-ENT-ID.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE VENDOR_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '2000-GET-ENT-ID'     TO PV-PARAGRAPH-NAME                   
              MOVE 'CLOSE VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       2000-GET-ENT-ID-EXIT.                                                    
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS ALL ENT-ID'S TO FIND THE ONE THAT SHOULD BE USED.      *        
      ******************************************************************        
       3000-PROCESS-ENT-ID.                                                     
                                                                                
           EXEC SQL                                                             
              FETCH VENDOR_CURSOR                                               
              INTO :TRDPRT-ENT-ID,                                              
                   :TRDTRN-PROD-TEST-IND                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ENT-ID-SW                                   
                 GO TO 3000-PROCESS-ENT-ID-EXIT                                 
              ELSE                                                              
                 MOVE '3000-PROCESS-ENT-ID' TO PV-PARAGRAPH-NAME                
                 MOVE 'FETCH VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           PERFORM 6000-CHECK-TTRDMLT-TABLE                                     
              THRU 6000-CHECK-TTRDMLT-TABLE-EXIT.                               
                                                                                
           IF ON-TTRDMLT-TABLE-SW = 'N' OR                                      
              (ON-TTRDMLT-TABLE AND PARENT-ENT-ID-SW = 'Y')                     
              IF TRDTRN-PROD-TEST-IND = 'T'                                     
                 MOVE TRDPRT-ENT-ID TO WS-ENT-ID-NUMBER                         
                 MOVE WS-TEST-ENT-ID TO WS-ENT-ID                               
              ELSE                                                              
                 MOVE TRDPRT-ENT-ID TO WS-PROD-ENT-ID                           
                 MOVE WS-PROD-ENT-ID TO WS-ENT-ID                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-PROCESS-ENT-ID-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * READ THE INPUT FILE AND WRITE EACH RECORD TO THE OUTOUT FILE.  *        
      * ADD THE ENT-ID TO THE OUTPUT FILE ON TYPE 01 RECORDS.          *        
      ******************************************************************        
       4000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO ET032-RELATIONSHIP-RECORD                       
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE                                  
                 GO TO 4000-READ-INPUT-FILE-EXIT.                               
                                                                                
           IF ET032-RECORD-TYPE-CODE = 01                                       
              MOVE WS-ENT-ID TO ET032-HDR-ENT-ID.                               
                                                                                
           WRITE OUTPUT-RECORD FROM ET032-RELATIONSHIP-RECORD.                  
                                                                                
       4000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * READ THE LIST OF VENDORS WHO ARE TO RECEIVE FULL ADDRESS FILES *        
      ******************************************************************        
       5000-READ-VENDOR-FILE.                                                   
                                                                                
           READ VENDOR-FILE                                                     
              AT END                                                            
                 SET END-OF-VENDOR-FILE TO TRUE                                 
                 GO TO 5000-READ-VENDOR-FILE-EXIT.                              
                                                                                
           MOVE VENDOR-QUALIFIER TO WS-VENDOR-ID-DESC.                          
           MOVE VENDOR-ID TO WS-VENDOR-ID.                                      
                                                                                
           DISPLAY 'VENDOR REQUESTED: '                                         
                   WS-VENDOR-ID-DESC ' '                                        
                   WS-VENDOR-ID.                                                
                                                                                
       5000-READ-VENDOR-FILE-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * DO NOT WRITE RECORDS TO THE OUTPUT FILE IF THE ENT-ID IS PART  *        
      * OF A PARENT/CHILD RELATIONSHIP AND IS NOT THE PARENT ENT-ID.   *        
      ******************************************************************        
       6000-CHECK-TTRDMLT-TABLE.                                                
                                                                                
           EXEC SQL                                                             
              SELECT PRNT_ENT_ID                                                
              INTO   :TRDMLT-PRNT-ENT-ID                                        
              FROM   TTRDMLT                                                    
              WHERE  VND_INCHG_ID = :WS-VENDOR-ID                               
                AND  VND_INCHG_ID_DESC = :WS-VENDOR-ID-DESC                     
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              MOVE 'Y' TO ON-TTRDMLT-TABLE-SW                                   
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'N' TO ON-TTRDMLT-TABLE-SW                                
              ELSE                                                              
                 MOVE '6000-CHECK-TTRDMLT-TABLE' TO PV-PARAGRAPH-NAME           
                 MOVE 'SELECT FROM TTRDMLT' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           IF ON-TTRDMLT-TABLE                                                  
              IF TRDPRT-ENT-ID = TRDMLT-PRNT-ENT-ID                             
                 MOVE 'Y' TO PARENT-ENT-ID-SW                                   
              ELSE                                                              
                 MOVE 'N' TO PARENT-ENT-ID-SW                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-CHECK-TTRDMLT-TABLE-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * DB2 ABEND PROCESSING - THE CODE WE HOPE IS NEVER NEEDED.       *        
      ******************************************************************        
       9999-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
