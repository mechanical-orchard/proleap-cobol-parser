       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.    XSKUP012.                                         00020002
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030002
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040002
       DATE-WRITTEN.  09/29/2002.                                       00050022
       DATE-COMPILED.                                                   00060002
      ******************************************************************00070022
      **       VSM ITEM MESSAGE - NUMBER 050 - SKU COLOR/SIZE ATTR CHNG 00080022
      ******************************************************************00090022
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO FOLLOWING SKU          00100032
      ***     ATTRIBUTES:                                               00110032
      ***     1. KOHLS_SIZE_CDE                                         00120032
      ***     2. NRF_CLOR_CDE                                           00130032
      ***     3. CSTM_CLOR_DESC                                         00140032
091004***     4. CUSTFACG_CLOR_DESC                                     00150040
      ******************************************************************00160022
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00170022
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00180030
011403**01/14/2003 CHANGED BY:JULIE SMITH  FIXES                        00190033
022103**02/21/2003 CHANGED BY:JULIE SMITH  COMPARE INTRL UPC ID AND     00200037
022103**           UPC ID ON MESSAGE AND IF EQUAL PROCESS MESSAGE       00210037
022103**           ELSE DO NOT PROCESS BECAUSE IT IS A RENUMBER SKU     00220037
091004**09/10/2004 CHANGED BY:JULIE TEWES  ADD CUSTFACG_CLOR_DESC       00230040
      ******************************************************************00240022
       ENVIRONMENT DIVISION.                                            00250002
       CONFIGURATION SECTION.                                           00260002
       SOURCE-COMPUTER.  IBM-3090.                                      00270002
       OBJECT-COMPUTER.  IBM-3090.                                      00280002
       DATA DIVISION.                                                   00290002
       WORKING-STORAGE SECTION.                                         00300002
      ******************************************************************00310002
      **PROGRAM SWITCHES                                                00320002
      ******************************************************************00330002
       01  PS-PROGRAM-SWITCHES.                                         00340002
           05  PS-PROCESS-CONTINUE-SW      PIC X(01) VALUE 'Y'.         00350022
               88  PS-PROCESS-CONTINUE               VALUE 'Y'.         00360022
               88  PS-PROCESS-COMPLETE               VALUE 'N'.         00370022
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00380027
               88  PS-ACTION-REVERSE                  VALUE 01.         00390027
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00400027
               88  PS-ACTION-NOTHING                  VALUE 02.         00410027
      ******************************************************************00420002
      **PROGRAM COUNTERS                                                00430024
      ******************************************************************00440002
       01  PC-PROGRAM-COUNTER.                                          00450024
           05  PC-MAX-RETRIES              PIC 9(01) VALUE 5.           00460024
           05  PC-RETRIES                  PIC 9(01) VALUE ZEROS.       00470024
           05  PC-RETRY-COUNTER-2          PIC 9(01) VALUE ZEROS.       00480022
      ******************************************************************00490016
      **PROGRAM CALCULATIONS                                            00500016
      ******************************************************************00510016
       01  PC-PROGRAM-CALCULATIONS.                                     00520016
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00530016
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00540016
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00550016
      ******************************************************************00560002
      **PROGRAM VARIABLES                                               00570024
      ******************************************************************00580002
       01  PV-PROGRAM-VARIABLES.                                        00590024
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00600024
      ******************************************************************00610002
      **COPYBOOKS                                                       00620002
      ******************************************************************00630002
      ***ABEND COPYBOOK                                                 00640002
           COPY DPWS004.                                                00650040
      ***DB2 MESSAGE AREA                                               00660002
           COPY SQLCA2.                                                 00670040
      ***ITEM COPYBOOK                                                  00680022
           COPY IMRD008.                                                00690040
      ******************************************************************00700002
      **TABLE INCLUSIONS                                                00710002
      ******************************************************************00720002
      ***DB2 COMMUNICATIONS AREA                                        00730002
           EXEC SQL                                                     00740002
                INCLUDE SQLCA                                           00750002
           END-EXEC.                                                    00760002
      ***XST_SKU TABLE                                                  00770002
           EXEC SQL                                                     00780002
               INCLUDE XST00010                                         00790002
           END-EXEC.                                                    00800002
                                                                        00810002
       LINKAGE SECTION.                                                 00820002
      ******************************************************************00830002
      **LINKAGE VARIABLES                                               00840002
      ******************************************************************00850002
       01  LV-IMRD008-RECORD               PIC X(450).                  00860022
       01  LV-RETURN-CODE                  PIC S9(04).                  00870016
       01  LV-ACTION-IND                   PIC 9(02).                   00880027
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00890027
                                                                        00900002
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00910022
                                LV-RETURN-CODE,                         00920027
                                LV-ACTION-IND,                          00930027
                                LV-CURRENT-TIMESTAMP.                   00940027
      ******************************************************************00950002
       0000-MAINLINE.                                                   00960002
                                                                        00970002
            PERFORM 1000-INITIALIZATION.                                00980002
            PERFORM 2000-PROCESS-MESSAGE                                00990022
                    UNTIL PS-PROCESS-COMPLETE.                          01000022
            PERFORM 9999-WRAPUP.                                        01010016
                                                                        01020002
       EJECT.                                                           01030016
      ***************************************************************** 01040002
      **INITIALIZATION AREA                                             01050002
      ***************************************************************** 01060002
       1000-INITIALIZATION.                                             01070002
                                                                        01080002
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01090024
                                                                        01100002
           INITIALIZE PV-PROGRAM-VARIABLES.                             01110025
           MOVE LV-IMRD008-RECORD     TO IMRD008-RECORD.                01120026
                                                                        01130016
           MOVE IM008-SKU-CLR-KOHLS-SIZE-CDE-N                          01140018
                                      TO XST00010-KOHLS-SIZE-CDE.       01150019
           MOVE IM008-SKU-CLR-NRF-CLOR-CDE-N                            01160020
                                      TO XST00010-NRF-CLOR-CDE.         01170019
           MOVE IM008-SKU-CLR-CSTM-CLOR-DESC-N                          01180020
                                      TO XST00010-CSTM-CLOR-DESC.       01190019
011403     MOVE IM008-SKU-CLR-SIZE-DESC-NEW TO XST00010-SIZE-DESC.      01200034
011403     MOVE IM008-SKU-CLR-NRF-CLOR-DESC-N                           01210035
011403                                TO XST00010-NRF-CLOR-DESC.        01220033
121402     MOVE LV-CURRENT-TIMESTAMP  TO XST00010-LAST-UPD-TMST.        01230030
121402     MOVE IM008-CRE-TMST        TO XST00010-SOR-LAST-MSG-TMST.    01240030
091004     MOVE IM008-SKU-CLR-CUSTFACG-CLOR-N                           01250041
091004                                TO XST00010-CUSTFACG-CLOR-DESC.   01260041
                                                                        01270018
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01280023
      *     COMP FIELDS.                                                01290023
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01300016
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01310016
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01320016
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01330016
                                                                        01340002
022103*DETERMINES IF MESSAGE SHOULD BE PROCESSED OR PASSED IF FIELDS    01350037
022103*    ARE NOT EQUAL THEN MESSAGE IS FOR THE RENUMBERED SKU         01360037
022103*    AND SHOULD NOT BE PROCESSED AT THE SKU LEVEL                 01370037
022103     IF  IM008-ITEM-KEY-INTR-UPC-ID = IM008-ITEM-KEY-UPC-ID       01380038
022103         SET PS-PROCESS-CONTINUE TO TRUE                          01390037
022103     ELSE                                                         01400037
022103         SET PS-PROCESS-COMPLETE TO TRUE                          01410037
022103     END-IF.                                                      01420037
                                                                        01430037
       EJECT.                                                           01440016
      ***************************************************************** 01450002
      **UPDATES A COLOR ATTRIBUTES ON XST_SKU                           01460032
      ***************************************************************** 01470002
       2000-PROCESS-MESSAGE.                                            01480022
                                                                        01490002
           MOVE '2000-PROCESS-MESSAGE'  TO PV-PARAGRAPH-NAME.           01500024
                                                                        01510002
           EXEC SQL                                                     01520016
              UPDATE XST_SKU                                            01530016
                  SET KOHLS_SIZE_CDE = :XST00010-KOHLS-SIZE-CDE         01540017
                    , NRF_CLOR_CDE   = :XST00010-NRF-CLOR-CDE           01550017
011403              , NRF_CLOR_DESC  = :XST00010-NRF-CLOR-DESC          01560033
011403              , SIZE_DESC      = :XST00010-SIZE-DESC              01570034
                    , CSTM_CLOR_DESC = :XST00010-CSTM-CLOR-DESC         01580017
                    , LAST_UPD_TMST  = :XST00010-LAST-UPD-TMST          01590030
121402              , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST   01600030
091004              , CUSTFACG_CLOR_DESC = :XST00010-CUSTFACG-CLOR-DESC 01610042
                  WHERE INTR_UPC_ID  = :XST00010-INTR-UPC-ID            01620017
           END-EXEC.                                                    01630016
                                                                        01640016
           EVALUATE TRUE                                                01650016
      ***SUCCESSFUL PROCESSING                                          01660032
              WHEN SQLCODE = ZERO                                       01670016
                   SET PS-PROCESS-COMPLETE         TO TRUE              01680022
      ***ROW/TABLE LOCK                                                 01690032
              WHEN SQLCODE = -911                                       01700016
              WHEN SQLCODE = -913                                       01710016
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     01720024
                       SET PS-PROCESS-COMPLETE     TO TRUE              01730022
                   ELSE                                                 01740016
                       ADD +1                      TO PC-RETRIES        01750024
                       SET PS-PROCESS-COMPLETE     TO TRUE              01760022
                   END-IF                                               01770016
              WHEN OTHER                                                01780016
                   SET PS-PROCESS-COMPLETE         TO TRUE              01790022
           END-EVALUATE.                                                01800016
                                                                        01810002
       EJECT.                                                           01820016
      ******************************************************************01830012
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01840002
      ******************************************************************01850002
       9999-WRAPUP.                                                     01860016
                                                                        01870002
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME.             01880029
                                                                        01890012
           MOVE SQLCODE               TO LV-RETURN-CODE.                01900029
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01910027
                                                                        01920027
           EXIT PROGRAM.                                                01930002
                                                                        01940002
       EJECT.                                                           01950016
