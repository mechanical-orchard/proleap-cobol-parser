      *----------------------------------------------------------------*00010002
       IDENTIFICATION DIVISION.                                         00020002
      *----------------------------------------------------------------*00030002
       PROGRAM-ID.      XSKUP366.                                       00040025
       AUTHOR.          COGNIZANT.                                      00050002
       DATE-WRITTEN.    05/30/2014.                                     00060002
                                                                        00070002
      *----------------------------------------------------------------*00080002
      *         PROGRAM TO LOAD XST_DMA TABLE FROM THE XSK099 JOB       00090002
      *----------------------------------------------------------------*00100002
      *     COMPARES THE INPUT FILE WITH XST_DMA TABLE AND INSERTS,     00110002
      *     UPDATES, DELETES RECORDS BASED ON INPUT RECORD.             00120002
      *----------------------------------------------------------------*00130002
      * HISTORY LOG:                                                    00140002
      *----------------------------------------------------------------*00150002
      * DATE:                                                           00160002
      * WR/IR#:                                                         00170002
      * PROGRAMMER:                                                     00180002
      * DESCRIPTION:                                                    00190002
      *                                                                 00200002
      *----------------------------------------------------------------*00210002
       ENVIRONMENT DIVISION.                                            00220002
      *----------------------------------------------------------------*00230002
       CONFIGURATION SECTION.                                           00240002
       SOURCE-COMPUTER.        IBM-3090.                                00250002
       OBJECT-COMPUTER.        IBM-3090.                                00260002
                                                                        00270002
       INPUT-OUTPUT SECTION.                                            00280002
       FILE-CONTROL.                                                    00290002
                                                                        00300002
           SELECT DMA-FILE                                              00310002
                  ASSIGN TO INP01.                                      00330002
           SELECT DMA-TABLE-FILE                                        00340019
                  ASSIGN TO INP02.                                      00350019
                                                                        00430002
       DATA DIVISION.                                                   00440002
       FILE SECTION.                                                    00450002
                                                                        00460002
       FD  DMA-FILE                                                     00470002
           RECORDING MODE IS F                                          00480002
           BLOCK CONTAINS 0 RECORDS                                     00490002
           LABEL RECORDS ARE STANDARD.                                  00510002
       01  DMA-FILE-RECORD                    PIC X(52).                00520002
                                                                        00530002
       FD  DMA-TABLE-FILE                                               00540019
           RECORDING MODE IS F                                          00550019
           BLOCK CONTAINS 0 RECORDS                                     00560019
           LABEL RECORDS ARE STANDARD.                                  00570019
       01  DMA-TABLE-FILE-RECORD              PIC X(45).                00580024
                                                                        00590019
                                                                        00740002
      *----------------------------------------------------------------*00750002
       WORKING-STORAGE SECTION.                                         00770002
                                                                        00950002
      *----------------------------------------------------------------*00960002
      * PS- PROGRAM SWITCHES                                            00970002
      *----------------------------------------------------------------*00980002
       01  PS-PROGRAM-SWITCHES.                                         00990002
                                                                        01000002
           05  PS-END-OF-DMA-FILE-SW           PIC X(01) VALUE SPACES.  01010002
               88  PS-END-OF-DMA-FILE                    VALUE 'Y'.     01020002
               88  PS-NOT-END-OF-DMA-FILE                VALUE 'N'.     01030002
           05  PS-END-OF-DMA-TABLE-FILE-SW     PIC X(01) VALUE 'N'.     01040019
               88  PS-END-OF-DMA-TABLE-FILE              VALUE 'Y'.     01050019
               88  PS-NOT-END-OF-DMA-TABLE-FILE          VALUE 'N'.     01060019
           05  PS-PROCESS-INDICATOR-SW         PIC X(01) VALUE 'Y'.     01070007
               88  PS-PROCESS-COMPLETE                   VALUE 'N'.     01080007
               88  PS-PROCESS-CONTINUE                   VALUE 'Y'.     01090007
                                                                        01094002
      *----------------------------------------------------------------*01095002
      * PC- PROGRAM CONSTANTS                                           01096002
      *----------------------------------------------------------------*01097002
       01  PC-PROGRAM-CONSTANTS.                                        01098002
                                                                        01099002
           05  PC-PROGRAM-NAME           PIC X(08)  VALUE 'XSKUP366'.   01100025
                                                                        01170002
      *----------------------------------------------------------------*01180019
      * PI- INPUT RECORD LAYOUT                                         01190020
      *----------------------------------------------------------------*01200019
       01  PL-TBL-INPUT-RECORD.                                         01210020
                                                                        01220019
           05 PL-TBL-DMA-NBR             PIC S9(4)V COMP-3.             01221020
           05 PL-TBL-GMA-NBR             PIC S9(4)V COMP-3.             01222020
           05 PL-TBL-DMA-NM              PIC  X(30).                    01223020
           05 PL-TBL-DMA-USR-ID          PIC  X(8).                     01224020
                                                                        01240019
      *----------------------------------------------------------------*01330002
      * PA- PROGRAM ACCUMULATORS                                        01340002
      *----------------------------------------------------------------*01350002
       01  PA-PROGRAM-ACCUMULATORS.                                     01360002
                                                                        01370002
           05  PA-DMAFILE-COUNT          PIC 9(09) VALUE ZEROS.         01380015
           05  PA-DMATAB-FILE-COUNT      PIC 9(09) VALUE ZEROS.         01381019
           05  PA-UPDATE-DMA-COUNT       PIC 9(09) VALUE ZEROS.         01390015
           05  PA-DELETE-DMA-COUNT       PIC 9(09) VALUE ZEROS.         01400015
           05  PA-INSERT-DMA-COUNT       PIC 9(09) VALUE ZEROS.         01410015
                                                                        01520002
      *----------------------------------------------------------------*01530007
      * PV- PROGRAM VARIABLES                                           01540007
      *----------------------------------------------------------------*01550007
       01  PV-PROGRAM-VARIABLES.                                        01560016
                                                                        01570007
           05  PV-DMA-NBR                PIC S9(4)V COMP-3.             01590015
           05  PV-GMA-NBR                PIC S9(4)V COMP-3.             01600015
           05  PV-MESSAGE                PIC X(60)  VALUE SPACES.       01610015
           05  PV-PARAGRAPH              PIC X(30)  VALUE SPACES.       01620015
           05  PV-SQL-CODE               PIC S9(04) VALUE +0 COMP-3.    01630015
           05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.        01640020
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.        01650020
           05  PV-OPERATION-MSG          PIC X(35)  VALUE SPACE.        01660020
                                                                        02140002
      ******************************************************************02150008
      *                        ERROR HANDLING                           02160008
      ******************************************************************02170008
       01  ABEND-CODE                    PIC S9(4)   VALUE ZEROS        02180008
                                                     COMP SYNC.         02190008
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.       02200008
                                                                        02210008
      *----------------------------------------------------------------*04270002
      * DMA FILE LAYOUT                                                 04280002
      *----------------------------------------------------------------*04290002
           COPY HMRD007.                                                04300002
                                                                        04310002
      *----------------------------------------------------------------*04320002
      * DB2 SQLCA ROUTINE DPPD004                                       04321002
      *----------------------------------------------------------------*04322002
           COPY DPWS004.                                                04323002
                                                                        04324002
      *----------------------------------------------------------------*04325002
      * DB2 ABEND COPYBOOK                                              04326002
      *----------------------------------------------------------------*04327002
           COPY DPWS900.                                                04328002
                                                                        04329002
      *----------------------------------------------------------------*04329102
      * DB2 MESSAGE AREA                                                04329202
      *----------------------------------------------------------------*04329302
           COPY SQLCA2.                                                 04329402
                                                                        04329502
      *----------------------------------------------------------------*04329602
      * DB2 COMMUNICATIONS AREA                                         04329702
      *----------------------------------------------------------------*04329802
           EXEC SQL                                                     04329902
             INCLUDE SQLCA                                              04330002
           END-EXEC.                                                    04330102
                                                                        04330202
      *----------------------------------------------------------------*04330302
      * DB2 TABLE XST00067(XST_DMA)                                     04330402
      *----------------------------------------------------------------*04330502
           EXEC SQL                                                     04330602
             INCLUDE XST00067                                           04330702
           END-EXEC.                                                    04330802
                                                                        04330902
      *----------------------------------------------------------------*04332802
       PROCEDURE DIVISION.                                              04332902
      *----------------------------------------------------------------*04333002
       0000-MAINLINE.                                                   04334002
                                                                        04335002
           PERFORM 1000-INITIALIZE.                                     04336002
                                                                        04337002
           PERFORM 2000-PROCESS-RECORDS                                 04338002
             UNTIL PS-END-OF-DMA-FILE                                   04339013
                OR PS-END-OF-DMA-TABLE-FILE.                            04339119
                                                                        04340019
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           04341019
             UNTIL PS-END-OF-DMA-FILE                                   04342002
               AND PS-END-OF-DMA-TABLE-FILE.                            04343019
                                                                        04344002
           PERFORM 9000-EOJ-ROUTINE.                                    04350013
                                                                        04360002
           GOBACK.                                                      04370002
      *----------------------------------------------------------------*04540002
      * 1000-INITIALIZATION.                                            04550002
      *     SETS UP INITIAL VALUES, OPENS FILES, PREPS FOR PROCESSING.  04560002
      *----------------------------------------------------------------*04570002
       1000-INITIALIZE.                                                 04580002
                                                                        04590002
            OPEN INPUT DMA-FILE                                         04600020
                       DMA-TABLE-FILE.                                  04600120
                                                                        04601002
            PERFORM 7000-READ-DMA-FILE.                                 04610013
                                                                        04611002
            IF PS-END-OF-DMA-FILE                                       04620002
               DISPLAY "NO DMA RECORDS TO PROCESS"                      04630002
               PERFORM 9999-PROCESS-ABEND                               04631021
            END-IF.                                                     04640002
                                                                        04650002
            PERFORM 7200-READ-DMA-TABLE-FILE.                           04660019
                                                                        04850002
            IF PS-END-OF-DMA-TABLE-FILE                                 04860019
               DISPLAY "NO DMA RECORDS TO PROCESS FROM TABLE"           04870019
            END-IF.                                                     04880019
                                                                        04890019
      *----------------------------------------------------------------*05800002
      *2000-PROCESS-RECORDS.                                            05810002
      * THIS SECTION COMPARES THE KEY FIELD OF XST_DMA TABLE WITH THE   05820002
      * INPUT FILE. IF RECORD MATCHES THEN COMPARISON OF FIELDS IS MADE 05830002
      * TO UPDATE THE TABLE. IF RECORD NOT FOUND IN XST_DMA TABLE THEN  05840002
      * INSERTION OF RECORD HAPPENS. IF A PARTICULAR RECORD IS MISSING  05850002
      * IN INPUT FILE THEN DELETE THAT PARTICULAR RECORD.               05860002
      *----------------------------------------------------------------*05880002
       2000-PROCESS-RECORDS.                                            05890002
                                                                        05900002
           MOVE HM007-DMA-NBR TO PV-DMA-NBR                             05910007
           MOVE HM007-GMA-NBR TO PV-GMA-NBR                             05920007
                                                                        05930007
           EVALUATE TRUE                                                05940020
           WHEN PV-DMA-NBR = PL-TBL-DMA-NBR                             05950020
                PERFORM 2100-COMPARE-FIELDS                             05960020
                PERFORM 7000-READ-DMA-FILE                              05961020
                PERFORM 7200-READ-DMA-TABLE-FILE                        05962020
           WHEN PV-DMA-NBR < PL-TBL-DMA-NBR                             05963020
                PERFORM 2300-INSERT-RECORDS                             05964020
                PERFORM 7000-READ-DMA-FILE                              05965020
           WHEN PV-DMA-NBR > PL-TBL-DMA-NBR                             05980020
                PERFORM 2200-DELETE-RECORDS                             05990020
                PERFORM 7200-READ-DMA-TABLE-FILE                        05990120
           END-EVALUATE.                                                05994020
                                                                        05996112
                                                                        05997007
      *----------------------------------------------------------------*06450002
      * 2100-COMPARE-FIELDS.                                            06460002
      *     SINCE THE KEY FIELD MATCHED, EXAMINE THE REMAINING          06470002
      *     FIELDS TO DETERMINE IF ANY OTHER FIELD DOES NOT MATCH.      06480002
      *----------------------------------------------------------------*06490002
       2100-COMPARE-FIELDS.                                             06500002
                                                                        06510002
           IF  PV-GMA-NBR       = PL-TBL-GMA-NBR     AND                06540020
               HM007-DMA-NM     = PL-TBL-DMA-NM      AND                06550020
               HM007-DMM-USR-ID = PL-TBL-DMA-USR-ID                     06570020
               CONTINUE                                                 06580002
           ELSE                                                         06590002
               PERFORM 2110-UPDATE-RECORDS                              06600002
           END-IF.                                                      06620002
                                                                        06630002
                                                                        07150002
      *----------------------------------------------------------------*07160002
      * 2110-UPDATE-RECORDS.                                            07170002
      *     UPDATE THE MODIFIED FIELDS.                                 07180002
      *----------------------------------------------------------------*07190002
       2110-UPDATE-RECORDS.                                             07200002
                                                                        07210002
           MOVE PV-DMA-NBR                  TO XST00067-DMA-NBR         07220019
           MOVE PV-GMA-NBR                  TO XST00067-GMA-NBR         07230013
           MOVE HM007-DMA-NM                TO XST00067-DMA-NM          07250013
           MOVE HM007-DMM-USR-ID            TO XST00067-DMA-USR-ID      07270013
                                                                        07280002
             EXEC SQL                                                   07281002
               UPDATE XST_DMA                                           07282002
               SET GMA_NBR    = :XST00067-GMA-NBR,                      07284022
                   DMA_NM     = :XST00067-DMA-NM,                       07286006
                   DMA_USR_ID = :XST00067-DMA-USR-ID                    07287002
                 WHERE                                                  07287122
                   DMA_NBR    = :XST00067-DMA-NBR                       07287223
             END-EXEC                                                   07288002
                                                                        07300002
           MOVE SQLCODE                     TO PV-SQL-CODE              07301013
           EVALUATE TRUE                                                07302002
              WHEN SQLCODE = 0                                          07302120
                ADD 1                       TO PA-UPDATE-DMA-COUNT      07302220
              WHEN SQLCODE = -904                                       07303002
              WHEN SQLCODE = -911                                       07304002
              WHEN SQLCODE = -913                                       07305002
                CONTINUE                                                07306002
              WHEN OTHER                                                07309102
                SET PS-PROCESS-COMPLETE     TO TRUE                     07309213
                MOVE '2110-UPDATE-RECORDS'  TO PV-PARAGRAPH             07309313
                MOVE 'BAD SQLCODE FOR UPDATE XST_DMA'                   07309413
                                            TO PV-MESSAGE               07309513
                PERFORM 8900-PROCESS-DB2-ERROR                          07309613
           END-EVALUATE.                                                07309702
                                                                        07310002
      *----------------------------------------------------------------*07311002
      * 2200-DELETE-RECORDS.                                            07312002
      *     DELETE THE RECORDS WHICH ARE NOT PRESENT IN THE INPUT FILE  07313002
      *----------------------------------------------------------------*07314002
       2200-DELETE-RECORDS.                                             07315002
                                                                        07319202
             MOVE PL-TBL-DMA-NBR TO XST00067-DMA-NBR                    07319320
                                                                        07319419
             EXEC SQL                                                   07319502
               DELETE FROM XST_DMA                                      07319602
               WHERE DMA_NBR    = :XST00067-DMA-NBR                     07319719
             END-EXEC                                                   07319902
                                                                        07320002
           MOVE SQLCODE                     TO PV-SQL-CODE              07320113
           EVALUATE TRUE                                                07320202
              WHEN SQLCODE = 0                                          07320320
                ADD 1                       TO PA-DELETE-DMA-COUNT      07320420
              WHEN SQLCODE = -904                                       07320502
              WHEN SQLCODE = -911                                       07320602
              WHEN SQLCODE = -913                                       07320702
                CONTINUE                                                07320802
              WHEN OTHER                                                07321002
                SET PS-PROCESS-COMPLETE     TO TRUE                     07321113
                MOVE '2200-DELETE-RECORDS'  TO PV-PARAGRAPH             07321213
                MOVE 'BAD SQLCODE FOR DELETE XST_DMA'                   07321313
                                            TO PV-MESSAGE               07321413
                PERFORM 8900-PROCESS-DB2-ERROR                          07321513
           END-EVALUATE.                                                07321602
                                                                        07321702
      *----------------------------------------------------------------*07321802
      * 2300-INSERT-RECORDS.                                            07321902
      *     INSERT THE RECORD WHEN THE INPUT FILE HAS THAT RECORD       07322002
      *----------------------------------------------------------------*07322102
       2300-INSERT-RECORDS.                                             07322203
                                                                        07322302
           MOVE PV-DMA-NBR                 TO XST00067-DMA-NBR          07322419
           MOVE PV-GMA-NBR                 TO XST00067-GMA-NBR          07322513
           MOVE HM007-DMA-NM               TO XST00067-DMA-NM           07322613
           MOVE HM007-DMM-USR-ID           TO XST00067-DMA-USR-ID       07322713
                                                                        07322802
             EXEC SQL                                                   07322902
               INSERT INTO XST_DMA                                      07323003
                  (DMA_NBR                                              07323103
                  ,GMA_NBR                                              07323203
                  ,DMA_NM                                               07323303
                  ,DMA_USR_ID)                                          07323403
                VALUES                                                  07323503
                  (:XST00067-DMA-NBR                                    07323619
                  ,:XST00067-GMA-NBR                                    07323703
                  ,:XST00067-DMA-NM                                     07323803
                  ,:XST00067-DMA-USR-ID)                                07323920
             END-EXEC                                                   07324002
                                                                        07324102
           MOVE SQLCODE                    TO PV-SQL-CODE               07324213
           EVALUATE TRUE                                                07324302
              WHEN SQLCODE = 0                                          07324420
                ADD 1                      TO PA-INSERT-DMA-COUNT       07324520
              WHEN SQLCODE = -904                                       07324602
              WHEN SQLCODE = -911                                       07324702
              WHEN SQLCODE = -913                                       07324802
                CONTINUE                                                07324902
              WHEN OTHER                                                07325102
                SET PS-PROCESS-COMPLETE    TO TRUE                      07325213
                MOVE '2300-INSERT-RECORDS' TO PV-PARAGRAPH              07325303
                MOVE 'BAD SQLCODE FOR INSERT XST_DMA'                   07325413
                                           TO PV-MESSAGE                07325513
                PERFORM 8900-PROCESS-DB2-ERROR                          07325613
           END-EVALUATE.                                                07325702
                                                                        07325802
      *----------------------------------------------------------------*07325903
      * 3000-PROCESS-EXTRA-RECORDS.                                     07326003
      * PROCESS RECORDS UNTIL END OF RECORDS IN BOTH INPUT AND TABLE    07326103
      *----------------------------------------------------------------*07326203
       3000-PROCESS-EXTRA-RECORDS.                                      07326303
                                                                        07326403
           MOVE HM007-DMA-NBR TO PV-DMA-NBR                             07326507
           MOVE HM007-GMA-NBR TO PV-GMA-NBR                             07326607
                                                                        07326707
           IF PS-NOT-END-OF-DMA-FILE THEN                               07326803
              PERFORM 2300-INSERT-RECORDS                               07326903
              PERFORM 7000-READ-DMA-FILE                                07327020
           END-IF                                                       07327120
                                                                        07327220
           IF PS-NOT-END-OF-DMA-TABLE-FILE THEN                         07327320
              PERFORM 2200-DELETE-RECORDS                               07327420
              PERFORM 7200-READ-DMA-TABLE-FILE                          07327520
           END-IF.                                                      07327620
                                                                        07327703
      *----------------------------------------------------------------*07327813
      * 7000-READ-DMA-FILE.                                             07327913
      *     READS THE INPUT DMA FILE.                                   07328013
      *----------------------------------------------------------------*07328113
       7000-READ-DMA-FILE.                                              07328213
                                                                        07328313
            READ DMA-FILE INTO HMRD007-RECORD                           07328413
              AT END                                                    07328513
                 SET PS-END-OF-DMA-FILE TO TRUE                         07328613
            END-READ.                                                   07328713
                                                                        07328813
            IF NOT PS-END-OF-DMA-FILE                                   07328913
                 ADD 1                  TO PA-DMAFILE-COUNT             07329015
            END-IF.                                                     07329113
                                                                        07329213
      *----------------------------------------------------------------*07329313
      * 7200-READ-DMA-TABLE-FILE.                                       07329419
      *     READS THE INPUT DMA TABLE FILE.                             07329519
      *----------------------------------------------------------------*07329613
       7200-READ-DMA-TABLE-FILE.                                        07329719
                                                                        07329813
            READ DMA-TABLE-FILE INTO PL-TBL-INPUT-RECORD                07329920
              AT END                                                    07330019
                 SET PS-END-OF-DMA-TABLE-FILE TO TRUE                   07330119
            END-READ.                                                   07330219
                                                                        07330319
            IF NOT PS-END-OF-DMA-TABLE-FILE                             07330419
                 ADD 1                        TO PA-DMATAB-FILE-COUNT   07330519
            END-IF.                                                     07330619
                                                                        07330726
      *----------------------------------------------------------------*07330826
       8900-PROCESS-DB2-ERROR.                                          07337113
      *----------------------------------------------------------------*07337226
           DISPLAY '***************************************************'07337407
           DISPLAY '**  ERROR     **'.                                  07337507
           DISPLAY '**  SQLCODE   **  = ' PV-SQL-CODE.                  07337607
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              07337707
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH.                 07337807
           DISPLAY '**  MESSAGE   **  = ' PV-MESSAGE.                   07337907
                                                                        07338004
      *ROUTINE TO CONVERT THE SQLCA INTO A READABLE FORMAT, USING       07338104
      *THE CALLED MODULE DSNTIAR.                                       07338204
           COPY DPPD004.                                                07338305
                                                                        07338404
           MOVE +4013 TO ABEND-CODE.                                    07338507
           CALL 'ILBOABN0' USING ABEND-CODE.                            07338607
                                                                        07338704
      *----------------------------------------------------------------*07339003
      * 9000-EOJ-ROUTINE.                                               07340013
      *----------------------------------------------------------------*07350003
       9000-EOJ-ROUTINE.                                                07360013
                                                                        07370003
           DISPLAY '***************************************************'07380003
           DISPLAY 'NUMBER OF RECS READ IN DMA FILE: ' PA-DMAFILE-COUNT.07390019
           DISPLAY 'NUMBER OF RECS READ FROM TABLE: '                   07391019
                                                PA-DMATAB-FILE-COUNT    07392019
           DISPLAY 'NUMBER OF RECS UPDATED: ' PA-UPDATE-DMA-COUNT.      07400019
           DISPLAY 'NUMBER OF RECS INSERTED: ' PA-INSERT-DMA-COUNT.     07410019
           DISPLAY 'NUMBER OF RECS DELETED: ' PA-DELETE-DMA-COUNT.      07420019
           DISPLAY '***************************************************'07430003
                                                                        07431017
           CLOSE DMA-FILE                                               07440020
                 DMA-TABLE-FILE.                                        07450020
                                                                        07460020
       9999-PROCESS-ABEND.                                              07470020
                                                                        07480020
           MOVE '1000-INITIALIZE'     TO PV-PARAGRAPH-NAME              07500020
           MOVE 'INPUT FILE CANNOT BE EMPTY'                            07502020
                                      TO PV-OPERATION-MSG               07503020
           DISPLAY '                     '.                             07510020
           DISPLAY '** ABEND           **'.                             07520020
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          07530020
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07540020
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         07550020
                                                                        07570020
           CALL 'ILBOABN0' USING ABEND-CODE.                            07580020
                                                                        07590020
