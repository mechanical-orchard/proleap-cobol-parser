       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT145.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  04/30/2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM REFORMATS THE STORE/DC RELATIONSHIP FILE INTO A  *        
      *  FORMAT THAT CAN BE USED BY THE APPLICATION INTEGRATOR MAP.    *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 30 CHARACTERS                                        
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD             PIC X(30).                                  
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS ET032-RELATIONSHIP-RECORD.                            
                                                                                
           COPY ETRD032.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-HL-LEVEL                    PIC 9(5)    VALUE ZERO.               
       01  WS-PREV-DC-LEVEL               PIC 9(5)    VALUE ZERO.               
                                                                                
       01  WS-STORE-COUNT                 PIC 9(5)    VALUE ZERO.               
                                                                                
       01  WS-DATE                        PIC X(8).                             
       01  FILLER REDEFINES WS-DATE.                                            
           05  WS-DATE-YEAR               PIC X(4).                             
           05  WS-DATE-MONTH              PIC X(2).                             
           05  WS-DATE-DAY                PIC X(2).                             
                                                                                
       01  WS-CURRENT-DATE                PIC X(6).                             
                                                                                
       01  INPUT-ADDRESS-RECORD.                                                
           05  INP-TYPE-CODE              PIC X(2).                             
           05  INP-STORE-NBR              PIC X(5).                             
           05  INP-EFFECTIVE-DATE         PIC X(10).                            
           05  FILLER REDEFINES INP-EFFECTIVE-DATE.                             
               10  INP-EFFECTIVE-YEAR     PIC X(4).                             
               10  FILLER                 PIC X(1).                             
               10  INP-EFFECTIVE-MONTH    PIC X(2).                             
               10  FILLER                 PIC X(1).                             
               10  INP-EFFECTIVE-DAY      PIC X(2).                             
                                                                                
       01  END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE                      VALUE 'Y'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE.                                    
                                                                                
           PERFORM 6000-READ-INPUT-FILE                                         
              THRU 6000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-REFORMAT-FILE                                           
              THRU 1000-REFORMAT-FILE-EXIT                                      
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  BEFORE THE FIRST RECORD IS PROCESSED, CREATE HEADER LEVEL     *        
      *  INFORMATION, WHICH CONSISTS OF:                               *        
      *    1.  RECORD TYPE 01 - HEADER LEVEL BHT & N1 SEGMENTS         *        
      *    2.  RECORD TYPE 02 - FIRST HL SEGMENT                       *        
      *    2.  RECORD TYPE 03 - FIRST N1 SEGMENT                       *        
      *                                                                *        
      *  FOR EVERY RECORD ON THE INPUT FILE, CREATE THE FOLLOWING:     *        
      *    1.  RECORD TYPE 02 - HL SEGMENT - IF THE RECORD TYPE IS     *        
      *        'DC' OR THE PREVIOUS RECORD TYPE WAS 'DC' (THE FIRST    *        
      *        STORE LISTED WITHIN A DC).                              *        
      *    2.  RECORD TYPE 03 - N1 SEGMENT                             *        
      *    3.  RECORD TYPE 06 - DTM SEGMENT - CREATE IF THERE IS AN    *        
      *        EFFECTIVE DATE                                          *        
      ******************************************************************        
       1000-REFORMAT-FILE.                                                      
                                                                                
           IF WS-HL-LEVEL = ZERO                                                
              PERFORM 2000-CREATE-RECORD-01                                     
                 THRU 2000-CREATE-RECORD-01-EXIT                                
              PERFORM 2500-FIRST-HL-LEVEL                                       
                 THRU 2500-FIRST-HL-LEVEL-EXIT.                                 
                                                                                
           IF INP-TYPE-CODE = 'DC' OR WS-STORE-COUNT = ZERO                     
              PERFORM 3000-CREATE-RECORD-02                                     
                 THRU 3000-CREATE-RECORD-02-EXIT.                               
                                                                                
           PERFORM 4000-CREATE-RECORD-03                                        
              THRU 4000-CREATE-RECORD-03-EXIT.                                  
                                                                                
           IF INP-EFFECTIVE-DATE NOT = SPACES                                   
                 PERFORM 5000-CREATE-RECORD-06                                  
                    THRU 5000-CREATE-RECORD-06-EXIT.                            
                                                                                
           PERFORM 6000-READ-INPUT-FILE                                         
              THRU 6000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-REFORMAT-FILE-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2000-CREATE-RECORD-01.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '01'                     TO ET032-RECORD-TYPE-CODE.             
           MOVE SPACES                   TO ET032-HDR-ENT-ID.                   
           MOVE '0057'                   TO ET032-HDR-STRUCTURE-CODE.           
           MOVE '00'                     TO ET032-HDR-PURPOSE-CODE.             
           MOVE 'FR'                     TO ET032-HDR-ID-CODE.                  
           MOVE 'KOHLS DEPARTMENT STORE' TO ET032-HDR-NAME.                     
                                                                                
                                                                                
           STRING '20'            DELIMITED BY SIZE                             
                  WS-CURRENT-DATE DELIMITED BY SIZE                             
                    INTO ET032-HDR-DATE.                                        
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       2000-CREATE-RECORD-01-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       2500-FIRST-HL-LEVEL.                                                     
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           ADD 1 TO WS-HL-LEVEL.                                                
                                                                                
           MOVE '02'        TO ET032-RECORD-TYPE-CODE.                          
           MOVE WS-HL-LEVEL TO ET032-HL-ID-NUMBER.                              
           MOVE 0           TO ET032-HL-PARENT-ID.                              
           MOVE '24'        TO ET032-HL-LEVEL-CODE.                             
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '03'                     TO ET032-RECORD-TYPE-CODE.             
           MOVE 'CQ'                     TO ET032-N1-ID-CODE1.                  
           MOVE 'KOHLS DEPARTMENT STORE' TO ET032-N1-NAME.                      
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       2500-FIRST-HL-LEVEL-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       3000-CREATE-RECORD-02.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           ADD 1 TO WS-HL-LEVEL.                                                
                                                                                
           MOVE '02'        TO ET032-RECORD-TYPE-CODE.                          
           MOVE WS-HL-LEVEL TO ET032-HL-ID-NUMBER.                              
                                                                                
           IF INP-TYPE-CODE = 'DC'                                              
              MOVE 1                TO ET032-HL-PARENT-ID                       
              MOVE '25'             TO ET032-HL-LEVEL-CODE                      
              MOVE WS-HL-LEVEL      TO WS-PREV-DC-LEVEL                         
              MOVE ZERO             TO WS-STORE-COUNT                           
           ELSE                                                                 
              MOVE WS-PREV-DC-LEVEL TO ET032-HL-PARENT-ID                       
              MOVE '26'             TO ET032-HL-LEVEL-CODE                      
              ADD 1                 TO WS-STORE-COUNT.                          
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       3000-CREATE-RECORD-02-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       4000-CREATE-RECORD-03.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '03'           TO ET032-RECORD-TYPE-CODE.                       
           MOVE '93'           TO ET032-N1-ID-QUALIFIER.                        
           MOVE INP-STORE-NBR  TO ET032-N1-LOCATION.                            
                                                                                
           IF INP-TYPE-CODE = 'DC'                                              
              MOVE 'WH' TO ET032-N1-ID-CODE1                                    
           ELSE                                                                 
              MOVE 'SN' TO ET032-N1-ID-CODE1.                                   
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       4000-CREATE-RECORD-03-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       5000-CREATE-RECORD-06.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '06'  TO ET032-RECORD-TYPE-CODE.                                
           MOVE '007' TO ET032-DTM-QUALIFIER.                                   
                                                                                
           MOVE INP-EFFECTIVE-YEAR  TO WS-DATE-YEAR.                            
           MOVE INP-EFFECTIVE-MONTH TO WS-DATE-MONTH.                           
           MOVE INP-EFFECTIVE-DAY   TO WS-DATE-DAY.                             
           MOVE WS-DATE             TO ET032-DTM-DATE.                          
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       5000-CREATE-RECORD-06-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       6000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO INPUT-ADDRESS-RECORD                            
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE.                                 
                                                                                
       6000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
