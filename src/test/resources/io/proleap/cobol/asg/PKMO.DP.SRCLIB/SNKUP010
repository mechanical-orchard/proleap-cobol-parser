00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP010
00003  PROGRAM-ID.    SNKUP010.                                            LV003
00004                                                                   SNKUP010
00005  AUTHOR.        ANNE YEKO.                                        SNKUP010
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP010
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP010
00008  DATE-COMPILED.                                                   SNKUP010
00009                                                                   SNKUP010
00010 ******************************************************************SNKUP010
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP010
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP010
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASDTPL TABLE.          **SNKUP010
00014 ******************************************************************SNKUP010
00015 * CHANGED 04/14/22 BY JEAN KURK                                 **SNKUP010
00016 *   CHANGE THE HARDCODED COMMIT COUNT FROM 500 TO 250 TO        **SNKUP010
00017 *   DECREASE THE # OF ROWS HELD AT A TIME     CHG0268937        **SNKUP010
00019 ******************************************************************SNKUP010
COMMIT* CHANGED 06/22/22 BY JIM HUNTER           CHG0271378           **SNKUP010
COMMIT*   THIS PROGRAM WAS CAUSING WARNINGS TO BE WRITTEN TO THE DB2  **SNKUP010
COMMIT*   LOGS BECAUSE EXCESSIVE UPDATES WERE BEING PERFORMED         **SNKUP010
COMMIT*   WITHOUT COMMITS.                                            **SNKUP010
COMMIT*   CHANGE THE HARDCODED COMMIT COUNT FROM 250 TO 100 PER TESTS **SNKUP010
COMMIT*   THAT SHOWED IT AVOIDS THE UNCOMMITTED DB2 LOG WARNINGS.     **SNKUP010
COMMIT*   ALSO ADDED A COMMIT COUNTER AND DISPLAY FOR MONITORING.     **SNKUP010
COMMIT******************************************************************SNKUP010
COMMI2* CHANGED 08/11/22 BY JIM HUNTER           CHG0273177           **SNKUP010
COMMI2*   CHANGE THE HARDCODED COMMIT COUNT FROM 100 TO 50 PER DBA.   **SNKUP010
COMMI2******************************************************************SNKUP010
00020                                                                   SNKUP010
00021      EJECT                                                        SNKUP010
00022                                                                   SNKUP010
00023  ENVIRONMENT DIVISION.                                            SNKUP010
00024                                                                   SNKUP010
00025  INPUT-OUTPUT SECTION.                                            SNKUP010
00026                                                                   SNKUP010
00027  FILE-CONTROL.                                                    SNKUP010
00028                                                                   SNKUP010
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP010
00030             ASSIGN TO INP01.                                      SNKUP010
00031                                                                   SNKUP010
00032  DATA DIVISION.                                                   SNKUP010
00033                                                                   SNKUP010
00034  FILE SECTION.                                                    SNKUP010
00035                                                                   SNKUP010
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP010
00037      RECORDING MODE IS F                                          SNKUP010
00038      LABEL RECORDS ARE STANDARD                                   SNKUP010
00039      RECORD CONTAINS 5  CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP010
00041                                                                   SNKUP010
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP010
00044      EJECT                                                        SNKUP010
00045  WORKING-STORAGE SECTION.                                         SNKUP010
00046                                                                   SNKUP010
00047  01  FILLER.                                                      SNKUP010
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP010
00049          ' ***** WORKING STORAGE PGM=SNKUP010 '.                  SNKUP010
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP010
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP010
00052          ' LOCATION='.                                            SNKUP010
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP010
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP010
00055          ' *****'.                                                SNKUP010
00056                                                                   SNKUP010
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP010
00058                                              COMP SYNC.           SNKUP010
00059                                                                   SNKUP010
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP010
00061                                                                   SNKUP010
00062      EJECT                                                        SNKUP010
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP010
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP010
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP010
00066                                                                   SNKUP010
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP010
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP010
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP010
00070                                                                   SNKUP010
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP010
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP010
00073                                              COMP-3.              SNKUP010
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP010
00075                                              COMP-3.              SNKUP010
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0                CL**2
00077                                              COMP-3.              SNKUP010
COMMIT     05  PV-COMMIT-COUNTER       PIC  S9(7)  VALUE +0                CL**2
COMMIT                                             COMP-3.              SNKUP010
00078                                                                   SNKUP010
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP010
00080                                                                   SNKUP010
00081      COPY SNRD002I.                                               SNKUP010
00082      EJECT                                                        SNKUP010
00083 ******************************************************************SNKUP010
00084 *    DB2 DATABASE AREA                                            SNKUP010
00085 ******************************************************************SNKUP010
00086                                                                   SNKUP010
00087 *    ASN DETAIL TO PO CROSS REFERENCE TABLE                       SNKUP010
00088                                                                   SNKUP010
00089      EXEC SQL                                                     SNKUP010
00090          INCLUDE TASDTPL                                          SNKUP010
00091      END-EXEC.                                                    SNKUP010
00092                                                                   SNKUP010
00093      EJECT                                                        SNKUP010
00094 *    SQL COMMUNICATION AREA                                       SNKUP010
00095                                                                   SNKUP010
00096      EXEC SQL                                                     SNKUP010
00097          INCLUDE SQLCA                                            SNKUP010
00098      END-EXEC.                                                    SNKUP010
00099      EJECT                                                        SNKUP010
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP010
00101                                                                   SNKUP010
00102      COPY SQLCA2.                                                 SNKUP010
00103      EJECT                                                        SNKUP010
00104                                                                   SNKUP010
00105 ***************************************************************** SNKUP010
00106  PROCEDURE DIVISION.                                              SNKUP010
00107 ***************************************************************** SNKUP010
00108                                                                   SNKUP010
00109  A000-MAIN-DRIVER.                                                SNKUP010
00110                                                                   SNKUP010
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP010
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP010
00113                                                                   SNKUP010
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP010
00115                                                                   SNKUP010
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP010
00117                                                                   SNKUP010
00118      PERFORM B100-DELETE-ROWS                                     SNKUP010
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP010
00120                                                                   SNKUP010
00121      DISPLAY 'SNKUP010 RECORDS READ:    ' PV-RECS-READ.           SNKUP010
00122      DISPLAY 'SNKUP010 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP010
COMMIT     DISPLAY 'SNKUP010 COMMITS DONE:    ' PV-COMMIT-COUNTER.      SNKUP010
00123                                                                   SNKUP010
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP010
00125                                                                   SNKUP010
00126      GOBACK.                                                      SNKUP010
00127                                                                   SNKUP010
00128      EJECT                                                        SNKUP010
00129                                                                   SNKUP010
00130 ***************************************************************** SNKUP010
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP010
00132 ***************************************************************** SNKUP010
00133  B100-DELETE-ROWS.                                                SNKUP010
00134                                                                   SNKUP010
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP010
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP010
00137                                  TO ABEND-PARAGRAPH.              SNKUP010
00138                                                                   SNKUP010
00139      PERFORM B300-DELETE-ASDTPL.                                  SNKUP010
COMMIT**   IF PV-DELETE-COUNTER > +100                                     CL**2
COMMI2     IF PV-DELETE-COUNTER > +50                                      CL**2
00141          PERFORM B400-COMMIT                                      SNKUP010
00142          MOVE ZERO               TO PV-DELETE-COUNTER                CL**2
00143      END-IF.                                                      SNKUP010
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP010
00145                                                                   SNKUP010
00146      EJECT                                                        SNKUP010
00147                                                                   SNKUP010
00148 ***************************************************************** SNKUP010
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP010
00150 ***************************************************************** SNKUP010
00151  B200-READ-INPUT-FILE.                                            SNKUP010
00152                                                                   SNKUP010
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP010
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP010
00155                                  TO ABEND-PARAGRAPH.              SNKUP010
00156                                                                   SNKUP010
00157      READ ASNHDR-DELETE-FILE                                      SNKUP010
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP010
00159          AT END                                                   SNKUP010
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP010
00161                                                                   SNKUP010
00162      IF NOT PS-END-OF-INPUT                                       SNKUP010
00163          ADD 1                   TO PV-RECS-READ                  SNKUP010
00164      END-IF.                                                      SNKUP010
00165                                                                   SNKUP010
00166      EJECT                                                        SNKUP010
00167                                                                   SNKUP010
00168 ***************************************************************** SNKUP010
00169 *    THIS PARAGRAPH DELETES ALL ASDTPL ROWS THAT ARE EQUAL TO     SNKUP010
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP010
00171 ***************************************************************** SNKUP010
00172  B300-DELETE-ASDTPL.                                              SNKUP010
00173                                                                   SNKUP010
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP010
00175      MOVE 'B300-DELETE-ASDTPL        '                            SNKUP010
00176                                  TO ABEND-PARAGRAPH.              SNKUP010
00177                                                                   SNKUP010
00178      EXEC SQL                                                     SNKUP010
00179          DELETE                                                   SNKUP010
00180          FROM   TASDTPL                                                   
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP010
00183                                                                   SNKUP010
00184      IF SQLCODE = ZERO                                            SNKUP010
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP010
00186          ADD +1                  TO PV-DELETE-COUNTER                CL**2
00187      ELSE                                                         SNKUP010
00188          IF SQLCODE = +100                                        SNKUP010
00189              CONTINUE                                             SNKUP010
00190          ELSE                                                     SNKUP010
00191              PERFORM Z998-SQL-ERROR                               SNKUP010
00192          END-IF                                                   SNKUP010
00193      END-IF.                                                      SNKUP010
00194                                                                   SNKUP010
00195      EJECT                                                        SNKUP010
00196                                                                   SNKUP010
00197 ***************************************************************** SNKUP010
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP010
00199 ***************************************************************** SNKUP010
00200  B400-COMMIT.                                                     SNKUP010
00201                                                                   SNKUP010
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP010
00203      MOVE 'B400-COMMIT               '                            SNKUP010
00204                                  TO ABEND-PARAGRAPH.              SNKUP010
00205                                                                   SNKUP010
00206      EXEC SQL                                                     SNKUP010
00207          COMMIT                                                   SNKUP010
00208      END-EXEC.                                                    SNKUP010
00209                                                                   SNKUP010
00210      IF SQLCODE = ZERO                                            SNKUP010
COMMIT         ADD +1                  TO PV-COMMIT-COUNTER                CL**2
00212      ELSE                                                         SNKUP010
00213          PERFORM Z998-SQL-ERROR                                   SNKUP010
00214      END-IF.                                                      SNKUP010
00215                                                                   SNKUP010
00216      EJECT                                                        SNKUP010
00217                                                                   SNKUP010
00218 ***************************************************************** SNKUP010
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP010
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP010
00221 ***************************************************************** SNKUP010
00222  Z998-SQL-ERROR.                                                  SNKUP010
00223                                                                   SNKUP010
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP010
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP010
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP010
00227      DISPLAY '** PROGRAM          ** = SNKUP010'.                 SNKUP010
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP010
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP010
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP010
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP010
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP010
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP010
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP010
