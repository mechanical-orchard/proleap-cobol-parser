      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             TFKRP470.                                00050000
       AUTHOR.                 JACK KRAMER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            APRIL 1997.                              00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *   THIS PROGRAM PRODUCES A TRANSFER REPORT BY TYPE SHOWING TOTAL 00120000
      *  TRANSFERED UNITS AND DOLLARS FOR PERIOD TO DATE AND YEAR TO    00130000
      *  DATE THIS YEAR VERSUS LAST YEAR; SORTED BY STORE WITHIN        00130100
      *  DISTRICT WITHIN TRANSFER TYPE. TOTALS ARE PRINTED FOR DISTRICT 00130200
      *  AND COMPANY FOR EACH TRANSFER TYPE.                            00130300
      ******************************************************************00130400
100404** 10/04/2004 CHANGED BY: RAY GRAF                                00130500
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                00130600
      ***************************************************************** 00130700
      ******************************************************************00130801
120108** 12/01/2008 CHANGED BY: TOM HANSON FOR WR35615                  00130901
120108**     EXPAND DISTRICT NUMBER FROM 2 TO 4 DIGITS.                 00131001
      ***************************************************************** 00131101
                                                                        00131200
      ******************************************************************00131300
       ENVIRONMENT DIVISION.                                            00131400
      ******************************************************************00132000
                                                                        00133000
       CONFIGURATION SECTION.                                           00134000
                                                                        00135000
       SOURCE-COMPUTER.        IBM-3090.                                00136000
       OBJECT-COMPUTER.        IBM-3090.                                00137000
                                                                        00138000
       INPUT-OUTPUT SECTION.                                            00139000
                                                                        00140000
       FILE-CONTROL.                                                    00150000
                                                                        00160000
           SELECT TRANSFER-REPORT-FILE                                  00170000
               ASSIGN TO RPT01.                                         00180000
                                                                        00190000
           SELECT PROCESS-DATES-FILE                                    00200000
               ASSIGN TO INP01.                                         00210000
                                                                        00220000
           SELECT TRANSFERS-BY-TYPE-FILE                                00230000
               ASSIGN TO INP02.                                         00240000
                                                                        00250000
           SELECT TRANSFERS-SUMMARY-FILE                                00260000
               ASSIGN TO INP03.                                         00270000
      *                                                                 00280000
      ******************************************************************00290000
       DATA DIVISION.                                                   00300000
      ******************************************************************00310000
                                                                        00320000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  TRANSFER-REPORT-FILE                                         00350000
           LABEL RECORDS ARE STANDARD                                   00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           RECORD CONTAINS 132 CHARACTERS                               00390000
           DATA RECORD IS TRANSFER-REPORT-RECORD.                       00400000
                                                                        00410000
       01  TRANSFER-REPORT-RECORD PIC X(132).                           00420000
                                                                        00430000
       FD  PROCESS-DATES-FILE                                           00440000
           LABEL RECORDS ARE STANDARD                                   00450000
           RECORDING MODE IS F                                          00450100
           BLOCK CONTAINS 0 RECORDS                                     00450200
           RECORD CONTAINS 16 CHARACTERS.                               00450300
                                                                        00450400
       01  PROCESS-DATES-RECORD            PIC X(16).                   00450500
                                                                        00450600
       FD  TRANSFERS-BY-TYPE-FILE                                       00450700
           LABEL RECORDS ARE STANDARD                                   00450800
           RECORDING MODE IS F                                          00450900
           BLOCK CONTAINS 0 RECORDS                                     00451000
           RECORD CONTAINS 30 CHARACTERS                                00451100
           DATA RECORD IS TRANSFERS-BY-TYPE-RECORD.                     00451200
                                                                        00451300
       01  TRANSFERS-BY-TYPE-RECORD PIC X(30).                          00451400
                                                                        00451500
       FD  TRANSFERS-SUMMARY-FILE                                       00451600
           LABEL RECORDS ARE STANDARD                                   00451700
           RECORDING MODE IS F                                          00451800
           BLOCK CONTAINS 0 RECORDS                                     00451900
           RECORD CONTAINS 30 CHARACTERS                                00452000
           DATA RECORD IS TRANSFERS-SUMMARY-RECORD.                     00453000
                                                                        00454000
       01  TRANSFERS-SUMMARY-RECORD PIC X(30).                          00455000
      *                                                                 00456000
       WORKING-STORAGE SECTION.                                         00457000
                                                                        00458000
      *                                                                 00459000
      *  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT                 00460000
           COPY DPWS132O.                                               00470000
      *                                                                 00480000
      *----------------------------------------------------------------*00490000
      *  DATE VALIDATION CALLING PARAMETER LIST                        *00500000
      *----------------------------------------------------------------*00510000
           COPY DPWS500.                                                00520000
      *----------------------------------------------------------------*00530000
      *  RECORD LAYOUT FOR THE PROCESS DATES FILE                       00540000
      *----------------------------------------------------------------*00550000
           COPY TFRD029.                                                00560000
      *----------------------------------------------------------------*00561000
      *  RECORD LAYOUT FOR THE TRANSFERS BY TYPE FILE                   00562000
      *----------------------------------------------------------------*00563000
           COPY TFRD036.                                                00564000
           EJECT                                                        00565000
      *                                                                 00566000
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          00567000
                                       COMP SYNC.                       00568000
                                                                        00569000
       01  PS-PROGRAM-SWITCHES.                                         00570000
           05  PS-FIRST-TIME-SW        PIC X(01)    VALUE 'Y'.          00580000
               88  PS-FIRST-TIME                    VALUE 'Y'.          00590000
               88  PS-NOT-FIRST-TIME                VALUE 'N'.          00600000
           05  PS-ITEMS-READ-SW        PIC  X(01)   VALUE 'N'.          00610000
               88  PS-ITEMS-READ                    VALUE 'Y'.          00620000
               88  PS-NO-ITEMS-READ                 VALUE 'N'.          00630000
           05  PS-END-OF-DATA-SW       PIC  X(01)   VALUE 'N'.          00640000
               88  PS-END-OF-DATA                   VALUE 'Y'.          00650000
               88  PS-NOT-END-OF-DATA               VALUE 'N'.          00660000
                                                                        00670000
       01  PC-PROGRAM-CONSTANTS.                                        00680000
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'TFKRP470'.   00690000
           05  PC-CALENDAR-PGM           PIC  X(08)  VALUE  'DPKUT500'. 00700000
           05  PC-TITLE-LITERAL          PIC  X(17)  VALUE              00710000
                                         ' TRANSFER REPORT*'.           00720000
           05  PC-DIST-LITERAL           PIC  X(04)  VALUE 'DIST'.      00730000
           05  PC-TOTAL-LITERAL          PIC  X(05)  VALUE 'TOTAL'.     00740000
           05  PC-COMPANY-LITERAL        PIC  X(07)  VALUE 'COMPANY'.   00750000
           05  PC-GRAND-LITERAL          PIC  X(07)  VALUE 'GRAND'.     00760000
           05  PC-COMP-LITERAL           PIC  X(05)  VALUE ' COMP'.     00770000
           05  PC-ALL-TYPES-LITERAL.                                    00780000
               10                        PIC  X(15)  VALUE SPACE.       00790000
               10                        PIC  X(20)  VALUE              00800000
                'ALL TRANSFER TYPES'.                                   00810000
           05  PC-PAGE-MAX               PIC  S9(03) VALUE  +61.        00820000
           05  PC-1-DAY                  PIC  S9(03) VALUE  +1  COMP-3. 00830000
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3      00840000
                                                           COMP-3.      00850000
                                                                        00860000
       01  PV-PROGRAM-VARIABLES.                                        00870000
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          00880000
                                       COMP SYNC.                       00890000
           05  PV-PAGE-COUNT           PIC 9(5)    VALUE ZERO.          00900000
           05  PV-LINE-COUNT           PIC S9(4)   VALUE +90.           00910000
           05  PV-DATE.                                                 00920000
               10  PV-MONTH            PIC 9(02).                       00930000
               10  FILLER              PIC X(01)   VALUE '/'.           00940000
               10  PV-DAY              PIC 9(02).                       00950000
               10  FILLER              PIC X(01)   VALUE '/'.           00960000
               10  PV-CENTURY          PIC 9(02).                       00970000
               10  PV-YEAR             PIC 9(02).                       00980000
           05  PV-POINTER              PIC S9(03)  VALUE +0 COMP-3.     00990000
           05  PV-CHARACTER-COUNT      PIC S9(03)  VALUE +0 COMP-3.     01000000
           05  PV-PARAGRAPH-NAME       PIC X(30)   VALUE SPACE.         01010000
           05  PV-DB2-OPERATION        PIC X(30)   VALUE SPACE.         01020000
           05  PV-TYPE-DESC            PIC X(50)   VALUE SPACE.         01030000
           05  PV-VARIANCE            PIC S9(05)V999 VALUE ZERO COMP-3. 01040000
       01  PA-PROGRAM-ACCUMULATORS.                                     01050000
           05  PA-PTD-LY-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01060000
           05  PA-PTD-LY-RTL           PIC S9(09)V99 VALUE ZERO COMP-3. 01070000
           05  PA-YTD-LY-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01080000
           05  PA-YTD-LY-RTL           PIC S9(09)V99 VALUE ZERO COMP-3. 01090000
           05  PA-PTD-TY-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01100000
           05  PA-PTD-TY-RTL           PIC S9(09)V99 VALUE ZERO COMP-3. 01100100
           05  PA-YTD-TY-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01100200
           05  PA-YTD-TY-RTL           PIC S9(09)V99 VALUE ZERO COMP-3. 01100300
           05  PA-PTD-LY-DS-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01100400
           05  PA-PTD-LY-DS-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01100500
           05  PA-YTD-LY-DS-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01100600
           05  PA-YTD-LY-DS-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01100700
           05  PA-PTD-TY-DS-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01100800
           05  PA-PTD-TY-DS-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01100900
           05  PA-YTD-TY-DS-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01101000
           05  PA-YTD-TY-DS-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01101100
           05  PA-PTD-LY-CO-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01101200
           05  PA-PTD-LY-CO-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01101300
           05  PA-YTD-LY-CO-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01101400
           05  PA-YTD-LY-CO-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01101500
           05  PA-PTD-TY-CO-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01101600
           05  PA-PTD-TY-CO-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01101700
           05  PA-YTD-TY-CO-QTY        PIC S9(08)    VALUE ZERO COMP-3. 01101800
           05  PA-YTD-TY-CO-RTL        PIC S9(09)V99 VALUE ZERO COMP-3. 01101900
           05  PA-PTD-LY-DS-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01102000
           05  PA-PTD-LY-DS-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01103000
           05  PA-YTD-LY-DS-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01103100
           05  PA-YTD-LY-DS-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01103200
           05  PA-PTD-TY-DS-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01103300
           05  PA-PTD-TY-DS-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01103400
           05  PA-YTD-TY-DS-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01103500
           05  PA-YTD-TY-DS-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01103600
           05  PA-PTD-LY-CO-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01103700
           05  PA-PTD-LY-CO-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01103800
           05  PA-YTD-LY-CO-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01103900
           05  PA-YTD-LY-CO-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01104000
           05  PA-PTD-TY-CO-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01104100
           05  PA-PTD-TY-CO-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01104200
           05  PA-YTD-TY-CO-COMP-QTY   PIC S9(08)    VALUE ZERO COMP-3. 01104300
           05  PA-YTD-TY-CO-COMP-RTL   PIC S9(09)V99 VALUE ZERO COMP-3. 01104400
       01  HV-HOLD-VARIABLES.                                           01104500
120108     05  HV-DISTRICT-NMBR        PIC 9(04)    VALUE ZERO.         01104602
100404*    05  HV-STORE-NUMBER         PIC 9(03)    VALUE ZERO.         01104700
100404     05  HV-STORE-NUMBER         PIC 9(05)    VALUE ZERO.         01104800
           05  HV-STORE-NAME           PIC X(10)    VALUE SPACE.        01104900
           05  HV-XFER-TYP-CDE         PIC X(02)    VALUE SPACE.        01105000
                                                                        01106000
       01  BLANK-LINE                  PIC X(132)   VALUE SPACES.       01107000
                                                                        01108000
       01  END-OF-REPORT-LINE.                                          01109000
           05  FILLER                  PIC X(52)    VALUE SPACES.       01110000
           05  FILLER                  PIC X(26)    VALUE               01120000
               '   *** END OF REPORT ***'.                              01130000
           05  FILLER                  PIC X(54)    VALUE SPACES.       01140000
                                                                        01150000
       01  NO-DATA-LINE.                                                01160000
           05  FILLER                  PIC X(35)    VALUE SPACES.       01170000
           05  FILLER                  PIC X(45)    VALUE               01180000
               '****  NO DATA TO PROCESS FOR THIS REPORT  ***'.         01190000
                                                                        01200000
       01  CD-CURRENT-DATE.                                             01210000
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.         01220000
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.         01230000
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.         01240000
                                                                        01250000
       01  CT-CURRENT-TIME.                                             01260000
           05  CT-CURRENT-HOUR         PIC 9(2)     VALUE ZERO.         01270000
           05  CT-CURRENT-MINUTE       PIC 9(2)     VALUE ZERO.         01280000
           05  FILLER                  PIC 9(4)     VALUE ZERO.         01290000
                                                                        01300000
       01  HL2-HEADING-LINE-2.                                          01310000
           05  FILLER                  PIC X(43)    VALUE SPACES.       01320000
           05  HL2-TITLE               PIC X(50)    VALUE SPACES.       01330000
           05  FILLER                  PIC X(52)    VALUE SPACES.       01340000
                                                                        01350000
       01  HL3-HEADING-LINE-3.                                          01360000
           05  FILLER                  PIC X(49)  VALUE SPACE.          01370000
           05  FILLER                  PIC X(11)    VALUE               01380000
               'FOR PERIOD'.                                            01390000
           05  HL3-FROM-DATE           PIC X(10)  VALUE SPACES.         01400000
           05  FILLER                  PIC X(04)  VALUE ' TO '.         01410000
           05  HL3-TO-DATE             PIC X(10)  VALUE SPACES.         01420000
                                                                        01430000
       01  HL4-HEADING-LINE-4.                                          01440000
           05  FILLER                  PIC X(05)  VALUE SPACE.          01450000
           05  FILLER                  PIC X(16)  VALUE 'STORE'.        01460000
           05  FILLER                  PIC X(21)  VALUE                 01470000
                                       'PTD THIS YEAR'.                 01480000
           05  FILLER                  PIC X(19)  VALUE                 01490000
                                       'PTD LAST YEAR'.                 01500000
           05  FILLER                  PIC X(18)  VALUE                 01510000
                                       'PTD VARIANCE'.                  01520000
           05  FILLER                  PIC X(21)  VALUE                 01530000
                                       'YTD THIS YEAR'.                 01540000
           05  FILLER                  PIC X(19)  VALUE                 01550000
                                       'YTD LAST YEAR'.                 01560000
           05  FILLER                  PIC X(18)  VALUE                 01570000
                                       'YTD VARIANCE'.                  01580000
                                                                        01590000
       01  HL5-HEADING-LINE-5.                                          01600000
           05  FILLER                  PIC X(17)  VALUE                 01610000
                                       '---------------'.               01620000
           05  FILLER                  PIC X(21)  VALUE                 01630000
                                       '--------------------'.          01640000
           05  FILLER                  PIC X(22)  VALUE                 01650000
                                       '--------------------'.          01660000
           05  FILLER                  PIC X(15)  VALUE                 01670000
                                       '-------------'.                 01680000
           05  FILLER                  PIC X(21)  VALUE                 01690000
                                       '--------------------'.          01700000
           05  FILLER                  PIC X(22)  VALUE                 01710000
                                       '--------------------'.          01720000
           05  FILLER                  PIC X(15)  VALUE                 01730000
                                       '-------------'.                 01740000
                                                                        01750000
       01  HL6-HEADING-LINE-6.                                          01760000
           05  FILLER                  PIC X(08)  VALUE 'NBR'.          01770000
           05  FILLER                  PIC X(11)  VALUE 'NAME'.         01780000
           05  FILLER                  PIC X(11)  VALUE 'UNITS'.        01790000
           05  FILLER                  PIC X(10)  VALUE 'DOLLARS'.      01800000
           05  FILLER                  PIC X(11)  VALUE 'UNITS'.        01810000
           05  FILLER                  PIC X(26)  VALUE                 01820000
                                       'DOLLARS  UNITS DOLLARS'.        01830000
           05  FILLER                  PIC X(11)  VALUE 'UNITS'.        01840000
           05  FILLER                  PIC X(10)  VALUE 'DOLLARS'.      01850000
           05  FILLER                  PIC X(11)  VALUE 'UNITS'.        01860000
           05  FILLER                  PIC X(26)  VALUE                 01870000
                                       'DOLLARS  UNITS DOLLARS'.        01880000
                                                                        01890000
       01  DL1-DETAIL-LINE-1.                                           01900000
           05  DL1-STORE-INFO.                                          01910000
100404*        10  DL1-STR-NBR         PIC 9(03).                       01920000
100404         10  DL1-STR-NBR         PIC 9(04).                       01930000
100404*        10  FILLER              PIC X(02)  VALUE SPACES.         01940000
100404         10  FILLER              PIC X(01)  VALUE SPACES.         01950000
               10  DL1-STR-NAME        PIC X(10).                       01960000
           05  DL1-TOTALS REDEFINES DL1-STORE-INFO.                     01970000
               10  DL1-COMPANY-LIT.                                     01980000
                   15  DL1-DIST-LIT    PIC X(04).                       01990000
                   15  FILLER          PIC X(01).                       02000000
120108             15  DL1-DISTRICT    PIC X(04).                       02010001
100404         10  FILLER              PIC X(01).                       02020002
               10  DL1-TOTAL-LIT       PIC X(05).                       02030000
           05  FILLER                  PIC X(01)  VALUE SPACES.         02040000
           05  DL1-PTD-TY-QTY          PIC ZZZZ,ZZ9.                    02050000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02060000
           05  DL1-PTD-TY-RTL          PIC ZZ,ZZZ,ZZZ.99.               02070000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02080000
           05  DL1-PTD-LY-QTY          PIC ZZZZ,ZZ9.                    02090000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02100000
           05  DL1-PTD-LY-RTL          PIC ZZ,ZZZ,ZZZ.99.               02110000
      *    05  FILLER                  PIC X(01)  VALUE SPACES.         02120000
           05  DL1-PTD-VAR-QTY         PIC ZZZZ9.9-.                    02130000
      *    05  FILLER                  PIC X(01)  VALUE SPACES.         02140000
           05  DL1-PTD-VAR-RTL         PIC ZZZZ9.9-.                    02150000
      *    05  FILLER                  PIC X(01)  VALUE SPACES.         02160000
           05  DL1-YTD-TY-QTY          PIC ZZZZ,ZZ9.                    02170000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02180000
           05  DL1-YTD-TY-RTL          PIC ZZ,ZZZ,ZZZ.99.               02190000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02200000
           05  DL1-YTD-LY-QTY          PIC ZZZZ,ZZ9.                    02210000
      *    05  FILLER                  PIC X(01)  VALUE SPACE.          02220000
           05  DL1-YTD-LY-RTL          PIC ZZ,ZZZ,ZZZ.99.               02230000
           05  FILLER                  PIC X(01)  VALUE SPACES.         02240000
           05  DL1-YTD-VAR-QTY         PIC ZZZ9.9-.                     02250000
           05  FILLER                  PIC X(01)  VALUE SPACES.         02260000
           05  DL1-YTD-VAR-RTL         PIC ZZZ9.9-.                     02270000
                                                                        02280000
       01  SL1-SUMMARY-LINE-1.                                          02290000
           05  SL1-TOTALS.                                              02300000
               10  SL1-COMPANY-LIT     PIC X(07).                       02310000
               10  FILLER              PIC X(01).                       02320000
               10  SL1-TOTAL-LIT       PIC X(05).                       02330000
           05  SL1-PTD-TY-QTY          PIC ZZZ,ZZZ,ZZ9.                 02340000
           05  FILLER                  PIC X(10)  VALUE SPACE.          02350000
           05  SL1-PTD-LY-QTY          PIC ZZZ,ZZZ,ZZ9.                 02360000
           05  FILLER                  PIC X(13)  VALUE SPACE.          02370000
           05  SL1-PTD-VAR-QTY         PIC ZZZZ9.9-.                    02380000
           05  FILLER                  PIC X(05)  VALUE SPACES.         02390000
           05  SL1-YTD-TY-QTY          PIC ZZZ,ZZZ,ZZ9.                 02400000
           05  FILLER                  PIC X(10)  VALUE SPACE.          02410000
           05  SL1-YTD-LY-QTY          PIC ZZZ,ZZZ,ZZ9.                 02420000
           05  FILLER                  PIC X(14)  VALUE SPACE.          02430000
           05  SL1-YTD-VAR-QTY         PIC ZZZ9.9-.                     02440000
       01  SL2-SUMMARY-LINE-2.                                          02450000
           05  FILLER                  PIC X(21)  VALUE SPACES.         02460000
           05  SL2-PTD-TY-RTL          PIC Z,ZZZ,ZZZ,ZZZ.99.            02470000
           05  FILLER                  PIC X(05)  VALUE SPACE.          02480000
           05  SL2-PTD-LY-RTL          PIC Z,ZZZ,ZZZ,ZZZ.99.            02490000
           05  FILLER                  PIC X(08)  VALUE SPACES.         02500000
           05  SL2-PTD-VAR-RTL         PIC ZZZZ9.9-.                    02510000
           05  FILLER                  PIC X(05)  VALUE SPACES.         02520000
           05  SL2-YTD-TY-RTL          PIC Z,ZZZ,ZZZ,ZZZ.99.            02530000
           05  FILLER                  PIC X(05)  VALUE SPACE.          02540000
           05  SL2-YTD-LY-RTL          PIC Z,ZZZ,ZZZ,ZZZ.99.            02550000
           05  FILLER                  PIC X(09)  VALUE SPACES.         02560000
           05  SL2-YTD-VAR-RTL         PIC ZZZ9.9-.                     02570000
      *---------------------------------------------------------------* 02580000
      *    DB2 AREA FOR TRANSFER TYPE CODE TABLE                      * 02590000
      *---------------------------------------------------------------* 02600000
           EXEC SQL                                                     02610000
                INCLUDE TTFTYCD                                         02620000
           END-EXEC.                                                    02630000
      *---------------------------------------------------------------* 02640000
100404*    DB2 AREA FOR XST_LOC TABLE                                 * 02650000
      *---------------------------------------------------------------* 02660000
           EXEC SQL                                                     02670000
100404          INCLUDE XST00001                                        02680000
           END-EXEC.                                                    02690000
                                                                        02700000
           EXEC SQL                                                     02710000
               INCLUDE SQLCA                                            02720000
           END-EXEC.                                                    02730000
      *                                                                 02740000
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           02750000
      *  SQL CODES FOR DISPLAY                                          02760000
      *                                                                 02770000
           COPY SQLCA2.                                                 02780000
                                                                        02790000
      ******************************************************************02800000
       PROCEDURE DIVISION.                                              02810000
      ******************************************************************02820000
       0000-MAINLINE.                                                   02830000
           PERFORM 1000-INITIALIZE.                                     02840000
           PERFORM 2000-PROCESS-DETAIL-FILE.                            02850000
           PERFORM 1500-INITIALIZE.                                     02860000
           PERFORM 2500-PROCESS-SUMMARY-FILE.                           02870000
                                                                        02880000
           IF PS-FIRST-TIME                                             02890000
               PERFORM 9000-PRINT-HEADINGS                              02900000
               WRITE TRANSFER-REPORT-RECORD                             02910000
                   FROM NO-DATA-LINE                                    02920000
                   AFTER ADVANCING 2                                    02930000
           ELSE                                                         02940000
               PERFORM 8000-GRAND-TOTAL                                 02950000
           END-IF.                                                      02960000
                                                                        02970000
           WRITE TRANSFER-REPORT-RECORD                                 02980000
               FROM END-OF-REPORT-LINE                                  02990000
               AFTER ADVANCING 2.                                       03000000
           CLOSE TRANSFER-REPORT-FILE                                   03010000
                 PROCESS-DATES-FILE                                     03020000
                 TRANSFERS-BY-TYPE-FILE                                 03030000
                 TRANSFERS-SUMMARY-FILE.                                03040000
           STOP RUN.                                                    03050000
                                                                        03060000
      ******************************************************************03070000
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, READ THE   03080000
      *  PROCESS DATES FILE OF THE STANDARD REPORT HEADER,              03090000
      *  READING FIRST RECORD, PRINT HEADINGS.                          03100000
      ******************************************************************03110000
                                                                        03120000
       1000-INITIALIZE.                                                 03130000
           OPEN OUTPUT TRANSFER-REPORT-FILE                             03140000
                INPUT  PROCESS-DATES-FILE                               03150000
                       TRANSFERS-BY-TYPE-FILE                           03160000
                       TRANSFERS-SUMMARY-FILE.                          03170000
           ACCEPT CD-CURRENT-DATE FROM DATE.                            03180000
           MOVE CD-CURRENT-YEAR TO DP132O-RUN-YEAR.                     03190000
           MOVE CD-CURRENT-MONTH TO DP132O-RUN-MONTH.                   03200000
           MOVE CD-CURRENT-DAY TO DP132O-RUN-DAY.                       03210000
           MOVE +90 TO PV-LINE-COUNT.                                   03220000
                                                                        03230000
           ACCEPT CT-CURRENT-TIME FROM TIME.                            03240000
           MOVE CT-CURRENT-HOUR TO DP132O-RUN-HOUR.                     03250000
           MOVE CT-CURRENT-MINUTE TO DP132O-RUN-MINUTE.                 03260000
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                 03270000
           MOVE 1 TO DP132O-REPORT-NUMBER.                              03280000
           READ PROCESS-DATES-FILE                                      03290000
               INTO TF029-PROCESS-DATES-RECORD                          03300000
           END-READ.                                                    03310000
           DISPLAY 'TF029-PROCESS-DATES-RECORD = '                      03320000
               TF029-PROCESS-DATES-RECORD.                              03330000
           MOVE TF029-PROCESS-START-MONTH    TO PV-MONTH.               03340000
           MOVE TF029-PROCESS-START-DAY      TO PV-DAY.                 03350000
           MOVE TF029-PROCESS-START-YEAR     TO PV-YEAR.                03360000
           MOVE TF029-PROCESS-START-CENTURY  TO PV-CENTURY.             03370000
           MOVE PV-DATE                      TO HL3-FROM-DATE.          03380000
           MOVE TF029-PROCESS-END-MONTH      TO PV-MONTH.               03390000
           MOVE TF029-PROCESS-END-DAY        TO PV-DAY.                 03400000
           MOVE TF029-PROCESS-END-YEAR       TO PV-YEAR.                03410000
           MOVE TF029-PROCESS-END-CENTURY    TO PV-CENTURY.             03420000
           MOVE PV-DATE                      TO HL3-TO-DATE.            03430000
      ******************************************************************03440000
      *  INITIALIZE SWITCHES AND ACCUMULATORS FOR PROCESSING THE        03450000
      *  SUMMARY FILE.                                                  03460000
      ******************************************************************03470000
                                                                        03480000
       1500-INITIALIZE.                                                 03490000
           INITIALIZE PA-PROGRAM-ACCUMULATORS                           03500000
                      HV-HOLD-VARIABLES.                                03510000
           MOVE +90                         TO PV-LINE-COUNT.           03520000
           SET PS-NO-ITEMS-READ                                         03530000
               PS-NOT-END-OF-DATA                                       03540000
               PS-FIRST-TIME                TO TRUE.                    03550000
           MOVE PC-ALL-TYPES-LITERAL        TO HL2-TITLE.               03560000
      ******************************************************************03570000
      * PROCESS TRANSFERS BY TYPE DETAIL FILE                           03580000
      ******************************************************************03590000
       2000-PROCESS-DETAIL-FILE.                                        03600000
           PERFORM                                                      03610000
               UNTIL PS-END-OF-DATA                                     03620000
               PERFORM 2100-READ-DETAIL                                 03630000
                                                                        03640000
               IF  PS-NOT-END-OF-DATA                                   03650000
                   PERFORM 3000-PROCESS-DETAIL                          03660000
               END-IF                                                   03670000
           END-PERFORM.                                                 03680000
           IF PS-ITEMS-READ                                             03690000
               PERFORM 5100-PRINT-STORE-LINE                            03700000
               PERFORM 6000-NEW-DISTRICT                                03710000
               PERFORM 7100-TYPE-TOTAL                                  03720000
           END-IF.                                                      03730000
                                                                        03740000
       EJECT                                                            03750000
      *----------------------------------------------------------------*03760000
      *    READ THE TRANSFERS BY TYPE FILE                             *03770000
      *----------------------------------------------------------------*03780000
       2100-READ-DETAIL.                                                03790000
                                                                        03800000
           READ TRANSFERS-BY-TYPE-FILE                                  03810000
               INTO TF036-TRANSFERS-BY-TYPE-RECORD                      03820000
                   AT END                                               03830000
                       SET PS-END-OF-DATA        TO  TRUE               03840000
           END-READ.                                                    03850000
      ******************************************************************03860000
      * PROCESS TRANSFERS BY TYPE SUMMARY FILE                          03870000
      ******************************************************************03880000
       2500-PROCESS-SUMMARY-FILE.                                       03890000
           PERFORM                                                      03900000
               UNTIL PS-END-OF-DATA                                     03910000
               PERFORM 2600-READ-SUMMARY                                03920000
                                                                        03930000
               IF  PS-NOT-END-OF-DATA                                   03940000
                   PERFORM 4000-PROCESS-SUMMARY                         03950000
               END-IF                                                   03960000
           END-PERFORM.                                                 03970000
           IF PS-ITEMS-READ                                             03980000
               PERFORM 5100-PRINT-STORE-LINE                            03990000
               PERFORM 6000-NEW-DISTRICT                                04000000
           END-IF.                                                      04010000
                                                                        04020000
       EJECT                                                            04030000
      *----------------------------------------------------------------*04040000
      *    READ THE TRANSFERS BY TYPE DETAIL FILE                      *04050000
      *----------------------------------------------------------------*04060000
       2600-READ-SUMMARY.                                               04070000
                                                                        04080000
           READ TRANSFERS-SUMMARY-FILE                                  04090000
               INTO TF036-TRANSFERS-BY-TYPE-RECORD                      04100000
                   AT END                                               04110000
                       SET PS-END-OF-DATA        TO  TRUE               04120000
           END-READ.                                                    04130000
      *----------------------------------------------------------------*04140000
      *    PROCESS THE DETAIL DATA                                      04150000
      *----------------------------------------------------------------*04160000
       3000-PROCESS-DETAIL.                                             04170000
           IF PS-FIRST-TIME                                             04180000
               PERFORM 7200-GET-TYPE-DESC                               04190000
               PERFORM 5200-GET-STORE-NAME                              04200000
               MOVE TF036-DISTRICT-NMBR      TO HV-DISTRICT-NMBR        04210000
               SET PS-NOT-FIRST-TIME                                    04220000
                   PS-ITEMS-READ             TO TRUE                    04230000
           END-IF.                                                      04240000
           IF TF036-XFER-TYP-CDE NOT = HV-XFER-TYP-CDE                  04250000
               PERFORM 7000-NEW-TYPE                                    04260000
           END-IF.                                                      04270000
           IF TF036-DISTRICT-NMBR NOT = HV-DISTRICT-NMBR                04280000
               PERFORM 5000-NEW-STORE                                   04290000
               PERFORM 6000-NEW-DISTRICT                                04300000
           END-IF.                                                      04310000
           IF TF036-STORE-NUMBER  NOT = HV-STORE-NUMBER                 04320000
               PERFORM 5000-NEW-STORE                                   04330000
           END-IF.                                                      04340000
           IF TF036-PTD-TY-TOTALS                                       04350000
               ADD TF036-TOTAL-UNITS         TO PA-PTD-TY-QTY           04360000
               ADD TF036-TOTAL-RETAIL        TO PA-PTD-TY-RTL           04370000
           END-IF.                                                      04380000
           IF TF036-PTD-LY-TOTALS                                       04390000
               ADD TF036-TOTAL-UNITS         TO PA-PTD-LY-QTY           04400000
               ADD TF036-TOTAL-RETAIL        TO PA-PTD-LY-RTL           04410000
           END-IF.                                                      04420000
           IF TF036-YTD-TY-TOTALS                                       04430000
               ADD TF036-TOTAL-UNITS         TO PA-YTD-TY-QTY           04440000
               ADD TF036-TOTAL-RETAIL        TO PA-YTD-TY-RTL           04450000
           END-IF.                                                      04460000
           IF TF036-YTD-LY-TOTALS                                       04470000
               ADD TF036-TOTAL-UNITS         TO PA-YTD-LY-QTY           04480000
               ADD TF036-TOTAL-RETAIL        TO PA-YTD-LY-RTL           04490000
           END-IF.                                                      04500000
      *----------------------------------------------------------------*04510000
      *    PROCESS THE SUMMARY DATA                                     04520000
      *----------------------------------------------------------------*04530000
       4000-PROCESS-SUMMARY.                                            04540000
           IF PS-FIRST-TIME                                             04550000
               PERFORM 5200-GET-STORE-NAME                              04560000
               MOVE TF036-DISTRICT-NMBR      TO HV-DISTRICT-NMBR        04570000
               SET PS-NOT-FIRST-TIME                                    04580000
                   PS-ITEMS-READ             TO TRUE                    04590000
           END-IF.                                                      04600000
           IF TF036-DISTRICT-NMBR NOT = HV-DISTRICT-NMBR                04610000
               PERFORM 5000-NEW-STORE                                   04620000
               PERFORM 6000-NEW-DISTRICT                                04630000
           END-IF.                                                      04640000
           IF TF036-STORE-NUMBER  NOT = HV-STORE-NUMBER                 04650000
               PERFORM 5000-NEW-STORE                                   04660000
           END-IF.                                                      04670000
           IF TF036-PTD-TY-TOTALS                                       04680000
               ADD TF036-TOTAL-UNITS         TO PA-PTD-TY-QTY           04690000
               ADD TF036-TOTAL-RETAIL        TO PA-PTD-TY-RTL           04700000
           END-IF.                                                      04710000
           IF TF036-PTD-LY-TOTALS                                       04720000
               ADD TF036-TOTAL-UNITS         TO PA-PTD-LY-QTY           04730000
               ADD TF036-TOTAL-RETAIL        TO PA-PTD-LY-RTL           04740000
           END-IF.                                                      04750000
           IF TF036-YTD-TY-TOTALS                                       04760000
               ADD TF036-TOTAL-UNITS         TO PA-YTD-TY-QTY           04770000
               ADD TF036-TOTAL-RETAIL        TO PA-YTD-TY-RTL           04780000
           END-IF.                                                      04790000
           IF TF036-YTD-LY-TOTALS                                       04800000
               ADD TF036-TOTAL-UNITS         TO PA-YTD-LY-QTY           04810000
               ADD TF036-TOTAL-RETAIL        TO PA-YTD-LY-RTL           04820000
           END-IF.                                                      04830000
                                                                        04840000
      *----------------------------------------------------------------*04850000
      *  STORE NUMBER BREAK.                                           *04860000
      *----------------------------------------------------------------*04870000
       5000-NEW-STORE.                                                  04880000
           PERFORM 5100-PRINT-STORE-LINE.                               04890000
           PERFORM 5200-GET-STORE-NAME.                                 04900000
      *----------------------------------------------------------------*04910000
      *  PRINT A DETAIL LINE FOR A STORE                               *04920000
      *----------------------------------------------------------------*04930000
       5100-PRINT-STORE-LINE.                                           04940000
           MOVE SPACES                       TO DL1-DETAIL-LINE-1.      04950000
           MOVE HV-STORE-NUMBER              TO DL1-STR-NBR.            04960000
           MOVE HV-STORE-NAME                TO DL1-STR-NAME.           04970000
           MOVE PA-PTD-TY-QTY                TO DL1-PTD-TY-QTY.         04980000
           MOVE PA-PTD-TY-RTL                TO DL1-PTD-TY-RTL.         04990000
           MOVE PA-PTD-LY-QTY                TO DL1-PTD-LY-QTY.         05000000
           MOVE PA-PTD-LY-RTL                TO DL1-PTD-LY-RTL.         05010000
           IF PA-PTD-TY-QTY > 0 AND PA-PTD-LY-QTY > 0                   05020000
               COMPUTE PV-VARIANCE ROUNDED =                            05030000
                 ((PA-PTD-TY-QTY - PA-PTD-LY-QTY) / PA-PTD-LY-QTY)      05040000
               MULTIPLY 100 BY PV-VARIANCE                              05050000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-QTY         05060000
           END-IF.                                                      05070000
           IF PA-PTD-TY-RTL > 0 AND PA-PTD-LY-RTL > 0                   05080000
               COMPUTE PV-VARIANCE ROUNDED =                            05090000
                 ((PA-PTD-TY-RTL - PA-PTD-LY-RTL) / PA-PTD-LY-RTL)      05100000
               MULTIPLY 100 BY PV-VARIANCE                              05110000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-RTL         05120000
           END-IF.                                                      05130000
           MOVE PA-YTD-TY-QTY                TO DL1-YTD-TY-QTY.         05140000
           MOVE PA-YTD-TY-RTL                TO DL1-YTD-TY-RTL.         05150000
           MOVE PA-YTD-LY-QTY                TO DL1-YTD-LY-QTY.         05160000
           MOVE PA-YTD-LY-RTL                TO DL1-YTD-LY-RTL.         05170000
           IF PA-YTD-TY-QTY > 0 AND PA-YTD-LY-QTY > 0                   05180000
               COMPUTE PV-VARIANCE ROUNDED =                            05190000
                 ((PA-YTD-TY-QTY - PA-YTD-LY-QTY) / PA-YTD-LY-QTY)      05200000
               MULTIPLY 100 BY PV-VARIANCE                              05210000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-QTY         05220000
           END-IF.                                                      05230000
           IF PA-YTD-TY-RTL > 0 AND PA-YTD-LY-RTL > 0                   05240000
               COMPUTE PV-VARIANCE ROUNDED =                            05250000
                 ((PA-YTD-TY-RTL - PA-YTD-LY-RTL) / PA-YTD-LY-RTL)      05260000
               MULTIPLY 100 BY PV-VARIANCE                              05270000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-RTL         05280000
           END-IF.                                                      05290000
           IF PV-LINE-COUNT > PC-PAGE-MAX                               05300000
               PERFORM 9000-PRINT-HEADINGS                              05310000
           END-IF.                                                      05320000
           WRITE TRANSFER-REPORT-RECORD                                 05330000
               FROM DL1-DETAIL-LINE-1                                   05340000
               AFTER ADVANCING 1.                                       05350000
           ADD +1                           TO PV-LINE-COUNT.           05360000
           IF PA-PTD-TY-QTY > 0 AND PA-PTD-LY-QTY > 0    AND            05370000
              PA-PTD-TY-RTL > 0 AND PA-PTD-LY-RTL > 0                   05380000
               ADD  PA-PTD-TY-QTY           TO PA-PTD-TY-DS-COMP-QTY    05390000
               ADD  PA-PTD-TY-RTL           TO PA-PTD-TY-DS-COMP-RTL    05400000
               ADD  PA-PTD-LY-QTY           TO PA-PTD-LY-DS-COMP-QTY    05410000
               ADD  PA-PTD-LY-RTL           TO PA-PTD-LY-DS-COMP-RTL    05420000
           END-IF.                                                      05430000
           IF PA-YTD-TY-QTY > 0 AND PA-YTD-LY-QTY > 0    AND            05440000
              PA-YTD-TY-RTL > 0 AND PA-YTD-LY-RTL > 0                   05450000
               ADD  PA-YTD-TY-QTY           TO PA-YTD-TY-DS-COMP-QTY    05460000
               ADD  PA-YTD-TY-RTL           TO PA-YTD-TY-DS-COMP-RTL    05470000
               ADD  PA-YTD-LY-QTY           TO PA-YTD-LY-DS-COMP-QTY    05480000
               ADD  PA-YTD-LY-RTL           TO PA-YTD-LY-DS-COMP-RTL    05490000
           END-IF.                                                      05500000
           ADD  PA-PTD-TY-QTY               TO PA-PTD-TY-DS-QTY.        05510000
           ADD  PA-PTD-TY-RTL               TO PA-PTD-TY-DS-RTL.        05520000
           ADD  PA-PTD-LY-QTY               TO PA-PTD-LY-DS-QTY.        05530000
           ADD  PA-PTD-LY-RTL               TO PA-PTD-LY-DS-RTL.        05540000
           ADD  PA-YTD-TY-QTY               TO PA-YTD-TY-DS-QTY.        05550000
           ADD  PA-YTD-TY-RTL               TO PA-YTD-TY-DS-RTL.        05560000
           ADD  PA-YTD-LY-QTY               TO PA-YTD-LY-DS-QTY.        05570000
           ADD  PA-YTD-LY-RTL               TO PA-YTD-LY-DS-RTL.        05580000
           MOVE ZEROES                      TO PA-PTD-TY-QTY            05590000
                                               PA-PTD-TY-RTL            05600000
                                               PA-PTD-LY-QTY            05610000
                                               PA-PTD-LY-RTL            05620000
                                               PA-YTD-TY-QTY            05630000
                                               PA-YTD-TY-RTL            05640000
                                               PA-YTD-LY-QTY            05650000
                                               PA-YTD-LY-RTL.           05660000
      *----------------------------------------------------------------*05670000
      *    GET THE STORE 10 CHARACTER ABBREVIATED NAME.                 05680000
      *----------------------------------------------------------------*05690000
       5200-GET-STORE-NAME.                                             05700000
100404     MOVE TF036-STORE-NUMBER   TO XST00001-LOC-NBR                05710000
                                        HV-STORE-NUMBER.                05720000
           PERFORM WITH TEST AFTER                                      05730000
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05740000
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                  05750000
                    OR SQLCODE = ZERO                                   05760000
                    OR SQLCODE = +100                                   05770000
               EXEC SQL                                                 05780000
100404             SELECT LOC_10_ABBR_NM                                05790000
100404                 INTO :XST00001-LOC-10-ABBR-NM                    05800000
100404                 FROM XST_LOC                                     05810000
100404                 WHERE LOC_NBR = :XST00001-LOC-NBR                05820000
100404                 WITH UR                                          05830000
               END-EXEC                                                 05840000
               EVALUATE TRUE                                            05850000
                   WHEN SQLCODE = ZERO                                  05860000
100404                 MOVE XST00001-LOC-10-ABBR-NM  TO HV-STORE-NAME   05870000
                   WHEN SQLCODE = +100                                  05880000
100404                 MOVE '??????????'             TO HV-STORE-NAME   05890000
                   WHEN SQLCODE = -904                                  05900000
                   WHEN SQLCODE = -913                                  05910000
                       CONTINUE                                         05920000
                   WHEN OTHER                                           05930000
                       MOVE '5200-GET-STORE-NAME' TO PV-PARAGRAPH-NAME  05940000
                       MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION  05950000
                       PERFORM 9100-SQL-ERROR                           05960000
               END-EVALUATE                                             05970000
           END-PERFORM.                                                 05980000
                                                                        05990000
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           06000000
               MOVE '5200-GET-STORE-NAME' TO PV-PARAGRAPH-NAME          06010000
               MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION          06020000
               PERFORM 9100-SQL-ERROR                                   06030000
           END-IF.                                                      06040000
      *----------------------------------------------------------------*06050000
      *  PRINT THE DISTRICT TOTAL LINE.                                *06060000
      *----------------------------------------------------------------*06070000
       6000-NEW-DISTRICT.                                               06080000
           MOVE SPACES                       TO DL1-DETAIL-LINE-1.      06090000
           MOVE PC-DIST-LITERAL              TO DL1-DIST-LIT.           06100000
           MOVE PC-COMP-LITERAL              TO DL1-TOTAL-LIT.          06110000
           MOVE HV-DISTRICT-NMBR             TO DL1-DISTRICT.           06120000
           MOVE PA-PTD-TY-DS-COMP-QTY        TO DL1-PTD-TY-QTY.         06130000
           MOVE PA-PTD-TY-DS-COMP-RTL        TO DL1-PTD-TY-RTL.         06140000
           MOVE PA-PTD-LY-DS-COMP-QTY        TO DL1-PTD-LY-QTY.         06150000
           MOVE PA-PTD-LY-DS-COMP-RTL        TO DL1-PTD-LY-RTL.         06160000
           IF PA-PTD-TY-DS-COMP-QTY > 0 AND PA-PTD-LY-DS-COMP-QTY > 0   06170000
               COMPUTE PV-VARIANCE ROUNDED =                            06180000
                 ((PA-PTD-TY-DS-COMP-QTY - PA-PTD-LY-DS-COMP-QTY)       06190000
                                             / PA-PTD-LY-DS-COMP-QTY)   06200000
               MULTIPLY 100 BY PV-VARIANCE                              06210000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-QTY         06220000
           END-IF.                                                      06230000
           IF PA-PTD-TY-DS-COMP-RTL > 0 AND PA-PTD-LY-DS-COMP-RTL > 0   06240000
               COMPUTE PV-VARIANCE ROUNDED =                            06250000
                 ((PA-PTD-TY-DS-COMP-RTL - PA-PTD-LY-DS-COMP-RTL)       06260000
                                             / PA-PTD-LY-DS-COMP-RTL)   06270000
               MULTIPLY 100 BY PV-VARIANCE                              06280000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-RTL         06290000
           END-IF.                                                      06300000
           MOVE PA-YTD-TY-DS-COMP-QTY        TO DL1-YTD-TY-QTY.         06310000
           MOVE PA-YTD-TY-DS-COMP-RTL        TO DL1-YTD-TY-RTL.         06320000
           MOVE PA-YTD-LY-DS-COMP-QTY        TO DL1-YTD-LY-QTY.         06330000
           MOVE PA-YTD-LY-DS-COMP-RTL        TO DL1-YTD-LY-RTL.         06340000
           IF PA-YTD-TY-DS-COMP-QTY > 0 AND PA-YTD-LY-DS-COMP-QTY > 0   06350000
               COMPUTE PV-VARIANCE ROUNDED =                            06360000
                 ((PA-YTD-TY-DS-COMP-QTY - PA-YTD-LY-DS-COMP-QTY)       06370000
                                             / PA-YTD-LY-DS-COMP-QTY)   06380000
               MULTIPLY 100 BY PV-VARIANCE                              06390000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-QTY         06400000
           END-IF.                                                      06410000
           IF PA-YTD-TY-DS-COMP-RTL > 0 AND PA-YTD-LY-DS-COMP-RTL > 0   06420000
               COMPUTE PV-VARIANCE ROUNDED =                            06430000
                 ((PA-YTD-TY-DS-COMP-RTL - PA-YTD-LY-DS-COMP-RTL)       06440000
                                             / PA-YTD-LY-DS-COMP-RTL)   06450000
               MULTIPLY 100 BY PV-VARIANCE                              06460000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-RTL         06470000
           END-IF.                                                      06480000
           WRITE TRANSFER-REPORT-RECORD                                 06490000
               FROM DL1-DETAIL-LINE-1                                   06500000
               AFTER ADVANCING 2.                                       06510000
           MOVE SPACES                       TO DL1-DETAIL-LINE-1.      06520000
           MOVE PC-TOTAL-LITERAL             TO DL1-TOTAL-LIT.          06530000
           MOVE PA-PTD-TY-DS-QTY             TO DL1-PTD-TY-QTY.         06540000
           MOVE PA-PTD-TY-DS-RTL             TO DL1-PTD-TY-RTL.         06550000
           MOVE PA-PTD-LY-DS-QTY             TO DL1-PTD-LY-QTY.         06560000
           MOVE PA-PTD-LY-DS-RTL             TO DL1-PTD-LY-RTL.         06570000
           IF PA-PTD-TY-DS-QTY > 0 AND PA-PTD-LY-DS-QTY > 0             06580000
               COMPUTE PV-VARIANCE ROUNDED =                            06590000
                 ((PA-PTD-TY-DS-QTY - PA-PTD-LY-DS-QTY)                 06600000
                                             / PA-PTD-LY-DS-QTY)        06610000
               MULTIPLY 100 BY PV-VARIANCE                              06620000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-QTY         06630000
           END-IF.                                                      06640000
           IF PA-PTD-TY-DS-RTL > 0 AND PA-PTD-LY-DS-RTL > 0             06650000
               COMPUTE PV-VARIANCE ROUNDED =                            06660000
                 ((PA-PTD-TY-DS-RTL - PA-PTD-LY-DS-RTL)                 06670000
                                             / PA-PTD-LY-DS-RTL)        06680000
               MULTIPLY 100 BY PV-VARIANCE                              06690000
               MOVE PV-VARIANCE              TO DL1-PTD-VAR-RTL         06700000
           END-IF.                                                      06710000
           MOVE PA-YTD-TY-DS-QTY             TO DL1-YTD-TY-QTY.         06720000
           MOVE PA-YTD-TY-DS-RTL             TO DL1-YTD-TY-RTL.         06730000
           MOVE PA-YTD-LY-DS-QTY             TO DL1-YTD-LY-QTY.         06740000
           MOVE PA-YTD-LY-DS-RTL             TO DL1-YTD-LY-RTL.         06750000
           IF PA-YTD-TY-DS-QTY > 0 AND PA-YTD-LY-DS-QTY > 0             06760000
               COMPUTE PV-VARIANCE ROUNDED =                            06770000
                 ((PA-YTD-TY-DS-QTY - PA-YTD-LY-DS-QTY)                 06780000
                                             / PA-YTD-LY-DS-QTY)        06790000
               MULTIPLY 100 BY PV-VARIANCE                              06800000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-QTY         06810000
           END-IF.                                                      06820000
           IF PA-YTD-TY-DS-RTL > 0 AND PA-YTD-LY-DS-RTL > 0             06830000
               COMPUTE PV-VARIANCE ROUNDED =                            06840000
                 ((PA-YTD-TY-DS-RTL - PA-YTD-LY-DS-RTL)                 06850000
                                             / PA-YTD-LY-DS-RTL)        06860000
               MULTIPLY 100 BY PV-VARIANCE                              06870000
               MOVE PV-VARIANCE              TO DL1-YTD-VAR-RTL         06880000
           END-IF.                                                      06890000
           WRITE TRANSFER-REPORT-RECORD                                 06900000
               FROM DL1-DETAIL-LINE-1                                   06910000
               AFTER ADVANCING 1.                                       06920000
           WRITE TRANSFER-REPORT-RECORD                                 06930000
               FROM BLANK-LINE                                          06940000
               AFTER ADVANCING 1.                                       06950000
           ADD +4                           TO PV-LINE-COUNT.           06960000
           ADD  PA-PTD-TY-DS-QTY            TO PA-PTD-TY-CO-QTY.        06970000
           ADD  PA-PTD-TY-DS-RTL            TO PA-PTD-TY-CO-RTL.        06980000
           ADD  PA-PTD-LY-DS-QTY            TO PA-PTD-LY-CO-QTY.        06990000
           ADD  PA-PTD-LY-DS-RTL            TO PA-PTD-LY-CO-RTL.        07000000
           ADD  PA-YTD-TY-DS-QTY            TO PA-YTD-TY-CO-QTY.        07010000
           ADD  PA-YTD-TY-DS-RTL            TO PA-YTD-TY-CO-RTL.        07020000
           ADD  PA-YTD-LY-DS-QTY            TO PA-YTD-LY-CO-QTY.        07030000
           ADD  PA-YTD-LY-DS-RTL            TO PA-YTD-LY-CO-RTL.        07040000
           ADD  PA-PTD-TY-DS-COMP-QTY       TO PA-PTD-TY-CO-COMP-QTY.   07050000
           ADD  PA-PTD-TY-DS-COMP-RTL       TO PA-PTD-TY-CO-COMP-RTL.   07060000
           ADD  PA-PTD-LY-DS-COMP-QTY       TO PA-PTD-LY-CO-COMP-QTY.   07070000
           ADD  PA-PTD-LY-DS-COMP-RTL       TO PA-PTD-LY-CO-COMP-RTL.   07080000
           ADD  PA-YTD-TY-DS-COMP-QTY       TO PA-YTD-TY-CO-COMP-QTY.   07090000
           ADD  PA-YTD-TY-DS-COMP-RTL       TO PA-YTD-TY-CO-COMP-RTL.   07100000
           ADD  PA-YTD-LY-DS-COMP-QTY       TO PA-YTD-LY-CO-COMP-QTY.   07110000
           ADD  PA-YTD-LY-DS-COMP-RTL       TO PA-YTD-LY-CO-COMP-RTL.   07120000
           MOVE ZEROES                      TO PA-PTD-TY-DS-QTY         07130000
                                               PA-PTD-TY-DS-RTL         07140000
                                               PA-PTD-LY-DS-QTY         07150000
                                               PA-PTD-LY-DS-RTL         07160000
                                               PA-YTD-TY-DS-QTY         07170000
                                               PA-YTD-TY-DS-RTL         07180000
                                               PA-YTD-LY-DS-QTY         07190000
                                               PA-YTD-LY-DS-RTL         07200000
                                               PA-PTD-TY-DS-COMP-QTY    07210000
                                               PA-PTD-TY-DS-COMP-RTL    07220000
                                               PA-PTD-LY-DS-COMP-QTY    07230000
                                               PA-PTD-LY-DS-COMP-RTL    07240000
                                               PA-YTD-TY-DS-COMP-QTY    07250000
                                               PA-YTD-TY-DS-COMP-RTL    07260000
                                               PA-YTD-LY-DS-COMP-QTY    07270000
                                               PA-YTD-LY-DS-COMP-RTL.   07280000
           MOVE TF036-DISTRICT-NMBR      TO HV-DISTRICT-NMBR.           07290000
                                                                        07300000
      *----------------------------------------------------------------*07310000
      *  PRINT THE COMPANY TOTAL LINE FOR THIS TYPE                    *07320000
      *----------------------------------------------------------------*07330000
       7000-NEW-TYPE.                                                   07340000
           PERFORM 5000-NEW-STORE.                                      07350000
           PERFORM 6000-NEW-DISTRICT.                                   07360000
           PERFORM 7100-TYPE-TOTAL.                                     07370000
           PERFORM 7200-GET-TYPE-DESC.                                  07380000
      *----------------------------------------------------------------*07390000
      *  PRINT THE COMPANY TOTAL LINE FOR THIS TYPE                    *07400000
      *----------------------------------------------------------------*07410000
       7100-TYPE-TOTAL.                                                 07420000
           MOVE SPACES                       TO SL1-SUMMARY-LINE-1      07430000
                                                SL2-SUMMARY-LINE-2.     07440000
           MOVE PC-COMPANY-LITERAL           TO SL1-COMPANY-LIT.        07450000
           MOVE PC-COMP-LITERAL              TO SL1-TOTAL-LIT.          07460000
           MOVE PA-PTD-TY-CO-COMP-QTY        TO SL1-PTD-TY-QTY.         07470000
           MOVE PA-PTD-TY-CO-COMP-RTL        TO SL2-PTD-TY-RTL.         07480000
           MOVE PA-PTD-LY-CO-COMP-QTY        TO SL1-PTD-LY-QTY.         07490000
           MOVE PA-PTD-LY-CO-COMP-RTL        TO SL2-PTD-LY-RTL.         07500000
           IF PA-PTD-TY-CO-COMP-QTY > 0 AND PA-PTD-LY-CO-COMP-QTY > 0   07510000
               COMPUTE PV-VARIANCE ROUNDED =                            07520000
                 ((PA-PTD-TY-CO-COMP-QTY - PA-PTD-LY-CO-COMP-QTY)       07530000
                                             / PA-PTD-LY-CO-COMP-QTY)   07540000
               MULTIPLY 100 BY PV-VARIANCE                              07550000
               MOVE PV-VARIANCE              TO SL1-PTD-VAR-QTY         07560000
           END-IF.                                                      07570000
           IF PA-PTD-TY-CO-COMP-RTL > 0 AND PA-PTD-LY-CO-COMP-RTL > 0   07580000
               COMPUTE PV-VARIANCE ROUNDED =                            07590000
                 ((PA-PTD-TY-CO-COMP-RTL - PA-PTD-LY-CO-COMP-RTL)       07600000
                                             / PA-PTD-LY-CO-COMP-RTL)   07610000
               MULTIPLY 100 BY PV-VARIANCE                              07620000
               MOVE PV-VARIANCE              TO SL2-PTD-VAR-RTL         07630000
           END-IF.                                                      07640000
           MOVE PA-YTD-TY-CO-COMP-QTY        TO SL1-YTD-TY-QTY.         07650000
           MOVE PA-YTD-TY-CO-COMP-RTL        TO SL2-YTD-TY-RTL.         07660000
           MOVE PA-YTD-LY-CO-COMP-QTY        TO SL1-YTD-LY-QTY.         07670000
           MOVE PA-YTD-LY-CO-COMP-RTL        TO SL2-YTD-LY-RTL.         07680000
           IF PA-YTD-TY-CO-COMP-QTY > 0 AND PA-YTD-LY-CO-COMP-QTY > 0   07690000
               COMPUTE PV-VARIANCE ROUNDED =                            07700000
                 ((PA-YTD-TY-CO-COMP-QTY - PA-YTD-LY-CO-COMP-QTY)       07710000
                                             / PA-YTD-LY-CO-COMP-QTY)   07720000
               MULTIPLY 100 BY PV-VARIANCE                              07730000
               MOVE PV-VARIANCE              TO SL1-YTD-VAR-QTY         07740000
           END-IF.                                                      07750000
           IF PA-YTD-TY-CO-COMP-RTL > 0 AND PA-YTD-LY-CO-COMP-RTL > 0   07760000
               COMPUTE PV-VARIANCE ROUNDED =                            07770000
                 ((PA-YTD-TY-CO-COMP-RTL - PA-YTD-LY-CO-COMP-RTL)       07780000
                                             / PA-YTD-LY-CO-COMP-RTL)   07790000
               MULTIPLY 100 BY PV-VARIANCE                              07800000
               MOVE PV-VARIANCE              TO SL2-YTD-VAR-RTL         07810000
           END-IF.                                                      07820000
           IF PV-LINE-COUNT > PC-PAGE-MAX - 6                           07830000
               PERFORM 9000-PRINT-HEADINGS                              07840000
           END-IF.                                                      07850000
           WRITE TRANSFER-REPORT-RECORD                                 07860000
               FROM SL1-SUMMARY-LINE-1                                  07870000
               AFTER ADVANCING 2.                                       07880000
           WRITE TRANSFER-REPORT-RECORD                                 07890000
               FROM SL2-SUMMARY-LINE-2                                  07900000
               AFTER ADVANCING 1.                                       07910000
           MOVE SPACES                       TO SL1-SUMMARY-LINE-1      07920000
                                                SL2-SUMMARY-LINE-2.     07930000
           MOVE PC-TOTAL-LITERAL             TO SL1-TOTAL-LIT.          07940000
           MOVE PA-PTD-TY-CO-QTY             TO SL1-PTD-TY-QTY.         07950000
           MOVE PA-PTD-TY-CO-RTL             TO SL2-PTD-TY-RTL.         07960000
           MOVE PA-PTD-LY-CO-QTY             TO SL1-PTD-LY-QTY.         07970000
           MOVE PA-PTD-LY-CO-RTL             TO SL2-PTD-LY-RTL.         07980000
           IF PA-PTD-TY-CO-QTY > 0 AND PA-PTD-LY-CO-QTY > 0             07990000
               COMPUTE PV-VARIANCE ROUNDED =                            08000000
                 ((PA-PTD-TY-CO-QTY - PA-PTD-LY-CO-QTY)                 08010000
                                             / PA-PTD-LY-CO-QTY)        08020000
               MULTIPLY 100 BY PV-VARIANCE                              08030000
               MOVE PV-VARIANCE              TO SL1-PTD-VAR-QTY         08040000
           END-IF.                                                      08050000
           IF PA-PTD-TY-CO-RTL > 0 AND PA-PTD-LY-CO-RTL > 0             08060000
               COMPUTE PV-VARIANCE ROUNDED =                            08070000
                 ((PA-PTD-TY-CO-RTL - PA-PTD-LY-CO-RTL)                 08080000
                                             / PA-PTD-LY-CO-RTL)        08090000
               MULTIPLY 100 BY PV-VARIANCE                              08100000
               MOVE PV-VARIANCE              TO SL2-PTD-VAR-RTL         08110000
           END-IF.                                                      08120000
           MOVE PA-YTD-TY-CO-QTY             TO SL1-YTD-TY-QTY.         08130000
           MOVE PA-YTD-TY-CO-RTL             TO SL2-YTD-TY-RTL.         08140000
           MOVE PA-YTD-LY-CO-QTY             TO SL1-YTD-LY-QTY.         08150000
           MOVE PA-YTD-LY-CO-RTL             TO SL2-YTD-LY-RTL.         08160000
           IF PA-YTD-TY-CO-QTY > 0 AND PA-YTD-LY-CO-QTY > 0             08170000
               COMPUTE PV-VARIANCE ROUNDED =                            08180000
                 ((PA-YTD-TY-CO-QTY - PA-YTD-LY-CO-QTY)                 08190000
                                             / PA-YTD-LY-CO-QTY)        08200000
               MULTIPLY 100 BY PV-VARIANCE                              08210000
               MOVE PV-VARIANCE              TO SL1-YTD-VAR-QTY         08220000
           END-IF.                                                      08230000
           IF PA-YTD-TY-CO-RTL > 0 AND PA-YTD-LY-CO-RTL > 0             08240000
               COMPUTE PV-VARIANCE ROUNDED =                            08250000
                 ((PA-YTD-TY-CO-RTL - PA-YTD-LY-CO-RTL)                 08260000
                                             / PA-YTD-LY-CO-RTL)        08270000
               MULTIPLY 100 BY PV-VARIANCE                              08280000
               MOVE PV-VARIANCE              TO SL2-YTD-VAR-RTL         08290000
           END-IF.                                                      08300000
           WRITE TRANSFER-REPORT-RECORD                                 08310000
               FROM SL1-SUMMARY-LINE-1                                  08320000
               AFTER ADVANCING 2.                                       08330000
           WRITE TRANSFER-REPORT-RECORD                                 08340000
               FROM SL2-SUMMARY-LINE-2                                  08350000
               AFTER ADVANCING 1.                                       08360000
           MOVE ZEROES                      TO PA-PTD-TY-CO-QTY         08370000
                                               PA-PTD-TY-CO-RTL         08380000
                                               PA-PTD-LY-CO-QTY         08390000
                                               PA-PTD-LY-CO-RTL         08400000
                                               PA-YTD-TY-CO-QTY         08410000
                                               PA-YTD-TY-CO-RTL         08420000
                                               PA-YTD-LY-CO-QTY         08430000
                                               PA-YTD-LY-CO-RTL         08440000
                                               PA-PTD-TY-CO-COMP-QTY    08450000
                                               PA-PTD-TY-CO-COMP-RTL    08460000
                                               PA-PTD-LY-CO-COMP-QTY    08470000
                                               PA-PTD-LY-CO-COMP-RTL    08480000
                                               PA-YTD-TY-CO-COMP-QTY    08490000
                                               PA-YTD-TY-CO-COMP-RTL    08500000
                                               PA-YTD-LY-CO-COMP-QTY    08510000
                                               PA-YTD-LY-CO-COMP-RTL.   08520000
           MOVE +90                          TO PV-LINE-COUNT.          08530000
       7200-GET-TYPE-DESC.                                              08540000
           MOVE TF036-XFER-TYP-CDE     TO TFTYCD-XFER-TYP-CDE           08550000
                                          HV-XFER-TYP-CDE.              08560000
           PERFORM WITH TEST AFTER                                      08570000
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       08580000
               UNTIL   SQLCODE = +100 OR                                08590000
                       SQLCODE = ZERO OR                                08600000
                       PV-RETRY-COUNT > PC-MAX-RETRIES                  08610000
                                                                        08620000
               EXEC SQL                                                 08630000
                  SELECT                                                08640000
                      TYP_1ST_LNE_DESC                                  08650000
                     ,TYP_2ND_LNE_DESC                                  08660000
                  INTO                                                  08670000
                      :TFTYCD-TYP-1ST-LNE-DESC                          08680000
                     ,:TFTYCD-TYP-2ND-LNE-DESC                          08690000
                  FROM                                                  08700000
                      TTFTYCD                                           08710000
                  WHERE   XFER_TYP_CDE   = :TFTYCD-XFER-TYP-CDE         08720000
               END-EXEC                                                 08730000
                                                                        08740000
               EVALUATE TRUE                                            08750000
                   WHEN SQLCODE = ZERO                                  08760000
                   WHEN SQLCODE = -904                                  08770000
                   WHEN SQLCODE = -913                                  08780000
                        CONTINUE                                        08790000
                   WHEN SQLCODE = +100                                  08800000
                       MOVE 'NOT FOUND'  TO TFTYCD-TYP-1ST-LNE-DESC     08810000
                   WHEN OTHER                                           08820000
                       MOVE '7200-GET-TYPE-DESC' TO PV-PARAGRAPH-NAME   08830000
                       MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION  08840000
                       PERFORM 9100-SQL-ERROR                           08850000
               END-EVALUATE                                             08860000
           END-PERFORM.                                                 08870000
                                                                        08880000
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           08890000
               MOVE '7200-GET-TYPE-DESC' TO PV-PARAGRAPH-NAME           08900000
               MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION          08910000
               PERFORM 9100-SQL-ERROR                                   08920000
           END-IF.                                                      08930000
           MOVE SPACES                    TO PV-TYPE-DESC               08940000
                                             HL2-TITLE.                 08950000
           IF TFTYCD-TYP-2ND-LNE-DESC = SPACES                          08960000
               MOVE TFTYCD-TYP-1ST-LNE-DESC TO PV-TYPE-DESC             08970000
           ELSE                                                         08980000
               STRING TFTYCD-TYP-1ST-LNE-DESC DELIMITED BY '  '         08990000
                      TFTYCD-TYP-2ND-LNE-DESC DELIMITED BY '  '         09000000
                                          INTO PV-TYPE-DESC             09010000
           END-IF.                                                      09020000
           STRING PV-TYPE-DESC            DELIMITED BY '  '             09030000
                  PC-TITLE-LITERAL        DELIMITED BY SIZE             09040000
                                          INTO PV-TYPE-DESC.            09050000
           MOVE ZERO                      TO PV-CHARACTER-COUNT.        09060000
           INSPECT PV-TYPE-DESC TALLYING PV-CHARACTER-COUNT             09070000
             FOR CHARACTERS BEFORE '*'.                                 09080000
           COMPUTE PV-POINTER =                                         09090000
                   (LENGTH OF PV-TYPE-DESC - PV-CHARACTER-COUNT) / 2.   09100000
           STRING PV-TYPE-DESC DELIMITED BY '*'                         09110000
               INTO HL2-TITLE                                           09120000
               WITH POINTER PV-POINTER.                                 09130000
      *----------------------------------------------------------------*09140000
      *  PRINT THE REPORT GRAND TOTALS                                 *09150000
      *----------------------------------------------------------------*09160000
       8000-GRAND-TOTAL.                                                09170000
           MOVE SPACES                       TO SL1-SUMMARY-LINE-1      09180000
                                                SL2-SUMMARY-LINE-2.     09190000
           MOVE PC-GRAND-LITERAL             TO SL1-COMPANY-LIT.        09200000
           MOVE PC-COMP-LITERAL              TO SL1-TOTAL-LIT.          09210000
           MOVE PA-PTD-TY-CO-COMP-QTY        TO SL1-PTD-TY-QTY.         09220000
           MOVE PA-PTD-TY-CO-COMP-RTL        TO SL2-PTD-TY-RTL.         09230000
           MOVE PA-PTD-LY-CO-COMP-QTY        TO SL1-PTD-LY-QTY.         09240000
           MOVE PA-PTD-LY-CO-COMP-RTL        TO SL2-PTD-LY-RTL.         09250000
           IF PA-PTD-TY-CO-COMP-QTY > 0 AND PA-PTD-LY-CO-COMP-QTY > 0   09260000
               COMPUTE PV-VARIANCE ROUNDED =                            09270000
                 ((PA-PTD-TY-CO-COMP-QTY - PA-PTD-LY-CO-COMP-QTY)       09280000
                                             / PA-PTD-LY-CO-COMP-QTY)   09290000
               MULTIPLY 100 BY PV-VARIANCE                              09300000
               MOVE PV-VARIANCE              TO SL1-PTD-VAR-QTY         09310000
           END-IF.                                                      09320000
           IF PA-PTD-TY-CO-COMP-RTL > 0 AND PA-PTD-LY-CO-COMP-RTL > 0   09330000
               COMPUTE PV-VARIANCE ROUNDED =                            09340000
                 ((PA-PTD-TY-CO-COMP-RTL - PA-PTD-LY-CO-COMP-RTL)       09350000
                                             / PA-PTD-LY-CO-COMP-RTL)   09360000
               MULTIPLY 100 BY PV-VARIANCE                              09370000
               MOVE PV-VARIANCE              TO SL2-PTD-VAR-RTL         09380000
           END-IF.                                                      09390000
           MOVE PA-YTD-TY-CO-COMP-QTY        TO SL1-YTD-TY-QTY.         09400000
           MOVE PA-YTD-TY-CO-COMP-RTL        TO SL2-YTD-TY-RTL.         09410000
           MOVE PA-YTD-LY-CO-COMP-QTY        TO SL1-YTD-LY-QTY.         09420000
           MOVE PA-YTD-LY-CO-COMP-RTL        TO SL2-YTD-LY-RTL.         09430000
           IF PA-YTD-TY-CO-COMP-QTY > 0 AND PA-YTD-LY-CO-COMP-QTY > 0   09440000
               COMPUTE PV-VARIANCE ROUNDED =                            09450000
                 ((PA-YTD-TY-CO-COMP-QTY - PA-YTD-LY-CO-COMP-QTY)       09460000
                                             / PA-YTD-LY-CO-COMP-QTY)   09470000
               MULTIPLY 100 BY PV-VARIANCE                              09480000
               MOVE PV-VARIANCE              TO SL1-YTD-VAR-QTY         09490000
           END-IF.                                                      09500000
           IF PA-YTD-TY-CO-COMP-RTL > 0 AND PA-YTD-LY-CO-COMP-RTL > 0   09510000
               COMPUTE PV-VARIANCE ROUNDED =                            09520000
                 ((PA-YTD-TY-CO-COMP-RTL - PA-YTD-LY-CO-COMP-RTL)       09530000
                                             / PA-YTD-LY-CO-COMP-RTL)   09540000
               MULTIPLY 100 BY PV-VARIANCE                              09550000
               MOVE PV-VARIANCE              TO SL2-YTD-VAR-RTL         09560000
           END-IF.                                                      09570000
           IF PV-LINE-COUNT > PC-PAGE-MAX - 6                           09580000
               PERFORM 9000-PRINT-HEADINGS                              09590000
           END-IF.                                                      09600000
           WRITE TRANSFER-REPORT-RECORD                                 09610000
               FROM SL1-SUMMARY-LINE-1                                  09620000
               AFTER ADVANCING 3.                                       09630000
           WRITE TRANSFER-REPORT-RECORD                                 09640000
               FROM SL2-SUMMARY-LINE-2                                  09650000
               AFTER ADVANCING 1.                                       09660000
           MOVE SPACES                       TO SL1-SUMMARY-LINE-1.     09670000
           MOVE PC-TOTAL-LITERAL             TO SL1-TOTAL-LIT.          09680000
           MOVE PA-PTD-TY-CO-QTY             TO SL1-PTD-TY-QTY.         09690000
           MOVE PA-PTD-TY-CO-RTL             TO SL2-PTD-TY-RTL.         09700000
           MOVE PA-PTD-LY-CO-QTY             TO SL1-PTD-LY-QTY.         09710000
           MOVE PA-PTD-LY-CO-RTL             TO SL2-PTD-LY-RTL.         09720000
           IF PA-PTD-TY-CO-QTY > 0 AND PA-PTD-LY-CO-QTY > 0             09730000
               COMPUTE PV-VARIANCE ROUNDED =                            09740000
                 ((PA-PTD-TY-CO-QTY - PA-PTD-LY-CO-QTY)                 09750000
                                             / PA-PTD-LY-CO-QTY)        09760000
               MULTIPLY 100 BY PV-VARIANCE                              09770000
               MOVE PV-VARIANCE              TO SL1-PTD-VAR-QTY         09780000
           END-IF.                                                      09790000
           IF PA-PTD-TY-CO-RTL > 0 AND PA-PTD-LY-CO-RTL > 0             09800000
               COMPUTE PV-VARIANCE ROUNDED =                            09810000
                 ((PA-PTD-TY-CO-RTL - PA-PTD-LY-CO-RTL)                 09820000
                                             / PA-PTD-LY-CO-RTL)        09830000
               MULTIPLY 100 BY PV-VARIANCE                              09840000
               MOVE PV-VARIANCE              TO SL2-PTD-VAR-RTL         09850000
           END-IF.                                                      09860000
           MOVE PA-YTD-TY-CO-QTY             TO SL1-YTD-TY-QTY.         09870000
           MOVE PA-YTD-TY-CO-RTL             TO SL2-YTD-TY-RTL.         09880000
           MOVE PA-YTD-LY-CO-QTY             TO SL1-YTD-LY-QTY.         09890000
           MOVE PA-YTD-LY-CO-RTL             TO SL2-YTD-LY-RTL.         09900000
           IF PA-YTD-TY-CO-QTY > 0 AND PA-YTD-LY-CO-QTY > 0             09910000
               COMPUTE PV-VARIANCE ROUNDED =                            09920000
                 ((PA-YTD-TY-CO-QTY - PA-YTD-LY-CO-QTY)                 09930000
                                             / PA-YTD-LY-CO-QTY)        09940000
               MULTIPLY 100 BY PV-VARIANCE                              09950000
               MOVE PV-VARIANCE              TO SL1-YTD-VAR-QTY         09960000
           END-IF.                                                      09970000
           IF PA-YTD-TY-CO-RTL > 0 AND PA-YTD-LY-CO-RTL > 0             09980000
               COMPUTE PV-VARIANCE ROUNDED =                            09990000
                 ((PA-YTD-TY-CO-RTL - PA-YTD-LY-CO-RTL)                 10000000
                                             / PA-YTD-LY-CO-RTL)        10010000
               MULTIPLY 100 BY PV-VARIANCE                              10020000
               MOVE PV-VARIANCE              TO SL2-YTD-VAR-RTL         10030000
           END-IF.                                                      10040000
           WRITE TRANSFER-REPORT-RECORD                                 10050000
               FROM SL1-SUMMARY-LINE-1                                  10060000
               AFTER ADVANCING 2.                                       10070000
           WRITE TRANSFER-REPORT-RECORD                                 10080000
               FROM SL2-SUMMARY-LINE-2                                  10090000
               AFTER ADVANCING 1.                                       10100000
      ******************************************************************10110000
      *  PRINT REPORT HEADINGS                                          10120000
      ******************************************************************10130000
                                                                        10140000
       9000-PRINT-HEADINGS.                                             10150000
           ADD 1 TO PV-PAGE-COUNT.                                      10160000
           MOVE 8             TO PV-LINE-COUNT.                         10170000
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    10180000
           WRITE TRANSFER-REPORT-RECORD                                 10190000
               FROM DP132O-STANDRD-HEADER-132-COLS                      10200000
               AFTER ADVANCING PAGE.                                    10210000
           WRITE TRANSFER-REPORT-RECORD                                 10220000
               FROM HL2-HEADING-LINE-2                                  10230000
               AFTER ADVANCING 1.                                       10240000
           WRITE TRANSFER-REPORT-RECORD                                 10250000
               FROM HL3-HEADING-LINE-3                                  10260000
               AFTER ADVANCING 1.                                       10270000
           WRITE TRANSFER-REPORT-RECORD                                 10280000
               FROM HL4-HEADING-LINE-4                                  10290000
               AFTER ADVANCING 2.                                       10300000
           WRITE TRANSFER-REPORT-RECORD                                 10310000
               FROM HL5-HEADING-LINE-5                                  10320000
               AFTER ADVANCING 1.                                       10330000
           WRITE TRANSFER-REPORT-RECORD                                 10340000
               FROM HL6-HEADING-LINE-6                                  10350000
               AFTER ADVANCING 1.                                       10360000
           WRITE TRANSFER-REPORT-RECORD                                 10370000
               FROM BLANK-LINE                                          10380000
               AFTER ADVANCING 1.                                       10390000
      ******************************************************************10400000
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *10410000
      *  PROGRAM.                                                      *10420000
      ******************************************************************10430000
       9100-SQL-ERROR.                                                  10440000
           MOVE SQLCODE TO SQLCODE2.                                    10450000
           MOVE SQLERRD(3) TO SQLERRD3.                                 10460000
           DISPLAY '** ABEND **'.                                       10470000
           DISPLAY 'TFKRP470 - SQL ERROR'.                              10480000
           DISPLAY '       SQLCODE = ' SQLCODE2.                        10490000
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        10500000
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        10510000
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        10520000
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        10530000
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        10540000
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        10550000
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        10560000
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        10570000
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        10580000
           DISPLAY                    '** PARAGRAPH ** = '              10590000
                                       PV-PARAGRAPH-NAME.               10600000
           DISPLAY                    '** OPERATION ** = '              10610000
                                       PV-DB2-OPERATION.                10620000
                                                                        10630000
           EXEC SQL                                                     10640000
               COMMIT                                                   10650000
           END-EXEC.                                                    10660000
                                                                        10670000
           MOVE +4013 TO ABEND-CODE.                                    10680000
           CALL 'ILBOABN0' USING ABEND-CODE.                            10690000
                                                                        10700000
