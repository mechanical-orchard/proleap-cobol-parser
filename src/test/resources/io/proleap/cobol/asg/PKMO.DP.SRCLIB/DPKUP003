      *                                                                         
       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.    DPKUP003.                                                 
      *                                                                         
       AUTHOR.        CALVIN BUSH.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/28/98.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      *****************************************************************         
      ** THIS PROGRAM READS AN INPUT FILE CONTAINING USER INFORMATION           
      ** INCLUDING DEFAULT PRINTER INFORMATION GATHERED FROM RACF AND           
      ** INFOPAC.  THE DATA IS MATCH/MERGED WITH DATA IN THE TUSER              
      ** DB2 TABLE.  THE DB2 TABLE IS USED BY THE CICS ARCHITECTURE             
      ** TO PROVIDE THE DEFAULT PRINTER TO CICS PROGRAMS.                       
      **                                                                        
      ** CHANGE LOG:                                                            
      **   DATE    BY               DESCRIPTION                                 
      ** 04/20/98  CAL BUSH         INITIAL INSTALLATION.                       
      **                                                                        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT INPUT-USER-FILE           ASSIGN TO INP01.                    
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  INPUT-USER-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-USER-RECORD             PIC  X(80).                            
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  ABEND-CODE                    PIC S9(04)  VALUE +0                   
                                                     COMP                       
                                                     SYNC.                      
      *                                                                         
       01  DK-DB2-KEYS.                                                         
           05  DK-USER-CODE              PIC  X(08)  VALUE SPACE.               
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'DPKUP003'.          
           05  PC-COMMA                  PIC  X(01)  VALUE ','.                 
      *                                                                         
       01  PR-PRINT-FORMAT-VARIABLES.                                           
           05  PR-INPUT-USER-CNT         PIC  ZZZZ9  VALUE ZERO.                
           05  PR-ROWS-UPDATED           PIC  ZZZZ9  VALUE ZERO.                
           05  PR-ROWS-DELETED           PIC  ZZZZ9  VALUE ZERO.                
           05  PR-ROWS-INSERTED          PIC  ZZZZ9  VALUE ZERO.                
           05  PR-DB2-RECS-READ          PIC  ZZZZ9  VALUE ZERO.                
                                                                                
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-USER-FILE-SW       PIC  X(01)  VALUE 'N'.                 
               88 PS-END-OF-USER-FILE                VALUE 'Y'.                 
           05  PS-END-USER-TABLE-SW     PIC  X(01)  VALUE 'N'.                  
               88 PS-END-OF-USER-TABLE              VALUE 'Y'.                  
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INPUT-USER-CNT         PIC S9(05)  COMP-3                     
                                                     VALUE +0.                  
           05  PV-DB2-RECS-READ          PIC S9(05)  COMP-3                     
                                                     VALUE +0.                  
           05  PV-ROWS-UPDATED           PIC S9(09)  COMP SYNC                  
                                                     VALUE +0.                  
           05  PV-ROWS-DELETED           PIC S9(09)  COMP SYNC                  
                                                     VALUE +0.                  
           05  PV-ROWS-INSERTED          PIC S9(09)  COMP SYNC                  
                                                     VALUE +0.                  
           05  PV-UNSTRING-PTR           PIC S9(04)  COMP SYNC                  
                                                     VALUE ZERO.                
           05  PV-UNSTRING-DELIM         PIC  X(01)  VALUE SPACE.               
           05  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACE.               
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                         PIC  X(30)  VALUE SPACE.               
           05  PV-FIRST-NAME             PIC  X(40)  VALUE SPACE.               
           05  PV-LAST-NAME              PIC  X(40)  VALUE SPACE.               
           05  PV-MID-INIT               PIC  X(01)  VALUE SPACE.               
           05  PV-LAST-UID               PIC  X(08)  VALUE SPACE.               
           05  PV-SAVE-TUSER             PIC  X(128) VALUE SPACE.               
      *                                                                         
      **   INPUT RECORD LAYOUT FOR INPUT USERID FILE                            
      *                                                                         
      *                                                                         
      ****************************************************************          
      ** THIS IS THE RECORD LAYOUT FOR THE INPUT RECORD USED BY THIS**          
      ** PROGRAM.                                                   **          
      ****************************************************************          
       01  UID-I-INPUT-RECORD.                                                  
           05  UID-I-USERID              PIC  X(08)  VALUE SPACE.               
           05  FILLER                    PIC  X(01)  VALUE SPACE.               
           05  UID-I-NAME                PIC  X(40)  VALUE SPACE.               
           05  FILLER                    PIC  X(01)  VALUE SPACE.               
           05  UID-I-TYPE-CODE           PIC  X(01)  VALUE SPACE.               
               88  UID-I-TYPE-GROUP                  VALUE 'G'.                 
               88  UID-I-TYPE-USER                   VALUE 'U'.                 
           05  FILLER                    PIC  X(01)  VALUE SPACE.               
           05  UID-I-REVOKE-IND          PIC  X(01)  VALUE SPACE.               
           05  FILLER                    PIC  X(01)  VALUE SPACE.               
           05  UID-I-DFLT-PRTR           PIC  X(08)  VALUE SPACE.               
           05  FILLER                    PIC  X(18)  VALUE SPACE.               
                                                                                
                                                                                
           COPY DPWS004.                                                        
      *                                                                         
      **   TUSER DB2 TABLE LAYOUT                                               
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TUSER                                                    
           END-EXEC.                                                            
      *                                                                         
      **   DB2 SQL COMM-AREA                                                    
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      **   TUSER CURSOR                                                         
      *                                                                         
           EXEC SQL                                                             
               DECLARE TUSER_CSR CURSOR FOR                                     
                   SELECT USER_CODE,                                            
                          INITIAL_MNMNC_CODE,                                   
                          DFLT_PRTR_ID,                                         
                          USR_LAST_NM,                                          
                          USR_1ST_NM,                                           
                          USR_MID_INIT_NM                                       
                   FROM TUSER                                                   
                   WHERE USER_CODE > :DK-USER-CODE                              
                   ORDER BY USER_CODE                                           
           END-EXEC.                                                            
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.             
      *****************************************************************         
       0000-MAIN-DRIVER.                                                        
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-MATCH-FILES                                             
               UNTIL (PS-END-OF-USER-FILE                                       
                 AND PS-END-OF-USER-TABLE).                                     
           PERFORM 3000-TERMINATION.                                            
           STOP RUN.                                                            
      *****************************************************************         
      **  THIS MODULE WILL OPEN FILES AND DO INITIAL READS ON THE TWO           
      **  INPUT FILES.                                                          
      *****************************************************************         
       1000-INITIALIZATION.                                                     
           DISPLAY '************ DPKUP003 ***********'.                         
           OPEN INPUT  INPUT-USER-FILE.                                         
           PERFORM 2100-READ-USER-FILE.                                         
           PERFORM 1100-OPEN-USER-CSR.                                          
           PERFORM 2200-FETCH-USER-CSR.                                         
      *****************************************************************         
      **  OPEN THE CURSOR FOR THE TUSER TABLE.                                  
      *****************************************************************         
       1100-OPEN-USER-CSR.                                                      
           EXEC SQL                                                             
               OPEN TUSER_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE NOT = 0                                               
             WHEN SQLWARN0    = 'W'                                             
               MOVE '1100-OPEN-USER-CSR' TO PV-PARAGRAPH-NAME                   
               MOVE 'OPEN TUSER_CSR'      TO PV-DB2-OPERATION-ATTEMPTED         
               PERFORM 9000-SQL-ABEND                                           
           END-EVALUATE.                                                        
      ****************************************************************          
      **  THIS MODULE WILL MATCH RECORDS FROM THE INPUT FILE AND THE            
      **  DB2 TABLE ON USERID.  WHEN THERE IS A MATCH, ALL FIELDS ARE           
      **  CHECKED.  IF THERE IS A DIFFERENCE IN ANY OF THEM, THE                
      **  TABLE IS UPDATED.  IF A USER IS MISSING FROM THE TABLE,               
      **  THE USER IS INSERTED.  IF A USER IS MISSING FROM THE FILE,            
      **  THE USER IS DELETED FROM DB2.                                         
      ****************************************************************          
       2000-MATCH-FILES.                                                        
           PERFORM 2900-FORMAT-NAME.                                            
           IF PV-LAST-UID = UID-I-USERID                                        
               PERFORM 2400-UPDATE-USER-RECORD                                  
               PERFORM 2100-READ-USER-FILE                                      
               PERFORM 2200-FETCH-USER-CSR                                      
           ELSE                                                                 
               IF  PV-LAST-UID > UID-I-USERID                                   
                   MOVE DCLTUSER              TO PV-SAVE-TUSER                  
                   PERFORM 2500-CREATE-USER-RECORD                              
                   MOVE PV-SAVE-TUSER         TO DCLTUSER                       
                   PERFORM 2100-READ-USER-FILE                                  
               ELSE                                                             
                   IF  PV-LAST-UID < UID-I-USERID                               
                       PERFORM 2600-DELETE-USER                                 
                       PERFORM 2200-FETCH-USER-CSR                              
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      ****************************************************************          
      **  THIS PARAGRAPH READS THE INPUT USERID / PRINTER FILE.                 
      ****************************************************************          
       2100-READ-USER-FILE.                                                     
           READ INPUT-USER-FILE INTO UID-I-INPUT-RECORD                         
               AT END                                                           
                   MOVE 99999999               TO UID-I-USERID                  
                   SET PS-END-OF-USER-FILE     TO TRUE.                         
      *                                                                         
           IF  NOT PS-END-OF-USER-FILE                                          
               ADD +1                          TO PV-INPUT-USER-CNT             
           END-IF.                                                              
      ****************************************************************          
      **  THIS PARAGRAPH READS THE DB2 TABLE.                                   
      ****************************************************************          
       2200-FETCH-USER-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH TUSER_CSR                                                  
                 INTO :USER-USER-CODE,                                          
                      :USER-INITIAL-MNMNC-CODE,                                 
                      :USER-DFLT-PRTR-ID,                                       
                      :USER-USR-LAST-NM,                                        
                      :USER-USR-1ST-NM,                                         
                      :USER-USR-MID-INIT-NM                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                 ADD +1 TO PV-DB2-RECS-READ                                     
                 MOVE USER-USER-CODE TO PV-LAST-UID                             
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-USER-TABLE TO TRUE                             
                   MOVE '99999999'           TO PV-LAST-UID                     
               WHEN OTHER                                                       
                   MOVE '2200-FETCH-USER-CSR           '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH NEXT ROW FROM TUSER     '                        
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ****************************************************************          
      **  CREATE A NEW TUSER ROW                                                
      ****************************************************************          
       2400-UPDATE-USER-RECORD.                                                 
           IF  (USER-USR-LAST-NM = PV-LAST-NAME)                                
             AND (USER-USR-1ST-NM = PV-FIRST-NAME)                              
             AND (USER-USR-MID-INIT-NM = PV-MID-INIT)                           
             AND (USER-DFLT-PRTR-ID = UID-I-DFLT-PRTR)                          
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY 'UPDATE: ' UID-I-USERID                                  
               MOVE UID-I-USERID        TO USER-USER-CODE                       
               MOVE SPACE               TO USER-INITIAL-MNMNC-CODE              
               MOVE UID-I-DFLT-PRTR     TO USER-DFLT-PRTR-ID                    
               MOVE PV-LAST-NAME        TO USER-USR-LAST-NM                     
               MOVE PV-FIRST-NAME       TO USER-USR-1ST-NM                      
               MOVE PV-MID-INIT         TO USER-USR-MID-INIT-NM                 
               PERFORM 2410-UPDATE-TUSER                                        
           END-IF.                                                              
      ****************************************************************          
      **  UPDATE THE CURRENT ROW ON THE TUSER TABLE.                            
      ****************************************************************          
       2410-UPDATE-TUSER.                                                       
           EXEC SQL                                                             
               UPDATE TUSER                                                     
               SET DFLT_PRTR_ID =    :USER-DFLT-PRTR-ID,                        
                   USR_LAST_NM =     :USER-USR-LAST-NM,                         
                   USR_1ST_NM =      :USER-USR-1ST-NM,                          
                   USR_MID_INIT_NM = :USER-USR-MID-INIT-NM,                     
                   LAST_UPDATE_TMESTP = CURRENT TIMESTAMP                       
               WHERE USER_CODE = :USER-USER-CODE                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    ADD +1 TO PV-ROWS-UPDATED                                   
               WHEN OTHER                                                       
                   MOVE '2410-UPDATE-TUSER'                                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'UPDATE A ROW IN TUSER'                                 
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ****************************************************************          
      **  CREATE A NEW TUSER ROW                                                
      ****************************************************************          
       2500-CREATE-USER-RECORD.                                                 
           DISPLAY 'INSERT: ' UID-I-USERID.                                     
           MOVE UID-I-USERID        TO USER-USER-CODE.                          
           MOVE SPACE               TO USER-INITIAL-MNMNC-CODE.                 
           MOVE UID-I-DFLT-PRTR     TO USER-DFLT-PRTR-ID.                       
           MOVE PV-LAST-NAME        TO USER-USR-LAST-NM.                        
           MOVE PV-FIRST-NAME       TO USER-USR-1ST-NM.                         
           MOVE PV-MID-INIT         TO USER-USR-MID-INIT-NM.                    
           PERFORM 2510-INSERT-USER-ROW.                                        
                                                                                
      ****************************************************************          
      **  INSERT A ROW INTO THE TUSER TABLE.                                    
      ****************************************************************          
       2510-INSERT-USER-ROW.                                                    
           EXEC SQL                                                             
               INSERT INTO TUSER                                                
                   (USER_CODE,                                                  
                    INITIAL_MNMNC_CODE,                                         
                    DFLT_PRTR_ID,                                               
                    USR_LAST_NM,                                                
                    USR_1ST_NM,                                                 
                    USR_MID_INIT_NM)                                            
               VALUES                                                           
                   (:USER-USER-CODE,                                            
                    :USER-INITIAL-MNMNC-CODE,                                   
                    :USER-DFLT-PRTR-ID,                                         
                    :USER-USR-LAST-NM,                                          
                    :USER-USR-1ST-NM,                                           
                    :USER-USR-MID-INIT-NM)                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1 TO PV-ROWS-INSERTED                                   
               WHEN OTHER                                                       
                   MOVE '2510-INSERT-USER-ROW'                                  
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'INSERT NEW ROW IN TUSER       '                        
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ****************************************************************          
      **  DELETE THE CURRENT TUSER ROW.                                         
      ****************************************************************          
       2600-DELETE-USER.                                                        
           DISPLAY 'DELETE: ' USER-USER-CODE                                    
           EXEC SQL                                                             
               DELETE                                                           
               FROM TUSER                                                       
               WHERE USER_CODE = :USER-USER-CODE                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = 0                                                 
                   ADD +1 TO PV-ROWS-DELETED                                    
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                   MOVE '2600-DELETE-USER'                                      
                                     TO  PV-PARAGRAPH-NAME                      
                   MOVE 'DELETE CURRENT OF CURSOR'                              
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ****************************************************************          
      **  THE INPUT NAME FIELD CONTAINS THE COMMA-DELIMTED NAME.    **          
      **  THIS SPLITS IT INTO THREE FIELDS BASED ON THE DELIMITER   **          
      ****************************************************************          
       2900-FORMAT-NAME.                                                        
           MOVE SPACE                    TO PV-LAST-NAME                        
                                            PV-FIRST-NAME                       
                                            PV-MID-INIT.                        
           MOVE 1                        TO PV-UNSTRING-PTR.                    
           UNSTRING UID-I-NAME                                                  
               DELIMITED BY PC-COMMA                                            
                     OR ALL SPACES                                              
               INTO         PV-LAST-NAME                                        
               DELIMITER IN PV-UNSTRING-DELIM                                   
               POINTER      PV-UNSTRING-PTR                                     
           END-UNSTRING.                                                        
      *                                                                         
           UNSTRING UID-I-NAME                                                  
               DELIMITED BY PC-COMMA                                            
                     OR ALL SPACES                                              
               INTO         PV-FIRST-NAME                                       
               DELIMITER IN PV-UNSTRING-DELIM                                   
               POINTER      PV-UNSTRING-PTR                                     
           END-UNSTRING.                                                        
      *                                                                         
           UNSTRING UID-I-NAME                                                  
               DELIMITED BY PC-COMMA                                            
                     OR ALL SPACES                                              
               INTO         PV-MID-INIT                                         
               DELIMITER IN PV-UNSTRING-DELIM                                   
               POINTER      PV-UNSTRING-PTR                                     
           END-UNSTRING.                                                        
      ****************************************************************          
      **  THIS PARAGRAPH DISPLAYS INPUT AND OUTPUT RECORD TOTALS AND            
      **  THEN CLOSES ALL FILES.                                                
      ****************************************************************          
       3000-TERMINATION.                                                        
           MOVE PV-INPUT-USER-CNT        TO PR-INPUT-USER-CNT.                  
           MOVE PV-DB2-RECS-READ         TO PR-DB2-RECS-READ.                   
           MOVE PV-ROWS-UPDATED          TO PR-ROWS-UPDATED.                    
           MOVE PV-ROWS-DELETED          TO PR-ROWS-DELETED.                    
           MOVE PV-ROWS-INSERTED         TO PR-ROWS-INSERTED.                   
           DISPLAY 'INPUT USER RECORDS  = ' PR-INPUT-USER-CNT.                  
           DISPLAY 'INPUT TUSER RECORDS = ' PR-DB2-RECS-READ.                   
           DISPLAY 'TUSER ROWS INSERTED = ' PR-ROWS-INSERTED.                   
           DISPLAY 'TUSER ROWS UPDATED  = ' PR-ROWS-UPDATED.                    
           DISPLAY 'TUSER ROWS DELETED  = ' PR-ROWS-DELETED.                    
      *                                                                         
           CLOSE INPUT-USER-FILE.                                               
           PERFORM 3100-CLOSE-USER-CSR.                                         
           PERFORM 3200-COMMIT.                                                 
      *****************************************************************         
      **  OPEN THE CURSOR FOR THE TUSER TABLE.                                  
      *****************************************************************         
       3100-CLOSE-USER-CSR.                                                     
           EXEC SQL                                                             
               CLOSE TUSER_CSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE NOT = 0                                               
             WHEN SQLWARN0    = 'W'                                             
               MOVE '3100-CLOSE-USER-CSR' TO PV-PARAGRAPH-NAME                  
               MOVE 'CLOSE TUSER CSR'      TO PV-DB2-OPERATION-ATTEMPTED        
               PERFORM 9000-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *********************************************************                 
      ** COMMIT THE CHANGES TO DB2                           **                 
      *********************************************************                 
       3200-COMMIT.                                                             
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
             WHEN SQLCODE NOT = 0                                               
             WHEN SQLWARN0    = 'W'                                             
               MOVE '3200-COMMIT'          TO PV-PARAGRAPH-NAME                 
               MOVE 'COMMIT'               TO PV-DB2-OPERATION-ATTEMPTED        
               PERFORM 9000-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *****************************************************************         
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                *         
      *                                                               *         
      *****************************************************************         
       9000-SQL-ABEND.                                                          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.                       
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.            
      *****************************************************************         
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-  *         
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE       *         
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE   *         
      * FORMAT.                                                       *         
      *****************************************************************         
           COPY DPPD004.                                                        
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
