       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP024.                                         00020019
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050014
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070020
      **       VSM ITEM MESSAGE - NUMBER 087 - WEB ITEM ATTRIBUTES CHG  00080020
      ******************************************************************00090020
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100026
      ***     ATTRIBUTES ON XST_SKU:                                    00110026
      ***     1. E_BUS_PRD_GP_NBR                                       00120026
      ******************************************************************00130020
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00140020
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00150025
      ******************************************************************00160020
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.  IBM-3090.                                      00190000
       OBJECT-COMPUTER.  IBM-3090.                                      00200000
       DATA DIVISION.                                                   00210000
                                                                        00220005
       WORKING-STORAGE SECTION.                                         00230000
      ******************************************************************00240005
      **PROGRAM SWITCHES                                                00250005
      ******************************************************************00260005
       01  PS-PROGRAM-SWITCHES.                                         00270005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00280005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00290005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00300005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00310022
               88  PS-ACTION-REVERSE                  VALUE 01.         00320022
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00330022
               88  PS-ACTION-NOTHING                  VALUE 02.         00340022
      ******************************************************************00350000
      **PROGRAM VARIABLES                                               00360000
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00400007
      ******************************************************************00410019
      **PROGRAM CALCULATIONS                                            00420019
      ******************************************************************00430019
       01  PC-PROGRAM-CALCULATIONS.                                     00440019
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00450019
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00460019
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00470019
      ******************************************************************00480000
      **PROGRAM VARIABLES                                               00490021
      ******************************************************************00500000
       01  PV-MISC-ABEND-FIELDS.                                        00510021
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00520021
      ******************************************************************00530000
      **COPYBOOKS                                                       00540000
      ******************************************************************00550000
      ***VSM ITEM COPYBOOK                                              00560005
           COPY IMRD008.                                                00570027
      ***ABEND COPYBOOK                                                 00580005
           COPY DPWS004.                                                00590027
      ***DB2 MESSAGE AREA                                               00600000
           COPY SQLCA2.                                                 00610027
      ******************************************************************00620000
      **TABLE INCLUSIONS                                                00630000
      ******************************************************************00640000
      ***DB2 COMMUNICATIONS AREA                                        00650000
           EXEC SQL                                                     00660000
                INCLUDE SQLCA                                           00670000
           END-EXEC.                                                    00680000
      ***XST_SKU TABLE                                                  00690019
           EXEC SQL                                                     00700000
               INCLUDE XST00010                                         00710019
           END-EXEC.                                                    00720000
                                                                        00730000
       LINKAGE SECTION.                                                 00740000
      ******************************************************************00750000
      **LINKAGE VARIABLES                                               00760000
      ******************************************************************00770000
       01  LV-IMRD008-RECORD               PIC X(450).                  00780005
       01  LV-RETURN-CODE                  PIC S9(04).                  00790005
       01  LV-ACTION-IND                   PIC 9(02).                   00800022
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00810022
                                                                        00820000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00830005
                                LV-RETURN-CODE,                         00840022
                                LV-ACTION-IND,                          00850022
                                LV-CURRENT-TIMESTAMP.                   00860022
      ******************************************************************00870000
       0000-MAINLINE.                                                   00880000
                                                                        00890000
            PERFORM 1000-INITIALIZATION.                                00900000
            PERFORM 2000-PROCESS-MESSAGE                                00910014
                    UNTIL PS-PROCESS-COMPLETE.                          00920005
            PERFORM 9999-WRAPUP.                                        00930000
                                                                        00940000
       EJECT.                                                           00950005
      ***************************************************************** 00960000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00970007
      ***************************************************************** 00980000
       1000-INITIALIZATION.                                             00990000
                                                                        01000000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    01010021
                                                                        01020000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01030008
           INITIALIZE PC-PROGRAM-COUNTERS.                              01040020
                                                                        01050008
           MOVE LV-IMRD008-RECORD             TO IMRD008-RECORD.        01060014
                                                                        01070020
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01080019
      *     COMP FIELDS.                                                01090019
           MOVE IM008-ITEM-KEY-INTR-UPC-ID   TO PC-10-DIGIT.            01100019
           MOVE PC-10-DIGIT                  TO PC-18-DIGIT.            01110019
           MOVE PC-18-DIGIT                  TO PC-09-DIGIT.            01120019
           MOVE PC-09-DIGIT                  TO XST00010-INTR-UPC-ID.   01130019
                                                                        01140020
           MOVE IM008-WEB-ATTR-PRD-GP-NBR TO XST00010-E-BUS-PRD-GP-NBR. 01150019
121402     MOVE LV-CURRENT-TIMESTAMP          TO XST00010-LAST-UPD-TMST.01160025
121402     MOVE IM008-CRE-TMST          TO XST00010-SOR-LAST-MSG-TMST.  01170025
                                                                        01180017
       EJECT.                                                           01190005
      ***************************************************************** 01200000
      **UPDATES WEB ITEM ATTRIBUTES ON XST_VND_STYL                     01210026
      ***************************************************************** 01220000
       2000-PROCESS-MESSAGE.                                            01230014
                                                                        01240000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01250021
                                                                        01260005
           EXEC SQL                                                     01270005
              UPDATE XST_SKU                                            01280019
                  SET   E_BUS_PRD_GP_NBR = :XST00010-E-BUS-PRD-GP-NBR   01290019
                      , LAST_UPD_TMST    = :XST00010-LAST-UPD-TMST      01300025
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01310025
                  WHERE INTR_UPC_ID      = :XST00010-INTR-UPC-ID        01320019
           END-EXEC.                                                    01330005
                                                                        01340005
           EVALUATE TRUE                                                01350005
      ***SUCCESSFUL PROCESSING                                          01360026
              WHEN SQLCODE = ZERO                                       01370005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01380008
      ***ROW/TABLE LOCK                                                 01390026
              WHEN SQLCODE = -911                                       01400005
              WHEN SQLCODE = -913                                       01410007
                   ADD +1                      TO PC-RETRY-COUNTER      01420007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01430007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01440007
                   ELSE                                                 01450007
                       ADD +1                  TO PC-RETRY-COUNTER      01460007
                   END-IF                                               01470007
              WHEN OTHER                                                01480005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01490005
           END-EVALUATE.                                                01500005
                                                                        01510000
       EJECT.                                                           01520005
      ******************************************************************01530000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01540000
      ******************************************************************01550000
       9999-WRAPUP.                                                     01560000
                                                                        01570000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01580024
                                                                        01590000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01600024
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01610022
                                                                        01620022
           EXIT PROGRAM.                                                01630000
                                                                        01640000
       EJECT.                                                           01650005
