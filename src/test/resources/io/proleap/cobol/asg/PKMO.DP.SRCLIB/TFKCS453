      *-----------------------*                                         00010001
       IDENTIFICATION DIVISION.                                         00020001
      *-----------------------*                                         00030001
       PROGRAM-ID.    TFKCS453.                                         00040001
       AUTHOR.        JACK KRAMER.                                      00050001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060001
       DATE-WRITTEN.  MAY 1997.                                         00070001
       DATE-COMPILED.                                                   00080001
      *----------------------------------------------------------------*00090001
      *    P453 - RECEIVE STORE OVERRIDE.                              *00100001
      *                                                                *00110001
      *    THIS PROGRAM ALLOWS THE USER TO ENTER A RECEIVE STORE NMBR  *00120001
      *    TO BE USED IN RECEIVING A TRANSFER AT A STORE OTHER THAN    *00130001
      *    THEIR DEFAULT STORE LOCATION - USED ONLY BY CORPORATE AND   *00140001
      *    DC PERSONNEL WHO HAVE SECURITY ACCESS.                      *00150001
      *----------------------------------------------------------------*00160001
100404**DATE:10/04/2004 CHANGED BY: RAY GRAF                            00161002
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                00162002
060110******************************************************************00162109
060110*  DATE 06/01/10 JACKIE HELING                                    00162209
060110*  BLOCK CERTAIN LOCATIONS FROM SENDING/RECEIVING TRANSFERS.      00162309
      ***************************************************************** 00163002
033111* WR39649 -  DAVID WELLER - 03/31/2011                            00164010
033111*            MLOF - DO NOT EXCLUDE LOCATIONS 807, 808 AND 809     00165010
033111***************************************************************** 00166010
CICS  * CHANGED 11/16/2022    BY JIM HUNTER      WR: CHG0278345         00167011
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL        00168011
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.       00169011
CICS  *-----------------------------------------------------------------00169111
       ENVIRONMENT DIVISION.                                            00170001
                                                                        00180001
       DATA DIVISION.                                                   00190001
                                                                        00200001
       WORKING-STORAGE SECTION.                                         00210001
      *                                                                 00220001
       01  DK-DB2-KEYS.                                                 00230001
           05  DK-STORE-NBR                   PIC  S9(4)  VALUE ZERO    00240001
                                                          COMP.         00250001
      *                                                                 00260001
       01  PC-PROGRAM-CONSTANTS.                                        00270001
           05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TF453A  '. 00280001
           05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TFKM453 '. 00290001
           05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TFKCS453'. 00300001
           05  PC-DEPT-STORE             PIC  X(02)  VALUE  'DS'.       00360001
           05  PC-TSYMSG-NUMBERS.                                       00400001
               10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.      00410001
               10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.      00420001
               10  PC-TSYMSG-00017       PIC  9(05)  VALUE  00017.      00430001
060110         10  PC-TSYMSG-01552       PIC  9(05)  VALUE  01552.      00440009
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       00460001
                                                          COMP-3.       00470001
      *                                                                 00480001
       01  PV-PROGRAM-VARABLES.                                         00490001
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE +0      00500001
                                                          COMP SYNC.    00510001
      *                                                                 00520001
       01  PS-PROGRAM-SWITCHES.                                         00530001
           05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        00540001
               88  PS-ERROR                          VALUE  'Y'.        00550001
               88  PS-NO-ERROR                       VALUE  'N'.        00560001
           05  PS-STORE-FOUND-SW              PIC  X(01)  VALUE 'Y'.    00570001
               88  PS-STORE-FOUND                         VALUE 'Y'.    00580001
               88  PS-STORE-NOT-FOUND                     VALUE 'N'.    00590001
      *                                                                 00600001
       01  PM-PROGRAM-MESSAGES.                                         00610001
           05  PM-ABEND-INFO-A.                                         00620001
               10  FILLER                PIC X(15)   VALUE              00630001
                                         'DP020-SRC-AID: '.             00640001
               10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.      00650001
               10  FILLER                PIC X(19)   VALUE              00660001
                                         '. DP020-FK-ACTION: '.         00670001
               10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.      00680001
               10  FILLER                PIC X(22)   VALUE              00690001
                                         '. DP020-FK-DISPN-IND: '.      00700001
               10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.      00710001
               10  FILLER                PIC X(01)   VALUE '.'.         00720001
               10  FILLER                PIC X(19)   VALUE SPACE.       00730001
                                                                        00740001
      *---------------------------------------------------------------* 00741003
      *    DB2 AREA FOR XST_LOC TABLE                                 * 00742003
      *---------------------------------------------------------------* 00743003
           EXEC SQL                                                     00744003
                INCLUDE XST00001                                        00745003
           END-EXEC.                                                    00746003
                                                                        00750001
      *----------------------------------------------------------------*00760001
      *    MAP LAYOUT                                                  *00770001
      *----------------------------------------------------------------*00780001
                                                                        00790001
           COPY TFKM453.                                                00800001
           EJECT                                                        00810001
      *                                                                 00820001
060110*----------------------------------------------------------------*00821009
060110*  LIST OF LOCATION EXCLUSIONS                                    00822009
060110*----------------------------------------------------------------*00823009
060110     COPY TFWS010.                                                00824009
                                                                        00825005
      *----------------------------------------------------------------*00830001
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *00840001
      *----------------------------------------------------------------*00850001
                                                                        00860001
           COPY DPWS015.                                                00870001
                                                                        00880001
      *----------------------------------------------------------------*00890001
      *    FUNCTION KEYS COPYBOOK                                      *00900001
      *----------------------------------------------------------------*00910001
                                                                        00920001
           COPY DPWS016.                                                00930001
       EJECT                                                            00940001
      ******************************************************************00950001
      ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **00960001
      ** SUB-ROUTINE (DPKUT100).                                      **00970001
      ******************************************************************00980001
           COPY DPWS010I.                                               00990001
       EJECT                                                            01000001
      ******************************************************************01010001
      ** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **01020001
      ** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **01030001
      ******************************************************************01040001
           COPY DPWS030.                                                01050001
CICS       COPY DPWS930.                                                01051011
       EJECT                                                            01060001
      ***************************************************************** 01070001
      *    STANDARD COMMAREA.                                           01080001
      ***************************************************************** 01090001
                                                                        01100001
           COPY DPWS020.                                                01110001
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                01120001
                                                                        01130001
      ***************************************************************** 01140001
      *    INTER-APPLICATION COMMAREA.                                  01150001
      ***************************************************************** 01160001
                                                                        01170001
               10  INTER-APPL-COMM-AREA PIC X(200).                     01180001
      *---------------------------------------------------------------* 01190001
      * INTER-APPLICATION COMMAREA.                                   * 01200001
      *   TFWS006 - IS THE INPUT LAYOUT WHEN COMING FROM THE          * 01250001
      *             RECEIVE TRANSFER PROGRAM AND IS ALSO THE          * 01260001
      *             OUTPUT LAYOUT THAT WILL BE RETURNED TO THAT       * 01270001
      *             PROGRAM.                                          * 01280001
      *---------------------------------------------------------------* 01290001
           COPY TFWS006.                                                01310001
      ***************************************************************** 01320001
      *    SPECIFIC COMMMAREA FOR TFKCS453.                             01330001
      ***************************************************************** 01340001
               10  ASC-SPECIFIC-COMMAREA.                               01350001
                   15  ASC-STORE-NBR-X.                                 01360001
100404*                20  ASC-STORE-NBR           PIC  9(03).          01370002
100404                 20  ASC-STORE-NBR           PIC  9(04).          01371004
                   15  ASC-STORE-NAM               PIC  X(10).          01380001
           EJECT                                                        01400001
      *----------------------------------------------------------------*01410001
      *    ABEND PROCESSING COPYBOOK.                                  *01420001
      *----------------------------------------------------------------*01430001
                                                                        01440001
           COPY DPWS013.                                                01450001
                                                                        01460001
      *---------------------------------------------------------------* 01530001
      *    DB2 AREA FOR COMMUNICATIONS                                * 01540001
      *---------------------------------------------------------------* 01550001
           EXEC SQL                                                     01560001
                INCLUDE SQLCA                                           01570001
           END-EXEC.                                                    01580001
                                                                        01590001
       EJECT                                                            01600001
       LINKAGE SECTION.                                                 01610001
                                                                        01620001
       01  DFHCOMMAREA.                                                 01630001
           05  FILLER                         OCCURS  1 TO 4072 TIMES   01640001
                                              DEPENDING ON EIBCALEN.    01650001
               10  FILLER                     PIC X.                    01660001
                                                                        01670001
                                                                        01680001
       PROCEDURE DIVISION.                                              01690001
      ***************************************************************** 01700001
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** 01710001
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** 01720001
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** 01730001
      **                                                             ** 01740001
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** 01750001
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** 01760001
      ***************************************************************** 01770001
       0000-MAIN-MODULE.                                                01780001
           INITIALIZE DP030-CICS-API-FIELDS.                            01790001
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       01800001
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          01810001
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         01820001
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       01830001
           MOVE LENGTH OF TF453AI        TO DP030-MAP-LENGTH (1).       01840001
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     01850001
           PERFORM 0001-CALL-CICS-ARCH-API.                             01860001
      *                                                                 01870001
           PERFORM 1000-CONTROL-PROCESSING                              01880001
      *                                                                 01890001
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     01900001
           PERFORM 0001-CALL-CICS-ARCH-API.                             01910001
                                                                        01920001
       0001-CALL-CICS-ARCH-API.                                         01930001
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      01940011
                                          DFHCOMMAREA                   01950011
                                          DP030-CICS-API-FIELDS         01960011
                                          DP020-STANDARD-COMMAREA       01970011
                                          TF453AI.                      01980011
      *                                                                 01990001
           IF  DP030-RC-CALL-SUCCESSFUL                                 02000001
               CONTINUE                                                 02010001
           ELSE                                                         02020001
               SET DP013-NO-ROLLBACK                                    02030001
                   DP013-XCTL-DISPLAY-RESTART                           02040001
                   DP013-CICS-ABEND      TO TRUE                        02050001
               MOVE 'BEFORE 0000-MAIN-MODULE'                           02060001
                                         TO DP013-PARAGRAPH             02070001
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O02071011
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      02072011
               MOVE DP030-RETURN-CODE                                   02100001
                                         TO DP013-MESSAGE-TEXT (3)      02110001
               PERFORM DP013-0000-PROCESS-ABEND                         02120001
           END-IF.                                                      02130001
       EJECT                                                            02140001
      *----------------------------------------------------------------*02150001
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *02160001
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *02170001
      *----------------------------------------------------------------*02180001
                                                                        02190001
       1000-CONTROL-PROCESSING.                                         02200001
                                                                        02210001
           EVALUATE TRUE                                                02220001
               WHEN DP020-NEXT-ACT-INITIAL                              02230001
                   INITIALIZE ASC-SPECIFIC-COMMAREA                     02240001
                   MOVE TF006-RCV-STR-NBR   TO ASC-STORE-NBR            02241001
                   PERFORM 4000-BUILD-INITIAL-PANEL                     02260001
                                                                        02270001
               WHEN DP020-NEXT-ACT-READ-MAP                             02280001
                   PERFORM 2000-FKEY-PROCESSING                         02290001
                                                                        02300001
               WHEN DP020-NEXT-ACT-RETURN                               02310001
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02320001
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02330001
                                                                        02340001
               WHEN OTHER                                               02350001
                   SET DP013-LOGIC-ABEND                                02360001
                       DP013-NO-ROLLBACK TO TRUE                        02370001
                   MOVE '1000-CONTROL-PROCESSING'                       02380001
                                         TO  DP013-PARAGRAPH            02390001
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   02400001
                                         TO  DP013-MESSAGE-TEXT(1)      02410001
                   MOVE DP020-NEXT-APPL-ACTIVITY                        02420001
                                         TO DP013-MESSAGE-TEXT(2)       02430001
                   PERFORM DP013-0000-PROCESS-ABEND                     02440001
           END-EVALUATE.                                                02450001
       EJECT                                                            02610001
      *----------------------------------------------------------------*02620001
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*02630001
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*02640001
      * EDITS.                                                         *02650001
      *----------------------------------------------------------------*02660001
                                                                        02670001
       2000-FKEY-PROCESSING.                                            02680001
                                                                        02690001
           EVALUATE TRUE                                                02700001
                                                                        02710001
               WHEN DP020-SRC-AID = DP016-CLEAR                         02720001
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02730001
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02740001
                                                                        02750001
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     02760001
                   PERFORM 4000-BUILD-INITIAL-PANEL                     02770001
                                                                        02780001
               WHEN OTHER                                               02790001
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 02800001
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02810001
                   IF  PS-NO-ERROR                                      02820001
                       SET DP030-CONTINUE-GO TO TRUE                    02830001
                       PERFORM 2100-CHECK-FUNCTION-KEY                  02840001
                   ELSE                                                 02850001
                       SET DP030-OVERRIDE-GO TO TRUE                    02860001
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             02870001
                   END-IF                                               02880001
           END-EVALUATE.                                                02890001
      ******************************************************************02900001
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **02910001
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **02920001
      ** FROM THE CICS ARCHITECTURE API.                              **02930001
      ******************************************************************02940001
       2100-CHECK-FUNCTION-KEY.                                         02950001
           EVALUATE TRUE                                                02960001
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)              02970001
      * ------  CONTINUE  ----------------------------------------------02980001
                   PERFORM 2110-PREPARE-INTER-APPL-COMM                 02990001
                                                                        03000001
               WHEN DP020-SRC-AID = DP016-ENTER                         03010001
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 03020001
                                                                        03030001
               WHEN OTHER                                               03040001
                   SET DP013-NO-ROLLBACK                                03050001
                       DP013-XCTL-DISPLAY-RESTART                       03060001
                       DP013-CICS-ABEND  TO TRUE                        03070001
                   MOVE '2100-CHECK-FUNCTION-KEY'                       03080001
                                         TO DP013-PARAGRAPH             03090001
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      03100001
                                         TO DP013-MESSAGE-TEXT (1)      03110001
                   MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:' 03120001
                                         TO DP013-MESSAGE-TEXT (2)      03130001
                   MOVE DP020-SRC-AID    TO PM-A-SRC-AID                03140001
                   MOVE DP020-FK-ACTION (DP020-SRC-AID)                 03150001
                                         TO PM-A-FK-ACTION              03160001
                   MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)              03170001
                                         TO PM-A-FK-DISPN               03180001
                   MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)      03190001
                   MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'        03200001
                                         TO DP013-MESSAGE-TEXT (4)      03210001
                   PERFORM DP013-0000-PROCESS-ABEND                     03220001
           END-EVALUATE.                                                03230001
      ******************************************************************03240001
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **03250001
      ** INTER-APPLICATION COMMAREA.                                  **03260001
      ******************************************************************03270001
       2110-PREPARE-INTER-APPL-COMM.                                    03280001
           INITIALIZE TF006-INTER-APPL-COMMAREA.                        03350001
           MOVE ASC-STORE-NBR         TO TF006-RCV-STR-NBR.             03360001
           SET TF006-RCV-STR-OVERRIDE TO TRUE.                          03370001
       EJECT                                                            03390001
      *----------------------------------------------------------------*03400001
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*03410001
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *03420001
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *03430001
      *----------------------------------------------------------------*03440001
                                                                        03450001
       2200-MOVE-SCREEN-TO-COMMAREA.                                    03460001
           IF  ASTRNBRL > 0                                             03470001
               MOVE ASTRNBRI             TO ASC-STORE-NBR-X             03480001
           ELSE                                                         03490001
               IF  ASTRNBRF = DP015-ERASE-EOF                           03500001
                   MOVE SPACE            TO ASC-STORE-NBR-X             03510001
               END-IF                                                   03520001
           END-IF.                                                      03530001
                                                                        03540001
      *----------------------------------------------------------------*03550001
      * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *03560001
      * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *03570001
      * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*03580001
      * IN THE DESTINATION PROGRAM, NOT HERE.                          *03590001
      *----------------------------------------------------------------*03600001
                                                                        03610001
       3000-EDIT-DATA-IN-COMMAREA.                                      03620001
           PERFORM 3200-RESET-ATTRIBUTES.                               03630001
           IF ASC-STORE-NBR-X NOT > SPACE                               03640001
               SET DP020-MSG-FATAL   TO TRUE                            03650001
      *        -- REQUIRED FIELD --                                     03660001
               MOVE DP015-UNP-ALP-BRT-OFF                               03670001
                                     TO ASTRNBRA                        03680001
               MOVE DP015-RED        TO ASTRNBRC                        03690001
               MOVE DP015-REVERSE    TO ASTRNBRH                        03700001
               IF PS-NO-ERROR                                           03710001
                   MOVE PC-TSYMSG-00007 TO DP020-MSG-NUMBER             03720001
                   MOVE -1           TO ASTRNBRL                        03730001
                   SET DP030-SET-CURSOR-APPL-1                          03740001
                                     TO TRUE                            03750001
                   SET PS-ERROR      TO TRUE                            03760001
               END-IF                                                   03770001
           ELSE                                                         03780001
               IF ASC-STORE-NBR-X NOT NUMERIC                           03790001
                   SET DP020-MSG-FATAL   TO TRUE                        03800001
      *        -- MUST BE NUMERIC --                                    03810001
                   MOVE DP015-UNP-ALP-BRT-OFF                           03820001
                                     TO ASTRNBRA                        03830001
                   MOVE DP015-RED        TO ASTRNBRC                    03840001
                   MOVE DP015-REVERSE    TO ASTRNBRH                    03850001
                   IF PS-NO-ERROR                                       03860001
                       MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER         03870001
                       MOVE -1           TO ASTRNBRL                    03880001
                       SET DP030-SET-CURSOR-APPL-1                      03890001
                                         TO TRUE                        03900001
                       SET PS-ERROR      TO TRUE                        03910001
                   END-IF                                               03920001
               ELSE                                                     03930001
                   SET PS-STORE-NOT-FOUND TO TRUE                       03940001
                   PERFORM 4500-READ-STORE                              03950001
                   IF PS-STORE-NOT-FOUND                                03960001
                       SET DP020-MSG-FATAL   TO TRUE                    03970001
      *        -- STORE DOES NOT EXIST --                               03980001
                       MOVE DP015-UNP-ALP-BRT-OFF                       03990001
                                             TO ASTRNBRA                04000001
                       MOVE DP015-RED        TO ASTRNBRC                04010001
                       MOVE DP015-REVERSE    TO ASTRNBRH                04020001
                       IF PS-NO-ERROR                                   04030001
                           MOVE PC-TSYMSG-00017 TO DP020-MSG-NUMBER     04040001
                           MOVE -1           TO ASTRNBRL                04050001
                           SET DP030-SET-CURSOR-APPL-1                  04060001
                                             TO TRUE                    04070001
                           SET PS-ERROR      TO TRUE                    04080001
                       END-IF                                           04090001
                   END-IF                                               04100001
               END-IF                                                   04110001
           END-IF.                                                      04120001
                                                                        04120106
060110     MOVE ASC-STORE-NBR-X TO PV-LOC-EXCLUDE                       04120209
060110     IF PV-EXCLUDE                                                04121009
060110         SET DP020-MSG-FATAL   TO TRUE                            04122009
060110*        -- INVLAID STORE --                                      04123009
060110         MOVE DP015-UNP-ALP-BRT-OFF                               04124009
060110                               TO ASTRNBRA                        04125009
060110         MOVE DP015-RED        TO ASTRNBRC                        04126009
060110         MOVE DP015-REVERSE    TO ASTRNBRH                        04127009
060110         IF PS-NO-ERROR                                           04128009
060110             MOVE PC-TSYMSG-01552 TO DP020-MSG-NUMBER             04129009
060110             MOVE -1           TO ASTRNBRL                        04129109
060110             SET DP030-SET-CURSOR-APPL-1                          04129209
060110                               TO TRUE                            04129309
060110             SET PS-ERROR      TO TRUE                            04129409
060110         END-IF                                                   04129509
060110     END-IF.                                                      04129609
                                                                        04129706
           IF  PS-NO-ERROR                                              04130001
               MOVE -1                   TO ASTRNBRL                    04140001
               SET DP030-SET-CURSOR-APPL-1                              04150001
                                         TO TRUE                        04160001
           END-IF.                                                      04170001
      ******************************************************************04180001
      ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **04190001
      ** MAP.                                                         **04200001
      ******************************************************************04210001
       3200-RESET-ATTRIBUTES.                                           04220001
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRNBRA.                   04230001
           MOVE DP015-GREEN              TO ASTRNBRC.                   04240001
           MOVE DP015-UNDERLINE          TO ASTRNBRH.                   04250001
       EJECT                                                            04260001
      *                                                                 04270001
      *----------------------------------------------------------------*04280001
      *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *04290001
      *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *04300001
      *  IT HERE.                                                      *04310001
      *----------------------------------------------------------------*04320001
       4000-BUILD-INITIAL-PANEL.                                        04330001
                                                                        04340001
           SET DP030-SET-CURSOR-APPL-1   TO TRUE                        04350001
           MOVE -1                       TO ASTRNBRL.                   04360001
           PERFORM 3000-EDIT-DATA-IN-COMMAREA                           04370001
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        04380001
      *----------------------------------------------------------------*04390001
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *04400001
      *----------------------------------------------------------------*04410001
       4400-MOVE-COMMAREA-TO-SCREEN.                                    04420001
           MOVE ASC-STORE-NBR            TO ASTRNBRO.                   04430001
           MOVE ASC-STORE-NAM            TO ASTRNAMO.                   04440001
       EJECT                                                            04450001
      *----------------------------------------------------------------*04460001
      *    GET THE 10 CHARACTER ABBREVIATED STORE NAME.                *04470001
      *----------------------------------------------------------------*04480001
       4500-READ-STORE.                                                 04490001
           MOVE ASC-STORE-NBR            TO DK-STORE-NBR.               04500001
           PERFORM WITH TEST AFTER                                      04510001
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       04520001
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                  04530001
                    OR SQLCODE = ZERO                                   04540001
                    OR SQLCODE = +100                                   04550001
               EXEC SQL                                                 04560001
100404             SELECT LOC_10_ABBR_NM                                04570002
100404                 INTO :XST00001-LOC-10-ABBR-NM                    04580002
100404                 FROM XST_LOC                                     04590002
100404                 WHERE LOC_NBR     = :DK-STORE-NBR                04600002
               END-EXEC                                                 04610001
               EVALUATE TRUE                                            04620001
                   WHEN SQLCODE = ZERO                                  04621002
100404                 MOVE XST00001-LOC-10-ABBR-NM TO                  04622002
                                       ASC-STORE-NAM                    04623002
                       SET PS-STORE-FOUND  TO TRUE                      04624002
                   WHEN SQLCODE = +100                                  04625002
                       MOVE 'INVALID'      TO ASC-STORE-NAM             04626002
                   WHEN SQLCODE = -904                                  04630001
                   WHEN SQLCODE = -913                                  04640001
                       CONTINUE                                         04650001
                   WHEN OTHER                                           04720002
                        MOVE '4500-READ-STORE'                          04750001
                                          TO DP013-PARAGRAPH            04760001
                        MOVE 'READ A ROW FROM THE STORE TABLE'          04770001
                                          TO DP013-MESSAGE-TEXT(1)      04780001
                        MOVE SQLCA        TO DP013-SQLCA                04790001
100404                  MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (1)   04800002
                        SET DP013-DB2-ABEND                             04810001
                            DP013-XCTL-DISPLAY-RESTART TO TRUE          04820001
                        PERFORM DP013-0000-PROCESS-ABEND                04830001
               END-EVALUATE                                             04840001
           END-PERFORM.                                                 04850001
                                                                        04860001
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           04870001
               MOVE '4500-READ-STORE' TO DP013-PARAGRAPH                04880001
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO READ'       04890001
                                          TO DP013-MESSAGE-TEXT (1)     04900001
               MOVE 'THE STORE TABLE'     TO DP013-MESSAGE-TEXT (2)     04910001
               MOVE SQLCA                 TO DP013-SQLCA                04920001
100404         MOVE 'XST_LOC'             TO DP013-DB2-TABLE-NAME (1)   04930002
               SET DP013-DB2-ABEND                                      04940001
                   DP013-XCTL-DISPLAY-RESTART                           04950001
                                          TO TRUE                       04960001
               PERFORM DP013-0000-PROCESS-ABEND                         04970001
           END-IF.                                                      04980001
       EJECT                                                            04990001
      *----------------------------------------------------------------*05000001
      *    ABEND PROCESSOR MODULE                                      *05010001
      *----------------------------------------------------------------*05020001
                                                                        05030001
           COPY DPPD013.                                                05040001
