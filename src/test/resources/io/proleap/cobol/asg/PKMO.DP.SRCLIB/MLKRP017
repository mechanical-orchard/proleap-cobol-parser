       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP017.                                                 
       AUTHOR.        TESS BESTE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-18-94.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           MLKRP017 - ACCUM PRICE CHANGES RETAIL AND QTY                 
      *                                                                         
      * THIS PROGRAM ACCUMULATES THE AMOUNTS FROM A FILE OF PRICE CHANGE        
      * TRANSACTIONS IN THE MLRD017 FORMAT, WHICH IS INPUT TO MLKUP020          
      * IN JOB MLK140, ON DD'S INP07, INP08, AND INP09. THE TRANSACTION         
      * COUNT AND TOTAL RETAIL ARE DISPLAYED.                                   
      *                                                                         
      * CONTROL PARAMETER TO THE PROGRAM ARE AS FOLLOW:                         
      *   POSITION  DESCRIPTION                                                 
      *   --------  --------------------------------------------------          
      *   01-08     REQUESTED REPORT BREAK OPTIONS                              
      *             THIS PARAMETER CONTROLS THE LEVEL AT WHICH THIS             
      *             PROGRAM WILL BREAK TO SHOW THE ACCUM TOTALS.                
      *          ***THE PROGRAM ASSUME THAT THE INPUT FILE HAS BEEN             
      *             SORTED IN ORDER OF THE BREAK OPTION REQUESTED.              
      *             OPTIONS ARE:                                                
      *               STORE    - REPORT BY STORE NUMBER                         
      *               BATCH    - REPORT BY BATCH NUMBER                         
      *               DEPT     - REPORT BY DEPARTMENT NUMBER                    
      *               RSN      - REPORT BY MARKDOWN/MARKUP REASON CODE          
      *               SPACES   - DEFAULT, REPORT THE FINAL TOTAL ONLY           
      *                          ANYTHING OTHER THAN LISTED HERE WILL           
      *                          BE TREATED THE SAME AS SPACES                  
      *               STOREDPT - REPORT BY STORE THEN DEPARTMENT NUMBER         
      *               DEPTSTR  - REPORT BY DEPARTMENT THEN STORE NUMBER         
      *               BATCHDPT - REPORT BY BATCH THEN DEPARTMENT NUMBER         
      *               DEPTBTCH - REPORT BY DEPARTMENT THEN BATCH NUMBER         
      *               DEPTRSN  - REPORT BY DEPT THEN MARKDOWN/UP REASON         
      *               RSNDEPT  - REPORT BY MARKDOWN/UP REASON THEN BATCH        
      *   09-16     MISSED CATEGORIZED DISPLAY OPTION                           
      *             THIS PARAMETER WILL EITHER SHOW POTENTIAL MISSED            
      *             CATEGORIZED PRICE CHANGES.                                  
      *             OPTIONS ARE:                                                
      *               MISS-CAT - SHOW MISSED CATEGORIZED ONLY                   
      *               SPACES   - DEFAULT, NO MISSED CATEGORIZED REPORT          
      *                          ANYTHING OTHER THAN LISTED HERE WILL           
      *                          BE TREATED THE SAME AS SPACES                  
      *               BOTH     - SHOW BOTH                                      
      *   17-24     PLU OR REGISTER PRICE CHANGES QUANTITY CONSIDERATION        
      *             THIS PARAMETER CONTROLS WHETHER THE PLU AND                 
      *             REGISTER MARKDOWNS WILL BE EXTENDED USING QUANTITY          
      *             PROVIDED IN THE EXTRACT.  ML GET A VERSION OF THE           
      *             FILE THAT HAS THE RETAIL EXTENDED AND ALWAYS A              
      *             UNIT OF 1, WHILE DATA WAREHOUSE GET THE ACTUAL              
      *             RETAIL AND UNIT OF THE TRANSACTION.                         
      *             OPTIONS ARE:                                                
      *               EXT_QTY  - USE THE UNIT PROVIDED IN CALCULATING           
      *                          THE EXTENDED RETAIL                            
      *               SPACES   - DEFAULT, DO NOT USE THE UNIT                   
      *                          ANYTHING OTHER THAN LISTED HERE WILL           
      *                          BE TREATED THE SAME AS SPACES                  
      *   25-80     USER SUPPLIED DESCRIPTION OF THE REPORT                     
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * LY0307   2003-07-15  CHANGE PROGRAM TO EXTEND PRICE ONLY IF    *        
      *                      PC-CATEG-CDE IS NOT '1' AND NOT '2'.      *        
      * LY0309   2003-09-11  UPDATE THE PROGRAM TO SHOW NET QUANTITY   *        
      *                      AND PROVIDE AN OPTION OF CALCULATING      *        
      *                      EXTENDED RETAIL WITH QTY OR WITHOUT ON    *        
      *                      PLU AND REGISTER PRICE CHANGES.           *        
      * LY0404   2004-04-14  UPDATE THE PROGRAM TO PROVIDE OPTION FOR  *        
      *                      REPORT BREAK ON MARKDOWN/MARKUP REASON    *        
      *                      CODE.                                     *        
      * LY0405   2004-05-06  CHANGE REPORT TO SHOW LEADING NEG SIGN    *        
      *                      INSTEAD OF TRAILING.                      *        
      * LY0406   2004-06-30  CHANGE PROGRAM TO ALLOW STORE/DEPT COMBO  *        
W27710* W27700  01-24-2005 ROD JANSSEN GENERATE A REPORT RATHER THAN            
W27710*                                PERFORM DISPLAYS.                        
      ******************************************************************        
                                                                       *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
W27710     SELECT PCHG-REPORT                                                   
W27710         ASSIGN TO RPT01.                                                 
W27710                                                                          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
W26603                                                                          
W26603     COPY MLRD017.                                                        
                                                                                
W27710 FD  PCHG-REPORT                                                          
W27710     LABEL RECORDS ARE OMITTED                                            
W27710     RECORD CONTAINS 200 CHARACTERS                                       
W27710     RECORDING  MODE IS F                                                 
W27710     DATA RECORD IS REPORT-LINE.                                          
W27710 01  REPORT-LINE         PIC X(200).                                      
W27710                                                                          
       WORKING-STORAGE SECTION.                                                 
W26603******************************************************************        
  |   * INPUT RECORD LAYOUT                                                     
  |   ******************************************************************        
  |   *01  ML017-PRICE-CHANGE-RECORD.                                           
  |   *    COPY MLRD017.                                                        
  |   *                                                                         
W26603*                                                                         
      ******************************************************************        
      * REPORT AREA                                                             
      ******************************************************************        
       01  RP-REPORT-HEADING.                                                   
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-CRIT-1-HEADING           PIC X(05)       VALUE SPACES.        
               88  RP-CRIT-1-BATCH                         VALUE                
                   'BATCH'.                                                     
               88  RP-CRIT-1-STORE                         VALUE                
                   'STORE'.                                                     
               88  RP-CRIT-1-REASON                        VALUE                
                   'RSN  '.                                                     
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-CRIT-2-HEADING           PIC X(04)       VALUE SPACES.        
               88  RP-CRIT-2-DEPT                          VALUE                
                   'DEPT'.                                                      
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
LY0309         '          NET RETAIL '.                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0309     05  FILLER                      PIC X(16)       VALUE                
LY0309         '        NET QTY '.                                              
LY0309     05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '              MARKUP '.                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '       MARKUP CANCEL '.                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '            MARKDOWN '.                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '     MARKDOWN CANCEL '.                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               '    OTHER '.                                                    
                                                                                
       01  RP-REPORT-HEADING-2.                                                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(05)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(16)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE ALL            
                                                           '-'.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE ALL            
                                                           '-'.                 
                                                                                
       01  RP-REPORT-LINE.                                                      
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-LABEL                    PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RP-LABEL.                                       
               10  RP-MISSED-CAT-SW        PIC X(01).                           
                   88  RP-MISSED-CAT                       VALUE '*'.           
                   88  RP-NOT-MISSED-CAT                   VALUE SPACES.        
W26603         10  RP-CRIT-1               PIC X(05).                           
               10  FILLER                  PIC X(01).                           
               10  RP-CRIT-2               PIC X(03).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-ACCUM-RTL                PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
W27710     05  RP-ACCUM-RTL                PIC --,---,---,---,--9.99            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-ACCUM-QTY                PIC ZZ,ZZZ,ZZZ,ZZ9-                  
W27710     05  RP-ACCUM-QTY                PIC ---,---,---,--9                  
LY0309                                                     VALUE ZEROES.        
LY0309     05  RP-QTY-NOT-1-IND            PIC X(01)       VALUE SPACES.        
LY0309         88  RP-QTY-NOT-1                            VALUE '!'.           
LY0309         88  RP-QTY-IS-1                             VALUE SPACES.        
LY0309     05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-MARKUP                   PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
W27710     05  RP-MARKUP                   PIC --,---,---,---,--9.99            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-MARKUP-CANCEL            PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
W27710     05  RP-MARKUP-CANCEL            PIC --,---,---,---,--9.99            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-MARKDOWN                 PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
W27710     05  RP-MARKDOWN                 PIC --,---,---,---,--9.99            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-MARKDOWN-CANCEL          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
W27710     05  RP-MARKDOWN-CANCEL          PIC --,---,---,---,--9.99            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-OTHER                    PIC ZZ,ZZ9.99-                       
W27710     05  RP-OTHER                    PIC ---,--9.99                       
                                                           VALUE ZEROES.        
                                                                                
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES                                                        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-FILE-SW     PIC X           VALUE 'N'.           
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.           
           05  PS-ACCUM-BREAK-SW           PIC X           VALUE 'N'.           
               88  PS-ACCUM-BREAK                          VALUE 'Y'.           
               88  PS-NOT-ACCUM-BREAK                      VALUE 'N'.           
LY0307     05  PS-QTY-OF-1-SW              PIC X           VALUE 'N'.           
LY0307         88  PS-QTY-OF-1                             VALUE 'Y'.           
LY0307         88  PS-NOT-QTY-OF-1                         VALUE 'N'.           
                                                                                
                                                                                
      ******************************************************************        
      * PROGRAM VARIABLES                                                       
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECORD-COUNT             PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-DISPLAY           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9-               
                                                           VALUE ZEROES.        
           05  PV-PRICE-CHANGE-CALC        PIC S9(08)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKUP-AMT               PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKDOWN-AMT             PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKUP-CANCEL-AMT        PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKDOWN-CANCEL-AMT      PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OTHER-AMT                PIC S9(03)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-AMT                PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
LY0309     05  PV-MARKUP-QTY               PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKDOWN-QTY             PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKUP-CANCEL-QTY        PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKDOWN-CANCEL-QTY      PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-OTHER-QTY                PIC S9(04)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-ACCUM-QTY                PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
           05  PV-TOT-MARKUP-AMT           PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKDOWN-AMT         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKUP-CANCEL-AMT    PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKDOWN-CANCEL-AMT  PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-OTHER-AMT            PIC S9(05)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-ACCUM-AMT            PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
LY0309     05  PV-TOT-MARKUP-QTY           PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKDOWN-QTY         PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKUP-CANCEL-QTY    PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKDOWN-CANCEL-QTY  PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-OTHER-QTY            PIC S9(05)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-ACCUM-QTY            PIC S9(11)      VALUE ZEROES         
LY0309                         COMP-3.                                          
           05  PV-MARKUP-MC-AMT            PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKDOWN-MC-AMT          PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKUP-CANCEL-MC-AMT     PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MARKDOWN-CANCEL-MC-AMT   PIC S9(09)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-MC-AMT             PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
LY0309     05  PV-MARKUP-MC-QTY            PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKDOWN-MC-QTY          PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKUP-CANCEL-MC-QTY     PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-MARKDOWN-CANCEL-MC-QTY   PIC S9(07)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-ACCUM-MC-QTY             PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
           05  PV-TOT-MARKUP-MC-AMT        PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKDOWN-MC-AMT      PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKUP-CANCEL-MC-AMT PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-MARKDOWN-CANCEL-MC-AMT PIC S9(11)V99 VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-ACCUM-MC-AMT         PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
LY0309     05  PV-TOT-MARKUP-MC-QTY        PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKDOWN-MC-QTY      PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKUP-CANCEL-MC-QTY PIC S9(09)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-MARKDOWN-CANCEL-MC-QTY PIC S9(09)    VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-ACCUM-MC-QTY         PIC S9(11)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-ACCUM-QTY-NOT-1      PIC S9(11)      VALUE ZEROES         
LY0309                         COMP-3.                                          
LY0309     05  PV-TOT-TRAN-QTY-NOT-1       PIC S9(11)      VALUE ZEROES         
LY0309                         COMP-3.                                          
                                                                                
                                                                                
      ******************************************************************        
      * PROGRAM LINKAGE SECTION                                                 
      ******************************************************************        
       LINKAGE SECTION.                                                         
                                                                                
       01  PARM-DATA-AREA.                                                      
           05  PARM-LENGTH                  PIC S9(4)                           
                               COMP.                                            
           05  PARM-DATA.                                                       
               10  PARM-REQUESTED-OPTION    PIC X(08).                          
                   88  PARM-VALID-REQUESTED-OPTIONS        VALUES ARE           
LY0309                 'STORE   '                                               
                       'BATCH   '                                               
                       'DEPT    '                                               
LY0404                 'RSN     '                                               
                       'BATCHDPT' 'DEPTBTCH'                                    
LY0406                 'DEPTSTR ' 'STOREDPT'                                    
LY0404                 'RSNDEPT ' 'DEPTRSN '.                                   
LY0309             88  PARM-REQ-BY-STORE                   VALUE                
LY0309                 'STORE   '.                                              
                   88  PARM-REQ-BY-BATCH                   VALUE                
                       'BATCH   '.                                              
                   88  PARM-REQ-BY-DEPT                    VALUE                
                       'DEPT    '.                                              
LY0404             88  PARM-REQ-BY-MKDNUP-RSN              VALUE                
LY0404                 'RSN     '.                                              
LY0406             88  PARM-REQ-BY-STORE-DEPT              VALUE                
LY0406                 'STOREDPT'.                                              
LY0406             88  PARM-REQ-BY-DEPT-STORE              VALUE                
LY0406                 'DEPTSTR '.                                              
                   88  PARM-REQ-BY-BATCH-DEPT              VALUE                
                       'BATCHDPT'.                                              
                   88  PARM-REQ-BY-DEPT-BATCH              VALUE                
                       'DEPTBTCH'.                                              
LY0404             88  PARM-REQ-BY-DEPT-MKDNUP-RSN         VALUE                
LY0404                 'DEPTRSN '.                                              
LY0404             88  PARM-REQ-BY-MKDNUP-RSN-DEPT         VALUE                
LY0404                 'RSNDEPT '.                                              
               10  PARM-DISPLAY-OPTION      PIC X(08).                          
                   88  PARM-DIS-MISSED-CATEGORIZE          VALUE                
                       'MISS-CAT'.                                              
                   88  PARM-DIS-BOTH                       VALUE                
                       'BOTH    '.                                              
LY0309         10  PARM-PLU-REG-OPTION      PIC X(08).                          
LY0309             88  PARM-USE-QTY                        VALUE                
LY0309                 'EXT_QTY '.                                              
LY0309         10  PARM-DISPLAY-TEXT        PIC X(56).                          
                                                                                
                                                                                
       PROCEDURE DIVISION USING PARM-DATA-AREA.                                 
                                                                                
                                                                                
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           OPEN INPUT INPUT-FILE                                                
W27710         OUTPUT PCHG-REPORT.                                              
                                                                                
           PERFORM Z100-READ-INPUT.                                             
                                                                                
           INSPECT PARM-DATA                                                    
               REPLACING ALL LOW-VALUES  BY ' '                                 
                         ALL HIGH-VALUES BY ' '.                                
           IF PARM-VALID-REQUESTED-OPTIONS                                      
LY0404         EVALUATE TRUE                                                    
LY0404             WHEN PARM-REQ-BY-STORE                                       
LY0404                 SET RP-CRIT-1-STORE     TO TRUE                          
LY0404                 MOVE ML017-PC-STORE-NBR TO RP-CRIT-1                     
LY0404             WHEN PARM-REQ-BY-BATCH                                       
LY0404                 SET RP-CRIT-1-BATCH     TO TRUE                          
LY0404                 MOVE ML017-PC-BATCH-NBR TO RP-CRIT-1                     
LY0404             WHEN PARM-REQ-BY-DEPT                                        
LY0404                 SET RP-CRIT-2-DEPT      TO TRUE                          
LY0404                 MOVE ML017-PC-DEPT-NBR  TO RP-CRIT-2                     
LY0404             WHEN PARM-REQ-BY-MKDNUP-RSN                                  
LY0404                 SET RP-CRIT-1-REASON    TO TRUE                          
LY0404                 MOVE ML017-PC-MKDN-MKUP-RSN-CDE TO RP-CRIT-1             
LY0406             WHEN OTHER                                                   
LY0404                 SET RP-CRIT-2-DEPT      TO TRUE                          
LY0404                 MOVE ML017-PC-DEPT-NBR  TO RP-CRIT-2                     
LY0404                 IF PARM-REQ-BY-BATCH-DEPT                                
LY0404                     OR PARM-REQ-BY-DEPT-BATCH                            
LY0404                     SET RP-CRIT-1-BATCH     TO TRUE                      
LY0404                     MOVE ML017-PC-BATCH-NBR TO RP-CRIT-1                 
LY0404                 ELSE                                                     
LY0406                 IF PARM-REQ-BY-STORE-DEPT                                
LY0406                     OR PARM-REQ-BY-DEPT-STORE                            
LY0406                     SET RP-CRIT-1-STORE     TO TRUE                      
LY0406                     MOVE ML017-PC-STORE-NBR TO RP-CRIT-1                 
LY0406                 ELSE                                                     
LY0404                     SET RP-CRIT-1-REASON    TO TRUE                      
LY0404                     MOVE ML017-PC-MKDN-MKUP-RSN-CDE TO RP-CRIT-1         
LY0406                 END-IF                                                   
LY0404                 END-IF                                                   
LY0404         END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
           IF PARM-LENGTH > 8                                                   
               AND PARM-VALID-REQUESTED-OPTIONS                                 
LY0309         DISPLAY ' OPTIONS USED: ' PARM-DATA                              
           END-IF.                                                              
                                                                                
W27710     WRITE REPORT-LINE        FROM RP-REPORT-HEADING.                     
W27710     WRITE REPORT-LINE        FROM RP-REPORT-HEADING-2.                   
W27710*    DISPLAY RP-REPORT-HEADING.                                           
W27710*    DISPLAY RP-REPORT-HEADING-2.                                         
                                                                                
LY0307     SET PS-QTY-OF-1   TO TRUE.                                           
LY0309     SET RP-QTY-IS-1   TO TRUE.                                           
           PERFORM B100-PROCESS-RECORD                                          
               UNTIL PS-END-OF-INPUT-FILE.                                      
                                                                                
           IF PV-RECORD-COUNT > 0                                               
               IF PARM-VALID-REQUESTED-OPTIONS                                  
                   PERFORM C100-PROCESS-PARM                                    
               ELSE                                                             
                   PERFORM H100-CALC-NET                                        
                   MOVE PV-ACCUM-AMT           TO PV-TOT-ACCUM-AMT              
                   MOVE PV-ACCUM-MC-AMT        TO PV-TOT-ACCUM-MC-AMT           
LY0309             MOVE PV-ACCUM-QTY           TO PV-TOT-ACCUM-QTY              
LY0309             MOVE PV-ACCUM-MC-QTY        TO PV-TOT-ACCUM-MC-QTY           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM N200-PRINT-TOTAL.                                            
                                                                                
           MOVE PV-RECORD-COUNT TO PV-RECORD-DISPLAY.                           
           DISPLAY '# RECORDS   : ' PV-RECORD-DISPLAY.                          
                                                                                
LY0309     IF PS-NOT-QTY-OF-1                                                   
LY0309         DISPLAY ' ***! THERE ARE PLU AND/OR REGISTER'                    
LY0309                 ' PRICE CHANGES WITH TRANS QTY > 1. ****'                
LY0309         DISPLAY ' ***! RESULTS NEED TO BE VERIFIED'                      
LY0309                 ' SEE OPTIONS USED FOR EXT WITH QTY. ****'               
LY0309         DISPLAY ' ***! THE INPUT MAY HAVE BEEN SORT/SUMMED'              
LY0309                 ' OR DATA WAREHOUSE VERSION OF THE FILE. ****'           
LY0309         DISPLAY ' ***! TOTAL UNITS FOR TRANS QTY > 1: '                  
LY0309                 PV-TOT-ACCUM-QTY-NOT-1                                   
LY0309                 ' ON ' PV-TOT-TRAN-QTY-NOT-1 ' TRANS.'                   
LY0309         MOVE +4 TO RETURN-CODE                                           
LY0309     END-IF.                                                              
                                                                                
           CLOSE INPUT-FILE                                                     
W27710            PCHG-REPORT.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-PROCESS-RECORD.                                                     
                                                                                
           ADD +1 TO PV-RECORD-COUNT.                                           
                                                                                
           IF PARM-VALID-REQUESTED-OPTIONS                                      
               PERFORM C100-PROCESS-PARM                                        
           END-IF.                                                              
                                                                                
LY0307     IF ML017-PLU-MKDN-MKON                                               
LY0307         OR ML017-REGISTER-MKDN-MKON                                      
LY0309         IF PARM-USE-QTY                                                  
LY0309             COMPUTE PV-PRICE-CHANGE-CALC                                 
LY0309                   = ((ML017-PC-OLD-PR-AMT - ML017-PC-NEW-PR-AMT)         
LY0309                      * ML017-PC-QTY)                                     
LY0309         ELSE                                                             
LY0307             COMPUTE PV-PRICE-CHANGE-CALC                                 
LY0307                    = (ML017-PC-OLD-PR-AMT - ML017-PC-NEW-PR-AMT)         
LY0309         END-IF                                                           
LY0307         IF ML017-PC-QTY NOT = 1                                          
LY0307             SET PS-NOT-QTY-OF-1 TO TRUE                                  
LY0309             SET RP-QTY-NOT-1    TO TRUE                                  
LY0309             ADD ML017-PC-QTY TO PV-TOT-ACCUM-QTY-NOT-1                   
LY0309             ADD 1            TO PV-TOT-TRAN-QTY-NOT-1                    
LY0307         END-IF                                                           
LY0307     ELSE                                                                 
LY0307         COMPUTE PV-PRICE-CHANGE-CALC                                     
LY0307                 = ((ML017-PC-OLD-PR-AMT - ML017-PC-NEW-PR-AMT)           
LY0307                   * ML017-PC-QTY)                                        
LY0307     END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML017-MARKDOWN                                              
                   COMPUTE PV-MARKDOWN-AMT                                      
                           = PV-MARKDOWN-AMT + PV-PRICE-CHANGE-CALC             
LY0309             COMPUTE PV-MARKDOWN-QTY                                      
LY0309                     = PV-MARKDOWN-QTY + ML017-PC-QTY                     
                   IF PV-PRICE-CHANGE-CALC < 0                                  
                       COMPUTE PV-MARKDOWN-MC-AMT                               
                               = PV-MARKDOWN-MC-AMT                             
                                 + PV-PRICE-CHANGE-CALC                         
LY0309                 COMPUTE PV-MARKDOWN-MC-QTY                               
LY0309                         = PV-MARKDOWN-MC-QTY + ML017-PC-QTY              
                   END-IF                                                       
               WHEN ML017-MARKDOWN-CANCEL                                       
                   COMPUTE PV-MARKDOWN-CANCEL-AMT                               
                           = PV-MARKDOWN-CANCEL-AMT                             
                             + PV-PRICE-CHANGE-CALC                             
LY0309             COMPUTE PV-MARKDOWN-CANCEL-QTY                               
LY0309                     = PV-MARKDOWN-CANCEL-QTY + ML017-PC-QTY              
                   IF PV-PRICE-CHANGE-CALC > 0                                  
                       COMPUTE PV-MARKDOWN-CANCEL-MC-AMT                        
                               = PV-MARKDOWN-CANCEL-MC-AMT                      
                                 + PV-PRICE-CHANGE-CALC                         
LY0309                 COMPUTE PV-MARKDOWN-CANCEL-MC-QTY                        
LY0309                         = PV-MARKDOWN-CANCEL-MC-QTY                      
LY0309                           + ML017-PC-QTY                                 
                   END-IF                                                       
               WHEN ML017-MARKUP                                                
                   COMPUTE PV-MARKUP-AMT                                        
                           = PV-MARKUP-AMT + PV-PRICE-CHANGE-CALC               
LY0309             COMPUTE PV-MARKUP-QTY                                        
LY0309                     = PV-MARKUP-QTY + ML017-PC-QTY                       
                   IF PV-PRICE-CHANGE-CALC > 0                                  
                       COMPUTE PV-MARKUP-MC-AMT                                 
                               = PV-MARKUP-MC-AMT                               
                                 + PV-PRICE-CHANGE-CALC                         
LY0309                 COMPUTE PV-MARKUP-MC-QTY                                 
LY0309                         = PV-MARKUP-MC-QTY + ML017-PC-QTY                
                   END-IF                                                       
               WHEN ML017-MARKUP-CANCEL                                         
                   COMPUTE PV-MARKUP-CANCEL-AMT                                 
                           = PV-MARKUP-CANCEL-AMT + PV-PRICE-CHANGE-CALC        
LY0309             COMPUTE PV-MARKUP-CANCEL-QTY                                 
LY0309                     = PV-MARKUP-CANCEL-QTY + ML017-PC-QTY                
                   IF PV-PRICE-CHANGE-CALC < 0                                  
                       COMPUTE PV-MARKUP-CANCEL-MC-AMT                          
                               = PV-MARKUP-CANCEL-MC-AMT                        
                                 + PV-PRICE-CHANGE-CALC                         
LY0309                 COMPUTE PV-MARKUP-CANCEL-MC-QTY                          
LY0309                         = PV-MARKUP-CANCEL-MC-QTY + ML017-PC-QTY         
                   END-IF                                                       
               WHEN OTHER                                                       
                   DISPLAY '  *** INVALID PRICE CHANGE TYPE CODE'               
                           '  (' ML017-PC-TYP-CDE ') ***'                       
                   COMPUTE PV-OTHER-AMT                                         
                           = PV-OTHER-AMT + PV-PRICE-CHANGE-CALC                
LY0309             COMPUTE PV-OTHER-QTY                                         
LY0309                     = PV-OTHER-QTY + ML017-PC-QTY                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM Z100-READ-INPUT.                                             
                                                                                
       C100-PROCESS-PARM.                                                       
                                                                                
           IF PS-END-OF-INPUT-FILE                                              
               SET PS-ACCUM-BREAK      TO TRUE                                  
           ELSE                                                                 
               SET PS-NOT-ACCUM-BREAK  TO TRUE                                  
               EVALUATE TRUE                                                    
                   WHEN PARM-REQ-BY-STORE                                       
                       IF RP-CRIT-1 NOT = ML017-PC-STORE-NBR                    
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
                   WHEN PARM-REQ-BY-BATCH                                       
                       IF RP-CRIT-1 NOT = ML017-PC-BATCH-NBR                    
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
                   WHEN PARM-REQ-BY-DEPT                                        
                       IF RP-CRIT-2 NOT = ML017-PC-DEPT-NBR                     
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
                   WHEN PARM-REQ-BY-MKDNUP-RSN                                  
                       IF RP-CRIT-1 NOT = ML017-PC-MKDN-MKUP-RSN-CDE            
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
LY0406             WHEN PARM-REQ-BY-STORE-DEPT                                  
LY0406             WHEN PARM-REQ-BY-DEPT-STORE                                  
LY0406                 IF RP-CRIT-1 NOT = ML017-PC-STORE-NBR                    
LY0406                     OR RP-CRIT-2 NOT = ML017-PC-DEPT-NBR                 
LY0406                     SET PS-ACCUM-BREAK TO TRUE                           
LY0406                 END-IF                                                   
                   WHEN PARM-REQ-BY-BATCH-DEPT                                  
                   WHEN PARM-REQ-BY-DEPT-BATCH                                  
                       IF RP-CRIT-1 NOT = ML017-PC-BATCH-NBR                    
                           OR RP-CRIT-2 NOT = ML017-PC-DEPT-NBR                 
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
                   WHEN PARM-REQ-BY-MKDNUP-RSN-DEPT                             
                   WHEN PARM-REQ-BY-DEPT-MKDNUP-RSN                             
                       IF RP-CRIT-1 NOT = ML017-PC-MKDN-MKUP-RSN-CDE            
                           OR RP-CRIT-2 NOT = ML017-PC-DEPT-NBR                 
                           SET PS-ACCUM-BREAK TO TRUE                           
                       END-IF                                                   
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
           IF PS-ACCUM-BREAK                                                    
               PERFORM H100-CALC-NET                                            
               ADD PV-ACCUM-AMT            TO PV-TOT-ACCUM-AMT                  
               ADD PV-ACCUM-MC-AMT         TO PV-TOT-ACCUM-MC-AMT               
LY0309         ADD PV-ACCUM-QTY            TO PV-TOT-ACCUM-QTY                  
LY0309         ADD PV-ACCUM-MC-QTY         TO PV-TOT-ACCUM-MC-QTY               
               PERFORM N100-PRINT-DETAIL                                        
                                                                                
               MOVE ZEROES                 TO PV-MARKUP-AMT                     
                                              PV-MARKDOWN-AMT                   
                                              PV-MARKUP-CANCEL-AMT              
                                              PV-MARKDOWN-CANCEL-AMT            
                                              PV-OTHER-AMT                      
LY0309         MOVE ZEROES                 TO PV-MARKUP-QTY                     
LY0309                                        PV-MARKDOWN-QTY                   
LY0309                                        PV-MARKUP-CANCEL-QTY              
LY0309                                        PV-MARKDOWN-CANCEL-QTY            
LY0309                                        PV-OTHER-QTY                      
               MOVE ZEROES                 TO PV-MARKUP-MC-AMT                  
                                              PV-MARKDOWN-MC-AMT                
                                              PV-MARKUP-CANCEL-MC-AMT           
                                              PV-MARKDOWN-CANCEL-MC-AMT         
LY0309         MOVE ZEROES                 TO PV-MARKUP-MC-QTY                  
LY0309                                        PV-MARKDOWN-MC-QTY                
LY0309                                        PV-MARKUP-CANCEL-MC-QTY           
LY0309                                        PV-MARKDOWN-CANCEL-MC-QTY         
LY0309         SET RP-QTY-IS-1             TO TRUE                              
                                                                                
LY0404         EVALUATE TRUE                                                    
LY0404             WHEN PARM-REQ-BY-STORE                                       
LY0404                 MOVE ML017-PC-STORE-NBR TO RP-CRIT-1                     
LY0404             WHEN PARM-REQ-BY-BATCH                                       
LY0404                 MOVE ML017-PC-BATCH-NBR TO RP-CRIT-1                     
LY0404             WHEN PARM-REQ-BY-DEPT                                        
LY0404                 MOVE ML017-PC-DEPT-NBR  TO RP-CRIT-2                     
LY0404             WHEN PARM-REQ-BY-MKDNUP-RSN                                  
LY0404                 MOVE ML017-PC-MKDN-MKUP-RSN-CDE TO RP-CRIT-1             
LY0406             WHEN OTHER                                                   
LY0404                 MOVE ML017-PC-DEPT-NBR  TO RP-CRIT-2                     
LY0404                 IF PARM-REQ-BY-BATCH-DEPT                                
LY0404                     OR PARM-REQ-BY-DEPT-BATCH                            
LY0404                     MOVE ML017-PC-BATCH-NBR TO RP-CRIT-1                 
LY0404                 ELSE                                                     
LY0406                 IF PARM-REQ-BY-STORE-DEPT                                
LY0406                     OR PARM-REQ-BY-DEPT-STORE                            
LY0406                     MOVE ML017-PC-STORE-NBR TO RP-CRIT-1                 
LY0406                 ELSE                                                     
LY0404                     MOVE ML017-PC-MKDN-MKUP-RSN-CDE TO RP-CRIT-1         
LY0406                 END-IF                                                   
LY0404                 END-IF                                                   
LY0404         END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
                                                                                
       H100-CALC-NET.                                                           
                                                                                
           ADD PV-MARKUP-AMT           TO PV-TOT-MARKUP-AMT.                    
           ADD PV-MARKDOWN-AMT         TO PV-TOT-MARKDOWN-AMT.                  
           ADD PV-MARKUP-CANCEL-AMT    TO PV-TOT-MARKUP-CANCEL-AMT.             
           ADD PV-MARKDOWN-CANCEL-AMT  TO PV-TOT-MARKDOWN-CANCEL-AMT.           
           ADD PV-OTHER-AMT            TO PV-TOT-OTHER-AMT.                     
                                                                                
           COMPUTE PV-ACCUM-AMT                                                 
                   = PV-MARKDOWN-AMT + PV-MARKDOWN-CANCEL-AMT                   
                     + PV-MARKUP-AMT + PV-MARKUP-CANCEL-AMT                     
                     + PV-OTHER-AMT.                                            
                                                                                
LY0309     ADD PV-MARKUP-QTY           TO PV-TOT-MARKUP-QTY.                    
LY0309     ADD PV-MARKDOWN-QTY         TO PV-TOT-MARKDOWN-QTY.                  
LY0309     ADD PV-MARKUP-CANCEL-QTY    TO PV-TOT-MARKUP-CANCEL-QTY.             
LY0309     ADD PV-MARKDOWN-CANCEL-QTY  TO PV-TOT-MARKDOWN-CANCEL-QTY.           
LY0309     ADD PV-OTHER-QTY            TO PV-TOT-OTHER-QTY.                     
LY0309                                                                          
LY0309     COMPUTE PV-ACCUM-QTY                                                 
LY0309             = PV-MARKDOWN-QTY + PV-MARKDOWN-CANCEL-QTY                   
LY0309               + PV-MARKUP-QTY + PV-MARKUP-CANCEL-QTY                     
LY0309               + PV-OTHER-QTY.                                            
LY0309                                                                          
           ADD PV-MARKUP-MC-AMT          TO PV-TOT-MARKUP-MC-AMT.               
           ADD PV-MARKDOWN-MC-AMT        TO PV-TOT-MARKDOWN-MC-AMT.             
           ADD PV-MARKUP-CANCEL-MC-AMT   TO PV-TOT-MARKUP-CANCEL-MC-AMT.        
           ADD PV-MARKDOWN-CANCEL-MC-AMT                                        
               TO PV-TOT-MARKDOWN-CANCEL-MC-AMT.                                
                                                                                
           COMPUTE PV-ACCUM-MC-AMT                                              
                   = PV-MARKDOWN-MC-AMT + PV-MARKDOWN-CANCEL-MC-AMT             
                     + PV-MARKUP-MC-AMT + PV-MARKUP-CANCEL-MC-AMT.              
                                                                                
LY0309     ADD PV-MARKUP-MC-QTY          TO PV-TOT-MARKUP-MC-QTY.               
LY0309     ADD PV-MARKDOWN-MC-QTY        TO PV-TOT-MARKDOWN-MC-QTY.             
LY0309     ADD PV-MARKUP-CANCEL-MC-QTY   TO PV-TOT-MARKUP-CANCEL-MC-QTY.        
LY0309     ADD PV-MARKDOWN-CANCEL-MC-QTY                                        
LY0309         TO PV-TOT-MARKDOWN-CANCEL-MC-QTY.                                
LY0309                                                                          
LY0309     COMPUTE PV-ACCUM-MC-QTY                                              
LY0309             = PV-MARKDOWN-MC-QTY + PV-MARKDOWN-CANCEL-MC-QTY             
LY0309               + PV-MARKUP-MC-QTY + PV-MARKUP-CANCEL-MC-QTY.              
LY0309                                                                          
                                                                                
       N100-PRINT-DETAIL.                                                       
                                                                                
           IF (PARM-DIS-MISSED-CATEGORIZE OR PARM-DIS-BOTH)                     
               AND (PV-MARKUP-MC-AMT          NOT = ZEROES OR                   
                    PV-MARKDOWN-MC-AMT        NOT = ZEROES OR                   
                    PV-MARKUP-CANCEL-MC-AMT   NOT = ZEROES OR                   
                    PV-MARKDOWN-CANCEL-MC-AMT NOT = ZEROES)                     
               MOVE PV-MARKUP-MC-AMT          TO RP-MARKUP                      
               MOVE PV-MARKDOWN-MC-AMT        TO RP-MARKDOWN                    
               MOVE PV-MARKUP-CANCEL-MC-AMT   TO RP-MARKUP-CANCEL               
               MOVE PV-MARKDOWN-CANCEL-MC-AMT TO RP-MARKDOWN-CANCEL             
               MOVE ZEROES                    TO RP-OTHER                       
LY0309         MOVE PV-ACCUM-MC-AMT           TO RP-ACCUM-RTL                   
LY0309         MOVE PV-ACCUM-MC-QTY           TO RP-ACCUM-QTY                   
               SET  RP-MISSED-CAT             TO TRUE                           
W27710         WRITE REPORT-LINE    FROM RP-REPORT-LINE                         
W27710*        DISPLAY RP-REPORT-LINE                                           
           END-IF.                                                              
                                                                                
           IF PARM-DIS-MISSED-CATEGORIZE                                        
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-MARKUP-AMT          TO RP-MARKUP                         
               MOVE PV-MARKDOWN-AMT        TO RP-MARKDOWN                       
               MOVE PV-MARKUP-CANCEL-AMT   TO RP-MARKUP-CANCEL                  
               MOVE PV-MARKDOWN-CANCEL-AMT TO RP-MARKDOWN-CANCEL                
               MOVE PV-OTHER-AMT           TO RP-OTHER                          
LY0309         MOVE PV-ACCUM-AMT           TO RP-ACCUM-RTL                      
LY0309         MOVE PV-ACCUM-QTY           TO RP-ACCUM-QTY                      
               SET  RP-NOT-MISSED-CAT      TO TRUE                              
W27710         WRITE REPORT-LINE    FROM RP-REPORT-LINE                         
W27710*        DISPLAY RP-REPORT-LINE                                           
           END-IF.                                                              
                                                                                
                                                                                
       N200-PRINT-TOTAL.                                                        
                                                                                
           MOVE ' TOTAL: '      TO RP-LABEL.                                    
           IF (PARM-DIS-MISSED-CATEGORIZE OR PARM-DIS-BOTH)                     
               AND (PV-TOT-MARKUP-MC-AMT          NOT = ZEROES OR               
                    PV-TOT-MARKDOWN-MC-AMT        NOT = ZEROES OR               
                    PV-TOT-MARKUP-CANCEL-MC-AMT   NOT = ZEROES OR               
                    PV-TOT-MARKDOWN-CANCEL-MC-AMT NOT = ZEROES)                 
               MOVE PV-TOT-MARKUP-MC-AMT          TO RP-MARKUP                  
               MOVE PV-TOT-MARKDOWN-MC-AMT        TO RP-MARKDOWN                
               MOVE PV-TOT-MARKUP-CANCEL-MC-AMT   TO RP-MARKUP-CANCEL           
               MOVE PV-TOT-MARKDOWN-CANCEL-MC-AMT TO RP-MARKDOWN-CANCEL         
               MOVE ZEROES                        TO RP-OTHER                   
LY0309         MOVE PV-TOT-ACCUM-MC-AMT           TO RP-ACCUM-RTL               
LY0309         MOVE PV-TOT-ACCUM-MC-QTY           TO RP-ACCUM-QTY               
               SET  RP-MISSED-CAT                 TO TRUE                       
W27710         WRITE REPORT-LINE    FROM RP-REPORT-LINE                         
W27710*        DISPLAY RP-REPORT-LINE                                           
           END-IF.                                                              
           IF PARM-DIS-MISSED-CATEGORIZE                                        
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-TOT-MARKUP-AMT          TO RP-MARKUP                     
               MOVE PV-TOT-MARKDOWN-AMT        TO RP-MARKDOWN                   
               MOVE PV-TOT-MARKUP-CANCEL-AMT   TO RP-MARKUP-CANCEL              
               MOVE PV-TOT-MARKDOWN-CANCEL-AMT TO RP-MARKDOWN-CANCEL            
               MOVE PV-TOT-OTHER-AMT           TO RP-OTHER                      
LY0309         MOVE PV-TOT-ACCUM-AMT           TO RP-ACCUM-RTL                  
LY0309         MOVE PV-TOT-ACCUM-QTY           TO RP-ACCUM-QTY                  
               SET  RP-NOT-MISSED-CAT          TO TRUE                          
W27710         WRITE REPORT-LINE    FROM RP-REPORT-LINE                         
W27710*        DISPLAY RP-REPORT-LINE                                           
           END-IF.                                                              
                                                                                
                                                                                
       Z100-READ-INPUT.                                                         
                                                                                
W26603***  READ INPUT-FILE INTO ML017-PRICE-CHANGE-RECORD                       
W26603     READ INPUT-FILE NEXT                                                 
               AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
                                                                                
