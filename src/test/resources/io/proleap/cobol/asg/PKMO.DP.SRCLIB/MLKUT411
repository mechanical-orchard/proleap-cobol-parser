       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              MLKUT411.                                       
       AUTHOR.                  SCOTT JACKSON.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            06/30/98.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *            MAJOR CLASS MERCHANDISE LEDGER EXTRACT.                      
      *                                                                         
      * THIS PROGRAM WILL EXTRACT FROM THE MONTH-TO-DATE MAJOR CLASS            
      * LEDGER TABLE, THE DATA TO BE REPORTED ON THE CLASS-LEVEL LEDGER         
      * REPORT.                                                                 
      *                                                                         
      * IT IS DRIVEN BY A CURSOR OF DEPT/CLASS THAT WERE ACTIVE ON              
      * MBS FOR MERCHANDISE LEDGER AS OF THE END OF THE OPEN FISCAL             
      * MONTH.                                                                  
      *                                                                         
      * FOR EACH DEPT/MCL, THE PROGRAM  EXTRACTS THE BEGINNING                  
      * INVENTORY AT COST AND RETAIL FOR THE OPEN FISCAL YEAR, AND              
      * ALL COST AND RETAIL AMOUNTS FOR EACH MONTH FROM THE                     
      * BEGINNING OF THE OPEN FISCAL YEAR THROUGH THE POSTING FISCAL            
      * MONTH.                                                                  
      *                                                                         
      * JBF 07/07/00 -CHANGED THE PROGRAM TO USE THE POSTING MONTH              
      *               INSTEAD OF THE OPEN FISCAL MONTH WHEN THE                 
      *               FISCAL YEARS ARE NOT THE SAME.                            
      *                                                                         
W26603******************************************************************        
W26603* CHG ID   DATE        DESCRIPTION                               *        
W26603* ------   ----------  ----------------------------------------- *        
W26603* W26603   03-18-2004  STORE EXPANSION.  EXPANDED LRECL OF       *        
W26603*                      OUT01 BECAUSE OF CHANGES TO MLRD102.      *        
W26603*                      ADDED 'WITH UR' TO DB2 SELECTS.           *        
W26603*                      - RONNA MCDONALD                          *        
      ******************************************************************        
                                                                                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT LEDGER-EXTRACT-DATE-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT LEDGER-EXTRACT-FILE                                           
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD              PIC X(80).                          
                                                                                
       FD  LEDGER-EXTRACT-DATE-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  LEDGER-EXTRACT-DATE-RECORD       PIC X(80).                          
                                                                                
       FD  LEDGER-EXTRACT-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603*01  LEDGER-EXTRACT-RECORD            PIC X(1106).                        
W26603 01  LEDGER-EXTRACT-RECORD            PIC X(1288).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR LEDGER EXTRACT DATE RECORD                            
      ****************************************************************          
                                                                                
           COPY MLRD114.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR LEDGER EXTRACT RECORD                                 
      ****************************************************************          
                                                                                
           COPY MLRD102.                                                        
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      ****************************************************************          
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-EXTRACT-DTE               PIC X(10).                          
           05  FILLER                       PIC X(70).                          
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
               88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
           05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
               88  PS-DATE-FOUND                       VALUE 'Y'.               
               88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
           05  PS-END-OF-DEPT-MCL-NBR-CSR-SW PIC X     VALUE 'N'.               
               88  PS-END-OF-DEPT-MCL-NBR-CSR          VALUE 'Y'.               
           05  PS-END-OF-LEDGER-CSR-SW      PIC X      VALUE 'N'.               
               88  PS-END-OF-LEDGER-CSR                VALUE 'Y'.               
               88  PS-NOT-END-OF-LEDGER-CSR            VALUE 'N'.               
           05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.               
               88  PS-MBS-INFO-FOUND                   VALUE 'Y'.               
               88  PS-MBS-INFO-NOT-FOUND               VALUE 'N'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-FSCL-YR-BEG-DTE           PIC X(10)  VALUE SPACE.             
013700     05  PV-FSCL-YR-NBR               PIC X(04)  VALUE SPACE.             
           05  PV-PREV-FSCL-YR-END-DTE      PIC X(10)  VALUE SPACE.             
                                                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
      *    05  PV-RETRY-COUNT               PIC S9(04) COMP SYNC                
      *                                                VALUE ZERO.              
       01  SS-SUBSCRIPTS.                                                       
           05  SS-FSCL-MN                   PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
      ****************************************************************          
      * LEDGER MONTH-TO-DATE MAJOR CLASS TABLE                                  
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNMCL                                                   
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * DATE ATTRIBUTE TABLE / LEDGER OPEN FISCAL DATE INFORMATION              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS MERCHANDISE AREA TABLE                                              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS APPLICATION AVAILABILITY TABLE                                      
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  DEPARTMENT / MAJOR CLASS NUMBER CURSOR                                 
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE DEPT-MCL-NBR-CSR CURSOR FOR                               
                  SELECT                                                        
                      DEPT_NBR                                                  
                     ,MAJ_CL_NBR                                                
                  FROM                                                          
                      TMDSEAR A                                                 
                     ,TAPLAVL B                                                 
                                                                                
                  WHERE A.OPERCO_NBR     = :PC-OPERCO-NBR                       
                    AND A.MDSELV_CDE     = 'MCL'                                
                    AND :ML114-EXTRACT-DTE                                      
                         BETWEEN A.STRT_DTE                                     
                             AND A.END_DTE                                      
                    AND A.MDSEAR_DKEY    =  B.MDSEAR_DKEY                       
                    AND B.APLSYS_CDE     = 'ML'                                 
                    AND :ML114-EXTRACT-DTE                                      
                         BETWEEN B.MDSEAR_STRT_DTE                              
                             AND B.MDSEAR_END_DTE                               
                                                                                
              UNION                                                             
                                                                                
                  SELECT DISTINCT                                          CL**3
                      DEPT_NBR                                                  
                     ,MAJ_CL_NBR                                                
                  FROM                                                          
                      TMNMCL                                                    
                  WHERE FSCL_MN_END_DTE                                         
                            BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
                                AND :ML114-EXTRACT-DTE                          
W26603        WITH UR                                                           
           END-EXEC.                                                            
      ****************************************************************          
      *  LEDGER CURSOR                                                          
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE LEDGER-CSR CURSOR FOR                                     
                  SELECT                                                        
                      FSCL_MN_NBR                                               
                     ,FSCL_MN_END_DTE                                           
                     ,SLS_RTL_AMT                                               
                     ,MKDN_RTL_AMT                                              
                     ,MKUP_RTL_AMT                                              
                     ,XFER_IN_RTL_AMT                                           
                     ,XFER_OUT_RTL_AMT                                          
                     ,RCPT_RTL_AMT                                              
                     ,RCPT_NET_COST_AMT                                         
                     ,PADJ_RTL_AMT                                              
                     ,PADJ_NET_COST_AMT                                         
                     ,INV_ADJ_RTL_AMT                                           
                     ,SHNK_RTL_AMT                                              
                     ,END_INV_RTL_AMT                                           
                     ,END_INV_COST_AMT                                          
                     ,SLS_COST_AMT                                              
                     ,GRO_MRGN_AMT                                              
                  FROM                                                          
                      TMNMCL                                                    
                  WHERE FSCL_MN_END_DTE                                         
                            BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
                                AND :ML114-EXTRACT-DTE                          
                    AND DEPT_NBR   = :MDSEAR-DEPT-NBR                           
                    AND MAJ_CL_NBR = :MDSEAR-MAJ-CL-NBR                         
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
               OUTPUT LEDGER-EXTRACT-DATE-FILE                                  
                       LEDGER-EXTRACT-FILE.                                     
                                                                                
           PERFORM 1150-SET-EXTRACT-DTE.                                        
                                                                                
           PERFORM 8200-OPEN-DEPT-MCL-NBR-CSR.                                  
                                                                                
           PERFORM 8300-FETCH-DEPT-MCL-NBR-CSR.                                 
                                                                                
           PERFORM 2000-EXTRACT-DEPT-DATA                                       
               UNTIL PS-END-OF-DEPT-MCL-NBR-CSR.                                
                                                                                
           PERFORM 8400-CLOSE-DEPT-MCL-NBR-CSR.                                 
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
                 LEDGER-EXTRACT-DATE-FILE                                       
                 LEDGER-EXTRACT-FILE.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1150-SET-EXTRACT-DTE.                                                    
                                                                                
           PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
                                                                                
           IF PS-END-OF-CONTROL-CARDS                                           
           OR CC-EXTRACT-DTE = SPACE                                            
               PERFORM 1170-USE-ML-CNTL-DTES                                    
           ELSE                                                                 
               DISPLAY '** INPUT CONTROL CARD: ' CC-CONTROL-CARD                
               PERFORM 1160-USE-CONTROL-CARD-DTE                                
           END-IF.                                                              
                                                                                
           DISPLAY '** SELECTED EXTRACT DATE: ' ML114-EXTRACT-DTE.              
                                                                                
           PERFORM  7100-WRITE-LEDGER-EXTRACT-DATE.                             
                                                                                
      *    GET THE MONTH-END DATE FOR THE LAST MONTH OF THE PREVIOUS            
      *    ** PREVIOUS FISCAL YEAR.  THIS DATE WILL BE USED AS THE              
      *    ** STARTING DATE FOR EXTRACTING THE MONTHLY DATA SO                  
      *    ** THAT THE BEGINNING INVENTORIES FOR THE OPEN FISCAL                
      *    ** YEAR CAN BE DETERMINED.                                           
                                                                                
           MOVE PV-FSCL-YR-BEG-DTE  TO DTATTR-KY-DTE.                           
                                                                                
           PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
                                                                                
032300     IF PS-DATE-NOT-FOUND                                                 
032400         DISPLAY '** ABEND **'                                            
032500         DISPLAY '** PROGRAM MLKUT404 **'                                 
032600         DISPLAY '** PARAGRAPH 1150-SET-EXTRACT-DTE'                      
032700         DISPLAY '** PREV FSCL YR BEG DTE IS NOT A VALID DATE.'           
032800         MOVE +4008 TO ABEND-CODE                                         
032900         PERFORM 9999-ABEND                                               
033000     END-IF.                                                              
033100                                                                          
           MOVE DTATTR-PFM-END-DTE      TO PV-PREV-FSCL-YR-END-DTE.             
                                                                                
033400                                                                          
033500 1160-USE-CONTROL-CARD-DTE.                                               
033600                                                                          
033700     DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'               
033800                                                                          
033900     MOVE CC-EXTRACT-DTE TO DTATTR-KY-DTE.                                
034000                                                                          
034100     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
034200                                                                          
034300     IF  PS-DATE-FOUND                                                    
034400     AND DTATTR-KY-DTE = DTATTR-FSCL-MN-END-DTE                           
034500         MOVE DTATTR-FSCL-MN-END-DTE TO ML114-EXTRACT-DTE                 
034600         MOVE DTATTR-FSCL-MN-NBR     TO ML114-EXTRACT-MN-NBR              
034700         MOVE DTATTR-FSCL-YR-BEG-DTE TO PV-FSCL-YR-BEG-DTE                
034800     ELSE                                                                 
034900         DISPLAY '** ABEND **'                                            
035000         DISPLAY '** PROGRAM MLKUT404 **'                                 
035100         DISPLAY '** PARAGRAPH 1160-USE-CONTROL-CARD-DTE'                 
035200         DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'                
035300         DISPLAY '** MONTH-END DATE. '                                    
035400         MOVE +4003 TO ABEND-CODE                                         
035500         PERFORM 9999-ABEND                                               
035600     END-IF.                                                              
035700                                                                          
035800                                                                          
035900 1170-USE-ML-CNTL-DTES.                                                   
036000                                                                          
036100     PERFORM 8000-GET-ML-CNTL-DTES.                                       
                                                                                
036300     DISPLAY                                                              
036400      '** POSTING FISCAL MONTH DATE: ' MLCNTL-PSTG-FSCL-MN-DTE.           
036500     DISPLAY                                                              
036600      '** MAX POSTING MONTH DATE: ' MLCNTL-MXPSTG-FSCL-MN-DTE.            
036700                                                                          
036800     MOVE MLCNTL-MXPSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                     
036900                                                                          
037000     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
037100                                                                          
037200     IF PS-DATE-NOT-FOUND                                                 
037300         DISPLAY '** ABEND **'                                            
037400         DISPLAY '** PROGRAM MLKUT411 **'                                 
037500         DISPLAY '** PARAGRAPH 1160-USE-ML-CNTL-DTE'                      
037600         DISPLAY '** MAX POSTING DATE IS NOT A VALID DATE.'               
037700         MOVE +4008 TO ABEND-CODE                                         
037800         PERFORM 9999-ABEND                                               
037900     END-IF.                                                              
038000                                                                          
038100     MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE.                 
038200     MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR.              
038300     MOVE DTATTR-FSCL-YR-NBR        TO PV-FSCL-YR-NBR.                    
038400     MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE.                
038500                                                                          
038600     MOVE MLCNTL-PSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                       
038700                                                                          
038800     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
038900                                                                          
039000     IF PS-DATE-NOT-FOUND                                                 
039100         DISPLAY '** ABEND **'                                            
039200         DISPLAY '** PROGRAM MLKUT404 **'                                 
039300         DISPLAY '** PARAGRAPH 1160-USE-ML-CNTL-DTE'                      
039400         DISPLAY                                                          
               '** POSTING FISCAL MONTH DATE IS NOT A VALID DATE.'              
039500         MOVE +4008 TO ABEND-CODE                                         
039600         PERFORM 9999-ABEND                                               
039700     END-IF.                                                              
039800                                                                          
039900     IF DTATTR-FSCL-YR-NBR NOT = PV-FSCL-YR-NBR                           
040000         DISPLAY                                                          
               '** USING POSTING FISCAL MONTH FOR EXTRACT DATE.'                
040100         MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE              
040200         MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR           
040300         MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE             
040400     ELSE                                                                 
040500         DISPLAY '** USING MAX POSTING MONTH FOR EXTRACT DATE.'           
040600     END-IF.                                                              
040700                                                                          
                                                                                
       2000-EXTRACT-DEPT-DATA.                                                  
                                                                                
           INITIALIZE ML102-LEDGER-EXTRACT-REC.                                 
                                                                                
041300     PERFORM 2050-GET-MBS-INFO.                                           
041400                                                                          
           SET PS-NOT-END-OF-LEDGER-CSR TO TRUE.                                
           PERFORM 8500-OPEN-LEDGER-CSR.                                        
                                                                                
           PERFORM 8600-FETCH-LEDGER-CSR.                                       
                                                                                
000900     MOVE MDSEAR-MAJ-CL-NBR TO             ML102-MAJ-CL-NBR.              
                                                                                
           PERFORM 3000-EXTRACT-MONTHS-FOR-DEPT                                 
               UNTIL PS-END-OF-LEDGER-CSR.                                      
                                                                                
           PERFORM 8700-CLOSE-LEDGER-CSR.                                       
                                                                                
           PERFORM 7200-WRITE-LEDGER-EXTRACT.                                   
           PERFORM 8300-FETCH-DEPT-MCL-NBR-CSR.                                 
                                                                                
042800                                                                          
042900 2050-GET-MBS-INFO.                                                       
043000                                                                          
043100*****************************************************************         
043200* GET THE MBS INFO THAT WAS EFFECTIVE FOR THE MOST RECENT PERIOD          
043300* BEING REPORTED. IF THE DEPARTMENT WAS NOT ACTIVE AT THAT TIME,          
043400* GET THE MOST RECENT MBS INFO FOR THE DEPARTMENT.                        
043500*****************************************************************         
043600                                                                          
043700     PERFORM 8060-GET-EFFECTIVE-MBS-INFO.                                 
043800                                                                          
043900     IF PS-MBS-INFO-NOT-FOUND                                             
044000         PERFORM 8070-GET-LATEST-MBS-INFO                                 
044100     END-IF.                                                              
044200                                                                          
044300     MOVE MDSEAR-DEPT-NBR     TO ML102-DEPT-NBR.                          
044400                                                                          
044500     IF PS-MBS-INFO-FOUND                                                 
044600         MOVE MDSEAR-GMA-NBR  TO ML102-GMA-NBR                            
044700         MOVE MDSEAR-DMA-NBR  TO ML102-DMA-NBR                            
044800         MOVE MDSEAR-BYR-NBR  TO ML102-BYR-NBR                            
044900     END-IF.                                                              
045000                                                                          
                                                                                
       3000-EXTRACT-MONTHS-FOR-DEPT.                                            
                                                                                
           IF MNMCL-FSCL-MN-END-DTE = PV-PREV-FSCL-YR-END-DTE                   
               MOVE MNMCL-END-INV-COST-AMT                                      
                                TO ML102-BEG-INV-COST-AMT                       
               MOVE MNMCL-END-INV-RTL-AMT                                       
                                TO ML102-BEG-INV-RTL-AMT                        
           ELSE                                                                 
               MOVE MNMCL-FSCL-MN-NBR                                           
                                TO SS-FSCL-MN                                   
               MOVE MNMCL-SLS-RTL-AMT                                           
                                TO ML102-SLS-RTL-AMT       (SS-FSCL-MN)         
               MOVE MNMCL-MKDN-RTL-AMT                                          
                                TO ML102-MKDN-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-MKUP-RTL-AMT                                          
                                TO ML102-MKUP-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-XFER-IN-RTL-AMT                                       
                                TO ML102-XFER-IN-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNMCL-XFER-OUT-RTL-AMT                                      
                                TO ML102-XFER-OUT-RTL-AMT  (SS-FSCL-MN)         
               MOVE MNMCL-RCPT-RTL-AMT                                          
                                TO ML102-RCPT-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-RCPT-NET-COST-AMT                                     
                                TO ML102-RCPT-NET-COST-AMT (SS-FSCL-MN)         
               MOVE MNMCL-PADJ-RTL-AMT                                          
                                TO ML102-PADJ-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-PADJ-NET-COST-AMT                                     
                                TO ML102-PADJ-NET-COST-AMT (SS-FSCL-MN)         
               MOVE MNMCL-INV-ADJ-RTL-AMT                                       
                                TO ML102-INV-ADJ-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNMCL-SHNK-RTL-AMT                                          
                                TO ML102-SHNK-RTL-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-END-INV-RTL-AMT                                       
                                TO ML102-END-INV-RTL-AMT   (SS-FSCL-MN)         
               MOVE MNMCL-END-INV-COST-AMT                                      
                                TO ML102-END-INV-COST-AMT  (SS-FSCL-MN)         
               MOVE MNMCL-SLS-COST-AMT                                          
                                TO ML102-SLS-COST-AMT      (SS-FSCL-MN)         
               MOVE MNMCL-GRO-MRGN-AMT                                          
                                TO ML102-GRO-MRGN-AMT      (SS-FSCL-MN)         
           END-IF.                                                              
                                                                                
           PERFORM 8600-FETCH-LEDGER-CSR.                                       
                                                                                
                                                                                
       7000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
                                                                                
                                                                                
       7100-WRITE-LEDGER-EXTRACT-DATE.                                          
                                                                                
           WRITE LEDGER-EXTRACT-DATE-RECORD                                     
               FROM ML114-LEDGER-EXTRACT-DATE-REC.                              
                                                                                
                                                                                
       7200-WRITE-LEDGER-EXTRACT.                                               
                                                                                
           WRITE LEDGER-EXTRACT-RECORD                                          
               FROM ML102-LEDGER-EXTRACT-REC.                                   
                                                                                
                                                                                
       8000-GET-ML-CNTL-DTES.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    PSTG_FSCL_MN_DTE                                            
                   ,MXPSTG_FSCL_MN_DTE                                          
                INTO                                                            
                    :MLCNTL-PSTG-FSCL-MN-DTE                                    
                   ,:MLCNTL-MXPSTG-FSCL-MN-DTE                                  
                FROM                                                            
                    TMLCNTL                                                     
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8000-GET-ML-CNTL-DTES' TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMLCNTL'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8050-GET-DATE-ATTRIBUTES.                                                
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_YR_NBR                                                 
                   ,FSCL_MN_NBR                                                 
                   ,FSCL_MN_END_DTE                                             
                   ,FSCL_YR_BEG_DTE                                             
                   ,PFM_END_DTE                                                 
                INTO                                                            
                    :DTATTR-FSCL-YR-NBR                                         
                   ,:DTATTR-FSCL-MN-NBR                                         
                   ,:DTATTR-FSCL-MN-END-DTE                                     
                   ,:DTATTR-FSCL-YR-BEG-DTE                                     
                   ,:DTATTR-PFM-END-DTE                                         
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    KY_DTE = :DTATTR-KY-DTE                                     
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-DATE-FOUND     TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-DATE-NOT-FOUND TO TRUE                                
               WHEN OTHER                                                       
                   DISPLAY '** DTATTR-KY-DTE: ' DTATTR-KY-DTE                   
                   MOVE    '8050-GET-DATE-ATTRIBUTES'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8060-GET-EFFECTIVE-MBS-INFO.                                             
                                                                                
      ******************************************************************        
      * GET THE MBS INFO FROM THE EFFECTIVE ROW FOR THE DEPARTMENT.             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    GMA_NBR                                                     
                   ,DMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR                                                     
                WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
                  AND MDSELV_CDE  = 'DPT'                                       
                  AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
                  AND :ML114-EXTRACT-DTE                                        
                       BETWEEN STRT_DTE                                         
                           AND END_DTE                                          
W26603        WITH UR                                                           
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-MBS-INFO-FOUND TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8060-GET-EFFECTIVE-MBS-INFO'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMDSEAR'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8070-GET-LATEST-MBS-INFO.                                                
                                                                                
      ******************************************************************        
      * GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    GMA_NBR                                                     
                   ,DMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR                                                     
                WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
                  AND MDSELV_CDE  = 'DPT'                                       
                  AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
                  AND END_DTE     =                                             
                        (SELECT MAX (END_DTE)                                   
                         FROM   TMDSEAR                                         
                         WHERE  OPERCO_NBR = :PC-OPERCO-NBR                     
                           AND  MDSELV_CDE = 'DPT'                         CL**3
                           AND  DEPT_NBR   = :MDSEAR-DEPT-NBR)                  
W26603        WITH UR                                                           
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-MBS-INFO-FOUND TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8070-GET-LATEST-MBS-INFO'                              
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMDSEAR'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
                                                                                
       8200-OPEN-DEPT-MCL-NBR-CSR.                                              
                                                                                
               EXEC SQL                                                         
                    OPEN DEPT-MCL-NBR-CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8200-OPEN-DEPT-MCL-NBR-CSR'                        
                                                TO PV-PARAGRAPH-NAME            
                       MOVE 'OPEN DEPT-MCL-NBR-CSR'                             
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9998-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
                                                                                
       8300-FETCH-DEPT-MCL-NBR-CSR.                                             
      *****************************************************************         
      * RETRY LOGIC IS NOT INCLUDED IN THIS PARAGRAPH BECAUSE A -911            
      * CAUSES THE CURSOR TO BE CLOSED.  ALSO, FOR THIS CURSOR,                 
      * THE ROWS ARE ACTUALLY RETRIEVED WHEN THE CURSOR IS OPENED, NOT          
      * WHEN THE ROW IS FETCHED, SO IF A PAGE WAS LOCKED, A -911                
      * WOULD OCCUR ON THE OPEN, NOT ON THE FETCH.                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   DEPT-MCL-NBR-CSR                                             
               INTO                                                             
                   :MDSEAR-DEPT-NBR                                             
                  ,:MDSEAR-MAJ-CL-NBR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-DEPT-MCL-NBR-CSR TO TRUE                       
               WHEN OTHER                                                       
                   MOVE '8300-FETCH-DEPT-MCL-NBR-CSR'                           
                                           TO PV-PARAGRAPH-NAME                 
                   MOVE 'FETCH DEPT-MCL-NBR-CSR'                                
                                           TO PV-DB2-OPER-ATTEMPTED             
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8400-CLOSE-DEPT-MCL-NBR-CSR.                                             
                                                                                
           EXEC SQL                                                             
               CLOSE DEPT-MCL-NBR-CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8400-CLOSE-DEPT-MCL-NBR-CSR'                           
                                           TO PV-PARAGRAPH-NAME                 
                   MOVE 'CLOSE DEPT-MCL-NBR-CSR'                        D       
                                           TO PV-DB2-OPER-ATTEMPTED             
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8500-OPEN-LEDGER-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                OPEN LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8500-OPEN-LEDGER-CSR'                                  
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN LEDGER-CSR'                                       
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8600-FETCH-LEDGER-CSR.                                                   
      *****************************************************************         
      * RETRY LOGIC IS NOT INCLUDED IN THIS PARAGRAPH BECAUSE A -911            
      * CAUSES THE CURSOR TO BE CLOSED.                                         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   LEDGER-CSR                                                   
               INTO                                                             
                  :MNMCL-FSCL-MN-NBR                                            
                 ,:MNMCL-FSCL-MN-END-DTE                                        
                 ,:MNMCL-SLS-RTL-AMT                                            
                 ,:MNMCL-MKDN-RTL-AMT                                           
                 ,:MNMCL-MKUP-RTL-AMT                                           
                 ,:MNMCL-XFER-IN-RTL-AMT                                        
                 ,:MNMCL-XFER-OUT-RTL-AMT                                       
                 ,:MNMCL-RCPT-RTL-AMT                                           
                 ,:MNMCL-RCPT-NET-COST-AMT                                      
                 ,:MNMCL-PADJ-RTL-AMT                                           
                 ,:MNMCL-PADJ-NET-COST-AMT                                      
                 ,:MNMCL-INV-ADJ-RTL-AMT                                        
                 ,:MNMCL-SHNK-RTL-AMT                                           
                 ,:MNMCL-END-INV-RTL-AMT                                        
                 ,:MNMCL-END-INV-COST-AMT                                       
                 ,:MNMCL-SLS-COST-AMT                                           
                 ,:MNMCL-GRO-MRGN-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-LEDGER-CSR TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '8600-FETCH-LEDGER-CSR'                                 
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8700-CLOSE-LEDGER-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               CLOSE LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8700-CLOSE-LEDGER-CSR'                                 
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKUT411'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
079700                                                                          
079800 9999-ABEND.                                                              
079900                                                                          
080000     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
