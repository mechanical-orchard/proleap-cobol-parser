00001 ***************************************************************** 01/08/96
00002  IDENTIFICATION DIVISION.                                         RCKRP011
00003 *****************************************************************    LV028
00004                                                                      CL*17
00005  PROGRAM-ID.             RCKRP011.                                   CL*17
00006  AUTHOR.                 STEPHANIE HERON.                            CL*17
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL*17
00008  DATE-WRITTEN            12-19-95.                                   CL*17
00009  DATE-COMPILED.                                                      CL*17
00010                                                                      CL*17
00011 *****************************************************************    CL*17
00012 *                                                               *    CL*17
00013 *  THIS PROGRAM WILL CREATE THE MONTHLY UNITS VERIFIED REPORT   *    CL*17
00014 *  FROM THE DAILY EXTRACT FILE.                                 *    CL*17
00015 *                                                               *    CL*17
00016 *****************************************************************    CL*17
00017 *                                                               *    CL*17
00018 *  CHANGED MM/DD/YY BY XXXXXXXXXXX                              *    CL*17
00019 *   WR# XXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX *    CL*17
00020 *                                                               *    CL*17
00021 *                                                               *    CL*17
00022 *****************************************************************    CL*17
00023  ENVIRONMENT DIVISION.                                               CL*17
00024 *****************************************************************    CL*17
00025                                                                      CL*17
00026  CONFIGURATION SECTION.                                              CL*17
00027                                                                      CL*17
00028  SOURCE-COMPUTER.        IBM-3090.                                   CL*17
00029  OBJECT-COMPUTER.        IBM-3090.                                   CL*17
00030                                                                      CL*17
00031  INPUT-OUTPUT SECTION.                                               CL*17
00032                                                                      CL*17
00033  FILE-CONTROL.                                                       CL*17
00034                                                                      CL*17
00035      SELECT STORE-SUMMED-EXTRACT-FILE                                CL*17
00036          ASSIGN TO INP01.                                            CL*17
00037                                                                      CL*17
00038      SELECT RECV-REPORT-FILE                                         CL*17
00039          ASSIGN TO RPT01.                                            CL*17
00040                                                                      CL*17
00041                                                                      CL*17
00042 ******************************************************************   CL*17
00043  DATA DIVISION.                                                      CL*17
00044 ******************************************************************   CL*17
00045                                                                      CL*17
00046  FILE SECTION.                                                       CL*17
00047                                                                      CL*17
00048  FD  STORE-SUMMED-EXTRACT-FILE                                       CL*17
00049      RECORDING MODE IS F                                             CL*17
00050      LABEL RECORDS ARE STANDARD                                      CL*17
00051      BLOCK CONTAINS 0 RECORDS.                                       CL*17
00052                                                                      CL*17
00053  01  RC010-INPUT-RECORD               PIC  X(150).                   CL*17
00054                                                                      CL*17
00055  FD  RECV-REPORT-FILE                                                CL*17
00056      RECORDING MODE IS F                                             CL*17
00057      BLOCK CONTAINS 0  RECORDS.                                      CL*17
00058  01  RECV-REPORT-RECORD              PIC  X(132).                    CL*17
00059                                                                      CL*17
00060  WORKING-STORAGE SECTION.                                            CL*17
00061                                                                      CL*17
00062  01  FILLER.                                                         CL*17
00063      05  FILLER                      PIC  X(36)    VALUE             CL*17
00064          ' ***** WORKING STORAGE PGM=RCKRP011 '.                     CL*17
00065      05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.      CL*17
00066      05  FILLER                      PIC  X(10)    VALUE             CL*17
00067          ' LOCATION='.                                               CL*17
00068      05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.      CL*17
00069      05  FILLER                      PIC  X(06)    VALUE             CL*17
00070          ' *****'.                                                   CL*17
00071                                                                      CL*17
00072  01  PROGRAM-SWITCHES.                                               CL*17
00073      05  PS-NO-MORE-RECV-RECS-IND    PIC  X(01)    VALUE 'Y'.        CL*17
00074          88  PS-MORE-RECV-RECS                     VALUE 'Y'.        CL*17
00075          88  PS-NO-MORE-RECV-RECS                  VALUE 'N'.        CL*17
00076                                                                      CL*17
00077  01  PC-CONSTANTS.                                                   CL*17
00078      05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.        CL*28
00079      05  PC-2                        PIC S9(03)    VALUE +2          CL*17
00080                                                    COMP-3.           CL*17
00081      05  PC-4                        PIC S9(03)    VALUE +4          CL*17
00082                                                    COMP-3.           CL*17
00083      05  PC-60                       PIC S9(03)    VALUE +60         CL*17
00084                                                    COMP-3.           CL*17
00085      05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE             CL*17
00086                                                    'RCKRP011'.       CL*17
00085      05  PC-LOCATION-MODULE          PIC  X(08)    VALUE             CL*17
00086                                                    'XLKUT071'.       CL*17
00087      05  PC-REPORT-NBR-1             PIC  9(01)    VALUE 1.          CL*17
00088      05  PC-UNKNOWN                  PIC  X(07)    VALUE             CL*17
00089                                                    'UNKNOWN'.        CL*17
00090      05  PC-MEN-FALLS                PIC  X(15)    VALUE             CL*17
00091          'MENOMONEE FALLS'.                                          CL*17
00092      05  PC-FINDLAY                  PIC  X(15)    VALUE             CL*17
00093          'FINDLAY        '.                                          CL*17
00094                                                                      CL*17
00095                                                                      CL*17
00096  01  PV-ACCUMULATOR-FIELDS.                                          CL*17
00097      05  PV-PAGE-CNT                 PIC S9(04)    VALUE ZERO        CL*17
00098                                                    COMP-3.           CL*17
00099      05  PV-LINE-CNT                 PIC S9(04)    VALUE ZERO        CL*17
00100                                                    COMP SYNC.        CL*17
00101      05  PV-DPT-VRFYD-QTY           PIC S9(07)    VALUE ZERO         CL*17
00102                                                    COMP-3.           CL*17
00103      05  PV-DPT-GOH-QTY             PIC S9(07)    VALUE ZERO         CL*17
00104                                                    COMP-3.           CL*17
00105      05  PV-DPT-PRETKT-QTY          PIC S9(07)    VALUE ZERO         CL*17
00106                                                    COMP-3.           CL*17
00107      05  PV-BYR-VRFYD-QTY           PIC S9(07)    VALUE ZERO         CL*17
00108                                                    COMP-3.           CL*17
00109      05  PV-BYR-GOH-QTY             PIC S9(07)    VALUE ZERO         CL*17
00110                                                    COMP-3.           CL*17
00111      05  PV-BYR-PRETKT-QTY          PIC S9(07)    VALUE ZERO         CL*17
00112                                                    COMP-3.           CL*17
00113      05  PV-DMM-VRFYD-QTY            PIC S9(09)    VALUE ZERO        CL*17
00114                                                    COMP-3.           CL*17
00115      05  PV-DMM-GOH-QTY              PIC S9(09)    VALUE ZERO        CL*17
00116                                                    COMP-3.           CL*17
00117      05  PV-DMM-PRETKT-QTY           PIC S9(09)    VALUE ZERO        CL*17
00118                                                    COMP-3.           CL*17
00119      05  PV-OPR-VRFYD-QTY            PIC S9(09)    VALUE ZERO        CL*18
00120                                                    COMP-3.           CL*18
00121      05  PV-OPR-GOH-QTY              PIC S9(09)    VALUE ZERO        CL*18
00122                                                    COMP-3.           CL*18
00123      05  PV-OPR-PRETKT-QTY           PIC S9(09)    VALUE ZERO        CL*18
00124                                                    COMP-3.           CL*18
00125                                                                      CL*17
00126                                                                      CL*17
00127  01  SAVE-FIELDS.                                                    CL*17
00128      05  SAVE-DMM-NBR                PIC  X(02)    VALUE SPACES.     CL*17
00129      05  SAVE-BYR-NBR                PIC  X(03)    VALUE SPACES.     CL*17
00130      05  SAVE-DPT-NBR                PIC  X(03)    VALUE SPACES.     CL*17
00131      05  SAVE-DMM-NAME               PIC  X(25)    VALUE SPACES.     CL*17
00132      05  SAVE-BYR-NAME               PIC  X(25)    VALUE SPACES.     CL*17
00133      05  SAVE-OPER-UNT-ID            PIC  X(04)    VALUE SPACES.     CL*18
00134                                                                      CL*17
00135  01  PV-MISC-FIELDS.                                                 CL*17
00136      05  ABEND-CODE                  PIC S9(04)    VALUE ZERO        CL*21
00137                                                    COMP SYNC.        CL*17
00138      05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.     CL*17
00139      05  PV-PARA                         PIC X(30)   VALUE SPACES.   CL*20
00140                                                                      CL*20
00141      05  PV-PARAGRAPH-NAME               PIC  X(30)  VALUE SPACE.    CL*20
00142      05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(30)  VALUE SPACE.    CL*20
00143      05  PV-OPER-UNT-ID              PIC  S9(4) COMP.                CL*18
00144      05  PV-FCLTY-ID                 PIC  S9(4) COMP.                CL*20
00145                                                                      CL*17
00146      05  PV-CURR-DATE-YYMMDD.                                        CL*17
00147          10  PV-CURR-DATE-YY         PIC  9(02)    VALUE ZERO.       CL*17
00148          10  PV-CURR-DATE-MM         PIC  9(02)    VALUE ZERO.       CL*17
00149          10  PV-CURR-DATE-DD         PIC  9(02)    VALUE ZERO.       CL*17
00150                                                                      CL*17
00151      05  PV-CURR-TIME.                                               CL*17
00152          10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.       CL*17
00153          10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.       CL*17
00154          10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.       CL*17
00155                                                                      CL*17
00156      05  PV-HOLD-CURR-DATE-MMDDYY    PIC  9(06)    VALUE ZERO.       CL*17
00157      05  PV-HOLD-CURR-DATE-MMDDYY-X  REDEFINES                       CL*17
00158          PV-HOLD-CURR-DATE-MMDDYY.                                   CL*17
00159          10  PV-HOLD-CURR-DATE-MM    PIC  9(02).                     CL*17
00160          10  PV-HOLD-CURR-DATE-DD    PIC  9(02).                     CL*17
00161          10  PV-HOLD-CURR-DATE-YY    PIC  9(02).                     CL*17
00162                                                                      CL*17
00163      05  PV-DATE-SHIPPED.                                            CL*17
00164          10  PV-DATE-SHIPPED-MM      PIC  9(02).                     CL*17
00165          10  PV-DATE-SHIPPED-DD      PIC  9(02).                     CL*17
00166          10  PV-DATE-SHIPPED-CCYY    PIC  9(04).                     CL*17
00167          10  FILLER REDEFINES PV-DATE-SHIPPED-CCYY.                  CL*17
00168              15  PV-DATE-SHIPPED-CC      PIC  9(02).                 CL*17
00169              15  PV-DATE-SHIPPED-YY      PIC  9(02).                 CL*17
00170                                                                      CL*17
00171                                                                      CL*17
00172      COPY DPWS004.                                                   CL*20
00173 ******************************************************************   CL*17
00174 *  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                  CL*17
00175 ******************************************************************   CL*17
00176      COPY DPWS132O.                                                  CL*17
00177                                                                      CL*17
00178  01  HEADING-LINES.                                                  CL*17
00179      05  HEADING-LINE-0.                                             CL*17
00180          10  FILLER                  PIC  X(44)    VALUE SPACES.     CL*17
00181          10  FILLER                  PIC  X(33)    VALUE             CL*24
00182              'TOTAL NUMBER OF UNITS VERIFIED - '.                    CL*18
00183          10  HL0-FCLTY-NBR           PIC  X(04)    VALUE SPACES.     CL*25
00184          10  FILLER                  PIC  X(01)    VALUE SPACES.     CL*25
00185          10  HL0-FCLTY-NAME          PIC  X(25)    VALUE SPACES.     CL*24
00186          10  FILLER                  PIC  X(55)    VALUE SPACES.     CL*17
00187                                                                      CL*17
00188      05  HEADING-LINE-1.                                             CL*17
00189          10  FILLER                  PIC X(04)     VALUE             CL*17
00190              ' DMM'.                                                 CL*17
00191          10  FILLER                  PIC X(03)     VALUE SPACES.     CL*17
00192          10  HL1-DMM-NBR             PIC X(02)     VALUE SPACES.     CL*17
00193          10  FILLER                  PIC X(03)     VALUE SPACES.     CL*17
00194          10  HL1-DMM-NAME            PIC X(25)     VALUE SPACES.     CL*17
00195          10  FILLER                  PIC X(98)     VALUE SPACES.     CL*17
00196                                                                      CL*17
00197      05  HEADING-LINE-2.                                             CL*17
00198          10  FILLER                  PIC X(06)     VALUE             CL*17
00199              ' BUYER'.                                               CL*17
00200          10  FILLER                  PIC X(01)     VALUE SPACES.     CL*17
00201          10  HL1-BYR-NBR             PIC X(03)     VALUE SPACES.     CL*17
00202          10  FILLER                  PIC X(02)     VALUE SPACES.     CL*17
00203          10  HL1-BYR-NAME            PIC X(25)     VALUE SPACES.     CL*17
00204          10  FILLER                  PIC X(98)     VALUE SPACES.     CL*17
00205                                                                      CL*17
00206      05  HEADING-LINE-3.                                             CL*17
00207          10  FILLER                  PIC X(06)     VALUE SPACES.     CL*17
00208          10  FILLER                  PIC X(11)     VALUE SPACES.     CL*17
00209          10  FILLER                  PIC X(05)     VALUE             CL*17
00210              'VRFYD'.                                                CL*17
00211          10  FILLER                  PIC X(24)     VALUE SPACES.     CL*17
00212          10  FILLER                  PIC X(03)     VALUE             CL*17
00213              'PRE'.                                                  CL*17
00214          10  FILLER                  PIC X(98)     VALUE SPACES.     CL*17
00215                                                                      CL*17
00216                                                                      CL*17
00217      05  HEADING-LINE-4.                                             CL*17
00218          10  FILLER                  PIC X(06)     VALUE SPACES.     CL*17
00219          10  FILLER                  PIC X(04)     VALUE             CL*17
00220              'DEPT'.                                                 CL*17
00221          10  FILLER                  PIC X(08)     VALUE SPACES.     CL*17
00222          10  FILLER                  PIC X(03)     VALUE             CL*17
00223              'QTY'.                                                  CL*17
00224          10  FILLER                  PIC X(13)     VALUE SPACES.     CL*17
00225          10  FILLER                  PIC X(03)     VALUE             CL*17
00226              'GOH'.                                                  CL*17
00227          10  FILLER                  PIC X(09)     VALUE SPACES.     CL*17
00228          10  FILLER                  PIC X(03)     VALUE             CL*17
00229              'TKT'.                                                  CL*17
00230          10  FILLER                  PIC X(98)     VALUE SPACES.     CL*17
00231                                                                      CL*17
00232                                                                      CL*17
00233  01  MISCELLANEOUS-LINES.                                            CL*17
00234      05  END-OF-REPORT-LINE.                                         CL*17
00235          10  FILLER                  PIC  X(44)    VALUE SPACES.     CL*17
00236          10  FILLER                  PIC  X(45)    VALUE             CL*17
00237              '*******   E N D   O F   R E P O R T   *******'.        CL*17
00238          10  FILLER                  PIC  X(43)    VALUE SPACES.     CL*17
00239                                                                      CL*17
00240      05  BLANK-LINE                  PIC X(132)    VALUE SPACES.     CL*17
00241                                                                      CL*17
00242      05  BYR-TOTAL-LINE1.                                            CL*17
00243          10  FILLER                  PIC  X(10)    VALUE             CL*17
00244              ' BYR TOTAL'.                                           CL*17
00245          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00246          10  T1-VRFYD-QTY            PIC  Z,ZZZ,ZZ9                  CL*17
00247                                           VALUE SPACES.              CL*17
00248          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00249          10  T1-GOH-QTY              PIC  Z,ZZZ,ZZ9                  CL*17
00250                                           VALUE SPACES.              CL*17
00251          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00252          10  T1-PRETKT-QTY           PIC  Z,ZZZ,ZZ9                  CL*17
00253                                           VALUE SPACES.              CL*17
00254                                                                      CL*17
00255      05  DMM-TOTAL-LINE3.                                            CL*17
00256          10  FILLER                  PIC  X(05)    VALUE             CL*17
00257              ' DMM '.                                                CL*17
00258          10  T2-DMM-NBR              PIC  X(02)    VALUE SPACES.     CL*17
00259          10  FILLER                  PIC  X(06)    VALUE             CL*17
00260              ' TOTAL'.                                               CL*17
00261          10  FILLER                  PIC  X(01)    VALUE SPACES.     CL*17
00262          10  T2-VRFYD-QTY            PIC  Z,ZZZ,ZZ9                  CL*17
00263                                                    VALUE ZEROES.     CL*17
00264          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00265          10  T2-GOH-QTY              PIC  Z,ZZZ,ZZ9                  CL*17
00266                                                    VALUE ZEROES.     CL*17
00267          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00268          10  T2-PRETKT-QTY           PIC  Z,ZZZ,ZZ9                  CL*17
00269                                                    VALUE ZEROES.     CL*17
00270                                                                      CL*17
00271      05  OPR-TOTAL-LINE1.                                            CL*18
00272          10  FILLER                  PIC  X(12)    VALUE             CL*22
00273              ' FCLTY TOTAL'.                                         CL*22
00274          10  FILLER                  PIC  X(02)    VALUE SPACES.     CL*22
00275          10  T3-VRFYD-QTY            PIC  Z,ZZZ,ZZ9                  CL*18
00276                                                    VALUE ZEROES.     CL*18
00277          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*18
00278          10  T3-GOH-QTY              PIC  Z,ZZZ,ZZ9                  CL*18
00279                                                    VALUE ZEROES.     CL*18
00280          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*18
00281          10  T3-PRETKT-QTY           PIC  Z,ZZZ,ZZ9                  CL*18
00282                                                    VALUE ZEROES.     CL*18
00283  01  DETAIL-LINES.                                                   CL*17
00284      05  DETAIL-LINE1.                                               CL*17
00285          10  FILLER                  PIC  X(07)    VALUE SPACES.     CL*17
00286          10  DL1-DPT-NBR             PIC  X(03)    VALUE SPACES.     CL*17
00287          10  FILLER                  PIC  X(06)    VALUE SPACES.     CL*17
00288          10  DL1-VRFYD-QTY           PIC  ZZZ,ZZ9   VALUE ZEROES.    CL*17
00289          10  FILLER                  PIC  X(06)    VALUE SPACES.     CL*17
00290          10  DL1-GOH-QTY             PIC  ZZZ,ZZ9   VALUE ZEROES.    CL*17
00291          10  FILLER                  PIC  X(06)    VALUE SPACES.     CL*17
00292          10  DL1-PRETKT-QTY          PIC  ZZZ,ZZ9   VALUE ZEROES.    CL*17
00293          10  FILLER                  PIC  X(04)    VALUE SPACES.     CL*17
00294                                                                      CL*17
00295 ******************************************************************   CL*17
00296 *    STORE SUMMED EXTRACT FILE LAYOUT                                CL*17
00297 ******************************************************************   CL*17
00298      COPY RCRD010.                                                   CL*17
00305                                                                      CL*19
00295 ******************************************************************   CL*17
00296 *    LOCATION MODULE COPYBOOK                                        CL*17
00297 ******************************************************************   CL*17
00298      COPY XLWS071.                                                   CL*17
00305                                                                      CL*19
00314 *****************************************************************    CL*17
00315 * PROCEDURE DIVISION.                                           *    CL*17
00316 *****************************************************************    CL*17
00317  PROCEDURE DIVISION.                                                 CL*17
00318                                                                      CL*17
00319  0000-MAINLINE-PROCESSING.                                           CL*17
00320                                                                      CL*17
00321      PERFORM 1000-INITIALIZATION.                                    CL*17
00322                                                                      CL*17
00323      PERFORM 2000-PROCESS-RECV-RPT-RECS                              CL*17
00324        UNTIL PS-NO-MORE-RECV-RECS.                                   CL*17
00325                                                                      CL*17
00326      PERFORM 2200-PRINT-DPT-LNE.                                     CL*17
00327                                                                      CL*17
00328      PERFORM 2300-PRINT-BYR-LNE.                                     CL*17
00329                                                                      CL*17
00330      PERFORM 2400-PRINT-DMM-LNE.                                     CL*17
00331                                                                      CL*17
00332      PERFORM 2600-PRINT-OPR-LNE.                                     CL*18
00333                                                                      CL*18
00334      WRITE RECV-REPORT-RECORD                                        CL*17
00335        FROM END-OF-REPORT-LINE                                       CL*17
00336          AFTER ADVANCING 2 LINES.                                    CL*17
00337                                                                      CL*17
00338      CLOSE STORE-SUMMED-EXTRACT-FILE                                 CL*17
00339            RECV-REPORT-FILE.                                         CL*17
00340                                                                      CL*17
00341      STOP RUN.                                                       CL*17
00342                                                                      CL*17
00344 ******************************************************************   CL*17
00345 ** READ THE DB2 CONTROL TABLE AND READ THE FIRST ASN EXTRACT REC *   CL*17
00346 ******************************************************************   CL*17
00347  1000-INITIALIZATION.                                                CL*17
00348                                                                      CL*17
00349      OPEN INPUT  STORE-SUMMED-EXTRACT-FILE                           CL*17
00350           OUTPUT RECV-REPORT-FILE.                                   CL*17
00351                                                                      CL*17
00352      ACCEPT PV-CURR-DATE-YYMMDD FROM DATE.                           CL*17
00353      MOVE PV-CURR-DATE-YY TO PV-HOLD-CURR-DATE-YY.                   CL*17
00354      MOVE PV-CURR-DATE-MM TO PV-HOLD-CURR-DATE-MM.                   CL*17
00355      MOVE PV-CURR-DATE-DD TO PV-HOLD-CURR-DATE-DD.                   CL*17
00356                                                                      CL*17
00357      ACCEPT PV-CURR-TIME FROM TIME.                                  CL*17
00358                                                                      CL*17
00359      MOVE PV-CURR-DATE-MM        TO DP132O-RUN-MONTH.                CL*17
00360      MOVE PV-CURR-DATE-DD        TO DP132O-RUN-DAY.                  CL*17
00361      MOVE PV-CURR-DATE-YY        TO DP132O-RUN-YEAR.                 CL*17
00362                                                                      CL*17
00363      MOVE PV-CURR-TIME-HH        TO DP132O-RUN-HOUR.                 CL*17
00364      MOVE PV-CURR-TIME-MM        TO DP132O-RUN-MINUTE.               CL*17
00365      MOVE PC-CURRENT-PROGRAM-NAME                                    CL*17
00366                                  TO DP132O-PROGRAM-NAME.             CL*17
00367      PERFORM 7000-READ-INPUT.                                        CL*17
00368                                                                      CL*17
00369      MOVE RC010-DMM-NBR          TO SAVE-DMM-NBR.                    CL*17
00370      MOVE RC010-BUYER-NBR        TO SAVE-BYR-NBR.                    CL*17
00371      MOVE RC010-DEPT-NBR         TO SAVE-DPT-NBR.                    CL*17
00372      MOVE RC010-BUYER-NAME       TO SAVE-BYR-NAME.                   CL*17
00373      MOVE RC010-DMM-NAME         TO SAVE-DMM-NAME.                   CL*17
00374      MOVE RC010-OPER-UNT-ID      TO SAVE-OPER-UNT-ID                 CL*25
00375                                     HL0-FCLTY-NBR.                   CL*25
00376                                                                      CL*17
00377      PERFORM 2800-GET-FCLTY-NAME.                                    CL*18
00378                                                                      CL*18
00381      PERFORM 8000-PRINT-HEADINGS.                                    CL*17
00382                                                                      CL*17
00383 ******************************************************************   CL*17
00384 *  PROCESS THE RECEIVING REPORT FILE.                            *   CL*17
00385 *                                                                *   CL*17
00386 ******************************************************************   CL*17
00387  2000-PROCESS-RECV-RPT-RECS.                                         CL*17
00388                                                                      CL*17
00389      IF RC010-OPER-UNT-ID       =   SAVE-OPER-UNT-ID                 CL*20
00390         IF RC010-DMM-NBR        =   SAVE-DMM-NBR                     CL*18
00391            IF RC010-BUYER-NBR   =   SAVE-BYR-NBR                     CL*18
00392               IF RC010-DEPT-NBR =   SAVE-DPT-NBR                     CL*18
00393                 CONTINUE                                             CL*18
00394               ELSE                                                   CL*18
00395                  PERFORM 2200-PRINT-DPT-LNE                          CL*18
00396                  INITIALIZE    PV-DPT-PRETKT-QTY                     CL*18
00397                                PV-DPT-GOH-QTY                        CL*18
00398                                PV-DPT-VRFYD-QTY                      CL*18
00399                  MOVE RC010-DEPT-NBR    TO SAVE-DPT-NBR              CL*18
00400               END-IF                                                 CL*18
00401            ELSE                                                      CL*18
00402               PERFORM 2200-PRINT-DPT-LNE                             CL*18
00403               PERFORM 2300-PRINT-BYR-LNE                             CL*18
00404               WRITE RECV-REPORT-RECORD FROM BLANK-LINE               CL*18
00405                     AFTER ADVANCING 2 LINE                           CL*18
00406               ADD +2 TO PV-LINE-CNT                                  CL*18
00407               INITIALIZE    PV-DPT-PRETKT-QTY   PV-BYR-PRETKT-QTY    CL*18
00408                             PV-DPT-GOH-QTY      PV-BYR-GOH-QTY       CL*18
00409                             PV-DPT-VRFYD-QTY    PV-BYR-VRFYD-QTY     CL*18
00410               MOVE RC010-DEPT-NBR      TO SAVE-DPT-NBR               CL*18
00411               MOVE RC010-BUYER-NBR     TO SAVE-BYR-NBR               CL*18
00412               MOVE RC010-BUYER-NAME       TO SAVE-BYR-NAME           CL*18
00413               MOVE RC010-DMM-NAME         TO SAVE-DMM-NAME           CL*18
00414               PERFORM 8010-PRINT-DPT-HEAD                            CL*18
00415               WRITE RECV-REPORT-RECORD FROM BLANK-LINE               CL*18
00416                     AFTER ADVANCING 1 LINE                           CL*18
00417               ADD +1 TO PV-LINE-CNT                                  CL*18
00418            END-IF                                                    CL*18
00419         ELSE                                                         CL*18
00420            PERFORM 2200-PRINT-DPT-LNE                                CL*18
00421            PERFORM 2300-PRINT-BYR-LNE                                CL*18
00422            PERFORM 2400-PRINT-DMM-LNE                                CL*18
00423            INITIALIZE PV-DPT-PRETKT-QTY PV-BYR-PRETKT-QTY            CL*18
00424                       PV-DMM-PRETKT-QTY  PV-DPT-GOH-QTY              CL*18
00425                       PV-BYR-GOH-QTY     PV-DMM-GOH-QTY              CL*18
00426                       PV-DPT-VRFYD-QTY   PV-BYR-VRFYD-QTY            CL*18
00427                       PV-DMM-VRFYD-QTY                               CL*18
00428            MOVE RC010-DEPT-NBR      TO SAVE-DPT-NBR                  CL*18
00429            MOVE RC010-BUYER-NBR     TO SAVE-BYR-NBR                  CL*18
00430            MOVE RC010-DMM-NBR       TO SAVE-DMM-NBR                  CL*18
00431            MOVE RC010-BUYER-NAME    TO SAVE-BYR-NAME                 CL*18
00432            MOVE RC010-DMM-NAME      TO SAVE-DMM-NAME                 CL*18
00433            PERFORM 8000-PRINT-HEADINGS                               CL*18
00434         END-IF                                                       CL*18
00435      ELSE                                                            CL*18
00436         PERFORM 2200-PRINT-DPT-LNE                                   CL*18
00437         PERFORM 2300-PRINT-BYR-LNE                                   CL*18
00438         PERFORM 2400-PRINT-DMM-LNE                                   CL*18
00439         PERFORM 2600-PRINT-OPR-LNE                                   CL*18
00440         INITIALIZE PV-DPT-PRETKT-QTY PV-BYR-PRETKT-QTY               CL*18
00441                    PV-DMM-PRETKT-QTY  PV-DPT-GOH-QTY                 CL*18
00442                    PV-BYR-GOH-QTY     PV-DMM-GOH-QTY                 CL*18
00443                    PV-DPT-VRFYD-QTY   PV-BYR-VRFYD-QTY               CL*18
00444                    PV-DMM-VRFYD-QTY   PV-OPR-PRETKT-QTY              CL*18
00445                    PV-OPR-VRFYD-QTY   PV-OPR-GOH-QTY                 CL*18
00446         MOVE RC010-DEPT-NBR      TO SAVE-DPT-NBR                     CL*18
00447         MOVE RC010-BUYER-NBR     TO SAVE-BYR-NBR                     CL*18
00448         MOVE RC010-DMM-NBR       TO SAVE-DMM-NBR                     CL*18
00449         MOVE RC010-BUYER-NAME    TO SAVE-BYR-NAME                    CL*18
00450         MOVE RC010-DMM-NAME      TO SAVE-DMM-NAME                    CL*18
00451         MOVE RC010-OPER-UNT-ID   TO SAVE-OPER-UNT-ID                 CL*18
00452                                     HL0-FCLTY-NBR                    CL*25
00453         PERFORM 2800-GET-FCLTY-NAME                                  CL*18
00455         MOVE 0 TO PV-PAGE-CNT                                        CL*26
00456         PERFORM 8000-PRINT-HEADINGS                                  CL*18
00457      END-IF.                                                         CL*18
00458                                                                      CL*18
00459      PERFORM 2100-ACCUM-FIELDS.                                      CL*17
00460                                                                      CL*17
00461      PERFORM 7000-READ-INPUT.                                        CL*17
00462                                                                      CL*17
00463  2100-ACCUM-FIELDS.                                                  CL*17
00464 ******************************************************************   CL*17
00465 *  THIS PARAGRAPH ADDS THE CURRENT QUANTITIES TO THE DEPT TOTALS *   CL*17
00466 ******************************************************************   CL*17
00467                                                                      CL*17
00468      ADD RC010-VRFYD-CNT             TO PV-DPT-VRFYD-QTY             CL*17
00469                                         PV-BYR-VRFYD-QTY             CL*17
00470                                         PV-DMM-VRFYD-QTY             CL*18
00471                                         PV-OPR-VRFYD-QTY.            CL*26
00472                                                                      CL*17
00473      IF RC010-GOH-IND = 'Y'                                          CL*17
00474         ADD RC010-VRFYD-CNT     TO PV-DPT-GOH-QTY                    CL*17
00475                                    PV-BYR-GOH-QTY                    CL*17
00476                                    PV-DMM-GOH-QTY                    CL*17
00477                                    PV-OPR-GOH-QTY                    CL*18
00478      END-IF.                                                         CL*17
00479                                                                      CL*17
00480      IF RC010-PRE-TKT-IND = 'Y'                                      CL*17
00481         ADD RC010-VRFYD-CNT     TO PV-DPT-PRETKT-QTY                 CL*17
00482                                    PV-BYR-PRETKT-QTY                 CL*17
00483                                    PV-DMM-PRETKT-QTY                 CL*17
00484                                    PV-OPR-PRETKT-QTY                 CL*18
00485      END-IF.                                                         CL*17
00486                                                                      CL*17
00487  2200-PRINT-DPT-LNE.                                                 CL*17
00488 ******************************************************************   CL*17
00489 *  THIS PARAGRAPH WILL PRINT THE DETAIL LINE FOR A DEPARTMENT    *   CL*17
00490 ******************************************************************   CL*17
00491                                                                      CL*17
00492      IF  PV-LINE-CNT > PC-LINE-LIMIT                                 CL*17
00493          PERFORM 8000-PRINT-HEADINGS                                 CL*17
00494      END-IF.                                                         CL*17
00495                                                                      CL*17
00496      MOVE SAVE-DPT-NBR         TO DL1-DPT-NBR.                       CL*17
00497      MOVE PV-DPT-VRFYD-QTY     TO DL1-VRFYD-QTY.                     CL*17
00498      MOVE PV-DPT-GOH-QTY       TO DL1-GOH-QTY.                       CL*17
00499      MOVE PV-DPT-PRETKT-QTY    TO DL1-PRETKT-QTY.                    CL*17
00500                                                                      CL*17
00501      WRITE RECV-REPORT-RECORD                                        CL*17
00502        FROM DETAIL-LINE1                                             CL*17
00503          AFTER ADVANCING 1 LINE.                                     CL*17
00504                                                                      CL*17
00505      ADD +1 TO PV-LINE-CNT.                                          CL*17
00506                                                                      CL*17
00507  2300-PRINT-BYR-LNE.                                                 CL*17
00508 ******************************************************************   CL*17
00509 *  THIS PARAGRAPH WILL PRINT THE BUYER SUBTOTAL LINE             *   CL*17
00510 ******************************************************************   CL*17
00511                                                                      CL*17
00512      IF  PV-LINE-CNT > PC-LINE-LIMIT                                 CL*17
00513          PERFORM 8000-PRINT-HEADINGS                                 CL*17
00514      END-IF.                                                         CL*17
00515                                                                      CL*17
00516      MOVE PV-BYR-VRFYD-QTY     TO T1-VRFYD-QTY.                      CL*17
00517      MOVE PV-BYR-GOH-QTY       TO T1-GOH-QTY.                        CL*17
00518      MOVE PV-BYR-PRETKT-QTY    TO T1-PRETKT-QTY.                     CL*17
00519                                                                      CL*17
00520      WRITE RECV-REPORT-RECORD                                        CL*17
00521        FROM BYR-TOTAL-LINE1                                          CL*17
00522          AFTER ADVANCING 2 LINE.                                     CL*17
00523                                                                      CL*17
00524      ADD +2 TO PV-LINE-CNT.                                          CL*17
00525  2400-PRINT-DMM-LNE.                                                 CL*17
00526 ******************************************************************   CL*17
00527 *  THIS PARAGRAPH WILL PRINT THE BUYER SUBTOTAL LINE             *   CL*17
00528 ******************************************************************   CL*17
00529                                                                      CL*17
00530      IF  PV-LINE-CNT > PC-LINE-LIMIT                                 CL*17
00531          PERFORM 8000-PRINT-HEADINGS                                 CL*17
00532      END-IF.                                                         CL*17
00533                                                                      CL*17
00534      MOVE SAVE-DMM-NBR         TO T2-DMM-NBR.                        CL*17
00535      MOVE PV-DMM-VRFYD-QTY     TO T2-VRFYD-QTY.                      CL*17
00536      MOVE PV-DMM-GOH-QTY       TO T2-GOH-QTY.                        CL*17
00537      MOVE PV-DMM-PRETKT-QTY    TO T2-PRETKT-QTY.                     CL*17
00538                                                                      CL*17
00539      WRITE RECV-REPORT-RECORD                                        CL*17
00540        FROM DMM-TOTAL-LINE3                                          CL*17
00541          AFTER ADVANCING 1 LINE.                                     CL*17
00542                                                                      CL*17
00543      ADD +1 TO PV-LINE-CNT.                                          CL*27
00544  2600-PRINT-OPR-LNE.                                                 CL*18
00545 ******************************************************************   CL*18
00546 *  THIS PARAGRAPH WILL PRINT THE BUYER SUBTOTAL LINE             *   CL*18
00547 ******************************************************************   CL*18
00548                                                                      CL*18
00549      IF  PV-LINE-CNT > PC-LINE-LIMIT                                 CL*18
00550          PERFORM 8000-PRINT-HEADINGS                                 CL*18
00551      END-IF.                                                         CL*18
00552                                                                      CL*18
00553      MOVE PV-OPR-VRFYD-QTY     TO T3-VRFYD-QTY.                      CL*18
00554      MOVE PV-OPR-GOH-QTY       TO T3-GOH-QTY.                        CL*18
00555      MOVE PV-OPR-PRETKT-QTY    TO T3-PRETKT-QTY.                     CL*18
00556                                                                      CL*18
00557      WRITE RECV-REPORT-RECORD                                        CL*18
00558        FROM OPR-TOTAL-LINE1                                          CL*18
00559          AFTER ADVANCING 2 LINE.                                     CL*22
00560                                                                      CL*18
00561      ADD +2 TO PV-LINE-CNT.                                          CL*18
00562                                                                      CL*18
00563                                                                      CL*17
00564  2800-GET-FCLTY-NAME.                                                CL*18
00565                                                                      CL*18
090500     INITIALIZE XL071-COMMAREA-REC.                                       
090600     MOVE RC010-OPER-UNT-ID      TO XL071-LOCATION-NUMBER.                
090700                                                                          
090800     CALL PC-LOCATION-MODULE  USING                                       
091000***GB 02/010/15 BATCH CALL SHOULD NOT USE XL071-COMMAREA                  
091000***GB 02/010/15                     XL071-COMMAREA                        
091100                                    XL071-COMMAREA-REC.                   
091300     EVALUATE TRUE                                                        
091400         WHEN XL071-NO-ERRORS                                             
247100             MOVE XL071-LOC-NM  TO HL0-FCLTY-NAME                         
091700         WHEN XL071-LOC-NOT-FOUND                                         
070000             MOVE SPACES        TO HL0-FCLTY-NAME                         
092000         WHEN XL071-BAD-DB2-RETURN                                        
071300             MOVE '2800-GET-FCLTY-NAME ' TO PV-PARAGRAPH-NAME             
00591              MOVE 'ERROR ON SELECT TO XLT_LOC TABLE'                 CL*20
00592                               TO  PV-DB2-OPERATION-ATTEMPTED         CL*20
00593              PERFORM 9100-SQL-ABEND                                  CL*20
092900     END-EVALUATE.                                                        
072600                                                                          
00596 ******************************************************************   CL*17
00597 *  READ THE ASN EXTRACT RECORDS.                                 *   CL*17
00598 ******************************************************************   CL*17
00599  7000-READ-INPUT.                                                    CL*17
00600                                                                      CL*17
00601      MOVE '7000'                  TO PGM-LOCATION.                   CL*17
00602      MOVE '7000-READ-INPUT           '                               CL*17
00603                                   TO PV-ABEND-PARAGRAPH.             CL*17
00604                                                                      CL*17
00605      READ STORE-SUMMED-EXTRACT-FILE                                  CL*17
00606        INTO RC010-REC                                                CL*17
00607          AT END                                                      CL*17
00608            SET PS-NO-MORE-RECV-RECS TO TRUE.                         CL*17
00609                                                                      CL*17
00610                                                                      CL*17
00611  8000-PRINT-HEADINGS.                                                CL*17
00612 ******************************************************************   CL*17
00613 *  PRINT THE HEADING FOR THE FIRST PAGE OF AN ASN.  SUBSEQUENT   *   CL*17
00614 *  PAGES FOR THE SAME ASN DO NOT REPEAT ALL OF THE HEADING INFO. *   CL*17
00615 ******************************************************************   CL*17
00616                                                                      CL*17
00617      ADD 1                        TO PV-PAGE-CNT.                    CL*17
00618                                                                      CL*17
00619      MOVE PV-PAGE-CNT             TO DP132O-PAGE-NUMBER.             CL*17
00620                                                                      CL*17
00621      MOVE PC-REPORT-NBR-1        TO DP132O-REPORT-NUMBER.            CL*17
00622                                                                      CL*17
00623      WRITE RECV-REPORT-RECORD                                        CL*17
00624        FROM DP132O-STANDRD-HEADER-132-COLS                           CL*17
00625          AFTER ADVANCING PAGE.                                       CL*17
00626                                                                      CL*17
00627      WRITE RECV-REPORT-RECORD                                        CL*17
00628        FROM HEADING-LINE-0                                           CL*17
00629          AFTER ADVANCING 2 LINE.                                     CL*17
00630                                                                      CL*17
00631      MOVE 2                       TO PV-LINE-CNT.                    CL*17
00632                                                                      CL*17
00633      MOVE SAVE-DMM-NBR            TO HL1-DMM-NBR.                    CL*17
00634      MOVE SAVE-DMM-NAME           TO HL1-DMM-NAME.                   CL*17
00635                                                                      CL*17
00636      WRITE RECV-REPORT-RECORD                                        CL*17
00637        FROM HEADING-LINE-1                                           CL*17
00638          AFTER ADVANCING 2 LINES.                                    CL*17
00639                                                                      CL*17
00640      ADD +2                       TO PV-LINE-CNT.                    CL*27
00641                                                                      CL*27
00642      PERFORM 8010-PRINT-DPT-HEAD.                                    CL*17
00643                                                                      CL*17
00644      WRITE RECV-REPORT-RECORD                                        CL*17
00645        FROM HEADING-LINE-3                                           CL*17
00646          AFTER ADVANCING 2 LINES.                                    CL*17
00647                                                                      CL*17
00648      ADD +2                       TO PV-LINE-CNT.                    CL*27
00649                                                                      CL*17
00650      WRITE RECV-REPORT-RECORD                                        CL*17
00651        FROM HEADING-LINE-4                                           CL*17
00652          AFTER ADVANCING 1 LINES.                                    CL*17
00653                                                                      CL*17
00654      ADD +1                          TO PV-LINE-CNT.                 CL*27
00655                                                                      CL*17
00656  8010-PRINT-DPT-HEAD.                                                CL*17
00657      MOVE SAVE-BYR-NBR            TO HL1-BYR-NBR.                    CL*17
00658      MOVE SAVE-BYR-NAME           TO HL1-BYR-NAME.                   CL*17
00659                                                                      CL*17
00660      WRITE RECV-REPORT-RECORD                                        CL*17
00661        FROM HEADING-LINE-2                                           CL*17
00662          AFTER ADVANCING 1 LINES.                                    CL*17
00663                                                                      CL*17
00664                                                                      CL*17
00665      ADD +2                          TO PV-LINE-CNT.                 CL*27
00666 /                                                                    CL*17
00667 ******************************************************************   CL*18
00668 * 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE         CL*18
00669 * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                    CL*18
00670 ******************************************************************   CL*18
00671  9100-SQL-ABEND.                                                     CL*18
00672      DISPLAY '** ABEND     **'.                                      CL*18
00673      DISPLAY '** PROGRAM   ** = RCKUT031'.                           CL*18
00674      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL*18
00675      DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL*18
00676                                                                      CL*18
