       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   POKUT222.                                                  
       AUTHOR.       KRITIVEEN KUSHAL.                                          
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 11/15/2019.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS IS A EDW VS EP ONORDER COMPARISION PROGRAM.               *        
      ******************************************************************        
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      *  WR/PBI        DATE        DESCRIPTION OF CHANGES                       
      *  ----------    --------    ------------------------------------         
      *  CHGXXXXXXX    11/15/19    INITIAL IMPLEMENTATION.                      
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT ONORDER-EDW-FILE ASSIGN TO INP01.                             
           SELECT ONORDER-EP-FILE  ASSIGN TO INP02.                             
           SELECT EDW-EP-OUT-FILE  ASSIGN TO OUT01.                             
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  ONORDER-EDW-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ONORDER-EDW-RECORD                PIC X(104).                        
                                                                                
                                                                                
       FD  ONORDER-EP-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ONORDER-EP-RECORD                 PIC X(300).                        
                                                                                
       FD  EDW-EP-OUT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EDW-EP-OUT-RECORD                 PIC X(80).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-SQL-SUB                 PIC S9(4)  VALUE +0 COMP.              
           05 PV-PARAGRAPH-NAME          PIC X(30)  VALUE SPACES.               
           05 PV-DB2-OPERATION-ATTEMPTED PIC X(30)  VALUE SPACES.               
           05 PV-ABEND-CODE              PIC S9(04) COMP                        
                                                    VALUE ZERO.                 
           05 PV-SQLCODE-DISPLAY         PIC +(8)9  VALUE ZEROES.               
           05  PV-CURR-SEAS.                                                    
               15  PV-CURR-SEAS-SSN      PIC X(01).                             
               15  PV-CURR-SEAS-YY       PIC 9(04).                             
               10  FILLER                PIC X(01)        VALUE 'W'.            
               10  PV-CURR-SEAS-PP       PIC 9(02).                             
           05  PV-CURR-DAY-NBR           PIC 9(03).                             
                                                                                
           05  PV-CURRENT-DATE.                                                 
               10 PV-CURRENT-YEAR        PIC X(04).                             
               10 FILLER                 PIC X(01)        VALUE '-'.            
               10 PV-CURRENT-MONTH       PIC X(02).                             
               10 FILLER                 PIC X(01)        VALUE '-'.            
               10 PV-CURRENT-DAY         PIC X(02).                             
           05  PV-DATE-COMPARE           REDEFINES                              
               PV-CURRENT-DATE           PIC X(10).                             
                                                                                
           05  PV-EP-ONORDER-SUM         PIC S9(16)V9(05) VALUE ZERO.           
           05  PV-EDW-ONORDER-SUM        PIC S9(16)V9(05) VALUE ZERO.           
           05  PV-ONORDER-DIFF           PIC S9(16)V9(05) VALUE ZERO.           
           05  PV-RTL-PAST-FASHION       PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-RTL-PAST-FASTTRK       PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-RTL-PAST-REPLEN        PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-RTL-CURR-FASHION       PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-RTL-CURR-FASTTRK       PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-RTL-CURR-REPLEN        PIC S9(06)V9(05) VALUE ZERO.           
           05  PV-ONORDER-DIFF-COMP      PIC 9(16)        VALUE ZERO.           
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-EDW-FILE-SW     PIC X(01)        VALUE 'N'.            
               88  PS-END-OF-EDW-FILE                     VALUE 'Y'.            
               88  PS-NOT-END-OF-EDW-FILE                 VALUE 'N'.            
                                                                                
           05  PS-END-OF-EP-FILE-SW      PIC X(01)        VALUE 'N'.            
               88  PS-END-OF-EP-FILE                      VALUE 'Y'.            
               88  PS-NOT-END-OF-EP-FILE                  VALUE 'N'.            
                                                                                
           05  PS-DIFF-MORE-THAN-50M     PIC X(01)        VALUE 'N'.            
               88  PS-MORE-THAN-50M                       VALUE 'Y'.            
               88  PS-NOT-MORE-THAN-50M                   VALUE 'N'.            
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-EDW-ROWS-READ           PIC S9(09) COMP-3                     
                                                          VALUE ZEROES.         
           05  PA-EP-ROWS-READ            PIC S9(09) COMP-3                     
                                                          VALUE ZEROES.         
       01  ABEND-CODE                     PIC S9(04) COMP SYNC                  
                                                          VALUE 0.              
           88  ABEND-4001-CC-MISSING-ERR                  VALUE +4001.          
           88  ABEND-4002-CC-INVALID-ERR                  VALUE +4002.          
                                                                                
      *****************************************************************         
      * PROGRAM CONSTANTS                                                       
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC X(08)                           
                                                   VALUE 'POKUT222'.            
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +5 COMP.           
           05  PC-COMMIT-MAX                PIC 9(09) VALUE   999.              
                                                                                
      ******************************************************************        
      * INPUT CONTROL CARD DEFINITION                                           
      ******************************************************************        
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUNDATE                PIC X(08).                             
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT                                                      
      ******************************************************************        
                                                                                
       01  OUT-REC-LAYOUT.                                                      
           05  PV-OUT-REC-LINE1          PIC X(10)        VALUE                 
                  'Hi All,'.                                                    
           05  PV-OUT-REC-LINE2          PIC X(10)        VALUE SPACES.         
           05  PV-OUT-REC-LINE3.                                                
               10  PV-OUT-REC-LINE3A     PIC X(80)       VALUE                  
                  'Please find the reconcilation between EP and EDW'.           
           05  PV-OUT-REC-LINE4.                                                
               10  PV-OUT-REC-LINE4A     PIC X(48)       VALUE                  
                  'for On-order and past due for the week ending - '.           
               10  PV-WEEK               PIC 9(2)         VALUE ZERO.           
               10  PV-COMMA              PIC x(1)         VALUE '.'.            
           05  PV-LINE-L1.                                                      
               10  PV-LINE-L1A           PIC X(19)        VALUE                 
                  'EP Total           '.                                        
               10  PV-EP-ONORDER-SUMD    PIC z,zzz,zzz,zzz,zzz,zzz.99.          
           05  PV-LINE-L2.                                                      
               10  PV-LINE-L2A           PIC X(16)        VALUE                 
                  'EDW Total       '.                                           
               10  PV-EDW-ONORDER-SUMD   PIC z,zzz,zzz,zzz,zzz,zzz.99.          
           05  PV-LINE-L3.                                                      
               10  PV-LINE-L3A           PIC X(17)        VALUE                 
                  'Difference       '.                                          
               10  PV-ONORDER-DIFFD      PIC z,zzz,zzz,zzz,zzz,zzz.99.          
           05  PV-DIFF-MORE-50M-S1       PIC X(47)        VALUE                 
                  'EP Vs EDW On-order difference is less than 50M.'.            
           05  PV-DIFF-MORE-50M-S2       PIC X(47)        VALUE                 
                  'EP Vs EDW On-order difference is more than 50M.'.            
           05  PV-EOM-NOTE1              PIC X(10)        VALUE                 
                  'Thanks,'.                                                    
           05  PV-EOM-NOTE2              PIC X(12)        VALUE                 
                  'Merch Team.'.                                                
                                                                                
      ******************************************************************        
      * DATE ATTRIBUTE TABLE                                                    
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * PO ON-ORDER EDW INPUT FILE LAYOUT.                                      
      ******************************************************************        
           COPY PORD018.                                                        
                                                                                
      ******************************************************************        
      * FORMATTED OUTPUT RECORD LAYOUT FOR PO DATA.                             
      ******************************************************************        
           COPY POWS090.                                                        
                                                                                
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-DATE-PROCESSING.                                        
                                                                                
           PERFORM 3000-SUM-EDW-ONORDER                                         
             UNTIL PS-END-OF-EDW-FILE.                                          
                                                                                
           PERFORM 4000-SUM-EP-ONORDER                                          
             UNTIL PS-END-OF-EP-FILE.                                           
                                                                                
           PERFORM 5000-EP-EDW-COMP.                                            
                                                                                
           PERFORM 7000-WRITE-OUT-REC.                                          
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
           INITIALIZE DCLTDTATTR                                                
                                                                                
           OPEN INPUT  ONORDER-EDW-FILE                                         
                       ONORDER-EP-FILE                                          
           OPEN OUTPUT EDW-EP-OUT-FILE.                                         
                                                                                
      ******************************************************************        
      * 3000-SUM-EDW-ONORDER.                                                   
      ******************************************************************        
       3000-SUM-EDW-ONORDER.                                                    
                                                                                
           PERFORM 3100-READ-ONORDER-EDW-FILE.                                  
           IF PO018-ONORD-WEEK-DATE = PV-CURRENT-DATE                           
           COMPUTE PV-EDW-ONORDER-SUM =                                         
                   PV-EDW-ONORDER-SUM + PO018-ONORD-RTL-AMT.                    
                                                                                
      ******************************************************************        
      * 4000-SUM-EP-ONORDER.                                                    
      ******************************************************************        
       4000-SUM-EP-ONORDER.                                                     
                                                                                
           PERFORM 4100-READ-ONORDER-EP-FILE.                                   
           IF PO090-CALENDAR-NAME = PV-CURR-SEAS                                
             MOVE PO090-PASTDUEREG-DLR  TO PV-RTL-PAST-FASHION                  
             MOVE PO090-PASTDUEFT-DLR   TO PV-RTL-PAST-FASTTRK                  
             MOVE PO090-PASTDUERPLN-DLR TO PV-RTL-PAST-REPLEN                   
             MOVE PO090-CURWREG-DLR     TO PV-RTL-CURR-FASHION                  
             MOVE PO090-CURWFT-DLR      TO PV-RTL-CURR-FASTTRK                  
             MOVE PO090-CURWRPLN-DLR    TO PV-RTL-CURR-REPLEN                   
                                                                                
           COMPUTE PV-EP-ONORDER-SUM =  PV-EP-ONORDER-SUM                       
                                      + PV-RTL-PAST-FASHION                     
                                      + PV-RTL-PAST-FASTTRK                     
                                      + PV-RTL-PAST-REPLEN                      
                                      + PV-RTL-CURR-FASHION                     
                                      + PV-RTL-CURR-FASTTRK                     
                                      + PV-RTL-CURR-REPLEN.                     
                                                                                
      ******************************************************************        
      * 3100-READ-ONORDER-EDW-FILE.                                             
      ******************************************************************        
       3100-READ-ONORDER-EDW-FILE.                                              
                                                                                
           READ ONORDER-EDW-FILE                                                
           INTO PO018-ONORD-EXTRACT-RECORD                                      
             AT END                                                             
               SET PS-END-OF-EDW-FILE TO TRUE                                   
             NOT AT END                                                         
               ADD +1                    TO  PA-EDW-ROWS-READ                   
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 4100-READ-ONORDER-EP-FILE.                                              
      ******************************************************************        
       4100-READ-ONORDER-EP-FILE.                                               
                                                                                
           READ ONORDER-EP-FILE                                                 
           INTO POWS090-RECORD                                                  
             AT END                                                             
               SET PS-END-OF-EP-FILE TO TRUE                                    
             NOT AT END                                                         
               ADD +1                    TO  PA-EP-ROWS-READ                    
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  2000-DATE-PROCESSING.                                                  
      ******************************************************************        
       2000-DATE-PROCESSING.                                                    
                                                                                
           ACCEPT CC-CONTROL-CARD.                                              
                                                                                
           DISPLAY 'INPUT CONTROL CARD = ', CC-CONTROL-CARD.                    
                                                                                
           MOVE CC-RUNDATE (1:4)         TO PV-CURRENT-YEAR.                    
           MOVE CC-RUNDATE (5:2)         TO PV-CURRENT-MONTH.                   
           MOVE CC-RUNDATE (7:2)         TO PV-CURRENT-DAY.                     
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_YR_NBR                                                 
                   ,FSCL_WK_NBR                                                 
                   ,SEA_DESC                                                    
                INTO                                                            
                    :DTATTR-FSCL-YR-NBR                                         
                   ,:DTATTR-FSCL-WK-NBR                                         
                   ,:DTATTR-SEA-DESC                                            
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    KY_DTE = :PV-DATE-COMPARE                                   
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE DTATTR-SEA-DESC(1:1)    TO PV-CURR-SEAS-SSN             
                   MOVE DTATTR-FSCL-YR-NBR      TO PV-CURR-SEAS-YY              
                   MOVE DTATTR-FSCL-WK-NBR      TO PV-CURR-SEAS-PP              
                   DISPLAY ' '                                                  
                   DISPLAY 'PROCESSING FISCAL YEAR ' PV-CURR-SEAS-YY            
                           ',FISCAL MONTH '          PV-CURR-SEAS-PP            
                   DISPLAY ' '                                                  
               WHEN OTHER                                                       
                   MOVE    '1100-DATE-PROCESSING'                               
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                             TO PV-DB2-OPERATION-ATTEMPTED                      
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      * 5000-EP-EDW-COMP.                                                       
      ******************************************************************        
       5000-EP-EDW-COMP.                                                        
           COMPUTE PV-EP-ONORDER-SUM = PV-EP-ONORDER-SUM * 1000                 
           SUBTRACT PV-EDW-ONORDER-SUM FROM PV-EP-ONORDER-SUM                   
                                       GIVING PV-ONORDER-DIFF ROUNDED           
                   MOVE PV-EP-ONORDER-SUM  TO PV-EP-ONORDER-SUMD                
                   MOVE PV-EDW-ONORDER-SUM TO PV-EDW-ONORDER-SUMD               
                   MOVE PV-CURR-SEAS-PP    TO PV-WEEK.                          
                   MOVE PV-ONORDER-DIFF    TO PV-ONORDER-DIFFD                  
                   MOVE PV-ONORDER-DIFF(1:16) TO PV-ONORDER-DIFF-COMP           
                  DISPLAY 'PV-ONORDER-DIFF-COMP ' PV-ONORDER-DIFF-COMP          
                   IF PV-ONORDER-DIFF-COMP > 50000000                           
                      SET PS-MORE-THAN-50M TO TRUE                              
                      MOVE 4050 TO RETURN-CODE                             CL**3
                   END-IF.                                                      
      *****************************************************************         
      * 7000-WRITE-OUT-REC.                                           *         
      *****************************************************************         
       7000-WRITE-OUT-REC.                                                      
                                                                                
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE1                       
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE2                       
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE3                       
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE4                       
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE2                       
            WRITE EDW-EP-OUT-RECORD FROM PV-LINE-L1                             
            WRITE EDW-EP-OUT-RECORD FROM PV-LINE-L2                             
            WRITE EDW-EP-OUT-RECORD FROM PV-LINE-L3                             
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE2                       
           IF PS-NOT-MORE-THAN-50M                                              
            WRITE EDW-EP-OUT-RECORD FROM PV-DIFF-MORE-50M-S1                    
           ELSE                                                                 
            WRITE EDW-EP-OUT-RECORD FROM PV-DIFF-MORE-50M-S2                    
           END-IF                                                               
            WRITE EDW-EP-OUT-RECORD FROM PV-OUT-REC-LINE2                       
            WRITE EDW-EP-OUT-RECORD FROM PV-EOM-NOTE1                           
            WRITE EDW-EP-OUT-RECORD FROM PV-EOM-NOTE2.                          
                                                                                
       EJECT.                                                                   
      *****************************************************************         
      * 8000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE ONORDER-EDW-FILE                                               
                 ONORDER-EP-FILE                                                
                 EDW-EP-OUT-FILE.                                               
                                                                                
      ******************************************************************        
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *        
      *                 ERROR OR WARNING CODE OCCURS.                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE        TO  SQLCODE2.                                    
           MOVE SQLERRD(3)     TO  SQLERRD3.                                    
                                                                                
           DISPLAY '***************************'                                
           DISPLAY '**       ABEND           **'                                
           DISPLAY '**  IN PROGRAM POKUT222  **'                                
           DISPLAY '***************************'                                
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME                  
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED         
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLCODE **        = ' SQLCODE2.                          
                                                                                
           MOVE +4013         TO PV-ABEND-CODE                                  
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
