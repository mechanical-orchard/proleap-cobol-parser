       ID  DIVISION.                                                            
       PROGRAM-ID.         INKUP013.                                            
       AUTHOR.             B. GRAHAM.                                           
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       02/15/2011.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * REMARKS                                                                 
      * -------                                                                 
      *                                                                         
      * TITLE        -  CUTOVER FROM ML TO IN FOR BV/IN LEDGER                  
      *              -  DB2 SELECTION LOGIC                                     
      *                                                                         
      ******************************************************************        
      *                                                                         
      *               !!!  I M P O R T A N T  !!!                               
      *                                                                         
      *    !!!  THIS PROGRAM WAS COPIED FROM INKUP163  INKUT163  !!!            
      *                                                                         
      ******************************************************************        
      *                                                                         
      * DESCRIPTION  -  THE PURPOSE OF THIS PROGRAM IS TO DO THE                
      * SELECTION OF DATA FOR THE FINAL CUTOVER FROM THE MERCHANDISE            
      * LEDGER TO THE INT_SUB_CL_INV_LDGR TABLE FOR THOSE STORES                
      * THAT PLANNED TO TAKE INVENTORY DURING THE CLOSING WEEK AND              
      * ANY OTHER STORES IN THE SAME CYCLE.                                     
      *                                                                         
      *  STORES WILL BE IDENTIFIED USING THE TINCNTL AND TINVPAR                
      *  TABLES.                                                                
      *                                                                         
      *  RECORDS WILL BE SELECTED FROM THE MLT_MNLY_SUM TABLE USING             
      *  THE MONTH END DATE FROM TMLCNTL. THESE WILL BE INSERTED TO             
      *  INT_SUB_CL_INV_LDGR TABLE IN PGM INKUP014.                             
      *                                                                         
      *  CONTROL CARD RECORDS WILL BE WRITTEN TO A SEPARATE FILE                
      *  FOR USE IN UNLOADING DATA LATER ON IN INK525.                          
      *                                                                         
      *  THE ACTL_LDG_CTOF_TMST IN TINVPAR WILL BE UPDATED FOR ALL              
      *  STORES BEING SELECTED.                                                 
      *                                                                         
      * PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
      * AND ITS VALUES                                                          
      *                                                                         
      ******************** HISTORY OF CHANGES *************************         
      * WR NUMBER     PROGRAMMER       DATE                                     
      * ---------------------------------------------------------------         
      * WR34557       BRUCE GRAHAM     09/13/2011  ORIGINAL INSTALLATION        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT ALTERNATE-DATE-CNTL-FILE                                      
               ASSIGN                TO INP01.                                  
                                                                                
           SELECT UNLOAD-CNTL-FILE                                              
               ASSIGN                TO OUT01.                                  
                                                                                
           SELECT INV-LDGR-FILE                                                 
               ASSIGN                TO OUT02.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  ALTERNATE-DATE-CNTL-FILE                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  ALTERNATE-DATE-CNTL-REC       PIC X(80).                             
                                                                                
       FD  UNLOAD-CNTL-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  UNLOAD-CNTL-REC               PIC X(80).                             
                                                                                
       FD  INV-LDGR-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  INV-LDGR-REC                  PIC X(131).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-SUB-CL-INV-LDGR-REC-CNT                                       
                                       PIC  9(09) VALUE ZERO.                   
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TINVPAR-SW         PIC  X     VALUE 'N'.               
               88  PS-END-OF-TINVPAR                   VALUE 'Y'.               
               88  PS-END-OF-TINVPAR-NO                VALUE 'N'.               
           05  PS-END-OF-TMLT-MNLY-SUM-SW   PIC  X     VALUE 'N'.               
               88  PS-END-OF-TMLT-MNLY-SUM             VALUE 'Y'.               
           05  PS-END-OF-INCNTL-CSR-SW      PIC  X     VALUE 'N'.               
               88  PS-END-OF-INCNTL-CSR                VALUE 'Y'.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.                     
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'INK520'.            
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUP013'.          
           05  PC-MAX-STORE-COUNT      PIC S9(05) COMP-3 VALUE +9999.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-EDIT-SUB-CLASS       PIC 9(03)  VALUE ZEROS.                  
           05  PV-EDIT-SUB-CLASS-RED REDEFINES                                  
               PV-EDIT-SUB-CLASS.                                               
               10  PV-EDIT-CLASS-P1    PIC X(01).                               
               10  PV-EDIT-CLASS-P2    PIC X(02).                               
           05  PV-EDIT-DEPT-NBR        PIC 9(03)  VALUE ZEROS.                  
           05  PV-EDIT-DEPT-NBR-RED REDEFINES                                   
               PV-EDIT-DEPT-NBR        PIC X(03).                               
           05  PV-EDIT-CUTOFF-TIMESTAMP PIC X(26) VALUE SPACES.                 
           05  PV-RETRY-COUNT          PIC S9(04) COMP SYNC                     
                                                  VALUE ZERO.                   
           05  PV-PROGRAM-NAME         PIC  X(08) VALUE 'INKUP013'.             
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACE.                  
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30) VALUE SPACE.                  
           05  PV-SQL-SUB              PIC S9(04) VALUE ZERO  COMP.             
           05  PV-SLS-RTL-AMT          PIC S9(11)V9(2) COMP-3.                  
           05  PV-STORE-NBR            PIC X(05)  VALUE ZERO.                   
           05  PV-STORE-NBR-N REDEFINES PV-STORE-NBR                            
                                       PIC 9(05).                               
           05  PV-STORE-SUB            PIC S9(05) VALUE ZERO  COMP-3.           
           05  PV-SS-SUB               PIC S9(05) VALUE ZERO  COMP-3.           
           05  PV-TINCNTL-FETCH        PIC  9(09) VALUE ZEROES.                 
      ***THESE RECORDS ARE USED IN A LATER STEP TO                              
      *** UNLOAD THE INVENTORY LEDGER.                                          
      **** SAMPLE:                                                              
      ****     WHEN (FSCL_MN_END_DTE  = '2011-02-26'                            
      ****       AND FSCL_MN_NBR      = '01'                                    
                                                                                
       01  PV-UNLOAD-CNTL-RECORDS.                                              
           05  PV-UNLOAD-CNTL-REC-1.                                            
               10  FILLER              PIC  X(10) VALUE SPACES.                 
               10  FILLER              PIC  X(25) VALUE                         
                   'WHEN (FSCL_MN_END_DTE  = '.                                 
               10  FILLER              PIC  X(01) VALUE "'".                    
               10  PV-UNLOAD-DATE      PIC  X(10).                              
               10  FILLER              PIC  X(01) VALUE "'".                    
               10  FILLER              PIC  X(33) VALUE SPACES.                 
                                                                                
           05  PV-UNLOAD-CNTL-REC-2.                                            
               10  FILLER              PIC  X(12) VALUE SPACES.                 
               10  FILLER              PIC  X(23) VALUE                         
                   'AND FSCL_MN_NBR      = '.                                   
               10  FILLER              PIC  X(01) VALUE "'".                    
               10  PV-UNLOAD-MN-NBR    PIC  X(02).                              
               10  FILLER              PIC  X(01) VALUE "'".                    
               10  FILLER              PIC  X(41) VALUE SPACES.                 
                                                                                
       01  ERROR-CODE                       PIC S9(04)  VALUE +0                
                                                         COMP SYNC.             
                                                                                
       01  ABEND-CODE                  PIC S9(04) VALUE 0     COMP SYNC.        
           88  ABEND-4013-DB2-ERROR                 VALUE +4013.                
           88  ABEND-4014-INTERNL-TBL-ERROR         VALUE +4014.                
           88  ABEND-4095-MISC-ERROR                VALUE +4095.                
                                                                                
      ******************************************************************        
      *  STORE TABLE IS USED TO HOLD THE STORE NUMBERS OF THE STORES TO         
      *  BE INVENTORIED AND THEIR ASSOCIATED INVENTORY IDS.                     
      ******************************************************************        
       01  WS-STORE-TABLE.                                                      
           05  WS-STORE-TAB OCCURS 9999 TIMES INDEXED BY STR-IDX.               
               10  WS-INV-STORE            PIC S9(04) COMP.                     
               10  WS-INV-ID               PIC S9(09) COMP.                     
                                                                                
      *****************************************************************         
      *  DB2 DATE ATTRIBUTES                                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  M/L OPEN FISCAL MONTH CONTROL TABLE                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  M/L DETAIL LEDGER PARTITION BALANCE TBL (MLT_PARTN_BAL)                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  M/L DETAIL LEDGER PARTITION CONTROL TBL (MLT_PARTN_CNTL)               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  M/L MONTHLY DETAIL LEDGER SUMMARY TABLE (MLT_MNLY_SUM)                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLT00004                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY MONTHLY SUBCLASS TABLE                                       
      *  INT_SUB_CL_INV_LDGR                                                    
      *  THIS DECLARATION IS INCLUDED FOR THE DATA LAYOUT SO THAT THE           
      *  DESCRIPTION CAN BE USED TO CREATE THE OUTPUT FLAT FILE.                
      *  THE TABLE IS NOT ACCESSED IN THIS PROGRAM.                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE INT00007                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY PARAMETERS TABLE                                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY CYCLE CONTROL TABLE                                          
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
      *****************************************************************         
      *  COPYBOOK OF INPUT ALTERNATE DATE CONTROL CARD RECORD LAYOUT  *         
      *****************************************************************         
      *                                                                         
           COPY INRD001.                                                        
                                                                                
      *****************************************************************         
      *  COPYBOOK OF FIELDS USED IN THE EDIT / VALIDATE PROCESS.      *         
      *****************************************************************         
                                                                                
           COPY INWS050.                                                        
                                                                                
      ******************************************************************        
      *  CURSOR DECLARATION FOR INCNTL-CSR CURSOR                      *        
      ******************************************************************        
      *    THIS CURSOR WILL IDENTIFY ANY CYCLE ID IF THE PLANNED       *        
      *    INVENTORY DATE OF ANY OF THE STORES ARE WITHIN THE WEEK EVEN*        
      *    IF NOT ALL OF THE STORES ARE WITHIN THE WEEK. BECAUSE THIS  *        
      *    PROGRAM ONLY RUNS AT THE END OF CLOSING WEEK, THE IN001     *        
      *    DATA POINTS TO THE FIRST WEEK OF THE ACCOUNTING MONTH WHICH *        
      *    IS ALSO THE CLOSING WEEK OF THE PRIOR MONTH.                *        
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE INCNTL-CSR CURSOR FOR                                    
                SELECT DISTINCT B.INV_ID                                        
                  FROM TINVPAR A                                                
                      ,TINCNTL B                                                
                 WHERE A.INV_ID              = B.INV_ID                         
                   AND B.ACTV_IND            = 'Y'                              
                   AND B.OPN_FSCL_MN_NBR     = :INCNTL-OPN-FSCL-MN-NBR          
                   AND B.OPN_FSCL_MN_DTE     = :INCNTL-OPN-FSCL-MN-DTE          
                   AND (A.PLND_INV_DTE      >= :INVPAR-PLND-INV-DTE             
                   AND A.PLND_INV_DTE       <= :IN001-OPN-FSCL-WK-DTE)          
                   AND A.ACTL_FIN_BK_DTE     = '9999-09-09'                     
                   AND A.LOC_INV_STAT_CDE    = 'IN'                             
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *-----------------------------------------------------------------        
      * DB2 INVENTORY PARAMETERS CURSOR DECLARATION                             
      *                                                                         
      * SELECT EACH STORE WHICH WILL BE INVENTORIED.                            
      * ACTL_FIN_BK_DTE = '9999-09-09'                                          
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
               DECLARE TINVPAR-CSR CURSOR FOR                                   
                   SELECT   LOC_NBR,                                            
                            INV_ID,                                             
                            ACTL_LDG_CTOF_TMST                                  
                   FROM     TINVPAR                                             
                   WHERE    INV_ID            = :INCNTL-INV-ID                  
                   AND      SCHD_PHY_CNT_CDE <> 'EI'                            
                   AND      ACTL_FIN_BK_DTE   = '9999-09-09'                    
                   ORDER BY LOC_NBR                                             
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      * DB2 M/L MONTHLY DETAIL LEDGER SUMMARY CURSOR DECLARATION                
      *                                                                         
      * SELECT EACH LEDGER ROW WHICH HAS A NON-ZERO AMOUNT FOR THE              
      * OPEN FISCAL MONTH                                                       
      *                                                                         
      * LOC_NBR > 0 IN THE WHERE CLAUSE BELOW IS FOR PERFORMANCE                
      * PURPOSES. IT CAUSES DB2 TO USE AN INDEX ASSOCIATED WITH THE             
      * TABLE INSTEAD OF DOING A FULL TABLE SCAN                                
      *                                                                         
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
             DECLARE MLT-MNLY-SUM-CSR CURSOR FOR                                
               SELECT FSCL_MN_END_DTE,                                          
                      DEPT_NBR,                                                 
                      SUB_CL_NBR,                                               
                      LOC_NBR,                                                  
                      ML_LO_MDSE_LVL_ID,                                        
                      SLS_REG_RTL_AMT,                                          
                      SLS_PROMO_RTL_AMT,                                        
                      SLS_PROCLR_RTL_AMT,                                       
                      SLS_CLR_RTL_AMT,                                          
                      SLS_OTH_RTL_AMT,                                          
                      SHNK_RTL_AMT,                                             
                      END_INV_RTL_AMT                                           
               FROM MLT_MNLY_SUM                                                
               WHERE PARTN_NBR IN                                               
                     (SELECT DISTINCT PARTN_NBR                                 
                      FROM MLT_PARTN_CNTL                                       
                      WHERE TBL_TM_LVL_CDE = 'M'                                
                        AND FSCL_DTE = :MLCNTL-OPN-FSCL-MN-DTE)                 
                 AND LOC_NBR > 0                                                
                 AND ((SLS_REG_RTL_AMT                                          
                      + SLS_PROMO_RTL_AMT                                       
                      + SLS_PROCLR_RTL_AMT                                      
                      + SLS_CLR_RTL_AMT                                         
                      + SLS_OTH_RTL_AMT) <> 0                                   
                  OR SHNK_RTL_AMT    <> 0                                       
                  OR END_INV_RTL_AMT <> 0)                                      
               ORDER BY FSCL_MN_END_DTE,                                        
                        DEPT_NBR,                                               
                        SUB_CL_NBR,                                             
                        LOC_NBR,                                                
                        ML_LO_MDSE_LVL_ID                                       
           END-EXEC.                                                            
                                                                                
           EJECT                                                                
      *-----------------------------------------------------------------        
      * DB2 SQL COMMUNICATIONS AREA                                             
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************INKUP013 STARTING*************'.                
           DISPLAY '******************************************'.                
                                                                                
           PERFORM 0100-GET-OPN-FSCL-MN.                                        
                                                                                
           PERFORM 0200-INIT.                                                   
                                                                                
           IF  IN001-IN-CLOSE-WK-YES                                            
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY 'NOT IN CLOSE WEEK, THIS JOB SHOULD NOT BE RUN'          
               SET ABEND-4095-MISC-ERROR    TO TRUE                             
               MOVE '0000-MAINLINE'         TO PV-PARAGRAPH-NAME                
               PERFORM 9200-ABEND                                               
           END-IF.                                                              
                                                                                
           DISPLAY 'MLCNTL OPEN FISCAL MONTH: '                                 
               MLCNTL-OPN-FSCL-MN-DTE                                           
           DISPLAY 'MLCNTL OPEN FISCAL MONTH NBR:  '                            
               MLCNTL-OPN-FSCL-MN-NBR.                                          
                                                                                
           DISPLAY 'IN001 OPEN FISCAL MONTH: '                                  
               IN001-OPN-FSCL-MN-DTE                                            
           DISPLAY 'IN001 OPEN FISCAL MONTH NBR:  '                             
               IN001-OPN-FSCL-MN-NBR.                                           
                                                                                
           STRING MLCNTL-OPN-FSCL-WK-DTE DELIMITED BY SIZE                      
                  '-23.59.59.999999' DELIMITED BY SIZE                          
                               INTO PV-EDIT-CUTOFF-TIMESTAMP.                   
                                                                                
           DISPLAY '    CUTOFF TIMESTAMP     :  '                               
               PV-EDIT-CUTOFF-TIMESTAMP.                                        
                                                                                
                                                                                
           PERFORM 0300-OPEN-INCNTL-CSR.                                        
                                                                                
           PERFORM 0400-FETCH-INCNTL-CSR                                        
             UNTIL PS-END-OF-INCNTL-CSR.                                        
                                                                                
           PERFORM 0500-CLOSE-INCNTL-CSR.                                       
                                                                                
           IF PV-SS-SUB > ZEROS                                                 
               PERFORM 8300-OPEN-MLT-MNLY-SUM-CSR                               
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       PERFORM 0600-PROCESS-TMLT-MNLY-SUM                       
                          UNTIL PS-END-OF-TMLT-MNLY-SUM                         
                       PERFORM 8500-CLOSE-MNLY-SUM-CURSOR                       
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
               END-EVALUATE                                                     
                                                                                
      * FOR EACH STORE IN WS-STORE-TABLE, UPDATE TINVPAR TABLE WITH    *        
      * ACTUAL CUTOFF TIMESTAMP                                        *        
                                                                                
               PERFORM 8150-UPDATE-TINVPAR                                      
                  VARYING PV-STORE-SUB FROM 1 BY 1                              
                  UNTIL PV-STORE-SUB > PV-SS-SUB                                
                                                                                
           END-IF.                                                              
                                                                                
           DISPLAY 'PA-SUB-CL-INV-LDGR-REC WRITTEN '                            
                   PA-SUB-CL-INV-LDGR-REC-CNT.                                  
                                                                                
           CLOSE ALTERNATE-DATE-CNTL-FILE                                       
                 UNLOAD-CNTL-FILE                                               
                 INV-LDGR-FILE.                                                 
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************INKUP013 ENDING***************'.                
           DISPLAY '******************************************'.                
                                                                                
           MOVE ERROR-CODE TO RETURN-CODE.                                      
           STOP RUN.                                                            
                                                                                
                                                                                
       0100-GET-OPN-FSCL-MN.                                                    
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                        OPN_FSCL_WK_DTE                                         
                       ,OPN_FSCL_WK_NBR                                         
                       ,OPN_FSCL_MN_DTE                                         
                       ,OPN_FSCL_MN_NBR                                         
                    INTO                                                        
                       :MLCNTL-OPN-FSCL-WK-DTE                                  
                      ,:MLCNTL-OPN-FSCL-WK-NBR                                  
                      ,:MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                    FROM                                                        
                        TMLCNTL                                                 
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                                                                                
                   WHEN SQLCODE        = -911                                   
                   WHEN SQLCODE        = -913                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '0100-GET-OPN-FSCL-MN'                           
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE    '0100-GET-OPN-FSCL-MN'                                   
                                                TO PV-PARAGRAPH-NAME            
               MOVE    'SELECT ROW FROM TMLCNTL'                                
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       0200-INIT.                                                               
      ******************************************************************        
      * DESCRIPTION     : INITIALISE FIELDS AND OPEN FILES USED        *        
      ******************************************************************        
                                                                                
           OPEN INPUT  ALTERNATE-DATE-CNTL-FILE                                 
                OUTPUT UNLOAD-CNTL-FILE                                         
                       INV-LDGR-FILE.                                           
                                                                                
           INITIALIZE IN001-RECORD.                                             
                                                                                
           MOVE 'INKUP013'                  TO PV-IN050-PGM-NUMBER              
           PERFORM 1100-EDIT-VALIDATE-CNTL.                                     
                                                                                
           MOVE IN001-OPN-FSCL-MN-NBR       TO PV-UNLOAD-MN-NBR.                
           MOVE IN001-OPN-FSCL-MN-DTE       TO PV-UNLOAD-DATE.                  
                                                                                
           DISPLAY 'INVENTORY LEDGER UNLOAD CNTL REC 1 '                        
                          PV-UNLOAD-CNTL-REC-1.                                 
                                                                                
           DISPLAY 'INVENTORY LEDGER UNLOAD CNTL REC 2 '                        
                          PV-UNLOAD-CNTL-REC-2.                                 
                                                                                
           WRITE UNLOAD-CNTL-REC          FROM PV-UNLOAD-CNTL-REC-1.            
           WRITE UNLOAD-CNTL-REC          FROM PV-UNLOAD-CNTL-REC-2.            
                                                                                
                                                                                
       0300-OPEN-INCNTL-CSR.                                                    
      *****************************************************************         
      * DESCRIPTION     : OPEN THE INCNTL-CSR                         *         
      *****************************************************************         
                                                                                
           MOVE IN001-OPN-FSCL-MN-NBR       TO INCNTL-OPN-FSCL-MN-NBR           
           MOVE IN001-OPN-FSCL-MN-DTE       TO INCNTL-OPN-FSCL-MN-DTE           
           MOVE IN001-OPN-FSCL-WK-BEG-DTE   TO INVPAR-PLND-INV-DTE              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN INCNTL-CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD OPEN FOR INCNTL-CSR'                        
                       MOVE '0300-OPEN-INCNTL-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                       MOVE 'OPEN INCNTL-CSR      '                             
                                            TO PV-DB2-OPER-ATTEMPTED            
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR OPEN **'          
               MOVE '0300-OPEN-INCNTL-CSR'  TO PV-PARAGRAPH-NAME                
               MOVE 'OPEN INCNTL-CSR  '     TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       0400-FETCH-INCNTL-CSR.                                                   
      ******************************************************************        
      * DESCRIPTION     : FETCH INCNTL CURSOR RECORDS                  *        
      ******************************************************************        
      *    THIS CURSOR WILL IDENTIFY ANY CYCLE ID IF THE PLANNED       *        
      *    INVENTORY DATE OF ANY OF THE STORES ARE WITHIN THE WEEK     *        
      *    EVEN IF NOT ALL OF THE STORES ARE WITHIN THE WEEK. BECAUSE  *        
      *    THIS PROGRAM ONLY RUNS AT THE END OF CLOSING WEEK,          *        
      *    THE IN001 DATA POINTS TO THE FIRST WEEK OF THE ACCOUNTING   *        
      *    MONTH WHICH IS ALSO THE CLOSING WEEK OF THE PRIOR MONTH.    *        
      ******************************************************************        
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   FETCH  INCNTL-CSR                                            
                    INTO :INCNTL-INV-ID                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       ADD 1 TO PV-TINCNTL-FETCH                                
                       PERFORM 0420-PROCESS-INCNTL-CSR                          
                   WHEN SQLCODE = 100                                           
                       SET PS-END-OF-INCNTL-CSR                                 
                                            TO TRUE                             
                       IF PV-TINCNTL-FETCH = ZERO                               
                           DISPLAY 'NOTHING TO CUTOVER'                         
                       END-IF                                                   
                   WHEN SQLWARN0 NOT = SPACE                                    
                   WHEN SQLCODE  NOT = ZERO                                     
                     DISPLAY 'BAD RETURN FROM INCNTL-CSR FETCH'                 
                     DISPLAY SPACES                                             
                     DISPLAY 'TINCNTL.ACTV_IND =  Y'                            
                     DISPLAY 'TINCNTL.OPN_FSCL_MN_NBR =  '                      
                              INCNTL-OPN-FSCL-MN-NBR                            
                     DISPLAY 'TINCNTL.OPN_FSCL_MN_END_DTE = '                   
                              INCNTL-OPN-FSCL-MN-DTE                            
                     DISPLAY 'TINCNTL.PLND_INV_DTE >= '                         
                              INVPAR-PLND-INV-DTE                               
                     DISPLAY 'TINCNTL.PLND_INV_DTE <= '                         
                              IN001-OPN-FSCL-WK-DTE                             
                     DISPLAY 'ACTL_FIN_BK_DTE = 9999-09-09'                     
                     DISPLAY 'LOC_INV_STAT_CDE = IN'                            
                     DISPLAY SPACES                                             
                     MOVE 'FETCH ON INCNTL-CSR  '                               
                                            TO PV-DB2-OPER-ATTEMPTED            
                     MOVE '0400-FETCH-INCNTL-CSR'                               
                                            TO PV-PARAGRAPH-NAME                
                     DISPLAY SPACES                                             
                     PERFORM 9100-SQL-ERROR                                     
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR FETCH **'         
               MOVE '0400-FETCH-INCNTL-CSR'    TO PV-PARAGRAPH-NAME             
               MOVE 'FETCH INCNTL-CSR  '       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 0420-PROCESS-INCNTL-CSR.                                       *        
      * FOR EACH CYCLE ID IDENTIFIED BY INCNTL-CSR, GET ALL THE STORES *        
      * UNDERGOING PHYSICAL INVENTORY AND LOAD TO WS-STORE-TABLE       *        
      * INTERNAL TABLE. FOR EACH STORE IN WS-STORE-TABLE, UPDATE       *        
      * TINVPAR TABLE WITH ACTUAL CUTOFF TIMESTAMP                     *        
      ******************************************************************        
       0420-PROCESS-INCNTL-CSR.                                                 
                                                                                
           MOVE 'N'                    TO PS-END-OF-TINVPAR-SW                  
           PERFORM 8000-OPEN-TINVPAR-CSR.                                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   PERFORM 0440-PROCESS-TINVPAR-CSR                             
                                                                                
                   PERFORM 8200-CLOSE-TINVPAR-CURSOR                            
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 0440-PROCESS-TINVPAR-CSR                                                
      * LOAD WS-STORE-TABLE INTERNAL TABLE WITH ALL THE STORES                  
      * UNDERGOING PHYSICAL INVENTORY.                                          
      ******************************************************************        
       0440-PROCESS-TINVPAR-CSR.                                                
                                                                                
           PERFORM 0450-LOAD-WS-STORE-TABLE                                     
              UNTIL PS-END-OF-TINVPAR.                                          
                                                                                
       0450-LOAD-WS-STORE-TABLE.                                                
      ******************************************************************        
      *  LOAD THE WORKING STORAGE STORE TABLE WITH THE STORES DOING             
      *  INVENTORY                                                              
      ******************************************************************        
                                                                                
           PERFORM 8100-FETCH-TINVPAR-CSR.                                      
                                                                                
           IF  PS-END-OF-TINVPAR                                                
               CONTINUE                                                         
           ELSE                                                                 
               ADD +1                TO PV-SS-SUB                               
               IF PV-SS-SUB > PC-MAX-STORE-COUNT                                
                   DISPLAY 'WS-STORE-TABLE ENTRIES EXCEEDED'                    
                   MOVE '0450-LOAD-WS-STORE-TABLE '                             
                                           TO PV-PARAGRAPH-NAME                 
                   MOVE +4014 TO ABEND-CODE                                     
                   PERFORM 9200-ABEND                                           
               ELSE                                                             
                   MOVE INVPAR-LOC-NBR   TO WS-INV-STORE(PV-SS-SUB)             
                   MOVE INVPAR-INV-ID    TO WS-INV-ID(PV-SS-SUB)                
                   MOVE INVPAR-LOC-NBR   TO PV-STORE-NBR                        
                   DISPLAY 'INVENTORING STR: ' PV-STORE-NBR                     
           END-IF.                                                              
                                                                                
       0500-CLOSE-INCNTL-CSR.                                                   
      *****************************************************************         
      * DESCRIPTION     : CLOSES THE INCNTL-CSR                       *         
      *****************************************************************         
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE INCNTL-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD CLOSE FOR INCNTL-CSR'                       
                       MOVE '0500-CLOSE-INCNTL-CSR'                             
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE INCNTL-CSR      '                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR CLOSE **'         
               MOVE '0500-CLOSE-INCNTL-CSR'    TO PV-PARAGRAPH-NAME             
               MOVE 'CLOSE INCNTL-CSR  '       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      *-------------------------------------------------------------------      
      *  BASED UPON THE SELECTION CRITERIA FROM THE MNLY-SUM TABLE,             
      *  WRITE A RECORD TO THE INV-LDGR-REC-FILE THAT MATCHES ANY               
      *  ANY STORES WHOSE INVENTORY CYCLE HAS STORES TAKING                     
      *  TAKING INVENTORY DURING CLOSE WEEK.                                    
      *-------------------------------------------------------------------      
       0600-PROCESS-TMLT-MNLY-SUM.                                              
                                                                                
           PERFORM 8400-FETCH-MLT-MNLY-SUM-CSR.                                 
      *                                                                         
           IF  PS-END-OF-TMLT-MNLY-SUM                                          
               CONTINUE                                                         
           ELSE                                                                 
      *-------------------------------------------------------------------      
      *        ROWS ARE MOVED FOR ONLY STORES DOING INVENTORY                   
      *-------------------------------------------------------------------      
               SET STR-IDX TO 1                                                 
               SEARCH WS-STORE-TAB                                              
                  VARYING STR-IDX                                               
                  AT END                                                        
                      CONTINUE                                                  
                  WHEN WS-INV-STORE(STR-IDX) = MLT00004-LOC-NBR                 
                      PERFORM 0610-LOAD-SUB-CL-INV-LDGR                         
                  WHEN WS-INV-STORE(STR-IDX) = ZERO                             
                      CONTINUE                                                  
               END-SEARCH                                                       
           END-IF.                                                              
                                                                                
       0610-LOAD-SUB-CL-INV-LDGR.                                               
                                                                                
           MOVE IN001-OPN-FSCL-MN-DTE  TO INT00007-FSCL-MN-END-DTE              
                                          INT00007-ML-PSTG-MN-END-DTE.          
           MOVE IN001-OPN-FSCL-MN-NBR  TO INT00007-FSCL-MN-NBR.                 
                                                                                
           MOVE MLT00004-DEPT-NBR     TO PV-EDIT-DEPT-NBR.                      
           MOVE PV-EDIT-DEPT-NBR-RED  TO INT00007-DEPT-NBR.                     
                                                                                
           MOVE MLT00004-SUB-CL-NBR   TO PV-EDIT-SUB-CLASS.                     
           MOVE PV-EDIT-CLASS-P2      TO INT00007-SUB-CL-NBR.                   
                                                                                
           MOVE MLT00004-ML-LO-MDSE-LVL-ID                                      
                                      TO INT00007-IN-LO-MDSE-LVL-ID.            
           MOVE MLT00004-LOC-NBR      TO INT00007-STR-NBR.                      
      *    COMPUTE PV-SLS-RTL-AMT =  MLT00004-SLS-REG-RTL-AMT                   
      *                           +  MLT00004-SLS-PROMO-RTL-AMT                 
      *                           +  MLT00004-SLS-PROCLR-RTL-AMT                
      *                           +  MLT00004-SLS-CLR-RTL-AMT                   
      *                           +  MLT00004-SLS-OTH-RTL-AMT.                  
      *    MOVE PV-SLS-RTL-AMT        TO INT00007-CTOFF-SLS-AMT.                
      *    MOVE MLT00004-SHNK-RTL-AMT TO INT00007-CTOFF-SHNK-AMT.               
           MOVE ZEROS                 TO INT00007-CTOFF-SLS-AMT.                
           MOVE ZEROS                 TO INT00007-CTOFF-SHNK-AMT.               
           MOVE MLT00004-END-INV-RTL-AMT TO INT00007-CTOFF-END-INV-AMT. .       
           MOVE ZEROS                 TO INT00007-SLS-RTL-AMT                   
                                         INT00007-MKDN-RTL-AMT                  
                                         INT00007-MKUP-RTL-AMT                  
                                         INT00007-XFER-IN-RTL-AMT               
                                         INT00007-XFER-OUT-RTL-AMT              
                                         INT00007-RCPT-RTL-AMT                  
                                         INT00007-PADJ-RTL-AMT                  
                                         INT00007-SHNK-RTL-AMT                  
OCFSFS                                   INT00007-STR-PARL-SHIP-RTL-AMT         
                                         INT00007-STR-EXPTED-RTL-AMT            
                                         INT00007-END-INV-RTL-AMT.              
                                                                                
           WRITE INV-LDGR-REC FROM DCLINT00007.                                 
                                                                                
           ADD 1 TO PA-SUB-CL-INV-LDGR-REC-CNT.                                 
           EJECT                                                                
      *                                                                         
       8000-OPEN-TINVPAR-CSR.                                                   
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN TINVPAR-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8000-OPEN-TINVPAR-CSR'                             
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON INVENTORY PARAMETERS CURSOR'               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8000-OPEN-TINVPAR-CSR' TO PV-PARAGRAPH-NAME                
               MOVE 'OPEN RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       8100-FETCH-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                FETCH TINVPAR-CSR                                               
                INTO  :INVPAR-LOC-NBR,                                          
                      :INVPAR-INV-ID,                                           
                      :INVPAR-ACTL-LDG-CTOF-TMST                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-TINVPAR TO TRUE                                
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '8100-FETCH-TINVPAR-CSR'                                
                                 TO  PV-PARAGRAPH-NAME                          
                   MOVE 'FETCH INVENTORY PARAMETERS CURSOR'                     
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9100-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8150-UPDATE-TINVPAR.                                                     
                                                                                
           MOVE PV-EDIT-CUTOFF-TIMESTAMP TO INVPAR-ACTL-LDG-CTOF-TMST.          
           MOVE WS-INV-STORE(PV-STORE-SUB) TO INVPAR-LOC-NBR.                   
           MOVE WS-INV-ID(PV-STORE-SUB) TO INVPAR-INV-ID.                       
                                                                                
           EXEC SQL                                                             
               UPDATE TINVPAR                                                   
               SET ACTL_LDG_CTOF_TMST  = :INVPAR-ACTL-LDG-CTOF-TMST             
               WHERE INV_ID            = :INVPAR-INV-ID                         
                 AND LOC_NBR           = :INVPAR-LOC-NBR                        
           END-EXEC.                                                            
                                                                                
       8200-CLOSE-TINVPAR-CURSOR.                                               
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE TINVPAR-CSR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8200-CLOSE-TINVPAR-CSR'                            
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON INVENTORY PARAMETERS CURSOR'              
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8200-CLOSE-TINVPAR-CSR' TO PV-PARAGRAPH-NAME               
               MOVE 'CLOSE RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED           
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       8300-OPEN-MLT-MNLY-SUM-CSR.                                              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN MLT-MNLY-SUM-CSR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8000-OPEN-MLT-MNLY-SUM-CSR'                        
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON M/L MTH SUB CLASS CURSOR'                  
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8300-OPEN-MLT-MNLY-SUM-CSR' TO PV-PARAGRAPH-NAME           
               MOVE 'OPEN RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       8400-FETCH-MLT-MNLY-SUM-CSR.                                             
                                                                                
           EXEC SQL                                                             
                FETCH MLT-MNLY-SUM-CSR                                          
                INTO  :MLT00004-FSCL-MN-END-DTE,                                
                      :MLT00004-DEPT-NBR,                                       
                      :MLT00004-SUB-CL-NBR,                                     
                      :MLT00004-LOC-NBR,                                        
                      :MLT00004-ML-LO-MDSE-LVL-ID,                              
                      :MLT00004-SLS-REG-RTL-AMT,                                
                      :MLT00004-SLS-PROMO-RTL-AMT,                              
                      :MLT00004-SLS-PROCLR-RTL-AMT,                             
                      :MLT00004-SLS-CLR-RTL-AMT,                                
                      :MLT00004-SLS-OTH-RTL-AMT,                                
                      :MLT00004-SHNK-RTL-AMT,                                   
                      :MLT00004-END-INV-RTL-AMT                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-TMLT-MNLY-SUM TO TRUE                          
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '8400-FETCH-MLT-MNLY-SUM-CSR'                           
                                 TO  PV-PARAGRAPH-NAME                          
                   MOVE 'FETCH M/L MTH SUBCLASS CURSOR'                         
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9100-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       8500-CLOSE-MNLY-SUM-CURSOR.                                              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE MLT-MNLY-SUM-CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8500-CLOSE-MLT-MNLY-SUM-CSR'                       
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON M/L MTH SUBCLASS CURSOR'                  
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8500-CLOSE-MLT-MNLY-SUM-CSR' TO PV-PARAGRAPH-NAME          
               MOVE 'CLOSE RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED           
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      *--------------------------------------------------------------*          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      *--------------------------------------------------------------*          
                                                                                
       9100-SQL-ERROR.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUP013'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *****************************************************************         
      * 9200-ABEND.                                                   *         
      *  - PERFORM AN ABEND                                                     
      *****************************************************************         
       9200-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = '  PC-PROGRAM-NAME.                 
           DISPLAY '** PARAGRAPH       ** = '  PV-PARAGRAPH-NAME.               
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
           COPY INPD050.                                                        
      *****************************************************************         
