      *-----------------------*                                         12/20/93
00002  IDENTIFICATION DIVISION.                                         INKCS001
00003 *-----------------------*                                            LV074
00004  PROGRAM-ID.    INKCS001.                                            CL*17
00005  AUTHOR.        M. FURLICK - STRATAGEM                               CL*17
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL*17
00007  DATE-WRITTEN.  07-28-93.                                            CL*17
00008  DATE-COMPILED.                                                      CL*17
00009                                                                      CL*17
00010 *----------------------------------------------------------------*   CL*17
00011 *    I001 - PHYSICAL INVENTORY INVALID DATE SELECTION CRITERION  *   CL*17
00012 *                                                                *   CL*17
00013 *    THIS PROGRAM IS THE ENTRY POINT TO THE PI INVALID DATA SUB- *   CL*17
00014 *    SYSTEM.  A CORPORATE USER IS PROMPTED FOR THE STORE NUMBER; *   CL*17
00015 *    A STORE USER HAS THIS FILLED IN FOR THEM.  EITHER THE AREA  *   CL*17
00016 *    NUMBER OR ASTERISKS IS ENTERED TO DETERMINE THE LIST        *   CL*17
00017 *    CRITERION.                                                  *   CL*17
00018 *                                                                *   CL*17
      *CHANGE HISTORY:                                                          
      * WR/PRJ DATE       PGMR         DESCRIPTION                              
      * ------ ---------- ------------ -----------------------------            
W26602* W26600 06-29-2004 ROD JANSSEN  INV STORE EXPANSION.                     
W28545* W28545 03-29-2006 JAN BLAZICH  INV STORE EXPANSION CORRECTION           
J28545* W28545 06-16-2006 JAN BLAZICH  TINVPAR/TINCNTL ARE USED TO GET          
J28545*                                THE PROPER INV-ID AND STORE              
      * W33304 05-01-2008 JILL LIN     HOLDING INVENTORY LEDGER OPEN            
      *                                ACROSS PERIODS INVENTORY SHORTAGE        
      *                                REPORTING                                
00019 *----------------------------------------------------------------*   CL*17
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
1222RM*----------------------------------------------------------------*   CL*17
1022RM* CHANGED 12/01/2022    BY BOB MCASEY      WR: CHG0277188                 
1222RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1222RM* THE CICS ARCHITECTURE API.                                              
263126*----------------------------------------------------------------*        
00020                                                                      CL*17
00022  ENVIRONMENT DIVISION.                                               CL*17
00026  DATA DIVISION.                                                      CL*17
00028                                                                      CL*17
00029  WORKING-STORAGE SECTION.                                            CL*17
00030                                                                      CL*17
00031  01  PC-PROGRAM-CONSTANTS.                                           CL*17
00032      05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'INKCS001'.     CL*17
00033      05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'IN001A  '.     CL*17
00034      05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'INKM001 '.     CL*17
00035      05  PC-APPL-MAP              PIC S9(04)  VALUE  +1              CL*17
00036                                               COMP.                  CL*17
00037      05  PC-MAX-MSG-VAR           PIC S9(04)  VALUE  +1              CL*17
00038                                               COMP.                  CL*17
00039      05  PC-INTER-APPL-CODES.                                        CL*17
00040          10  PC-INTER-APPL-CRIT-SPEC                                 CL*17
00041                                   PIC  X(01)  VALUE '1'.             CL*17
00042          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.             CL*17
00043      05  PC-MSG-VAR-SYMBOLIC      PIC  X(04)  VALUE '&VAR'.          CL*17
00044      05  PC-ASTERISK              PIC  X(01)  VALUE '*'.             CL*17
00045      05  PC-COMPANY               PIC  X(02)  VALUE '03'.            CL*17
00046      05  PC-6                     PIC  9(01)  VALUE 6.               CL*45
W26600     05  PC-4                     PIC  9(01)  VALUE 4.               CL*51
00048      05  PC-Y                     PIC  X(01)  VALUE 'Y'.             CL*55
00049      05  PC-N                     PIC  X(01)  VALUE 'N'.             CL*58
00050      05  PC-NOT-INVENTORIED       PIC  X(10)  VALUE                  CL*60
00051                                               '1993-12-08'.          CL*71
00052                                                                      CL*17
00053      05  PC-TSYMSG-NUMBERS.                                          CL*17
00054          10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.          CL*17
00055          10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.          CL*17
00056          10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.          CL*26
00057          10  PC-TSYMSG-00017      PIC  9(05)  VALUE  00017.          CL*17
00058          10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.          CL*17
00059          10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.          CL*17
00060          10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.          CL*17
00061          10  PC-TSYMSG-00095      PIC  9(05)  VALUE  00095.          CL*17
00062          10  PC-TSYMSG-00137      PIC  9(05)  VALUE  00137.          CL*35
00063          10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.          CL*35
00064          10  PC-TSYMSG-00302      PIC  9(05)  VALUE  00302.          CL*17
00065          10  PC-TSYMSG-00388      PIC  9(05)  VALUE  00388.          CL*17
00066          10  PC-TSYMSG-00543      PIC  9(05)  VALUE  00543.          CL*17
00067          10  PC-TSYMSG-00551      PIC  9(05)  VALUE  00551.          CL*19
00068          10  PC-TSYMSG-99901      PIC  9(05)  VALUE  99901.          CL*17
00069          10  PC-TSYMSG-99903      PIC  9(05)  VALUE  99903.          CL*17
00070                                                                      CL*17
00071  01  PS-PROGRAM-SWITCHES.                                            CL*17
00072      05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.            CL*17
00073          88  PS-ERROR                         VALUE  'Y'.            CL*17
00074          88  PS-NO-ERROR                      VALUE  'N'.            CL*17
00075      05  PS-STORE-INVENTORY-SW    PIC  X(01)  VALUE  'Y'.            CL*17
00076          88  PS-STORE-INVENTORIED             VALUE  'N'.            CL*53
00077                                                                      CL*17
00078  01  PV-PROGRAM-VARIABLES.                                           CL*17
W26600     05  PV-LOC-NBR-X             PIC X(04)  VALUE SPACES.           CL*17
W26600     05  PV-LOC-NBR-9 REDEFINES                                      CL*17
W26600         PV-LOC-NBR-X             PIC 9(04).                         CL*17
W26600     05  PV-EDIT-LOC-NBR          PIC 9(04)  VALUE ZEROS.            CL*17
00079      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0               CL*17
00080                                               COMP SYNC.             CL*17
00081      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0               CL*17
00082                                               COMP SYNC.             CL*17
00083      05  PV-ASTERISK-CNT          PIC S9(04)  VALUE +0               CL*17
00084                                               COMP SYNC.             CL*17
00085      05  PV-NULL                  PIC S9(04)  VALUE +0               CL*34
00086                                               COMP SYNC.             CL*34
00087                                                                      CL*17
00088 /                                                                    CL*17
00089 /                                                                    CL*17
00090 *----------------------------------------------------------------*   CL*17
00091 *    MAP LAYOUT                                                  *   CL*17
00092 *----------------------------------------------------------------*   CL*17
00093                                                                      CL*17
W26600     COPY INKM001.                                                   CL*17
00096 /                                                                    CL*17
00097 *----------------------------------------------------------------*   CL*17
00098 *    ATTRIBUTE SETTINGS COPYBOOK.                                *   CL*17
00099 *----------------------------------------------------------------*   CL*17
00100                                                                      CL*17
00101      COPY DPWS015.                                                   CL*17
00102                                                                      CL*17
00103 *----------------------------------------------------------------*   CL*17
00104 *    FUNCTION KEYS COPYBOOK                                      *   CL*17
00105 *----------------------------------------------------------------*   CL*17
00106                                                                      CL*17
00107      COPY DPWS016.                                                   CL*17
00108                                                                      CL*17
00109 /                                                                    CL*17
00110 *----------------------------------------------------------------*   CL*17
00111 *  THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT     *   CL*17
00112 *  SUB-ROUTINE (DPKUT100).                                       *   CL*17
00113 *----------------------------------------------------------------*   CL*17
00114      COPY DPWS010I.                                                  CL*17
00115 /                                                                    CL*17
00116 *----------------------------------------------------------------*   CL*17
00117 *  THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS ARCHITECTURE*   CL*17
00118 *  APPLICATION PROGRAMMING INTERFACE MODULE -- DPKCS030.         *   CL*17
00119 *----------------------------------------------------------------*   CL*17
00120      COPY DPWS030.                                                   CL*17
1222RM     COPY DPWS930.                                                   CL*17
00121 /                                                                    CL*17
00122 **--------------------------------------------------------------**   CL*17
00123 *    STANDARD COMMAREA.                                              CL*17
00124 **--------------------------------------------------------------**   CL*17
00125                                                                      CL*17
00126      COPY DPWS020.                                                   CL*17
00127      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL*17
00128                                                                      CL*17
00129 *****************************************************************    CL*17
00130 *    INTER-APPLICATION COMMAREA.                                     CL*17
00131 *****************************************************************    CL*17
00132                                                                      CL*17
00133          10  INTER-APPL-COMM-AREA PIC X(200).                        CL*17
W26600     COPY INWS001.                                                   CL*74
00135 *****************************************************************    CL*17
00136 *    SPECIFIC COMMMAREA FOR DPKCS041.                                CL*17
00137 *****************************************************************    CL*17
00138          10  ASC-SPECIFIC-COMMAREA.                                  CL*17
00139              15  ASC-INT-APPL-FORMAT-IND   PIC  X(01).               CL*17
00140              15  ASC-STORE-NBRX.                                     CL*17
W26600                 20  ASC-STORE-NBR         PIC  9(04).               CL*17
00142              15  ASC-AREA-NBRX.                                      CL*17
00143                  88  ASC-AREA-ASTERISK     VALUE ALL '*'.            CL*17
00144                  20  ASC-AREA-NBR          PIC  9(06).               CL*17
00145              15  ASC-DISPLAY-COMPLETE-SW   PIC  X(01).               CL*27
00146                  88  ASC-DISPLAY-COMPLETE            VALUE 'Y'.      CL*27
00147                  88  ASC-DISPLAY-NOT-COMPLETE        VALUE 'N'.      CL*27
W26600             15  ASC-LOCATION              PIC  X(04).               CL*25
00149              15  ASC-LOCATION-TYPE         PIC  X(02).               CL*17
00150                  88  ASC-CORPORATE                    VALUE 'HQ'.    CL*17
00151              15  ASC-STORE-NAME            PIC  X(25).               CL*17
00152              15  ASC-HOLD-INTER-APPL       PIC  X(200).              CL*33
W26600         10  FILLER                        PIC  X(197).              CL*17
00154 /                                                                    CL*53
00155 *----------------------------------------------------------------*   CL*17
00156 *    ABEND PROCESSING COPYBOOK.                                  *   CL*17
00157 *----------------------------------------------------------------*   CL*17
00158                                                                      CL*17
00159      COPY DPWS013.                                                   CL*17
00160                                                                      CL*17
00161 *----------------------------------------------------------------*   CL*53
00162 *    CURRENT DATE COPYBOOK.                                      *   CL*53
00163 *----------------------------------------------------------------*   CL*53
00164                                                                      CL*53
00165      COPY DPWS017.                                                   CL*53
00166 /                                                                    CL*53
00167 *----------------------------------------------------------------*   CL*17
00168 *    STORE TABLE                                                 *   CL*17
00169 *----------------------------------------------------------------*   CL*17
00170                                                                      CL*17
00171      EXEC SQL                                                        CL*17
W26600          INCLUDE XMT00001                                           CL*17
00173      END-EXEC.                                                       CL*17
00174                                                                      CL*17
00175 *----------------------------------------------------------------*   CL*17
00176 *    INVENTORY PARAMETERS TABLE                                  *   CL*17
00177 *----------------------------------------------------------------*   CL*17
00178                                                                      CL*17
00179      EXEC SQL                                                        CL*17
00180           INCLUDE TINVPAR                                            CL*17
00181      END-EXEC.                                                       CL*17
00182                                                                      CL*17
00175 *----------------------------------------------------------------*   CL*17
00176 *    INVENTORY CONTROL TABLE                                     *   CL*17
00177 *----------------------------------------------------------------*   CL*17
J28545     EXEC SQL                                                        CL*17
J28545          INCLUDE TINCNTL                                            CL*17
J28545     END-EXEC.                                                       CL*17
J28545                                                                     CL*17
00183 *----------------------------------------------------------------*   CL*17
00184 *    COMMUNICATIONS AREA                                         *   CL*17
00185 *----------------------------------------------------------------*   CL*17
00186                                                                      CL*17
00187      EXEC SQL                                                        CL*17
00188           INCLUDE SQLCA                                              CL*17
00189      END-EXEC.                                                       CL*17
00190                                                                      CL*17
00191 /                                                                    CL*17
00192  LINKAGE SECTION.                                                    CL*17
00193                                                                      CL*17
00194  01  DFHCOMMAREA.                                                    CL*17
00195      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL*17
00196                                         DEPENDING ON EIBCALEN.       CL*17
00197          10  FILLER                     PIC X.                       CL*17
00198                                                                      CL*17
00199                                                                      CL*17
00200  PROCEDURE DIVISION.                                                 CL*17
00201 **-------------------------------------------------------------**    CL*17
00202 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **    CL*17
00203 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **    CL*17
00204 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **    CL*17
00205 **                                                             **    CL*17
00206 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **    CL*17
00207 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **    CL*17
00208 **-------------------------------------------------------------**    CL*17
00209                                                                      CL*17
00210  0000-MAIN-MODULE.                                                   CL*17
00211                                                                      CL*17
00212      INITIALIZE DP030-CICS-API-FIELDS.                               CL*17
00213      MOVE +1                       TO DP030-NUMBER-OF-MAPS.          CL*17
00214      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.             CL*17
00215      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).            CL*17
00216      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL*17
00217                                                                      CL*17
00218      MOVE LENGTH OF IN001AI        TO DP030-MAP-LENGTH (1).          CL*17
00219      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).        CL*17
00220      PERFORM 0001-CALL-CICS-ARCH-API.                                CL*17
00221                                                                      CL*17
00222      PERFORM 1000-CONTROL-PROCESSING                                 CL*17
00223                                                                      CL*17
00224      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).        CL*17
00225      PERFORM 0001-CALL-CICS-ARCH-API.                                CL*17
00226                                                                      CL*17
00227  0001-CALL-CICS-ARCH-API.                                            CL*17
00228                                                                      CL*17
1222RM*    CALL 'DPKCS030' USING DFHEIBLK                                  CL*17
1222RM     CALL DP930-CICS-ARCH-API                                     INKCS002
1222RM                    USING  DFHEIBLK                                  CL*17
1222RM                           DFHCOMMAREA                               CL*17
00231                            DP030-CICS-API-FIELDS                     CL*17
00232                            DP020-STANDARD-COMMAREA                   CL*17
00233                            IN001AI.                                  CL*17
00234                                                                      CL*17
00235      IF  DP030-RC-CALL-SUCCESSFUL                                    CL*17
00236          CONTINUE                                                    CL*17
00237      ELSE                                                            CL*17
00238          SET DP013-NO-ROLLBACK                                       CL*17
00239              DP013-XCTL-DISPLAY-RESTART                              CL*17
00240              DP013-CICS-ABEND      TO TRUE                           CL*17
00241          MOVE 'BEFORE 0000-MAIN-MODULE'                              CL*17
00242                                    TO DP013-PARAGRAPH                CL*17
1222RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX   CL*17
1222RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)         CL*17
1222RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1222RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00245          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)         CL*17
00246          PERFORM DP013-0000-PROCESS-ABEND                            CL*17
00247      END-IF.                                                         CL*17
00248 /                                                                    CL*17
00249 *----------------------------------------------------------------*   CL*17
00250 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *   CL*17
00251 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *   CL*17
00252 *----------------------------------------------------------------*   CL*17
00253                                                                      CL*17
00254  1000-CONTROL-PROCESSING.                                            CL*17
00255                                                                      CL*17
00256      EVALUATE TRUE                                                   CL*17
00257          WHEN DP020-NEXT-ACT-INITIAL                                 CL*17
00258               INITIALIZE ASC-SPECIFIC-COMMAREA                       CL*17
00259               PERFORM 4000-BUILD-INITIAL-PANEL                       CL*17
00260                                                                      CL*17
00261          WHEN DP020-NEXT-ACT-READ-MAP                                CL*17
00262               PERFORM 2000-PROCESS-PANEL                             CL*17
00263                                                                      CL*17
00264          WHEN DP020-NEXT-ACT-RETURN                                  CL*17
00265               MOVE ASC-HOLD-INTER-APPL                               CL*33
00266                                   TO INTER-APPL-COMM-AREA            CL*33
00267               PERFORM 3000-EDIT-DATA-IN-COMMAREA                     CL*17
00268               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                   CL*17
00269                                                                      CL*17
00270          WHEN OTHER                                                  CL*17
00271               SET DP013-LOGIC-ABEND TO TRUE                          CL*17
00272               SET DP013-NO-ROLLBACK TO TRUE                          CL*17
00273               MOVE '1000-CONTROL-PROCESSING'                         CL*17
00274                                     TO DP013-PARAGRAPH               CL*17
00275               MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'     CL*17
00276                                     TO DP013-MESSAGE-TEXT(1)         CL*17
00277               MOVE DP020-NEXT-APPL-ACTIVITY                          CL*17
00278                                     TO DP013-MESSAGE-TEXT(2)         CL*17
00279               PERFORM DP013-0000-PROCESS-ABEND                       CL*17
00280      END-EVALUATE.                                                   CL*17
00281 /                                                                    CL*17
00282 *----------------------------------------------------------------*   CL*17
00283 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *   CL*17
00284 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *   CL*17
00285 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *   CL*17
00286 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *   CL*17
00287 * FUNCTION KEYS CAN GET THIS FAR.                                *   CL*17
00288 *----------------------------------------------------------------*   CL*17
00289                                                                      CL*17
00290  2000-PROCESS-PANEL.                                                 CL*17
00291                                                                      CL*17
00292      SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                         CL*28
00293      IF  IN001-STORE-NBR NOT NUMERIC                                 CL*59
00294          MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA            CL*59
00295      END-IF.                                                         CL*59
00296                                                                      CL*28
00297      EVALUATE TRUE                                                   CL*17
00298                                                                      CL*17
00299          WHEN DP020-SRC-AID = DP016-CLEAR                            CL*17
00300               PERFORM 3000-EDIT-DATA-IN-COMMAREA                     CL*17
00301               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                   CL*17
00302                                                                      CL*17
00303          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL*17
00304               SET DP030-DONT-ERASE                                   CL*17
00305                   DP030-SEND-DATAONLY (PC-APPL-MAP)                  CL*17
00306                                    TO TRUE                           CL*17
00307               PERFORM 4000-BUILD-INITIAL-PANEL                       CL*17
00308               PERFORM 3200-RESET-ATTRIBUTES                          CL*17
00309                                                                      CL*17
00310          WHEN OTHER                                                  CL*17
00311               PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                   CL*17
00312               IF (DP020-SRC-AID EQUAL DP016-ENTER                    CL*49
00313               AND DP020-NAV-MNEMONIC GREATER THAN SPACES)            CL*49
00314                   CONTINUE                                           CL*49
00315               ELSE                                                   CL*49
00316                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                 CL*49
00317                   IF  PS-NO-ERROR                                    CL*49
00318                       SET DP030-CONTINUE-GO                          CL*49
00319                                    TO TRUE                           CL*49
00320                       PERFORM 2100-CHECK-FUNCTION-KEY                CL*49
00321                   ELSE                                               CL*49
00322                       SET DP030-OVERRIDE-GO                          CL*49
00323                                    TO TRUE                           CL*49
00324                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN           CL*50
00325                   END-IF                                             CL*49
00326               END-IF                                                 CL*49
00327      END-EVALUATE.                                                   CL*17
00328                                                                      CL*17
00329 *----------------------------------------------------------------*   CL*17
00330 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *   CL*17
00331 * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *   CL*17
00332 * FROM THE CICS ARCHITECTURE API.                                *   CL*17
00333 *----------------------------------------------------------------*   CL*17
00334                                                                      CL*17
00335  2100-CHECK-FUNCTION-KEY.                                            CL*17
00336                                                                      CL*17
00337      EVALUATE TRUE                                                   CL*17
00338          WHEN DP020-SRC-AID = DP016-ENTER                            CL*17
00339               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                   CL*50
00340                                                                      CL*17
00341          WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                    CL*17
00342               MOVE INTER-APPL-COMM-AREA                              CL*33
00343                                    TO  ASC-HOLD-INTER-APPL           CL*33
00344                                                                      CL*17
00345          WHEN OTHER                                                  CL*17
00346              SET DP013-NO-ROLLBACK                                   CL*17
00347                  DP013-XCTL-DISPLAY-RESTART                          CL*17
00348                  DP013-CICS-ABEND  TO TRUE                           CL*17
00349              MOVE '2000-PROCESS-PANEL'                               CL*17
00350                                    TO DP013-PARAGRAPH                CL*17
00351              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL*17
00352                                    TO DP013-MESSAGE-TEXT (1)         CL*17
00353              PERFORM DP013-0000-PROCESS-ABEND                        CL*17
00354      END-EVALUATE.                                                   CL*17
00355                                                                      CL*17
00356 /                                                                    CL*17
00357 *----------------------------------------------------------------*   CL*17
00358 * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS   *   CL*17
00359 * IN THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN   *   CL*17
00360 * THE APPLICATION-SPECIFIC COMMAERA, NOT ON THE SCREEN.          *   CL*17
00361 *----------------------------------------------------------------*   CL*17
00362                                                                      CL*17
00363  2200-MOVE-SCREEN-TO-COMMAREA.                                       CL*17
00364                                                                      CL*17
00365      IF  ASTOREL > ZERO                                              CL*17
00366          MOVE ASTOREI              TO ASC-STORE-NBRX                 CL*17
00367      ELSE                                                            CL*17
00368          IF  ASTOREF = DP015-ERASE-EOF                               CL*17
00369              MOVE SPACE            TO ASC-STORE-NBRX                 CL*17
00370          END-IF                                                      CL*17
00371      END-IF.                                                         CL*17
00372                                                                      CL*17
00373      IF  AAREAL > ZERO                                               CL*17
00374          MOVE AAREAI               TO ASC-AREA-NBRX                  CL*17
00375      ELSE                                                            CL*17
00376          IF  AAREAF = DP015-ERASE-EOF                                CL*17
00377              MOVE SPACE            TO ASC-AREA-NBRX                  CL*17
00378          END-IF                                                      CL*17
00379      END-IF.                                                         CL*17
00380                                                                      CL*17
00381      IF  AREACMPL > ZERO                                             CL*26
00382          MOVE AREACMPI             TO ASC-DISPLAY-COMPLETE-SW        CL*26
00383      ELSE                                                            CL*25
00384          IF  AREACMPF = DP015-ERASE-EOF                              CL*25
00385              MOVE SPACE            TO ASC-DISPLAY-COMPLETE-SW        CL*26
00386          END-IF                                                      CL*25
00387      END-IF.                                                         CL*25
00388                                                                      CL*25
00389 /                                                                    CL*17
00390 *----------------------------------------------------------------*   CL*17
00391 * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *   CL*17
00392 * IS FOUND, THE CURSOR IS POSITIONED AT THAT FIELD AND AN        *   CL*17
00393 * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *   CL*17
00394 *----------------------------------------------------------------*   CL*17
00395                                                                      CL*17
00396  3000-EDIT-DATA-IN-COMMAREA.                                         CL*17
00397                                                                      CL*17
00398      PERFORM 3200-RESET-ATTRIBUTES.                                  CL*17
00399      SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                         CL*35
00400                                                                      CL*17
00401      IF (ASC-DISPLAY-COMPLETE                                        CL*27
00402      OR  ASC-DISPLAY-NOT-COMPLETE)                                   CL*27
00403          MOVE ASC-DISPLAY-COMPLETE-SW TO IN001-DISPLAY-COMPLETE-SW   CL*27
00404      ELSE                                                            CL*25
00405          SET PS-ERROR                                                CL*25
00406              DP020-MSG-FATAL       TO  TRUE                          CL*25
00407          MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER              CL*29
00408          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*25
00409                                    TO  AREACMPA                      CL*25
00410          MOVE DP015-RED            TO  AREACMPC                      CL*25
00411          MOVE DP015-REVERSE        TO  AREACMPH                      CL*25
00412          MOVE -1                   TO  AREACMPL                      CL*25
00413      END-IF.                                                         CL*25
00414                                                                      CL*25
00415      MOVE ASC-AREA-NBR             TO  DP010I-UNEDITED-FIELD.        CL*44
00416      MOVE PC-6                     TO  DP010I-MAXIMUM-DIGITS.        CL*44
00417      MOVE +0                       TO  DP010I-MAXIMUM-DECIMALS.      CL*44
00418      SET DP010I-NEGATIVE-NOT-ALLOWED                                 CL*44
00419                                    TO  TRUE.                         CL*44
263126     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263126          USING DP010I-NUMERIC-EDIT-AREA.                                 
00421                                                                      CL*44
00422      IF  NOT DP010I-ERROR-DETECTED                                   CL*44
00423          IF  DP010I-NUMERIC-FIELD EQUAL ZERO                         CL*48
00424              SET PS-ERROR                                            CL*48
00425                  DP020-MSG-FATAL   TO  TRUE                          CL*48
00426              MOVE PC-TSYMSG-00005  TO  DP020-MSG-NUMBER              CL*48
00427              MOVE DP015-UNP-ALP-BRT-OFF                              CL*48
00428                                    TO  AAREAA                        CL*48
00429              MOVE DP015-RED        TO  AAREAC                        CL*48
00430              MOVE DP015-REVERSE    TO  AAREAH                        CL*48
00431              MOVE -1               TO  AAREAL                        CL*48
00432          ELSE                                                        CL*48
00433              MOVE DP010I-NUMERIC-FIELD                               CL*48
00434                                    TO  IN001-AREA-NBR                CL*48
00435                                        ASC-AREA-NBR                  CL*44
00436          END-IF                                                      CL*48
00437      ELSE                                                            CL*17
00438          INSPECT ASC-AREA-NBRX                                       CL*17
00439              TALLYING PV-ASTERISK-CNT                                CL*17
00440              FOR ALL PC-ASTERISK                                     CL*17
00441                                                                      CL*17
00442          IF  PV-ASTERISK-CNT GREATER THAN ZERO                       CL*17
00443              SET ASC-AREA-ASTERISK                                   CL*47
00444                  IN001-AREA-ASTERISK                                 CL*47
00445                                    TO  TRUE                          CL*47
00446          ELSE                                                        CL*17
00447              SET PS-ERROR                                            CL*17
00448                  DP020-MSG-FATAL   TO  TRUE                          CL*24
00449              MOVE PC-TSYMSG-00005  TO  DP020-MSG-NUMBER              CL*17
00450              MOVE DP015-UNP-ALP-BRT-OFF                              CL*17
00451                                    TO  AAREAA                        CL*17
00452              MOVE DP015-RED        TO  AAREAC                        CL*17
00453              MOVE DP015-REVERSE    TO  AAREAH                        CL*17
00454              MOVE -1               TO  AAREAL                        CL*17
00455          END-IF                                                      CL*17
00456      END-IF.                                                         CL*17
00457                                                                      CL*36
00458      IF (ASC-AREA-ASTERISK                                           CL*36
00459      AND DP020-SRC-AID NOT = DP016-PF20                              CL*36
00460      AND DP020-FK-LOCAL-GOTO (DP020-SRC-AID))                        CL*36
00461          SET DP030-OVERRIDE-GO     TO  TRUE                          CL*36
00462          MOVE PC-TSYMSG-00137      TO  DP020-MSG-NUMBER              CL*36
00463          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*36
00464                                    TO  AAREAA                        CL*36
00465          MOVE DP015-RED            TO  AAREAC                        CL*36
00466          MOVE DP015-REVERSE        TO  AAREAH                        CL*36
00467          MOVE -1                   TO  AAREAL                        CL*36
00468          SET DP020-MSG-FATAL                                         CL*36
00469              PS-ERROR              TO  TRUE                          CL*36
00470      END-IF.                                                         CL*36
00471                                                                      CL*36
00472      IF  ASC-CORPORATE                                               CL*36
00473          MOVE ASC-STORE-NBR        TO  DP010I-UNEDITED-FIELD         CL*51
W26600         MOVE PC-4                 TO  DP010I-MAXIMUM-DIGITS         CL*51
00475          MOVE ZERO                 TO  DP010I-MAXIMUM-DECIMALS       CL*51
00476          SET DP010I-NEGATIVE-NOT-ALLOWED                             CL*51
00477                                    TO  TRUE                          CL*51
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00479                                                                      CL*51
00480          IF  DP010I-ERROR-DETECTED                                   CL*51
00481              SET PS-ERROR                                            CL*51
00482                  DP020-MSG-FATAL                                     CL*51
00483                                    TO  TRUE                          CL*51
00484              MOVE PC-TSYMSG-00005  TO  DP020-MSG-NUMBER              CL*51
00485              MOVE DP015-UNP-ALP-BRT-OFF                              CL*51
00486                                    TO  ASTOREA                       CL*51
00487              MOVE DP015-RED        TO  ASTOREC                       CL*51
00488              MOVE DP015-REVERSE    TO  ASTOREH                       CL*51
00489              MOVE -1               TO  ASTOREL                       CL*51
00490          ELSE                                                        CL*51
00491              MOVE DP010I-NUMERIC-FIELD                               CL*51
00492                                    TO  ASC-STORE-NBR                 CL*52
00493              MOVE ASC-STORE-NBR    TO  ASC-LOCATION                  CL*17
00494              PERFORM 4100-GET-STORE-NAME                             CL*17
00495                                                                      CL*17
00496              IF  SQLCODE NOT EQUAL ZERO                              CL*17
00497                  SET PS-ERROR                                        CL*17
00498                      DP020-MSG-FATAL                                 CL*17
00499                                    TO  TRUE                          CL*17
00500                  MOVE PC-TSYMSG-00017                                CL*17
00501                                    TO  DP020-MSG-NUMBER              CL*17
00502                  MOVE DP015-UNP-ALP-BRT-OFF                          CL*17
00503                                    TO  ASTOREA                       CL*17
00504                  MOVE DP015-RED    TO  ASTOREC                       CL*17
00505                  MOVE DP015-REVERSE TO ASTOREH                       CL*17
00506                  MOVE -1           TO  ASTOREL                       CL*17
00507              ELSE                                                    CL*17
W26600                 MOVE XMT00001-LOC-NM                                CL*17
00509                                    TO  IN001-STORE-NAME              CL*17
00510                                        ASTRNAMO                      CL*24
00511                  MOVE ASC-STORE-NBR TO IN001-STORE-NBR               CL*17
J28545                                       INVPAR-LOC-NBR                     
00512                  PERFORM 4200-READ-TINVPAR                           CL*17
00513                  PERFORM 4300-CHECK-INVENTORIED                      CL*17
00514                  IF  NOT PS-STORE-INVENTORIED                        CL*17
00515                      SET PS-ERROR  TO  TRUE                          CL*17
00516                      SET DP020-MSG-FATAL                             CL*17
00517                                    TO  TRUE                          CL*17
00518                      MOVE DP015-UNP-ALP-BRT-OFF                      CL*56
00519                                    TO  ASTOREA                       CL*56
00520                      MOVE DP015-RED TO ASTOREC                       CL*56
00521                      MOVE DP015-REVERSE                              CL*56
00522                                    TO  ASTOREH                       CL*56
00523                      MOVE -1       TO  ASTOREL                       CL*56
00524                      MOVE PC-TSYMSG-00543                            CL*17
00525                                    TO  DP020-MSG-NUMBER              CL*17
00526                  END-IF                                              CL*17
00527              END-IF                                                  CL*17
00528          END-IF                                                      CL*17
00529      END-IF.                                                         CL*17
00530                                                                      CL*17
00531      IF  PS-NO-ERROR                                                 CL*17
00532          IF  ASC-CORPORATE                                           CL*17
00533              MOVE -1               TO  ASTOREL                       CL*17
00534          ELSE                                                        CL*17
00535              MOVE -1               TO  AAREAL                        CL*17
00536          END-IF                                                      CL*17
00537      END-IF.                                                         CL*17
00538                                                                      CL*17
00539 *----------------------------------------------------------------*   CL*17
00540 * MOVE THE DEFAULT ATTRIBUTE VALUES TO THE ENTERABLE FIELDS ON   *   CL*17
00541 * MAP.                                                           *   CL*17
00542 *----------------------------------------------------------------*   CL*17
00543                                                                      CL*17
00544  3200-RESET-ATTRIBUTES.                                              CL*17
00545                                                                      CL*17
00546      MOVE DP015-UNP-ALP-NOR-OFF    TO  AAREAA                        CL*28
00547                                        AREACMPA.                     CL*28
00548      MOVE DP015-GREEN              TO  AAREAC                        CL*28
00549                                        AREACMPC.                     CL*28
00550      MOVE DP015-UNDERLINE          TO  AAREAH                        CL*28
00551                                        AREACMPH.                     CL*28
00552                                                                      CL*17
00553      IF  ASC-CORPORATE                                               CL*17
00554          MOVE DP015-UNP-ALP-NOR-OFF                                  CL*17
00555                                    TO  ASTOREA                       CL*17
00556          MOVE DP015-GREEN          TO  ASTOREC                       CL*17
00557          MOVE DP015-UNDERLINE      TO  ASTOREH                       CL*17
00558      END-IF.                                                         CL*17
00559                                                                      CL*17
00560 /                                                                    CL*17
00561 *----------------------------------------------------------------*   CL*17
00562 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *   CL*17
00563 *    ENTRY.                                                      *   CL*17
00564 *----------------------------------------------------------------*   CL*17
00565                                                                      CL*17
00566  4000-BUILD-INITIAL-PANEL.                                           CL*17
00567                                                                      CL*17
00568      MOVE SPACES                   TO  IN001-STORE-NAME              CL*38
00569                                        ASC-LOCATION                  CL*38
00570                                        ASC-AREA-NBRX.                CL*57
00571      MOVE PC-N                     TO  ASC-DISPLAY-COMPLETE-SW.      CL*57
00572                                                                      CL*38
00573      MOVE DP020-LOCATION-TYPE      TO  ASC-LOCATION-TYPE.            CL*17
00574      SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                         CL*17
00575                                                                      CL*17
00576      IF  ASC-CORPORATE                                               CL*17
00577          MOVE DP015-UNP-ALP-NOR-OFF                                  CL*17
00578                                    TO  ASTOREA                       CL*17
00579          MOVE DP015-GREEN          TO  ASTOREC                       CL*17
00580          MOVE DP015-UNDERLINE      TO  ASTOREH                       CL*17
00581          MOVE -1                   TO  ASTOREL                       CL*17
00582      ELSE                                                            CL*17
W28545         MOVE DP020-LOCATION-NBR   TO  PV-EDIT-LOC-NBR               CL*40
W26600         MOVE PV-EDIT-LOC-NBR      TO  ASC-LOCATION                  CL*40
00584                                        IN001-STORE-NBR               CL*40
J28545                                       INVPAR-LOC-NBR                     
00585          PERFORM 4100-GET-STORE-NAME                                 CL*17
00586          PERFORM 4200-READ-TINVPAR                                   CL*17
00587          IF  PS-NO-ERROR                                             CL*34
00588              IF  INVPAR-SCAN-INV-IND EQUAL PC-Y                      CL*53
00589                  PERFORM 4300-CHECK-INVENTORIED                      CL*53
00590                  IF  PS-STORE-INVENTORIED                            CL*53
W26600                     MOVE XMT00001-LOC-NM                            CL*53
00592                                     TO  IN001-STORE-NAME             CL*53
00593                      MOVE -1        TO  AAREAL                       CL*53
00594                  ELSE                                                CL*53
00595                      MOVE PC-TSYMSG-00543                            CL*53
00596                                     TO  DP020-MSG-NUMBER             CL*53
00597                      SET PS-ERROR                                    CL*53
00598                          DP020-MSG-FATAL                             CL*53
00599                          DP020-NEXT-ACT-APPL-ERROR                   CL*53
00600                                     TO  TRUE                         CL*53
00601                  END-IF                                              CL*53
00602              ELSE                                                    CL*53
00603                  MOVE PC-TSYMSG-99903                                CL*53
00604                                     TO  DP020-MSG-NUMBER             CL*53
00605                  SET PS-ERROR                                        CL*53
00606                          DP020-MSG-FATAL                             CL*53
00607                          DP020-NEXT-ACT-APPL-ERROR                   CL*53
00608                                     TO  TRUE                         CL*53
00609              END-IF                                                  CL*53
00610          END-IF                                                      CL*53
00611      END-IF.                                                         CL*34
00612                                                                      CL*17
00613      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                           CL*17
00614 /                                                                    CL*17
00615 *----------------------------------------------------------------*   CL*17
00616 * SELECT A ROW FROM THE DB2 STORE TABLE.                         *   CL*17
00617 *----------------------------------------------------------------*   CL*17
00618                                                                      CL*17
00619  4100-GET-STORE-NAME.                                                CL*17
00620                                                                      CL*17
W26600     MOVE ASC-LOCATION        TO PV-LOC-NBR-X.                            
W26600     MOVE PV-LOC-NBR-9        TO XMT00001-LOC-NBR.                        
W26600                                                                          
00621          EXEC SQL                                                    CL*17
W26600             SELECT LOC_NM                                           CL*17
W26600             INTO :XMT00001-LOC-NM                                   CL*17
W26600             FROM XMT_LOC                                            CL*17
W26600             WHERE LOC_NBR = :XMT00001-LOC-NBR                       CL*17
W26600             WITH UR                                                      
00631          END-EXEC.                                                   CL*17
00632                                                                      CL*17
00633          EVALUATE TRUE                                               CL*17
00634              WHEN SQLCODE = ZERO                                     CL*17
00635              WHEN SQLCODE = +100                                     CL*17
00636                  CONTINUE                                            CL*17
00637              WHEN SQLCODE = -904                                     CL*17
00638              WHEN SQLCODE = -913                                     CL*17
00639                   PERFORM 9000-SOFT-ABORT                            CL*17
00640              WHEN SQLWARN0 NOT = SPACES                              CL*17
00641              WHEN SQLCODE NOT EQUAL ZERO                             CL*17
00642                  MOVE '4100-GET-STORE-NAME'                          CL*17
00643                                    TO DP013-PARAGRAPH                CL*17
00644                  MOVE 'SELECT STORE NAME'                            CL*17
00645                                    TO DP013-MESSAGE-TEXT (1)         CL*17
00646                  MOVE SQLCA        TO DP013-SQLCA                    CL*17
W26600                 MOVE 'XMTLOC'     TO DP013-DB2-TABLE-NAME (1)       CL*17
00648                  SET DP013-DB2-ABEND                                 CL*17
00649                      DP013-XCTL-DISPLAY-RESTART                      CL*17
00650                                    TO TRUE                           CL*17
00651                  PERFORM DP013-0000-PROCESS-ABEND                    CL*17
00652          END-EVALUATE.                                               CL*17
00653 /                                                                    CL*17
00654 *----------------------------------------------------------------*   CL*17
00655 *     GET THE ACTUAL DATE AND SCAN SWITCH.                       *   CL*17
00656 *----------------------------------------------------------------*   CL*17
00657                                                                      CL*17
J28545 4200-READ-TINVPAR.                                                  CL*17
J28545     MOVE ASC-LOCATION        TO PV-LOC-NBR-X.                            
J28545     MOVE PV-LOC-NBR-9        TO INVPAR-LOC-NBR.                          
J28545                                                                     CL*17
J28545     EXEC SQL                                                             
J28545       SELECT   A.ACTL_INV_DTE                                            
J28545               ,A.SCAN_INV_IND                                       CL*23
J28545               ,A.ACTL_INV_RECD_DTE                                  CL*22
J28545               ,B.INV_ID                                                  
J28545               ,B.INV_NM                                                  
J28545         INTO  :INVPAR-ACTL-INV-DTE                                       
J28545              ,:INVPAR-SCAN-INV-IND                                       
J28545              ,:INVPAR-ACTL-INV-RECD-DTE                                  
J28545              ,:INCNTL-INV-ID                                             
J28545              ,:INCNTL-INV-NM                                             
J28545         FROM   TINVPAR A                                                 
J28545               ,TINCNTL B                                                 
J28545       WHERE A.INV_ID = B.INV_ID                                          
J28545         AND B.ACTV_IND = 'Y'                                             
J28545         AND A.LOC_NBR = :XMT00001-LOC-NBR                                
J28545         AND A.ACTL_FIN_BK_DTE = '9999-09-09'                        CL*22
W33304         AND A.LOC_INV_STAT_CDE = 'IN'                               CL*22
J28545     END-EXEC.                                                            
J28545                                                                     CL*17
J28545     EVALUATE TRUE                                                   CL*22
J28545         WHEN SQLCODE = ZERO                                         CL*22
J28545         WHEN SQLCODE = +100                                         CL*22
J28545             CONTINUE                                                CL*22
J28545         WHEN SQLCODE = -904                                         CL*22
J28545         WHEN SQLCODE = -913                                         CL*22
J28545              PERFORM 9000-SOFT-ABORT                                CL*22
J28545         WHEN SQLWARN0 NOT = SPACES                                  CL*22
J28545         WHEN SQLCODE NOT EQUAL ZERO                                 CL*22
J28545             MOVE '4200-READ-TINVPAR - 1'                            CL*23
J28545                               TO DP013-PARAGRAPH                    CL*22
J28545             MOVE 'SELECT STORE NAME'                                CL*22
J28545                               TO DP013-MESSAGE-TEXT (1)             CL*22
J28545             MOVE SQLCA            TO DP013-SQLCA                    CL*22
J28545             MOVE 'TINVPAR'        TO DP013-DB2-TABLE-NAME (1)       CL*22
J28545             SET DP013-DB2-ABEND                                     CL*22
J28545                 DP013-XCTL-DISPLAY-RESTART                          CL*22
J28545                               TO TRUE                               CL*22
J28545             PERFORM DP013-0000-PROCESS-ABEND                        CL*22
J28545     END-EVALUATE.                                                   CL*22
J28545                                                                          
J28545     IF  SQLCODE EQUAL ZERO                                          CL*41
J28545         CONTINUE                                                         
J28545     ELSE                                                                 
J28545         IF  ASC-CORPORATE                                           CL*43
J28545             MOVE DP015-UNP-ALP-BRT-OFF                              CL*43
J28545                                   TO  ASTOREA                       CL*43
J28545             MOVE DP015-RED        TO  ASTOREC                       CL*43
J28545             MOVE DP015-REVERSE    TO  ASTOREH                       CL*43
J28545             MOVE -1               TO  ASTOREL                       CL*43
J28545         ELSE                                                        CL*43
J28545             MOVE -1               TO  AAREAL                        CL*43
J28545         END-IF                                                      CL*43
J28545     END-IF.                                                              
J28545                                                                          
J28545     IF (SQLCODE = ZERO                                              CL*34
J28545     AND INVPAR-ACTL-INV-DTE GREATER THAN SPACES)                    CL*53
J28545         MOVE INVPAR-ACTL-INV-DTE  TO  IN001-DB2-ACTL-INV-DTE        CL*17
J28545         MOVE INVPAR-SCAN-INV-IND  TO  IN001-SCANNING-STORE-SW       CL*17
J28545         MOVE INCNTL-INV-ID        TO  IN001-INV-ID                       
J28545         MOVE INCNTL-INV-NM        TO  IN001-INV-NM                       
J28545     ELSE                                                            CL*34
J28545         MOVE PC-TSYMSG-00543      TO  DP020-MSG-NUMBER              CL*42
J28545         SET PS-ERROR                                                CL*34
J28545             DP020-MSG-FATAL       TO  TRUE                          CL*34
J28545     END-IF.                                                         CL*17
J28545                                                                     CL*17
00750 *----------------------------------------------------------------*   CL*17
00751 *    DETERMINE WHETHER THE STORE HAS BEEN INVENTORIED.           *   CL*17
00752 *----------------------------------------------------------------*   CL*17
00753                                                                      CL*17
00754  4300-CHECK-INVENTORIED.                                             CL*17
00755                                                                      CL*17
00756      PERFORM DP017-0000-GET-CURR-DATE-TIME.                          CL*53
00757      SET DP017-EDITED-YMD-DATE-DASH-1                                CL*53
00758          DP017-EDITED-YMD-DATE-DASH-2                                CL*53
00759                                    TO  TRUE.                         CL*53
00760 *    MOVE PC-NOT-INVENTORIED       TO DP017-EDITED-DATE-YYYYMMDD.    CL*73
00761      IF  INVPAR-ACTL-INV-DTE NOT >     DP017-EDITED-DATE-YYYYMMDD    CL*72
00762      AND INVPAR-ACTL-INV-RECD-DTE NOT > DP017-EDITED-DATE-YYYYMMDD   CL*72
00763          SET PS-STORE-INVENTORIED  TO  TRUE                          CL*17
00764      END-IF.                                                         CL*17
00765                                                                      CL*17
00766 *----------------------------------------------------------------*   CL*17
00767 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *   CL*17
00768 *----------------------------------------------------------------*   CL*17
00769                                                                      CL*17
00770  4400-MOVE-COMMAREA-TO-SCREEN.                                       CL*17
00771                                                                      CL*17
00772      MOVE IN001-STORE-NAME         TO  ASTRNAMO.                     CL*17
00773      MOVE ASC-LOCATION             TO  ASTOREO.                      CL*17
00774      MOVE ASC-AREA-NBRX            TO  AAREAO.                       CL*17
00775      MOVE ASC-DISPLAY-COMPLETE-SW  TO  AREACMPO.                     CL*29
00776                                                                      CL*17
00777 /                                                                    CL*17
00778 *----------------------------------------------------------------*   CL*17
00779 *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *   CL*17
00780 *    AND THE ERROR SWITCHES.                                     *   CL*17
00781 *----------------------------------------------------------------*   CL*17
00782                                                                      CL*17
00783  9000-SOFT-ABORT.                                                    CL*17
00784                                                                      CL*17
00785      SET PS-ERROR                TO  TRUE.                           CL*17
00786      SET DP020-MSG-WARNING       TO  TRUE.                           CL*17
00787      MOVE PC-TSYMSG-00551        TO  DP020-MSG-NUMBER.               CL*17
00788 /                                                                    CL*17
00789 *----------------------------------------------------------------*   CL*17
00790 *    ABEND PROCESSOR MODULE                                      *   CL*17
00791 *----------------------------------------------------------------*   CL*17
00792                                                                      CL*17
00793      COPY DPPD013.                                                   CL*17
00794 /                                                                    CL*53
00795 *----------------------------------------------------------------*   CL*53
00796 *    OBTAIN CURRENT DATE MODULE                                  *   CL*53
00797 *----------------------------------------------------------------*   CL*53
00798                                                                      CL*53
00799      COPY DPPD017.                                                   CL*53
