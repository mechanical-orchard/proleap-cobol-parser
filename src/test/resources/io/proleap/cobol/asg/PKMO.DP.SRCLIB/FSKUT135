      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.   FSKUT135.                                                  
       AUTHOR.       RICH KELLEN.                                               
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. SEPT 2006.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *  THIS PROGRAM WILL EXTRACT DATA THAT WILL BE USED TO CREATE             
      *  A FILE FOR MARKET SOLUTIONS.  MARKET SOLUTIONS WILL USE THIS           
      *  DATA TO CREATE THE LOW VOLUME STORE AVERAGE REPORT.                    
      ******************************************************************        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                                 
      * WR/PROJ    DATE        DESCRIPTION OF CHANGES                           
      * ---------- ----------  ----------------------------------------         
      * WR31380    10/16/2006  INITIAL IMPLEMENTATION                           
KP0107* PM00968241 01/07/2006  FIX ISSUE WHERE LAST YEAR DATES WERE             
KP0107*                        BEING CALCULATED INCORRECTLY.  CHANGED           
KP0107*                        TO USE TABLE TDTATTR.                            
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-FILE                                                  
               ASSIGN                       TO INP01.                           
      *                                                                         
           SELECT DRIVER-FILE                                                   
               ASSIGN                       TO INP02.                           
      *                                                                         
            SELECT MARKET-SOLUTIONS-FILE                                        
              ASSIGN                        TO OUT01.                           
      *                                                                         
            SELECT PRCSS-DTE-TY-FILE                                            
              ASSIGN                        TO OUT02.                           
      *                                                                         
            SELECT PRCSS-DTE-LY-FILE                                            
              ASSIGN                        TO OUT03.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
       FD  CONTROL-FILE                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 080 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS CONTROL-RECORD.                                       
       01  CONTROL-REC.                                                         
         05  CTL-DATE-IN                      PIC X(10).                        
         05  FILLER                           PIC X(70).                        
      *                                                                         
       FD DRIVER-FILE                                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS DRIVER-FILE-RECORD.                                   
                                                                                
       01 DRIVER-FILE-RECORD                  PIC X(062).                       
      *                                                                         
       FD MARKET-SOLUTIONS-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS MARKET-SOLUTIONS-REC.                                 
                                                                                
       01 MARKET-SOLUTIONS-REC                PIC X(050).                       
      *                                                                         
       FD PRCSS-DTE-TY-FILE                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS PROCESS-DATE-TY-REC.                                  
                                                                                
       01 PROCESS-DATE-TY-REC.                                                  
          05 PROCESS-DTE-TY-TICK1             PIC X.                            
          05 PROCESS-DTE-TY                   PIC X(10).                        
          05 PROCESS-DTE-TY-TICK2             PIC X.                            
          05 PROCESS-DTE-TY-FILLER            PIC X(68).                        
      *                                                                         
       FD PRCSS-DTE-LY-FILE                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS PROCESS-DATE-LY-REC.                                  
                                                                                
       01 PROCESS-DATE-LY-REC.                                                  
          05 PROCESS-DTE-LY-TICK1             PIC X.                            
          05 PROCESS-DTE-LY                   PIC X(10).                        
          05 PROCESS-DTE-LY-TICK2             PIC X.                            
          05 PROCESS-DTE-LY-FILLER            PIC X(68).                        
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
           COPY SQLCA2.                                                         
      *                                                                         
                                                                                
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO              
                                                        COMP SYNC.              
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
      *                                                                         
      *                                                                         
       01 PC-PROGRAM-CONSTANTS.                                                 
          05  PC-DPKUT500                 PIC X(10)      VALUE                  
              'DPKUT500  '.                                                     
          05  PC-PROGRAM-NAME             PIC X(08)  VALUE                      
              'FSKUT135'.                                                       
          05  PC-MAX-OPN-STORES           PIC S9(04) VALUE +1500                
                                                     COMP-3.                    
          05  PC-SATURDAY                 PIC X(09)  VALUE 'SATURDAY'.          
          05  PC-SUNDAY                   PIC X(09)  VALUE 'SUNDAY'.            
          05  PC-MAX-RETRY-CNT            PIC S9(04) VALUE 3                    
                                                     COMP SYNC.                 
          05  PC-MORE-THAN-MAX-RETRY      PIC S9(04) VALUE 4                    
                                                     COMP SYNC.                 
      *                                                                         
       01 PV-PROGRAM-VARIABLES.                                                 
          05  PV-DATE-IN                  PIC X(10)     VALUE SPACE.            
          05  PV-FSCL-YR-NBR              PIC 9(4)      VALUE ZERO.             
          05  PV-FSCL-WK-NBR              PIC 9(2)      VALUE ZERO.             
          05  PV-FSCL-YR-NBR-LY           PIC 9(4)      VALUE ZERO.             
          05  PV-FSCL-WK-NBR-LY           PIC 9(2)      VALUE ZERO.             
          05  PV-TY-FIRST-DAY-OF-WEEK     PIC X(10)     VALUE SPACE.            
          05  PV-TY-LAST-DAY-OF-WEEK      PIC X(10)     VALUE SPACE.            
          05  PV-LY-FIRST-DAY-OF-WEEK     PIC X(10)     VALUE SPACE.            
          05  PV-LY-LAST-DAY-OF-WEEK      PIC X(10)     VALUE SPACE.            
          05  PV-PREV-ITEM-NBR            PIC 9(15)     VALUE ZERO.             
          05  PV-PREV-VS-ID               PIC S9(9)     VALUE +0  COMP.         
          05  PV-SQLCODE                  PIC +9(04).                           
          05  PV-NBR-OPN-STORES           PIC  9(04)    VALUE ZERO.             
          05  PV-NBR-OF-MKT-RECS          PIC  9(09)    VALUE ZERO.             
          05  PV-NBR-OF-INPUT-RECORDS     PIC  9(09)    VALUE ZERO.             
          05  PV-RECORDS-BYPASSED         PIC  9(09)    VALUE ZERO.             
          05  PV-TSKXREF-SELECTS-CTR      PIC  9(09)    VALUE ZERO.             
          05  PV-OPERATION-MSG            PIC X(60)     VALUE SPACES.           
          05  PV-CURS-CNT                 PIC 9(7)      VALUE ZERO.             
          05  PV-DATE-6-MONTHS.                                                 
              10  PV-DATE-6-MONTHS-CC        PIC 9(02).                         
              10  PV-DATE-6-MONTHS-YYMMDD    PIC 9(06).                         
          05  PV-DATE-6-MONTHS-RED REDEFINES PV-DATE-6-MONTHS                   
                                          PIC 9(8).                             
          05  PV-DATE-6-MONTHS-CHECK      PIC S9(07)V   COMP-3.                 
          05  PV-ABEND-CODE               PIC S9(04)    VALUE +0                
                                                        COMP SYNC.              
          05  PV-PARA-NAME                PIC X(35).                            
      *                                                                         
       01 PS-PROGRAM-SWITCHES.                                                  
          05  END-OF-DRIVER-FILE-SW    PIC X(1)    VALUE 'N'.                   
              88  PS-END-OF-DRIVER-FILE            VALUE 'Y'.                   
              88  PS-NOT-END-OF-CTL-FILE           VALUE 'N'.                   
          05  PS-END-OF-CTL-FILE-SW    PIC X(1)    VALUE 'N'.                   
              88  PS-END-OF-CTL-FILE               VALUE 'Y'.                   
          05  PS-DEPT-NBR-FOUND-SW     PIC X(1)    VALUE 'N'.                   
              88  PS-DEPT-NBR-FOUND                VALUE 'Y'.                   
              88  PS-DEPT-NBR-NOT-FOUND            VALUE 'N'.                   
          05  PS-NO-MORE-OPN-STORES-SW PIC X(1)    VALUE 'N'.                   
              88  PS-NO-MORE-OPN-STORES            VALUE 'Y'.                   
      *                                                                         
      ******************************************************************        
      * DB2 ERROR MESSAGES FORMAT AREA.                                         
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
           COPY DPWS500.                                                        
      *                                                                         
      ******************************************************************        
      * COPY MEMBER FOR THE INPUT FILE                                          
      ******************************************************************        
      *                                                                         
           COPY FSRD131.                                                        
      *                                                                         
      ******************************************************************        
      * COPY MEMBER FOR THE MARKET SOLUTIONS OUTPUT FILE                        
      ******************************************************************        
      *                                                                         
           COPY FSRD132.                                                        
      *                                                                         
      ******************************************************************        
      * TABLE OF OPEN STORES                                                    
      ******************************************************************        
       01  PT-OPN-STORE-TABLE.                                                  
           05  PT-OPN-STORE-ENRIES OCCURS 1500 TIMES                            
                                    ASCENDING KEY IS PT-LOC-NBR                 
                                    INDEXED BY OPN-TABLE-IDX.                   
             10  PT-LOC-NBR               PIC 9(04).                            
      ******************************************************************        
      * SQLCA AREA                                                              
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * TABLE DECLARATIONS FOR SKU XREF (TSKXREF)                               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      ******************************************************************        
      * TABLE DECLARATIONS FOR THE DATE ATTRIBUTE TABLE (TDTATTR)               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      * TABLE DECLARATIONS FOR MERCHANDISING LOCATION TABLE (XMT_LOC)           
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * CURSOR TO GET ALL OPEN DEPARTMENT STORES, EXCLUDING E-COMMERCE          
      * STORES                                                                  
      ******************************************************************        
           EXEC SQL                                                             
            DECLARE OPN_STORE_CSR CURSOR FOR                                    
             SELECT DISTINCT LOC_NBR                                            
               FROM XMT_LOC                                                     
              WHERE LOC_TYP_CDE  = 'DS'                                         
                AND E_BUS_IND   ^= 'Y'                                          
                AND OPN_DTE     <= :XMT00001-OPN-DTE                            
                AND (CLO_DTE     > :XMT00001-CLO-DTE                            
                 OR  CLO_DTE    IS NULL)                                        
           ORDER BY LOC_NBR                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *                                                                         
          EJECT                                                                 
                                                                                
       0000-MAINLINE-PROCESSING.                                                
      ******************************************************************        
      * MAIN PROCESSING ROUTINE                                                 
      ******************************************************************        
                                                                                
            PERFORM 1000-INIT.                                                  
                                                                                
            PERFORM 2000-PROCESS-OPN-STORES                                     
              UNTIL PS-END-OF-DRIVER-FILE.                                      
                                                                                
            PERFORM 9000-END-OF-JOB.                                            
                                                                                
            GOBACK.                                                             
                                                                                
      ******************************************************************        
      * OPEN FILES AND CURSOR, GET CURRENT DATE AND TIME FROM DB2.              
      ******************************************************************        
       1000-INIT.                                                               
                                                                                
           MOVE '*1000-INIT.              *' TO PV-PARA-NAME.                   
           OPEN INPUT  CONTROL-FILE                                             
                       DRIVER-FILE                                              
                OUTPUT MARKET-SOLUTIONS-FILE                                    
                       PRCSS-DTE-TY-FILE                                        
                       PRCSS-DTE-LY-FILE.                                       
                                                                                
           READ CONTROL-FILE                                                    
              AT END                                                            
                DISPLAY 'NO CONTROL PRESENT, NO PROCESSING DONE'                
                  UPON CONSOLE                                                  
                SET  PS-END-OF-CTL-FILE TO TRUE                                 
           END-READ.                                                            
                                                                                
           IF  PS-NOT-END-OF-CTL-FILE                                           
               MOVE CTL-DATE-IN      TO PV-DATE-IN                              
                                        PV-TY-LAST-DAY-OF-WEEK                  
               DISPLAY SPACES                                                   
               DISPLAY 'PROCESSING FOR THE WEEK ENDING '                        
                       PV-TY-LAST-DAY-OF-WEEK                                   
               DISPLAY SPACES                                                   
                                                                                
               PERFORM 9910-DECREMENT-CURRENT-DATE                              
               PERFORM 3000-GET-CURRENT-FSCL-WK                                 
               IF  DTATTR-DAY-NM NOT = 'SATURDAY'                               
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = FSKUT135'                                  
                   DISPLAY '** PARAGRAPH = 1000-INIT'                           
                   DISPLAY '** CONTROL CARD DATE NOT A SATURDAY DATE.'          
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               ELSE                                                             
                   PERFORM 3100-GET-LY-WEEK-END-DATE                            
               END-IF                                                           
               MOVE "'" TO PROCESS-DTE-TY-TICK1                                 
                           PROCESS-DTE-TY-TICK2                                 
                           PROCESS-DTE-LY-TICK1                                 
                           PROCESS-DTE-LY-TICK2                                 
               PERFORM 8110-WRITE-PRCSS-DTE-TY                                  
               PERFORM 8120-WRITE-PRCSS-DTE-LY                                  
                                                                                
      * INTIALIZE OPEN STORE TABLE                                              
               PERFORM VARYING OPN-TABLE-IDX FROM 1 BY 1                        
                 UNTIL OPN-TABLE-IDX > PC-MAX-OPN-STORES                        
                 MOVE  9999 TO PT-LOC-NBR (OPN-TABLE-IDX)                       
               END-PERFORM                                                      
               MOVE PV-TY-LAST-DAY-OF-WEEK TO XMT00001-OPN-DTE                  
                                              XMT00001-CLO-DTE                  
               PERFORM 4200-OPEN-OPN-STORE-CURS                                 
               PERFORM 4300-FETCH-OPN-STORE-CURS                                
               PERFORM VARYING OPN-TABLE-IDX FROM 1 BY 1                        
                 UNTIL OPN-TABLE-IDX > PC-MAX-OPN-STORES                        
                    OR PS-NO-MORE-OPN-STORES                                    
                 MOVE  XMT00001-LOC-NBR TO PT-LOC-NBR (OPN-TABLE-IDX)           
                 ADD 1 TO PV-NBR-OPN-STORES                                     
                 PERFORM 4300-FETCH-OPN-STORE-CURS                              
               END-PERFORM                                                      
               PERFORM 4400-CLOSE-OPN-STORE-CURS                                
               DISPLAY 'NBR OF OPEN STORES - ' PV-NBR-OPN-STORES                
               PERFORM 8000-READ-DRIVER-FILE                                    
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-FILE                                                   
                 PRCSS-DTE-TY-FILE                                              
                 PRCSS-DTE-LY-FILE.                                             
                                                                                
      ******************************************************************        
      * PROCESS OPEN STORES                                                     
      ******************************************************************        
       2000-PROCESS-OPN-STORES.                                                 
                                                                                
           ADD 1 TO PV-NBR-OF-INPUT-RECORDS.                                    
                                                                                
           SET OPN-TABLE-IDX TO 1.                                              
                                                                                
           SEARCH ALL PT-OPN-STORE-ENRIES                                       
             AT END                                                             
               SET OPN-TABLE-IDX TO 1                                           
             WHEN PT-LOC-NBR (OPN-TABLE-IDX) = FS131-LOC-NBR.                   
                                                                                
      * MUST BE AN OPEN STORE AND ONHAND QTY > 0                                
           IF  FS131-LOC-NBR = PT-LOC-NBR (OPN-TABLE-IDX)                       
           AND FS131-ONHAND-QTY > 0                                             
                                                                                
      * MUST HAVE A SALE OR RECEIPT IN THE LAST 6 MONTHS                        
               IF  FS131-LAST-SALE-DATE > PV-DATE-6-MONTHS-CHECK                
               OR  FS131-LAST-RCVD-DATE > PV-DATE-6-MONTHS-CHECK                
                   MOVE PV-TY-FIRST-DAY-OF-WEEK                                 
                                             TO FS132-WEEK-BEG-DTE-TY           
                   MOVE PV-TY-LAST-DAY-OF-WEEK                                  
                                             TO FS132-WEEK-END-DTE-TY           
                   MOVE PV-LY-FIRST-DAY-OF-WEEK                                 
                                             TO FS132-WEEK-BEG-DTE-LY           
                   MOVE PV-LY-LAST-DAY-OF-WEEK                                  
                                             TO FS132-WEEK-END-DTE-LY           
                   MOVE FS131-LOC-NBR        TO FS132-LOC-NBR                   
                   MOVE FS131-VS-ID          TO FS132-VS-ID                     
                   IF  PV-PREV-ITEM-NBR NOT = FS131-ITEM-NMBR                   
                       MOVE FS131-ITEM-NMBR  TO SKXREF-ITM-NBR                  
                                                PV-PREV-ITEM-NBR                
                       PERFORM 5000-SELECT-DEPT-NBR                             
                       MOVE SKXREF-DEPT      TO FS132-DEPT-NBR                  
                   END-IF                                                       
                   IF  PS-DEPT-NBR-FOUND                                        
                       PERFORM 8100-WRITE-OUT-FILE                              
                   END-IF                                                       
               ELSE                                                             
                   ADD 1 TO PV-RECORDS-BYPASSED                                 
           ELSE                                                                 
               ADD 1 TO PV-RECORDS-BYPASSED                                     
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-DRIVER-FILE.                                       
                                                                                
      ******************************************************************        
      * GET THE CURRENT FISCAL WEEK                                             
      ******************************************************************        
       3000-GET-CURRENT-FSCL-WK.                                                
                                                                                
           MOVE PV-TY-LAST-DAY-OF-WEEK TO DTATTR-KY-DTE.                        
                                                                                
           EXEC SQL                                                             
             SELECT FSCL_YR_NBR                                                 
                  , FSCL_WK_NBR                                                 
                  , DAY_NM                                                      
               INTO :DTATTR-FSCL-YR-NBR                                         
                  , :DTATTR-FSCL-WK-NBR                                         
                  , :DTATTR-DAY-NM                                              
               FROM TDTATTR                                                     
              WHERE KY_DTE = :DTATTR-KY-DTE                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               MOVE PV-TY-LAST-DAY-OF-WEEK TO PROCESS-DTE-TY                    
               MOVE SPACES                 TO PROCESS-DTE-TY-FILLER             
               MOVE DTATTR-FSCL-YR-NBR     TO PV-FSCL-YR-NBR                    
               MOVE DTATTR-FSCL-WK-NBR     TO PV-FSCL-WK-NBR                    
               PERFORM 3010-GET-FIRST-DAY-TY                                    
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************************'          
               DISPLAY 'PARAGRAPH     : 3000-GET-CURRENT-FSCL-WK     '          
               DISPLAY 'ERROR GETTING FISCAL WK FOR PROCESSING DATE: '          
                        PV-TY-LAST-DAY-OF-WEEK                                  
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************************'          
               DISPLAY '                                 '                      
                                                                                
               MOVE '3000-GET-CURRENT-FSCL-WK' TO PV-PARA-NAME                  
               MOVE   'ERROR GETTING CURRENT FISCAL WEEK' TO                    
                      PV-OPERATION-MSG                                          
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * GET THE FIRST DAY OF THE WEEK FOR 'THIS YEAR' PROCESSING WEEK           
      ******************************************************************        
       3010-GET-FIRST-DAY-TY.                                                   
                                                                                
           MOVE PV-TY-LAST-DAY-OF-WEEK TO DTATTR-KY-DTE.                        
                                                                                
           EXEC SQL                                                             
             SELECT KY_DTE                                                      
               INTO :DTATTR-KY-DTE                                              
               FROM TDTATTR                                                     
              WHERE FSCL_YR_NBR = :DTATTR-FSCL-YR-NBR                           
                AND FSCL_WK_NBR = :DTATTR-FSCL-WK-NBR                           
                AND DAY_NM      = :PC-SUNDAY                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               MOVE DTATTR-KY-DTE TO PV-TY-FIRST-DAY-OF-WEEK                    
               DISPLAY 'TY BEG-OF-WEEK DATE   = '                               
                                              PV-TY-FIRST-DAY-OF-WEEK           
               DISPLAY 'TY END-OF-WEEK DATE   = '                               
                                              PV-TY-LAST-DAY-OF-WEEK            
               DISPLAY 'TY FISCAL WEEK        = ' DTATTR-FSCL-WK-NBR            
               DISPLAY 'TY FISCAL YEAR        = ' DTATTR-FSCL-YR-NBR            
               DISPLAY SPACES                                                   
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************************'          
               DISPLAY 'PARAGRAPH     : 3010-GET-FIRST-DAY-TY        '          
               DISPLAY 'ERROR GETTING FIRST DAY OF TY FISCAL WEEK'              
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************************'          
               DISPLAY '                                 '                      
                                                                                
               MOVE '3010-GET-FIRST-DAY-TY' TO PV-PARA-NAME                     
               MOVE   'ERROR GETTING FIRST DAY OF TY FISCAL WEEK' TO            
                      PV-OPERATION-MSG                                          
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * GET THE SATURDAY DATE FOR THE CORRESPONDING FISCAL WEEK LAST YR         
      ******************************************************************        
       3100-GET-LY-WEEK-END-DATE.                                               
                                                                                
           COMPUTE PV-FSCL-YR-NBR-LY = PV-FSCL-YR-NBR - 1.                      
           MOVE PV-FSCL-YR-NBR-LY TO DTATTR-FSCL-YR-NBR.                        
                                                                                
           IF  PV-FSCL-WK-NBR = 53                                              
               COMPUTE PV-FSCL-WK-NBR-LY = PV-FSCL-WK-NBR - 1                   
               MOVE PV-FSCL-WK-NBR-LY TO DTATTR-FSCL-WK-NBR                     
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
             SELECT KY_DTE                                                      
               INTO :DTATTR-KY-DTE                                              
               FROM TDTATTR                                                     
              WHERE FSCL_YR_NBR = :DTATTR-FSCL-YR-NBR                           
                AND FSCL_WK_NBR = :DTATTR-FSCL-WK-NBR                           
                AND DAY_NM      = :PC-SATURDAY                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               MOVE DTATTR-KY-DTE TO PV-LY-LAST-DAY-OF-WEEK                     
               MOVE PV-LY-LAST-DAY-OF-WEEK TO PROCESS-DTE-LY                    
               MOVE SPACES                 TO PROCESS-DTE-LY-FILLER             
               PERFORM 3110-GET-LY-WEEK-BEG-DATE                                
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************************'          
               DISPLAY 'PARAGRAPH     : 3100-GET-LY-WEEK-END-DATE    '          
               DISPLAY 'ERROR GETTING LAST YEAR DATE'                           
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************************'          
               DISPLAY '                                 '                      
                                                                                
               MOVE '3100-GET-LY-WEEK-END-DATE' TO PV-PARA-NAME                 
               MOVE   'ERROR GETTING LAST YEAR DATE' TO                         
                      PV-OPERATION-MSG                                          
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * GET THE SUNDAY DATE FOR THE CORRESPONDING FISCAL WEEK LAST YR           
      ******************************************************************        
       3110-GET-LY-WEEK-BEG-DATE.                                               
                                                                                
           EXEC SQL                                                             
             SELECT KY_DTE                                                      
               INTO :DTATTR-KY-DTE                                              
               FROM TDTATTR                                                     
              WHERE FSCL_YR_NBR = :DTATTR-FSCL-YR-NBR                           
                AND FSCL_WK_NBR = :DTATTR-FSCL-WK-NBR                           
                AND DAY_NM      = :PC-SUNDAY                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               MOVE DTATTR-KY-DTE TO PV-LY-FIRST-DAY-OF-WEEK                    
               DISPLAY 'LY BEG-OF-WEEK DATE   = '                               
                                              PV-LY-FIRST-DAY-OF-WEEK           
               DISPLAY 'LY END-OF-WEEK DATE   = '                               
                                              PV-LY-LAST-DAY-OF-WEEK            
               DISPLAY 'LY FISCAL WEEK        = ' DTATTR-FSCL-WK-NBR            
               DISPLAY 'LY FISCAL YEAR        = ' DTATTR-FSCL-YR-NBR            
               DISPLAY SPACES                                                   
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************************'          
               DISPLAY 'PARAGRAPH     : 3110-GET-LY-WEEK-BEG-DATE    '          
               DISPLAY 'ERROR GETTING LAST YEAR DATE'                           
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************************'          
               DISPLAY '                                 '                      
                                                                                
               MOVE '3110-GET-LY-WEEK-BEG-DATE' TO PV-PARA-NAME                 
               MOVE   'ERROR GETTING LAST YEAR DATE' TO                         
                      PV-OPERATION-MSG                                          
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * OPEN OPN-STORE-CURS CURSOR FOR PROCESSING                               
      ******************************************************************        
       4200-OPEN-OPN-STORE-CURS.                                                
                                                                                
           EXEC SQL                                                             
               OPEN OPN_STORE_CSR                                               
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE NOT = +0                                                 
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************'                      
               DISPLAY 'PARAGRAPH     : 4200-OPEN-OPN-STORE-CURS'               
               DISPLAY 'ERROR OPENING OPN_STORE_CSR CURSOR:   '                 
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************'                      
               DISPLAY '                                 '                      
                                                                                
               STRING 'ERROR OPENING OPN_STORE_CSR CURSOR SQLCODE= '            
                      PV-SQLCODE DELIMITED BY SIZE INTO                         
                      PV-OPERATION-MSG                                          
               END-STRING                                                       
                                                                                
               MOVE '4200-OPEN-OPN-STORE-CURS' TO PV-PARA-NAME                  
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FETCH OPN_STORE_CSR FOR PROCESSING                                      
      ******************************************************************        
       4300-FETCH-OPN-STORE-CURS.                                               
                                                                                
           EXEC SQL                                                             
               FETCH OPN_STORE_CSR                                              
               INTO :XMT00001-LOC-NBR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               ADD 1 TO PV-CURS-CNT                                             
             WHEN SQLCODE = +100                                                
               SET PS-NO-MORE-OPN-STORES TO TRUE                                
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************'                      
               DISPLAY 'PARAGRAPH     : 4300-FETCH-OPN-STORE-CURS'              
               DISPLAY 'ERROR FETCHING OPN_STORE_CSR CURSOR:   '                
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************'                      
               DISPLAY '                                 '                      
                                                                                
               STRING 'ERROR FETCHING OPN_STORE_CSR CURSOR SQLCODE= '           
                      PV-SQLCODE DELIMITED BY SIZE INTO                         
                      PV-OPERATION-MSG                                          
               END-STRING                                                       
                                                                                
               MOVE '4300-FETCH-OPN-STORE-CURS' TO PV-PARA-NAME                 
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * CLOSE OPN-STORE-CURS CURSOR                                             
      ******************************************************************        
       4400-CLOSE-OPN-STORE-CURS.                                               
                                                                                
           EXEC SQL                                                             
               CLOSE OPN_STORE_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN OTHER                                                         
               DISPLAY '                                 '                      
               DISPLAY '*********************************'                      
               DISPLAY 'PARAGRAPH     : 4400-CLOSE-OPN-STORE-CURS'              
               DISPLAY 'ERROR CLOSING OPN-STORE-CURS CURSOR:   '                
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************'                      
               DISPLAY '                                 '                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * SELECT DEPT NUMBER.                                                     
      ******************************************************************        
       5000-SELECT-DEPT-NBR.                                                    
                                                                                
           EXEC SQL                                                             
             SELECT DEPT                                                        
               INTO :SKXREF-DEPT                                                
               FROM TSKXREF                                                     
              WHERE ITM_NBR   = :SKXREF-ITM-NBR                                 
              FETCH FIRST ROW ONLY                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               ADD +1 TO PV-TSKXREF-SELECTS-CTR                                 
               SET PS-DEPT-NBR-FOUND TO TRUE                                    
             WHEN SQLCODE = +100                                                
               SET PS-DEPT-NBR-NOT-FOUND TO TRUE                                
               DISPLAY 'DEPT NOT FOUND FOR ITEM- ' SKXREF-ITM-NBR               
             WHEN OTHER                                                         
               MOVE SQLCODE TO PV-SQLCODE                                       
                                                                                
               DISPLAY '                                 '                      
               DISPLAY '*********************************'                      
               DISPLAY 'PARAGRAPH     : 5000-SELECT-DEPT-NBR'                   
               DISPLAY 'ERROR SELECTING DEPT NBR:      '                        
               DISPLAY 'SQLCODE       : ' PV-SQLCODE                            
               DISPLAY '*********************************'                      
               DISPLAY '                                 '                      
                                                                                
               STRING 'ERROR SELECTING STYLE NBR SQLCODE= '                     
                      PV-SQLCODE DELIMITED BY SIZE INTO                         
                      PV-OPERATION-MSG                                          
               END-STRING                                                       
                                                                                
               MOVE '5000-SELECT-DEPT-NBR' TO PV-PARA-NAME                      
                                                                                
               PERFORM 9999-SQL-ERROR                                           
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  READ INPUT DRIVER FILE                                                 
      ******************************************************************        
       8000-READ-DRIVER-FILE.                                                   
                                                                                
           READ DRIVER-FILE INTO FS131-FS-MERGED-RECORD                         
             AT END                                                             
               SET PS-END-OF-DRIVER-FILE TO TRUE.                               
                                                                                
      ******************************************************************        
      *  WRITE OUTPUT FILE                                                      
      ******************************************************************        
       8100-WRITE-OUT-FILE.                                                     
                                                                                
           ADD 1 TO PV-NBR-OF-MKT-RECS.                                         
                                                                                
           WRITE MARKET-SOLUTIONS-REC FROM                                      
                 FS132-FS-MRKT-SOL-REC.                                         
                                                                                
      ******************************************************************        
      *  WRITE OUTPUT PRCSS-DTE-TY-FILE                                         
      ******************************************************************        
       8110-WRITE-PRCSS-DTE-TY.                                                 
                                                                                
           WRITE PROCESS-DATE-TY-REC.                                           
                                                                                
      ******************************************************************        
      *  WRITE OUTPUT PRCSS-DTE-LY-FILE                                         
      ******************************************************************        
       8120-WRITE-PRCSS-DTE-LY.                                                 
                                                                                
           WRITE PROCESS-DATE-LY-REC.                                           
                                                                                
      ******************************************************************        
      *  CLOSE FILES, DISPLAY COUNTERS                                          
      ******************************************************************        
       9000-END-OF-JOB.                                                         
           DISPLAY '*******************************************'.               
           DISPLAY 'PROGRAM ' PC-PROGRAM-NAME ' STATS:'.                        
           DISPLAY 'NBR OF INPUT RECS READ -          '                         
              PV-NBR-OF-INPUT-RECORDS.                                          
           DISPLAY 'NBR OF RECORDS BYPASSED -         '                         
              PV-RECORDS-BYPASSED.                                              
           DISPLAY 'NBR OF SELECTS ON TSKXREF -       '                         
              PV-TSKXREF-SELECTS-CTR.                                           
           DISPLAY 'NBR OF MKT SOLUTION RECS WRITTEN- '                         
                   PV-NBR-OF-MKT-RECS.                                          
           DISPLAY '*******************************************'.               
           CLOSE DRIVER-FILE                                                    
                 MARKET-SOLUTIONS-FILE.                                         
                                                                                
      ******************************************************************        
      * 9910-DECREMENT-CURRENT-DATE                                             
      ******************************************************************        
       9910-DECREMENT-CURRENT-DATE.                                             
                                                                                
           MOVE '9910-DECREMENT-CURRENT-DATE'                                   
                                       TO PV-PARA-NAME.                         
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE PV-DATE-IN (1:2)      TO DPG52-DB2-ISO-DATE-YR-X(1:2).          
           MOVE PV-DATE-IN (3:2)      TO DPG52-DB2-ISO-DATE-YR-X(3:2).          
           MOVE PV-DATE-IN (6:2)      TO DPG52-DB2-ISO-DATE-MO-X.               
           MOVE PV-DATE-IN (9:2)      TO DPG52-DB2-ISO-DATE-DY-X.               
                                                                                
      * GET CURRENT DATE MINUS SIX MONTHS.                                      
           SET DPG51-VALID-FISCAL-CALENDAR                                      
                                       TO TRUE.                                 
           SET DPG51-DECREMENT-DATE                                             
                                       TO TRUE.                                 
           SET DPG52-LK-DTE-ISO        TO TRUE.                                 
           MOVE 182 TO DPG51-INCR-DECR-DAYS-9.                                  
           CALL PC-DPKUT500 USING DPG51 DPG52                                   
                                  DPG53 DPG54                                   
                                  DPG55 DPG56.                                  
                                                                                
           IF DPG54-INCR-DECR-VALID                                             
             MOVE DPG55-DB2-ISO-DATE   TO PV-DATE-6-MONTHS                      
             MOVE PV-DATE-6-MONTHS-RED TO PV-DATE-6-MONTHS-CHECK                
           ELSE                                                                 
               DISPLAY 'BAD DATE DECREMENT1'                                    
               SET ABEND-4013-DB2-ERROR   TO TRUE                               
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * ABEND ROUTINE FOR BAD SQL CALLS                                         
      ******************************************************************        
       9999-SQL-ERROR.                                                          
           DISPLAY '** ABEND **          = SQL ERROR'.                          
           DISPLAY '** PROGRAM **        = FSKUT135'                            
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME                       
      *                                                                         
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
            COPY DPPD004.                                                       
      *                                                                         
           MOVE +4013                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
