000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'PREPARE CHECKPOINT RESTART EXTRACT FOR IN-TRANSIT PURGE'.         
000300                                                                          
000400 PROGRAM-ID.         PCKUT069.                                            
000500 AUTHOR.             DENNIS STEFFEN.                                      
000600 INSTALLATION.       KOHLS DEPARTMENT STORES.                             
000700 DATE-WRITTEN.       JULY 2006.                                           
000800 DATE-COMPILED.                                                           
000900************************************************************              
001000* THIS PROGRAM PREPS THE INTRANSIT FILE SO THAT IT CAN USE                
001100* CHECKPOINT/RESTART PROCESSING FUNCTIONS WHEN UPDATING THE               
001200* INTRANSIT TABLES IN PROGRAM PCKUP014.                                   
001300************************************************************              
001400 ENVIRONMENT DIVISION.                                                    
001500 CONFIGURATION SECTION.                                                   
001600                                                                          
001700 SOURCE-COMPUTER.    IBM-3090.                                            
001800 OBJECT-COMPUTER.    IBM-3090.                                            
001900                                                                          
002000 INPUT-OUTPUT SECTION.                                                    
002100 FILE-CONTROL.                                                            
002200                                                                          
002300     SELECT IN-TRANSIT-FILE              ASSIGN TO INP01.                 
002400                                                                          
002500 DATA DIVISION.                                                           
002600 FILE SECTION.                                                            
002700                                                                          
002800 FD  IN-TRANSIT-FILE                                                      
002900     LABEL RECORDS ARE STANDARD                                           
003000     RECORDING MODE IS F                                                  
003100     BLOCK CONTAINS 0 RECORDS                                             
003200     RECORD CONTAINS 026 CHARACTERS                                       
003300     DATA RECORD IS IN-TRANSIT-RECORD.                                    
003400 01  IN-TRANSIT-RECORD    PIC  X(026).                                    
003500                                                                          
003600*                                                                         
003700 WORKING-STORAGE SECTION.                                                 
003800                                                                          
003900 01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.           
004000                                                                          
004100 01  PROGRAM-CONSTANTS.                                                   
004200     05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'PCKUT069'.         
004300     05  PC-TRAN-PREP-FUNC-CODES.                                         
004400         10  PC-TRAN-PREP-FUNC-OPEN                                       
004500                                 PIC X(05)      VALUE 'OPEN '.            
004600         10  PC-TRAN-PREP-FUNC-WRITE                                      
004700                                 PIC X(05)      VALUE 'WRITE'.            
004800         10  PC-TRAN-PREP-FUNC-CLOSE                                      
004900                                 PIC X(05)      VALUE 'CLOSE'.            
005000     05  PC-TRANS-RECORD-LENGTH  PIC S9(04)     VALUE +100 COMP.          
005100     05  PC-JOB-NAME             PIC X(08)      VALUE 'PCK115  '.         
005200     05  PC-PLAN-NAME            PIC X(08)      VALUE 'PCKUP014'.         
005300                                                                          
005400 01  PROGRAM-VARIABLES.                                                   
005500     05  PV-CHECKPOINT-TYPE      PIC X(01)      VALUE SPACE.              
005600         88  CONTROL-BREAK                      VALUE 'C'.                
005700         88  LOGICAL-TRANSACTION                VALUE 'L'.                
005800         88  TIME-INTERVAL                      VALUE 'T'.                
005900         88  REQUESTED-BY-PROGRAM               VALUE 'R'.                
006000     05  PS-TRANS-EOF-SW     PIC X(01)          VALUE 'N'.                
006100         88  PS-TRANS-EOF                       VALUE 'Y'.                
006200     05  PV-SKIP-CLOSE-SW        PIC X(01)      VALUE 'N'.                
006300         88  SKIP-CLOSE                         VALUE 'Y'.                
006400     05  PV-CURRENT-DATE         PIC X(10).                               
006500                                                                          
006600*     IN-TRANSIT PURGE RECORD                                             
006700     COPY PCRD033.                                                        
006800*                                                                         
006900*     TRANSACTION PREPARATION MODULE LINKAGE SECTION.                     
007000*                                                                         
260107     COPY DPWS990.                                                        
007200                                                                          
007100     COPY DPLS000.                                                        
007200                                                                          
007300     EXEC SQL                                                             
007400         INCLUDE SQLCA                                                    
007500     END-EXEC.                                                            
007600     COPY SQLCA2.                                                         
007700                                                                          
007800 PROCEDURE DIVISION.                                                      
007900******************************************************************        
008000* 0000-MAINLINE-PROCESSING                                       *        
008100*      CONTROLS FLOW OF THE PROGRAM.                             *        
008200******************************************************************        
008300 0000-MAINLINE-PROCESSING.                                                
008400                                                                          
008500     PERFORM 1000-INITIALIZE.                                             
008600     PERFORM 2000-PROCESS UNTIL PS-TRANS-EOF                              
008700     PERFORM 9000-EOJ.                                                    
008800                                                                          
008900     GOBACK.                                                              
009000                                                                          
009100 1000-INITIALIZE.                                                         
009200                                                                          
009300     OPEN INPUT IN-TRANSIT-FILE.                                          
009400                                                                          
009500     ACCEPT PC-JOB-NAME FROM SYSIN.                                       
009600                                                                          
009700     EXEC SQL                                                             
009800          SET :PV-CURRENT-DATE = CURRENT DATE                             
009900     END-EXEC.                                                            
010000                                                                          
010100     PERFORM 8000-READ-INPUT.                                             
010200                                                                          
010300     IF PS-TRANS-EOF                                                      
010400        SET SKIP-CLOSE                TO TRUE                             
010500        MOVE +1                       TO RETURN-CODE                      
010600        DISPLAY ' PCKUT069 - TRANSACTION FILE IS EMPTY '                  
010700        DISPLAY ' PCKUT069 - PCKUP014 WILL BE SKIPPED  '                  
010800     ELSE                                                                 
010900        PERFORM 1100-ISSUE-OPEN-REQUEST                                   
011000     END-IF.                                                              
011100                                                                          
011200 1100-ISSUE-OPEN-REQUEST.                                                 
011300                                                                          
011400     MOVE PC-TRAN-PREP-FUNC-OPEN      TO DP000-FUNC-CODE.                 
011500     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
011600     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
011700     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
011800     MOVE DP000-CKPT-TYPE-CODE        TO PV-CHECKPOINT-TYPE.              
011900                                                                          
012000 2000-PROCESS.                                                            
012100     PERFORM 2100-MOVE-TRANS-DATA                                         
012200                                                                          
012300     PERFORM 8000-READ-INPUT.                                             
012400                                                                          
012500 2100-MOVE-TRANS-DATA.                                                    
012600                                                                          
012700     MOVE LENGTH OF PC033-INTRANSIT-DTL-PURGE-REC                         
012800                                      TO DP000-TRANS-REC-LENGTH.          
012900                                                                          
013000     MOVE PC033-INTRANSIT-DTL-PURGE-REC TO DP000-TRANS-REC.               
013100                                                                          
013200     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
013300     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
013400     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
013500     IF CONTROL-BREAK OR LOGICAL-TRANSACTION                              
013600         MOVE 'Y'                     TO DP000-CHECKPOINT-IND             
013700     ELSE                                                                 
013800         MOVE 'N'                     TO DP000-CHECKPOINT-IND             
013900     END-IF.                                                              
014000                                                                          
014100     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
014200                                                                          
014300 8000-READ-INPUT.                                                         
014400                                                                          
014500     READ IN-TRANSIT-FILE INTO PC033-INTRANSIT-DTL-PURGE-REC              
014600         AT END                                                           
014700           SET PS-TRANS-EOF           TO TRUE                             
014800     END-READ.                                                            
014900                                                                          
015000 9000-EOJ.                                                                
015100     CLOSE IN-TRANSIT-FILE.                                               
015200                                                                          
015300     IF SKIP-CLOSE                                                        
015400        NEXT SENTENCE                                                     
015500     ELSE                                                                 
015600        PERFORM 9100-ISSUE-CLOSE-REQUEST                                  
015700     END-IF.                                                              
015800                                                                          
015900 9100-ISSUE-CLOSE-REQUEST.                                                
016000                                                                          
016100     MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
016200     MOVE PC-JOB-NAME TO DP000-JOB-NAME.                                  
016300     MOVE PC-PLAN-NAME TO DP000-PLAN-NAME.                                
016400                                                                          
016500     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
016600                                                                          
016700     COPY DPPD000.                                                        
016800*                                                                         
016900*     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
017000*                                                                         
