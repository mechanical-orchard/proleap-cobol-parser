       IDENTIFICATION DIVISION.                                         00010014
       PROGRAM-ID.    INKUT800.                                         00020014
       AUTHOR.        PAUL OMAN.                                        00030014
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040014
       DATE-WRITTEN.  JULY 2004.                                        00050014
       DATE-COMPILED.                                                   00060014
                                                                        00070014
      ****************************************************************  00080014
      *    NOTE: THE 2ND TYPE OF ACCESS WILL BE PUT IN SOON.         *  00140014
      *          YOU WILL NOTICE SOME COMMENTS INDICATING THIS.      *  00140014
      *                                                              *  00140014
      ****************************************************************  00080014
      *    BATCH VERSION FOR INVENTORY INFORMATION ACCESS.           *  00140014
      *          ONLINE VERSION IS INKCS800.                         *  00140014
      *                                                              *  00140014
      *    PROGRAM IS USED TO ASSESS WHETHER A STORE IS IN THE       *  00090014
      *        INVENTORY CYCLE. CALLING PROGRAMS ARE REQUIRED TO     *  00100014
      *        PASS IN A STORE NUMBER AND A VALID DATE.  THIS        *  00110014
      *        PROGRAM PASSES BACK A SWITCH ON WHETHER THE STORE     *  00120014
      *        IS IN THE INVENTORY CYLCE ON THAT DATE OR NOT.        *  00130014
      *                                                              *  00140014
      *    IT IS ASSUMED THAT YOU ARE PASSING IN A VALID DATE AND    *  00150014
      *       A VALID LOCATION.  NO EDITING IS DONE IN THIS PROGRAM  *  00160014
      *       FOR EITHER OF THESE.                                   *  00170014
      *                                                              *  00180014
      *    THE FIRST THING YOU SHOULD CHECK IS IN800-RETURN-CODE.    *  00190014
      *        IF IT IS SET TO IN800-SUCCESSFUL, THEN CONTINUE       *  00200014
      *        BY CHECKING THE IN800-INVENTORY-SW.                   *  00210014
      *                                                              *  00220014
      *        ELSE CHECK IF IT IS SET TO IN800-SQL-ERROR. IF SO     *  00230014
      *             THE IN800-SQLCODE, SQLCA, AND ERROR-PARAGRAPH    *  00240014
      *             FIELDS ARE FILLED WHEN THIS IS THE CASE.         *  00250014
      *             YOU NEED TO TERMINATE APPROPRIATELY.             *  00260014
      *                                                              *  00270014
      *    INWS800  IS THE COPYBOOK USED TO PASS DATA.               *  00280014
      ****************************************************************  00290014
012505**DATE:01/25/2005 CHANGED BY: RAY GRAF                                    
012505**     SQL CHANGE IN DETERMINING WHETHER OR NOT A STORE IS IN             
012505**     PHYSICAL INVENTORY.                                                
      *****************************************************************         
      *  W33304  05-01-2008  HOLDING INVENTORY LEDGER OPEN ACROSS               
      *                      PERIODS INVENTORY SHORTAGE REPORTING               
      *                      -JILL LIN                                          
      *****************************************************************         
                                                                        00300014
       ENVIRONMENT DIVISION.                                            00310014
       CONFIGURATION SECTION.                                           00320014
       SOURCE-COMPUTER. IBM-3090.                                       00330014
       OBJECT-COMPUTER. IBM-3090.                                       00340014
                                                                        00350014
       DATA DIVISION.                                                   00360014
       WORKING-STORAGE SECTION.                                         00370014
       01  PV-STUFF.                                                    00380014
           05  PV-LOCATION-NBR              PIC S9(4) VALUE 0 COMP.     00390014
           05  PV-CURRENT-DATE              PIC X(10) VALUE SPACES.     00400014
                                                                        00410014
       01  DK-KEYS.                                                     00420014
           05  DK-SUB-CL-NBR                PIC X(03).                  00430014
                                                                        00440014
       01  PS-SWITCHES.                                                 00450014
           05  PS-DISPLAY-PARA-SW          PIC X(01)  VALUE 'Y'.        00460014
               88  PS-DISPLAY-PARA-YES                VALUE 'Y'.        00470014
               88  PS-DISPLAY-PARA-NO                 VALUE 'N'.        00480014
                                                                        00490014
       01  PC-CONSTANTS.                                                00500014
           05  PC-IT                    PIC X(02)  VALUE 'IT'.          00510014
           05  PC-SE                    PIC X(02)  VALUE 'SE'.          00520014
           05  PC-FIVE-BLANKS           PIC X(05)  VALUE SPACES.        00530014
           05  PC-INVENTORY-IN-PROGRESS PIC X(10)  VALUE '9999-09-09'.  00540014
                                                                        00560014
      ***  TINVPAR                                                      00570014
           EXEC SQL                                                     00580014
               INCLUDE TINVPAR                                          00590014
           END-EXEC.                                                    00600014
                                                                        00610014
           EXEC SQL                                                     00620014
                INCLUDE SQLCA                                           00630014
           END-EXEC.                                                    00640014
                                                                        00650014
           COPY INWS800.                                                00660014
                                                                        00670014
       LINKAGE SECTION.                                                 00680014
                                                                        00690014
       01  DFHCOMMAREA                 PIC X(350).                      00700014
                                                                        00710014
       PROCEDURE DIVISION USING DFHCOMMAREA.                            00720014
                                                                        00730014
           INITIALIZE IN800-INVENTORY-COMMAREA.                         00740014
           MOVE DFHCOMMAREA TO IN800-INVENTORY-COMMAREA.                00750014
                                                                        00760014
           SET IN800-SUCCESSFUL        TO TRUE.                         00770014
           INITIALIZE DCLTINVPAR.                                       00780014
                                                                        00790014
           PERFORM 1000-VALIDATE-INPUT.                                 00800014
                                                                        00810014
           IF IN800-SUCCESSFUL                                          00820014
              PERFORM 2000-PROCESS-REQUEST                                      
           END-IF.                                                      00850014
                                                                        00860014
           MOVE IN800-INVENTORY-COMMAREA TO DFHCOMMAREA.                00870014
                                                                        00880014
           GOBACK.                                                      00890014
                                                                        00900014
      ****************************************************************  00910014
      *    CHECK THAT THE APPROPRIATE KEY FIELDS HAVE BEEN FILLED IN *  00920014
      *    FOR THE ACCESS METHOD SELECTED.                           *  00930014
      *                                                              *  00940014
      ****************************************************************  00950014
       1000-VALIDATE-INPUT.                                             00960014
                                                                        00970014
           EVALUATE TRUE                                                00980014
               WHEN IN800-CHECK-INVENTORY-DATE                          00990014
                    EVALUATE TRUE                                       01000014
                        WHEN IN800-LOC-NBR = 0                          01010014
                             SET IN800-LOCATION-ZERO TO TRUE            01020014
                                                                        01030014
                        WHEN IN800-COUNT-DATE = SPACES                  01040014
                             SET IN800-DATE-SPACES TO TRUE              01050014
                                                                        01030014
                        WHEN OTHER                                            14
                             CONTINUE                                           
                                                                        01030014
                    END-EVALUATE                                        01070014
                                                                              14
      **??    TEMPORARY COMMENT OUT UNTIL FINAL VERSION IS PUT IN.            14
      **       WHEN IN800-PASS-INVENTORY-DATA                                 14
      **            EVALUATE TRUE                                       01000014
      **                WHEN IN800-LOC-NBR = 0                          01010014
      **                     SET IN800-LOCATION-ZERO TO TRUE            01020014
      **                                                                01030014
      **                WHEN OTHER                                            14
      **                     CONTINUE                                           
      **                                                                01030014
      **            END-EVALUATE                                        01070014
                                                                        01080014
               WHEN OTHER                                               01090014
                    SET IN800-INVALID-REQUEST TO TRUE                   01100014
                                                                        01110014
           END-EVALUATE.                                                01120014
                                                                        01130014
                                                                        00900014
      ****************************************************************  00910014
      *                                                              *  00940014
      *                                                              *  00940014
      ****************************************************************  00950014
       2000-PROCESS-REQUEST.                                            00960014
                                                                        00970014
           EVALUATE TRUE                                                00980014
               WHEN IN800-CHECK-INVENTORY-DATE                          00990014
                    MOVE IN800-LOC-NBR TO PV-LOCATION-NBR                     00
                    PERFORM 4000-SELECT-TINVPAR                               00
                                                                        01060014
      **??    TEMPORARY COMMENT OUT UNTIL FINAL VERSION IS PUT IN.            14
      **??     WHEN IN800-PASS-INVENTORY-DATA                                 14
      **            MOVE IN800-LOC-NBR TO PV-LOCATION-NBR                     00
      **            PERFORM 5000-SELECT-INVENTORY-DATA                        00
      **            IF IN800-INVENTORY-DATA-FOUND                               
      **               PERFORM 5200-FORMAT-INVENTORY-DATA                     00
      **            END-IF                                                      
                                                                        01080014
               WHEN OTHER                                               01090014
                    SET IN800-INVALID-REQUEST TO TRUE                   01100014
                                                                        01110014
           END-EVALUATE.                                                01120014
                                                                        01110014
      ****************************************************************  01140014
      *    CHECK TO SEE IF THE LOCATION IS DOING AN INVENTORY.       *  01150014
      *                                                              *  01170014
      *    PLND-INV-DTE - IF THIS IS EQUAL TO THE DATE PASSED IN     *  01180014
      *                   THE STORE IS DOING INVENTORY.              *  01190014
      *                                                              *  01170014
      *    ACTL-FIN-BK-DTE = PC-INVENTORY-IN-PROGRESS 9999-09-09     *  01180014
      *                      MEANS INVENTORY IS IN PROGRESS IF       *  01190014
W33304*                      SCHD-PHY-CNT-CDE = 'PI' OR 'CL' ALSO.   *  01170014
      *                                                              *  01170014
W33304*    SCHD-PHY-CNT-CDE = 'PI' MEANS STORE IS OPEN WHILE COUNT   *  01200014
      *                           IS DONE.                           *  01210014
W33304*                       'CL' MEANS STORE IS CLOSED WHILE COUNT *  01220014
      *                           IS DONE.                           *  01230014
      *                                                              *  01170014
      *    NOTE: THE ACTL-FIN-BK-DTE CAN EQUAL 9999-09-09 AND        *  01170014
W33304*          SCHD-PHY-CNT-CDE = 'EI' FOR WHEN THEY ARE DOING     *  01170014
      *          AN ESTIMATED INVENTORY.                             *  01170014
      *                                                              *  01170014
      *                                                              *  01170014
      *                                                              *  01170014
      ****************************************************************  01240014
                                                                        01250014
       4000-SELECT-TINVPAR.                                             01260014
                                                                        01270014
           EXEC SQL                                                     01280014
              SELECT   PLND_INV_DTE                                     01290014
                      ,SCAN_INV_IND                                     01300014
                      ,ACTL_FIN_BK_DTE                                  01310014
W33304                ,SCHD_PHY_CNT_CDE                                 01320014
                                                                        01330014
                INTO   :INVPAR-PLND-INV-DTE                             01340014
                      ,:INVPAR-SCAN-INV-IND                             01350014
                      ,:INVPAR-ACTL-FIN-BK-DTE                          01360014
W33304                ,:INVPAR-SCHD-PHY-CNT-CDE                         01370014
                                                                        01380014
                FROM  TINVPAR                                           01390014
               WHERE  LOC_NBR          = :PV-LOCATION-NBR               01400014
012505           AND ( (ACTL_FIN_BK_DTE  = :PC-INVENTORY-IN-PROGRESS    01410014
W33304           AND LOC_INV_STAT_CDE = 'IN'                            01410014
W33304           AND (SCHD_PHY_CNT_CDE = 'PI'                           01420014
W33304            OR  SCHD_PHY_CNT_CDE = 'CL'))                         01430014
                                                                        01440014
012505*           OR (PLND_INV_DTE     = :IN800-COUNT-DATE)                   14
012505           AND (PLND_INV_DTE    <= :IN800-COUNT-DATE)                   14
012505           AND (ACTL_UNIT_BK_DTE > :IN800-COUNT-DATE) )                 14
                WITH UR                                                       14
                                                                        01440014
           END-EXEC.                                                    01450014
                                                                        01460014
           EVALUATE TRUE                                                01470014
               WHEN SQLCODE = ZERO                                      01480014
               WHEN SQLCODE = -811                                      01480014
                    SET IN800-IN-INVENTORY-CYCLE TO TRUE                01490014
                                                                        01500014
               WHEN SQLCODE = +100                                      01510014
                    SET IN800-NOT-IN-INVENTORY-CYCLE TO TRUE            01520014
                                                                        01530014
               WHEN OTHER                                               01540014
                    MOVE '4000-' TO IN800-ERROR-PARAGRAPH               01550014
                    SET  IN800-SQL-ERROR TO TRUE                        01560014
                    MOVE SQLCODE TO IN800-SQLCODE                       01570014
                    MOVE SQLCA   TO IN800-SQLCA                         01580014
                                                                        01590014
           END-EVALUATE.                                                01600014
                                                                        01610014
      ****************************************************************  01240014
      *    SELECTS MOST OF THE INVENTORY FIELDS AND PASSES THEM      *  01170014
      *        BACK TO THE CALLING PROGRAM.                          *  01170014
      *                                                              *  01170014
      ****************************************************************  01240014
                                                                        01250014
       5000-SELECT-INVENTORY-DATA.                                      01260014
                                                                        01270014
           EXEC SQL                                                     01280014
              SELECT                                                          14
                       ACTL_FIN_BK_DTE                                  01310014
                      ,ACTL_INV_DTE                                     01310014
                      ,ACTL_INV_RECD_DTE                                01310014
                      ,ACTL_UNIT_BK_DTE                                 01310014
                                                                        01330014
                      ,PLND_FIN_BK_DTE                                  01290014
                      ,PLND_INV_DTE                                     01290014
                      ,PLND_UNIT_BK_DTE                                 01290014
                                                                        01330014
                      ,XFER_INV_CTOFF_DTE                               01290014
                                                                        01330014
W33304                ,SCHD_PHY_CNT_CDE                                 01320014
                      ,SCAN_INV_IND                                     01300014
                                                                        01330014
                INTO   :INVPAR-ACTL-FIN-BK-DTE                          01310014
                      ,:INVPAR-ACTL-INV-DTE                             01310014
                      ,:INVPAR-ACTL-INV-RECD-DTE                        01310014
                      ,:INVPAR-ACTL-UNIT-BK-DTE                         01310014
                                                                        01330014
                      ,:INVPAR-PLND-FIN-BK-DTE                          01290014
                      ,:INVPAR-PLND-INV-DTE                             01290014
                      ,:INVPAR-PLND-UNIT-BK-DTE                         01290014
                                                                        01330014
                      ,:INVPAR-XFER-INV-CTOFF-DTE                       01290014
                                                                        01330014
W33304                ,:INVPAR-SCHD-PHY-CNT-CDE                               14
                      ,:INVPAR-SCAN-INV-IND                                   14
                                                                        01380014
                FROM  TINVPAR                                           01390014
               WHERE  LOC_NBR          = :PV-LOCATION-NBR               01400014
                  OR  PLND_INV_DTE     = :IN800-COUNT-DATE                    14
                WITH UR                                                       14
                                                                        01440014
           END-EXEC.                                                    01450014
                                                                        01460014
           EVALUATE TRUE                                                01470014
               WHEN SQLCODE = ZERO                                      01480014
               WHEN SQLCODE = -811                                      01480014
                    SET IN800-INVENTORY-DATA-FOUND TO TRUE              01490014
                                                                        01500014
               WHEN SQLCODE = +100                                      01510014
                    SET IN800-INVENTORY-DATA-NOT-FOUND TO TRUE          01490014
                                                                        01530014
               WHEN OTHER                                               01540014
                    MOVE '5000-' TO IN800-ERROR-PARAGRAPH               01550014
                    SET  IN800-SQL-ERROR TO TRUE                        01560014
                    MOVE SQLCODE TO IN800-SQLCODE                       01570014
                    MOVE SQLCA   TO IN800-SQLCA                         01580014
                                                                        01590014
           END-EVALUATE.                                                01600014
                                                                        01610014
      ****************************************************************  01240014
      *                                                              *  01170014
      *                                                              *  01170014
      *                                                              *  01170014
      ****************************************************************  01240014
                                                                        01250014
       5200-FORMAT-INVENTORY-DATA.                                      01260014
                                                                        01270014
           MOVE INVPAR-ACTL-FIN-BK-DTE    TO IN800-ACTL-FIN-BK-DTE.           14
           MOVE INVPAR-ACTL-INV-DTE       TO IN800-ACTL-INV-DTE.              14
           MOVE INVPAR-ACTL-INV-RECD-DTE  TO IN800-ACTL-INV-RECD-DTE.         14
           MOVE INVPAR-ACTL-UNIT-BK-DTE   TO IN800-ACTL-UNIT-BK-DTE.          14
           MOVE INVPAR-PLND-FIN-BK-DTE    TO IN800-PLND-FIN-BK-DTE.           14
           MOVE INVPAR-PLND-INV-DTE       TO IN800-PLND-INV-DTE.              14
           MOVE INVPAR-PLND-UNIT-BK-DTE   TO IN800-PLND-UNIT-BK-DTE.          14
           MOVE INVPAR-XFER-INV-CTOFF-DTE TO IN800-XFER-INV-CTOFF-DTE.        14
W33304     MOVE INVPAR-SCHD-PHY-CNT-CDE   TO IN800-SCHD-PHY-CNT-CDE.          14
           MOVE INVPAR-SCAN-INV-IND       TO IN800-SCAN-INV-IND.              14
                                                                              14
