      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    FSKUT021.                                                 
       AUTHOR.        JEFF SNYDER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  DEC 09,1987.                                              
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *  THIS PROGRAM WILL BUILD AN EXTRACT FILE FROM THE SKU STORE    *        
      *  FILE.  THIS EXTRACT FILE WILL BE USED TO UPDATE THE INVENTORY *        
      *  TABLE IN A LATER PROGRAM.  THE BEGINNING OF WEEK VALUE FOR THE*        
      *  EXTRACT IS DERIVED FROM THE CONTROL CARD DATE (SUNDAY DATE)   *        
      ******************************************************************        
      *  REVISED TO ADD RETAIL AMT TO THE EXTRACT TABLE.  RETAIL-AMT   *        
      *  IS COMPUTED AS FOLLOWS:                                       *        
      *      RETAIL-AMT = TSKST-ONHAND-QTY * TSK-CUR-UNIT-RTL-AMT      *        
      *  ALSO ADDED CODE TO READ TSK BECAUSE THE CUR-UNIT-RTL-AMT      *        
      *  FIELD IS NEEDED.                                              *        
      *  CHANGE MADE BY:   MARIA ROSSI    2/18/88      WR# 386         *        
      ******************************************************************        
      * REVISED TO USE 'JOIN' OF THE 2 TABLES USED (TSK AND TSKST).    *        
      * THIS IS FOUND TO BE MORE EFFICIENT.                            *        
      * CHANGE MADE BY:  MARIA ROSSI 2/29/88  WR# 386                  *        
      ******************************************************************        
      * REVISED TO BYPASS TSKST RECORDS WHOSE ONHAND QTY = 0           *        
      * CHANGE MADE BY:  MARIA ROSSI 3/02/88  WR# 386                  *        
      ******************************************************************        
      * REVISIONS:                                                     *        
      *   1. ADD 1 CHARACTER  TO OUTPUT RECORD FOR PARTITION CODE.     *        
      *   2. CHANGE CONTROL CARD TO INCLUDE MAX WEEKS TO BE RETAINED   *        
      *      ON THE DB2 TABLE, AND ANOTHER FIELD TO OVERRIDE THE DB2   *        
      *      PARTITION CODE OF THE FILE.                               *        
      *   3. ADD LOGIC TO DETERMINE THE PARTITION CODE TO USE, AND     *        
      *      TO USE THE OVERRIDE IF PROVIDED.                          *        
      * CHANGE MADE BY: RICK WALTON  4/6/89   WR# 747                  *        
      ******************************************************************        
      * REVISIONS:                                                     *        
      *      EXPANDED OUTPUT RECORD LENGTH OF INVENTORY EXTRACT BY 8   *        
      *      BYTES TO INCLUDE NEW FIELD - ITEM NUMBER.                 *        
      * CHANGE MADE BY: DORIE CALLIES  10/15/90                        *        
      ******************************************************************        
      * REVISIONS:                                                     *        
      *      REMOVED PART-CODE, AND INVEN-INDCT FROM INVENTORY EXTRACT *        
      *      AND ADDED DB2 DATE COLUMN FSCL-BOW-DATE TO INDICATE THE   *        
      *      BEGINNING OF WEEK DATE (SUNDAY DATE).  EXPANDED INVENTORY *        
      *      EXTRACT TO 37 BYTES DUE TO THIS CHANGE.  REMOVED INP01    *        
      *      (OLD INVENTORY EXTRACT FILE), OUT01 (PART CODE FILE),     *        
      *      AND OUT02 (DSUM FILE) SINCE THESE FILES ARE NO LONGER     *        
      *      NEEDED.  ALSO CHANGED CONTROL CARD TO BE IN DB2 ISO       *        
      *      FORMAT (CCYY-MM-DD) AND REMOVED PART CODE FROM CONTROL    *        
      *      CARD FORMAT.  ADDED INVENTORY PURGE DATE FILE TO PASS TO  *        
      *      DBK403 TO INDICATE WHICH WEEK TO DELETE FROM TSKSTWKI.    *        
      * CHANGE MADE BY: BETTY DORST    06/12/98                        *        
      *                                                                         
      * 05/03/99  ROLLIN KRING - REMOVED REFERENCE TO DPG51-SYSTEM-DATE*        
      *       WHICH IS NO LONGER SUPPORTED BY THE CALENDAR ROUTINE.    *        
      *       REMOVED WORKING STORAGE FIELD PC-CALENDAR-ROUTINE  TO    *        
      *       STANDARDIZE FIELD IN CALENDAR ROUTINE COPYBOOK.          *        
      *                                           (WR# 10647)          *        
      *                                                                *        
      * 07/20/99  CRAIG FAHEY - CHANGED FOR REGIONAL CLEARANCE PRICING *        
      *           ADDED CODE TO LOOK AT THE REGN_PRIC_IND FROM TSK     *        
      *           TO DETERMINE WHETHER A FETCH OF TUPCPLS IS NEEDED    *        
      *           TO FIND THE STORES UNT_RTL_AMT FOR THE ITEM.         *        
      *           IF A FETCH IS ATTEMPTED, AND A ROW IS NOT FOUND      *        
      *           ON TUPCPLS, PROCESS THE SAME AS BEFORE,              *        
      *           IF A ROW IS FOUND, USE THE CURRENT RETAIL            *        
      *           PRICE FROM THE TUPCPLS TABLE.                        *        
      *                                           (WR# 10760)          *        
      *                                                                *        
      * 05/16/00  DAN FRANKLIN - CHANGED TO USE AN UNLOAD OF TSKST AS  *        
      *           INPUT INSTEAD OF JOINING TSKST WITH TSK.             *        
      *                                           (WR#      )          *        
      *                                                                *        
      * 04/08/03  DON TOMLINSON- NI-SKU CHANGES. REPLACE TSK DEPT, CL, *        
      *           SEQ WITH TSKXREF 'SKU' FIELD.                        *        
      *                                           (WR23946)                     
      *  03/29/05 - ANNE KINTOPF                                        00310011
      *             (WR# 26354) CHANGED FOR STORE NUMBER EXPANSION      00320011
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT INVENTORY-INPUT-FILE                                          
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INVENTORY-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT INVENTORY-PURGE-DATE-FILE                                     
               ASSIGN TO OUT02.                                                 
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  INVENTORY-INPUT-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 34 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IE-INVENTORY-INPUT-RECORD PIC X(34).                                 
      *                                                                         
       FD  INVENTORY-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 36 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IE-INVENTORY-EXTRACT-RECORD PIC X(36).                               
      *                                                                         
       FD  INVENTORY-PURGE-DATE-FILE                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVENTORY-PURGE-DATE-RECORD     PIC X(80).                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                  PIC S9(04)    VALUE ZEROES               
                                                     COMP SYNC.                 
      *                                                                         
       01  CR-CONTROL-RECORD.                                                   
           05 CC-ISO-FMT-DATE.                                                  
              10 CC-ISO-FMT-CCYY     PIC X(4).                                  
              10 CC-ISO-FMT-DASH1    PIC X(1).                                  
              10 CC-ISO-FMT-MM       PIC X(2).                                  
              10 CC-ISO-FMT-DASH1    PIC X(1).                                  
              10 CC-ISO-FMT-DD       PIC X(2).                                  
           05 FILLER                 PIC X(70).                                 
      *                                                                         
       01  DL-DISPLAY-LINE.                                                     
           05  FILLER                  PIC X(40)     VALUE                      
           ' *** TOTAL NUMBER OF EXTRACTS CREATED = '.                          
           05  DL-HOLD-VALUE           PIC ZZZ,ZZZ,ZZ9 VALUE ZERO.              
      *                                                                         
       01  II-INVENT-INPUT-RECORD.                                              
           05  II-ITEM-NUMBER          PIC S9(15)    COMP-3                     
                                                     VALUE 0.                   
           05  II-STORE-NUMBER         PIC S9(4)     COMP VALUE 0.              
           05  FILLER                  PIC X(08)     VALUE SPACES.              
           05  II-ONHAND-QTY           PIC S9(07)    COMP-3                     
                                                     VALUE 0.                   
           05  FILLER                  PIC X(12)     VALUE SPACES.              
      *                                                                         
       01  IE-INVENT-EXTRACT-RECORD.                                            
           05  IE-SKU                  PIC X(08)     VALUE SPACES.              
           05  IE-STORE-NUMBER         PIC S9(4)     COMP VALUE 0.              
           05  IE-FSCL-BOW-DTE         PIC X(10)     VALUE SPACES.              
           05  IE-INVENTORY-QTY        PIC S9(07)    VALUE ZERO                 
                                                     COMP-3.                    
           05  IE-INVENTORY-RTL-AMT    PIC S9(07)    VALUE ZERO                 
                                                     COMP-3.                    
           05  IE-ITEM-NUMBER          PIC S9(15)    VALUE ZERO                 
                                                     COMP-3.                    
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SW               PIC X(1)      VALUE SPACES.              
               88  PS-EOF                            VALUE 'Y'.                 
           05  PS-STORE-FOUND-SW       PIC X(1)      VALUE SPACES.              
               88  PS-STORE-FOUND                    VALUE 'Y'.                 
               88  PS-STORE-NOT-FOUND                VALUE 'N'.                 
           05  PS-SKU-FOUND-SW         PIC X(1)      VALUE SPACES.              
               88  PS-SKU-FOUND                      VALUE 'Y'.                 
               88  PS-SKU-NOT-FOUND                  VALUE 'N'.                 
      *                                                                         
       01  WS-RETAIL-PRICE             PIC 9(5)V99   COMP-3.                    
      *                                                                         
       01  WS-INVENTORY-PURGE-DATE-RECORD.                                      
           05  WS-TICK-MARK1           PIC X(1)      VALUE SPACES.              
           05  WS-INVEN-PURGE-BOW-DTE  PIC X(10).                               
           05  WS-TICK-MARK2           PIC X(1)      VALUE SPACES.              
           05  FILLER                  PIC X(68)     VALUE SPACES.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME          PIC X(30)  VALUE SPACES.              
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)  VALUE SPACES.              
           05  PV-RETRY-COUNT             PIC S9(04) VALUE 0                    
                                                     COMP SYNC.                 
           05  PV-ITEM-NUMBER          PIC S9(15)    COMP-3                     
                                                     VALUE 0.                   
           05  PV-INPUT-COUNT             PIC 999999 VALUE 0.                   
      ***  05  PV-NOT-FOUND-COUNT         PIC 9(09)  VALUE 0.                   
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-INVEN-EXTRACTS          PIC S9(09) VALUE ZERO.                
           05  PC-MAX-RETRY-COUNT         PIC S9(04) VALUE 3                    
                                                     COMP SYNC.                 
           05  PC-MORE-THAN-MAX-RETRY     PIC S9(04) VALUE 4                    
                                                     COMP SYNC.                 
           05  PC-PURGE-BOW-DTE           PIC  X(10) VALUE SPACES.              
      *                                                                         
      * NI-SKU CONVERSION WORK FIELDS                                           
      *                                                                         
           COPY ISWS006.                                                        
      *                                                                         
      * COPY DB2 ERROR MESAGES FORMAT                                           
      *                                                                         
           COPY DPWS004.                                                        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TSK                                                      
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
      *                                                                         
      * COPY WORKING STORAGE FOR CALENDAR ROUTINE                               
      *                                                                         
           COPY DPWS500.                                                        
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *                                                                         
       A000-MAINLINE-PROCESSING.                                                
      ******************************************************************        
      *A000-MAINLINE-PROCESSING                                        *        
      ******************************************************************        
           PERFORM B000-INITIALIZATION.                                         
           PERFORM B200-PRODUCE-INVENTORY-EXTRACT                               
               UNTIL PS-EOF.                                                    
           PERFORM B300-END-OF-JOB.                                             
           GOBACK.                                                              
      *                                                                         
       B000-INITIALIZATION.                                                     
      ******************************************************************        
      * PERFORM ALL PROGRAM INITIALIZATION.                            *        
      ******************************************************************        
           OPEN INPUT  INVENTORY-INPUT-FILE                                     
                OUTPUT INVENTORY-EXTRACT-FILE                                   
                       INVENTORY-PURGE-DATE-FILE.                               
      *                                                                         
           ACCEPT CR-CONTROL-RECORD FROM SYSIN.                                 
           PERFORM B050-EDIT-CONTROL-CARD.                                      
           DISPLAY 'FSCL BOW DATE   = ' CC-ISO-FMT-DATE.                        
      *                                                                         
           SET PS-STORE-NOT-FOUND TO TRUE.                                      
           PERFORM Z100-READ-INPUT-FILE                                         
               UNTIL PS-STORE-FOUND OR                                          
                     PS-EOF.                                                    
      *                                                                         
      ******************************************************************        
      * CONTROL CARD IS CHECKED FOR VALID DATE.  MUST BE IN CCYY-MM-DD *        
      * FORMAT AND BE A SUNDAY DATE                                    *        
      ******************************************************************        
       B050-EDIT-CONTROL-CARD.                                                  
           DISPLAY SPACES.                                                      
           DISPLAY 'CC-ISO-FMT-DATE = ' CC-ISO-FMT-DATE.                        
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
              USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-WARNING-ERROR                                         
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                  DISPLAY '** ABEND **'                                         
                  DISPLAY '** PGM = FSKUT021'                                   
                  DISPLAY '** PARAGRAPH = B050-EDIT-CONTROL-CARD'               
                  DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'             
                  DISPLAY '** USING CONTROL CARD DATE AS INPUT.'                
                  DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE             
                  DISPLAY '** DPG51 = ' DPG51                                   
                  DISPLAY '** DPG52 = ' DPG52                                   
                  DISPLAY '** DPG53 = ' DPG53                                   
                  DISPLAY '** DPG54 = ' DPG54                                   
                  MOVE +4003 TO ABEND-CODE                                      
                  CALL 'ILBOABN0' USING ABEND-CODE                              
               WHEN OTHER                                                       
                  CONTINUE                                                      
           END-EVALUATE.                                                        
      *                                                                         
           DISPLAY 'DAY OF WEEK RETURNED = ' DPG55-ALPHA-WEEK-DAY.              
           IF DPG55-ALPHA-WEEK-DAY-SUNDAY                                       
             CONTINUE                                                           
           ELSE                                                                 
             DISPLAY '** ABEND **'                                              
             DISPLAY '** PGM = FSKUT021'                                        
             DISPLAY '** PARAGRAPH = B050-EDIT-CONTROL-CARD'                    
             DISPLAY '** CONTROL CARD DATE MUST BE A SUNDAY DATE.'              
             MOVE +4003 TO ABEND-CODE                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * DECREMENT BEGINNING OF WEEK SUNDAY DATE FOR NEW EXTRACT BY 49  *        
      * DAYS (7 WEEKS) TO DETERMINE PURGE DATE SINCE WE ONLY WEEK 7    *        
      * WEEKS OF INFORMATION ON TSKSTWKI.                              *        
      ******************************************************************        
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2                                          
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT     TO TRUE.                                
           MOVE 49 TO DPG51-INCR-DECR-DAYS-9.                                   
           MOVE CC-ISO-FMT-DATE        TO DPG52-LK-DATE-INPUT.                  
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
              USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                        
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PC-PURGE-BOW-DTE.                  
           DISPLAY 'PURGE FSCL BOW DTE = ' PC-PURGE-BOW-DTE.                    
                                                                                
           INITIALIZE WS-INVENTORY-PURGE-DATE-RECORD.                           
           MOVE QUOTE TO WS-TICK-MARK1                                          
                         WS-TICK-MARK2.                                         
           MOVE PC-PURGE-BOW-DTE  TO WS-INVEN-PURGE-BOW-DTE.                    
           PERFORM Z400-WRITE-INVEN-PURGE-DTE-EXT.                              
                                                                                
      *                                                                         
       B200-PRODUCE-INVENTORY-EXTRACT.                                          
      ******************************************************************        
      * B200-PRODUCE-WEEKLY-INVENTORY                                  *        
      ******************************************************************        
           IF II-ITEM-NUMBER NOT = PV-ITEM-NUMBER                               
               PERFORM Z150-READ-TSK                                            
                                                                                
               MOVE II-ITEM-NUMBER TO PV-ITEM-NUMBER                            
           END-IF.                                                              
                                                                                
           MOVE CC-ISO-FMT-DATE      TO IE-FSCL-BOW-DTE.                        
                                                                                
           IF PS-SKU-FOUND                                                      
               MOVE SKXREF-SKU           TO NI-SKU-9                            
               MOVE NI-SKU               TO IE-SKU                              
               MOVE II-STORE-NUMBER      TO IE-STORE-NUMBER                     
               MOVE II-ONHAND-QTY        TO IE-INVENTORY-QTY                    
                                                                                
               IF  TSK-REGN-PRIC-IND = 'Y'                                      
                   PERFORM Z200-FETCH-TUPCPLS-INFO                              
               ELSE                                                             
                   IF  TSK-GP-QTY = ZERO                                        
                       COMPUTE IE-INVENTORY-RTL-AMT ROUNDED =                   
                           II-ONHAND-QTY * TSK-CUR-UNIT-RTL-AMT                 
                   ELSE                                                         
                       COMPUTE WS-RETAIL-PRICE =                                
                           TSK-GP-RTL-AMT / TSK-GP-QTY                          
                       COMPUTE IE-INVENTORY-RTL-AMT ROUNDED =                   
                           II-ONHAND-QTY * WS-RETAIL-PRICE                      
                   END-IF                                                       
               END-IF                                                           
                                                                                
               MOVE TSK-ITEM-NMBR        TO IE-ITEM-NUMBER                      
               PERFORM Z300-WRITE-INVEN-EXTRACT                                 
               ADD +1 TO PC-INVEN-EXTRACTS                                      
      ***  ELSE                                                                 
      ***      ADD 1 TO PV-NOT-FOUND-COUNT                                      
           END-IF.                                                              
                                                                                
           SET PS-STORE-NOT-FOUND TO TRUE.                                      
           PERFORM Z100-READ-INPUT-FILE                                         
               UNTIL PS-STORE-FOUND OR                                          
                     PS-EOF.                                                    
      *                                                                         
       B300-END-OF-JOB.                                                         
      ******************************************************************        
      * B300-END-OF-JOB   CLOSE CURSOR                                 *        
      ******************************************************************        
           MOVE PC-INVEN-EXTRACTS TO DL-HOLD-VALUE.                             
           DISPLAY DL-DISPLAY-LINE.                                             
           CLOSE INVENTORY-INPUT-FILE                                           
                 INVENTORY-EXTRACT-FILE                                         
                 INVENTORY-PURGE-DATE-FILE.                                     
      ***  DISPLAY 'NOT FND = ' PV-NOT-FOUND-COUNT.                             
      *                                                                         
       Z100-READ-INPUT-FILE.                                                    
      ******************************************************************        
      * Z100-READ-INPUT-FILE.  READ UNTIL ONHAND QUANTITY NOT = 0.              
      ******************************************************************        
                                                                                
           READ INVENTORY-INPUT-FILE                                            
               INTO II-INVENT-INPUT-RECORD                                      
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
                                                                                
           IF II-ONHAND-QTY NOT = 0                                             
               SET PS-STORE-FOUND TO TRUE                                       
           END-IF.                                                              
                                                                                
       Z150-READ-TSK.                                                           
      ******************************************************************        
      * Z150-READ-TSK.  READ THE TSK TABLE USING ITEM NUMBER FROM TSKST         
      ******************************************************************        
                                                                                
            EXEC SQL                                                            
                 SELECT SKU,                                                    
                        CUR_UNIT_RTL_AMT,                                       
                        GP_QTY,                                                 
                        GP_RTL_AMT,                                             
                        ITEM_NMBR,                                              
                        REGN_PRIC_IND                                           
                    INTO :SKXREF-SKU                                            
                        ,:TSK-CUR-UNIT-RTL-AMT                                  
                        ,:TSK-GP-QTY                                            
                        ,:TSK-GP-RTL-AMT                                        
                        ,:TSK-ITEM-NMBR                                         
                        ,:TSK-REGN-PRIC-IND                                     
                    FROM  TSK                                                   
                         ,TSKXREF                                               
                    WHERE ITEM_NMBR    = :II-ITEM-NUMBER                        
                      AND ITEM_NMBR    = ITM_NBR                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET PS-SKU-FOUND TO TRUE                                     
               WHEN +100                                                        
                   SET PS-SKU-NOT-FOUND TO TRUE                                 
               WHEN OTHER                                                       
                   MOVE 'Z150-READ-TSK' TO PV-PARAGRAPH-NAME                    
                   MOVE 'READ TSK     ' TO  PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
       Z200-FETCH-TUPCPLS-INFO.                                                 
      ******************************************************************        
      * Z200-FETCH-TUPCPLS-INFO. FOR REGIONAL CLEARANCE PRICING        *        
      ******************************************************************        
            MOVE TSK-ITEM-NMBR       TO UPCPLS-ITM-NBR                          
            MOVE II-STORE-NUMBER     TO UPCPLS-STR-NBR                          
            EXEC SQL                                                            
                SELECT UNT_RTL_AMT                                              
                    INTO :UPCPLS-UNT-RTL-AMT                                    
                    FROM  TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                    AND   STR_NBR      = :UPCPLS-STR-NBR                        
                    AND   OPERCO_NBR   = '03'                                   
                    AND   UPC_STAT_CDE = '30'                                   
                    AND                                                         
                        ((EFF_BEG_TMST <= CURRENT TIMESTAMP                     
                    AND    EFF_END_TMST >= CURRENT TIMESTAMP)                   
                    OR                                                          
                         (EFF_BEG_TMST <= CURRENT TIMESTAMP                     
                   AND    EFF_END_TMST IS NULL))                                
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   COMPUTE IE-INVENTORY-RTL-AMT ROUNDED =                       
                       II-ONHAND-QTY * UPCPLS-UNT-RTL-AMT                       
               WHEN +100                                                        
                   IF  TSK-GP-QTY = ZERO                                        
                       COMPUTE IE-INVENTORY-RTL-AMT ROUNDED =                   
                           II-ONHAND-QTY * TSK-CUR-UNIT-RTL-AMT                 
                   ELSE                                                         
                       COMPUTE WS-RETAIL-PRICE              =                   
                           TSK-GP-RTL-AMT / TSK-GP-QTY                          
                       COMPUTE IE-INVENTORY-RTL-AMT ROUNDED =                   
                           II-ONHAND-QTY * WS-RETAIL-PRICE                      
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE 'Z200-FETCH-TUPCPLS-INFO' TO PV-PARAGRAPH-NAME          
                   MOVE 'FETCH TUPCPLS' TO  PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
       Z300-WRITE-INVEN-EXTRACT.                                                
      ******************************************************************        
      * Z300-WRITE-INVEN-EXTRACT  INVENTORY RECORDS ARE WRITTEN        *        
      ******************************************************************        
           WRITE IE-INVENTORY-EXTRACT-RECORD                                    
             FROM IE-INVENT-EXTRACT-RECORD.                                     
      *                                                                         
       Z400-WRITE-INVEN-PURGE-DTE-EXT.                                          
      ******************************************************************        
      * Z400-WRITE-INVEN-PURGE-DATE RECORD - THIS DATE IS PASSED TO    *        
      * DBK403 AND IS USED TO PURGE OLD INVENTORY RECORDS FROM TSKSTWKI*        
      ******************************************************************        
           WRITE INVENTORY-PURGE-DATE-RECORD                                    
             FROM WS-INVENTORY-PURGE-DATE-RECORD.                               
      *                                                                         
       Z999-SQL-ABEND.                                                          
      *****************************************************************         
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      *****************************************************************         
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = FSKUT021'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      *****************************************************************         
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *****************************************************************         
           COPY DPPD004.                                                        
                                                                                
           EXEC SQL                                                             
             COMMIT                                                             
           END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
