       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUP348                                          00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  10-01-2000.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE: LEDGER DATABASE UPDATE PROGRAM FOR MONTH DEPARTMENT     00080000
      *         CHANNEL TABLE, TMNDPTC....                              00090000
      *              (THIS PROGRAM WAS MODELED AFTER MLKUP341)          00100000
      *                                                                 00110000
      ******************************************************************00120000
      ******************************************************************00130000
      *                                                                 00140000
      *PROJECT HISTORY:                                                 00150000
      *  DATE  11/13/2000                                               00160000
99999 *      CHANGED BY:  ROD JANSSEN          WR#99999 PITS# XXXXXX    00170000
99999 *        DATA INTEGRITY - OUT OF BALANCE (MLK991 - 11/13/2000)    00180000
W26603******************************************************************00190000
W26603* CHG ID   DATE        DESCRIPTION                               *00200000
W26603* ------   ----------  ----------------------------------------- *00210000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00220000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00230000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00240000
W26603*                      A DB2 SELECT.                             *00250000
W26603*                      EXPANDED INPUT FILE TO 170.               *00260000
W26603*                                                                *00270000
W26603*                      - RONNA MCDONALD                          *00280000
      *                                                                 00290000
      ******************************************************************00300000
      *                                                                 00310000
                                                                        00320000
       ENVIRONMENT DIVISION.                                            00330000
       CONFIGURATION SECTION.                                           00340000
       SOURCE-COMPUTER.        IBM-3090.                                00350000
       OBJECT-COMPUTER.        IBM-3090.                                00360000
       INPUT-OUTPUT SECTION.                                            00370000
       FILE-CONTROL.                                                    00380000
           SELECT CONDENSED-FILE                                        00390000
               ASSIGN TO INP01.                                         00400000
                                                                        00410000
       DATA DIVISION.                                                   00420000
       FILE SECTION.                                                    00430000
                                                                        00440000
       FD  CONDENSED-FILE                                               00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS.                                    00480000
W26603 01  CONDENSED-RECORD            PIC X(170).                      00490000
                                                                        00500000
      ******************************************************************00510000
       WORKING-STORAGE SECTION.                                         00520000
      ******************************************************************00530000
                                                                        00540000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00550000
                                                                        00560000
       01  PC-PROGRAM-CONSTANTS.                                        00570000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK295'.    00580000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP348'.  00590000
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.             00600000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00610000
                                                                        00620000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00630000
           05  PE-MSG-01                       PIC X(50) VALUE          00640000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00650000
           05  PE-MSG-02                       PIC X(50) VALUE          00660000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNDPTC FAILED     '.   00670000
           05  PE-MSG-03                       PIC X(50) VALUE          00680000
                'ATTEMPT TO INSERT ROW ON TABLE TMNDPTC FAILED     '.   00690000
           05  PE-MSG-04                       PIC X(50) VALUE          00700000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00710000
           05  PE-MSG-05                       PIC X(50) VALUE          00720000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00730000
           05  PE-MSG-06                       PIC X(50) VALUE          00740000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00750000
           05  PE-MSG-07                       PIC X(50) VALUE          00760000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00770000
           05  PE-MSG-08                       PIC X(50) VALUE          00780000
                'SELECT OF CHECKPOINT RESTART INFO FROM TCKRSTINF'.     00790000
           05  PE-MSG-09                       PIC X(50) VALUE          00800000
                'SET HOLD TIMESTAMP EQUAL TO CURRENT TIMESTAMP  '.      00810000
           05  PE-MSG-10                       PIC X(50) VALUE          00820000
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00830000
           05  PE-MSG-11                       PIC X(50) VALUE          00840000
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00850000
      *                                                                 00860000
       01  PS-PROGRAM-SWITCHES.                                         00870000
           05  PS-CONDENSED-FILE-EOF-SW     PIC  X        VALUE 'N'.    00880000
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00890000
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00900000
           05  PS-EMPTY-FILE-SW             PIC  X        VALUE 'N'.    00910000
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00920000
           05  PS-MNDPTC-CURSOR-EOF-SW      PIC  X        VALUE 'N'.    00930000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00940000
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00950000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00960000
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00970000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00980000
           05  PS-PROCESSING-OPTIONS        PIC X         VALUE 'P'.    00990000
               88  PS-UPDATE-MNDPTC-ROW                   VALUE 'U'.    01000000
               88  PS-INSERT-MNDPTC-ROW                   VALUE 'I'.    01010000
      *                                                                 01020000
       01  PROGRAM-VARIABLES.                                           01030000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01040000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01050000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01060000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01070000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01080000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01090000
      *                                                                 01100000
       01  PA-PROGRAM-ACCUMULATORS.                                     01110000
           05  PA-RECORD-COUNTS.                                        01120000
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01130000
               10  PA-UPDATE-COUNT             PIC  9(9)  VALUE ZEROES. 01140000
               10  PA-INSERT-COUNT             PIC  9(9)  VALUE ZEROES. 01150000
               10  PA-DEPTS-PROCESSED          PIC  9(9)  VALUE ZEROES. 01160000
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01170000
      *                                                                 01180000
       01  SAVE-AREA.                                                   01190000
           05  SV-PRIMARY-KEY.                                          01200000
               10  SV-FSCL-MN-NBR        PIC X(02)         VALUE SPACES.01210000
               10  SV-FSCL-MN-END-DTE    PIC X(10)         VALUE SPACES.01220000
               10  SV-DEPT-NBR           PIC X(03)         VALUE SPACES.01230000
               10  SV-CHNL-CDE           PIC X(01)         VALUE SPACES.01240000
           05  SV-UPDATE-FIELDS.                                        01250000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01260000
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01270000
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01280000
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01290000
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01300000
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01310000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01320000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01330000
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01340000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01350000
W26603         10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01360000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01370000
W26603         10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01380000
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01390000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01400000
      *                                                                 01410000
      ******************************************************************01420000
      * MTHLY TRANSACTION UPDATE FOR TMNDPTC ARGUMENTS....              01430000
      ******************************************************************01440000
           COPY MLRD152.                                                01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01480000
      ******************************************************************01490000
           COPY DPWS004.                                                01500000
      *                                                                 01510000
      ******************************************************************01520000
      *  USED FOR DB2 ERRORS                                            01530000
      ******************************************************************01540000
           EXEC SQL                                                     01550000
               INCLUDE SQLCA                                            01560000
           END-EXEC.                                                    01570000
      *                                                                 01580000
      ******************************************************************01590000
      *  M/L MTHLY DEPARTMENT/CHANNEL TABLE                             01600000
      ******************************************************************01610000
           EXEC SQL                                                     01620000
               INCLUDE TMNDPTC                                          01630000
           END-EXEC.                                                    01640000
      *                                                                 01650000
      ***************************************************************** 01660000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01670000
      ***************************************************************** 01680000
           EXEC SQL                                                     01690000
               INCLUDE TCKRSTCN                                         01700000
           END-EXEC.                                                    01710000
      *                                                                 01720000
           EXEC SQL                                                     01730000
               INCLUDE TCKRSTIN                                         01740000
           END-EXEC.                                                    01750000
      *                                                                 01760000
      ******************************************************************01770000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01780000
      ******************************************************************01790000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01800000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +62  COMP. 01810000
           05  CR-CHECKPOINT-RESTART-VAR.                               01820000
               10  CR-PREV-DTL-KEY          PIC  X(16) VALUE SPACES.    01830000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01840000
                   15  CR-FSCL-MN-NBR       PIC  X(02).                 01850000
                   15  CR-FSCL-MN-END-DTE   PIC  X(10).                 01860000
                   15  CR-DEPT-NBR          PIC  X(03).                 01870000
                   15  CR-CHNL-CDE          PIC  X(01).                 01880000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01890000
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    01900000
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01910000
               10  CR-DEPTS-PROCESSED       PIC  9(09) VALUE ZEROES.    01920000
               10  CR-TMNDPTC-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.    01930000
      *                                                                 01940000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01950000
           05  CK-SAVE-PREV-KEY         PIC  X(16) VALUE SPACES.        01960000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01970000
               10  CK-FSCL-MN-NBR       PIC  X(02).                     01980000
               10  CK-FSCL-MN-END-DTE   PIC  X(10).                     01990000
               10  CK-DEPT-NBR          PIC  X(03).                     02000000
               10  CK-CHNL-CDE          PIC  X(01).                     02010000
                                                                        02020000
      ***************************************************************** 02030000
       PROCEDURE DIVISION.                                              02040000
      ***************************************************************** 02050000
                                                                        02060000
       0000-MAIN-MODULE.                                                02070000
                                                                        02080000
           PERFORM 1000-INITIALIZATION.                                 02090000
                                                                        02100000
           PERFORM 2000-PROCESS-INPUT                                   02110000
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02120000
                                                                        02130000
           PERFORM 8000-EOJ-ROUTINE.                                    02140000
                                                                        02150000
           STOP RUN.                                                    02160000
                                                                        02170000
      ******************************************************************02180000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02190000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02200000
      *  TCKRSTCNTL-CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION  02210000
      ******************************************************************02220000
      *                                                                 02230000
       1000-INITIALIZATION.                                             02240000
      *                                                                 02250000
           OPEN INPUT CONDENSED-FILE.                                   02260000
      *                                                                 02270000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02280000
      *                                                                 02290000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02300000
                PERFORM 7500-SELECT-RESTART-INFO                        02310000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02320000
                                              CR-CHECKPOINT-RESTART-VAR 02330000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02340000
           ELSE                                                         02350000
               SET PS-FIRST-COMMIT TO TRUE                              02360000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02370000
           END-IF.                                                      02380000
      *                                                                 02390000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02400000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02410000
            OR PS-NO-MORE-INPUT-RECORDS.                                02420000
      *                                                                 02430000
      * IF PROGRAM IN RESTART MODE, BUT NO RECORDS EXIST AFTER LAST CKPT02440000
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02450000
           IF PS-NO-MORE-INPUT-RECORDS                                  02460000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02470000
                   MOVE CR-INPUT-READ-COUNT     TO PA-INPUT-COUNT       02480000
                   MOVE CR-TMNDPTC-COMMIT-COUNT TO PA-COMMIT-COUNT      02490000
                   MOVE CR-UPDATE-COUNT         TO PA-UPDATE-COUNT      02500000
                   MOVE CR-INSERT-COUNT         TO PA-INSERT-COUNT      02510000
                   MOVE CR-DEPTS-PROCESSED      TO PA-DEPTS-PROCESSED   02520000
               ELSE                                                     02530000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02540000
           ELSE                                                         02550000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02560000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02570000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02580000
           END-IF.                                                      02590000
      *                                                                 02600000
      ******************************************************************02610000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02620000
      ******************************************************************02630000
       2000-PROCESS-INPUT.                                              02640000
      *                                                                 02650000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02660000
      *                                                                 02670000
           PERFORM 7000-SELECT-DB-ROW.                                  02680000
      *                                                                 02690000
           EVALUATE TRUE                                                02700000
               WHEN PS-UPDATE-MNDPTC-ROW                                02710000
                   PERFORM 7100-UPDATE-MNDPTC-ROW                       02720000
               WHEN PS-INSERT-MNDPTC-ROW                                02730000
                   PERFORM 7200-INSERT-MNDPTC-ROW                       02740000
               WHEN OTHER                                               02750000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     02760000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             02770000
                   PERFORM 9999-SQL-ABEND                               02780000
           END-EVALUATE.                                                02790000
      *                                                                 02800000
           IF PS-PROGRAM-DEADLOCKED                                     02810000
               CONTINUE                                                 02820000
           ELSE                                                         02830000
               PERFORM 7700-COMMIT-PROCESSING                           02840000
               PERFORM 4000-READ-CONDENSED-FILE.                        02850000
      *                                                                 02860000
      ******************************************************************02870000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02880000
      ******************************************************************02890000
       2100-CALCULATE-UPDATES.                                          02900000
      *                                                                 02910000
           COMPUTE SV-SLS-RTL-AMT        = MNDPTC-SLS-RTL-AMT           02920000
                                         + ML152-SLS-RTL-AMT.           02930000
           COMPUTE SV-MKDN-PLU-RTL-AMT   = MNDPTC-MKDN-PLU-RTL-AMT      02940000
                                         + ML152-MKDN-PLU-RTL-AMT.      02950000
           COMPUTE SV-MKDN-RGST-RTL-AMT  = MNDPTC-MKDN-RGST-RTL-AMT     02960000
                                         + ML152-MKDN-RGST-RTL-AMT.     02970000
           COMPUTE SV-MKDN-UMTCH-RTL-AMT = MNDPTC-MKDN-UMTCH-RTL-AMT    02980000
                                         + ML152-MKDN-UNMTCH-RTL-AMT.   02990000
           COMPUTE SV-MKDN-BYR-RTL-AMT   = MNDPTC-MKDN-BYR-RTL-AMT      03000000
                                         + ML152-MKDN-BYR-RTL-AMT.      03010000
           COMPUTE SV-MKDN-STR-RTL-AMT   = MNDPTC-MKDN-STR-RTL-AMT      03020000
                                         + ML152-MKDN-STR-RTL-AMT.      03030000
           COMPUTE SV-MKUP-RTL-AMT       = MNDPTC-MKUP-RTL-AMT          03040000
                                         + ML152-MKUP-RTL-AMT.          03050000
           COMPUTE SV-XFER-IN-RTL-AMT    = MNDPTC-XFER-IN-RTL-AMT       03060000
                                         + ML152-XFER-IN-RTL-AMT.       03070000
           COMPUTE SV-XFER-OUT-RTL-AMT   = MNDPTC-XFER-OUT-RTL-AMT      03080000
                                         + ML152-XFER-OUT-RTL-AMT.      03090000
           COMPUTE SV-RCPT-RTL-AMT       = MNDPTC-RCPT-RTL-AMT          03100000
                                         + ML152-RCPT-RTL-AMT.          03110000
           COMPUTE SV-RCPT-NET-COST-AMT  = MNDPTC-RCPT-NET-COST-AMT     03120000
                                         + ML152-RCPT-NET-COST-AMT.     03130000
           COMPUTE SV-PADJ-RTL-AMT       = MNDPTC-PADJ-RTL-AMT          03140000
                                         + ML152-PADJ-RTL-AMT.          03150000
           COMPUTE SV-PADJ-NET-COST-AMT  = MNDPTC-PADJ-NET-COST-AMT     03160000
                                         + ML152-PADJ-NET-COST-AMT.     03170000
           COMPUTE SV-PADJ-FRGT-COST-AMT = MNDPTC-PADJ-FRGT-COST-AMT    03180000
                                         + ML152-PADJ-FRGT-COST-AMT.    03190000
           COMPUTE SV-INV-ADJ-RTL-AMT    = MNDPTC-INV-ADJ-RTL-AMT       03200000
                                         + ML152-INV-ADJ-RTL-AMT.       03210000
      *                                                                 03220000
      ******************************************************************03230000
      * READS THE CONDENSED FILE INTO THE TMNDPTC LAYOUT                03240000
      ******************************************************************03250000
       4000-READ-CONDENSED-FILE.                                        03260000
            READ CONDENSED-FILE INTO ML152-REC                          03270000
               AT END MOVE 'Y' TO PS-CONDENSED-FILE-EOF-SW.             03280000
      *                                                                 03290000
            IF PS-MORE-INPUT-RECORDS                                    03300000
                ADD 1                        TO PA-INPUT-COUNT.         03310000
      *                                                                 03320000
      ******************************************************************03330000
      * 7000-SELECT-DB-ROW.                                             03340000
      ******************************************************************03350000
       7000-SELECT-DB-ROW.                                              03360000
      *                                                                 03370000
           EXEC SQL                                                     03380000
               SELECT SLS_RTL_AMT                                       03390000
                     ,MKDN_PLU_RTL_AMT                                  03400000
                     ,MKDN_RGST_RTL_AMT                                 03410000
                     ,MKDN_UMTCH_RTL_AMT                                03420000
                     ,MKDN_BYR_RTL_AMT                                  03430000
                     ,MKDN_STR_RTL_AMT                                  03440000
                     ,MKUP_RTL_AMT                                      03450000
                     ,XFER_IN_RTL_AMT                                   03460000
                     ,XFER_OUT_RTL_AMT                                  03470000
                     ,RCPT_RTL_AMT                                      03480000
                     ,RCPT_NET_COST_AMT                                 03490000
                     ,PADJ_RTL_AMT                                      03500000
                     ,PADJ_NET_COST_AMT                                 03510000
                     ,PADJ_FRGT_COST_AMT                                03520000
                     ,INV_ADJ_RTL_AMT                                   03530000
                 INTO :MNDPTC-SLS-RTL-AMT                               03540000
                     ,:MNDPTC-MKDN-PLU-RTL-AMT                          03550000
                     ,:MNDPTC-MKDN-RGST-RTL-AMT                         03560000
                     ,:MNDPTC-MKDN-UMTCH-RTL-AMT                        03570000
                     ,:MNDPTC-MKDN-BYR-RTL-AMT                          03580000
                     ,:MNDPTC-MKDN-STR-RTL-AMT                          03590000
                     ,:MNDPTC-MKUP-RTL-AMT                              03600000
                     ,:MNDPTC-XFER-IN-RTL-AMT                           03610000
                     ,:MNDPTC-XFER-OUT-RTL-AMT                          03620000
                     ,:MNDPTC-RCPT-RTL-AMT                              03630000
                     ,:MNDPTC-RCPT-NET-COST-AMT                         03640000
                     ,:MNDPTC-PADJ-RTL-AMT                              03650000
                     ,:MNDPTC-PADJ-NET-COST-AMT                         03660000
                     ,:MNDPTC-PADJ-FRGT-COST-AMT                        03670000
                     ,:MNDPTC-INV-ADJ-RTL-AMT                           03680000
               FROM TMNDPTC                                             03690000
              WHERE FSCL_MN_NBR         = :ML152-FSCL-MN-NBR            03700000
                AND FSCL_MN_END_DTE     = :ML152-FSCL-MN-END-DTE        03710000
                AND DEPT_NBR            = :ML152-DEPT-NBR               03720000
                AND CHNL_CDE            = :ML152-CHNL-CDE               03730000
W26603        WITH UR                                                   03740000
           END-EXEC.                                                    03750000
      *                                                                 03760000
           EVALUATE TRUE                                                03770000
               WHEN SQLCODE IS EQUAL TO ZERO                            03780000
                   SET PS-UPDATE-MNDPTC-ROW       TO TRUE               03790000
               WHEN SQLCODE IS EQUAL TO +100                            03800000
                   SET PS-INSERT-MNDPTC-ROW       TO TRUE               03810000
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      03820000
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        03830000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03840000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03850000
                   PERFORM 9999-SQL-ABEND                               03860000
           END-EVALUATE.                                                03870000
      *                                                                 03880000
      ***************************************************************** 03890000
      * 7100-UPDATE-MNDPTC-ROW.                                         03900000
      ***************************************************************** 03910000
       7100-UPDATE-MNDPTC-ROW.                                          03920000
      *                                                                 03930000
           PERFORM 2100-CALCULATE-UPDATES.                              03940000
      *                                                                 03950000
           EXEC SQL                                                     03960000
               UPDATE TMNDPTC                                           03970000
                  SET SLS_RTL_AMT          = :SV-SLS-RTL-AMT            03980000
                     ,MKDN_PLU_RTL_AMT     = :SV-MKDN-PLU-RTL-AMT       03990000
                     ,MKDN_RGST_RTL_AMT    = :SV-MKDN-RGST-RTL-AMT      04000000
                     ,MKDN_UMTCH_RTL_AMT   = :SV-MKDN-UMTCH-RTL-AMT     04010000
                     ,MKDN_BYR_RTL_AMT     = :SV-MKDN-BYR-RTL-AMT       04020000
                     ,MKDN_STR_RTL_AMT     = :SV-MKDN-STR-RTL-AMT       04030000
                     ,MKUP_RTL_AMT         = :SV-MKUP-RTL-AMT           04040000
                     ,XFER_IN_RTL_AMT      = :SV-XFER-IN-RTL-AMT        04050000
                     ,XFER_OUT_RTL_AMT     = :SV-XFER-OUT-RTL-AMT       04060000
                     ,RCPT_RTL_AMT         = :SV-RCPT-RTL-AMT           04070000
                     ,RCPT_NET_COST_AMT    = :SV-RCPT-NET-COST-AMT      04080000
                     ,PADJ_RTL_AMT         = :SV-PADJ-RTL-AMT           04090000
                     ,PADJ_NET_COST_AMT    = :SV-PADJ-NET-COST-AMT      04100000
                     ,PADJ_FRGT_COST_AMT   = :SV-PADJ-FRGT-COST-AMT     04110000
                     ,INV_ADJ_RTL_AMT      = :SV-INV-ADJ-RTL-AMT        04120000
              WHERE FSCL_MN_NBR         = :ML152-FSCL-MN-NBR            04130000
                AND FSCL_MN_END_DTE     = :ML152-FSCL-MN-END-DTE        04140000
                AND DEPT_NBR            = :ML152-DEPT-NBR               04150000
                AND CHNL_CDE            = :ML152-CHNL-CDE               04160000
           END-EXEC.                                                    04170000
      *                                                                 04180000
           EVALUATE TRUE                                                04190000
               WHEN SQLCODE IS EQUAL TO ZERO                            04200000
                   CONTINUE                                             04210000
               WHEN SQLCODE IS EQUAL TO -911                            04220000
                   ADD +1             TO PV-DEADLOCK-COUNT              04230000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04240000
                       MOVE '7100-UPDATE-MNDPTC-ROW' TO                 04250000
                                                    PV-PARAGRAPH-NAME   04260000
                       PERFORM 9000-DEADLOCK-ERROR                      04270000
                   ELSE                                                 04280000
                       PERFORM 7999-RESTART-PROCESS                     04290000
                   END-IF                                               04300000
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      04310000
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        04320000
                   MOVE '7100-UPDATE-MNDPTC-ROW' TO PV-PARAGRAPH-NAME   04330000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04340000
                   PERFORM 9999-SQL-ABEND                               04350000
           END-EVALUATE.                                                04360000
      *                                                                 04370000
           IF PS-PROGRAM-DEADLOCKED                                     04380000
               CONTINUE                                                 04390000
           ELSE                                                         04400000
               ADD 1 TO PA-UPDATE-COUNT                                 04410000
                        PA-DEPTS-PROCESSED.                             04420000
      *                                                                 04430000
      ***************************************************************** 04440000
      * 7200-INSERT-MNDPTC-ROW.                                         04450000
      ***************************************************************** 04460000
       7200-INSERT-MNDPTC-ROW.                                          04470000
      *                                                                 04480000
           EXEC SQL                                                     04490000
               INSERT INTO TMNDPTC                                      04500000
                     (FSCL_MN_NBR                                       04510000
                     ,FSCL_MN_END_DTE                                   04520000
                     ,DEPT_NBR                                          04530000
                     ,CHNL_CDE                                          04540000
                     ,SLS_RTL_AMT                                       04550000
                     ,MKDN_PLU_RTL_AMT                                  04560000
                     ,MKDN_RGST_RTL_AMT                                 04570000
                     ,MKDN_UMTCH_RTL_AMT                                04580000
                     ,MKDN_BYR_RTL_AMT                                  04590000
                     ,MKDN_STR_RTL_AMT                                  04600000
                     ,MKUP_RTL_AMT                                      04610000
                     ,XFER_IN_RTL_AMT                                   04620000
                     ,XFER_OUT_RTL_AMT                                  04630000
                     ,RCPT_RTL_AMT                                      04640000
                     ,RCPT_NET_COST_AMT                                 04650000
                     ,PADJ_RTL_AMT                                      04660000
                     ,PADJ_NET_COST_AMT                                 04670000
                     ,PADJ_FRGT_COST_AMT                                04680000
                     ,INV_ADJ_RTL_AMT                                   04690000
                     ,SHNK_RTL_AMT                                      04700000
                     ,END_INV_RTL_AMT                                   04710000
                     ,CUM_MKUP_PCT                                      04720000
                     ,END_INV_COST_AMT                                  04730000
                     ,SLS_COST_AMT                                      04740000
                     ,GRO_MRGN_AMT)                                     04750000
               VALUES                                                   04760000
                     (:ML152-FSCL-MN-NBR                                04770000
                     ,:ML152-FSCL-MN-END-DTE                            04780000
                     ,:ML152-DEPT-NBR                                   04790000
                     ,:ML152-CHNL-CDE                                   04800000
                     ,:ML152-SLS-RTL-AMT                                04810000
                     ,:ML152-MKDN-PLU-RTL-AMT                           04820000
                     ,:ML152-MKDN-RGST-RTL-AMT                          04830000
                     ,:ML152-MKDN-UNMTCH-RTL-AMT                        04840000
                     ,:ML152-MKDN-BYR-RTL-AMT                           04850000
                     ,:ML152-MKDN-STR-RTL-AMT                           04860000
                     ,:ML152-MKUP-RTL-AMT                               04870000
                     ,:ML152-XFER-IN-RTL-AMT                            04880000
                     ,:ML152-XFER-OUT-RTL-AMT                           04890000
                     ,:ML152-RCPT-RTL-AMT                               04900000
                     ,:ML152-RCPT-NET-COST-AMT                          04910000
                     ,:ML152-PADJ-RTL-AMT                               04920000
                     ,:ML152-PADJ-NET-COST-AMT                          04930000
                     ,:ML152-PADJ-FRGT-COST-AMT                         04940000
                     ,:ML152-INV-ADJ-RTL-AMT                            04950000
                     ,0                                                 04960000
                     ,0                                                 04970000
                     ,0                                                 04980000
                     ,0                                                 04990000
                     ,0                                                 05000000
                     ,0 )                                               05010000
           END-EXEC.                                                    05020000
      *                                                                 05030000
           EVALUATE TRUE                                                05040000
               WHEN SQLCODE IS EQUAL TO ZERO                            05050000
                   CONTINUE                                             05060000
               WHEN SQLCODE IS EQUAL TO -911                            05070000
                   ADD +1             TO PV-DEADLOCK-COUNT              05080000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05090000
                       MOVE '7200-INSERT-MNDPTC-ROW' TO                 05100000
                                                    PV-PARAGRAPH-NAME   05110000
                       PERFORM 9000-DEADLOCK-ERROR                      05120000
                   ELSE                                                 05130000
                       PERFORM 7999-RESTART-PROCESS                     05140000
                   END-IF                                               05150000
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      05160000
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        05170000
                   MOVE '7200-INSERT-MNDPTC-ROW' TO PV-PARAGRAPH-NAME   05180000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05190000
                   PERFORM 9999-SQL-ABEND                               05200000
           END-EVALUATE.                                                05210000
      *                                                                 05220000
           IF PS-PROGRAM-DEADLOCKED                                     05230000
               CONTINUE                                                 05240000
           ELSE                                                         05250000
               ADD 1 TO PA-INSERT-COUNT                                 05260000
                        PA-DEPTS-PROCESSED.                             05270000
      *                                                                 05280000
      ******************************************************************05290000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05300000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05310000
      *            CONTROL TABLE                                       *05320000
      ******************************************************************05330000
       7300-GET-COMMIT-TIMESTAMP.                                       05340000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05350000
                                                                        05360000
           EXEC SQL                                                     05370000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05380000
               INTO :PV-COMMIT-TIMESTAMP                                05390000
               FROM TCKRSTCNTL                                          05400000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05410000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05420000
           END-EXEC.                                                    05430000
                                                                        05440000
           EVALUATE TRUE                                                05450000
               WHEN SQLCODE = 0                                         05460000
                   CONTINUE                                             05470000
               WHEN SQLCODE NOT EQUAL ZERO                              05480000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05490000
                   MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED 05500000
                   PERFORM 9999-SQL-ABEND                               05510000
           END-EVALUATE.                                                05520000
      *                                                                 05530000
      ******************************************************************05540000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05550000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05560000
      *            IF WE ARE IN A RESTART CONDITION.                   *05570000
      ******************************************************************05580000
       7400-INIT-CHECKPOINT-SELECT.                                     05590000
                                                                        05600000
           EXEC SQL                                                     05610000
             SELECT  CKPT_STAT_CODE                                     05620000
                    ,CKPT_FRQNCY_QTY                                    05630000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05640000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05650000
               FROM  TCKRSTCNTL                                         05660000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05670000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05680000
           END-EXEC.                                                    05690000
                                                                        05700000
           IF SQLCODE = 0                                               05710000
               CONTINUE                                                 05720000
           ELSE                                                         05730000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05740000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05750000
               PERFORM 9999-SQL-ABEND                                   05760000
           END-IF.                                                      05770000
      *                                                                 05780000
      ******************************************************************05790000
      * 7500-SELECT-RESTART-INFO                                       *05800000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05810000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05820000
      ******************************************************************05830000
       7500-SELECT-RESTART-INFO.                                        05840000
                                                                        05850000
           EXEC SQL                                                     05860000
             SELECT  WORK_STRG_DESC                                     05870000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05880000
               FROM  TCKRSTINF                                          05890000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05900000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05910000
           END-EXEC.                                                    05920000
                                                                        05930000
           IF SQLCODE = 0                                               05940000
               CONTINUE                                                 05950000
           ELSE                                                         05960000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05970000
               MOVE PE-MSG-08             TO PV-OPERATION-ATTEMPTED     05980000
               PERFORM 9999-SQL-ABEND                                   05990000
           END-IF.                                                      06000000
      *                                                                 06010000
      ******************************************************************06020000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06030000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06040000
      ******************************************************************06050000
       7600-SET-CURRENT-TIMESTAMP.                                      06060000
                                                                        06070000
           EXEC SQL                                                     06080000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06090000
           END-EXEC.                                                    06100000
                                                                        06110000
           IF SQLCODE = 0                                               06120000
               CONTINUE                                                 06130000
           ELSE                                                         06140000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06150000
               MOVE PE-MSG-09             TO PV-OPERATION-ATTEMPTED     06160000
               PERFORM 9999-SQL-ABEND                                   06170000
           END-IF.                                                      06180000
      *                                                                 06190000
      ******************************************************************06200000
      * 7700-COMMIT-PROCESSING.                                        *06210000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06220000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06230000
      ******************************************************************06240000
       7700-COMMIT-PROCESSING.                                          06250000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06260000
                                                                        06270000
                                                                        06280000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06290000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06300000
      *        PREPARE COMMIT RECORD FOR UPDATE                         06310000
               ADD 1                          TO PA-COMMIT-COUNT        06320000
               MOVE ML152-FSCL-MN-NBR         TO CR-FSCL-MN-NBR         06330000
               MOVE ML152-FSCL-MN-END-DTE     TO CR-FSCL-MN-END-DTE     06340000
               MOVE ML152-DEPT-NBR            TO CR-DEPT-NBR            06350000
               MOVE ML152-CHNL-CDE            TO CR-CHNL-CDE            06360000
               MOVE PA-COMMIT-COUNT           TO CR-TMNDPTC-COMMIT-COUNT06370000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06380000
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        06390000
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        06400000
               MOVE PA-DEPTS-PROCESSED        TO CR-DEPTS-PROCESSED     06410000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06420000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06430000
                                             TCKRSTINF-WORK-STRG-DESC   06440000
               IF PS-FIRST-COMMIT                                       06450000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06460000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06470000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06480000
               END-IF                                                   06490000
               PERFORM 7800-COMMIT-CHANGES                              06500000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06510000
           END-IF.                                                      06520000
      *                                                                 06530000
      ******************************************************************06540000
      * 7800-COMMIT-CHANGES.                                            06550000
      *   PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE. TAKE A COMMIT 06560000
      ******************************************************************06570000
       7800-COMMIT-CHANGES.                                             06580000
                                                                        06590000
           EXEC SQL                                                     06600000
             UPDATE TCKRSTINF                                           06610000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06620000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06630000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06640000
           END-EXEC.                                                    06650000
                                                                        06660000
           IF SQLCODE = 0                                               06670000
               CONTINUE                                                 06680000
           ELSE                                                         06690000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06700000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06710000
               PERFORM 9999-SQL-ABEND                                   06720000
           END-IF.                                                      06730000
                                                                        06740000
           EXEC SQL                                                     06750000
               COMMIT                                                   06760000
           END-EXEC.                                                    06770000
                                                                        06780000
           IF SQLCODE = 0                                               06790000
               CONTINUE                                                 06800000
           ELSE                                                         06810000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06820000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06830000
               PERFORM 9999-SQL-ABEND                                   06840000
           END-IF.                                                      06850000
      *                                                                 06860000
      ******************************************************************06870000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06880000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06890000
      *                                                                 06900000
      ******************************************************************06910000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06920000
                                                                        06930000
           EXEC SQL                                                     06940000
             UPDATE  TCKRSTCNTL                                         06950000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06960000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06970000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06980000
           END-EXEC.                                                    06990000
                                                                        07000000
           IF SQLCODE = 0                                               07010000
               CONTINUE                                                 07020000
           ELSE                                                         07030000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07040000
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED07050000
               PERFORM 9999-SQL-ABEND                                   07060000
           END-IF.                                                      07070000
                                                                        07080000
           EJECT                                                        07090000
      ******************************************************************07100000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNDPTC ROW FOR UPDATE07110000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07120000
      ******************************************************************07130000
       7999-RESTART-PROCESS.                                            07140000
      *                                                                 07150000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07160000
      *                                                                 07170000
           CLOSE CONDENSED-FILE.                                        07180000
      *                                                                 07190000
           PERFORM 7500-SELECT-RESTART-INFO.                            07200000
      *                                                                 07210000
           DISPLAY '**** RESTART **** '.                                07220000
           DISPLAY '** SQLCODE -911 ** ', PV-DEADLOCK-COUNT, 'TIMES'.   07230000
           DISPLAY 'TCKRSTINF-LAST CKPT ', TCKRSTINF-WORK-STRG-DESC.    07240000
      *                                                                 07250000
           MOVE TCKRSTINF-WORK-STRG-DESC TO CR-CHECKPOINT-RESTART-AREA. 07260000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07270000
      *                                                                 07280000
           OPEN INPUT CONDENSED-FILE.                                   07290000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07300000
      *                                                                 07310000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07320000
           PERFORM 4000-READ-CONDENSED-FILE                             07330000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07340000
      *                                                                 07350000
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07360000
           MOVE CR-UPDATE-COUNT        TO PA-UPDATE-COUNT.              07370000
           MOVE CR-INSERT-COUNT        TO PA-INSERT-COUNT.              07380000
           MOVE CR-DEPTS-PROCESSED     TO PA-DEPTS-PROCESSED.           07390000
      *                                                                 07400000
      *                                                                 07410000
      ******************************************************************07420000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07430000
      ******************************************************************07440000
       8000-EOJ-ROUTINE.                                                07450000
      *                                                                 07460000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07470000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07480000
      *                                                                 07490000
           DISPLAY '                                             '.     07500000
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  07510000
           DISPLAY 'UPDATE COUNT    ', PA-UPDATE-COUNT.                 07520000
           DISPLAY 'INSERT COUNT    ', PA-INSERT-COUNT.                 07530000
           DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.              07540000
           DISPLAY '                                             '.     07550000
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 07560000
      *                                                                 07570000
           CLOSE CONDENSED-FILE.                                        07580000
      *                                                                 07590000
      ******************************************************************07600000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07610000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07620000
      ******************************************************************07630000
       9000-DEADLOCK-ERROR.                                             07640000
      *                                                                 07650000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07660000
               PERFORM 9999-SQL-ABEND.                                  07670000
      *                                                                 07680000
      ******************************************************************07690000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07700000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07710000
      ******************************************************************07720000
       9999-SQL-ABEND.                                                  07730000
                                                                        07740000
           MOVE +4013                 TO ABEND-CODE.                    07750000
           DISPLAY '**  ABEND     **'.                                  07760000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07770000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07780000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07790000
                                                                        07800000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07810000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07820000
                                                                        07830000
           COPY DPPD004.                                                07840000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07850000
