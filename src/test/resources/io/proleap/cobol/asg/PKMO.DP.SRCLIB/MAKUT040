       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MAKUT040.                                                 
       AUTHOR.        FRANK P SANTANDREA JR.                                    
       DATE-WRITTEN.  02/11/2000.                                               
                                                                                
      *----------------------------------------------------------------*        
      *        MA - EXTRACT REGIONAL CLEARANCE DATA                    *        
      *        REPLACES THE EXISTING MAKUT040.                         *        
      *----------------------------------------------------------------*        
      * REPLACES THE EXISTING MAKUT040.                                *        
      *                                                                *        
      * MODIFIED TO HANDLE MULTIPLE INPUT FILES AND TO EXTRACT THE     *        
NM0603* ORACLE ITEM NUMBER FROM TSKXREF AND SET THE END DATES ACCORDING*        
      * TO THE PHASE II PROJECT DOCUMENTATION.                         *        
FPS403* 04/03/2000 FRANK SANTANDREA                                    *        
FPS403*            MODIFIED SELECT IN 8200 TO ONLY RETRIEVE RECORDS    *        
FPS403*            WITH A DATE GREATER THAN THE CURRENT DATETIME STAMP *        
AS0815* 08/15/2000 ANN SUN                                             *        
AS0815*            ADDED CODE TO HANDLE RCP BACK TO ACTIVE CASE.       *        
FPS603* 06/03/2003 FRANK SANTANDREA                                    *        
FPS603*            MODIFIED SELECT IN 8200 TO ONLY RETRIEVE RECORDS    *        
FPS603*            ANS UPC_STAT_CDE IS '20'                            *        
NM0603* 06/13/2003 NICK MOULTON                                                 
NM0603*            NON-INTELLIGENT SKU PROJECT CHANGES.                         
ASLG04* 09/20/2004 ANN SUN AND LINDA GRIESBACH                                  
ASLG04*            REMOVE CODE TO JION WITH TUPCPLS.                            
ASLG04*            REMOVE CODE TO CALL PERM TCENPPC.                            
DGM001* 12/01/2004 DAVE METZGER CHANGED CODE TO ACCOMODATE A 4 BYTES            
DGM001* WR25478    STORE NUMBER.  RCP EXTRACT INCREASED FROM 40 TO 60.          
TM 001* 10/14/2005 PM00811332 - CHECK FOR ONLY CLEARANCE STATUS IN              
TM 001*            8300-GET-STAT-20                                             
      *----------------------------------------------------------------*        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT RCP-COMBINED-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CTRL-CARD-FILE                                                
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT RCP-DATA-EXTRACT-FILE                                         
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  RCP-COMBINED-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RCP-COMBO-REC                   PIC  X(86).                          
                                                                                
       FD  CTRL-CARD-FILE                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CTRL-CARD-REC                   PIC  X(80).                          
                                                                                
       FD  RCP-DATA-EXTRACT-FILE                                                
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
DGM001 01  RCP-DATA-EXTRACT-REC            PIC  X(60).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CTRL-CARD.                                                        
           05  CC-EFF-TMST                   PIC X(26)  VALUE SPACE.            
           05  FILLER                        PIC X(54)  VALUE SPACE.            
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME                 PIC  X(08) VALUE                     
                                                        'MAKUT040'.             
           05  PC-MAX-RETRIES              PIC S9(04) VALUE +3 COMP.            
           05  PC-25                       PIC  X(02) VALUE '25'.               
           05  PC-30                       PIC  X(02) VALUE '30'.               
           05  PC-TUPCPLS                  PIC  X(08)                           
                                                VALUE 'TUPCPLS'.                
           05  PC-TSIPREX                  PIC  X(08)                           
                                                VALUE 'TSIPREX'.                
           05  PC-TSIPEPR                  PIC  X(08)                           
                                                VALUE 'TSIPEPR'.                
DGM001     05  PC-STR-ZERO                 PIC  X(04) VALUE '0000'.             
           05  PC-RESET-STAT25-BEG         PIC  X(10)                           
                                                VALUE '9999-99-99'.             
           05  PC-RESET-STAT30-BEG         PIC  X(10)                           
                                                VALUE '0000-00-00'.             
                                                                                
FPS403     05  PC-SET-INIT-TIME            PIC  X(26)                           
                                 VALUE '0000-00-00-23.59.59.999999'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-ABEND-CODE                 PIC S9(04) VALUE ZERO              
                                                        COMP SYNC.              
           05  PV-COMBINED-COUNT             PIC  9(10) VALUE ZERO.             
           05  PV-EXTRACT-RECS-WRITTEN       PIC  9(10) VALUE ZERO.             
           05  PV-EXTRACT-NO-SKU             PIC  9(10) VALUE ZERO.             
           05  PV-EXTRACT-25-HELD            PIC  9(10) VALUE ZERO.             
           05  PV-25-INSERTED                PIC  9(10) VALUE ZERO.             
           05  PV-30-INSERTED                PIC  9(10) VALUE ZERO.             
           05  PV-ABEND-PARAGRAPH            PIC  X(30) VALUE SPACE.            
           05  PV-STR-NBR                    PIC  9(04) VALUE ZERO.             
           05  PV-HOLD-UPC-ID                PIC S9(09)  COMP.                  
           05  PV-DISP-NBR                   PIC  9(15) VALUE ZEROES.           
           05  PV-DISP-UPC                   PIC  9(09) VALUE ZEROES.           
           05  PV-RCP-END-DTE                PIC  X(10) VALUE SPACES.           
           05  PV-COMPARE-DTE                PIC  X(10) VALUE SPACES.           
           05  PV-BEGIN-DATE                 PIC  X(10) VALUE SPACES.           
           05  PV-END-DATE                   PIC  X(10) VALUE SPACES.           
           05  PV-SAVE-30-BEG                PIC  X(10) VALUE SPACES.           
           05  PV-END-30-DATE                PIC  X(10) VALUE SPACES.           
           05  PV-END-25-DATE                PIC  X(10) VALUE SPACES.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-COMBINED-SW            PIC X  VALUE 'N'.                  
               88  PS-END-COMBINED                  VALUE 'Y'.                  
           05  PS-END-OF-CTRL-CARD-FILE-SW   PIC X  VALUE 'N'.                  
               88  PS-END-OF-CTRL-CARD-FILE         VALUE 'Y'.                  
           05  PS-SAVE-DATES-SW              PIC X  VALUE 'N'.                  
               88  PS-SAVE-DATES                    VALUE 'Y'.                  
           05  PS-CHECK-PERM-SW              PIC X  VALUE 'N'.                  
               88  PS-CHECK-PERM                    VALUE 'Y'.                  
           05  PS-CHECK-STAT20-SW            PIC X  VALUE 'N'.                  
               88  PS-CHECK-STAT20                  VALUE 'Y'.                  
           05  PS-WRITE-25-00-SW             PIC X  VALUE 'N'.                  
               88  PS-WRITE-25-00                   VALUE 'Y'.                  
           05  PS-WRITE-30-00-SW             PIC X  VALUE 'N'.                  
               88  PS-WRITE-30-00                   VALUE 'Y'.                  
           05  PS-SAVE-25-SW                 PIC X  VALUE 'N'.                  
               88  PS-SAVE-25                       VALUE 'Y'.                  
           05  PS-BACK-TO-20-SW              PIC X  VALUE 'N'.                  
               88  PS-BACK-TO-20                    VALUE 'Y'.                  
           05  PS-FOUND-30-SW                PIC X  VALUE 'N'.                  
               88  PS-FOUND-30                      VALUE 'Y'.                  
           05  PS-SINGLE-WRITE-SW            PIC X  VALUE 'N'.                  
               88  PS-SINGLE-WRITE                  VALUE 'Y'.                  
AS0815     05  PS-RCP-BCK-ACT-SW             PIC X  VALUE 'N'.                  
AS0815         88  PS-RCP-BCK-20                    VALUE 'Y'.                  
                                                                                
      *****************************************************************         
      * SAVE STATUS 25 RECORDS.                                       *         
      *****************************************************************         
       01  WS-RCP-25-DATA.                                                      
           05  WS-ITM-NAME.                                                     
               10  WS-DEPT-NBR          PIC  X(03) VALUE SPACE.                 
               10  DL-DASH1             PIC  X(01) VALUE '-'.                   
               10  WS-CLASS-NBR         PIC  X(02) VALUE SPACE.                 
               10  DL-DASH2             PIC  X(01) VALUE '-'.                   
NM0603         10  WS-SEQ-NBR           PIC  X(08) VALUE SPACE.                 
           05  WS-STAT-CDE              PIC  X(02) VALUE SPACES.                
           05  WS-STR-NBR               PIC S9(04) COMP VALUE ZEROES.           
           05  WS-STAT-BEG-DTE          PIC  X(10) VALUE SPACES.                
           05  WS-STAT-END-DTE          PIC  X(10) VALUE SPACES.                
DGM001     05  FILLER                   PIC  X(19) VALUE SPACES.                
                                                                                
      *****************************************************************.        
      * CALENDAR ROUTINE ABEND CODE.                                  *.        
      *****************************************************************.        
        01  ABEND-CODE                      PIC S9(04) VALUE ZERO          CL**6
                                                       COMP SYNC.          CL**6
            88 ABEND-4019                   VALUE +4019.                   CL**6
                                                                           CL**6
                                                                                
      *----------------------------------------------------------------*        
      * RECORD LAYOUT FOR THE REGIONAL CLEARANCE PRICING DATA EXTRACT  *        
      * FILE .                                                         *        
      *----------------------------------------------------------------*        
           COPY MARD040.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    COMBINED UNLAOD FILE TUPCPLS,TSIPREX,TSIPEPR.                        
      *----------------------------------------------------------------*        
           COPY MARD083.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    WORK AREA FOR THE ABEND ROUTINE.                                     
      *----------------------------------------------------------------*        
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    WORK AREA FOR THE CALENDAR ROUTINE.                                  
      *----------------------------------------------------------------*        
           COPY DPWS500.                                                        
                                                                                
       01  PV-SKU-DATA.                                                         
           05  PV-DEPT-NBR                   PIC X(03) VALUE SPACES.            
           05  PV-DASH-1                     PIC X(01) VALUE '-'.               
           05  PV-CLASS                      PIC X(02) VALUE SPACES.            
           05  PV-DASH-2                     PIC X(01) VALUE '-'.               
NM0603     05  PV-SEQ-NBR                    PIC X(08) VALUE SPACES.            
                                                                                
NM0603 01  PV-TSKXREF-DEPT                   PIC S9(04)V COMP-3.                
NM0603 01  PV-TSKXREF-SUBCLASS               PIC S9(04)V COMP-3.                
NM0603 01  PV-TSKXREF-SKU                    PIC S9(08)V COMP-3.                
NM0603 01  PV-DEPT-4                         PIC 9(04).                         
NM0603 01  PV-DEPT REDEFINES PV-DEPT-4.                                         
NM0603     05  PV-DEPT-SPACE                 PIC X(01).                         
NM0603     05  PV-DEPT-3                     PIC X(03).                         
NM0603 01  PV-SUBCLASS-4                     PIC 9(04).                         
NM0603 01  PV-SUBCLASS REDEFINES PV-SUBCLASS-4.                                 
NM0603     05  PV-SUBCLASS-SPACE             PIC X(02).                         
NM0603     05  PV-SUBCLASS-2                 PIC X(02).                         
NM0603 01  PV-SIGNIF-SKU-8                   PIC 9(08) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-7                   PIC 9(07) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-6                   PIC 9(06) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-5                   PIC 9(05) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-4                   PIC 9(04) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-3                   PIC 9(03) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-2                   PIC 9(02) VALUE ZEROS.             
NM0603 01  PV-SIGNIF-SKU-1                   PIC 9(01) VALUE ZEROS.             
NM0603 01  PV-EVALUATE-SKU                   PIC 9(08) VALUE ZEROS.             
       01  PV-ITEM-NBR                       PIC S9(15)V COMP-3.                
       01  PV-UPC-ID                         PIC S9(09)  COMP.                  
       01  PV-CENPPC-NBR                     PIC S9(08)V COMP-3.                
       01  PV-STR-GP-CDE                     PIC  X(01)  VALUE SPACES.          
       01  PV-CURRENT-DATE                   PIC  X(26)  VALUE SPACES.          
       01  PV-CURRENT-DATE-CMP               PIC  X(10)  VALUE SPACES.          
       01  PV-NEXT-DATE                      PIC  X(10)  VALUE SPACES.          
       01  PV-STAT20-BEG-DTE                 PIC  X(10)  VALUE SPACES.          
       01  PV-STAT30-END-DTE                 PIC  X(10)  VALUE SPACES.          
       01  PV-STAT25-BEG-DTE                 PIC  X(10)  VALUE SPACES.          
       01  PV-STAT30-BEG-DTE                 PIC  X(10)  VALUE SPACES.          
                                                                                
      *----------------------------------------------------------------*        
      *    SKU TABLE                                                   *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
NM0603         INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    TSIPEPR TABLE                                               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TSIPEPR                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    TCENPPC TABLE                                               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TCENPPC                                                  
           END-EXEC.                                                            
                                                                                
0922RM*---------------------------------------------------------------*         
0922RM*    TSIPREX TABLE                                               *        
0922RM*----------------------------------------------------------------*        
0922RM     EXEC SQL                                                             
0922RM         INCLUDE TSIPREX                                                  
0922RM     END-EXEC.                                                            
0922RM                                                                          
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA                                     *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           DISPLAY '*************************'.                                 
           DISPLAY '  BEGINING OF MAKUT040   '.                                 
                                                                                
           PERFORM 1000-INITIALIZE.                                             
           PERFORM 2000-INITIAL-READS.                                          
           PERFORM 5200-GET-ONE-DATE-AHEAD.                                     
           PERFORM 3000-PROCESS-COMBINED UNTIL PS-END-COMBINED.                 
                                                                                
      * MAKE SURE TO WRITE THE LAST RECORDS.                                    
      *    PERFORM 3010-PROCESS-LAST-RECORD.                                    
                                                                                
           PERFORM 1999-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'COMBINDED RECORDS READ : ' PV-COMBINED-COUNT.               
           DISPLAY 'EXTRACT RECORDS NO SKU : ' PV-EXTRACT-NO-SKU.               
           DISPLAY 'EXTRACT 25 HELD        : ' PV-EXTRACT-25-HELD.              
           DISPLAY 'EXTRACT 25 INSERTED    : ' PV-25-INSERTED.                  
           DISPLAY 'EXTRACT 30 INSERTED    : ' PV-30-INSERTED.                  
           DISPLAY 'EXTRACT RECORDS WRITTEN: ' PV-EXTRACT-RECS-WRITTEN.         
                                                                                
                                                                                
           DISPLAY '   ENDING OF MAKUT040    '.                                 
           DISPLAY '*************************'.                                 
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           PERFORM 1100-OPEN-FILES.                                             
           MOVE ZEROES TO PV-HOLD-UPC-ID.                                       
                                                                                
      ************************************************************              
      * 1100-OPEN-FILES.                                                        
      ************************************************************              
       1100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  RCP-COMBINED-FILE                                        
                       CTRL-CARD-FILE                                           
                OUTPUT RCP-DATA-EXTRACT-FILE.                                   
                                                                                
      ************************************************************              
      * 1999-CLOSE-FILES.                                                       
      ************************************************************              
       1999-CLOSE-FILES.                                                        
                                                                                
           CLOSE RCP-COMBINED-FILE                                              
                 CTRL-CARD-FILE                                                 
                 RCP-DATA-EXTRACT-FILE.                                         
                                                                                
      ************************************************************              
      * 2000-INITIAL-READS STAGE THE INPUT FILES.                               
      ************************************************************              
       2000-INITIAL-READS.                                                      
                                                                                
           READ CTRL-CARD-FILE INTO CC-CTRL-CARD                                
               AT END SET PS-END-OF-CTRL-CARD-FILE TO TRUE.                     
FPS403     MOVE PC-SET-INIT-TIME TO PV-CURRENT-DATE.                            
FPS403     MOVE CC-EFF-TMST TO PV-CURRENT-DATE(1:19).                           
FPS403**** MOVE CC-EFF-TMST TO PV-CURRENT-DATE.                                 
           MOVE PV-CURRENT-DATE(1:10) TO PV-CURRENT-DATE-CMP.                   
                                                                                
           DISPLAY 'CC-EFF-TMST         = ' CC-EFF-TMST.                        
           DISPLAY 'PV-CURRENT-DATE     = ' PV-CURRENT-DATE.                    
           DISPLAY 'PV-CURRENT-DATE-CMP = ' PV-CURRENT-DATE-CMP.                
                                                                                
           PERFORM 2100-READ-COMBINED.                                          
                                                                                
      ************************************************************              
      * 2100-READ-COMBINED.                                                     
      ************************************************************              
       2100-READ-COMBINED.                                                      
                                                                                
           READ RCP-COMBINED-FILE INTO MA083-RCP-COMBINED-RECORD                
             AT END SET PS-END-COMBINED TO TRUE.                                
           IF NOT PS-END-COMBINED                                               
              ADD 1 TO PV-COMBINED-COUNT                                        
           END-IF.                                                              
                                                                                
      ************************************************************              
      * 3000-PROCESS-COMBINED.                                                  
      ************************************************************              
       3000-PROCESS-COMBINED.                                                   
           IF MA083-UPC-ID = PV-HOLD-UPC-ID                                     
              PERFORM 3100-BUILD-EXTRACT                                        
              PERFORM 3400-CHECK-SAVE-DATES                                     
           ELSE                                                                 
              PERFORM 3440-CHECK-WRITE-30                                       
              PERFORM 3430-CHECK-WRITE-25                                       
              PERFORM 3450-CHECK-25-EXISTS                                      
              MOVE MA083-UPC-ID TO PV-HOLD-UPC-ID                               
              PERFORM 3025-RESET-SWITCHES-DATES                                 
              PERFORM 3050-SET-SWITCHES                                         
              PERFORM 8000-GET-SKU-INFO                                         
              PERFORM 3400-CHECK-SAVE-DATES                                     
              PERFORM 3420-CHECK-STAT20                                         
              PERFORM 3100-BUILD-EXTRACT                                        
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-COMBINED.                                          
                                                                                
      ************************************************************              
      * 3010-PROCESS-LAST-RECORD.                                               
      ************************************************************              
NM0603*3010-PROCESS-LAST-RECORD.                                                
NM0603*                                                                         
NM0603*    PERFORM 3100-BUILD-EXTRACT.                                          
NM0603*    PERFORM 3400-CHECK-SAVE-DATES.                                       
NM0603*    PERFORM 3440-CHECK-WRITE-30.                                         
NM0603*    PERFORM 3430-CHECK-WRITE-25.                                         
NM0603*    PERFORM 3450-CHECK-25-EXISTS.                                        
                                                                                
      ************************************************************              
      * 3025-RESET-SWITCHES-DATES.                                              
      ************************************************************              
       3025-RESET-SWITCHES-DATES.                                               
           MOVE PC-RESET-STAT25-BEG TO PV-STAT25-BEG-DTE.                       
           MOVE PC-RESET-STAT30-BEG TO PV-STAT30-BEG-DTE.                       
           MOVE SPACES              TO PV-COMPARE-DTE                           
                                       PV-STR-GP-CDE                            
                                       PV-RCP-END-DTE                           
                                       PV-BEGIN-DATE                            
                                       PV-END-DATE                              
                                       PV-SAVE-30-BEG                           
                                       PV-END-30-DATE                           
AS0815                                 PV-END-25-DATE.                          
                                                                                
           MOVE 'N' TO PS-SAVE-DATES-SW                                         
                       PS-CHECK-PERM-SW                                         
                       PS-CHECK-STAT20-SW                                       
                       PS-WRITE-25-00-SW                                        
                       PS-WRITE-30-00-SW                                        
                       PS-SAVE-25-SW                                            
                       PS-BACK-TO-20-SW                                         
                       PS-FOUND-30-SW                                           
                       PS-SINGLE-WRITE-SW                                       
AS0815                 PS-RCP-BCK-ACT-SW.                                       
      ************************************************************              
      * 3050-SET-SWITCHES.                                                      
ASLG04* REMOVE 3410-CHECK-PERM CALL.                                            
      ************************************************************              
       3050-SET-SWITCHES.                                                       
      * SET THE SAVE DATE SWITCH TO TRUE IF THE FIRST RECORD                    
      * IS NOT FROM TSIPEPR.                                                    
           IF MA083-REC-ID NOT EQUAL PC-TSIPEPR                                 
              SET PS-SAVE-DATES TO TRUE                                         
           END-IF.                                                              
                                                                                
      * SET THE WRITE SIGNLE DATA TO TRUE IF THE FIRST RECORD                   
      * IS FROM TUPCPLS. IT MEANS THERE ARE NO TSIPEPR AND TSIPREX.             
           IF (MA083-REC-ID EQUAL PC-TUPCPLS AND                                
               MA083-STR-NBR EQUAL PC-STR-ZERO AND                              
               MA083-UPC-STAT-CDE EQUAL PC-25)                                  
AS0815* ADDED CODE TO CHECK STATUS FOR THIS UPC UNDER TSIPEPR                   
AS0815* IF THERE IS FUTURE STATUS 20, THIS MEANS THIS UPC WILL                  
AS0815* FROM STATUS 25 CHANGE TO 20.                                            
AS0815         SET PS-CHECK-STAT20 TO TRUE                                      
AS0815         PERFORM 3420-CHECK-STAT20                                        
ASLG04*        IF NOT PS-BACK-TO-20                                             
ASLG04*          SET PS-CHECK-PERM TO TRUE                                      
ASLG04*          PERFORM 3410-CHECK-PERM                                        
ASLG04*        END-IF                                                           
           END-IF.                                                              
                                                                                
      * SET THE CHECK STAT20 SWITCH TO TRUE IF THE FIRST RECORD                 
      * IS NOT FROM TSIPEPR.                                                    
           IF MA083-REC-ID NOT EQUAL PC-TSIPEPR                                 
              SET PS-CHECK-STAT20 TO TRUE                                       
           END-IF.                                                              
                                                                                
      * SET THE WRITE 25 SWITCH TO TRUE IF THE FIRST RECORD                     
      * IS  FROM TUPCPLS AND IS NOT ZERO STORE.                                 
           IF (MA083-REC-ID EQUAL PC-TUPCPLS AND                                
               MA083-UPC-STAT-CDE EQUAL PC-30 AND                               
               MA083-STR-NBR NOT EQUAL PC-STR-ZERO)                             
                 SET PS-WRITE-25-00 TO TRUE                                     
           END-IF.                                                              
                                                                                
      * SET THE WRITE 25 SWITCH TO TRUE IF THE FIRST RECORD                     
      * IS NOT FROM TSIPEPR AND THE STATUS IS NOT 25.                           
           IF (MA083-REC-ID EQUAL PC-TSIPREX)                                   
ASLG04*       SET PS-CHECK-PERM TO TRUE                                         
ASLG04*       PERFORM 3410-CHECK-PERM                                           
              IF PS-WRITE-25-00                                                 
                 SET PS-WRITE-25-00 TO TRUE                                     
              ELSE                                                              
                 SET PS-SINGLE-WRITE TO TRUE                                    
              END-IF                                                            
           END-IF.                                                              
      ************************************************************              
      * 3100-BUILD-EXTRACT.                                                     
      ************************************************************              
       3100-BUILD-EXTRACT.                                                      
                                                                                
           MOVE SPACES TO MA040-RCP-DATA.                                       
                                                                                
           IF SQLCODE = ZERO                                                    
              MOVE PV-STAT20-BEG-DTE TO PV-RCP-END-DTE                          
           ELSE                                                                 
              MOVE SPACES TO PV-RCP-END-DTE.                                    
                                                                                
           PERFORM 4000-BUILD-EXTRACT.                                          
                                                                                
      ************************************************************              
      * 3400-CHECK-SAVE-DATES.                                                  
      ************************************************************              
       3400-CHECK-SAVE-DATES.                                                   
                                                                                
      * SAVE THE BEGIN DATES TO BE USED WHEN INSERTING EITHER                   
      * STATUS 25 OR 30 RECORDS.                                                
              IF PS-SAVE-DATES                                                  
                 PERFORM 5000-SAVE-BEG-DATES                                    
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3410-CHECK-PERM.                                                        
ASLG04* THIS CODE IS NOT EXECUTED.                                              
      ************************************************************              
       3410-CHECK-PERM.                                                         
                                                                                
      * CHECK THE PERM TABLES TO SEE IF THERE IS A FUTURE ALL                   
      * STORE CLEARENCE DATE.                                                   
              IF PS-CHECK-PERM                                                  
                 PERFORM 3500-SELECT-PERM-ROUTE                                 
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3420-CHECK-STAT20.                                                      
      ************************************************************              
       3420-CHECK-STAT20.                                                       
                                                                                
      * CHECK THE WINSIGN TABLES TO DETERMINE IF THE SKU IS GOING               
      * BACK TO ACTIVE STATUS IN TEH FUTURE.                                    
              IF PS-CHECK-STAT20                                                
                 PERFORM 8300-GET-STAT-20                                       
                 IF SQLCODE = ZERO                                              
                    MOVE PV-STAT20-BEG-DTE TO PV-BEGIN-DATE                     
                    PERFORM 5100-GET-ONE-DATE-BACK                              
AS0815* CHECK IF CURRENT UPC STATUS IS 25. IF IT IS TRUE, THIS IS               
AS0815* 25 BACK TO 20 CASE.                                                     
AS0815              IF MA083-UPC-STAT-CDE = 25                                  
AS0815                 MOVE PV-END-DATE TO PV-END-25-DATE                       
AS0815                 SET PS-RCP-BCK-20 TO TRUE                                
AS0815              ELSE                                                        
AS0815                 MOVE PV-END-DATE TO PV-END-30-DATE                       
AS0815              END-IF                                                      
                 END-IF                                                         
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3430-CHECK-WRITE-25                                                     
      ************************************************************              
       3430-CHECK-WRITE-25.                                                     
                                                                                
      * WRITE A STATUS 25 RECORD.                                               
              IF PS-WRITE-25-00                                                 
                 PERFORM 4100-BUILD-STATUS-25-RECORD                            
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3440-CHECK-WRITE-30                                                     
      ************************************************************              
       3440-CHECK-WRITE-30.                                                     
                                                                                
      * WRITE A STATUS 30 RECORD.                                               
              IF PS-WRITE-30-00                                                 
                 PERFORM 4200-BUILD-STATUS-30-RECORD                            
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3450-CHECK-25-EXISTS.                                                   
      ************************************************************              
       3450-CHECK-25-EXISTS.                                                    
                                                                                
      * WRITE A STATUS 25 RECORD.                                               
              IF PS-SAVE-25                                                     
                 PERFORM 4100-BUILD-STATUS-25-RECORD                            
              END-IF.                                                           
                                                                                
      ************************************************************              
      * 3500-SELECT-PERM-ROUTE.                                                 
ASLG04* THIS CODE IS NOT EXECUTED.                                              
      ************************************************************              
       3500-SELECT-PERM-ROUTE.                                                  
                                                                                
           IF MA083-REC-ID EQUAL PC-TSIPREX                                     
              PERFORM 8400-GET-PERM-RECORD                                      
           ELSE                                                                 
              PERFORM 8450-GET-TUPCPLS-PERM-RECORD                              
           END-IF.                                                              
                                                                                
      ************************************************************              
      * 4000-BUILD-EXTRACT                                                      
      ************************************************************              
       4000-BUILD-EXTRACT.                                                      
                                                                                
           MOVE PV-SKU-DATA        TO MA040-ITM-NAME.                           
           MOVE MA083-STR-NBR      TO MA040-STR-NBR.                            
           MOVE MA083-UPC-STAT-CDE TO MA040-STAT-CDE.                           
                                                                                
      * BASED ON THE RECORD ID POPULATE THE OUTPUT WITH THE                     
      * APPROPRIATE DATA.                                                       
                                                                                
           IF MA083-REC-ID = PC-TUPCPLS                                         
              MOVE MA083-UPC-STAT-CHG-DTE TO MA040-STAT-BEG-DTE                 
           ELSE                                                                 
              MOVE MA083-BEG-DTE    TO MA040-STAT-BEG-DTE                       
           END-IF.                                                              
                                                                                
           IF MA083-REC-ID = PC-TUPCPLS                                         
              IF MA083-STR-NBR = PC-STR-ZERO                                    
                 MOVE PV-RCP-END-DTE TO MA040-STAT-END-DTE                      
              END-IF                                                            
           ELSE                                                                 
               IF MA083-END-DTE(1:4) = '9999'                                   
                  MOVE SPACES TO MA040-STAT-END-DTE                             
               ELSE                                                             
                  MOVE MA083-END-DTE    TO MA040-STAT-END-DTE                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MA040-STAT-CDE EQUAL PC-30 AND                                   
               MA040-STR-NBR = PC-STR-ZERO)                                     
               MOVE MA040-STAT-BEG-DTE TO PV-SAVE-30-BEG                        
               SET PS-FOUND-30 TO TRUE                                          
           END-IF.                                                              
                                                                                
           IF PS-BACK-TO-20 AND                                                 
              (MA040-STR-NBR EQUAL PC-STR-ZERO)                                 
              MOVE PV-END-30-DATE TO MA040-STAT-END-DTE                         
           END-IF.                                                              
                                                                                
AS0815     IF PS-RCP-BCK-20                                                     
AS0815        MOVE PV-END-25-DATE TO MA040-STAT-END-DTE                         
AS0815     END-IF.                                                              
                                                                                
           IF MA040-STAT-CDE = PC-25 AND                                        
              NOT PS-RCP-BCK-20                                                 
              MOVE MA040-RCP-DATA TO WS-RCP-25-DATA                             
              SET PS-SAVE-25 TO TRUE                                            
              ADD 1 TO PV-EXTRACT-25-HELD                                       
           ELSE                                                                 
              PERFORM 6000-WRITE-EXTRACT                                        
           END-IF.                                                              
                                                                                
      ************************************************************              
      * 4100-BUILD-STATUS-25-RECORD.                                            
      ************************************************************              
       4100-BUILD-STATUS-25-RECORD.                                             
                                                                                
           IF PS-SAVE-25                                                        
              MOVE WS-RCP-25-DATA     TO MA040-RCP-DATA                         
           ELSE                                                                 
              MOVE SPACES             TO MA040-RCP-DATA                         
              MOVE PV-SKU-DATA        TO MA040-ITM-NAME                         
              MOVE PC-STR-ZERO        TO MA040-STR-NBR                          
              MOVE PC-25              TO MA040-STAT-CDE                         
              MOVE PV-STAT25-BEG-DTE  TO MA040-STAT-BEG-DTE                     
           END-IF.                                                              
                                                                                
           IF PS-BACK-TO-20                                                     
              MOVE PV-END-30-DATE TO MA040-STAT-END-DTE                         
           ELSE                                                                 
              IF PS-WRITE-30-00 OR PS-FOUND-30                                  
                 MOVE PV-SAVE-30-BEG TO PV-BEGIN-DATE                           
                 PERFORM 5100-GET-ONE-DATE-BACK                                 
                 MOVE PV-END-DATE TO MA040-STAT-END-DTE                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO PV-25-INSERTED                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT.                                          
                                                                                
      ************************************************************              
      * 4200-BUILD-STATUS-30-RECORD.                                            
      ************************************************************              
       4200-BUILD-STATUS-30-RECORD.                                             
                                                                                
           MOVE SPACES TO MA040-RCP-DATA.                                       
                                                                                
           MOVE PV-SKU-DATA        TO MA040-ITM-NAME.                           
           MOVE PC-STR-ZERO        TO MA040-STR-NBR.                            
           MOVE PC-30              TO MA040-STAT-CDE.                           
           MOVE PV-STAT30-BEG-DTE  TO MA040-STAT-BEG-DTE.                       
      * SAVE THE STAT30 BEG-DTE FOR USE ON THE STAT25 RECORD.                   
           MOVE PV-STAT30-BEG-DTE  TO PV-SAVE-30-BEG.                           
           ADD 1 TO PV-30-INSERTED                                              
                                                                                
           IF PS-BACK-TO-20                                                     
              MOVE PV-END-30-DATE TO MA040-STAT-END-DTE                         
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT.                                          
                                                                                
      ************************************************************              
      * 5000-SAVE-BEG-DATES.                                                    
      ************************************************************              
       5000-SAVE-BEG-DATES.                                                     
                                                                                
      * IF THE RECORD IS FROM TUPCPLS USE THE STATUS CHANGED DATE               
      * OF THE COMPARE.                                                         
           IF MA083-REC-ID = PC-TUPCPLS                                         
              MOVE MA083-UPC-STAT-CHG-DTE TO PV-COMPARE-DTE                     
           ELSE                                                                 
              MOVE MA083-BEG-DTE TO PV-COMPARE-DTE                              
           END-IF.                                                              
                                                                                
      * SAVE THE LOWEST DATE FOR THE STATUS 25 STORE 0000 START DATE.           
           IF PV-COMPARE-DTE < PV-STAT25-BEG-DTE                                
              MOVE PV-COMPARE-DTE TO PV-STAT25-BEG-DTE                          
           END-IF.                                                              
                                                                                
      * SAVE THE HIGHEST DATE FOR THE STATUS 30 STORE 0000 START DATE.          
           IF PV-COMPARE-DTE > PV-STAT30-BEG-DTE                                
              MOVE PV-COMPARE-DTE TO PV-STAT30-BEG-DTE                          
           END-IF.                                                              
                                                                                
      ******************************************************************   CL**6
      *  5100-GET-ONE-DATE-BACK.                                       *   CL**6
      *  USE THE CALENDAR ROUTINE TO OBTAIN THE PREVIOUS DATE.         *   CL**6
      ******************************************************************   CL**6
       5100-GET-ONE-DATE-BACK.                                             CL**6
                                                                           CL**6
           INITIALIZE DPG51 DPG52                                          CL**6
                      DPG53 DPG54                                          CL**6
                      DPG55 DPG56                                          CL**6
                                                                           CL**6
           SET DPG51-DECREMENT-DATE        TO TRUE.                             
           MOVE 1                          TO  DPG51-INCR-DECR-DAYS-9.          
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                        CL**6
           SET DPG51-FISCAL-JAN-DEC-F2     TO TRUE.                        CL**6
           MOVE PV-BEGIN-DATE              TO DPG52-LK-DATE-INPUT.         CL**6
               CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52             
                                                        DPG53 DPG54             
                                                        DPG55 DPG56             
               IF ( (DPG54-SEVERE-ERROR)                                        
                 OR (DPG54-WARNING-ERROR AND DPG54-DATE-INVALID) )              
                   DISPLAY '** ABEND **'                                        
                   DISPLAY ' MAKUT040'                                          
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR ON DECREMENT DAYS FROM DATE.'             
                   DISPLAY ' MAKUT040 '                                         
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR INDICATOR = '  DPG54-ERROR-IND            
                   DISPLAY ' MAKUT040 '                                         
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR MESSAGE = '  DPG54-ERROR-MESSAGE          
                   SET  ABEND-4019  TO  TRUE                                    
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT  TO PV-END-DATE.                    CL**6
      ******************************************************************   CL**6
      *  5200-GET-ONE-DATE-AHEAD                                       *   CL**6
      *  USE THE CALENDAR ROUTINE TO OBTAIN THE NEXT DATE.             *   CL**6
      ******************************************************************   CL**6
       5200-GET-ONE-DATE-AHEAD.                                            CL**6
                                                                           CL**6
           INITIALIZE DPG51 DPG52                                          CL**6
                      DPG53 DPG54                                          CL**6
                      DPG55 DPG56                                          CL**6
                                                                           CL**6
           SET DPG51-INCREMENT-DATE        TO TRUE.                             
           MOVE 1                          TO  DPG51-INCR-DECR-DAYS-9.          
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                        CL**6
           SET DPG51-FISCAL-JAN-DEC-F2     TO TRUE.                        CL**6
           MOVE PV-CURRENT-DATE-CMP        TO DPG52-LK-DATE-INPUT.         CL**6
               CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52             
                                                        DPG53 DPG54             
                                                        DPG55 DPG56             
               IF ( (DPG54-SEVERE-ERROR)                                        
                 OR (DPG54-WARNING-ERROR AND DPG54-DATE-INVALID) )              
                   DISPLAY '** ABEND **'                                        
                   DISPLAY ' MAKUT040'                                          
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR ON INCREASE DAYS FROM DATE.'              
                   DISPLAY ' MAKUT040 '                                         
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR INDICATOR = '  DPG54-ERROR-IND            
                   DISPLAY ' MAKUT040 '                                         
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR MESSAGE = '  DPG54-ERROR-MESSAGE          
                   DISPLAY ' INPUT DATE = ' PV-CURRENT-DATE-CMP                 
                   SET  ABEND-4019  TO  TRUE                                    
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT  TO PV-NEXT-DATE.                   CL**6
      ************************************************************              
      * 6000-WRITE-EXTRACT.                                                     
      ************************************************************              
       6000-WRITE-EXTRACT.                                                      
                                                                                
           MOVE SPACES TO RCP-DATA-EXTRACT-REC.                                 
                                                                                
           IF MA040-ITM-NAME(1:3) NOT EQUAL SPACES                              
              WRITE RCP-DATA-EXTRACT-REC FROM MA040-RCP-DATA                    
              ADD 1 TO PV-EXTRACT-RECS-WRITTEN                                  
           ELSE                                                                 
              ADD 1 TO PV-EXTRACT-NO-SKU                                        
           END-IF.                                                              
                                                                                
           MOVE SPACES TO MA040-RCP-DATA.                                       
                                                                                
      ************************************************************              
      * 8000-GET-SKU-INFO.                                                      
      ************************************************************              
       8000-GET-SKU-INFO.                                                       
                                                                                
           IF MA083-REC-ID = PC-TUPCPLS                                         
              PERFORM 8100-GET-SKU                                              
           ELSE                                                                 
              PERFORM 8200-GET-SKU-UPCPLS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ************************************************************              
      * 8100-GET-SKU.                                                           
NM0603* STRAIGHT CALL TO TSKXREF : ITM_NBR IS KNOWN.                            
      ************************************************************              
       8100-GET-SKU.                                                            
                                                                                
           MOVE '8100-GET-SKU' TO PV-ABEND-PARAGRAPH.                           
           MOVE MA083-ITM-NBR TO PV-ITEM-NBR.                                   
           EXEC SQL                                                             
NM0603         SELECT  XREF.DEPT                                                
NM0603                ,XREF.SUBCLASS                                            
NM0603                ,XREF.SKU                                                 
NM0603           INTO  :PV-TSKXREF-DEPT                                         
NM0603                ,:PV-TSKXREF-SUBCLASS                                     
NM0603                ,:PV-TSKXREF-SKU                                          
NM0603           FROM  TSKXREF XREF                                             
NM0603          WHERE  XREF.ITM_NBR = :PV-ITEM-NBR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
NM0603         MOVE PV-TSKXREF-SKU      TO PV-EVALUATE-SKU                      
NM0603* PARSE THROUGH THE DIGITS, REMOVE THE LEADING ZEROES                     
NM0603         EVALUATE TRUE                                                    
NM0603           WHEN PV-EVALUATE-SKU > 9999999                                 
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-8                   
NM0603                MOVE PV-SIGNIF-SKU-8 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 999999                                  
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-7                   
NM0603                MOVE PV-SIGNIF-SKU-7 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 99999                                   
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-6                   
NM0603                MOVE PV-SIGNIF-SKU-6 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 9999                                    
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-5                   
NM0603                MOVE PV-SIGNIF-SKU-5 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 999                                     
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-4                   
NM0603                MOVE PV-SIGNIF-SKU-4 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 99                                      
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-3                   
NM0603                MOVE PV-SIGNIF-SKU-3 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 9                                       
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-2                   
NM0603                MOVE PV-SIGNIF-SKU-2 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 0                                       
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-1                   
NM0603                MOVE PV-SIGNIF-SKU-1 TO PV-SEQ-NBR                        
NM0603         END-EVALUATE                                                     
NM0603         MOVE PV-TSKXREF-DEPT     TO PV-DEPT-4                            
NM0603         MOVE PV-DEPT-3           TO PV-DEPT-NBR                          
NM0603         MOVE PV-TSKXREF-SUBCLASS TO PV-SUBCLASS-4                        
NM0603         MOVE PV-SUBCLASS-2       TO PV-CLASS                             
             WHEN SQLCODE = 0100                                                
               MOVE PV-ITEM-NBR TO PV-DISP-NBR                                  
ASLG04*        DISPLAY ' NOT SKU INFORMATION FOUND FOR: ' PV-DISP-NBR           
             WHEN OTHER                                                         
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM Z999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * 8200-GET-SKU-UPCPLS                                                     
NM0603* JOIN TSKXREF AND TUPCLS. ITM-NBR IS NOT KNOWN                           
ASLG04* REMOVE JOIN TO TUPCPLS.                                                 
      ************************************************************              
       8200-GET-SKU-UPCPLS.                                                     
                                                                                
           MOVE '8200-GET-SKU-UPCPLS' TO PV-ABEND-PARAGRAPH.                    
           MOVE MA083-UPC-ID  TO PV-UPC-ID.                                     
                                                                                
           EXEC SQL                                                             
NM0603         SELECT  XREF.DEPT                                                
NM0603                ,XREF.SUBCLASS                                            
NM0603                ,XREF.SKU                                                 
NM0603                ,XREF.ITM_NBR                                             
NM0603           INTO  :PV-TSKXREF-DEPT                                         
NM0603                ,:PV-TSKXREF-SUBCLASS                                     
NM0603                ,:PV-TSKXREF-SKU                                          
                      ,:PV-ITEM-NBR                                             
NM0603           FROM  TSKXREF XREF                                             
ASLG04          WHERE  XREF.INTR_UPC_ID = :PV-UPC-ID                            
ASLG04          AND    XREF.SKU_STAT_CDE <> '98'                                
ASLG04          AND    XREF.SKU_STAT_CDE <> '00'                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
NM0603         MOVE PV-TSKXREF-SKU TO PV-EVALUATE-SKU                           
NM0603* PARSE THROUGH THE DIGITS, REMOVE THE LEADING ZEROES                     
NM0603         EVALUATE TRUE                                                    
NM0603           WHEN PV-EVALUATE-SKU > 9999999                                 
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-8                   
NM0603                MOVE PV-SIGNIF-SKU-8 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 999999                                  
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-7                   
NM0603                MOVE PV-SIGNIF-SKU-7 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 99999                                   
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-6                   
NM0603                MOVE PV-SIGNIF-SKU-6 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 9999                                    
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-5                   
NM0603                MOVE PV-SIGNIF-SKU-5 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 999                                     
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-4                   
NM0603                MOVE PV-SIGNIF-SKU-4 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 99                                      
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-3                   
NM0603                MOVE PV-SIGNIF-SKU-3 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 9                                       
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-2                   
NM0603                MOVE PV-SIGNIF-SKU-2 TO PV-SEQ-NBR                        
NM0603           WHEN PV-EVALUATE-SKU > 0                                       
NM0603                MOVE PV-EVALUATE-SKU TO PV-SIGNIF-SKU-1                   
NM0603                MOVE PV-SIGNIF-SKU-1 TO PV-SEQ-NBR                        
NM0603         END-EVALUATE                                                     
NM0603         MOVE PV-TSKXREF-DEPT     TO PV-DEPT-4                            
NM0603         MOVE PV-DEPT-3           TO PV-DEPT-NBR                          
NM0603         MOVE PV-TSKXREF-SUBCLASS TO PV-SUBCLASS-4                        
NM0603         MOVE PV-SUBCLASS-2       TO PV-CLASS                             
             WHEN SQLCODE = 0100                                                
               MOVE SPACES TO PV-DEPT-NBR                                       
               MOVE SPACES TO PV-CLASS                                          
               MOVE SPACES TO PV-SEQ-NBR                                        
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
ASLG04*        DISPLAY ' NOT SKU INFORMATION FOUND FOR: ' PV-DISP-UPC           
             WHEN OTHER                                                         
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
               DISPLAY ' ABEND ON UPC ID: ' PV-DISP-UPC                         
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM Z999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * 8300-GET-STAT-20.                                                       
      * GET STATUS 20 RECORD FROM TSIPEPR.                                      
      ************************************************************              
       8300-GET-STAT-20.                                                        
                                                                                
           MOVE '8300-GET-STAT-20' TO PV-ABEND-PARAGRAPH.                       
           MOVE MA083-UPC-ID TO PV-UPC-ID.                                      
                                                                                
           EXEC SQL                                                             
               SELECT  PEPR.BEG_DTE                                             
                 INTO  :PV-STAT20-BEG-DTE                                       
                 FROM  TSIPEPR PEPR                                             
                WHERE  PEPR.UPC_ID = :PV-UPC-ID                                 
                 AND   PEPR.UPC_STAT_CDE = '20'                                 
                 AND   PEPR.LAST_CHG_TMST =                                     
                       (SELECT MAX(B.LAST_CHG_TMST)                             
                          FROM TSIPEPR B                                        
                         WHERE  B.UPC_ID       = PEPR.UPC_ID                    
                           AND  B.UPC_STAT_CDE = '20'                           
                           AND  B.UPC_STAT_CDE = PEPR.UPC_STAT_CDE)             
                       WITH UR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
      *        CONTINUE                                                         
               SET PS-BACK-TO-20 TO TRUE                                        
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
               DISPLAY PV-DISP-UPC ' GOES TO 20 ON ' PV-STAT20-BEG-DTE          
             WHEN SQLCODE = 0100                                                
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
             WHEN OTHER                                                         
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM Z999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * 8400-GET-PERM-RECORD.                                                   
      * GET THE DOCUMENT RECORD FROM PERM IF IT EXISTS.                         
ASLG04* THIS CODE IS NOT EXECUTED.                                              
      ************************************************************              
       8400-GET-PERM-RECORD.                                                    
           MOVE '8400-GET-PERM-RECORD' TO PV-ABEND-PARAGRAPH.                   
           MOVE MA083-UPC-ID          TO PV-UPC-ID.                             
           MOVE MA083-MSTR-CENPPC-NBR TO PV-CENPPC-NBR.                         
                                                                                
           EXEC SQL                                                             
               SELECT  CENP.STR_GP_CDE                                          
                 INTO  :PV-STR-GP-CDE                                           
                 FROM  TCENPPC CENP                                             
                WHERE  CENP.CENPPC_NBR = :PV-CENPPC-NBR                         
                  AND  CENP.STR_GP_CDE IN ('B', 'D', 'N')                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               IF MA083-BEG-DTE > PV-NEXT-DATE                                  
                  MOVE 'Y' TO PS-WRITE-25-00-SW                                 
                  MOVE 'Y' TO PS-WRITE-30-00-SW                                 
               ELSE                                                             
                  CONTINUE                                                      
               END-IF                                                           
             WHEN SQLCODE = 0100                                                
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
               MOVE 'Y'       TO PS-WRITE-25-00-SW                              
             WHEN OTHER                                                         
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM Z999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * 8450-GET-TUPCPLS-PERM-RECORD                                            
      * GET THE DOCUMENT RECORD FROM PERM IF IT EXISTS.                         
ASLG04* THIS CODE IS NOT EXECUTED.                                              
      ************************************************************              
       8450-GET-TUPCPLS-PERM-RECORD.                                            
           MOVE '8450-GET-TUPCPLS-PERM-RECORD' TO PV-ABEND-PARAGRAPH.           
           MOVE MA083-UPC-ID          TO PV-UPC-ID.                             
                                                                                
           EXEC SQL                                                             
               SELECT  DISTINCT                                                 
                       CENP.STR_GP_CDE                                          
                 INTO  :PV-STR-GP-CDE                                           
                 FROM  TCENPPC CENP                                             
                      ,TSIPREX PREX                                             
                WHERE  PREX.UPC_ID = :PV-UPC-ID                                 
                  AND  CENP.CENPPC_NBR = PREX.MSTR_CENPPC_NBR                   
                  AND  CENP.STR_GP_CDE IN ('B', 'D', 'N')                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               MOVE 'Y'       TO PS-WRITE-30-00-SW                              
               CONTINUE                                                         
             WHEN SQLCODE = 0100                                                
               MOVE PV-UPC-ID TO PV-DISP-UPC                                    
               MOVE 'Y'       TO PS-SINGLE-WRITE-SW                             
             WHEN OTHER                                                         
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM Z999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      * Z999-SQL-ABEND                                                          
      ************************************************************              
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-ABEND-CODE.                                       
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  SQLODE    **  = ' PV-ABEND-CODE.                        
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                          
           DISPLAY '**  PARAGRAPH **  = ' PV-ABEND-PARAGRAPH.                   
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
