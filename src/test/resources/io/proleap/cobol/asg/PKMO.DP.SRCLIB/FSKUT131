       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              FSKUT131.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/01/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *             TUPC AND SKU/LOC DOMAIN MERGE                      *        
      *             STYLE SUMMARY EXTRACT                              *        
      *                                                                *        
      *   THIS PROGRAM READS AN INPUT FILE OF ITEMS UNLOADED FROM TUPC *        
      *   AND MERGES THEM WITH RECORDS FROM THE SKU/LOC DOMAIN FILE.   *        
      *   THE SKU/LOC RECORDS ARE IN A FILE CREATED NIGHTLY BY IMK019, *        
      *   THAT CONTAINS ALL SKU/LOCS WHERE THE EFF_END_DTE IS NULL.    *        
      *                                                                *        
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  11/07/04 - BOB FORD                                                    
      *             (WR #26357) NEW PROGRAM - CLONED PROGRAM IMKUT080           
      ******************************************************************        
      *  06/03/09 - T. KONTAROVICH WR #36383                                    
      *    THE RECORD'S LENGTH OF A SKU/LOCATION EXTRACT FILE                   
      *    (COPYBOOK IMRD057) IS CHANGED TO 173 BYTES.                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT UPC-UNLD-FILE                                                 
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITEM-LOC-FILE                                                 
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ITEM-SUMMARY-FILE-OUT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  UPC-UNLD-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 72 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  UPC-UNLD-REC                    PIC  X(72).                          
                                                                                
       FD  ITEM-LOC-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W35383*    RECORD CONTAINS 250 CHARACTERS                                       
W35383     RECORD CONTAINS 173 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
W35383*01  ITEM-LOC-REC                    PIC  X(250).                         
W35383 01  ITEM-LOC-REC                    PIC  X(173).                         
                                                                                
       FD  ITEM-SUMMARY-FILE-OUT                                                
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 102 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ITEM-SUMMARY-REC.                                     
       01  ITEM-SUMMARY-REC       PIC X(102).                                   
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'FSKUT131'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-ITEM-NUMBER          PIC S9(15)V     VALUE ZERO               
                                                       COMP-3.                  
           05  PV-UPC-INTR-UPC-ID      PIC S9(9)       VALUE +0.                
           05  PV-UPCS-READ-CNT        PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-ITEM-LOC-READ-CNT    PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-RECS-WRITTEN-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-OUTPUT-MAX           PIC S9(7)      VALUE +100000             
                                                       COMP.                    
           05  PV-OUTPUT-CNT           PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-9-4                  PIC 9(04).                               
           05  PV-X-4 REDEFINES PV-9-4 PIC X(04).                               
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-UPC-UNLD-EOF-SW          PIC X       VALUE 'N'.               
               88  PS-UPC-UNLD-EOF                     VALUE 'Y'.               
           05  PS-ITEM-LOC-EOF-SW          PIC X       VALUE 'N'.               
               88  PS-ITEM-LOC-EOF                     VALUE 'Y'.               
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE ITEM/LOC DOMAIN FILE                               
      ******************************************************************        
           COPY IMRD057.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE TUPC UNLOAD INPUT FILE                             
      ******************************************************************        
           COPY FSRD119.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE MERGED OUTPUT FILE                                 
      ******************************************************************        
           COPY FSRD120.                                                        
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF FSKUT131               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-READ-UPC-UNLD-FILE.                                     
           IF PS-UPC-UNLD-EOF                                                   
              DISPLAY '***********************************************'         
              DISPLAY '*** NO REGIONAL CLEARANCE PRICED SKUS FOUND ***'         
              DISPLAY '***********************************************'         
           ELSE                                                                 
              PERFORM 2100-READ-ITEM-LOC-FILE                                   
              PERFORM 2200-PROCESS-UPC-UNLDS                                    
                  UNTIL PS-UPC-UNLD-EOF                                         
           END-IF.                                                              
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* FSKUT131 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL SKUS READ: ' PV-UPCS-READ-CNT.                      
           DISPLAY '* TOTAL ITEM-LOC READ: ' PV-ITEM-LOC-READ-CNT.              
           DISPLAY '* TOTAL OUTPUT RECORDS: '                                   
                                         PV-RECS-WRITTEN-CNT.                   
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF FSKUT131                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & DETERMINE CURRENT DATE CCYY-MM-DD.                      
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  UPC-UNLD-FILE                                            
                       ITEM-LOC-FILE                                            
                OUTPUT ITEM-SUMMARY-FILE-OUT.                                   
                                                                                
           INITIALIZE IM057-ITEM-SKU-LOC-DOMAIN-REC.                            
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE UPC-UNLD-FILE                                                  
                 ITEM-LOC-FILE                                                  
                 ITEM-SUMMARY-FILE-OUT.                                         
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE UPC-UNLD-FILE.                              
      ******************************************************************        
       2000-READ-UPC-UNLD-FILE.                                                 
                                                                                
           READ UPC-UNLD-FILE                                                   
               INTO FS119-FS-TUPC-RECORD                                        
               AT END                                                           
                   SET PS-UPC-UNLD-EOF TO TRUE                                  
               NOT AT END                                                       
                   ADD +1 TO PV-UPCS-READ-CNT                                   
                   MOVE FS119-INTR-UPC-ID TO PV-UPC-INTR-UPC-ID.                
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE ITEM-LOC-FILE.                              
      ******************************************************************        
       2100-READ-ITEM-LOC-FILE.                                                 
                                                                                
           READ ITEM-LOC-FILE                                                   
               INTO IM057-ITEM-SKU-LOC-DOMAIN-REC                               
               AT END                                                           
                   MOVE ZERO TO IM057-INTR-UPC-ID                               
                   SET PS-ITEM-LOC-EOF TO TRUE                                  
               NOT AT END                                                       
                   ADD +1 TO PV-ITEM-LOC-READ-CNT.                              
      ******************************************************************        
      *    PROCESS ALL REGIONAL CLEARANCE PRICED SKUS.                          
      ******************************************************************        
       2200-PROCESS-UPC-UNLDS.                                                  
                                                                                
           IF IM057-INTR-UPC-ID   = FS119-INTR-UPC-ID                           
               MOVE FS119-SUB-CL-MDSEAR-ID TO FS120-SUB-CL-MDSEAR-ID            
               MOVE FS119-VS-ID            TO FS120-VS-ID                       
               MOVE FS119-INTR-UPC-ID      TO FS120-INTR-UPC-ID                 
               MOVE FS119-KOHLS-SIZE-CDE   TO FS120-KOHLS-SIZE-CDE              
               MOVE FS119-CSTM-CLOR-DESC   TO FS120-CSTM-CLOR-DESC              
               MOVE FS119-GP-CDE           TO FS120-GP-CDE                      
               MOVE FS119-RPLNMT-IND       TO FS120-RPLNMT-IND                  
               MOVE FS119-KY-ITM-IND       TO FS120-KY-ITM-IND                  
               MOVE FS119-SEA-SEQ-NBR      TO FS120-SEA-SEQ-NBR                 
               MOVE FS119-REGN-PRIC-IND    TO FS120-REGN-PRIC-IND               
               MOVE FS119-INTR-UPC-DESC    TO FS120-INTR-UPC-DESC               
               MOVE FS119-ITM-NBR          TO FS120-ITM-NBR                     
               MOVE IM057-INTR-UPC-ID      TO FS120-INTR-UPC-ID                 
               MOVE IM057-STR-NBR          TO FS120-STR-NBR                     
               MOVE IM057-SKU-STAT-CDE     TO FS120-UPC-STAT-CDE                
               MOVE IM057-STAT-CHG-DTE     TO FS120-STAT-CHG-DTE                
               MOVE IM057-UNT-COST-AMT     TO FS120-UNT-COST-AMT                
               MOVE IM057-UNT-RTL-AMT      TO FS120-UNT-RTL-AMT                 
               MOVE IM057-MEITGP-NBR       TO FS120-MEITGP-NBR                  
               PERFORM 5000-WRITE-OUTPUT-REC                                    
           END-IF.                                                              
           IF NOT PS-ITEM-LOC-EOF                                               
              AND (IM057-INTR-UPC-ID   < FS119-INTR-UPC-ID OR                   
                   IM057-INTR-UPC-ID   = FS119-INTR-UPC-ID)                     
               PERFORM 2100-READ-ITEM-LOC-FILE.                                 
           IF IM057-INTR-UPC-ID   > FS119-INTR-UPC-ID                           
             OR PS-ITEM-LOC-EOF                                                 
               PERFORM 2000-READ-UPC-UNLD-FILE.                                 
      ******************************************************************        
      *    WRITE SUMMARY RECORDS                                                
      ******************************************************************        
       5000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE ITEM-SUMMARY-REC FROM FS120-FS-MERGED-RECORD.                  
           ADD +1 TO PV-OUTPUT-CNT                                              
                     PV-RECS-WRITTEN-CNT.                                       
      *    IF PV-OUTPUT-CNT > PV-OUTPUT-MAX                                     
      *        DISPLAY 'FS120-UPC = ' PV-UPC-INTR-UPC-ID                        
      *        MOVE FS120-STR-NBR TO PV-9-4                                     
      *        DISPLAY 'FS120-LOC = ' PV-X-4                                    
      *        DISPLAY 'OUTPUT RECS = ' PV-RECS-WRITTEN-CNT                     
      *        MOVE ZERO TO PV-OUTPUT-CNT.                                      
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
