      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
       PROGRAM-ID.             PMKUT111.                                00030000
       AUTHOR.                 DARYL WOELFEL.                           00040000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00050000
       DATE-WRITTEN.           SEPTEMBER 2003.                          00060000
       DATE-COMPILED.                                                   00070000
      ******************************************************************00080000
      *                                                                *00090000
      *    PMKUT111 - FORMAT PRICING EXCEPTION DATA INTO SQL-LOADER    *00100003
      *    FORMAT FOR INSERT INTO PMT_SKU_LVL_PRICG_BY_LOC             *00110003
      *    ORACLE TABLE (PM_PRD)                                       *00120006
      ******************************************************************00130000
      **   CHANGE LOG:                                                 *00140000
      ******************************************************************00150000
      *    10/28/2003 - COLLEEN CUMMINGS - P000602228                  *00160000
      *    ADDED COMMIT LOGIC.                                         *00170000
      *                                                                *00180000
      *    01/09/2004 - COLLEEN CUMMINGS - WR25854                     *00190000
      *    FOR NEW BOGO, ADDED CHECK TO NOT SKIP IF                    *00200000
      *    CURRENT PRICG-TYP-CDE = PREVIOUS PRICG-TYP-CDE              *00210000
      *    AND CURRENT MDSE-HIER-LVL-CDE = PREV MDSE-HIER-LVL-CDE      *00220000
      *    THIS ALLOWS GROUP PRICING (BOGO) AND EACH PRICING           *00230000
      *                                                                *00240000
      *    01/13/2004 - COLLEEN CUMMINGS - P000623934                  *00250000
      *    FIX TO LOAD OVERLAPPING SKUS IF THE PREV PRICG-TYP-CDE      *00260000
      *    IS GROUP PRICING (SPECIFICALLY A FIX FOR BOGO OVERLAPPING   *00270000
      *    WITHIN A PROMOTION.                                         *00280000
      *                                                                *00290000
      *    06/10/2004 - ALEXIS KEIKER - P000650492                     *00300000
      *    FIXED THE COUNT ON NUMBER OF RECORDS INSERTED TO THE        *00310000
      *    SKU PRICING TABLE.                                          *00320000
      *                                                                *00330000
      *    10/18/2005 - TOM BOYD - P000812801                          *00340000
      *    ADDED CHECK TO IGNORE UNIQUE CONSTRAINT ERROR ON INSERT.    *00350000
      *                                                                *00360000
      *    11/13/2009 - RILEY KEHOE - PKE 1976                          00370003
      *    CHANGED PROGRAM TO JUST FORMAT FILE INTO SQL-LOADER LOADABLE*00380003
      *    FORMAT FOR AN INSERT FURTHER DOWN THE JOBSTREAM             *00390003
      ******************************************************************00400000
      ******************************************************************00410000
       ENVIRONMENT DIVISION.                                            00420000
       CONFIGURATION SECTION.                                           00430000
       SOURCE-COMPUTER. IBM-3090.                                       00440000
       OBJECT-COMPUTER. IBM-3090.                                       00450000
                                                                        00460000
       INPUT-OUTPUT SECTION.                                            00470000
       FILE-CONTROL.                                                    00480000
                                                                        00490000
           SELECT EXPLODED-FILE                                         00500000
                  ASSIGN TO INP01.                                      00510000
           SELECT SQLLOAD-FILE                                          00520003
                  ASSIGN TO OUT01.                                      00530003
                                                                        00540000
       DATA DIVISION.                                                   00550000
                                                                        00560000
       FILE SECTION.                                                    00570000
                                                                        00580000
       FD  EXPLODED-FILE                                                00590000
           RECORDING MODE F                                             00600000
           BLOCK CONTAINS 0 RECORDS                                     00610000
           LABEL RECORDS ARE OMITTED.                                   00620000
       01  EXPLODED-LINE                    PIC  X(243).                00630000
                                                                        00640000
       FD  SQLLOAD-FILE                                                 00650003
           RECORDING MODE IS F                                          00660005
           LABEL RECORDS ARE STANDARD                                   00670005
           BLOCK CONTAINS 0 RECORDS.                                    00680005
       01  SQLLOAD-RECORD                  PIC  X(247).                 00690013
                                                                        00700003
       WORKING-STORAGE SECTION.                                         00710000
                                                                        00720000
                                                                        00730000
       01  PS-PROGRAM-SWITCHES.                                         00740000
           05  PS-END-OF-FILE-SW            PIC  X(01)  VALUE 'N'.      00750000
               88  PS-END-OF-FILE                       VALUE 'Y'.      00760000
                                                                        00770000
       01  PC-PROGRAM-CONSTANTS.                                        00780000
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE           00790000
                                                           'PMKUT111'.  00800000
       01  PV-PROGRAM-VARIABLES.                                        00810000
           05  PV-RECS-DIV                  PIC S9(09)  VALUE 0.        00820000
           05  PV-RECS-REM                  PIC S9(09)  VALUE 0.        00830000
           05  PV-PREV-SKU-NBR              PIC  9(08)  VALUE 0.        00840000
           05  PV-PREV-LOC-NBR              PIC  9(05)  VALUE 0.        00850000
           05  PV-PREV-PROMO-ID             PIC  9(10)  VALUE 0.        00860000
           05  PV-PREV-PROMO-PRICG-TYP-CDE  PIC  X(03)  VALUE SPACES.   00870000
           05  PV-PREV-MDSE-HIER-LVL-CDE    PIC  9(01)  VALUE 0.        00880000
                                                                        00890000
       01  PA-PROGRAM-ACCUMULATORS.                                     00900000
           05  PA-READ-COUNTER            PIC S9(11)  COMP-3            00910000
                                                        VALUE ZEROS.    00920000
           05  PA-SKIP-COUNTER              PIC S9(11)  COMP-3          00930000
                                                        VALUE ZEROS.    00940000
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3          00950002
                                                        VALUE ZEROS.    00960000
      ******************************************************************01620000
      *    INPUT  RECORD - EXPLODED BY SKU/STORE                        01630003
      ******************************************************************01640000
           COPY PMRD107.                                                01650000
      ******************************************************************01651012
      *    OUTPUT RECORD - EXPLODED BY SKU/STORE                        01652012
      ******************************************************************01653012
           COPY PMRD103.                                                01654012
                                                                        01660000
                                                                        01670000
       PROCEDURE DIVISION.                                              01680000
                                                                        01690000
      ***************************************************************** 01700000
      * A100-MAINLINE.                                                * 01710000
      ***************************************************************** 01720000
       A100-MAINLINE.                                                   01730000
                                                                        01740000
           PERFORM B100-OPEN-FILE                                       01750000
           PERFORM B200-READ-EXPLODED-FILE                              01760000
           IF PS-END-OF-FILE                                            01770000
               DISPLAY ' '                                              01780000
               DISPLAY '** PMKUT111 INPUT FILE IS EMPTY **'             01790000
               DISPLAY '** NO PROCESSING OCCURRED **'                   01800003
           ELSE                                                         01810000
                                                                        01820000
               PERFORM C100-PROCESS-RECORDS                             01830000
                 UNTIL PS-END-OF-FILE                                   01840000
                                                                        01850000
               DISPLAY '# OF RECORDS READ              = '              01860000
                                                       PA-READ-COUNTER  01870000
               DISPLAY ' '                                              01880000
               DISPLAY '# OF RECORDS WRITTEN TO OUTPUT ='               01890003
                                                       PA-RECS-WRITTEN  01900003
               DISPLAY '# OF DUPLICATE RECORDS SKIPPED = '              01910000
                                                       PA-SKIP-COUNTER  01920000
                                                                        01930000
           END-IF                                                       01940000
                                                                        01950000
           PERFORM B900-CLOSE-FILE                                      01960000
                                                                        01970000
           STOP RUN.                                                    01980000
                                                                        01990000
      ***************************************************************** 02000000
      * B100-OPEN-FILE.                                               * 02010000
      * ==> PROCESSES:                                                * 02020000
      *  - OPEN THE INPUT FILE                                        * 02030000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 02040000
      ***************************************************************** 02050000
       B100-OPEN-FILE.                                                  02060000
                                                                        02070000
           OPEN INPUT  EXPLODED-FILE                                    02080003
                OUTPUT SQLLOAD-FILE.                                    02090003
      ***************************************************************** 02100000
      *     READ A ROW FROM THE INPUT FILE                            * 02110000
      ***************************************************************** 02120000
       B200-READ-EXPLODED-FILE.                                         02130000
                                                                        02140000
           READ EXPLODED-FILE INTO PM107-EXCPT-PRCG-EXT-SKU             02150000
             AT END                                                     02160000
               SET PS-END-OF-FILE TO TRUE                               02170000
           END-READ                                                     02180000
                                                                        02190000
           IF NOT PS-END-OF-FILE                                        02200000
               ADD +1 TO PA-READ-COUNTER                                02210000
           END-IF.                                                      02220000
                                                                        02230000
      ***************************************************************** 02240000
      * B900-CLOSE-FILE                                               * 02250000
      * ==> PROCESSES:                                                * 02260000
      *  - CLOSE THE INPUT FILE                                       * 02270000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 02280000
      ***************************************************************** 02290000
       B900-CLOSE-FILE.                                                 02300000
                                                                        02310000
           CLOSE  EXPLODED-FILE                                         02320003
                  SQLLOAD-FILE.                                         02330003
      ***************************************************************** 02340000
      * C100-PROCESS-RECORDS                                          * 02350000
      * ==> PROCESSES:                                                * 02360000
      *  - WRITES THE NEW RECORD TO THE OUTPUT FILE.                  * 02370002
      * ==> PERFORMED FROM: A100-MAINLINE                             * 02380000
      ***************************************************************** 02390000
       C100-PROCESS-RECORDS.                                            02400000
                                                                        02410000
      *    SKIP DUPLICATE RECORDS - KEEP ONLY THE RECORD FROM THE       02420000
      *    LOWEST LEVEL IN THE MDSE HIERARCHY (THE ONE THAT WILL WIN)   02430000
      *    IT WILL BE FIRST DUE TO THE SORT BEFORE THIS PROGRAM RUNS.   02440000
      *    1/8/04 - NEED TO ALSO KEEP IF DIFFERENT PRICING TYPE CODE    02450000
      *    AND SAME LEVEL OF THE HIERARCHY FOR NEW BOGO.                02460000
      *    1/13/04 - NEED TO ALSO KEEP IF PREV-PRICG-TYP-CDE IS GROUP   02470000
      *    AND CURRENT SKU = PREV SKU (FOR OVERLAPPING BOGO).           02480000
           IF (PM107-SKU-NBR NOT = PV-PREV-SKU-NBR) OR                  02490000
              (PM107-LOC-NBR NOT = PV-PREV-LOC-NBR) OR                  02500000
              (PM107-PROMO-ID NOT = PV-PREV-PROMO-ID) OR                02510000
              ((PV-PREV-PROMO-PRICG-TYP-CDE =                           02520000
                'GAO' OR 'GPO' OR 'GBL' OR 'GBD' OR 'GBP') AND          02530000
                PM107-SKU-NBR = PV-PREV-SKU-NBR) OR                     02540000
              ((PM107-PROMO-PRICG-TYP-CDE NOT =                         02550000
                PV-PREV-PROMO-PRICG-TYP-CDE) AND                        02560000
               (PM107-MDSE-HIER-LVL-CDE =                               02570000
                PV-PREV-MDSE-HIER-LVL-CDE))                             02580000
                                                                        02590000
               PERFORM D100-CREATE-OUTPUT-FILE                          02600001
                                                                        02610000
               MOVE PM107-SKU-NBR TO PV-PREV-SKU-NBR                    02620000
               MOVE PM107-LOC-NBR TO PV-PREV-LOC-NBR                    02630000
               MOVE PM107-PROMO-ID TO PV-PREV-PROMO-ID                  02640000
               MOVE PM107-PROMO-PRICG-TYP-CDE TO                        02650000
                    PV-PREV-PROMO-PRICG-TYP-CDE                         02660000
               MOVE PM107-MDSE-HIER-LVL-CDE TO                          02670000
                    PV-PREV-MDSE-HIER-LVL-CDE                           02680000
           ELSE                                                         02690000
               ADD +1 TO PA-SKIP-COUNTER                                02700000
           END-IF                                                       02710000
                                                                        02720000
           PERFORM B200-READ-EXPLODED-FILE.                             02730000
                                                                        02740000
      ***************************************************************** 02750000
      * D100-CREATE-OUTPUT-FILE                                       * 02760001
      * ==> PROCESSES:                                                * 02770000
      *  - CREATE OUTPUT FILE TO BE SQL-LOADED LATER IN JOBSTREAM     * 02780002
      ***************************************************************** 02790000
       D100-CREATE-OUTPUT-FILE.                                         02800001
           MOVE PM107-SKU-NBR          TO PM103-SKU-NBR.                02820012
           MOVE PM107-LOC-NBR          TO PM103-LOC-NBR.                02830012
           MOVE PM107-PROMO-INTFC-ID   TO PM103-PROMO-INTFC-ID.         02840012
           MOVE PM107-MDSE-SELN-ID     TO PM103-MDSE-SELN-ID.           02850012
           MOVE PM107-MDSE-SELN-LNE-ID TO PM103-MDSE-SELN-LNE-ID.       02860012
           MOVE PM107-AD-EVNT-ID       TO PM103-AD-EVNT-ID.             02870012
           MOVE PM107-PROMO-ID         TO PM103-PROMO-ID.               02880012
           MOVE PM107-PROMO-STRT-TMST  TO PM103-PROMO-STRT-TMST.        02890012
           MOVE PM107-PROMO-END-TMST   TO PM103-PROMO-END-TMST.         02900012
           MOVE PM107-PROMO-ITM-ID     TO PM103-PROMO-ITM-ID.           02910012
           MOVE PM107-DEPT-NBR         TO PM103-DEPT-NBR.               02920012
           MOVE PM107-MAJ-CL-NBR       TO PM103-MAJ-CL-NBR.             02930012
           MOVE PM107-SUB-CL-NBR       TO PM103-SUB-CL-NBR.             02940012
           MOVE PM107-VS-ID            TO PM103-VS-ID.                  02950012
           MOVE PM107-VS-NBR           TO PM103-VS-NBR.                 02960012
           MOVE PM107-PROMO-PRICG-TYP-CDE TO PM103-PROMO-PRICG-TYP-CDE. 02970012
           MOVE PM107-PROMO-PRICG-AMT  TO PM103-PROMO-PRICG-AMT.        02990012
           MOVE PM107-PROMO-PRIC-PT-AMT TO PM103-PROMO-PRIC-PT-AMT.     03000012
           MOVE PM107-PROMO-GP-QTY     TO PM103-PROMO-GP-QTY.           03010012
           MOVE PM107-PROMO-GP-AMT     TO PM103-PROMO-GP-AMT.           03020012
           MOVE PM107-UNT-RTL-AMT      TO PM103-UNT-RTL-AMT.            03030012
           MOVE PM107-GP-RTL-QTY       TO PM103-GP-RTL-QTY.             03040012
           MOVE PM107-GP-RTL-AMT       TO PM103-GP-RTL-AMT.             03050012
           MOVE PM107-LOC-SKU-STAT-CDE TO PM103-LOC-SKU-STAT-CDE.       03060012
           MOVE PM107-OH-UNT-QTY       TO PM103-OH-UNT-QTY.             03070012
           MOVE PM107-ONORD-UNT-QTY-IND TO PM103-ONORD-UNT-QTY-IND.     03080012
           MOVE PM107-FRST-RCVD-DTE    TO PM103-FRST-RCVD-DTE.          03090012
           MOVE PM107-DONT-SHIP-BEF-DTE TO PM103-DONT-SHIP-BEF-DTE.     03100012
           WRITE SQLLOAD-RECORD FROM PM103-EXCPT-PRCG-EXT-SKU.          03110012
           ADD +1 TO PA-RECS-WRITTEN.                                   03120014
