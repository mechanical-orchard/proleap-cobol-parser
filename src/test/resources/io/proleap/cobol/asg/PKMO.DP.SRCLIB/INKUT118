       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT118.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       04/06/2001.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *                                                                         
      *  THE PROGRAM READ THE SORTED PHYSICAL COUNT FILES. THE MODULE   INKUT116
      *  REMOVES ALL CLEARANCE RECORDS.                                 INKUT116
      *                                                                         
      *CHANGE HISTORY:                                                          
W04061* DATE     04/06/2001             CHANGE ID=W04061                        
W04061*   INK365 - UNIT BOOKING PROCESS EFFICIENCIES (CREATION...)              
W04061*    MADE BY: ROD JANSSEN            WR/IR #:99999                        
RB0401*                                                                         
RB0401* DATE     06-05-2001                                                     
RB0401*   MANIFESTING CHANGES                                                   
RB0401*     CHANGES ARE FOR MANIFESTING.                                        
RB0401*     COPYBOOK INWS016 INCREASED IN SIZE                                  
RB0401*                                                                         
RB0401*     ALSO, THE ADJUSTMENT CALCULATION HAS BEEN MODIFIED TO               
RB0401*     ACCOMODATE MANIFESTED STORES.  INCLUDE A MANIFESTING                
RB0401*     INDICATORE IN THE STORE TABLE.  PERFORM THE CALCULATION             
RB0401*     ACCORDING TO THEIR MANIFESTING STATUS.                              
RB0401*     THEN IF THE ADJUSTMENT AMOUNT IS NOT ZERO, CONTINUE                 
RB0401*     PROCESSING, OTHERWISE WRITE THE RECORD TO A BYPASS FILE.            
W21732******************************************************************        
W21732* CHANGE LOG                                                              
W21732* DATE       WHO     CHANGE DESC                                          
W21732* ---------- ------- -----------------------------------------            
W21732* 04-12-2002 ASHOK,  NON-INTELLIGENT SKU CONVERSION.                      
W21732*            IGSI    INWS016 WAS INCREASED IN SIZE TO 80 BYTES.           
W21732*                    INCREASED PV-DEPT-TABLE TO 9999 FROM 999.            
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
W27710* W27710  12-16-2004 CHANGE GWP CEILING RETAIL FROM $0.03 TO $0.02        
W27808* W27808  07-19-2005 NEW MANIFESTING CHANGES. IF MFSTNG_IND = 'Y'         
W27808*                    SUBTRACT STORE-EXPTED OH.IF MFSTNG_IND = 'N'         
W27808*                    SUBTRACT AFTER-FLUSH-RECEIPTS                        
W28545* W28545  06-30-2006 J SELWITSCHKA                                        
W28545*                    DEPARTMENT LEVEL INVENTORY PROJECT                   
W34286* W34286 02-20-2008 T KONTAROVICH                                         
W34286*                   BYPASSED ITEMS SHOULD BE CHECKED FOR                  
W34286*                   INCDUDE/EXCLUDE CONDITIONS. INCLUDED                  
W34286*                   BYPASS ITEMS SHOULD FEED INTO VARIANCE PRC.           
      * W36280 03/19/2009 TODD C. MICHALEK                                      
      *  - INCREASED THE SIZE OF THE FOUR REPORT ENDING PV-COUNTS               
      *    COUNTERS FROM PIC 9(7) TO PIC 9(9)                                   
      *  - ADDED AND DISPLAYED TWO NEW REPORT ENDING COUNTERS:                  
      *    PV-TUPCPLS-E300-READS - COUNT THE NUMBER OF TUPCPLS STORE            
      *                            '000' SELECTS BEING DONE                     
      *    PV-TUPCPLS-E350-READS - COUNT THE NUMBER OF STORE SPECIFIC           
      *                            TUPCPLS SELECTS BEING DONE                   
      *  - IN E300-READ-TUPCPLS COMMENTED OUT THE IF STORE NUMBER CHANGE        
      *    LINE, SO THAT ONLY A CHANGE IN ITEM NUMBER WILL TRIGGER A            
      *    CALL TO THE TUPCPLS TABLE FOR STORE '000' PRICING/STATUS             
      *  - IN E300-READ-TUPCPLS ADDED BACK THE ELSE PART OF THE IF/ELSE         
      *    STATEMENT TO ALWAYS DO A STORE SPECIFIC TUPCPLS TABLE SELECT         
      *    WHEN IN016-STAT-CDE = PC-REGIONAL-STAT ('25')                        
W33304* W33304  06-30-2008 JILL LIN                                             
W33304*                    HOLDING INVENTORY LEDGER OPEN ACROSS                 
W33304*                    PERIODS INVENTORY SHORTAGE REPORTING                 
P40463*PRB40463 22-03-2013  INCLUDED 'WITH UR' IN THE QUERIES                   
P40463******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PHYSICAL-COUNT-FILE                                           
           ASSIGN TO INP01.                                                     
                                                                                
           SELECT CONTROL-CARD-FILE                                             
           ASSIGN TO INP02.                                                     
                                                                                
           SELECT NEW-PHYSICAL-COUNT-FILE                                       
           ASSIGN TO OUT01.                                                     
                                                                                
           SELECT RECORDS-EXCLUDED-FILE                                         
           ASSIGN TO OUT02.                                                     
                                                                                
           SELECT MISSING-SKU-REPORT-FILE                               INKUT116
           ASSIGN TO OUT03.                                             INKUT116
                                                                        INKUT116
           SELECT RECORDS-BYPASSED-FILE                                         
           ASSIGN TO OUT04.                                                     
W34286                                                                          
W34286     SELECT RECORDS-BYPASSED-EXCL-FILE                                    
W34286     ASSIGN TO OUT05.                                                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PHYSICAL-COUNT-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  PHYSICAL-COUNT-RECORD       PIC  X(80).                              
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-REC            PIC  X(80).                              
                                                                                
       FD  NEW-PHYSICAL-COUNT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  NEW-PHYSICAL-COUNT-RECORD   PIC  X(80).                              
                                                                                
       FD  RECORDS-EXCLUDED-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  RECORDS-EXCLUDED-RECORD   PIC  X(80).                                
                                                                                
       FD  MISSING-SKU-REPORT-FILE                                      INKUT116
           LABEL RECORDS ARE STANDARD                                   INKUT116
           RECORDING MODE IS F                                          INKUT116
           BLOCK CONTAINS 0 RECORDS.                                    INKUT116
W21732 01  MISSING-SKU-RECORD        PIC  X(80).                        INKUT116
                                                                        INKUT116
       FD  RECORDS-BYPASSED-FILE                                                
 |         LABEL RECORDS ARE STANDARD                                           
 |         RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  RECORDS-BYPASSED-RECORD   PIC  X(80).                                
W34286                                                                          
W34286 FD  RECORDS-BYPASSED-EXCL-FILE                                           
W34286     LABEL RECORDS ARE STANDARD                                           
W34286     RECORDING MODE IS F                                                  
W34286     BLOCK CONTAINS 0 RECORDS.                                            
W34286 01  RECORDS-BYPASSED-EXCL-RECORD   PIC  X(80).                           
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
       01  CONTROL-CARD-RECORD.                                                 
           05  FILLER                       PIC  X(15)    VALUE SPACES.         
           05  CC-INCLUDE-GPW-SW            PIC  X        VALUE 'N'.            
               88  CC-INCLUDE-GWP                         VALUE 'Y'.            
               88  CC-EXCLUDE-GWP                         VALUE 'N'.            
           05  FILLER                       PIC  X(14)    VALUE SPACES.         
           05  CC-INCLUDE-CLEAR-SW          PIC  X        VALUE 'N'.            
               88  CC-INCLUDE-CLEAR                       VALUE 'Y'.            
               88  CC-EXCLUDE-CLEAR                       VALUE 'N'.            
           05  FILLER                       PIC  X(12)    VALUE SPACES.         
           05  CC-INCLUDE-DISCONT-SW        PIC  X        VALUE 'N'.            
               88  CC-INCLUDE-DISCONT                     VALUE 'Y'.            
               88  CC-EXCLUDE-DISCONT                     VALUE 'N'.            
           05  FILLER                       PIC  X(36)    VALUE SPACES.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-RENUMBERED-SKU-STATUS PIC S9(03)  VALUE +98                   
                                                    COMP-3.                     
           05  PC-CLEARANCE-STATUS      PIC S9(03)  VALUE +30                   
                                                    COMP-3.                     
           05  PC-EMPTY-DATE            PIC X(10)   VALUE '9999-09-09'.         
           05  PC-MAX-RETRIES           PIC 9(03)   VALUE 3.                    
           05  PC-OPER-CO               PIC X(02)   VALUE '03'.                 
           05  PC-CO                    PIC X(02)   VALUE 'CO'.                 
W26600     05  PC-MAX-STORES            PIC 9(05)   VALUE 9998.                 
           05  PC-DEFAULT-STORE-NUMBER  PIC  S9(4)  COMP                        
                                                    VALUE ZERO.                 
           05  PC-REGIONAL-STAT         PIC X(02)   VALUE '25'.                 
           05  PC-CLEARANCE-STAT        PIC X(02)   VALUE '30'.                 
           05  PC-DISCONTINUE-STAT      PIC X(02)   VALUE '90'.                 
W27710     05  PC-GWP-CEILING           PIC 9V99    VALUE 0.02.                 
           05  PC-GWP-ITEM-TYPE         PIC X(01)   VALUE 'G'.                  
           05  PC-REGULAR-ITEM-TYPE     PIC X(01)   VALUE 'R'.                  
           05  PC-NO-ITEM-ITEM-TYPE     PIC X(01)   VALUE 'N'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RETRY-COUNT          PIC S9(03)    VALUE ZERO                 
                                                     COMP-3.                    
           05  PV-ITEM-NBR             PIC S9(15)V   VALUE ZERO                 
                                                     COMP-3.                    
           05  PV-UPC-CODE             PIC S9(15)V                              
                                       COMP-3        VALUE +0.                  
           05  PV-CONVERT-SKU.                                                  
               10  PV-CONVERT-DEPT        PIC 9(03)  VALUE ZERO.                
               10  PV-CONVERT-CLASS       PIC 9(02)  VALUE ZERO.                
               10  PV-CONVERT-SEQ         PIC 9(03)  VALUE ZERO.                
           05  PV-SQL-SUB              PIC 9(03)  VALUE ZERO.                   
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.                 
           05  PV-ABEND-MESSAGE        PIC X(75)  VALUE SPACE.                  
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.                 
W21732     05  PV-DEPT-TABLE           OCCURS 9999.                             
               10  PV-SKU-LEVEL-IND    PIC X(01)    VALUE SPACES.               
W21732     05  PV-DEPT-TABLE-INDEX     PIC 9(04)    VALUE ZERO.                 
W26600     05  PV-INDX                 PIC 9(05)    VALUE ZERO.                 
           05  PV-CONVERT-DATE         PIC 9(06)    VALUE ZERO.                 
           05  FILLER REDEFINES PV-CONVERT-DATE.                                
               10  PV-CONVERT-YY       PIC 9(02).                               
               10  PV-CONVERT-MM       PIC 9(02).                               
               10  PV-CONVERT-DD       PIC 9(02).                               
           05  PV-COMPARE-DATE.                                                 
               10  PV-COMPARE-CC       PIC X(02)  VALUE SPACES.                 
               10  PV-COMPARE-YY       PIC X(02)  VALUE SPACES.                 
               10  FILLER              PIC X(01)  VALUE '-'.                    
               10  PV-COMPARE-MM       PIC X(02)  VALUE SPACES.                 
               10  FILLER              PIC X(01)  VALUE '-'.                    
               10  PV-COMPARE-DD       PIC X(02)  VALUE SPACES.                 
           05  PV-STORE-NUMBER         PIC S9(4)  COMP.                         
           05  PV-COUNTS.                                                       
               10  PV-GWP-EXCLD        PIC 9(9)   VALUE ZERO.                   
               10  PV-CLEARANCE-EXCLD  PIC 9(9)   VALUE ZERO.                   
               10  PV-DISCNTD-EXCLD    PIC 9(9)   VALUE ZERO.                   
               10  PV-ITEMS-NOT-FOUND  PIC 9(9)   VALUE ZERO.                   
               10  PV-TUPCPLS-E300-READS                                        
                                       PIC 9(9)   VALUE ZERO.                   
               10  PV-TUPCPLS-E350-READS                                        
                                       PIC 9(9)   VALUE ZERO.                   
           05  PV-ACTL-INV-TMST        PIC X(26)  VALUE SPACES.                 
           05  PV-ACTL-INV-TMST-R REDEFINES PV-ACTL-INV-TMST.                   
               10  PV-ACTL-INV-DTE     PIC X(10).                               
               10  PV-ACTL-TMST        PIC X(16).                               
           05  PV-WRK-ADJUSTMENT-AMT PIC S9(7) COMP-3 VALUE ZEROS.              
RB0401     05  PV-MFSTNG-IND           PIC X(01)  VALUE SPACES.                 
W26600     05  PV-LOC-NBR              PIC 9(04)  VALUE ZEROS.                  
W26600     05  PV-EDIT-LOC-NBR REDEFINES                                        
W26600         PV-LOC-NBR              PIC X(04).                               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ITEM-FOUND-SW        PIC X        VALUE 'N'.                  
               88  PS-ITEM-FOUND                    VALUE 'Y'.                  
               88  PS-ITEM-NOT-FOUND                VALUE 'N'.                  
           05  PS-UPC-FOUND-SW         PIC X        VALUE 'N'.                  
               88  PS-UPC-FOUND                     VALUE 'Y'.                  
               88  PS-UPC-NOT-FOUND                 VALUE 'N'.                  
           05  PS-VALID-SKU-SW           PIC X  VALUE 'Y'.                      
               88  PS-VALID-SKU                 VALUE 'Y'.                      
               88  PS-INVALID-SKU               VALUE 'N'.                      
           05  PS-MORE-INPUT-RECS-SW     PIC X  VALUE 'Y'.                      
               88  PS-MORE-INPUT-RECS           VALUE 'Y'.                      
               88  PS-NO-MORE-INPUT-RECS        VALUE 'N'.                      
           05  PS-NO-MORE-DEPT-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-DEPT-ROWS            VALUE 'Y'.                      
               88  PS-NO-MORE-DEPT-ROWS         VALUE 'N'.                      
           05  PS-CLEARNCE-SKU-SW        PIC X  VALUE 'N'.                      
               88  PS-CLEARANCE-SKU             VALUE 'Y'.                      
               88  PS-NOT-CLEARANCE-SKU         VALUE 'N'.                      
           05  PS-VALID-RENUMBER-SW      PIC X  VALUE 'N'.                      
               88  PS-VALID-RENUMBER            VALUE 'Y'.                      
               88  PS-INVALID-RENUMBER          VALUE 'N'.                      
           05  PS-MORE-TINVPAR-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TINVPAR-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TINVPAR-ROWS      VALUE 'N'.                      
           05  PS-TUPCPLS-SW             PIC X  VALUE 'N'.                      
               88  PS-TUPCPLS-STORE-FOUND       VALUE 'Y'.                      
               88  PS-TUPCPLS-STORE-NOT-FOUND   VALUE 'N'.                      
           05  PS-STORE-IN-TABLE-SW      PIC X  VALUE 'N'.                      
               88  PS-STORE-IN-TABLE-FOUND      VALUE 'Y'.                      
               88  PS-STORE-IN-TABLE-NOT-FOUND  VALUE 'N'.                      
                                                                                
       01  SA-SAVE-AREAS.                                                       
           05  SA-SKU                  PIC X(08)  VALUE SPACES.                 
RB0401     05  SA-ITEM-NUMBER          PIC S9(15)V      COMP-3                  
RB0401                                            VALUE ZEROS.                  
W26600     05  SA-STORE                PIC X(04)  VALUE SPACES.                 
                                                                                
       01  STORE-TBL.                                                           
           05  STORE-OCC OCCURS 9999 TIMES.                                     
W26600         10  ST-STORE-NMBR       PIC  X(04).                              
W26600         10  ST-STORE-NMBR-NUM REDEFINES                                  
W26600             ST-STORE-NMBR       PIC  9(04).                              
               10  ST-ACTL-INV-DTE     PIC  X(10).                              
RB0401         10  ST-MFSTNG-IND       PIC  X(01).                              
                                                                                
      *****************************************************************         
      * INPUT/OUTPUT FILE RECORD LAYOUT                                         
      *****************************************************************         
W26600     COPY INWS016.                                                        
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * MERCHANDISE AREA DCLGEN                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
W28545*****************************************************************         
W28545* TINCNTL DCLGEN                                                          
W28545*****************************************************************         
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TUPCPLS DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
              INCLUDE TUPCPLS                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * SQL COMM AREA DCLGEN                                                    
      *****************************************************************         
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DEPARTMENT TABLE                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEDP                                                  
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * DB2 CURSOR FOR TINVPAR                                                  
      *****************************************************************         
           EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                         
                                                                                
W26600       SELECT LOC_NBR,                                                    
RB0401                ACTL_INV_DTE,                                             
RB0401                MFSTNG_IND                                                
W28545*      FROM TINVPAR                                                       
W28545       FROM TINVPAR A                                                     
W28545           ,TINCNTL B                                                     
             WHERE ACTL_FIN_BK_DTE = '9999-09-09'                               
W28545         AND B.ACTV_IND = 'Y'                                             
W28545         AND A.INV_ID = B.INV_ID                                          
W33304         AND A.LOC_INV_STAT_CDE = 'IN'                                    
W26600       ORDER BY LOC_NBR                                                   
P40463        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 DEPARTMENT CURSOR.                                                  
      *****************************************************************         
           EXEC SQL                                                             
              DECLARE DEPT_CURSOR CURSOR FOR                                    
                SELECT DEPT_NBR,                                                
                       SKU_LVL_UPD_IND                                          
                  FROM TMDSEDP A,                                               
                       TMDSEAR B                                                
                  WHERE (A.MDSEAR_DKEY = B.MDSEAR_DKEY)     AND                 
                        (B.OPERCO_NBR  = '03')              AND                 
                        (B.MDSELV_CDE  = 'DPT')             AND                 
                        (A.SKU_LVL_UPD_IND = 'Y')           AND                 
                        (CURRENT DATE  BETWEEN B.STRT_DTE                       
                                           AND B.END_DTE)                       
P40463            WITH UR                                                       
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       A100-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM B100-OPEN-FILES.                                             
                                                                                
           PERFORM Z010-READ-CONTROL-RECORD.                                    
           PERFORM Z100-READ-INPUT-REC.                                         
                                                                                
           PERFORM Z600-OPEN-DEPT-CURSOR.                                       
           PERFORM Z700-FETCH-DEPT-ROW.                                         
                                                                                
           PERFORM B200-LOAD-DEPT-TABLE                                         
               UNTIL PS-NO-MORE-DEPT-ROWS.                                      
                                                                                
           PERFORM Z800-CLOSE-DEPT-CURSOR.                                      
                                                                                
           PERFORM Z250-OPEN-TINVPAR-CURSOR.                                    
           SET PS-MORE-TINVPAR-ROWS TO TRUE.                                    
           INITIALIZE STORE-TBL.                                                
           MOVE +1 TO PV-INDX.                                                  
                                                                                
           PERFORM Z225-FETCH-TINVPAR                                           
               UNTIL PS-NO-MORE-TINVPAR-ROWS.                                   
                                                                                
           PERFORM Z275-CLOSE-TINVPAR-CURSOR.                                   
                                                                                
           PERFORM B300-PROCESS-RECS                                            
               UNTIL PS-NO-MORE-INPUT-RECS.                                     
                                                                                
           PERFORM B400-CLOSE-FILES.                                            
                                                                                
           DISPLAY '  '                                                         
           DISPLAY '*****************************************'.                 
           DISPLAY '  '                                                         
           DISPLAY 'DISCONTINUED EXCLUDED   = ' PV-DISCNTD-EXCLD.               
           DISPLAY 'CLEARANCE EXCLUDED      = ' PV-CLEARANCE-EXCLD.             
           DISPLAY 'GWP EXCLUDED            = ' PV-GWP-EXCLD.                   
           DISPLAY 'ITEMS NOT FOUND         = ' PV-ITEMS-NOT-FOUND.             
           DISPLAY 'STORE 000 READS         = ' PV-TUPCPLS-E300-READS.          
           DISPLAY 'STORE SPECIFIC READS    = ' PV-TUPCPLS-E350-READS.          
           DISPLAY '  '                                                         
           DISPLAY '*****************************************'.                 
           DISPLAY '  '                                                         
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      * B100-OPEN-FILES.                                                        
      ******************************************************************        
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  PHYSICAL-COUNT-FILE                                      
                       CONTROL-CARD-FILE                                        
                OUTPUT NEW-PHYSICAL-COUNT-FILE                                  
                       MISSING-SKU-REPORT-FILE                                  
                       RECORDS-BYPASSED-FILE                                    
W34286                 RECORDS-BYPASSED-EXCL-FILE                               
                       RECORDS-EXCLUDED-FILE.                                   
      *                                                                         
      ****************************************************************          
      * B200-LOAD-DEPT-TABLE.                                                   
      ****************************************************************          
       B200-LOAD-DEPT-TABLE.                                                    
     *                                                                          
           MOVE MDSEAR-DEPT-NBR             TO  PV-DEPT-TABLE-INDEX.            
                                                                                
           IF MDSEDP-SKU-LVL-UPD-IND = 'Y'                                      
               MOVE MDSEDP-SKU-LVL-UPD-IND      TO  PV-SKU-LEVEL-IND            
                                                (PV-DEPT-TABLE-INDEX)           
           END-IF.                                                              
                                                                                
           PERFORM Z700-FETCH-DEPT-ROW.                                         
      *                                                                         
      ****************************************************************          
      * B300-PROCESS-RECS.                                                      
      ****************************************************************          
       B300-PROCESS-RECS.                                                       
                                                                                
      *    COMPUTE PV-WRK-ADJUSTMENT-AMT = IN016-QTY                    INKUT117
      *                                  - (IN016-OLD-ONHAND-QTY        INKUT117
      *                                  - IN016-AFTER-RECEIPTS).       INKUT117
      *                                                                 INKUT117
      *    IF PV-WRK-ADJUSTMENT-AMT IS NOT EQUAL TO ZEROS                       
           MOVE IN016-DEPT TO PV-DEPT-TABLE-INDEX.                              
                                                                                
           IF (IN016-SCAN-IND NOT = 'Y') AND                                    
              (PV-SKU-LEVEL-IND(PV-DEPT-TABLE-INDEX) = ' ')                     
               CONTINUE                                                         
           ELSE                                                                 
                                                                                
               MOVE '-23.59.59.999999' TO PV-ACTL-TMST                          
               MOVE +1 TO PV-INDX                                               
               SET PS-STORE-IN-TABLE-NOT-FOUND TO TRUE                          
                                                                                
               PERFORM D300-GET-ACT-INV-DTE-FOR-STORE                           
                 UNTIL PS-STORE-IN-TABLE-FOUND                                  
                    OR PV-INDX > PC-MAX-STORES                                  
                                                                                
               IF PV-INDX > PC-MAX-STORES                                       
                 DISPLAY ' '                                                    
                 DISPLAY '************************************'                 
W26600           DISPLAY '*** STORE ' IN016-STORE-9 ' NOT FOUND '               
                 DISPLAY '************************************'                 
                 DISPLAY ' '                                                    
                 MOVE 'STORE NOT FOUND'    TO PV-ABEND-MESSAGE                  
                 MOVE 'B300-PROCESS-RECS'  TO PV-PARAGRAPH-NAME                 
                 MOVE +4003                TO ABEND-CODE                        
                 PERFORM Z999-ABEND                                             
               END-IF                                                           
                                                                                
RB0401*        THE STORE EXISTS IN INTERNAL STORE TABLE                         
RB0401*        PERFORM A CALCULATION ON THE ADJUSTMENT                          
RB0401*        THE CALCULATION DEPENDS UPON WHETHER THE                         
RB0401*        STORE IS MANIFESTING                                             
RB0401         IF PV-MFSTNG-IND = 'Y'                                           
RB0401             COMPUTE PV-WRK-ADJUSTMENT-AMT = IN016-QTY                    
RB0401                               - (IN016-OLD-ONHAND-QTY                    
W27808                               - IN016-STR-EXP-OH-QTY                     
RB0401                               - IN016-CUTOFF-INTRANSIT)                  
RB0401         ELSE                                                             
RB0401             COMPUTE PV-WRK-ADJUSTMENT-AMT = IN016-QTY                    
RB0401                              - (IN016-OLD-ONHAND-QTY                     
RB0401                              - IN016-AFTER-RECEIPTS)                     
RB0401         END-IF                                                           
RB0401                                                                          
RB0401*        IF THE ADJUSTMENT AMOUNT IS ZERO THEN WRITE THE                  
RB0401*        RECORD TO THE BYPASS FILE.                                       
W34286*        BYPASSED RECORDS SHOULD BE CHECKED FOR EXCLUSIONS.               
W34286*        BYPASSED INCLUDED RECORDS WILL BE PROCESSED BY                   
W34286*        VARIANCE JOB INK393.                                             
W34286         PERFORM E300-READ-TUPCPLS                                        
W34286         IF PV-WRK-ADJUSTMENT-AMT IS NOT EQUAL TO ZEROS                   
W34286             PERFORM D200-CHECK-INCLUDE-EXCLUDE-ITM                       
               ELSE                                                             
W34286             PERFORM D400-CHK-INCL-EXCL-BYPASS-ITM                        
RB0401         END-IF                                                           
RB0401*                                                                         
      *      END-IF                                                             
      *    ELSE                                                                 
      *      PERFORM Z500-WRITE-RECS-BYPASSED                                   
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-INPUT-REC.                                         
      *                                                                         
      ******************************************************************        
      * B400-CLOSE-FILES.                                                       
      ******************************************************************        
       B400-CLOSE-FILES.                                                        
                                                                                
           CLOSE PHYSICAL-COUNT-FILE                                            
                 NEW-PHYSICAL-COUNT-FILE                                        
                 CONTROL-CARD-FILE                                              
                 MISSING-SKU-REPORT-FILE                                        
                 RECORDS-BYPASSED-FILE                                          
W34286           RECORDS-BYPASSED-EXCL-FILE                                     
                 RECORDS-EXCLUDED-FILE.                                         
      *                                                                         
      ***************************************************************           
      * D200-CHECK-INCLUDE-EXCLUDE-ITM.                                         
      * THIS PROCEDURE CHECKS WHETHER TO INCLUDE OR EXCLUDE A ITEM              
      ***************************************************************           
       D200-CHECK-INCLUDE-EXCLUDE-ITM.                                          
                                                                                
           IF PS-TUPCPLS-STORE-NOT-FOUND                                        
      *        PROCESS ITEM NOT FOUND                                           
               MOVE PC-NO-ITEM-ITEM-TYPE TO IN016-ITEM-TYPE                     
               ADD 1 TO PV-ITEMS-NOT-FOUND                                      
               PERFORM Z350-WRITE-RECS-EXCLUDED                                 
               PERFORM Z400-WRITE-INVALID-SKU-REC                               
           ELSE                                                                 
               IF UPCPLS-UNT-RTL-AMT IS LESS THAN PC-GWP-CEILING                
      *            PROCESS GIFT WITH PURCHASE                                   
                   MOVE PC-GWP-ITEM-TYPE TO IN016-ITEM-TYPE                     
                   IF CC-EXCLUDE-GWP                                            
                       ADD 1 TO PV-GWP-EXCLD                                    
                       PERFORM Z350-WRITE-RECS-EXCLUDED                         
                   ELSE                                                         
                       PERFORM Z300-WRITE-PHYSCNT-RECORD                        
                   END-IF                                                       
               ELSE                                                             
                   IF UPCPLS-UPC-STAT-CDE IS EQUAL TO PC-CLEARANCE-STAT         
      *                PROCESS CLEARANCE ITEM                                   
                       MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE             
                       IF CC-EXCLUDE-CLEAR                                      
                           ADD 1 TO PV-CLEARANCE-EXCLD                          
                           PERFORM Z350-WRITE-RECS-EXCLUDED                     
                       ELSE                                                     
                           PERFORM Z300-WRITE-PHYSCNT-RECORD                    
                       END-IF                                                   
                   ELSE                                                         
                       IF UPCPLS-UPC-STAT-CDE IS EQUAL TO                       
                          PC-DISCONTINUE-STAT                                   
      *                    PROCESS DISCONTINUED ITEM                            
                           MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE         
                           IF CC-EXCLUDE-DISCONT                                
                               ADD 1 TO PV-DISCNTD-EXCLD                        
                               PERFORM Z350-WRITE-RECS-EXCLUDED                 
                           ELSE                                                 
                               PERFORM Z300-WRITE-PHYSCNT-RECORD                
                           END-IF                                               
                       ELSE                                                     
      *                    PROCESS REGULAR ITEM                                 
                           MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE         
                           PERFORM Z300-WRITE-PHYSCNT-RECORD                    
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ***************************************************************           
      * D300-GET-ACT-INV-DTE-FOR-STORE.                                         
      * FIND THE ACTUAL INVENTORY DATE FOR A STORE FROM THE INTERNAL            
      * TABLE.                                                                  
      ***************************************************************           
       D300-GET-ACT-INV-DTE-FOR-STORE.                                          
                                                                                
           IF ST-STORE-NMBR (PV-INDX) = IN016-STORE                             
W26600         MOVE ST-STORE-NMBR-NUM (PV-INDX) TO PV-STORE-NUMBER              
               MOVE ST-ACTL-INV-DTE (PV-INDX) TO PV-ACTL-INV-DTE                
RB0401         MOVE ST-MFSTNG-IND (PV-INDX)   TO PV-MFSTNG-IND                  
               SET PS-STORE-IN-TABLE-FOUND    TO TRUE                           
           END-IF.                                                              
                                                                                
           ADD +1                         TO PV-INDX.                           
                                                                                
W34286*  D400-CHK-INCL-EXCL-BYPASS-ITM.                                         
W34286*  THIS PROCEDURE CHECKS WHETHER TO INCLUDE OR EXCLUDE A BYPASSED         
W34286*  ITEM. INCLUDED ITEM WILL BE PROCESSED BY VARIANCE JOB INK393.          
W34286****************************************************************          
W34286 D400-CHK-INCL-EXCL-BYPASS-ITM.                                           
W34286                                                                          
W34286     IF PS-TUPCPLS-STORE-NOT-FOUND                                        
W34286*        IF PROCESS ITEM NOT FOUND THEN WRITE ITEM TO BYPASS              
W34286*        EXCLUDED FILE AND INVALID SKU FILE.                              
W34286         MOVE PC-NO-ITEM-ITEM-TYPE TO IN016-ITEM-TYPE                     
W34286         ADD 1 TO PV-ITEMS-NOT-FOUND                                      
W34286         PERFORM Z550-WRITE-RECS-BYPASSED-EXCL                            
W34286         PERFORM Z400-WRITE-INVALID-SKU-REC                               
W34286     ELSE                                                                 
W34286         IF UPCPLS-UNT-RTL-AMT IS LESS THAN PC-GWP-CEILING                
W34286*            PROCESS GIFT WITH PURCHASE                                   
W34286             MOVE PC-GWP-ITEM-TYPE TO IN016-ITEM-TYPE                     
W34286             IF CC-EXCLUDE-GWP                                            
W34286                 PERFORM Z550-WRITE-RECS-BYPASSED-EXCL                    
W34286             ELSE                                                         
W34286                 PERFORM Z500-WRITE-RECS-BYPASSED                         
W34286             END-IF                                                       
W34286         ELSE                                                             
W34286             IF UPCPLS-UPC-STAT-CDE IS EQUAL TO PC-CLEARANCE-STAT         
W34286*                PROCESS CLEARANCE ITEM                                   
W34286                 MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE             
W34286                 IF CC-EXCLUDE-CLEAR                                      
W34286                     PERFORM Z550-WRITE-RECS-BYPASSED-EXCL                
W34286                 ELSE                                                     
W34286                     PERFORM Z500-WRITE-RECS-BYPASSED                     
W34286                 END-IF                                                   
W34286             ELSE                                                         
W34286                 IF UPCPLS-UPC-STAT-CDE IS EQUAL TO                       
W34286                    PC-DISCONTINUE-STAT                                   
W34286*                    PROCESS DISCONTINUED ITEM                            
W34286                     MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE         
W34286                     IF CC-EXCLUDE-DISCONT                                
W34286                         PERFORM Z550-WRITE-RECS-BYPASSED-EXCL            
W34286                     ELSE                                                 
W34286                         PERFORM Z500-WRITE-RECS-BYPASSED                 
W34286                     END-IF                                               
W34286                 ELSE                                                     
W34286*                    PROCESS REGULAR ITEM                                 
W34286                     MOVE PC-REGULAR-ITEM-TYPE TO IN016-ITEM-TYPE         
W34286                     PERFORM Z500-WRITE-RECS-BYPASSED                     
W34286                 END-IF                                                   
W34286             END-IF                                                       
W34286         END-IF                                                           
W34286     END-IF.                                                              
      ****************************************************************          
      * FIND PRICE AND STATUS CODE FOR STORE 000                                
      ****************************************************************          
       E300-READ-TUPCPLS.                                                       
                                                                                
RB0401*    IF IN016-SKU IS NOT EQUAL TO SA-SKU                                  
RB0401*      MOVE IN016-SKU TO SA-SKU                                           
RB0401     IF IN016-ITEM-NUMBER IS NOT EQUAL TO SA-ITEM-NUMBER                  
RB0401*    OR IN016-STORE       IS NOT EQUAL TO SA-STORE                        
RB0401       MOVE IN016-ITEM-NUMBER TO SA-ITEM-NUMBER                           
             PERFORM                                                            
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
             EXEC SQL                                                           
               SELECT UNT_RTL_AMT,                                              
                      UPC_STAT_CDE                                              
               INTO   :UPCPLS-UNT-RTL-AMT,                                      
                      :UPCPLS-UPC-STAT-CDE                                      
               FROM TUPCPLS                                                     
               WHERE ITM_NBR    = :IN016-ITEM-NUMBER AND                        
                     OPERCO_NBR = :PC-OPER-CO AND                               
                     STR_NBR    = :PC-DEFAULT-STORE-NUMBER AND                  
                     EFF_BEG_TMST  <= :PV-ACTL-INV-TMST AND                     
                     (EFF_END_TMST >= :PV-ACTL-INV-TMST OR                      
                     EFF_END_TMST IS NULL)                                      
P40463         WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = 0                                                 
                 ADD 1                 TO PV-TUPCPLS-E300-READS                 
                 MOVE UPCPLS-UPC-STAT-CDE TO IN016-STAT-CDE                     
                 SET PS-TUPCPLS-STORE-FOUND TO TRUE                             
                 IF UPCPLS-UPC-STAT-CDE = PC-REGIONAL-STAT                      
                   MOVE '20' TO IN016-STAT-CDE                                  
                   PERFORM E350-READ-TUPCPLS                                    
                 END-IF                                                         
               WHEN SQLCODE = +100                                              
                 SET PS-TUPCPLS-STORE-NOT-FOUND TO TRUE                         
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                 MOVE  'E300-READ-TUPCPLS' TO PV-PARAGRAPH-NAME                 
                 MOVE 'READING TUPCPLS TABLE' TO PV-DB2-OPER-ATTEMPTED          
                 PERFORM Z900-SQL-ABEND                                         
               END-EVALUATE                                                     
             END-PERFORM                                                        
                                                                                
RB0401     ELSE                                                                 
RB0401                                                                          
RB0401       IF IN016-STAT-CDE = PC-REGIONAL-STAT                               
RB0401         MOVE '20' TO IN016-STAT-CDE                                      
RB0401         PERFORM E350-READ-TUPCPLS                                        
RB0401       END-IF                                                             
           END-IF.                                                              
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE  'E300-READ-TUPCPLS'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * FIND A STORE SPECIFIC PRICE AND STATUS CODE                             
      ****************************************************************          
       E350-READ-TUPCPLS.                                                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
           EXEC SQL                                                             
               SELECT UNT_RTL_AMT,                                              
                      UPC_STAT_CDE                                              
               INTO   :UPCPLS-UNT-RTL-AMT,                                      
                      :UPCPLS-UPC-STAT-CDE                                      
               FROM TUPCPLS                                                     
               WHERE ITM_NBR    = :IN016-ITEM-NUMBER AND                        
                     OPERCO_NBR = :PC-OPER-CO AND                               
                     STR_NBR    = :PV-STORE-NUMBER AND                          
                     EFF_BEG_TMST  <= :PV-ACTL-INV-TMST AND                     
                     (EFF_END_TMST >= :PV-ACTL-INV-TMST OR                      
                     EFF_END_TMST IS NULL)                                      
P40463         WITH UR                                                          
           END-EXEC                                                             
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       ADD 1           TO PV-TUPCPLS-E350-READS                 
                       SET PS-TUPCPLS-STORE-FOUND TO TRUE                       
                       MOVE UPCPLS-UPC-STAT-CDE TO IN016-STAT-CDE               
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE  'E350-READ-TUPCPLS'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'READING TUPCPLS TABLE'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE  'E300-READ-TUPCPLS'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * Z010-READ-CONTROL-RECORD.                                               
      ****************************************************************          
       Z010-READ-CONTROL-RECORD.                                                
                                                                                
           READ CONTROL-CARD-FILE INTO CONTROL-CARD-RECORD                      
              AT END DISPLAY '** ABEND ** IN PROGRAM INKUT129'                  
                     DISPLAY '** ITEM PREFERENCE CONTROL CARD IS EMPTY'         
                     MOVE +4003 TO ABEND-CODE                                   
                     CALL 'ILBOABN0' USING ABEND-CODE.                          
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '** PREFERENCE CONTROL CARD: ' CONTROL-CARD-RECORD.          
           DISPLAY '*************************************************'.         
           DISPLAY '   '.                                                       
                                                                                
           IF (CC-INCLUDE-GWP OR CC-EXCLUDE-GWP) AND                            
              (CC-INCLUDE-CLEAR OR CC-EXCLUDE-CLEAR) AND                        
              (CC-INCLUDE-DISCONT OR CC-EXCLUDE-DISCONT)                        
                CONTINUE                                                        
           ELSE                                                                 
              DISPLAY '** INVALID ITEM PREFERENCE CONTROL CARD'                 
              MOVE +4003 TO ABEND-CODE                                          
              CALL 'ILBOABN0' USING ABEND-CODE                                  
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * Z100-READ-INPUT-REC.                                                    
      ****************************************************************          
       Z100-READ-INPUT-REC.                                                     
                                                                                
           READ PHYSICAL-COUNT-FILE                                             
               INTO IN016-PHYSICAL-COUNT-RECORD                                 
                 AT END                                                         
                   SET PS-NO-MORE-INPUT-RECS TO TRUE.                           
      *                                                                         
      ****************************************************************          
      * Z225-FETCH-TINVPAR.                                                     
      * LOADS THE STORE TABLE WITH TINVPAR STORES                               
      ****************************************************************          
       Z225-FETCH-TINVPAR.                                                      
                                                                                
           EXEC SQL FETCH TINVPAR_CURSOR                                        
W26600        INTO :INVPAR-LOC-NBR,                                             
RB0401             :INVPAR-ACTL-INV-DTE,                                        
RB0401             :INVPAR-MFSTNG-IND                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
W26600            MOVE INVPAR-LOC-NBR      TO PV-LOC-NBR                        
W26600            MOVE PV-EDIT-LOC-NBR     TO ST-STORE-NMBR (PV-INDX)           
                  MOVE INVPAR-ACTL-INV-DTE TO ST-ACTL-INV-DTE (PV-INDX)         
RB0401            MOVE INVPAR-MFSTNG-IND   TO ST-MFSTNG-IND (PV-INDX)           
                  ADD +1                   TO PV-INDX                           
               WHEN +100                                                        
                  SET PS-NO-MORE-TINVPAR-ROWS TO TRUE                           
               WHEN OTHER                                                       
                  MOVE 'FETCH TINVPAR_CURSOR'                                   
                        TO  PV-DB2-OPER-ATTEMPTED                               
                  PERFORM Z900-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * Z250-OPEN-TINVPAR-CURSOR.                                               
      *****************************************************************         
       Z250-OPEN-TINVPAR-CURSOR.                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * Z275-CLOSE-TINVPAR-CURSOR.                                              
      *****************************************************************         
       Z275-CLOSE-TINVPAR-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TINVPAR_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z275-CLOSE-TINVPAR-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TINVPAR CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z275-CLOSE-TINVPAR-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * Z300-WRITE-PHYSCNT-RECORD.                                              
      ****************************************************************          
       Z300-WRITE-PHYSCNT-RECORD.                                               
                                                                                
           WRITE NEW-PHYSICAL-COUNT-RECORD                                      
               FROM IN016-PHYSICAL-COUNT-RECORD.                                
                                                                                
      *****************************************************************         
      *  Z350-WRITE-RECS-EXCLUDED.                                              
      *****************************************************************         
       Z350-WRITE-RECS-EXCLUDED.                                                
                                                                                
           WRITE RECORDS-EXCLUDED-RECORD                                        
               FROM IN016-PHYSICAL-COUNT-RECORD.                                
                                                                                
      ******************************************************************        
      * Z400-WRITE-INVALID-SKU-REC.                                             
      ******************************************************************        
       Z400-WRITE-INVALID-SKU-REC.                                              
                                                                                
           WRITE MISSING-SKU-RECORD                                             
               FROM IN016-PHYSICAL-COUNT-RECORD.                                
                                                                                
      *****************************************************************         
 |    *  Z500-WRITE-RECS-BYPASSED.                                              
 |    *****************************************************************         
 |     Z500-WRITE-RECS-BYPASSED.                                                
 |                                                                              
 |         WRITE RECORDS-BYPASSED-RECORD                                        
 |             FROM IN016-PHYSICAL-COUNT-RECORD.                                
                                                                                
W34286******************************************************************        
W34286*  Z550-WRITE-RECS-BYPASSED-EXCL.                                         
W34286******************************************************************        
W34286 Z550-WRITE-RECS-BYPASSED-EXCL.                                           
W34286                                                                          
W34286     WRITE RECORDS-BYPASSED-EXCL-RECORD                                   
W34286         FROM IN016-PHYSICAL-COUNT-RECORD.                                
W34286                                                                          
      ******************************************************************        
      * Z600-OPEN-DEPT-CURSOR.                                                  
      ******************************************************************        
       Z600-OPEN-DEPT-CURSOR.                                                   
                                                                                
           INITIALIZE DCLTMDSEDP                                                
                      DCLTMDSEAR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   OPEN DEPT_CURSOR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z600-OPEN-DEPT-CURSOR'                             
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON DEPT CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z600-OPEN-DEPT-CURSOR'                                     
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *    FETCH THE NEXT ROW OF THE DEPT CURSOR.                               
      ******************************************************************        
       Z700-FETCH-DEPT-ROW.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH DEPT_CURSOR                                                
               INTO  :MDSEAR-DEPT-NBR,                                          
                     :MDSEDP-SKU-LVL-UPD-IND                                    
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-DEPT-ROWS   TO TRUE                               
           ELSE                                                                 
               IF SQLCODE NOT = 0                                               
                    MOVE 'Z700-FETCH-DEPT-ROW'                                  
                                             TO PV-PARAGRAPH-NAME               
                    MOVE 'FETCH ROW FROM DEPT CURSOR'                           
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM Z900-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *    CLOSE THE INVENTORY PARAMTERS CURSOR.                                
      ******************************************************************        
       Z800-CLOSE-DEPT-CURSOR.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                   CLOSE DEPT_CURSOR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z800-CLOSE-DEPT-CURSOR'                            
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON DEPT CURSOR'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z800-CLOSE-DEPT-CURSOR'                                    
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ****************************************************************          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      ****************************************************************          
       Z900-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT118'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = INKUT118'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
