       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP015.                                         00020010
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/23/2002.                                       00050009
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070014
      **       VSM ITEM MESSAGE - NUMBER 085 - WEB ITEM ADD             00080014
      ******************************************************************00090014
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100021
      ***     ATTRIBUTES ON XST_SKU:                                    00110021
      ***     1. E_BUS_PRD_GP_NBR                                       00120021
      ***     2. E_BUS_ITM_IND                                          00130021
      ******************************************************************00140014
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00150014
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00160019
      ******************************************************************00170014
       ENVIRONMENT DIVISION.                                            00180000
       CONFIGURATION SECTION.                                           00190000
       SOURCE-COMPUTER.  IBM-3090.                                      00200000
       OBJECT-COMPUTER.  IBM-3090.                                      00210000
       DATA DIVISION.                                                   00220000
                                                                        00230005
       WORKING-STORAGE SECTION.                                         00240000
      ******************************************************************00250005
      **PROGRAM SWITCHES                                                00260005
      ******************************************************************00270005
       01  PS-PROGRAM-SWITCHES.                                         00280005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00290005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00300005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00310005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00320016
               88  PS-ACTION-REVERSE                  VALUE 01.         00330016
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00340016
               88  PS-ACTION-NOTHING                  VALUE 02.         00350016
      ******************************************************************00360000
      **PROGRAM COUNTERS                                                00370015
      ******************************************************************00380000
       01  PC-PROGRAM-COUNTERS.                                         00390007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00400007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00410007
      ******************************************************************00420010
      **PROGRAM CALCULATIONS                                            00430010
      ******************************************************************00440010
       01  PC-PROGRAM-CALCULATIONS.                                     00450010
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00460010
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00470010
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00480010
      ******************************************************************00490000
      **PROGRAM VARIABLES                                               00500015
      ******************************************************************00510000
       01  PV-PROGRAM-COUNTERS.                                         00520015
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00530015
      ******************************************************************00540000
      **COPYBOOKS                                                       00550000
      ******************************************************************00560000
      ***ABEND COPYBOOK                                                 00570005
           COPY DPWS004.                                                00580022
      ***DB2 MESSAGE AREA                                               00590000
           COPY SQLCA2.                                                 00600022
      ***VSM ITEM COPYBOOK                                              00610014
           COPY IMRD008.                                                00620022
      ******************************************************************00630000
      **TABLE INCLUSIONS                                                00640000
      ******************************************************************00650000
      ***DB2 COMMUNICATIONS AREA                                        00660000
           EXEC SQL                                                     00670000
                INCLUDE SQLCA                                           00680000
           END-EXEC.                                                    00690000
      ***XST_SKU TABLE                                                  00700009
           EXEC SQL                                                     00710000
               INCLUDE XST00010                                         00720009
           END-EXEC.                                                    00730000
                                                                        00740000
       LINKAGE SECTION.                                                 00750000
      ******************************************************************00760000
      **LINKAGE VARIABLES                                               00770000
      ******************************************************************00780000
       01  LV-IMRD008-RECORD               PIC X(450).                  00790005
       01  LV-RETURN-CODE                  PIC S9(04).                  00800005
       01  LV-ACTION-IND                   PIC 9(02).                   00810016
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00820016
                                                                        00830000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00840005
                                LV-RETURN-CODE,                         00850016
                                LV-ACTION-IND,                          00860016
                                LV-CURRENT-TIMESTAMP.                   00870016
      ******************************************************************00880000
       0000-MAINLINE.                                                   00890000
                                                                        00900000
            PERFORM 1000-INITIALIZATION.                                00910000
            PERFORM 2000-PROCESS-MESSAGE                                00920014
                    UNTIL PS-PROCESS-COMPLETE.                          00930005
            PERFORM 9999-WRAPUP.                                        00940000
                                                                        00950000
       EJECT.                                                           00960005
      ***************************************************************** 00970000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00980007
      ***************************************************************** 00990000
       1000-INITIALIZATION.                                             01000000
                                                                        01010000
           MOVE '1000-INITIALIZATION'    TO PV-PARAGRAPH-NAME.          01020015
                                                                        01030000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01040008
           INITIALIZE PC-RETRY-COUNTER.                                 01050008
                                                                        01060008
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01070009
                                                                        01080014
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01090010
      *     COMP FIELDS.                                                01100010
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01110010
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01120010
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01130010
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01140010
                                                                        01150014
           MOVE IM008-WEB-ADD-PRD-GP-NBR   TO XST00010-E-BUS-PRD-GP-NBR.01160009
      *FIELD IS MAINTAINED BY THE XS DOMAIN                             01170009
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00010-LAST-UPD-TMST.   01180019
121402     MOVE IM008-CRE-TMST         TO XST00010-SOR-LAST-MSG-TMST.   01190019
                                                                        01200000
       EJECT.                                                           01210005
      ***************************************************************** 01220000
      **UPDATES ECOMMERCE DATA ON XST_SKU TABLE                         01230021
      ***************************************************************** 01240000
       2000-PROCESS-MESSAGE.                                            01250014
                                                                        01260000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01270015
                                                                        01280005
           EXEC SQL                                                     01290005
              UPDATE XST_SKU                                            01300009
                  SET   E_BUS_PRD_GP_NBR = :XST00010-E-BUS-PRD-GP-NBR   01310009
                      , E_BUS_ITM_IND    = 'Y'                          01320021
                      , LAST_UPD_TMST    = :XST00010-LAST-UPD-TMST      01330019
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01340019
                  WHERE INTR_UPC_ID      = :XST00010-INTR-UPC-ID        01350009
           END-EXEC.                                                    01360005
                                                                        01370005
           EVALUATE TRUE                                                01380005
      ***SUCCESSFUL PROCESSING                                          01390021
              WHEN SQLCODE = ZERO                                       01400005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01410008
      ***ROW/TABLE LOCK                                                 01420021
              WHEN SQLCODE = -911                                       01430005
              WHEN SQLCODE = -913                                       01440007
                   ADD +1                      TO PC-RETRY-COUNTER      01450007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01460007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01470007
                   ELSE                                                 01480007
                       ADD +1                  TO PC-RETRY-COUNTER      01490007
                   END-IF                                               01500007
              WHEN OTHER                                                01510005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01520005
           END-EVALUATE.                                                01530005
                                                                        01540000
       EJECT.                                                           01550005
      ******************************************************************01560000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01570000
      ******************************************************************01580000
       9999-WRAPUP.                                                     01590000
                                                                        01600000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01610018
                                                                        01620000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01630018
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01640016
                                                                        01650016
           EXIT PROGRAM.                                                01660000
                                                                        01670000
       EJECT.                                                           01680005
