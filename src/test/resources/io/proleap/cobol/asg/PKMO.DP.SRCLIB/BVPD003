W31475**********************!!! ATTENTION !!!***************************00010002
W31475* THIS IS A CLONE OF BVPD001. ANY CHANGE HERE NEEDS TO REFLECT    00010103
W31475* THERE AND VICE VERSA.                                           00010202
      ******************** HISTORY OF CHANGES **************************00011002
      *                                                                *00020000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00030000
      * -------  ----------  ----------------------------------------  *00040000
RONNA *          6/15/2006   REMOVED NON-NUMERIC CHECK ON SKU          *00050000
RONNA *                      A SKU WAS GETTING IN THAT HAD ' 9999999'  *00051000
RONNA *                      INSTEAD OF CONTAINING A ZERO IN THE FIRST *00052000
RONNA *                      DIGIT.                                    *00053000
      ******************************************************************00054000
RYAN  *          7/21/2006   ADDED BV003-PV-PAST-FUT-ITEM-REL-SW TO    *00055005
RYAN  *                      INDICATE IF AN ITEM WAS FOUND BEFORE OR   *00056000
RYAN  *                      AFTER THE SPECIFIED DATE.                 *00057000
      ******************************************************************00058000
RONNA2*         10/05/2006   REMOVE LOGIC FOR SKU, ITEM, UPC, UPC ID,  *00058100
RONNA2*                      AND SKU DEPT LOOKUPS.                      00058200
RONNA2*                      THE PRICING/ATTRIBUTE UTILITY WILL BE     *00058300
RONNA2*                      USED TO GET THE ATTRIBUTES, THEN          *00058400
RONNA2*                      BV UTILTY WILL BE CALLED THROUGH THE       00058500
RONNA2*                      PRICING MODULE                             00058600
W31475*         05/20/2009   ML PERFORMANCE TUNING                      00058702
W31475*                      AVOID USE OF VIEW MLV_BRND_VND, USE BASE   00058802
W31475*                      TABLE INSTEAD                              00058902
      ******************************************************************00059002
      * THIS COPY CODE NEEDS BVWS003 TO FUNCTION.                       00059104
      * ALSO, THE CALLING PROGRAM NEEDS TO HAVE THE FOLLOWING TABLES    00059202
      * TSKXREF                                                         00059302
      * TVNDDPT                                                         00060000
      * TVNDSTL                                                         00070000
      * TUPC                                                            00080000
      * TUPCSKU                                                         00090000
      * TMDARVS                                                         00100000
      * IMT00017 (BRAND VENDOR RELATIONSHIP TABLE)                      00110000
      * MLT00011 (UNKNOWN BRAND VENDOR RELATIONSHIP TABLE)              00120000
      * MLV00000 (VIEW FOR COMBINED KNOWN AND UNKNOWN BRAND VENDOR      00130000
      *           RELATIONSHIP TABLES)                                  00140000
      *                                                                 00141000
      *  HOW TO USE THIS:                                               00142000
      *  ### SEARCHES ###                                               00143000
      *  - THE USER CAN GET BRAND VENDOR RELATIONSHIP(S) THROUGH THE    00144000
      *    FOLLOWING SEARCHES:                                          00145000
      *    -  BRAND (MAY RETURN MULTIPLE RECORDS)                       00146000
      *    -  BRAND/DEPT (MAY RETURN MULTIPLE RECORDS)                  00147000
      *    -  BRAND/VENDOR/DEPT                                         00148000
      *    -  ITEM NUMBER                                               00149000
      *    -  KNOWN DEPT/UNKNOWN BRAND/VENDOR MAY BE KNOWN OR UNKNOWN   00150000
      *    -  RELATIONSHIP ID                                           00160000
      *    -  SKU                                                       00170000
      *    -  SKU/DEPT                                                  00180000
      *    -  UPC ID                                                    00190000
      *    -  UPC NUMBER                                                00200000
      *    -  VENDOR (MAY RETURN MULTIPLE RECORDS)                      00201000
      *    -  VENDOR/DEPT (MAY RETURN MULTIPLE RECORDS)                 00202000
      *                                                                 00203000
      *    ###  SEARCHES RETURNING A SINGLE ROW   ###                   00204000
      *    -    THE FOLLOWING SEARCHES WILL RETURN A SINGLE ROW:        00205000
      *         - BRAND VENDOR DEPARTMENT                               00206000
      *         - RELATIONSHIP ID                                       00207000
      *         - KNOWN DEPT/UNKNOWN BRAND/VEND MAY BE KNOWN OR UNKNOWN 00208000
      *                                                                 00209000
      *    -    THE RESULTS OF THESE SEARCHES WILL BE CONTAINED IN:     00210000
      *         - BV003-PV-ENT-ID-OUT                                   00220005
      *         - BV003-PV-BRAND-OUT                                    00230005
      *         - BV003-PV-DEPT-OUT                                     00240005
      *         - BV003-PV-RLTNSHP-ID-OUT                               00250005
      *                                                                 00260000
      *    ###  SEARCHES RETURNING MULTIPLE ROWS  ###                   00270000
      *    -    THE FOLLOWING SEARCHES CAN RETURN MULTIPLE ROWS:        00280000
      *         - BRAND                                                 00290000
      *         - BRAND DEPT                                            00300000
      *         - VENDOR                                                00310000
      *         - DEPARTMENT VENDOR                                     00320000
      *                                                                 00330000
      *    -    A RECORD IN THE TABLE WILL CONTAIN VENDOR NUMBER,       00340000
      *         BRAND NUMBER, DEPARTMENT NUMBER, AND RELATIONSHIP ID.   00350000
      *                                                                 00360000
      *    -    THE NUMBER OF RECORDS IN THE TABLE WILL BE FOUND IN     00361000
      *         BV003-PV-RT-SUB-OUT.                                    00362005
      *                                                                 00363000
      *    ###  VENDOR DEPARTMENT SEARCH NOTES  ###                     00364000
      *    -    THE VENDOR DEPARTMENT SEARCH CAN FIND PAST OR FUTURE    00365000
      *         RELATIONSHIPS IF BV003-PV-VND-PAST-FUT-IN IS ON.        00366005
      *         IF THAT SWITCH IS ON BV003-PV-PAST-MONTHS MUST BE       00367005
      *         FILLED IN WITH THE NUMBERS OF MONTHS IN THE PAST YOU    00367100
      *         WANT TO LOOK.  IF NOTHING IS FOUND IN THE PAST, THEN    00367200
      *         THE PROCESS LOOKS FOR ANY FUTURE RELATIONSHIPS.         00367300
      *                                                                 00367400
      *    -    IF NO RELATIONSHIPS ARE FOUND IN THE TIMEFRAME YOU ARE  00367500
      *         LOOKING FOR, THIS UTILITY WILL GET THE KNOWN VENDOR/    00367600
      *         UNKNOWN BRAND ROW FOR THE DEPARTMENT.  IF ONE IS NOT    00367700
      *         NOT FOUND IT WILL BE INSERTED AND THEN RETURNED IN:     00367800
      *         - BV003-PV-ENT-ID-OUT                                   00367905
      *         - BV003-PV-BRAND-OUT                                    00368005
      *         - BV003-PV-DEPT-OUT                                     00369005
      *         - BV003-PV-RLTNSHP-ID-OUT                               00369105
      *                                                                 00369200
      *    ###  DATES  ###                                              00369300
      *    -  FOR EACH SEARCH A DATE CAN BE PROVIDED.  IF THE DATE IS   00369400
      *       PROVIDED THE SEARCH WILL LOOK FOR RELATIONSHIPS ACTIVE ON 00369500
      *       THE SPECIFIED DATE. IF NO DATE IS PROVIDED THE DATE WILL  00369600
      *       DEFAULT TO THE CURRENT DATE.                              00369700
      *                                                                 00369800
      *    -  FOR THE ITEM NUMBER, SKU, UPC ID, AND UPC NBR LOOKUPS, IF 00369900
      *       A RELATIONSHIP ROW IS NOT FOUND THAT IS ACTIVE ON THE     00370000
      *       SPECIFIED DATE, A CHECK IS DONE TO FIND THE RELATIONSHIP  00370100
      *       INFORMATION FOR THE SPECIFIED BRAND/DEPT/VENDOR THAT      00370200
      *       MOST RECENTLY EXPIRED.  IF THAT ROW IS NOT FOUND, A       00370300
      *       CHECK IS DONE TO FIND THE FIRST ROW FOR THE SPECIFIED     00370400
      *       BRAND/VENDOR/DEPT THAT IS ACTIVE IN THE FUTURE.  IF       00370500
      *       NO ROW IS FOUND THE RELATIONSHIP INFORMATION FOR THE      00370600
      *       UNKNOWN BRAND/UNKNOWN VENDOR FOR THE DEPARTMENT IS        00370700
      *       RETRIEVED UNLESS THE ENT ID IS PROVIDED, IN WHICH CASE    00370800
      *       THE KNOWN VENDOR/UNKNOWN BRAND ROW IS PROVIDED.           00370900
      *                                                                 00371000
      *    ###  DUMMY SKUS  ###                                         00371100
      *    -  IF A SKU ENDING IN 000 OR 999 IS PROVIDED IT IS PRESUMED  00371200
      *       TO BE A DUMMY SKU.  USERS CAN CHOOSE WHETHER TO FIND THE  00371300
      *       UNKNOWN VENDOR/UNKNOWN BRAND ROW FOR A DEPARTMENT.  IF    00371400
      *       THEY CHOOSE TO AND THE DEPARTMENT FIELD IS POPULATED      00371500
      *       THE UNKNOWN/UNKNOWN ROW WILL BE POPULATED, OTHERWISE AN   00371600
      *       ERROR IS RETURNED.  IF THE SKU IS A DUMMY SKU THEN        00371700
      *       BV003-DUMMY-SKU WILL BE TRUE, REGARDLESS OF WHETHER THE   00371805
      *       USER IS GETTING THE UNKNOWN/UNKNOWN ROW.  IF THE USER     00371900
      *       FINDS A DUMMY SKU BUT IS NOT USING THE UNKNOWN/UNKNOWN    00372000
      *       ROW AN ERROR IS !!NOT!! RETURNED.                         00372100
      *                                                                 00372200
      *    ###  ERROR HANDLING  ###                                     00372300
      *    -    IF FUNCTIONS IN THIS MODULE FIND ERRORS, AN ABEND       00372400
      *         !! WILL NOT !! OCCUR.                                   00372500
      *    -    THE SQL CODE WILL BE IN BV003-PV-SQLCODE-OUT            00372605
      *                                                                 00372700
      *    -    THE ERROR TEXT WILL BE IN BV003-PV-RETURN-MSG-1 & 2     00372805
      *                                                                 00372900
      *    -    THE RETURN CODE FIELD BV003-PV-RETURN-CDE WILL HAVE :   00373005
      *         BV003-PV-NO-ERRORS-FOUND                                00373105
      *         BV003-PV-NO-REL-FOUND (NO RELATIONSHIP FOR SPECIFIED    00373205
      *                               (DATE, OR PAST, OR FUTURE)        00373300
      *         BV003-BAD-INPUT                                         00373405
      *         BV003-ITEM-NOT-FOUND                                    00373505
      *         BV003-REL-ID-NOT-FOUND                                  00373605
      *         BV003-PV-SQL-ERROR                                      00373705
      *         BV003-NO-RLTNSHP-FOR-DTE (PAST OR FUTURE DOES EXIST)    00373805
      *         BV003-PV-INVALID-FUNCTION                               00373905
      *         BV003-PV-OTHER-ERROR                                    00374005
      *         BV003-PV-ZERO-BRAND                                     00374105
      *                                                                 00374200
      *         THE FOLLOWING ERROR TYPE REPRESENT MULTIPLE ERRORS      00374300
      *             BV003-PV-ERROR-FOUND                                00374405
      *                BV003-PV-NO-REL-FOUND                            00374505
      *                BV003-BAD-INPUT                                  00374605
      *                BV003-ITEM-NOT-FOUND                             00374705
      *                BV003-REL-ID-NOT-FOUND                           00374805
      *                BV003-PV-SQL-ERROR                               00374905
      *                BV003-NO-RLTNSHP-FOR-DTE                         00375005
      *                BV003-PV-INVALID-FUNCTION                        00375105
      *                BV003-PV-OTHER-ERROR                             00375205
      *                BV003-PV-ZERO-BRAND                              00375305
      *                                                                 00375400
      *             BV003-FOUND-REL-OR-UNK     (NOT FOR CURSOR USE      00375505
      *               BV003-PV-NO-ERRORS-FOUND  OR STRAIGHT REL LOOKUP) 00375605
      *               BV003-PV-NO-REL-FOUND                             00375705
      *               BV003-ITEM-NOT-FOUND                              00375805
      *               BV003-PV-ZERO-BRAND                               00375905
      *                                                                 00376000
      *             BV003-ITM-OR-REL-NOT-FOUND (NOT FOR CURSOR USE)     00376105
      *               BV003-PV-NO-REL-FOUND                             00376205
      *               BV003-ITEM-NOT-FOUND                              00376305
      *               BV003-PV-ZERO-BRAND                               00376405
      *                                                                 00376500
      *    - BECAUSE OF THE COMPLEXITIES OF WHAT MAY OR MAY NOT         00376600
      *      CONSTITUTE AN ERROR, IT MAY JUST BE BEST TO CODE EACH      00376700
      *      INDIVIDUAL CONDITION AS APPROPRIATE.                       00376800
      *                                                                 00376900
      *                                                                 00377000
RYAN  *    - IN THE EVENT AN DBV RELATIONSHIP IS NOT FOUND FOR THE      00377100
RYAN  *      SPECIFIED DATE, THE UTILITY WILL CHECK FOR A PAST          00377200
RYAN  *      RELATIONSHIP.  IF NO PAST RELATIONSHIP IS FOUND, THE       00377300
RYAN  *      UTILITY WILL CHECK FOR A FUTURE RELATIONSHIP.  IF NO       00377400
RYAN  *      FUTURE RELATIONSHIP IS FOUND, THE UNKNOWN BRAND ROW FOR    00377500
RYAN  *      THE VENDOR/DEPARTMENT WILL BE LOOKED UP.                   00377600
RYAN  *      IF A PAST RELATIONSHIP IS FOUND BV003-PAST-DBV-REL-FOUND   00377705
RYAN  *      IS SET TO TRUE.                                            00377800
RYAN  *      IF A FUTURE RELATIONSHIP IS FOUND BV003-FUT-DBV-REL-FOUND  00377905
RYAN  *      IS SET TO TRUE.                                            00378000
      *                                                                 00378100
      *   ###  ZERO BRAND  ###                                          00378200
      *    - A ZERO BRAND CONDITION CAN OCCUR WHEN LOOKING UP:          00378300
      *        ITEM NUMBER                                              00378400
      *        SKU                                                      00378500
      *        SKU DEPARTMENT                                           00378600
      *        UPC ID                                                   00378700
      *        UPC NUMBER                                               00378800
      *                                                                 00378900
      *    - IF A ZERO BRAND IS FOUND:                                  00379000
      *      -  BV003-PV-ZERO-BRAND IS SET TO TRUE                      00379105
      *      -  IF THE INPUT ENT ID FIELD AND INPUT DEPARTMENT FIELD    00379200
      *         ARE BOTH POPULATED THE UNKNOWN BRAND ROW FOR THAT       00379300
      *         DEPARTMENT AND VENDOR IS FOUND (AFTER VALIDATING THAT   00379400
      *         THE INPUT ENT ID AND DEPARTMENT ARE VALID).             00379500
      *      -  IF THE INPUT ENT ID FIELD IS POPULATED BUT THE INPUT    00379600
      *         DEPARTMENT IS NOT, THEN THE ENT ID IS VALIDATED AND     00379700
      *         THEN THE UNKNOWN BRAND ROW FOR THE INPUT ENT ID AND     00379800
      *         THE DEPARTMENT FOR THE ITEM IS FOUND.                   00379900
      *     -   IF THE INPUT DEPARTMENT FIELD IS POPULATED BUT THE INPUT00380000
      *         ENT ID IS NOT, THEN THE DEPARTMENT IS VALIDATED AND     00380100
      *         THEN THE UNKNOWN BRAND ROW FOR THE INPUT DEPARTMENT AND 00380200
      *         THE ENT ID FOR THE ITEM IS FOUND.                       00380300
      *    ###  UNKNOWN BRAND ###                                       00380400
      *    -  A USER CAN GET THE RELATIONSHIP INFORMATION FOR           00380500
      *       KNOWN DEPARTMENT/UNKNOWN BRAND/KNOWN OR UNKNOWN DEPARTMENT00380600
      *                                                                 00380700
      *    -  IF THE DEPARTMENT IS NOT PROVIDED, AN ERROR IS RETURNED.  00380800
      *                                                                 00380900
      *    -  IF THE ROW DOES NOT ALREADY EXIST FOR THE SPECIFIED       00381000
      *       COMBINATION. THE ROW IS INSERTED ONTO THE RELATIONSHIP    00381100
      *       TABLE.                                                    00381200
      *                                                                 00381300
      *    -  FOR LOOKING UP UNKNOWN VENDOR, THE USER CAN EITHER        00381400
      *       PROVIDE THE UNKNOWN ENT ID, SPACES OR ZEROES FOR THE ENT  00381500
      *       ID.                                                       00381600
      *                                                                 00381700
      *    -  THE FOLLOWING SEARCHES WILL RETURN EITHER AN UNKNOWN      00381800
      *       BRAND/KNOWN VENDOR ROW OR AN UNKNOWN VENDOR/UNKNOWN       00381900
      *       BRAND ROW IF THE DEPARTMENT IS FILLED IN.                 00382000
      *       -  ITEM NUMBER                                            00382100
      *       -  SKU                                                    00382200
      *       -  UPC ID                                                 00382300
      *       -  UPC NUMBER                                             00382400
      *       -  VENDOR/DEPT                                            00382500
      *       -  NOTE:                                                  00382600
      *          IF THE ENT ID IS FILLED IN, THE KNOWN VENDOR/UNKNOWN   00382700
      *          BRAND ROW WILL BE RETRIEVED.  OTHERWISE THE UNKNOWN    00382800
      *          BRAND/UNKNOWN VENDOR ROW WILL BE RETRIEVED.            00382900
      *    #  OTHER NOTES                                               00383000
      *    -  BECAUSE OF DIFFERENCES IN FIELD SIZES YOU MAY WISH TO     00384000
      *       MOVE INPUT VARIABLES TO THEIR -9 VERSIONS INSTEAD OF      00385000
      *       THE X VERSIONS.                                           00386000
      *                                                                 00387000
      ******************************************************************00388000
       BV003-LOOKUP-MAIN.                                               00389005
                                                                        00390000
            INITIALIZE BV003-PV-WORK-AREA.                              00391005
            INITIALIZE BV003-PV-OUTPUT-AREA.                            00391105
            MOVE SPACES TO BV003-PV-RETURN-MSG-1                        00391205
                           BV003-PV-RETURN-MSG-2.                       00391305
            PERFORM BV003-SET-DATE.                                     00391405
            IF BV003-PV-NO-ERRORS-FOUND                                 00391505
               SET BV003-PV-MORE-RECORDS TO TRUE                        00391605
                                                                        00391700
               IF NOT BV003-BAD-INPUT                                   00391805
                  EVALUATE TRUE                                         00391900
                    WHEN BV003-PV-BRND-DEPT-LOOKUP                      00392005
                         PERFORM BV003-LOOKUP-BRND-DPT                  00393005
                    WHEN BV003-PV-BRND-LOOKUP                           00394005
                         PERFORM BV003-LOOKUP-BRND                      00395005
                    WHEN BV003-PV-BRND-VND-DPT-LOOKUP                   00396005
                         PERFORM BV003-LOOKUP-BVD                       00397005
                    WHEN BV003-PV-REL-ID-LOOKUP                         00398005
                         PERFORM BV003-LOOKUP-REL-ID                    00399005
                    WHEN BV003-PV-UNK-BRND-LOOKUP                       00400005
                         PERFORM BV003-LOOKUP-UNKNOWN                   00410005
                    WHEN BV003-PV-VNDR-DEPT-LOOKUP                      00420005
                         PERFORM BV003-LOOKUP-VND-DPT                   00430005
                    WHEN BV003-PV-VNDR-LOOKUP                           00440005
                         PERFORM BV003-LOOKUP-VND                       00450005
                    WHEN OTHER                                          00460000
                         SET BV003-PV-INVALID-FUNCTION TO TRUE          00470005
                         STRING 'INVALID FUNCTION OF ' DELIMITED BY SIZE00480000
                               BV003-PV-LOOKUP-TYPE-IN DELIMITED BY SIZE00490005
                                'IN BV003-LOOKUP-MAIN' DELIMITED BY SIZE00500005
                           INTO BV003-PV-RETURN-MSG-1                   00510005
                  END-EVALUATE                                          00520000
              END-IF                                                    00530000
           END-IF.                                                      00540000
                                                                        00550000
      ******************************************************************00560000
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND.                        00570000
      ******************************************************************00580000
       BV003-LOOKUP-BRND.                                               00590005
            PERFORM BV003-CHECK-BRAND.                                  00600005
            IF BV003-PV-NO-ERRORS-FOUND                                 00610005
               PERFORM BV003-OPEN-BRND-CSR                              00620005
               IF BV003-PV-NO-ERRORS-FOUND                              00630005
                  PERFORM BV003-FETCH-BRND-CSR                          00640005
                  PERFORM BV003-LOAD-RLTNSHP-TABLE                      00650005
                     UNTIL BV003-PV-NO-MORE-RECORDS OR                  00660005
                           BV003-PV-ERROR-FOUND                         00670005
                  IF BV003-PV-NO-ERRORS-FOUND                           00680005
                     PERFORM BV003-CLOSE-BRND-CSR                       00690005
                  END-IF                                                00700000
               END-IF                                                   00710000
            END-IF.                                                     00720000
      ******************************************************************00730000
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND DEPARTMENT              00740000
      ******************************************************************00750000
       BV003-LOOKUP-BRND-DPT.                                           00760005
            PERFORM BV003-CHECK-BRAND.                                  00770005
            IF BV003-PV-NO-ERRORS-FOUND                                 00780005
               PERFORM BV003-CHECK-DEPT                                 00790005
               IF BV003-PV-NO-ERRORS-FOUND                              00800005
                  PERFORM BV003-OPEN-BRND-DPT-CSR                       00801005
                  IF BV003-PV-NO-ERRORS-FOUND                           00801105
                     PERFORM BV003-FETCH-BRND-DPT-CSR                   00801205
                     PERFORM BV003-LOAD-RLTNSHP-TABLE                   00801305
                        UNTIL BV003-PV-NO-MORE-RECORDS OR               00801405
                              BV003-PV-ERROR-FOUND                      00801505
                     IF BV003-PV-NO-ERRORS-FOUND                        00801605
                        PERFORM BV003-CLOSE-BRND-DPT-CSR                00801705
                     END-IF                                             00801800
                  END-IF                                                00801900
               END-IF                                                   00802000
            END-IF.                                                     00802100
      ******************************************************************00802200
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND/VENDOR/DEPT.            00802300
      ******************************************************************00802400
       BV003-LOOKUP-BVD.                                                00802505
            PERFORM BV003-CHECK-ENT-ID.                                 00802605
            IF BV003-PV-NO-ERRORS-FOUND                                 00802705
               PERFORM BV003-CHECK-DEPT                                 00802805
               IF BV003-PV-NO-ERRORS-FOUND                              00802905
                  PERFORM BV003-CHECK-BRAND                             00803005
                  IF BV003-PV-NO-ERRORS-FOUND                           00803105
                     PERFORM BV003-LOOKUP-RLTNSHP                       00803205
                  END-IF                                                00803300
               END-IF                                                   00803400
            END-IF.                                                     00803500
      ******************************************************************00803600
      * GET FUTURE RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR     00803700
      ******************************************************************00803800
       BV003-LOOKUP-FUT-RLTNSHP.                                        00803905
           EXEC SQL                                                     00804000
              SELECT  DEPT_BRND_VND_ID                                  00804100
                     ,DEPT_NBR                                          00804200
                     ,LBL_SEQ_NBR                                       00804300
                     ,ENT_ID                                            00804400
               INTO :BV003-PV-RLTNSHP-ID-OUT                            00804505
                   ,:BV003-PV-DEPT-OUT                                  00804605
                   ,:BV003-PV-BRAND-OUT                                 00804705
                   ,:BV003-PV-ENT-ID-OUT                                00804805
               FROM IMT_DEPT_BRND_VND                                   00804900
              WHERE ENT_ID        = :BV003-PV-WORK-ENT-ID               00805005
                AND LBL_SEQ_NBR   = :BV003-PV-WORK-BRAND                00805105
                AND DEPT_NBR      = :BV003-PV-WORK-DEPT                 00805205
                AND EFF_BEG_DTE   = (SELECT MIN(EFF_BEG_DTE)            00805300
                                       FROM IMT_DEPT_BRND_VND BV2       00805400
                                      WHERE ENT_ID      =               00805500
                                            :BV003-PV-WORK-ENT-ID       00805605
                                        AND LBL_SEQ_NBR =               00805700
                                            :BV003-PV-WORK-BRAND        00805805
                                        AND DEPT_NBR    =               00805900
                                            :BV003-PV-WORK-DEPT         00806005
                                        AND EFF_BEG_DTE >               00806100
                                            :BV003-PV-WORK-DATE)        00806205
              WITH UR                                                   00806300
           END-EXEC                                                     00806400
                                                                        00806500
           EVALUATE TRUE                                                00806600
              WHEN SQLCODE = 0                                          00806700
RYAN               SET BV003-FUT-DBV-REL-FOUND TO TRUE                  00806805
              WHEN SQLCODE = +100                                       00806900
                   SET BV003-PV-NO-REL-FOUND TO TRUE                    00807005
                   PERFORM BV003-LOOKUP-UNKNOWN                         00807105
              WHEN OTHER                                                00807200
                   SET BV003-PV-SQL-ERROR      TO TRUE                  00807305
                   SET BV003-WORK-ERROR-BV-DPT TO TRUE                  00807405
                   PERFORM BV003-SET-ERROR-MSG2                         00807505
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 00807605
                                   BV003-PV-WORK-SQL-CODE               00807705
                   STRING 'FAIL FUTURE RELATIONSHIP LOOKUP '            00807800
                                                 DELIMITED BY SIZE      00807900
                          'IN BV003-LOOKUP-FUT-RLTNSHP'                 00808005
                                                 DELIMITED BY SIZE      00809000
                     INTO BV003-PV-RETURN-MSG-1                         00809105
           END-EVALUATE.                                                00809200
      ******************************************************************00809300
      * GET PAST RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR       00809400
      ******************************************************************00809500
       BV003-LOOKUP-PAST-RLTNSHP.                                       00809605
           EXEC SQL                                                     00809700
              SELECT DEPT_BRND_VND_ID                                   00809800
                    ,DEPT_NBR                                           00809900
                    ,LBL_SEQ_NBR                                        00810000
                    ,ENT_ID                                             00811000
                INTO :BV003-PV-RLTNSHP-ID-OUT                           00812005
                    ,:BV003-PV-DEPT-OUT                                 00813005
                    ,:BV003-PV-BRAND-OUT                                00814005
                    ,:BV003-PV-ENT-ID-OUT                               00815005
                FROM IMT_DEPT_BRND_VND                                  00816000
               WHERE ENT_ID        = :BV003-PV-WORK-ENT-ID              00817005
                 AND LBL_SEQ_NBR   = :BV003-PV-WORK-BRAND               00818005
                 AND DEPT_NBR      = :BV003-PV-WORK-DEPT                00818105
                 AND EFF_END_DTE   = (SELECT MAX(EFF_END_DTE)           00818200
                                        FROM IMT_DEPT_BRND_VND BV2      00818300
                                       WHERE ENT_ID      =              00818400
                                             :BV003-PV-WORK-ENT-ID      00818505
                                         AND LBL_SEQ_NBR =              00818600
                                             :BV003-PV-WORK-BRAND       00818705
                                         AND DEPT_NBR    =              00818800
                                             :BV003-PV-WORK-DEPT        00818905
                                         AND EFF_END_DTE <              00819000
                                             :BV003-PV-WORK-DATE)       00819105
              WITH UR                                                   00819200
           END-EXEC                                                     00819300
                                                                        00819400
           EVALUATE TRUE                                                00819500
              WHEN SQLCODE = 0                                          00819600
RYAN               SET BV003-PAST-DBV-REL-FOUND TO TRUE                 00819705
              WHEN SQLCODE = +100                                       00819800
                   PERFORM BV003-LOOKUP-FUT-RLTNSHP                     00819905
              WHEN OTHER                                                00820000
                   SET BV003-PV-SQL-ERROR TO TRUE                       00820105
                   SET BV003-WORK-ERROR-BV-DPT TO TRUE                  00820205
                   PERFORM BV003-SET-ERROR-MSG2                         00820305
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 00820405
                                   BV003-PV-WORK-SQL-CODE               00820505
                   STRING 'FAIL PAST RELATIONSHIP LOOKUP '              00820600
                                                 DELIMITED BY SIZE      00820700
                          'IN BV003-LOOKUP-PAST-RLTNSHP'                00820805
                                                 DELIMITED BY SIZE      00820900
                     INTO BV003-PV-RETURN-MSG-1                         00821005
           END-EVALUATE.                                                00821100
      ******************************************************************00821200
      * GET RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR            00821300
      ******************************************************************00821400
       BV003-LOOKUP-RLTNSHP.                                            00821505
W31475*    EXEC SQL                                                     00821602
W31475*       SELECT ML_LO_MDSE_LVL_ID                                  00821702
W31475*             ,DEPT_NBR                                           00821802
W31475*             ,LBL_SEQ_NBR                                        00821902
W31475*             ,ENT_ID                                             00822002
W31475*        INTO :BV003-PV-RLTNSHP-ID-OUT                            00822105
W31475*            ,:BV003-PV-DEPT-OUT                                  00822205
W31475*            ,:BV003-PV-BRAND-OUT                                 00822305
W31475*            ,:BV003-PV-ENT-ID-OUT                                00822405
W31475*        FROM MLV_BRND_VND                                        00822502
W31475*       WHERE ENT_ID        = :BV003-PV-WORK-ENT-ID               00822605
W31475*         AND LBL_SEQ_NBR   = :BV003-PV-WORK-BRAND                00822705
W31475*         AND EFF_BEG_DTE  <= :BV003-PV-WORK-DATE                 00822805
W31475*         AND (EFF_END_DTE IS NULL OR                             00822902
W31475*              EFF_END_DTE >= :BV003-PV-WORK-DATE)                00823005
W31475*         AND DEPT_NBR      = :BV003-PV-WORK-DEPT                 00823105
W31475*       WITH UR                                                   00823202
W31475*    END-EXEC                                                     00823302
                                                                        00823402
W31475     EXEC SQL                                                     00823502
W31475        SELECT DEPT_BRND_VND_ID                                   00823602
W31475              ,DEPT_NBR                                           00823702
W31475              ,LBL_SEQ_NBR                                        00823802
W31475              ,ENT_ID                                             00823902
W31475         INTO :BV003-PV-RLTNSHP-ID-OUT                            00824005
W31475             ,:BV003-PV-DEPT-OUT                                  00824105
W31475             ,:BV003-PV-BRAND-OUT                                 00824205
W31475             ,:BV003-PV-ENT-ID-OUT                                00824305
W31475         FROM IMT_DEPT_BRND_VND                                   00824402
W31475        WHERE DEPT_NBR      = :BV003-PV-WORK-DEPT                 00824505
W31475          AND LBL_SEQ_NBR   = :BV003-PV-WORK-BRAND                00824605
W31475          AND ENT_ID        = :BV003-PV-WORK-ENT-ID               00824705
W31475          AND EFF_BEG_DTE  <= :BV003-PV-WORK-DATE                 00824805
W31475          AND (EFF_END_DTE IS NULL OR                             00824902
W31475               EFF_END_DTE >= :BV003-PV-WORK-DATE)                00825005
W31475        WITH UR                                                   00825102
W31475     END-EXEC                                                     00825202
                                                                        00825302
W31475     EVALUATE TRUE                                                00825402
W31475        WHEN SQLCODE = 0                                          00825502
W31475             CONTINUE                                             00825602
W31475        WHEN SQLCODE = +100                                       00825702
W31475             PERFORM BV003-LOOKUP-RLTNSHP-UNKN                    00825805
W31475        WHEN OTHER                                                00826302
W31475             SET BV003-PV-SQL-ERROR TO TRUE                       00826405
W31475             SET BV003-WORK-ERROR-BV-DPT TO TRUE                  00826505
W31475             PERFORM BV003-SET-ERROR-MSG2                         00826605
W31475             MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 00826705
W31475                             BV003-PV-WORK-SQL-CODE               00826805
W31475             STRING 'FAIL RELATIONSHIP LOOKUP' DELIMITED BY SIZE  00826902
W31475                    ' IN BV003-LOOKUP-RLTNSHP' DELIMITED BY SIZE  00827005
W31475               INTO BV003-PV-RETURN-MSG-1                         00827105
W31475     END-EVALUATE.                                                00827202
W31475******************************************************************00827302
W31475* GET RELATIONSHIP INFORMATION FOR UNKNOWN DEPT/BRAND/VENDOR      00827402
W31475******************************************************************00827502
W31475 BV003-LOOKUP-RLTNSHP-UNKN.                                       00827605
W31475     EXEC SQL                                                     00827702
W31475        SELECT UNKN_BRND_VND_ID                                   00827802
W31475              ,DEPT_NBR                                           00827902
W31475              ,LBL_SEQ_NBR                                        00828002
W31475              ,ENT_ID                                             00828102
W31475         INTO :BV003-PV-RLTNSHP-ID-OUT                            00828205
W31475             ,:BV003-PV-DEPT-OUT                                  00828305
W31475             ,:BV003-PV-BRAND-OUT                                 00828405
W31475             ,:BV003-PV-ENT-ID-OUT                                00828505
W31475         FROM MLT_UNKN_BRND_VND                                   00828602
W31475        WHERE DEPT_NBR      = :BV003-PV-WORK-DEPT                 00828705
W31475          AND LBL_SEQ_NBR   = :BV003-PV-WORK-BRAND                00828805
W31475          AND ENT_ID        = :BV003-PV-WORK-ENT-ID               00828905
W31475        WITH UR                                                   00829302
W31475     END-EXEC                                                     00829402
                                                                        00829502
           EVALUATE TRUE                                                00829602
              WHEN SQLCODE = 0                                          00829702
                   CONTINUE                                             00829802
              WHEN SQLCODE = +100                                       00829902
                   SET BV003-NO-RLTNSHP-FOR-DTE TO TRUE                 00830005
                   STRING 'RELATIONSHIP WAS NOT FOUND '                 00830102
                                                DELIMITED BY SIZE       00830202
                     INTO BV003-PV-RETURN-MSG-1                         00830305
                   PERFORM BV003-LOOKUP-PAST-RLTNSHP                    00830405
              WHEN OTHER                                                00830502
                   SET BV003-PV-SQL-ERROR TO TRUE                       00830605
                   SET BV003-WORK-ERROR-BV-DPT TO TRUE                  00830705
                   PERFORM BV003-SET-ERROR-MSG2                         00830805
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 00830905
                                   BV003-PV-WORK-SQL-CODE               00831005
                   STRING 'FAIL RELATIONSHIP LOOKUP' DELIMITED BY SIZE  00831102
W31475               ' IN BV003-LOOKUP-RLTNSHP-UNKN' DELIMITED BY SIZE  00831205
W31475*                   ' IN BV003-LOOKUP-RLTNSHP' DELIMITED BY SIZE  00831305
                     INTO BV003-PV-RETURN-MSG-1                         00831405
           END-EVALUATE.                                                00831502
      ******************************************************************00831602
      * PROCESS BRAND VENDOR LOOKUP FOR AN RELATIONSHIP ID              00831702
      ******************************************************************00831802
       BV003-LOOKUP-REL-ID.                                             00831905
            IF BV003-PV-RLTNSHP-ID-IN = SPACES                          00832005
               OR BV003-PV-RLTNSHP-ID-IN NOT NUMERIC                    00832105
               OR BV003-PV-RLTNSHP-ID-IN = ZEROES                       00832205
               STRING 'BAD RELATIONSHIP ID ' DELIMITED BY SIZE          00832302
                      BV003-PV-RLTNSHP-ID-IN DELIMITED BY SIZE          00832405
                 INTO BV003-PV-RETURN-MSG-1                             00832505
               SET BV003-BAD-INPUT TO TRUE                              00832605
            ELSE                                                        00832702
               MOVE BV003-PV-RLTNSHP-ID-IN                              00832805
                   TO BV003-PV-WORK-REL-ID                              00832905
               EXEC SQL                                                 00833002
                  SELECT ML_LO_MDSE_LVL_ID                              00833102
                        ,DEPT_NBR                                       00833202
                        ,ENT_ID                                         00833302
                        ,LBL_SEQ_NBR                                    00833402
                   INTO :BV003-PV-RLTNSHP-ID-OUT                        00833505
                       ,:BV003-PV-DEPT-OUT                              00833605
                       ,:BV003-PV-ENT-ID-OUT                            00833705
                       ,:BV003-PV-BRAND-OUT                             00833805
                   FROM MLV_BRND_VND                                    00833902
                  WHERE ML_LO_MDSE_LVL_ID = :BV003-PV-WORK-REL-ID       00834005
                  WITH UR                                               00834102
               END-EXEC                                                 00834202
                                                                        00834302
               EVALUATE TRUE                                            00834402
                  WHEN SQLCODE = 0                                      00834502
                       CONTINUE                                         00834602
                  WHEN SQLCODE = +100                                   00834702
                       SET BV003-REL-ID-NOT-FOUND TO TRUE               00834805
                       STRING 'RELATIONSHIP WAS NOT FOUND '             00834902
                                                    DELIMITED BY SIZE   00835002
                         INTO BV003-PV-RETURN-MSG-1                     00835105
                  WHEN OTHER                                            00835202
                       SET BV003-PV-SQL-ERROR TO TRUE                   00835305
                       MOVE SQLCODE TO BV003-PV-SQLCODE-OUT             00835405
                                       BV003-PV-WORK-SQL-CODE           00835505
                       STRING 'FAIL REL ID LOOKUP '                     00835602
                                                     DELIMITED BY SIZE  00835702
                              ' IN BV003-LOOKUP-REL ID'                 00835805
                                                     DELIMITED BY SIZE  00835902
                         INTO BV003-PV-RETURN-MSG-1                     00836005
                       STRING 'REL ID '              DELIMITED BY SIZE  00836102
                              BV003-PV-RLTNSHP-ID-IN DELIMITED BY SIZE  00836205
                              ' SQLCODE = '          DELIMITED BY SIZE  00836302
                              BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE  00836405
                         INTO BV003-PV-RETURN-MSG-2                     00836505
              END-EVALUATE                                              00836602
            END-IF.                                                     00836702
      ******************************************************************00836802
      * PROCESS BRAND VENDOR LOOKUP FOR AN RELATIONSHIP ID              00836902
      ******************************************************************00837002
       BV003-SEQ-INSERT.                                                00837105
            EXEC SQL                                                    00837202
      *         INSERT INTO IMT_BRND_VND_ID                             00837302
      *         VALUES (IDENTITY_VAL_LOCAL())                           00837402
                INSERT INTO IMT_BRND_VND_ID                             00837502
                       ( ROW_SEQ_NBR )                                  00837602
                VALUES ( 0 )                                            00837702
            END-EXEC.                                                   00837802
                                                                        00837902
            EVALUATE TRUE                                               00838002
              WHEN SQLCODE = 0                                          00838102
                   CONTINUE                                             00838202
              WHEN OTHER                                                00838302
                  SET BV003-PV-SQL-ERROR TO TRUE                        00838405
                  MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                  00838505
                                  BV003-PV-WORK-SQL-CODE                00838605
                  STRING 'FAIL SEQUENCE INSERT LOOKUP '                 00838702
                                                DELIMITED BY SIZE       00838802
                          'IN BV003-SEQ-INSERT'                         00838905
                                                DELIMITED BY SIZE       00839002
                    INTO BV003-PV-RETURN-MSG-1                          00839105
                  STRING ' SQLCODE = '          DELIMITED BY SIZE       00839202
                         BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE       00839305
                    INTO BV003-PV-RETURN-MSG-2                          00839405
            END-EVALUATE.                                               00839502
      ******************************************************************00839602
      * GET UNKNOWN BRAND FOR A DEPARTMENT                              00839702
      ******************************************************************00839802
       BV003-LOOKUP-UNKNOWN.                                            00840005
RONNA2*    IF BV003-PV-NO-REL-FOUND OR BV003-PV-ZERO-BRAND              00850005
RONNA2     IF BV003-PV-NO-REL-FOUND                                     00860005
              MOVE BV003-PV-WORK-ENT-ID TO BV003-PV-ENT-ID-OUT          00870005
           ELSE                                                         00880000
              PERFORM BV003-CHECK-DEPT                                  00890005
RONNA2*       IF BV003-PV-NO-ERRORS-FOUND OR BV003-ITEM-NOT-FOUND       00900005
RONNA2        IF BV003-PV-NO-ERRORS-FOUND                               00910005
                 IF BV003-PV-ENT-ID-IN NOT EQUAL SPACES AND             00920005
                    BV003-PV-ENT-ID-IN NOT EQUAL ZERO                   00930005
                    PERFORM BV003-CHECK-ENT-ID                          00940005
                    MOVE BV003-PV-WORK-ENT-ID TO BV003-PV-ENT-ID-OUT    00950005
                 ELSE                                                   00960000
                    SET BV003-PV-ENT-NOT-FOUND TO TRUE                  00970005
                 END-IF                                                 00980000
              END-IF                                                    00990000
           END-IF                                                       01000000
                                                                        01010000
           IF NOT BV003-BAD-INPUT                                       01020005
              SET BV003-PV-BRAND-NOT-FOUND TO TRUE                      01030005
              EXEC SQL                                                  01040000
                 SELECT UNKN_BRND_VND_ID                                01050000
                        ,DEPT_NBR                                       01060000
                  INTO :BV003-PV-RLTNSHP-ID-OUT                         01070005
                      ,:BV003-PV-DEPT-OUT                               01080005
                  FROM MLT_UNKN_BRND_VND                                01090000
                 WHERE ENT_ID         = :BV003-PV-ENT-ID-OUT            01100005
                   AND LBL_SEQ_NBR    = :BV003-PV-BRAND-OUT             01110005
                   AND DEPT_NBR       = :BV003-PV-WORK-DEPT             01120005
                 WITH UR                                                01130000
              END-EXEC                                                  01140000
                                                                        01150000
              EVALUATE TRUE                                             01160000
                WHEN SQLCODE = 0                                        01170000
                     CONTINUE                                           01180000
                WHEN SQLCODE = +100                                     01190000
                       IF BV003-PV-ENT-ID-OUT NOT EQUAL ZERO AND        01200005
                          NOT BV003-PV-ENT-NOT-FOUND                    01210005
                          DISPLAY 'INSERT KNOWN/UNKNOWN'                01220000
                          PERFORM BV003-INSERT-KNWN-UNK                 01230005
                       ELSE                                             01240000
                          DISPLAY 'COULD NOT FIND '                     01250000
                                  ' DEPT: '       BV003-PV-WORK-DEPT    01260005
                                  ' BRAND: '      BV003-PV-BRAND-OUT    01270005
                                  ' ENT ID: '     BV003-PV-ENT-ID-OUT   01280005
                          DISPLAY 'INSERT UNKNOWN/UNKNOWN'              01290000
                          PERFORM BV003-INSERT-UNK-UNK                  01300005
                       END-IF                                           01310000
                WHEN OTHER                                              01320000
                     SET BV003-PV-SQL-ERROR TO TRUE                     01330005
                     MOVE SQLCODE TO BV003-PV-SQLCODE-OUT               01340005
                                     BV003-PV-WORK-SQL-CODE             01350005
                     STRING 'FAIL UNKNOWN/UNKNOWN INSERT '              01360000
                                                   DELIMITED BY SIZE    01370000
                             'IN BV003-LOOKUP-UNKNOWN'                  01380005
                                                   DELIMITED BY SIZE    01390000
                       INTO BV003-PV-RETURN-MSG-1                       01400005
                     STRING 'VENDOR = UNKNOWN ' DELIMITED BY SIZE       01410000
                            ' BRAND = UNKNOWN ' DELIMITED BY SIZE       01420000
                            ' SQLCODE = '         DELIMITED BY SIZE     01430000
                            BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE    01440005
                       INTO BV003-PV-RETURN-MSG-2                       01450005
              END-EVALUATE                                              01460000
           END-IF.                                                      01470000
      ******************************************************************01480000
      * INSERT UNKNOWN/UNKNOWN FOR A DEPARTMENT                         01490000
      ******************************************************************01500000
       BV003-INSERT-UNK-UNK.                                            01510005
           SET BV003-PV-ENT-NOT-FOUND   TO TRUE.                        01520005
           SET BV003-PV-BRAND-NOT-FOUND TO TRUE.                        01530005
           MOVE BV003-PV-WORK-DEPT      TO BV003-PV-DEPT-OUT.           01540005
                                                                        01550000
           PERFORM BV003-INSERT-UNKNOWN.                                01560005
           PERFORM BV003-LOOKUP-UNKNOWN.                                01570005
                                                                        01580000
      ******************************************************************01590000
      * INSERT A ROW ONTO THE UNKNOWN TABLE                             01600000
      ******************************************************************01610000
       BV003-INSERT-UNKNOWN.                                            01620005
           PERFORM BV003-SEQ-INSERT.                                    01630005
RONNA2*    IF BV003-PV-NO-ERRORS-FOUND OR BV003-PV-ITM-OR-REL-NOT-FOUND 01640005
           IF BV003-PV-NO-ERRORS-FOUND OR BV003-PV-NO-REL-FOUND         01650005
                                                                        01660000
              EXEC SQL                                                  01670000
                 INSERT INTO MLT_UNKN_BRND_VND                          01680000
                        ( UNKN_BRND_VND_ID                              01690000
                         ,DEPT_NBR                                      01700000
                         ,LBL_SEQ_NBR                                   01710000
                         ,ENT_ID                                        01720000
                         ,CRE_TMST                                      01730000
                        )                                               01740000
                  VALUES( IDENTITY_VAL_LOCAL ()                         01750000
                        , :BV003-PV-WORK-DEPT                           01760005
                        , :BV003-PV-BRAND-OUT                           01770005
                        , :BV003-PV-ENT-ID-OUT                          01780005
                        , CURRENT TIMESTAMP)                            01790000
              END-EXEC                                                  01800000
                                                                        01810000
              EVALUATE TRUE                                             01820000
                 WHEN SQLCODE = 0                                       01830000
                      SET BV003-UNKNOWN-INS TO TRUE                     01840005
                      DISPLAY 'INSERTED: '                              01850000
                              ' DEPT: '    BV003-PV-WORK-DEPT           01860005
                              ' BRAND: '   BV003-PV-BRAND-OUT           01870005
                              ' ENT ID: '  BV003-PV-ENT-ID-OUT          01880005
                      PERFORM BV003-LOOKUP-UNKNOWN                      01890005
                 WHEN OTHER                                             01900000
                      DISPLAY 'UNKNOWN INSERT FAIL'                     01910000
                      SET BV003-PV-SQL-ERROR TO TRUE                    01920005
                      MOVE SQLCODE TO BV003-PV-SQLCODE-OUT              01930005
                                      BV003-PV-WORK-SQL-CODE            01940005
                      STRING 'FAIL UNKNOWN INSERT '                     01950000
                                                    DELIMITED BY SIZE   01960000
                             'IN BV003-INSERT-UNKNOWN'                  01970005
                                                    DELIMITED BY SIZE   01980000
                        INTO BV003-PV-RETURN-MSG-1                      01990005
                      STRING ' VENDOR = '        DELIMITED BY SIZE      02000000
                             BV003-PV-ENT-ID-IN  DELIMITED BY SIZE      02010005
                             ' DEPT = '          DELIMITED BY SIZE      02020000
                             BV003-PV-DEPT-IN    DELIMITED BY SIZE      02030005
                             ' BRAND = UNKNOWN ' DELIMITED BY SIZE      02040000
                             ' SQLCODE = '       DELIMITED BY SIZE      02050000
                             BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE   02060005
                        INTO BV003-PV-RETURN-MSG-2                      02070005
              END-EVALUATE                                              02080000
           END-IF.                                                      02090000
      ******************************************************************02100000
      * INSERT KNOWN/UNKNOWN FOR A VENDOR/DEPARTMENT                    02110000
      ******************************************************************02120000
       BV003-INSERT-KNWN-UNK.                                           02130005
            SET BV003-PV-BRAND-NOT-FOUND TO TRUE.                       02140005
            MOVE BV003-PV-WORK-DEPT      TO BV003-PV-DEPT-OUT.          02150005
                                                                        02160000
           PERFORM BV003-INSERT-UNKNOWN.                                02170005
           PERFORM BV003-LOOKUP-UNKNOWN.                                02180005
      ******************************************************************02190000
      * PROCESS BRAND VENDOR LOOKUP FOR A VENDOR.                       02200000
      ******************************************************************02210000
       BV003-LOOKUP-VND.                                                02220005
            PERFORM BV003-CHECK-ENT-ID.                                 02230005
            IF BV003-PV-NO-ERRORS-FOUND                                 02240005
               PERFORM BV003-OPEN-VNDR-CSR                              02250005
               IF BV003-PV-NO-ERRORS-FOUND                              02260005
                  PERFORM BV003-FETCH-VNDR-CSR                          02270005
                  PERFORM BV003-LOAD-RLTNSHP-TABLE                      02280005
                     UNTIL BV003-PV-NO-MORE-RECORDS OR                  02290005
                           BV003-PV-ERROR-FOUND                         02300005
                  IF BV003-PV-NO-ERRORS-FOUND                           02310005
                     PERFORM BV003-CLOSE-VNDR-CSR                       02320005
                  END-IF                                                02330000
               END-IF                                                   02340000
            END-IF.                                                     02350000
      ******************************************************************02360000
      * PROCESS BRAND VENDOR LOOKUP FOR A VENDOR DEPARTMENT             02370000
      ******************************************************************02380000
       BV003-LOOKUP-VND-DPT.                                            02390005
            SET BV003-PV-VNDR-DEPT-CSR-ORIG TO TRUE.                    02400005
            PERFORM BV003-CHECK-ENT-ID.                                 02410005
            IF BV003-PV-NO-ERRORS-FOUND                                 02420005
               PERFORM BV003-CHECK-DEPT                                 02430005
               IF BV003-PV-NO-ERRORS-FOUND                              02440005
                  PERFORM BV003-OPEN-VNDR-DPT-CSR                       02450005
                  IF BV003-PV-NO-ERRORS-FOUND                           02460005
                     PERFORM BV003-FETCH-VNDR-DPT-CSR                   02470005
                     PERFORM BV003-LOAD-RLTNSHP-TABLE                   02480005
                        UNTIL BV003-PV-NO-MORE-RECORDS OR               02490005
                              BV003-PV-ERROR-FOUND                      02500005
                     IF BV003-PV-NO-ERRORS-FOUND                        02510005
                        PERFORM BV003-CLOSE-VNDR-DPT-CSR                02520005
                     END-IF                                             02530000
                     IF BV003-PV-RT-SUB-OUT EQUAL ZERO AND              02540005
                        BV003-VND-LOOK-PAST-FUT AND                     02550005
                        BV003-PV-NO-ERRORS-FOUND                        02560005
                        IF BV003-PV-PAST-MONTHS > 0                     02570005
                           PERFORM BV003-VD-PAST-FUTURE                 02580005
                        ELSE                                            02590000
                           SET BV003-BAD-INPUT TO TRUE                  02600005
                           STRING 'TO GET PAST/FUT FOR VENDOR/DEPT '    02610000
                           DELIMITED BY SIZE                            02620000
                                'THE NUMBERS OF MONTHS INTO THE'        02630000
                           DELIMITED BY SIZE                            02640000
                                'PAST MUST BE POPULATED'                02650000
                           DELIMITED BY SIZE                            02660000
                           INTO BV003-PV-RETURN-MSG-1                   02670005
                        END-IF                                          02680000
                     END-IF                                             02690000
                     IF BV003-PV-RT-SUB-OUT EQUAL ZERO AND              02700005
                        BV003-USE-KNOWN-UNKNOWN AND                     02710005
                        BV003-PV-NO-ERRORS-FOUND                        02720005
                        PERFORM BV003-LOOKUP-UNKNOWN                    02730005
                     END-IF                                             02740000
                  END-IF                                                02750000
               END-IF                                                   02760000
            END-IF.                                                     02770000
      ******************************************************************02780000
      * GET PAST AND FUTURE VENDOR DEPARTMENT INFORMATION.              02790000
      ******************************************************************02800000
       BV003-VD-PAST-FUTURE.                                            02810005
           SET BV003-PV-VNDR-DEPT-CSR-PAST TO TRUE.                     02820005
           SET BV003-PV-MORE-RECORDS       TO TRUE.                     02830005
           PERFORM BV003-OPEN-VD-PAST-CSR.                              02840005
           IF BV003-PV-NO-ERRORS-FOUND                                  02850005
              PERFORM BV003-FETCH-VD-PAST-CSR                           02860005
              PERFORM BV003-LOAD-RLTNSHP-TABLE                          02870005
                 UNTIL BV003-PV-NO-MORE-RECORDS OR                      02880005
                       BV003-PV-ERROR-FOUND                             02890005
              IF BV003-PV-NO-ERRORS-FOUND                               02900005
                 PERFORM BV003-CLOSE-VD-PAST-CSR                        02910005
              END-IF                                                    02920000
              IF BV003-PV-RT-SUB-OUT EQUAL ZERO                         02930005
                 SET BV003-PV-VNDR-DEPT-CSR-FUT TO TRUE                 02940005
                 SET BV003-PV-MORE-RECORDS      TO TRUE                 02950005
                 PERFORM BV003-OPEN-VD-FUT-CSR                          02960005
                 IF BV003-PV-NO-ERRORS-FOUND                            02970005
                    PERFORM BV003-FETCH-VD-FUT-CSR                      02980005
                    PERFORM BV003-LOAD-RLTNSHP-TABLE                    02990005
                       UNTIL BV003-PV-NO-MORE-RECORDS OR                03000005
                             BV003-PV-ERROR-FOUND                       03010005
                    IF BV003-PV-NO-ERRORS-FOUND                         03020005
                       PERFORM BV003-CLOSE-VD-FUT-CSR                   03030005
                    END-IF                                              03040000
                    IF BV003-PV-RT-SUB-OUT EQUAL ZERO                   03050005
                       SET BV003-PV-BRAND-NOT-FOUND TO TRUE             03060005
                       PERFORM BV003-LOOKUP-UNKNOWN                     03070005
                    END-IF                                              03080000
                 END-IF                                                 03090000
              END-IF                                                    03100000
           END-IF.                                                      03110000
      ******************************************************************03120000
      * OPEN THE BRAND CURSOR                                           03130000
      ******************************************************************03140000
       BV003-OPEN-BRND-CSR.                                             03150005
           EXEC SQL                                                     03160000
              OPEN BV_BRND_CSR                                          03170000
           END-EXEC                                                     03180000
                                                                        03190000
           EVALUATE TRUE                                                03200000
              WHEN SQLCODE = 0                                          03210000
                   CONTINUE                                             03220000
              WHEN OTHER                                                03230000
                   SET BV003-PV-SQL-ERROR TO TRUE                       03240005
                   SET BV003-WORK-ERROR-JUST-BRND TO TRUE               03250005
                   PERFORM BV003-SET-ERROR-MSG2                         03260005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 03270005
                                   BV003-PV-WORK-SQL-CODE               03280005
                   STRING 'FAIL BRAND CURSOR OPEN '                     03290000
                                                 DELIMITED BY SIZE      03300000
                          'IN BV003-OPEN-BRND-CSR'                      03310005
                                                 DELIMITED BY SIZE      03320000
                     INTO BV003-PV-RETURN-MSG-1                         03330005
           END-EVALUATE.                                                03340000
      ******************************************************************03350000
      * OPEN THE BRAND DEPT CURSOR                                      03360000
      ******************************************************************03370000
       BV003-OPEN-BRND-DPT-CSR.                                         03380005
           EXEC SQL                                                     03390000
              OPEN BV_BRND_DEPT_CSR                                     03400000
           END-EXEC                                                     03410000
                                                                        03420000
           EVALUATE TRUE                                                03430000
              WHEN SQLCODE = 0                                          03440000
                   CONTINUE                                             03450000
              WHEN OTHER                                                03460000
                   SET BV003-PV-SQL-ERROR TO TRUE                       03470005
                   SET BV003-WORK-ERROR-BRND-DPT TO TRUE                03480005
                   PERFORM BV003-SET-ERROR-MSG2                         03490005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 03500005
                                   BV003-PV-WORK-SQL-CODE               03510005
                   STRING 'FAIL BRAND DEPT CURSOR OPEN '                03520000
                                                 DELIMITED BY SIZE      03530000
                          'IN BV003-OPEN-BRND-DPT-CSR'                  03540005
                                                 DELIMITED BY SIZE      03550000
                     INTO BV003-PV-RETURN-MSG-1                         03560005
           END-EVALUATE.                                                03570000
      ******************************************************************03580000
      * OPEN THE VENDOR CURSOR                                          03590000
      ******************************************************************03600000
       BV003-OPEN-VNDR-CSR.                                             03610005
           EXEC SQL                                                     03620000
              OPEN BV_VNDR_CSR                                          03630000
           END-EXEC                                                     03640000
                                                                        03650000
           EVALUATE TRUE                                                03660000
              WHEN SQLCODE = 0                                          03670000
                   CONTINUE                                             03680000
              WHEN OTHER                                                03690000
                   SET BV003-PV-SQL-ERROR TO TRUE                       03700005
                   SET BV003-WORK-ERROR-JUST-VNDR TO TRUE               03710005
                   PERFORM BV003-SET-ERROR-MSG2                         03720005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 03730005
                                   BV003-PV-WORK-SQL-CODE               03740005
                   STRING 'FAIL VENDOR CURSOR OPEN '                    03750000
                                                 DELIMITED BY SIZE      03760000
                          'IN BV003-OPEN-VNDR-CSR'                      03770005
                                                 DELIMITED BY SIZE      03780000
                     INTO BV003-PV-RETURN-MSG-1                         03790005
           END-EVALUATE.                                                03800000
      ******************************************************************03810000
      * OPEN THE VENDOR DEPT CURSOR                                     03820000
      ******************************************************************03830000
       BV003-OPEN-VNDR-DPT-CSR.                                         03840005
           EXEC SQL                                                     03850000
              OPEN BV_VNDR_DEPT_CSR                                     03860000
           END-EXEC                                                     03870000
                                                                        03880000
           EVALUATE TRUE                                                03890000
              WHEN SQLCODE = 0                                          03900000
                   CONTINUE                                             03910000
              WHEN OTHER                                                03920000
                   SET BV003-PV-SQL-ERROR TO TRUE                       03930005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                03940005
                   PERFORM BV003-SET-ERROR-MSG2                         03950005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 03960005
                                   BV003-PV-WORK-SQL-CODE               03970005
                   STRING 'FAIL VENDOR DEPT CURSOR OPEN '               03980000
                                                 DELIMITED BY SIZE      03990000
                          'IN BV003-OPEN-VNDR-DPT-CSR'                  04000005
                                                 DELIMITED BY SIZE      04010000
                     INTO BV003-PV-RETURN-MSG-1                         04020005
           END-EVALUATE.                                                04030000
      ******************************************************************04040000
      * OPEN THE VENDOR DEPT PAST CURSOR                                04050000
      ******************************************************************04060000
       BV003-OPEN-VD-PAST-CSR.                                          04070005
           EXEC SQL                                                     04080000
              OPEN BV_VND_DPT_PST_CSR                                   04090000
           END-EXEC                                                     04100000
                                                                        04110000
           EVALUATE TRUE                                                04120000
              WHEN SQLCODE = 0                                          04130000
                   CONTINUE                                             04140000
              WHEN OTHER                                                04150000
                   SET BV003-PV-SQL-ERROR TO TRUE                       04160005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                04170005
                   PERFORM BV003-SET-ERROR-MSG2                         04180005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 04190005
                                   BV003-PV-WORK-SQL-CODE               04200005
                   STRING 'FAIL VENDOR DEPT PAST CURSOR OPEN '          04210000
                                                 DELIMITED BY SIZE      04220000
                          'IN BV003-OPEN-VD-PAST-CSR'                   04230005
                                                 DELIMITED BY SIZE      04240000
                     INTO BV003-PV-RETURN-MSG-1                         04250005
           END-EVALUATE.                                                04260000
      ******************************************************************04270000
      * OPEN THE VENDOR DEPT FUTURE CURSOR                              04280000
      ******************************************************************04290000
       BV003-OPEN-VD-FUT-CSR.                                           04300005
           EXEC SQL                                                     04310000
              OPEN BV_VND_DPT_FUT_CSR                                   04320000
           END-EXEC                                                     04330000
                                                                        04340000
           EVALUATE TRUE                                                04350000
              WHEN SQLCODE = 0                                          04360000
                   CONTINUE                                             04370000
              WHEN OTHER                                                04380000
                   SET BV003-PV-SQL-ERROR TO TRUE                       04390005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                04400005
                   PERFORM BV003-SET-ERROR-MSG2                         04410005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 04420005
                                   BV003-PV-WORK-SQL-CODE               04430005
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR OPEN '        04440000
                                                 DELIMITED BY SIZE      04450000
                          'IN BV003-OPEN-VD-FUT-CSR'                    04460005
                                                 DELIMITED BY SIZE      04470000
                     INTO BV003-PV-RETURN-MSG-1                         04480005
           END-EVALUATE.                                                04490000
      ******************************************************************04500000
      * FETCH A RECORD FOR THE BRAND CURSOR                             04510000
      ******************************************************************04520000
       BV003-FETCH-BRND-CSR.                                            04530005
           EXEC SQL                                                     04540000
              FETCH BV_BRND_CSR                                         04550000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           04560005
                   ,:BV003-PV-WORK-DEPT                                 04570005
                   ,:BV003-PV-WORK-ENT-ID                               04580005
           END-EXEC                                                     04590000
                                                                        04600000
           EVALUATE TRUE                                                04610000
              WHEN SQLCODE = 0                                          04620000
                   CONTINUE                                             04630000
              WHEN SQLCODE = +100                                       04640000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 04650005
              WHEN OTHER                                                04660000
                   SET BV003-PV-SQL-ERROR TO TRUE                       04670005
                   SET BV003-WORK-ERROR-JUST-BRND TO TRUE               04680005
                   PERFORM BV003-SET-ERROR-MSG2                         04690005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 04700005
                                   BV003-PV-WORK-SQL-CODE               04710005
                   STRING 'FAIL BRAND CURSOR FETCH '                    04720000
                                                 DELIMITED BY SIZE      04730000
                          'IN BV003-FETCH-BRND-CSR'                     04740005
                                                 DELIMITED BY SIZE      04750000
                     INTO BV003-PV-RETURN-MSG-1                         04760005
           END-EVALUATE.                                                04770000
      ******************************************************************04780000
      * FETCH A RECORD FOR THE BRAND DEPARTMENT CURSOR                  04790000
      ******************************************************************04800000
       BV003-FETCH-BRND-DPT-CSR.                                        04810005
           EXEC SQL                                                     04820000
              FETCH BV_BRND_DEPT_CSR                                    04830000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           04840005
                   ,:BV003-PV-WORK-ENT-ID                               04850005
           END-EXEC                                                     04860000
                                                                        04870000
           EVALUATE TRUE                                                04880000
              WHEN SQLCODE = 0                                          04890000
                   CONTINUE                                             04900000
              WHEN SQLCODE = +100                                       04910000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 04920005
              WHEN OTHER                                                04930000
                   SET BV003-PV-SQL-ERROR TO TRUE                       04940005
                   SET BV003-WORK-ERROR-BRND-DPT TO TRUE                04950005
                   PERFORM BV003-SET-ERROR-MSG2                         04960005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 04970005
                                   BV003-PV-WORK-SQL-CODE               04980005
                   STRING 'FAIL BRAND DEPT CURSOR FETCH '               04990000
                                                 DELIMITED BY SIZE      05000000
                          'IN BV003-FETCH-BRND-DPT-CSR'                 05010005
                                                 DELIMITED BY SIZE      05020000
                     INTO BV003-PV-RETURN-MSG-1                         05030005
           END-EVALUATE.                                                05040000
      ******************************************************************05050000
      * FETCH A RECORD FOR THE VENDOR CURSOR                            05060000
      ******************************************************************05070000
       BV003-FETCH-VNDR-CSR.                                            05080005
           EXEC SQL                                                     05090000
              FETCH BV_VNDR_CSR                                         05100000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           05110005
                   ,:BV003-PV-WORK-DEPT                                 05120005
                   ,:BV003-PV-WORK-BRAND                                05130005
           END-EXEC                                                     05140000
                                                                        05150000
           EVALUATE TRUE                                                05160000
              WHEN SQLCODE = 0                                          05170000
                   CONTINUE                                             05180000
              WHEN SQLCODE = +100                                       05190000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 05200005
              WHEN OTHER                                                05210000
                   SET BV003-PV-SQL-ERROR TO TRUE                       05220005
                   SET BV003-WORK-ERROR-JUST-VNDR TO TRUE               05230005
                   PERFORM BV003-SET-ERROR-MSG2                         05240005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 05250005
                                   BV003-PV-WORK-SQL-CODE               05260005
                   STRING 'FAIL VENDOR CURSOR FETCH' DELIMITED BY SIZE  05270000
                          ' IN BV003-FETCH-VNDR-CSR' DELIMITED BY SIZE  05280005
                     INTO BV003-PV-RETURN-MSG-1                         05290005
           END-EVALUATE.                                                05300000
      ******************************************************************05310000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT CURSOR                 05320000
      ******************************************************************05330000
       BV003-FETCH-VNDR-DPT-CSR.                                        05340005
           EXEC SQL                                                     05350000
              FETCH BV_VNDR_DEPT_CSR                                    05360000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           05370005
                   ,:BV003-PV-WORK-BRAND                                05380005
           END-EXEC                                                     05390000
                                                                        05400000
           EVALUATE TRUE                                                05410000
              WHEN SQLCODE = 0                                          05420000
                   CONTINUE                                             05430000
              WHEN SQLCODE = +100                                       05440000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 05450005
              WHEN OTHER                                                05460000
                   SET BV003-PV-SQL-ERROR TO TRUE                       05470005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                05480005
                   PERFORM BV003-SET-ERROR-MSG2                         05490005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 05500005
                                   BV003-PV-WORK-SQL-CODE               05510005
                   STRING 'FAIL VENDOR DEPT CURSOR FETCH '              05520000
                                                 DELIMITED BY SIZE      05530000
                          'IN BV003-FETCH-VNDR-DPT-CSR'                 05540005
                                                 DELIMITED BY SIZE      05550000
                     INTO BV003-PV-RETURN-MSG-1                         05560005
           END-EVALUATE.                                                05570000
      ******************************************************************05580000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT PAST CURSOR            05590000
      ******************************************************************05600000
       BV003-FETCH-VD-PAST-CSR.                                         05610005
           EXEC SQL                                                     05620000
              FETCH BV_VND_DPT_PST_CSR                                  05630000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           05640005
                   ,:BV003-PV-WORK-BRAND                                05650005
           END-EXEC                                                     05660000
                                                                        05670000
           EVALUATE TRUE                                                05680000
              WHEN SQLCODE = 0                                          05690000
                   CONTINUE                                             05700000
              WHEN SQLCODE = +100                                       05710000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 05720005
              WHEN OTHER                                                05730000
                   SET BV003-PV-SQL-ERROR TO TRUE                       05740005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                05750005
                   PERFORM BV003-SET-ERROR-MSG2                         05760005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 05770005
                                   BV003-PV-WORK-SQL-CODE               05780005
                   STRING 'FAIL VENDOR DEPT PAST CURSOR FETCH '         05790000
                                                 DELIMITED BY SIZE      05800000
                          'IN BV003-FETCH-VNDR-DPT-CSR'                 05810005
                                                 DELIMITED BY SIZE      05820000
                     INTO BV003-PV-RETURN-MSG-1                         05830005
           END-EVALUATE.                                                05840000
      ******************************************************************05850000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT FUTURE CURSOR          05860000
      ******************************************************************05870000
       BV003-FETCH-VD-FUT-CSR.                                          05880005
           EXEC SQL                                                     05890000
              FETCH BV_VND_DPT_FUT_CSR                                  05900000
               INTO :BV003-PV-WORK-RLTNSHP-ID                           05910005
                   ,:BV003-PV-WORK-BRAND                                05920005
           END-EXEC                                                     05930000
                                                                        05940000
           EVALUATE TRUE                                                05950000
              WHEN SQLCODE = 0                                          05960000
                   CONTINUE                                             05970000
              WHEN SQLCODE = +100                                       05980000
                   SET BV003-PV-NO-MORE-RECORDS TO TRUE                 05990005
              WHEN OTHER                                                06000000
                   SET BV003-PV-SQL-ERROR TO TRUE                       06010005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                06020005
                   PERFORM BV003-SET-ERROR-MSG2                         06030005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 06040005
                                   BV003-PV-WORK-SQL-CODE               06050005
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR FETCH '       06060000
                                                 DELIMITED BY SIZE      06070000
                          'IN BV003-FETCH-VNDR-DPT-CSR'                 06080005
                                                 DELIMITED BY SIZE      06090000
                     INTO BV003-PV-RETURN-MSG-1                         06100005
           END-EVALUATE.                                                06110000
      ******************************************************************06120000
      * CLOSE THE BRAND CURSOR                                          06130000
      ******************************************************************06140000
       BV003-CLOSE-BRND-CSR.                                            06150005
           EXEC SQL                                                     06160000
              CLOSE BV_BRND_CSR                                         06170000
           END-EXEC                                                     06180000
                                                                        06190000
           EVALUATE TRUE                                                06200000
              WHEN SQLCODE = 0                                          06210000
                   CONTINUE                                             06220000
              WHEN OTHER                                                06230000
                   SET BV003-PV-SQL-ERROR TO TRUE                       06240005
                   SET BV003-WORK-ERROR-JUST-BRND TO TRUE               06250005
                   PERFORM BV003-SET-ERROR-MSG2                         06260005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 06270005
                                   BV003-PV-WORK-SQL-CODE               06280005
                   STRING 'FAIL BRAND CURSOR CLOSE '                    06290000
                                                 DELIMITED BY SIZE      06300000
                          'IN BV003-CLOSE-BRND-CSR'                     06310005
                                                 DELIMITED BY SIZE      06320000
                     INTO BV003-PV-RETURN-MSG-1                         06330005
           END-EVALUATE.                                                06340000
      ******************************************************************06350000
      * CLOSE THE BRAND DEPT CURSOR                                     06360000
      ******************************************************************06370000
       BV003-CLOSE-BRND-DPT-CSR.                                        06380005
           EXEC SQL                                                     06390000
              CLOSE BV_BRND_DEPT_CSR                                    06400000
           END-EXEC                                                     06410000
                                                                        06420000
           EVALUATE TRUE                                                06430000
              WHEN SQLCODE = 0                                          06440000
                   CONTINUE                                             06450000
              WHEN OTHER                                                06460000
                   SET BV003-PV-SQL-ERROR TO TRUE                       06470005
                   SET BV003-WORK-ERROR-BRND-DPT TO TRUE                06480005
                   PERFORM BV003-SET-ERROR-MSG2                         06490005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 06500005
                                   BV003-PV-WORK-SQL-CODE               06510005
                   STRING 'FAIL BRAND DEPT CURSOR CLOSE '               06520000
                                                 DELIMITED BY SIZE      06530000
                          'IN BV003-CLOSE-BRND-DPT-CSR'                 06540005
                                                 DELIMITED BY SIZE      06550000
                     INTO BV003-PV-RETURN-MSG-1                         06560005
           END-EVALUATE.                                                06570000
      ******************************************************************06580000
      * CLOSE THE VENDOR CURSOR                                         06590000
      ******************************************************************06600000
       BV003-CLOSE-VNDR-CSR.                                            06610005
           EXEC SQL                                                     06620000
              CLOSE BV_VNDR_CSR                                         06630000
           END-EXEC                                                     06640000
                                                                        06650000
           EVALUATE TRUE                                                06660000
              WHEN SQLCODE = 0                                          06670000
                   CONTINUE                                             06680000
              WHEN OTHER                                                06690000
                   SET BV003-PV-SQL-ERROR TO TRUE                       06700005
                   SET BV003-WORK-ERROR-JUST-VNDR TO TRUE               06710005
                   PERFORM BV003-SET-ERROR-MSG2                         06720005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 06730005
                                   BV003-PV-WORK-SQL-CODE               06740005
                   STRING 'FAIL VENDOR CURSOR CLOSE' DELIMITED BY SIZE  06750000
                          ' IN BV003-CLOSE-VNDR-CSR' DELIMITED BY SIZE  06760005
                     INTO BV003-PV-RETURN-MSG-1                         06770005
           END-EVALUATE.                                                06780000
      ******************************************************************06790000
      * CLOSE THE VENDOR DEPT CURSOR                                    06800000
      ******************************************************************06810000
       BV003-CLOSE-VNDR-DPT-CSR.                                        06820005
           EXEC SQL                                                     06830000
              CLOSE BV_VNDR_DEPT_CSR                                    06840000
           END-EXEC                                                     06850000
                                                                        06860000
           EVALUATE TRUE                                                06870000
              WHEN SQLCODE = 0                                          06880000
                   CONTINUE                                             06890000
              WHEN OTHER                                                06900000
                   SET BV003-PV-SQL-ERROR TO TRUE                       06910005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                06920005
                   PERFORM BV003-SET-ERROR-MSG2                         06930005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 06940005
                                   BV003-PV-WORK-SQL-CODE               06950005
                   STRING 'FAIL VENDOR DEPT CURSOR CLOSE '              06960000
                                                 DELIMITED BY SIZE      06970000
                          'IN BV003-CLOSE-VNDR-DPT-CSR'                 06980005
                                                 DELIMITED BY SIZE      06990000
                     INTO BV003-PV-RETURN-MSG-1                         07000005
           END-EVALUATE.                                                07010000
      ******************************************************************07020000
      * CLOSE THE VENDOR DEPT PAST CURSOR                               07030000
      ******************************************************************07040000
       BV003-CLOSE-VD-PAST-CSR.                                         07050005
           EXEC SQL                                                     07060000
              CLOSE BV_VND_DPT_PST_CSR                                  07070000
           END-EXEC                                                     07080000
                                                                        07090000
           EVALUATE TRUE                                                07100000
              WHEN SQLCODE = 0                                          07110000
                   CONTINUE                                             07120000
              WHEN OTHER                                                07130000
                   SET BV003-PV-SQL-ERROR TO TRUE                       07140005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                07150005
                   PERFORM BV003-SET-ERROR-MSG2                         07160005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 07170005
                                   BV003-PV-WORK-SQL-CODE               07180005
                   STRING 'FAIL VENDOR DEPT PAST CURSOR CLOSE '         07190000
                                                 DELIMITED BY SIZE      07200000
                          'IN BV003-CLOSE-VD-PAST-CSR'                  07210005
                                                 DELIMITED BY SIZE      07220000
                     INTO BV003-PV-RETURN-MSG-1                         07230005
           END-EVALUATE.                                                07240000
      ******************************************************************07250000
      * CLOSE THE VENDOR DEPT FUTURE CURSOR                             07260000
      ******************************************************************07270000
       BV003-CLOSE-VD-FUT-CSR.                                          07280005
           EXEC SQL                                                     07290000
              CLOSE BV_VND_DPT_FUT_CSR                                  07300000
           END-EXEC                                                     07310000
                                                                        07320000
           EVALUATE TRUE                                                07330000
              WHEN SQLCODE = 0                                          07340000
                   CONTINUE                                             07350000
              WHEN OTHER                                                07360000
                   SET BV003-PV-SQL-ERROR TO TRUE                       07370005
                   SET BV003-WORK-ERROR-VNDR-DPT TO TRUE                07380005
                   PERFORM BV003-SET-ERROR-MSG2                         07390005
                   MOVE SQLCODE TO BV003-PV-SQLCODE-OUT                 07400005
                                   BV003-PV-WORK-SQL-CODE               07410005
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR CLOSE '       07420000
                                                 DELIMITED BY SIZE      07430000
                          'IN BV003-CLOSE-VD-FUT-CSR'                   07440005
                                                 DELIMITED BY SIZE      07450000
                     INTO BV003-PV-RETURN-MSG-1                         07460005
           END-EVALUATE.                                                07470000
      ******************************************************************07480000
      *    LOAD THE OUTPUT TABLE WITH ALL RELATIONSHIPS OF THE SPECIFIED07490000
      *    TYPE.                                                        07500000
      ******************************************************************07510000
       BV003-LOAD-RLTNSHP-TABLE.                                        07520005
           ADD 1 TO BV003-PV-RT-SUB-OUT.                                07530005
                                                                        07540000
           MOVE BV003-PV-WORK-RLTNSHP-ID TO                             07550005
                    BV003-RT-RLTNSHP-ID-OUT(BV003-PV-RT-SUB-OUT).       07560005
                                                                        07570000
           MOVE BV003-PV-WORK-ENT-ID TO                                 07580005
                    BV003-RT-ENT-ID-OUT(BV003-PV-RT-SUB-OUT).           07590005
                                                                        07600000
           MOVE BV003-PV-WORK-BRAND TO                                  07610005
                    BV003-RT-BRND-OUT(BV003-PV-RT-SUB-OUT).             07620005
                                                                        07630000
           MOVE BV003-PV-WORK-DEPT        TO                            07640005
                    BV003-RT-DEPT-OUT(BV003-PV-RT-SUB-OUT).             07650005
                                                                        07660000
           EVALUATE TRUE                                                07670000
                 WHEN BV003-PV-VNDR-DEPT-LOOKUP                         07680005
                      EVALUATE TRUE                                     07690000
                         WHEN BV003-PV-VNDR-DEPT-CSR-ORIG               07700005
                              PERFORM BV003-FETCH-VNDR-DPT-CSR          07710005
                         WHEN BV003-PV-VNDR-DEPT-CSR-FUT                07720005
                              PERFORM BV003-FETCH-VD-FUT-CSR            07730005
                         WHEN BV003-PV-VNDR-DEPT-CSR-PAST               07740005
                              PERFORM BV003-FETCH-VD-PAST-CSR           07750005
                         WHEN OTHER                                     07760000
                              STRING 'INVALID VENDOR DEPT CURSOR TYPE ' 07770000
                              DELIMITED BY SIZE                         07780000
                                     'IN BV003-LOAD-RLTNSHP-TABLE'      07790005
                              DELIMITED BY SIZE                         07800000
                              INTO BV003-PV-RETURN-MSG-1                07810005
                              SET BV003-PV-OTHER-ERROR TO TRUE          07820005
                      END-EVALUATE                                      07830000
                 WHEN BV003-PV-VNDR-LOOKUP                              07840005
                      PERFORM BV003-FETCH-VNDR-CSR                      07850005
                 WHEN BV003-PV-BRND-DEPT-LOOKUP                         07860005
                      PERFORM BV003-FETCH-BRND-DPT-CSR                  07870005
                 WHEN BV003-PV-BRND-LOOKUP                              07880005
                      PERFORM BV003-FETCH-BRND-CSR                      07890005
           END-EVALUATE.                                                07900000
      ******************************************************************07910000
      * CHECK THAT THE ENT ID MEETS INITIAL REQUIREMENTS                07920000
      ******************************************************************07930000
       BV003-CHECK-ENT-ID.                                              07940005
            IF BV003-PV-ENT-ID-IN = SPACES                              07950005
                OR BV003-PV-ENT-ID-IN NOT NUMERIC                       07960005
                OR BV003-PV-ENT-ID-IN = ZEROES                          07970005
                STRING 'BAD VENDOR '      DELIMITED BY SIZE             07980000
                       BV003-PV-ENT-ID-IN DELIMITED BY SIZE             07990005
                  INTO BV003-PV-RETURN-MSG-1                            08000005
                SET BV003-BAD-INPUT TO TRUE                             08010005
            ELSE                                                        08020000
                MOVE BV003-PV-ENT-ID-IN-9 TO BV003-PV-WORK-ENT-ID       08030005
            END-IF.                                                     08040000
      ******************************************************************08050000
      * CHECK THAT THE DEPARTMENT MEETS INITIAL REQUIREMENTS            08060000
      ******************************************************************08070000
       BV003-CHECK-DEPT.                                                08080005
            IF BV003-PV-DEPT-IN = SPACES                                08090005
      *         OR BV003-PV-DEPT-IN NOT NUMERIC                         08100005
                OR BV003-PV-DEPT-IN = ZEROES                            08110005
                STRING 'BAD DEPT '      DELIMITED BY SIZE               08120000
                       BV003-PV-DEPT-IN DELIMITED BY SIZE               08130005
                  INTO BV003-PV-RETURN-MSG-1                            08140005
                SET BV003-BAD-INPUT TO TRUE                             08150005
            ELSE                                                        08160000
                MOVE BV003-PV-DEPT-IN-9 TO BV003-PV-WORK-DEPT           08170005
            END-IF.                                                     08180000
      ******************************************************************08190000
      * CHECK THAT THE BRAND MEETS INITIAL REQUIREMENTS                 08200000
      ******************************************************************08210000
       BV003-CHECK-BRAND.                                               08220005
            IF BV003-PV-BRAND-IN = SPACES                               08230005
                OR BV003-PV-BRAND-IN NOT NUMERIC                        08240005
                OR BV003-PV-BRAND-IN = ZEROES                           08250005
                STRING 'BAD BRAND '      DELIMITED BY SIZE              08260000
                       BV003-PV-BRAND-IN DELIMITED BY SIZE              08270005
                  INTO BV003-PV-RETURN-MSG-1                            08280005
                SET BV003-BAD-INPUT TO TRUE                             08290005
            ELSE                                                        08300000
                MOVE BV003-PV-BRAND-IN-9 TO BV003-PV-WORK-BRAND         08310005
            END-IF.                                                     08320000
      ******************************************************************08330000
      * CHECK THAT THE DATE FIELD MEETS INITIAL REQUIREMENTS            08340000
      * IF NO DATE IS PROVIDED, SET TO CURRENT DATE.                    08350000
      ******************************************************************08360000
       BV003-SET-DATE.                                                  08370005
            IF BV003-PV-EFF-DTE-IN = SPACES OR                          08380005
               BV003-PV-EFF-DTE-IN = ZEROES                             08390005
038845        EXEC SQL                                                  08400000
038846           SELECT CURRENT DATE                                    08410000
038847           INTO :BV003-PV-WORK-DATE                               08420005
038848           FROM SYSIBM.SYSDUMMY1                                  08430000
038849         END-EXEC                                                 08440000
               EVALUATE TRUE                                            08450000
                  WHEN SQLCODE = 0                                      08460000
                       CONTINUE                                         08470000
                  WHEN OTHER                                            08480000
                       SET BV003-PV-SQL-ERROR TO TRUE                   08490005
                       MOVE SQLCODE TO BV003-PV-SQLCODE-OUT             08500005
                                       BV003-PV-WORK-SQL-CODE           08510005
                       STRING 'FAIL CURRENT DATE FETCH SQLCODE = '      08520000
                                                     DELIMITED BY SIZE  08530000
                              BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE  08540005
                         INTO BV003-PV-RETURN-MSG-1                     08550005
               END-EVALUATE                                             08560000
            ELSE                                                        08570000
               IF BV003-PV-EFF-DTE-IN-YR NOT NUMERIC OR                 08580005
                  BV003-PV-EFF-DTE-IN-MN NOT NUMERIC OR                 08590005
                  BV003-PV-EFF-DTE-IN-DY NOT NUMERIC                    08600005
                  STRING 'BAD DATE '         DELIMITED BY SIZE          08610000
                         BV003-PV-EFF-DTE-IN DELIMITED BY SIZE          08620005
                    INTO BV003-PV-RETURN-MSG-1                          08630005
                  SET BV003-BAD-INPUT TO TRUE                           08640005
               ELSE                                                     08650000
                  MOVE BV003-PV-EFF-DTE-IN TO BV003-PV-WORK-DATE        08660005
            END-IF.                                                     08670000
      ******************************************************************08680000
      * POPULATE THE SECOND LINE OF THE ERROR MESSAGE APPROPRIATELY     08690000
      ******************************************************************08700000
       BV003-SET-ERROR-MSG2.                                            08710005
            EVALUATE TRUE                                               08720000
               WHEN BV003-WORK-ERROR-JUST-BRND                          08730005
                    STRING 'BRAND = '             DELIMITED BY SIZE     08740000
                           BV003-PV-BRAND-IN      DELIMITED BY SIZE     08750005
                           ' DATE = '             DELIMITED BY SIZE     08760000
                           BV003-PV-WORK-DATE     DELIMITED BY SIZE     08770005
                           ' SQLCODE = '          DELIMITED BY SIZE     08780000
                           BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE     08790005
                      INTO BV003-PV-RETURN-MSG-2                        08800005
               WHEN BV003-WORK-ERROR-BRND-DPT                           08810005
                    STRING 'BRAND = '             DELIMITED BY SIZE     08820000
                           BV003-PV-BRAND-IN      DELIMITED BY SIZE     08830005
                           ' DATE = '             DELIMITED BY SIZE     08840000
                           BV003-PV-WORK-DATE     DELIMITED BY SIZE     08850005
                           ' DEPT = '             DELIMITED BY SIZE     08860000
                           BV003-PV-DEPT-IN       DELIMITED BY SIZE     08870005
                           ' SQLCODE = '          DELIMITED BY SIZE     08880000
                           BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE     08890005
                      INTO BV003-PV-RETURN-MSG-2                        08900005
               WHEN BV003-WORK-ERROR-JUST-VNDR                          08910005
                    STRING ' VENDOR = '           DELIMITED BY SIZE     08920000
                           BV003-PV-ENT-ID-IN     DELIMITED BY SIZE     08930005
                           ' DATE = '             DELIMITED BY SIZE     08940000
                           BV003-PV-WORK-DATE     DELIMITED BY SIZE     08950005
                           ' SQLCODE = '          DELIMITED BY SIZE     08960000
                           BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE     08970005
                      INTO BV003-PV-RETURN-MSG-2                        08980005
               WHEN BV003-WORK-ERROR-VNDR-DPT                           08990005
                    STRING ' VENDOR = '           DELIMITED BY SIZE     09000000
                           BV003-PV-ENT-ID-IN     DELIMITED BY SIZE     09010005
                           ' DATE = '             DELIMITED BY SIZE     09020000
                           BV003-PV-WORK-DATE     DELIMITED BY SIZE     09030005
                           ' DEPT = '             DELIMITED BY SIZE     09040000
                           BV003-PV-DEPT-IN       DELIMITED BY SIZE     09050005
                           ' SQLCODE = '          DELIMITED BY SIZE     09060000
                           BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE     09070005
                      INTO BV003-PV-RETURN-MSG-2                        09080005
               WHEN BV003-WORK-ERROR-BV-DPT                             09090005
                    STRING 'VENDOR = '            DELIMITED BY SIZE     09100000
                           BV003-PV-ENT-ID-IN     DELIMITED BY SIZE     09110005
                           ' BRAND = '            DELIMITED BY SIZE     09120000
                           BV003-PV-BRAND-IN      DELIMITED BY SIZE     09130005
                           ' DATE = '             DELIMITED BY SIZE     09140000
                           BV003-PV-WORK-DATE     DELIMITED BY SIZE     09150005
                           ' DEPT = '             DELIMITED BY SIZE     09160000
                           BV003-PV-DEPT-IN       DELIMITED BY SIZE     09170005
                           ' SQLCODE = '          DELIMITED BY SIZE     09180000
                           BV003-PV-WORK-SQL-CODE DELIMITED BY SIZE     09190005
                      INTO BV003-PV-RETURN-MSG-2                        09200005
            END-EVALUATE.                                               09210000
