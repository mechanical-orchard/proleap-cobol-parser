       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUP004.                                        
       AUTHOR.                 MURALITHARAN V.                                  
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           02/18/2011.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * REMARKS                                                                 
      * -------                                                                 
      *                                                                         
      * TITLE        -  BV INVENTORY LEDGER PURGE FOR IDC STORES                
      *                                                                         
      * DESCRIPTION  -  THE PURPOSE OF THIS PROGRAM IS TO PURGE ANY ROWS        
      * FROM THE INT_SUB_CL_INV_LDGR TABLE FOR THOSE STORES THAT                
      * PLANNED TO TAKE INVENTORY DURING THE CLOSING WEEK. THIS PREPARES        
      * THE TABLE TO HANDLE THE FINAL CUT OVER WHEN THE PREVIOUS MONTH          
      * EIR IS KNOWN AND A FINAL CUT OVER CAN BE DONE. A BACKUP FILE            
      * RECORD WILL BE CREATED FOR EACH ROW DELETED.                            
      *                                                                         
      * PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
      * AND ITS VALUES                                                          
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR34557       MURALITHARAN V   09/13/2011                               
      * INITIAL VERSION                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT ALTERNATE-DATE-CNTL-FILE                                      
               ASSIGN                TO INP01.                                  
                                                                                
           SELECT PURGED-ROWS-FILE                                              
               ASSIGN                TO OUT01.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ALTERNATE-DATE-CNTL-FILE                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  ALTERNATE-DATE-CNTL-REC       PIC X(80).                             
                                                                                
       FD  PURGED-ROWS-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  PURGED-ROWS-REC               PIC X(131).                            
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      *****************************************************************         
      *  DB2 DATE ATTRIBUTES                                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  INVENTORY CONTROL TABLE                                                
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  INVENTORY PARM TABLE                                                   
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY MONTHLY SUBCLASS TABLE                                       
      *  INT_SUB_CL_INV_LDGR                                                    
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE INT00007                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COPYBOOK FOR SQL COMMUNICATION AREA                           *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  COPYBOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *  COPYBOOK OF ALTERNATE DATE CONTROL CARD RECORD LAYOUT        *         
      *****************************************************************         
                                                                                
           COPY INRD001.                                                        
                                                                                
      *****************************************************************         
      *  COPYBOOK OF FIELDS USED IN THE EDIT / VALIDATE PROCESS.      *         
      *****************************************************************         
                                                                                
           COPY INWS050.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  PA-PROGRAM ACCUMULATORS                                       *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           03  PA-TOT-PURGED-ROWS        PIC  9(09)  VALUE ZEROES.              
           03  PA-ROWS-PURGED            PIC  9(09)  VALUE ZEROES.              
           03  PA-NO-ROWS-COMMIT         PIC  9(05)  VALUE ZEROES.              
                                                                                
      ******************************************************************        
      *  PC-PROGRAM CONSTANTS                                          *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           03  PC-PGM-NAME               PIC  X(08)  VALUE 'INKUP004'.          
           03  PC-PURGE-COMMIT-LIMIT     PIC  9(03)  VALUE 900.                 
           03  PC-MAX-RETRIES            PIC S9(4) COMP SYNC VALUE +3.          
                                                                                
      ******************************************************************        
      *  PS-PROGRAM SWITCHS                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           03  PS-EOF-INCNTL-CSR-SW      PIC  X(01)  VALUE 'N'.                 
               88  PS-EOF-INCNTL-CSR                 VALUE 'Y'.                 
           03  PS-EOF-INVPAR-CSR-SW      PIC  X(01)  VALUE 'N'.                 
               88  PS-EOF-INVPAR-CSR                 VALUE 'Y'.                 
           03  PS-EOF-INV-LDGR-CSR-SW    PIC  X(01)  VALUE 'N'.                 
               88  PS-EOF-INV-LDGR-CSR               VALUE 'Y'.                 
                                                                                
      *****************************************************************         
      *  GENERAL WORKAREA                                             *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           03  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.              
           03  PV-RETRY-COUNT            PIC S9(4) COMP SYNC VALUE +0.          
           03  PV-OPERATION-MSG-1        PIC  X(60)  VALUE SPACES.              
           03  PV-OPERATION-MSG-2        PIC  X(60)  VALUE SPACES.              
           03  PV-DB2-OPER-ATTEMPTED     PIC  X(50)  VALUE SPACES.              
           03  PV-SQL-SUB                PIC S9(04) VALUE ZERO COMP.            
                                                                                
      *****************************************************************         
      *  GENERAL WORKAREA SQL ABEND                                    *        
      *****************************************************************         
       01  ABEND-CODE                    PIC S9(04)  VALUE +0 COMP SYNC.        
           88  ABEND-4013-DB2-ERROR                  VALUE +4013.               
           88  ABEND-4095-MISC-ERROR                 VALUE +4095.               
                                                                                
      ***************************************************************           
      *  CURSOR DECLARATION FOR INCNTL-CSR CURSOR                               
      ***************************************************************           
      *--  THIS CURSOR WILL IDENTIFY ANY CYCLE ID IF THE PLANNED                
      *    INVENTORY DATE OF ANY OF THE STORES ARE WITHIN THE WEEK EVEN         
      *    IF NOT ALL OF THE STORES ARE WITHIN THE WEEK. BECAUSE THIS           
      *    PROGRAM ONLY RUNS AT THE END OF CLOSING WEEK, THE IN001              
      *    DATA POINTS TO THE FIRST WEEK OF THE ACCOUNTING MONTH WHICH          
      *    IS ALSO THE CLOSING WEEK OF THE PRIOR MONTH.                         
      *    THE IN001 DATA COMES FROM THE INRD001 RECORD DESCRIPTION OF          
      *    THE INPUT ALTERNATE DATE CONTROL CARD. THE IN001 DATA IS             
      *    MOVED TO THE DCLGEN FIELDS IN PARAGRAPHS THAT PROCESS                
      *    THE CURSOR.                                                          
      *                                                                         
           EXEC SQL                                                             
               DECLARE INCNTL-CSR CURSOR WITH HOLD FOR                          
                SELECT DISTINCT B.INV_ID                                        
                  FROM TINVPAR A                                                
                      ,TINCNTL B                                                
                 WHERE A.INV_ID              = B.INV_ID                         
                   AND B.ACTV_IND            = 'Y'                              
                   AND B.OPN_FSCL_MN_NBR     = :INCNTL-OPN-FSCL-MN-NBR          
                   AND B.OPN_FSCL_MN_DTE     = :INCNTL-OPN-FSCL-MN-DTE          
                   AND (A.PLND_INV_DTE      >= :INVPAR-PLND-INV-DTE             
                   AND A.PLND_INV_DTE       <= :IN001-OPN-FSCL-WK-DTE)          
                   AND A.ACTL_FIN_BK_DTE     = '9999-09-09'                     
                   AND A.LOC_INV_STAT_CDE    = 'IN'                             
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *  CURSOR DECLARATION FOR INVPAR-CSR CURSOR                               
      ***************************************************************           
      *--  BASED UPON THE RETURN OF THE INCNTL-CSR, ALL STORES                  
      *    REGARDLESS OF PLANED INVENTORY DATE MUST BE IDENTIFIED.              
                                                                                
           EXEC SQL                                                             
               DECLARE INVPAR-CSR CURSOR WITH HOLD FOR                          
                SELECT LOC_NBR                                                  
                  FROM TINVPAR                                                  
                 WHERE INV_ID  = :INCNTL-INV-ID                                 
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *  CURSOR DECLARATION FOR INV-LDGR-CSR CURSOR                             
      ***************************************************************           
      *--  BASED UPON THE RETURN OF THE INVPAR-CSR, ALL ROWS                    
      *    FOR EACH STORE (LOCATION) ARE RETRIEVED FOR DELETION.                
      *    THE IN001 DATA IS MOVED TO THE DCLGEN FIELDS IN                      
      *    PARAGRAPHS THAT PROCESS THE CURSOR.                                  
                                                                                
           EXEC SQL                                                             
               DECLARE INV-LDGR-CSR CURSOR WITH HOLD FOR                        
                SELECT FSCL_MN_NBR                                              
                      ,FSCL_MN_END_DTE                                          
                      ,DEPT_NBR                                                 
                      ,SUB_CL_NBR                                               
                      ,STR_NBR                                                  
                      ,IN_LO_MDSE_LVL_ID                                        
                      ,SLS_RTL_AMT                                              
                      ,MKDN_RTL_AMT                                             
                      ,MKUP_RTL_AMT                                             
                      ,XFER_IN_RTL_AMT                                          
                      ,XFER_OUT_RTL_AMT                                         
                      ,RCPT_RTL_AMT                                             
                      ,PADJ_RTL_AMT                                             
                      ,SHNK_RTL_AMT                                             
                      ,END_INV_RTL_AMT                                          
                      ,CTOFF_SLS_AMT                                            
                      ,CTOFF_SHNK_AMT                                           
                      ,CTOFF_END_INV_AMT                                        
OCFSFS                ,STR_PARL_SHIP_RTL_AMT                                    
                      ,STR_EXPTED_RTL_AMT                                       
                      ,ML_PSTG_MN_END_DTE                                       
                  FROM INT_SUB_CL_INV_LDGR                                      
                 WHERE FSCL_MN_NBR       = :INT00007-FSCL-MN-NBR                
                   AND FSCL_MN_END_DTE   = :INT00007-FSCL-MN-END-DTE            
                   AND STR_NBR           = :INT00007-STR-NBR                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      *        MAINLINE ROUTINE                                       *         
      *****************************************************************         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INIT.                                                   
                                                                                
           IF  IN001-IN-CLOSE-WK-YES                                            
               PERFORM 2000-OPEN-INCNTL-CSR                                     
                                                                                
               MOVE 'N'                   TO PS-EOF-INCNTL-CSR-SW               
               PERFORM 2100-PROCESS-INCNTL-CSR                                  
                 UNTIL PS-EOF-INCNTL-CSR                                        
                                                                                
               PERFORM 2200-CLOSE-INCNTL-CSR                                    
                                                                                
           ELSE                                                                 
               MOVE '0000-MAINLINE'         TO PV-PARAGRAPH-NAME                
               DISPLAY 'NOT IN CLOSE WEEK, NOTHING TO PURGE'                    
               SET ABEND-4095-MISC-ERROR    TO TRUE                             
               PERFORM 9200-ABEND                                               
           END-IF.                                                              
      *                                                                         
           PERFORM 8000-CLOSE-FILES.                                            
      *                                                                         
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * 1000-INIT : OPEN INPUT/OUTPUT FILES AND VALIDATE THE DATES    *         
      *             IN INPUT FILE                                     *         
      *****************************************************************         
       1000-INIT.                                                               
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************INKUP004 STARTING*************'.                
           DISPLAY '******************************************'.                
                                                                                
           MOVE '*1000-INIT*'              TO PV-PARAGRAPH-NAME                 
                                                                                
           OPEN INPUT  ALTERNATE-DATE-CNTL-FILE                                 
                OUTPUT PURGED-ROWS-FILE.                                        
                                                                                
           INITIALIZE  IN001-RECORD.                                            
                                                                                
           MOVE 'INKUP004'                 TO PV-IN050-PGM-NUMBER               
           PERFORM 1100-EDIT-VALIDATE-CNTL.                                     
                                                                                
           DISPLAY 'IN001-IN-CLOSE-WK         '                                 
                          IN001-IN-CLOSE-WK.                                    
           DISPLAY 'IN001-OPN-FSCL-MN-DTE     '                                 
                          IN001-OPN-FSCL-MN-DTE.                                
           DISPLAY 'IN001-OPN-FSCL-WK-DTE     '                                 
                          IN001-OPN-FSCL-WK-DTE.                                
           DISPLAY 'IN001-OPN-FSCL-MN-NBR     '                                 
                          IN001-OPN-FSCL-MN-NBR.                                
           DISPLAY 'IN001-OPN-FSCL-WK-BEG-DTE '                                 
                          IN001-OPN-FSCL-WK-BEG-DTE.                            
                                                                                
      *****************************************************************         
      * 2000-OPEN-INCNTL-CSR : OPEN INCNTL-CSR CURSOR                 *         
      *****************************************************************         
       2000-OPEN-INCNTL-CSR.                                                    
                                                                                
           MOVE IN001-OPN-FSCL-MN-NBR       TO INCNTL-OPN-FSCL-MN-NBR           
                                               INT00007-FSCL-MN-NBR             
           MOVE IN001-OPN-FSCL-MN-DTE       TO INCNTL-OPN-FSCL-MN-DTE           
                                               INT00007-FSCL-MN-END-DTE         
           MOVE IN001-OPN-FSCL-WK-BEG-DTE   TO INVPAR-PLND-INV-DTE              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN INCNTL-CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD OPEN FOR INCNTL-CSR'                        
                       MOVE '2000-OPEN-INCNTL-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                       MOVE 'OPEN INCNTL-CSR      '                             
                                            TO PV-DB2-OPER-ATTEMPTED            
                       DISPLAY SPACES                                           
                       DISPLAY 'TINCNTL.ACTV_IND =  Y'                          
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_NBR =  '                    
                                               INCNTL-OPN-FSCL-MN-NBR           
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_END_DTE = '                 
                                               INCNTL-OPN-FSCL-MN-DTE           
                       DISPLAY 'TINCNTL.PLND_INV_DTE >= '                       
                                               INVPAR-PLND-INV-DTE              
                       DISPLAY 'TINCNTL.PLND_INV_DTE <= '                       
                                               IN001-OPN-FSCL-WK-DTE            
                       DISPLAY 'ACTL_FIN_BK_DTE = 9999-09-09'                   
                       DISPLAY 'LOC_INV_STAT_CDE = IN'                          
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR OPEN **'          
               MOVE '2000-OPEN-INCNTL-CSR'  TO PV-PARAGRAPH-NAME                
               MOVE 'OPEN INCNTL-CSR  '     TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    2100-PROCESS-INCNTL-CSR :                                   *        
      *    THIS CURSOR WILL IDENTIFY ANY CYCLE ID IF THE PLANNED       *        
      *    INVENTORY DATE OF ANY OF THE STORES ARE WITHIN THE WEEK     *        
      *    EVEN IF NOT ALL OF THE STORES ARE WITHIN THE WEEK. BECAUSE  *        
      *    THIS PROGRAM ONLY RUNS AT THE END OF CLOSING WEEK,          *        
      *    THE IN001 DATA POINTS TO THE FIRST WEEK OF THE ACCOUNTING   *        
      *    MONTH WHICH IS ALSO THE CLOSING WEEK OF THE PRIOR MONTH.    *        
      ******************************************************************        
       2100-PROCESS-INCNTL-CSR.                                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   FETCH  INCNTL-CSR                                            
                    INTO :INCNTL-INV-ID                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       PERFORM 2300-OPEN-INVPAR-CSR                             
                       MOVE 'N'             TO PS-EOF-INVPAR-CSR-SW             
                       PERFORM 2400-PROCESS-INVPAR-CSR                          
                         UNTIL PS-EOF-INVPAR-CSR                                
                       PERFORM 2500-CLOSE-INVPAR-CURSOR                         
                   WHEN SQLCODE = 100                                           
                       SET PS-EOF-INCNTL-CSR                                    
                                            TO TRUE                             
                   WHEN SQLWARN0 NOT = SPACE                                    
                   WHEN SQLCODE  NOT = ZERO                                     
                       DISPLAY 'BAD RETURN FROM INCNTL-CSR FETCH'               
                       MOVE 'FETCH ON INCNTL-CSR  '                             
                                            TO PV-DB2-OPER-ATTEMPTED            
                       MOVE '2100-FETCH-INCNTL-CSR'                             
                                            TO PV-PARAGRAPH-NAME                
                       DISPLAY SPACES                                           
                       DISPLAY 'TINCNTL.ACTV_IND =  Y'                          
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_NBR =  '                    
                                               INCNTL-OPN-FSCL-MN-NBR           
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_END_DTE = '                 
                                               INCNTL-OPN-FSCL-MN-DTE           
                       DISPLAY 'TINCNTL.PLND_INV_DTE >= '                       
                                               INVPAR-PLND-INV-DTE              
                       DISPLAY 'TINCNTL.PLND_INV_DTE <= '                       
                                               IN001-OPN-FSCL-WK-DTE            
                       DISPLAY 'ACTL_FIN_BK_DTE = 9999-09-09'                   
                       DISPLAY 'LOC_INV_STAT_CDE = IN'                          
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR FETCH **'         
               MOVE '2100-FETCH-INCNTL-CSR'    TO PV-PARAGRAPH-NAME             
               MOVE 'FETCH INCNTL-CSR  '       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2200-CLOSE-INCNTL-CSR : CLOSE INCNTL-CSR CURSOR               *         
      *****************************************************************         
       2200-CLOSE-INCNTL-CSR.                                                   
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE INCNTL-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD CLOSE FOR INCNTL-CSR'                       
                       MOVE '2200-CLOSE-INCNTL-CSR'                             
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE INCNTL-CSR      '                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       DISPLAY SPACES                                           
                       DISPLAY 'TINCNTL.ACTV_IND =  Y'                          
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_NBR =  '                    
                                               INCNTL-OPN-FSCL-MN-NBR           
                       DISPLAY 'TINCNTL.OPN_FSCL_MN_END_DTE = '                 
                                               INCNTL-OPN-FSCL-MN-DTE           
                       DISPLAY 'TINCNTL.PLND_INV_DTE >= '                       
                                               INVPAR-PLND-INV-DTE              
                       DISPLAY 'TINCNTL.PLND_INV_DTE <= '                       
                                               IN001-OPN-FSCL-WK-DTE            
                       DISPLAY 'ACTL_FIN_BK_DTE = 9999-09-09'                   
                       DISPLAY 'LOC_INV_STAT_CDE = IN'                          
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INCNTL-CSR CLOSE **'         
               MOVE '2200-CLOSE-INCNTL-CSR'    TO PV-PARAGRAPH-NAME             
               MOVE 'CLOSE INCNTL-CSR  '       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2300-OPEN-INVPAR-CSR : OPEN INVPAR-CSR CURSOR                 *         
      *****************************************************************         
       2300-OPEN-INVPAR-CSR.                                                    
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN INVPAR-CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD OPEN FOR INVPAR-CSR'                        
                       MOVE '2300-OPEN-INVPAR-CSR'                              
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON INVPAR-CSR '                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       DISPLAY 'INCNTL-INV-ID = ' INCNTL-INV-ID                 
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INVPAR-CSR OPEN **'          
               MOVE '2300-OPEN-INVPAR-CSR' TO PV-PARAGRAPH-NAME                 
               MOVE 'OPEN INVPAR-CSR      ' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2400-PROCESS-INVPAR-CSR :                                      *        
      * BASED UPON THE RETURN OF THE INCNTL-CSR, ALL STORES            *        
      * REGARDLESS OF PLANED INVENTORY DATE MUST BE IDENTIFIED         *        
      ******************************************************************        
       2400-PROCESS-INVPAR-CSR.                                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                    FETCH INVPAR-CSR                                            
                     INTO :INVPAR-LOC-NBR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       DISPLAY 'CYCLE ' INCNTL-INV-ID                           
                               'LOC ' INVPAR-LOC-NBR ' BEING PURGED'            
                                    ' FROM INVENTORY LEDGER'                    
                       MOVE INVPAR-LOC-NBR      TO INT00007-STR-NBR             
                       PERFORM 2600-OPEN-INV-LDGR-CSR                           
                       MOVE 'N'             TO PS-EOF-INV-LDGR-CSR-SW           
                       PERFORM 2700-PROCESS-INV-LDGR-CSR                        
                         UNTIL PS-EOF-INV-LDGR-CSR                              
                       PERFORM 2800-CLOSE-INV-LDGR-CSR                          
                                                                                
                   WHEN SQLCODE = 100                                           
                       SET PS-EOF-INVPAR-CSR                                    
                                            TO TRUE                             
                   WHEN SQLWARN0 NOT = SPACE                                    
                   WHEN SQLCODE  NOT = ZERO                                     
                       DISPLAY 'BAD RETURN FROM INVPAR-CSR FETCH'               
                       MOVE 'FETCH ON INVPAR-CSR '                              
                                            TO PV-DB2-OPER-ATTEMPTED            
                       MOVE '2400-PROCESS-INVPAR-CSR'                           
                                            TO PV-PARAGRAPH-NAME                
                       DISPLAY 'INCNTL-INV-ID = ' INCNTL-INV-ID                 
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INVPAR-CSR FETCH **'         
               MOVE '2400-PROCESS-INVPAR-CSR'  TO PV-PARAGRAPH-NAME             
               MOVE 'FETCH INVPAR-CSR  '       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2500-CLOSE-INVPAR-CURSOR : CLOSE INVPAR-CSR CURSOR            *         
      *****************************************************************         
       2500-CLOSE-INVPAR-CURSOR.                                                
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE INVPAR-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD CLOSE FOR INVPAR-CSR'                       
                       MOVE '2500-CLOSE-INVPAR-CSR'                             
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON INVPAR-CSR  '                             
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       DISPLAY 'INCNTL-INV-ID = ' INCNTL-INV-ID                 
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INVPAR-CSR CLOSE **'         
               MOVE 'CLOSE INVPAR-CSR  '     TO PV-DB2-OPER-ATTEMPTED           
               MOVE '2500-CLOSE-INVPAR-CSR'  TO PV-PARAGRAPH-NAME               
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2600-OPEN-INV-LDGR-CSR : OPEN INV-LDGR-CSR CURSOR             *         
      *****************************************************************         
       2600-OPEN-INV-LDGR-CSR.                                                  
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN INV-LDGR-CSR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD OPEN FOR INV-LDGR-CSR'                      
                       MOVE '2600-OPEN-INV-LDGR-CSR'                            
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN INV-LDGR-CSR CURSOR'                          
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       DISPLAY SPACES                                           
                       DISPLAY 'FSCL_MN_NBR = ' INT00007-FSCL-MN-NBR            
                       DISPLAY 'FSCL_MN_END_DTE = '                             
                                                INT00007-FSCL-MN-END-DTE        
                       DISPLAY 'STR_NBR = '     INT00007-STR-NBR                
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** MAX RETRIES EXCEEDED ON INV-LDGR-CSR OPEN**'         
               MOVE '2600-OPEN-INV-LDGR-CSR' TO PV-PARAGRAPH-NAME               
               MOVE 'OPEN INV-LDGR-CSR '     TO PV-DB2-OPER-ATTEMPTED           
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2700-PROCESS-INV-LDGR-CSR :                                    *        
      * BASED UPON THE RETURN OF THE INVPAR-CSR, ALL ROWS FOR EACH     *        
      * STORE (LOCATION) ARE RETRIEVED FOR DELETION AND AN AUDIT       *        
      * FILE RECORD IS WRITTEN.                                        *        
      ******************************************************************        
       2700-PROCESS-INV-LDGR-CSR.                                               
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                    FETCH INV-LDGR-CSR                                          
                     INTO :INT00007-FSCL-MN-NBR                                 
                         ,:INT00007-FSCL-MN-END-DTE                             
                         ,:INT00007-DEPT-NBR                                    
                         ,:INT00007-SUB-CL-NBR                                  
                         ,:INT00007-STR-NBR                                     
                         ,:INT00007-IN-LO-MDSE-LVL-ID                           
                         ,:INT00007-SLS-RTL-AMT                                 
                         ,:INT00007-MKDN-RTL-AMT                                
                         ,:INT00007-MKUP-RTL-AMT                                
                         ,:INT00007-XFER-IN-RTL-AMT                             
                         ,:INT00007-XFER-OUT-RTL-AMT                            
                         ,:INT00007-RCPT-RTL-AMT                                
                         ,:INT00007-PADJ-RTL-AMT                                
                         ,:INT00007-SHNK-RTL-AMT                                
                         ,:INT00007-END-INV-RTL-AMT                             
                         ,:INT00007-CTOFF-SLS-AMT                               
                         ,:INT00007-CTOFF-SHNK-AMT                              
                         ,:INT00007-CTOFF-END-INV-AMT                           
OCFSFS                   ,:INT00007-STR-PARL-SHIP-RTL-AMT                       
                         ,:INT00007-STR-EXPTED-RTL-AMT                          
                         ,:INT00007-ML-PSTG-MN-END-DTE                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       PERFORM 3000-WRITE-PURGED-ROWS-REC                       
                       PERFORM 4000-DELETE-INV-SCL                              
                   WHEN SQLCODE = 100                                           
                       SET PS-EOF-INV-LDGR-CSR                                  
                                            TO TRUE                             
                   WHEN SQLWARN0 NOT = SPACE                                    
                   WHEN SQLCODE  NOT = ZERO                                     
                       DISPLAY 'BAD RETURN FROM INV-LDGR-CSR FETCH'             
                       MOVE 'FETCH ON INV-LDGR-CSR'                             
                                            TO PV-DB2-OPER-ATTEMPTED            
                       MOVE '2700-PROCESS-INV-LDGR-CSR'                         
                                            TO PV-PARAGRAPH-NAME                
                       DISPLAY SPACES                                           
                       DISPLAY 'FSCL_MN_NBR = ' INT00007-FSCL-MN-NBR            
                       DISPLAY 'FSCL_MN_END_DTE = '                             
                                                INT00007-FSCL-MN-END-DTE        
                       DISPLAY 'STR_NBR = '     INT00007-STR-NBR                
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '**MAX RETRIES EXCEEDED ON INV-LDGR-CSR FETCH**'         
               MOVE '2700-PROCESS-INV-LDGR-CSR' TO PV-PARAGRAPH-NAME            
               MOVE 'FETCH INV-LDGR-CSR'       TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2800-CLOSE-INV-LDGR-CSR : CLOSE INV-LDGR-CSR CURSOR           *         
      *****************************************************************         
       2800-CLOSE-INV-LDGR-CSR.                                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE INV-LDGR-CSR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'BAD CLOSE FOR INV-LDGR-CSR'                     
                       MOVE '2800-CLOSE-INV-LDGR-CSR'                           
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON INV-LDGR-CSR'                             
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       DISPLAY SPACES                                           
                       DISPLAY 'FSCL_MN_NBR = ' INT00007-FSCL-MN-NBR            
                       DISPLAY 'FSCL_MN_END_DTE = '                             
                                                INT00007-FSCL-MN-END-DTE        
                       DISPLAY 'STR_NBR = '     INT00007-STR-NBR                
                       DISPLAY SPACES                                           
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '**MAX RETRIES EXCEEDED ON INV-LDGR-CSR CLOSE**'         
               MOVE '2800-CLOSE-INV-LDGR-CSR' TO PV-PARAGRAPH-NAME              
               MOVE 'CLOSE INV-LDGR-CSR'     TO PV-DB2-OPER-ATTEMPTED           
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * COMMITING THE DELETES AFTER THE LAST COMMIT                   *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC                                                             
                                                                                
           IF  SQLCODE NOT = ZERO                                               
               DISPLAY 'BAD COMMIT CALL'                                        
               MOVE '2800-CLOSE-INV-LDGR-CSR'                                   
                                           TO PV-PARAGRAPH-NAME                 
               MOVE 'COMMIT CALL TO INT_SUB_CL_INV_LDGR'                        
                                           TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           ELSE                                                                 
               ADD 1                       TO PA-NO-ROWS-COMMIT                 
               MOVE ZERO                   TO PA-ROWS-PURGED                    
               DISPLAY 'THE NUMBER OF COMMITS TAKEN : '                         
                        PA-NO-ROWS-COMMIT                                       
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 3000-WRITE-PURGED-ROWS-REC :                                  *         
      * WRITE RECORDS TO PURGED-ROWS-REC FROM DCLINT00007             *         
      *****************************************************************         
       3000-WRITE-PURGED-ROWS-REC.                                              
                                                                                
           WRITE PURGED-ROWS-REC  FROM DCLINT00007.                             
           ADD 1 TO PA-TOT-PURGED-ROWS.                                         
                                                                                
                                                                                
      *****************************************************************         
      * 4000-DELETE-INV-SCL :                                         *         
      * WRITE RECORDS FROM INT_SUB_CL_INV_LDGR TABLE                  *         
      *****************************************************************         
       4000-DELETE-INV-SCL.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                DELETE FROM INT_SUB_CL_INV_LDGR                                 
                 WHERE FSCL_MN_NBR        = :INT00007-FSCL-MN-NBR               
                   AND FSCL_MN_END_DTE    = :INT00007-FSCL-MN-END-DTE           
                   AND DEPT_NBR           = :INT00007-DEPT-NBR                  
                   AND SUB_CL_NBR         = :INT00007-SUB-CL-NBR                
                   AND STR_NBR            = :INT00007-STR-NBR                   
                   AND IN_LO_MDSE_LVL_ID  = :INT00007-IN-LO-MDSE-LVL-ID         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = +100                                   
                   WHEN SQLCODE        = -911                                   
                   WHEN SQLCODE        = -913                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '4000-DELETE-INV-SCL'                               
                                                TO PV-PARAGRAPH-NAME            
                       MOVE 'DELETE FROM INT_SUB_CL_INV_LDGR'                   
                                                TO PV-DB2-OPER-ATTEMPTED        
                       DISPLAY 'BAD RETURN FROM DELETE OF '                     
                               'INT_SUB_CL_INV_LDGR'                            
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY 'MAX RETRIES EXCEEDED INT_SUB_CL_INV_LDGR DELETE'        
               MOVE '4000-DELETE-INV-SCL'       TO PV-PARAGRAPH-NAME            
               MOVE 'DELETE FROM INT_SUB_CL_INV_LDGR'                           
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           PERFORM 4100-COMIT-CHECK.                                            
                                                                                
      ******************************************************************        
      * CHECK PURGED ROWS COUNT > COMMIT LIMIT AND ISSUE COMMIT        *        
      ******************************************************************        
       4100-COMIT-CHECK.                                                        
                                                                                
           ADD +1              TO PA-ROWS-PURGED                                
                                                                                
           IF  PA-ROWS-PURGED  > PC-PURGE-COMMIT-LIMIT                          
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
                                                                                
               IF  SQLCODE NOT = ZERO                                           
                   DISPLAY 'BAD COMMIT CALL'                                    
                   MOVE '4100-COMIT-CHECK'     TO PV-PARAGRAPH-NAME             
                   MOVE 'COMMIT CALL TO INT_SUB_CL_INV_LDGR'                    
                                               TO PV-DB2-OPER-ATTEMPTED         
                   PERFORM 9900-SQL-ABEND                                       
               ELSE                                                             
                   ADD 1                       TO PA-NO-ROWS-COMMIT             
                   MOVE ZERO                   TO PA-ROWS-PURGED                
                   DISPLAY 'THE NUMBER OF COMMITS TAKEN : '                     
                            PA-NO-ROWS-COMMIT                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 8000-CLOSE-FILES : CLOSE INPUT/OUTPUT FILES                   *         
      *****************************************************************         
       8000-CLOSE-FILES.                                                        
                                                                                
           CLOSE ALTERNATE-DATE-CNTL-FILE                                       
                 PURGED-ROWS-FILE.                                              
                                                                                
           DISPLAY 'TOTAL PURGED ROWS : '            PA-TOT-PURGED-ROWS.        
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************INKUP004 ENDING***************'.                
           DISPLAY '******************************************'.                
                                                                                
                                                                                
      *****************************************************************         
      * 9200-ABEND : PERFORM NON SQL ABENDS                           *         
      *****************************************************************         
       9200-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = '  PC-PGM-NAME.                     
           DISPLAY '** PARAGRAPH       ** = '  PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION       ** = '  PV-OPERATION-MSG-1.              
           DISPLAY '**                 ** = '  PV-OPERATION-MSG-2.              
           PERFORM 9950-ABEND.                                                  
                                                                                
      ******************************************************************        
      * 9900-SQL-ABEND : SQL ABEND ROUTINE                             *        
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4013-DB2-ERROR         TO TRUE.                            
           DISPLAY '**  ABEND          **'.                                     
           DISPLAY '**  PROGRAM        **  = ' PC-PGM-NAME.                     
           DISPLAY '**  PARAGRAPH      **  = ' PV-PARAGRAPH-NAME.               
           DISPLAY '**  OPERATION      **  = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      * THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                    
      * DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                   
      *                                                                         
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9950-ABEND.                                                  
      *                                                                         
       9950-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
      * PROCEDURE DIVISION COPYBOOK TO VALIDATE THE DATES IN I/P FILE *         
      *****************************************************************         
           COPY INPD050.                                                        
                                                                                
      *****************************************************************         
