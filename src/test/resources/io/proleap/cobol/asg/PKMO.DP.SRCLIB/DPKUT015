000200 IDENTIFICATION DIVISION.                                                 
000300                                                                          
000400 PROGRAM-ID.    DPKUT015.                                                 
000500                                                                          
000600 AUTHOR.        CALVIN BUSH.                                              
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  09/24/90.                                                 
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200** THIS PROGRAM CREATES THE FILE THAT WILL BE LOADED INTO THE             
001300** MENU-ITEM VSAM FILE USED BY THE CICS ARHCITECTURE MENU PROGRAM.        
001400*****************************************************************         
001500     EJECT                                                                
001600 ENVIRONMENT DIVISION.                                                    
001700                                                                          
001800 CONFIGURATION SECTION.                                                   
001900                                                                          
002000 SOURCE-COMPUTER.  IBM-3090.                                              
002100 OBJECT-COMPUTER.  IBM-3090.                                              
002200                                                                          
002300 INPUT-OUTPUT SECTION.                                                    
002400                                                                          
002500 FILE-CONTROL.                                                            
002600     SELECT OUTPUT-MENU-ITEM-FILE     ASSIGN TO OUT01.                    
002700     EJECT                                                                
002800 DATA DIVISION.                                                           
002900                                                                          
003000 FILE SECTION.                                                            
003100                                                                          
003200 FD  OUTPUT-MENU-ITEM-FILE                                                
003300     RECORDING MODE IS F                                                  
003400     LABEL RECORDS ARE STANDARD                                           
003500     RECORD CONTAINS 60 CHARACTERS                                        
003600     BLOCK CONTAINS 0 RECORDS.                                            
003700 01  OUTPUT-MENU-ITEM-RECORD          PIC  X(60).                         
003800     EJECT                                                                
003900 WORKING-STORAGE SECTION.                                                 
004000 01  PV-PROGRAM-VARIABLES.                                                
004100     05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACE.            
004200     05  PV-DB2-OPERATION             PIC  X(70)  VALUE SPACE.            
004300                                                                          
004400 01  PC-PROGRAM-CONSTANTS.                                                
004500     05  PC-MENU-TRAN-ID              PIC  X(04)  VALUE 'Z007'.           
004600                                                                          
004700 01  IV-INDICATOR-VARIABLES.                                              
004800     05  IV-DESC                      PIC S9(04)  VALUE +0                
004900                                                  COMP.                   
005000                                                                          
005100 01  ABEND-CODE                       PIC S9(04)  VALUE +0                
005200                                                  COMP                    
005300                                                  SYNC.                   
005400*                                                                         
005500**   PARAMETER LIST USED TO CALL DB2 ABEND ROUTINE DSNTIAR                
005600*                                                                         
005700     COPY DPWS004.                                                        
005800*                                                                         
005900**   OUTPUT RECORD LAYOUT FOR THE MENU ITEM FILE                          
006000*                                                                         
006100     COPY DPRD504.                                                        
006200*                                                                         
006300**   DCLGEN FOR TABLE TMENUITEM -- MENU ITEM                              
006400*                                                                         
006500     EXEC SQL                                                             
006600         INCLUDE TMNUITM                                                  
006700     END-EXEC.                                                            
006800*                                                                         
006900**   DCLGEN FOR TABLE TMNEMONIC -- MNEMONIC/TRANSACTION                   
007000*                                                                         
007100     EXEC SQL                                                             
007200         INCLUDE TMNMNC                                                   
007300     END-EXEC.                                                            
007400*                                                                         
007500**   THE DB2 COMMUNICATIONS AREA                                          
007600*                                                                         
007700     EXEC SQL                                                             
007800         INCLUDE SQLCA                                                    
007900     END-EXEC.                                                            
008000                                                                          
008100     EXEC SQL                                                             
008200         DECLARE  MENU_ITEM_CSR CURSOR FOR                                
008300         SELECT   TMNEMONIC.MNEMONIC_CODE,                                
008400                  TMNEMONIC.PANEL_TITLE_NAME,                             
008500                  TMENUITEM.ITEM_MNEMONIC_CODE,                           
008600                  TMENUITEM.SEQUENCE_NMBR,                                
008700                  TMENUITEM.DESC,                                         
008800                  TMENUITEM.IN_APLCTN_COMM_CDE                            
008900         FROM     TMNEMONIC,                                              
009000                  TMENUITEM                                               
009100         WHERE    TMNEMONIC.TRANS_ID_CODE = :PC-MENU-TRAN-ID AND          
009200                  TMENUITEM.MENU_NAME = TMNEMONIC.MNEMONIC_CODE           
009300         ORDER BY TMNEMONIC.MNEMONIC_CODE,                                
009400                  TMENUITEM.SEQUENCE_NMBR                                 
009500     END-EXEC.                                                            
009600     EJECT                                                                
009700 PROCEDURE DIVISION.                                                      
009800*****************************************************************         
009900**  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.             
010000*****************************************************************         
010100 A000-MAIN-DRIVER.                                                        
010200     PERFORM B100-INITIALIZATION.                                         
010300     PERFORM B200-BUILD-MENU-ITEM-RECORDS                                 
010400         UNTIL SQLCODE = +100.                                            
010500     PERFORM B300-TERMINATION.                                            
010600     GOBACK.                                                              
010700*****************************************************************         
010800**  THIS MODULE WILL OPEN THE OUTPUT FILE AND THE DB2 CURSOR AND          
010900**  DO AN INITIAL FETCH ON THE CURSOR.                                    
011000*****************************************************************         
011100 B100-INITIALIZATION.                                                     
011200     OPEN OUTPUT OUTPUT-MENU-ITEM-FILE.                                   
011300     INITIALIZE DCLTMENUITEM                                              
011400                DCLTMNEMONIC.                                             
011500     EXEC SQL                                                             
011600         OPEN MENU_ITEM_CSR                                               
011700     END-EXEC.                                                            
011800     IF  SQLCODE = 0                                                      
011900         CONTINUE                                                         
012000     ELSE                                                                 
012100         MOVE 'B100-INITIALIZATION'   TO PV-PARAGRAPH-NAME                
012200         MOVE 'OPEN MENU ITEM CURSOR' TO PV-DB2-OPERATION                 
012300         PERFORM U900-SQL-ABEND                                           
012400     END-IF.                                                              
012500     PERFORM C100-FETCH-MENU-ITEM-CURSOR.                                 
012600****************************************************************          
012700**  THIS PARAGRAPH FILLS AND WRITES THE OUTPUT MENU-ITEM RECORD.          
012800**  THE MENU ITEM DESCRIPTION DEFAULTS TO THE PANEL TITLE IF THE          
012900**  MENU ITEM DESCRIPTION IS NULL.                                        
013000****************************************************************          
013100 B200-BUILD-MENU-ITEM-RECORDS.                                            
013200     INITIALIZE DP504-MENU-ITEM-RECORD.                                   
013300     MOVE TMNEMONIC-MNEMONIC-CODE                                         
013400                                TO DP504-MENU-NAME.                       
013500     MOVE TMENUITEM-ITEM-MNEMONIC-CODE                                    
013600                                TO DP504-MENU-ITEM-MNEMONIC               
013700                                   TMNEMONIC-MNEMONIC-CODE.               
013800     MOVE TMENUITEM-SEQUENCE-NMBR                                         
013900                                TO DP504-MENU-ITEM-ORDER-NUMBER.          
014000                                                                          
014100     PERFORM C200-SELECT-MNEMONIC-TRANSID.                                
014200     IF  IV-DESC NEGATIVE                                                 
014300         MOVE TMNEMONIC-PANEL-TITLE-NAME                                  
014400                                TO DP504-MENU-ITEM-DESCRIPTION            
014500     ELSE                                                                 
014600         MOVE TMENUITEM-DESC    TO DP504-MENU-ITEM-DESCRIPTION            
014700     END-IF.                                                              
014800     MOVE TMNEMONIC-TRANS-ID-CODE                                         
014900                                TO DP504-MENU-ITEM-TRAN-ID.               
015000     MOVE TMNEMONIC-AVAIL-INDCT TO DP504-MENU-ITEM-AVAIL-IND.             
           MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                     
                                      TO DP504-ACRT-LOC-REQR-IND.               
015100     MOVE TMNEMONIC-CMAREA-FRMT-CODE                                      
015200                                TO DP504-MENU-ITEM-COMMAREA-FRMT.         
015300     MOVE TMENUITEM-IN-APLCTN-COMM-CDE                                    
015400                                TO DP504-INT-APPL-FORMAT-IND.             
015500     PERFORM U800-WRITE-MNEMONIC-RECORD.                                  
015600     PERFORM C100-FETCH-MENU-ITEM-CURSOR.                                 
015700                                                                          
015800                                                                          
015900 B300-TERMINATION.                                                        
016000     CLOSE OUTPUT-MENU-ITEM-FILE.                                         
016100     EXEC SQL                                                             
016200        CLOSE MENU_ITEM_CSR                                               
016300     END-EXEC.                                                            
016400                                                                          
016500     IF  SQLCODE = 0                                                      
016600         CONTINUE                                                         
016700     ELSE                                                                 
016800         MOVE 'B300-TERMINATION'       TO PV-PARAGRAPH-NAME               
016900         MOVE 'CLOSE MENU ITEM CURSOR' TO PV-DB2-OPERATION                
017000         PERFORM U900-SQL-ABEND                                           
017100     END-IF.                                                              
017200                                                                          
017300                                                                          
017400 C100-FETCH-MENU-ITEM-CURSOR.                                             
017500     EXEC SQL                                                             
017600         FETCH MENU_ITEM_CSR                                              
017700         INTO  :TMNEMONIC-MNEMONIC-CODE,                                  
017800               :TMNEMONIC-PANEL-TITLE-NAME,                               
017900               :TMENUITEM-ITEM-MNEMONIC-CODE,                             
018000               :TMENUITEM-SEQUENCE-NMBR,                                  
018100               :TMENUITEM-DESC                                            
018200                   :IV-DESC,                                              
018300               :TMENUITEM-IN-APLCTN-COMM-CDE                              
018400     END-EXEC.                                                            
018500     IF  (SQLCODE = 0                                                     
018600                 OR +100)                                                 
018700         CONTINUE                                                         
018800     ELSE                                                                 
018900         MOVE 'C100-FETCH-MENU-ITEM-CURSOR' TO PV-PARAGRAPH-NAME          
019000         MOVE 'FETCH MNEMONIC CURSOR'      TO PV-DB2-OPERATION            
019100         PERFORM U900-SQL-ABEND                                           
019200     END-IF.                                                              
019300                                                                          
019400                                                                          
019500 C200-SELECT-MNEMONIC-TRANSID.                                            
019600     EXEC SQL                                                             
019700         SELECT TRANS_ID_CODE,                                            
019800                AVAIL_INDCT,                                              
                      ACRT_LOC_REQR_IND,                                        
019900                CMAREA_FRMT_CODE,                                         
020000                PANEL_TITLE_NAME                                          
020100         INTO  :TMNEMONIC-TRANS-ID-CODE,                                  
020200               :TMNEMONIC-AVAIL-INDCT,                                    
                     :TMNEMONIC-ACRT-LOC-REQR-IND,                              
020300               :TMNEMONIC-CMAREA-FRMT-CODE,                               
020400               :TMNEMONIC-PANEL-TITLE-NAME                                
020500         FROM  TMNEMONIC                                                  
020600         WHERE MNEMONIC_CODE = :TMNEMONIC-MNEMONIC-CODE                   
020700     END-EXEC.                                                            
020800                                                                          
020900     IF  SQLCODE = 0                                                      
021000         CONTINUE                                                         
021100     ELSE                                                                 
021200         MOVE 'C200-SELECT-MNEMONIC-TRANSID' TO PV-PARAGRAPH-NAME         
021300         STRING 'SELECT TMNEMONIC.TRANS_ID_CODE FOR MNEMONIC ('           
021400                                            DELIMITED BY SIZE             
021500                TMNEMONIC-MNEMONIC-CODE     DELIMITED BY SIZE             
021600                ')'                         DELIMITED BY SIZE             
021700             INTO PV-DB2-OPERATION                                        
021800         PERFORM U900-SQL-ABEND                                           
021900     END-IF.                                                              
022000****************************************************************          
022100**  THIS PARAGRAPH WRITES THE OUTPUT CONVERSION RECORDS.                  
022200****************************************************************          
022300 U800-WRITE-MNEMONIC-RECORD.                                              
022400     WRITE OUTPUT-MENU-ITEM-RECORD                                        
022500         FROM DP504-MENU-ITEM-RECORD.                                     
022600******************************************************************        
022700* THIS PARAGRAPH PERFORMS THE ABEND FOR THE PROGRAM IN THE                
022800* EVENT THAT AN SQL ERROR CODE OCCURS.                                    
022900******************************************************************        
023000 U900-SQL-ABEND.                                                          
023100     DISPLAY '** ABEND **         **'.                                    
023200     DISPLAY '** PROGRAM          ** = DPKUT015'.                         
023300     DISPLAY '** PARAGRAPH NAME   ** = ' PV-PARAGRAPH-NAME.               
023400     DISPLAY '** DB2 OPERATION    ** = ' PV-DB2-OPERATION.                
023500                                                                          
023600     COPY DPPD004.                                                        
023700                                                                          
023800     EXEC SQL                                                             
023900         COMMIT                                                           
024000     END-EXEC.                                                            
024100                                                                          
024200     MOVE +4013 TO ABEND-CODE.                                            
024300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
024400                                                                          
