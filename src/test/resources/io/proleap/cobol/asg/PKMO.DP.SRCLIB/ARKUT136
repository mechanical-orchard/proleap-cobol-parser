      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             ARKUT136.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2019.                                       
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  PURPOSE:                                                               
      *  THIS PROGRAM WILL TOKENIZE THE ACCOUNT NUMBER FIELD IN THE             
      *  INPUT FILE.                                                            
      *  PROTEGRITY MODULE WILL BE INVOKED TO TOKENIZE THE ACCOUNT              
      *  NUMBER                                                                 
      *                                                                         
      *  INPUT FILES :   PSTDMON-FILE              DDNAME = INP01               
      *  OUTPUT FILES:   PSTDMON-TOKEN-FILE        DDNAME = OUT01               
      *                                                                         
      *****************************************************************         
      *----------------------------------------------------------------*        
      *                                                                *        
      * DATE:        07/24/2019                                        *        
      * WR/IR#:      CHG0230275                                        *        
      * PROGRAMMER:  COGNIZANT                                         *        
      * DESCRIPTION: INITIAL VERSION CREATED FOR FD FILE TRANSFER      *        
      *              PROJECT                                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PSTDMON-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PSTDMON-TOKEN-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PSTDMON-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  PSTDMON-RECORD              PIC X(276).                              
                                                                                
       FD  PSTDMON-TOKEN-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  PSTDMON-TOKEN-RECORD        PIC X(276).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  FILLER                            PIC X(50)  VALUE                   
           ' *** WORKING STORAGE STARTS HERE FOR ARKUT136 *** '.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW          PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-END-OF-FILE                VALUE 'N'.                 
               88  PS-END-OF-FILE                    VALUE 'Y'.                 
           05  PS-HDR-TRLR-REC-SW         PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-HDR-TRLR-REC               VALUE 'N'.                 
               88  PS-HDR-TRLR-REC                   VALUE 'Y'.                 
           05  PS-ACCT-SSN                PIC X(08)  VALUE SPACES.              
               88  PS-ACCT-NBR                       VALUE 'ACCT NBR'.          
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE                   
                                                       'ARKUT136'.              
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-READ-COUNT                 PIC  9(09) VALUE ZEROES.           
           05  PV-WRITE-COUNT                PIC  9(09) VALUE ZEROES.           
           05  PV-INPUT-LENGTH               PIC  9(09) VALUE ZEROES.           
           05  PV-OUTPUT-RECORD              PIC  X(276) VALUE SPACES.          
           05  PV-ACCT-NBR                   PIC  X(16) VALUE SPACES.           
           05  PV-PIID-NBR                   PIC  X(19) VALUE SPACES.           
                                                                                
       01  ABEND-CODE                        PIC S9(04) VALUE ZERO              
                                                        COMP SYNC.              
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR DATALINK ACCOUNT FILE - INPUT AND OUTPUT     *        
      ******************************************************************        
           COPY ARRD136.                                                        
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY PTYCCSIP.                                                       
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY DPWS031.                                                        
                                                                                
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA                                                      
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE NOMN-CHD-ACCOUNT-NUMBER             
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-PARA                                            
                 UNTIL PS-END-OF-FILE.                                          
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE                                                         
      *     INITIALIZE THE VARIABLES, OPEN THE INPUT AND OUTPUT FILES           
      *     AND READ THE INPUT FILE                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           DISPLAY 'PROGRAM - ' PC-PROGRAM-NAME.                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
           MOVE PC-PROGRAM-NAME               TO DP031-PROGRAM-NAME.            
                                                                                
           OPEN INPUT  PSTDMON-FILE                                             
                OUTPUT PSTDMON-TOKEN-FILE.                                      
                                                                                
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PARA                                                       
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE ACCOUNT NUMBER                      
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       2000-PROCESS-PARA.                                                       
                                                                                
           IF PS-NOT-HDR-TRLR-REC                                               
              IF AR136-NOMN-CHD-ACCOUNT-NUMBER NOT EQUAL SPACES                 
                 INITIALIZE PV-ACCT-NBR                                         
                            PV-INPUT-LENGTH                                     
                 SET PS-ACCT-NBR          TO TRUE                               
                 MOVE AR136-NOMN-CHD-ACCOUNT-NUMBER TO PV-ACCT-NBR              
                                                                                
                 INSPECT FUNCTION REVERSE (PV-ACCT-NBR)                         
                     TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                
                 COMPUTE PV-INPUT-LENGTH =                                      
                     LENGTH OF PV-ACCT-NBR - PV-INPUT-LENGTH                    
                                                                                
                 PERFORM 2300-TOKEN-NOMN-CHD-ACCT-NBR                           
              END-IF                                                            
              IF AR136-NOMN-PIID-NUMBER  NOT EQUAL SPACES                       
                                                                                
                 INITIALIZE PV-PIID-NBR                                         
                            PV-INPUT-LENGTH                                     
                                                                                
                 SET PS-ACCT-NBR          TO TRUE                               
                 MOVE AR136-NOMN-PIID-NUMBER  TO PV-PIID-NBR                    
                                                                                
                 INSPECT FUNCTION REVERSE (PV-PIID-NBR)                         
                     TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                
                 COMPUTE PV-INPUT-LENGTH =                                      
                     LENGTH OF PV-PIID-NBR - PV-INPUT-LENGTH                    
                                                                                
                 PERFORM 2400-TOKENIZE-PIID-NBR                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2200-WRITE-FILE.                                             
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2100-READ-FILE                                                          
      *      READ THE RECORDS IN PSTDMON-FILE                                   
      ******************************************************************        
       2100-READ-FILE.                                                          
                                                                                
           READ PSTDMON-FILE INTO AR136-RECORDS                                 
              AT END                                                            
                 SET PS-END-OF-FILE  TO TRUE                                    
              NOT AT END                                                        
                 ADD 1               TO PV-READ-COUNT                           
           END-READ.                                                            
                                                                                
           IF AR136-RECORDS(1:3) = 'TRL' OR 'HDR'                               
              SET PS-HDR-TRLR-REC     TO TRUE                                   
           ELSE                                                                 
              SET PS-NOT-HDR-TRLR-REC TO TRUE                                   
           END-IF.                                                              
      ******************************************************************        
      * 2200-WRITE-FILE                                                         
      *      WRITE THE RECORDS TO OUTPUT                                        
      ******************************************************************        
       2200-WRITE-FILE.                                                         
                                                                                
           INITIALIZE PV-OUTPUT-RECORD.                                         
           MOVE AR136-RECORDS            TO PV-OUTPUT-RECORD.                   
                                                                                
           WRITE PSTDMON-TOKEN-RECORD FROM PV-OUTPUT-RECORD.                    
           ADD 1                         TO PV-WRITE-COUNT.                     
                                                                                
      ******************************************************************        
      * 2300-TOKEN-NOMN-CHD-ACCT-NBR                                            
      *    TOKENIZE NOMN-CHD-ACCOUNT-NUMBER                                     
      ******************************************************************        
       2300-TOKEN-NOMN-CHD-ACCT-NBR.                                            
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-ACCT-NBR             TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT TO PV-ACCT-NBR                             
              MOVE PV-ACCT-NBR        TO AR136-NOMN-CHD-ACCOUNT-NUMBER          
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2400-TOKENIZE-PIID-NBR                                                  
      *    TOKENIZE NOMN-PIID-NBR                                               
      ******************************************************************        
       2400-TOKENIZE-PIID-NBR.                                                  
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-PIID-NBR             TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT TO PV-PIID-NBR                             
              MOVE PV-PIID-NBR        TO AR136-NOMN-PIID-NUMBER                 
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9000-EOJ-ROUTINE                                                        
      *      CLOSE THE INPUT AND OUTPUT FILES, DISPLAY THE MESSAGES             
      *      AND TERMINATE THE PROCESS.                                         
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE PSTDMON-FILE                                                   
                 PSTDMON-TOKEN-FILE.                                            
                                                                                
           IF PV-READ-COUNT = 0                                                 
              DISPLAY 'INPUT FILE IS EMPTY'                                     
           ELSE                                                                 
              DISPLAY 'RECORDS READ   : ' PV-READ-COUNT                         
              DISPLAY 'RECORDS WRITTEN: ' PV-WRITE-COUNT                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE           
      *      PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                      
      ******************************************************************        
            COPY DPPD031.                                                       
                                                                                
      ******************************************************************        
