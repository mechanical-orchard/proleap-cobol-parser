       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT220.                                       
       AUTHOR.                  JOHN SELWITSCHKA.                               
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            03/12/2008.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  MLKUT220 - BRAND VENDOR SHRINK PRORATION PROGRAM              *        
      *                                                                *        
      *  THIS PROGRAM TAKES IN THE WEEKLY DETAIL LEDGER CONDENSED FILE *        
      *  AND AN UNLOAD FILE OF THE TWKSHNK TABLE AND PRORATES THE      *        
      *  SHRINK DOWN TO THE WEEKLY DETAIL LEDGER FILE (FSCL-WK-END-DTE,*        
      *  DEPT-NBR, MAJ-CL-NBR, SUB-CL-NBR, LOC-NBR, ML-LO-MDSE-LVL-ID) *        
      *  ACCORDING TO THE PERCENTAGE OF SALES. THIS PROGRAM REPLACES   *        
      *  MLKUT203.                                                     *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * WR34567  2008-03-12  J SELWITSCHKA                             *        
      *                      INITIAL IMPLEMENTATION                    *        
W34517* WR34517  2008-07-23  J SELWITSCHKA                             *        
W34517*                      ALLOCATE REMAINING SHRINK TO THE DKEY     *        
W34517*                      WITH THE LARGEST ABSOLUTE POSITIVE OR     *        
W34517*                      NEGATIVE VALUE WITHIN DEPT/CLASS/LOC.     *        
W31475* W31475        06/01/2009   COGNIZANT                           *00570799
W31475*                            CHANGES ARE MADE AS PART OF ML      *00570899
W31475*                            BATCH PERF TUNING PROJECT           *00570999
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT WEEKLY-DTL-LDGR                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT TWKSHNK-EXTRACT                                               
               ASSIGN TO INP02.                                                 
                                                                                
W31475     SELECT WEEKLY-DTL-LDGR-CORR                                          
W31475         ASSIGN TO INP03.                                                 
                                                                                
           SELECT WEEKLY-DTL-LDGR-SHNK                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  WEEKLY-DTL-LDGR                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WEEKLY-DTL-LDGR-REC             PIC X(450).                          
                                                                                
       FD  TWKSHNK-EXTRACT                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TWKSHNK-EXTRACT-REC             PIC X(41).                           
                                                                                
W31475 FD  WEEKLY-DTL-LDGR-CORR                                                 
W31475     RECORDING MODE IS F                                                  
W31475     LABEL RECORDS ARE STANDARD                                           
W31475     BLOCK CONTAINS 0 RECORDS.                                            
W31475 01  WEEKLY-DTL-LDGR-CORR-REC        PIC X(450).                          
                                                                                
       FD  WEEKLY-DTL-LDGR-SHNK                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WEEKLY-DTL-LDGR-SHNK-REC        PIC X(80).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PROGRAM-NAME             PIC  X(08) VALUE 'MLKUT220'.         
           05  PC-MAX-RETRIES              PIC S9(04)      VALUE +3             
                               COMP.                                            
           05  PC-MAX-RPT-LINES            PIC S9(05)      VALUE +75            
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-WEEKLY-DTL        PIC X           VALUE 'N'.           
               88  END-OF-WEEKLY-DTL                       VALUE 'Y'.           
               88  NOT-END-OF-WEEKLY-DTL                   VALUE 'N'.           
           05  PS-END-OF-TWKSHNK           PIC X           VALUE 'N'.           
               88  END-OF-TWKSHNK                          VALUE 'Y'.           
               88  NOT-END-OF-TWKSHNK                      VALUE 'N'.           
W31475     05  PS-END-OF-WEEKLY-DTL-CORR   PIC X           VALUE 'N'.           
W31475         88  END-OF-WEEKLY-DTL-CORR                  VALUE 'Y'.           
W31475         88  NOT-END-OF-WEEKLY-DTL-CORR              VALUE 'N'.           
W31475     05  PS-HOLD-DTL-LDGR            PIC X           VALUE 'N'.           
W31475         88  HOLD-DTL-LDGR                           VALUE 'Y'.           
W31475         88  READ-DTL-LDGR                           VALUE 'N'.           
W31475     05  PS-HOLD-DTL-LDGR-CORR       PIC X           VALUE 'N'.           
W31475         88  HOLD-DTL-LDGR-CORR                      VALUE 'Y'.           
W31475         88  READ-DTL-LDGR-CORR                      VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLES BEGIN ***'.                               
                                                                                
W31475 01  PV-WK1-COMPARE.                                                      
W31475     05  PV-WK1-FSCL-WK-END-DTE       PIC  X(10).                         
W31475     05  PV-WK1-DEPT-NBR              PIC  S9(6).                         
W31475     05  PV-WK1-SUB-CL-NBR            PIC  S9(6).                         
W31475     05  PV-WK1-LOC-NBR               PIC  S9(5).                         
W31475     05  PV-WK1-ML-LWST-ID            PIC  X(10).                         
W31475 01  PV-WK2-COMPARE.                                                      
W31475     05  PV-WK2-FSCL-WK-END-DTE       PIC  X(10).                         
W31475     05  PV-WK2-DEPT-NBR              PIC  S9(6).                         
W31475     05  PV-WK2-SUB-CL-NBR            PIC  S9(6).                         
W31475     05  PV-WK2-LOC-NBR               PIC  S9(5).                         
W31475     05  PV-WK2-ML-LWST-ID            PIC  X(10).                         
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
           05  PV-CURR-EXTRACTED           PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ZERO-SLS-SHRINK          PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CURR-READ                PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TWKSHNK-READ             PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DKEY-NOT-FOUND           PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPERATION-ATTEMPTED      PIC X(30)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-LWST-ID-PCT-SALES        PIC S9(04)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KEY-DATA-SHNK-DIFF       PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KEY-DATA-SHNK-RTL-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
W34517     05  PV-KEY-SHNK-RTL-AMT-POS     PIC S9(11)V9(2) VALUE ZEROES         
W34517                         COMP-3.                                          
W34517     05  PV-KEY-SHNK-RTL-AMT-NEG     PIC S9(11)V9(2) VALUE ZEROES         
W34517                         COMP-3.                                          
           05  PV-SLS-RTL-AMT              PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-FIRST-FSCL-END-DTE       PIC X(10)       VALUE SPACES.        
           05  PV-PREV-DEPT-NBR            PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KEY-DATA.                                                     
               10  PV-FSCL-END-DTE         PIC X(10)       VALUE SPACES.        
               10  PV-DEPT-NBR-CHAR        PIC X(03).                           
               10  PV-DEPT-NBR REDEFINES PV-DEPT-NBR-CHAR                       
                                           PIC 9(03).                           
               10  PV-MAJ-CL-NBR           PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SUB-CL-NBR-CHAR      PIC X(02).                           
               10  PV-SUB-CL-NBR REDEFINES PV-SUB-CL-NBR-CHAR                   
                                           PIC 9(02).                           
               10  PV-LOC-NBR              PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
               10  PV-ENT-ID               PIC S9(9)       VALUE ZEROES         
                               COMP.                                            
           05  PV-ML-LWST-ID-9             PIC 9(09)       VALUE ZEROES.        
W34517     05  PV-ENT-ID-POS               PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
W34517     05  PV-LBL-SEQ-NBR-POS          PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
W34517     05  PV-ML-LO-MDSE-LVL-ID-POS    PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
W34517     05  PV-ENT-ID-NEG               PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
W34517     05  PV-LBL-SEQ-NBR-NEG          PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
W34517     05  PV-ML-LO-MDSE-LVL-ID-NEG    PIC 9(09)       VALUE ZEROES         
W34517                         COMP.                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** ABEND AREA BEGIN ***'.                                          
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4013.         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** INPUT/OUTPUT RECORD AREA ***'.                                  
                                                                                
      ******************************************************************        
      *  WEEKLY DETAIL LEDGER                                                   
      ******************************************************************        
           COPY MLRD254.                                                        
                                                                                
W31475******************************************************************        
W31475*  WEEKLY DETAIL LEDGER CORRECTION                                        
W31475******************************************************************        
W31475 01  PV-CORR-READ                  PIC S9(13) VALUE ZEROES COMP-3.        
W31475 01  WK-SV-UPDATE-REC              PIC  X(450).                           
W31475 01  MLCOR-UPDATE-REC.                                                    
W31475     05 MLCOR-FSCL-MN-NBR          PIC  X(2).                             
W31475     05 MLCOR-FSCL-MN-END-DTE      PIC  X(10).                            
W31475     05 MLCOR-FSCL-WK-NBR          PIC  X(2).                             
W31475     05 MLCOR-FSCL-WK-END-DTE      PIC  X(10).                            
W31475     05 MLCOR-DEPT-NBR             PIC  S9(4)V COMP-3.                    
W31475     05 MLCOR-MAJ-CL-NBR           PIC  S9(4)V COMP-3.                    
W31475     05 MLCOR-SUB-CL-NBR           PIC  S9(4)V COMP-3.                    
W31475     05 MLCOR-LOC-NBR              PIC  S9(4) COMP.                       
W31475     05 MLCOR-ENT-ID               PIC  S9(9) COMP.                       
W31475     05 MLCOR-LBL-SEQ-NBR          PIC  S9(9) COMP VALUE ZEROS.           
W31475     05 MLCOR-ML-LWST-ID           PIC  S9(9) COMP VALUE ZEROS.           
W31475     05 MLCOR-SKU-NBR              PIC  S9(8)V COMP-3.                    
W31475     05 MLCOR-FIN-LIAB-DTE         PIC  X(10)   VALUE SPACES.             
W31475     05 MLCOR-TRN-TYP-CDE          PIC  X(02)   VALUE SPACES.             
W31475     05 FILLER                     PIC  X(386)  VALUE SPACES.             
W31475 01  MLCUR-UPDATE-REC.                                                    
W31475     05 MLCUR-FSCL-MN-NBR          PIC  X(2).                             
W31475     05 MLCUR-FSCL-MN-END-DTE      PIC  X(10).                            
W31475     05 MLCUR-FSCL-WK-NBR          PIC  X(2).                             
W31475     05 MLCUR-FSCL-WK-END-DTE      PIC  X(10).                            
W31475     05 MLCUR-DEPT-NBR             PIC  S9(4)V COMP-3.                    
W31475     05 MLCUR-MAJ-CL-NBR           PIC  S9(4)V COMP-3.                    
W31475     05 MLCUR-SUB-CL-NBR           PIC  S9(4)V COMP-3.                    
W31475     05 MLCUR-LOC-NBR              PIC  S9(4) COMP.                       
W31475     05 MLCUR-ENT-ID               PIC  S9(9) COMP.                       
W31475     05 MLCUR-LBL-SEQ-NBR          PIC  S9(9) COMP VALUE ZEROS.           
W31475     05 MLCUR-ML-LWST-ID           PIC  S9(9) COMP VALUE ZEROS.           
W31475     05 MLCUR-SKU-NBR              PIC  S9(8)V COMP-3.                    
W31475     05 MLCUR-FIN-LIAB-DTE         PIC  X(10)   VALUE SPACES.             
W31475     05 MLCUR-TRN-TYP-CDE          PIC  X(02)   VALUE SPACES.             
W31475     05 FILLER                     PIC  X(386)  VALUE SPACES.             
                                                                                
      ******************************************************************        
      *    WEEKLY SHRINK LAYOUT (TWKSHNK FORMAT)                                
      ******************************************************************        
           COPY MLRD079.                                                        
                                                                                
      ******************************************************************        
      *  ML BRAND VENDOR SHRINK                                                 
      ******************************************************************        
           COPY MLRD080.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
W31475     SET NOT-END-OF-WEEKLY-DTL-CORR TO TRUE                               
W31475     READ WEEKLY-DTL-LDGR-CORR INTO MLCOR-UPDATE-REC                      
W31475         AT END  SET END-OF-WEEKLY-DTL-CORR TO TRUE                       
W31475         NOT AT END  ADD +1 TO PV-CORR-READ                               
W31475     END-READ                                                             
W31475     IF END-OF-WEEKLY-DTL-CORR                                            
W31475        MOVE HIGH-VALUES TO PV-WK2-COMPARE                                
W31475     ELSE                                                                 
W31475        MOVE MLCOR-FSCL-WK-END-DTE   TO PV-WK2-FSCL-WK-END-DTE            
W31475        MOVE MLCOR-DEPT-NBR          TO PV-WK2-DEPT-NBR                   
W31475        MOVE MLCOR-SUB-CL-NBR        TO PV-WK2-SUB-CL-NBR                 
W31475        MOVE MLCOR-LOC-NBR           TO PV-WK2-LOC-NBR                    
W31475        MOVE MLCOR-ML-LWST-ID        TO PV-WK2-ML-LWST-ID                 
W31475     END-IF                                                               
W31475                                                                          
W31475     SET READ-DTL-LDGR TO TRUE                                            
W31475     SET HOLD-DTL-LDGR-CORR TO TRUE                                       
W31475     MOVE LOW-VALUES TO PV-WK1-COMPARE                                    
                                                                                
                                                                                
           PERFORM 4010-READ-WKLY-DTL-LDGR.                                     
                                                                                
           PERFORM 2000-PROCESS-RECALC                                          
W31475         UNTIL END-OF-WEEKLY-DTL AND END-OF-WEEKLY-DTL-CORR.              
                                                                                
           PERFORM 3000-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM DATE AND TIME. INITIALIZE FIELDS.                        
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  WEEKLY-DTL-LDGR                                          
                       TWKSHNK-EXTRACT                                          
W31475                 WEEKLY-DTL-LDGR-CORR                                     
                OUTPUT WEEKLY-DTL-LDGR-SHNK.                                    
                                                                                
           INITIALIZE ML079-WEEKLY-SHNK                                         
                      ML080-BRAND-VENDOR-SHNK                                   
                      PV-DEPT-NBR                                               
                      PV-SUB-CL-NBR.                                            
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           DISPLAY ' *** CURRENT DATE: ' PV-PROCESSING-DATE.                    
                                                                                
      ******************************************************************        
      * PROCESS THE WEEKLY DETATIL LEDGER CONDENSED FILE. IF SALES              
      * ARE PRESENT, FIND THE SHRINK RECORD MATCHING THE                        
      * FSCL WK-END-DTE, DEPT-NBR, SUB-CL-NBR AND STR-NBR. PRORATE              
      * THE SHRINK ACCORDING TO THE PERCENTAGE OF SALES.                        
      ******************************************************************        
       2000-PROCESS-RECALC.                                                     
                                                                                
           IF PV-SLS-RTL-AMT = ZERO                                             
               ADD +1 TO PV-ZERO-SLS-SHRINK                                     
               PERFORM 4010-READ-WKLY-DTL-LDGR                                  
           ELSE                                                                 
               PERFORM                                                          
                   UNTIL END-OF-TWKSHNK                                         
                     OR (ML254-FSCL-WK-END-DTE = ML079-FSCL-WK-END-DTE          
                     AND ML254-DEPT-NBR        = PV-DEPT-NBR                    
                     AND ML254-SUB-CL-NBR      = PV-SUB-CL-NBR                  
                     AND ML254-LOC-NBR         = ML079-STR-NBR)                 
                                                                                
                   PERFORM 4020-READ-TWKSHNK                                    
                   MOVE ML079-DEPT-NBR    TO PV-DEPT-NBR-CHAR                   
                   MOVE ML079-SUB-CL-NBR  TO PV-SUB-CL-NBR-CHAR                 
                                                                                
               END-PERFORM                                                      
               MOVE ML079-SLS-RTL-AMT     TO ML080-SLS-RTL-AMT                  
               MOVE ML079-SHNK-RTL-AMT    TO ML080-SHNK-RTL-AMT                 
               PERFORM 2100-PRORATE-SHRINK                                      
W31475            UNTIL (END-OF-WEEKLY-DTL AND END-OF-WEEKLY-DTL-CORR)          
                  OR (ML254-FSCL-WK-END-DTE NOT = ML079-FSCL-WK-END-DTE         
                      OR ML254-DEPT-NBR     NOT = PV-DEPT-NBR                   
                      OR ML254-SUB-CL-NBR   NOT = PV-SUB-CL-NBR                 
                      OR ML254-LOC-NBR      NOT = ML079-STR-NBR)                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2100-PRORATE-SHRINK.                                                    
      * CALCULATE/PRORATE SHRINK RETAIL DOLLARS                                 
      ******************************************************************        
       2100-PRORATE-SHRINK.                                                     
                                                                                
           MOVE ML254-FSCL-WK-END-DTE TO ML080-FSCL-WK-END-DTE                  
           MOVE ML254-DEPT-NBR        TO ML080-DEPT-NBR.                        
           MOVE ML254-MAJ-CL-NBR      TO ML080-MAJ-CL-NBR.                      
           MOVE ML254-SUB-CL-NBR      TO ML080-SUB-CL-NBR.                      
           MOVE ML254-LOC-NBR         TO ML080-LOC-NBR.                         
           MOVE ML254-ML-LWST-ID      TO ML080-ML-LO-MDSE-LVL-ID.               
           MOVE ML254-FSCL-MN-END-DTE TO ML080-FSCL-MN-END-DTE                  
           MOVE ML254-LBL-SEQ-NBR     TO ML080-LBL-SEQ-NBR.                     
           MOVE ML254-ENT-ID          TO ML080-ENT-ID.                          
                                                                                
      ***  CALC % OF SALES FOR A DATA KEY                                       
           IF ML080-SLS-RTL-AMT = 0                                             
               MOVE 0         TO PV-LWST-ID-PCT-SALES                           
           ELSE                                                                 
               COMPUTE PV-LWST-ID-PCT-SALES ROUNDED                             
                              = PV-SLS-RTL-AMT                                  
                              / ML080-SLS-RTL-AMT                               
           END-IF.                                                              
                                                                                
      ***  CALC SHRINK RETAIL FOR A DATA KEY                                    
           COMPUTE ML080-CURR-SHNK-RTL-AMT ROUNDED                              
                               = PV-LWST-ID-PCT-SALES                           
                               * ML080-SHNK-RTL-AMT.                            
                                                                                
      ***  ACCUMULATOR FOR VENDOR LEVEL SHRINK AMOUNTS USED TO                  
      ***  BALANCE BACK TO WKSHNK SHRINK AMOUNTS                                
           ADD ML080-CURR-SHNK-RTL-AMT                                          
                               TO PV-KEY-DATA-SHNK-RTL-AMT.                     
                                                                                
W34517***  FIELDS FOR DETERMINING WHICH VENDOR, BRAND, DKEY TO                  
W34517***  ALLOCATE ANY SHRINK REMAINDER TO.                                    
W34517     IF ML080-CURR-SHNK-RTL-AMT > PV-KEY-SHNK-RTL-AMT-POS                 
W34517         MOVE ML080-CURR-SHNK-RTL-AMT TO PV-KEY-SHNK-RTL-AMT-POS          
W34517         MOVE ML080-ENT-ID            TO PV-ENT-ID-POS                    
W34517         MOVE ML080-LBL-SEQ-NBR       TO PV-LBL-SEQ-NBR-POS               
W34517         MOVE ML080-ML-LO-MDSE-LVL-ID TO PV-ML-LO-MDSE-LVL-ID-POS         
W34517     END-IF.                                                              
W34517     IF ML080-CURR-SHNK-RTL-AMT < PV-KEY-SHNK-RTL-AMT-NEG                 
W34517         MOVE ML080-CURR-SHNK-RTL-AMT TO PV-KEY-SHNK-RTL-AMT-NEG          
W34517         MOVE ML080-ENT-ID            TO PV-ENT-ID-NEG                    
W34517         MOVE ML080-LBL-SEQ-NBR       TO PV-LBL-SEQ-NBR-NEG               
W34517         MOVE ML080-ML-LO-MDSE-LVL-ID TO PV-ML-LO-MDSE-LVL-ID-NEG         
W34517     END-IF.                                                              
W34517                                                                          
W34517*    PERFORM 4010-READ-WKLY-DTL-LDGR.                                     
W34517*    IF END-OF-WEEKLY-DTL                                                 
W34517*    OR  (ML254-FSCL-WK-END-DTE NOT = ML079-FSCL-WK-END-DTE               
W34517*      OR ML254-DEPT-NBR        NOT = PV-DEPT-NBR                         
W34517*      OR ML254-SUB-CL-NBR      NOT = PV-SUB-CL-NBR                       
W34517*      OR ML254-LOC-NBR         NOT = ML079-STR-NBR)                      
W34517***  ON LAST ENT ID OF DEPT/CLASS/LOC, CHECK THAT SHRINK                  
W34517***  TOTALS ARE IN SYNC, IF NOT RECALC THE SHRINK FOR THAT LAST           
W34517***  ENT ID FOR THE DEPT/CLASS/LOC.                                       
W34517*        IF ML080-SHNK-RTL-AMT NOT = PV-KEY-DATA-SHNK-RTL-AMT             
W34517*            COMPUTE PV-KEY-DATA-SHNK-DIFF                                
W34517*                       = ML080-SHNK-RTL-AMT                              
W34517*                       - PV-KEY-DATA-SHNK-RTL-AMT                        
W34517*            COMPUTE ML080-CURR-SHNK-RTL-AMT                              
W34517*                       = ML080-CURR-SHNK-RTL-AMT                         
W34517*                       + PV-KEY-DATA-SHNK-DIFF                           
W34517*        END-IF                                                           
W34517*        MOVE ZEROES TO PV-KEY-DATA-SHNK-RTL-AMT                          
W34517*                       PV-KEY-SHNK-RTL-AMT-POS                           
W34517*                       PV-KEY-SHNK-RTL-AMT-NEG                           
W34517*    END-IF.                                                              
                                                                                
           PERFORM 5000-WRITE-DTL-LDGR-SHNK.                                    
                                                                                
W34517     PERFORM 4010-READ-WKLY-DTL-LDGR.                                     
W34517                                                                          
W34517***  ON LAST ENT ID OF DEPT/CLASS/LOC, CHECK THAT SHRINK TOTALS           
W34517***  ARE IN SYNC. IF NOT, ALLOCATE SHRINK REMAINDER.                      
W34517***  IF REMAINING SHRINK IS POSITIVE, IT WILL BE ALLOCATED TO THE         
W34517***  DKEY WITH THE LARGEST POSITIVE VALUE. IF REMAINING SHRINK IS         
W34517***  NEGATIVE, IT WILL BE ALLOCATED TO THE DKEY WITH THE LARGEST          
W34517***  ABSOLUTE NEGATIVE VALUE.                                             
W34517     IF (END-OF-WEEKLY-DTL AND END-OF-WEEKLY-DTL-CORR)                    
W34517     OR  (ML254-FSCL-WK-END-DTE NOT = ML079-FSCL-WK-END-DTE               
W34517       OR ML254-DEPT-NBR        NOT = PV-DEPT-NBR                         
W34517       OR ML254-SUB-CL-NBR      NOT = PV-SUB-CL-NBR                       
W34517       OR ML254-LOC-NBR         NOT = ML079-STR-NBR)                      
W34517         IF ML080-SHNK-RTL-AMT NOT = PV-KEY-DATA-SHNK-RTL-AMT             
W34517             COMPUTE PV-KEY-DATA-SHNK-DIFF                                
W34517                        = ML080-SHNK-RTL-AMT                              
W34517                        - PV-KEY-DATA-SHNK-RTL-AMT                        
W34517             IF PV-KEY-DATA-SHNK-DIFF > 0                                 
W34517                 IF PV-ML-LO-MDSE-LVL-ID-POS NOT = ZERO                   
W34517                     MOVE PV-ENT-ID-POS      TO ML080-ENT-ID              
W34517                     MOVE PV-LBL-SEQ-NBR-POS TO ML080-LBL-SEQ-NBR         
W34517                     MOVE PV-ML-LO-MDSE-LVL-ID-POS                        
W34517                                        TO ML080-ML-LO-MDSE-LVL-ID        
W34517                 END-IF                                                   
W34517                 MOVE PV-KEY-DATA-SHNK-DIFF                               
W34517                                        TO ML080-CURR-SHNK-RTL-AMT        
W34517                 PERFORM 5000-WRITE-DTL-LDGR-SHNK                         
W34517             END-IF                                                       
W34517             IF PV-KEY-DATA-SHNK-DIFF < 0                                 
W34517                 IF PV-ML-LO-MDSE-LVL-ID-NEG NOT = ZERO                   
W34517                     MOVE PV-ENT-ID-NEG      TO ML080-ENT-ID              
W34517                     MOVE PV-LBL-SEQ-NBR-NEG TO ML080-LBL-SEQ-NBR         
W34517                     MOVE PV-ML-LO-MDSE-LVL-ID-NEG                        
W34517                                        TO ML080-ML-LO-MDSE-LVL-ID        
W34517                 END-IF                                                   
W34517                 MOVE PV-KEY-DATA-SHNK-DIFF                               
W34517                                        TO ML080-CURR-SHNK-RTL-AMT        
W34517                 PERFORM 5000-WRITE-DTL-LDGR-SHNK                         
W34517             END-IF                                                       
W34517         END-IF                                                           
W34517         MOVE ZEROES TO PV-ENT-ID-POS                                     
W34517                        PV-LBL-SEQ-NBR-POS                                
W34517                        PV-ML-LO-MDSE-LVL-ID-POS                          
W34517                        PV-ENT-ID-NEG                                     
W34517                        PV-LBL-SEQ-NBR-NEG                                
W34517                        PV-ML-LO-MDSE-LVL-ID-NEG                          
W34517                        PV-KEY-DATA-SHNK-RTL-AMT                          
W34517                        PV-KEY-SHNK-RTL-AMT-POS                           
W34517                        PV-KEY-SHNK-RTL-AMT-NEG                           
W34517     END-IF.                                                              
W34517                                                                          
      ******************************************************************        
      * FINALIZE THE REPORT AND CLOSE OUT FILES                                 
      ******************************************************************        
       3000-FINALIZE.                                                           
                                                                                
W31475     COMPUTE PV-CURR-READ = PV-CORR-READ + PV-CURR-READ                   
           DISPLAY ' '.                                                         
           DISPLAY ' **** CURRENT INPUT:        ' PV-CURR-READ.                 
           DISPLAY ' **** CURRENT EXTRACTED:    ' PV-CURR-EXTRACTED.            
W34517*    DISPLAY '        ZERO SALES W/SHRINK ' PV-ZERO-SLS-SHRINK.           
W34517     DISPLAY '        ZERO SALES          ' PV-ZERO-SLS-SHRINK.           
                                                                                
           CLOSE   WEEKLY-DTL-LDGR                                              
                   TWKSHNK-EXTRACT                                              
                   WEEKLY-DTL-LDGR-SHNK.                                        
                                                                                
      ******************************************************************        
      * READ WEEKLY DETAIL LEDGER                                               
      ******************************************************************        
       4010-READ-WKLY-DTL-LDGR.                                                 
                                                                                
W31475*    READ WEEKLY-DTL-LDGR      INTO ML254-UPDATE-REC                      
W31475*        AT END      SET END-OF-WEEKLY-DTL TO TRUE                        
W31475*        NOT AT END  ADD +1 TO PV-CURR-READ                               
W31475*    END-READ.                                                            
W31475                                                                          
W31475     IF READ-DTL-LDGR AND NOT-END-OF-WEEKLY-DTL                           
W31475        READ WEEKLY-DTL-LDGR      INTO MLCUR-UPDATE-REC                   
W31475            AT END      SET END-OF-WEEKLY-DTL TO TRUE                     
W31475            NOT AT END  ADD +1 TO PV-CURR-READ                            
W31475        END-READ                                                          
W31475        IF NOT-END-OF-WEEKLY-DTL                                          
W31475           MOVE MLCUR-FSCL-WK-END-DTE   TO PV-WK1-FSCL-WK-END-DTE         
W31475           MOVE MLCUR-DEPT-NBR          TO PV-WK1-DEPT-NBR                
W31475           MOVE MLCUR-SUB-CL-NBR        TO PV-WK1-SUB-CL-NBR              
W31475           MOVE MLCUR-LOC-NBR           TO PV-WK1-LOC-NBR                 
W31475           MOVE MLCUR-ML-LWST-ID        TO PV-WK1-ML-LWST-ID              
W31475        ELSE                                                              
W31475           MOVE HIGH-VALUES TO PV-WK1-COMPARE                             
W31475        END-IF                                                            
W31475     END-IF                                                               
W31475                                                                          
W31475     IF READ-DTL-LDGR-CORR AND NOT-END-OF-WEEKLY-DTL-CORR                 
W31475        READ WEEKLY-DTL-LDGR-CORR INTO MLCOR-UPDATE-REC                   
W31475            AT END      SET END-OF-WEEKLY-DTL-CORR TO TRUE                
W31475            NOT AT END  ADD +1 TO PV-CORR-READ                            
W31475        END-READ                                                          
W31475        IF NOT-END-OF-WEEKLY-DTL-CORR                                     
W31475           MOVE MLCOR-FSCL-WK-END-DTE   TO PV-WK2-FSCL-WK-END-DTE         
W31475           MOVE MLCOR-DEPT-NBR          TO PV-WK2-DEPT-NBR                
W31475           MOVE MLCOR-SUB-CL-NBR        TO PV-WK2-SUB-CL-NBR              
W31475           MOVE MLCOR-LOC-NBR           TO PV-WK2-LOC-NBR                 
W31475           MOVE MLCOR-ML-LWST-ID        TO PV-WK2-ML-LWST-ID              
W31475        ELSE                                                              
W31475           MOVE HIGH-VALUES TO PV-WK2-COMPARE                             
W31475        END-IF                                                            
W31475     END-IF                                                               
W31475                                                                          
W31475     EVALUATE TRUE                                                        
W31475     WHEN END-OF-WEEKLY-DTL AND END-OF-WEEKLY-DTL-CORR                    
W31475        CONTINUE                                                          
W31475     WHEN END-OF-WEEKLY-DTL                                               
W31475        SET READ-DTL-LDGR-CORR TO TRUE                                    
W31475        MOVE MLCOR-UPDATE-REC TO ML254-UPDATE-REC                         
W31475     WHEN END-OF-WEEKLY-DTL-CORR                                          
W31475        SET READ-DTL-LDGR TO TRUE                                         
W31475        MOVE MLCUR-UPDATE-REC TO ML254-UPDATE-REC                         
W31475     WHEN PV-WK2-COMPARE < PV-WK1-COMPARE                                 
W31475        SET READ-DTL-LDGR-CORR TO TRUE                                    
W31475        SET HOLD-DTL-LDGR TO TRUE                                         
W31475        MOVE MLCOR-UPDATE-REC TO ML254-UPDATE-REC                         
W31475     WHEN PV-WK2-COMPARE >= PV-WK1-COMPARE                                
W31475        SET HOLD-DTL-LDGR-CORR TO TRUE                                    
W31475        SET READ-DTL-LDGR TO TRUE                                         
W31475        MOVE MLCUR-UPDATE-REC TO ML254-UPDATE-REC                         
W31475     WHEN OTHER                                                           
W31475        CONTINUE                                                          
W31475     END-EVALUATE                                                         
                                                                                
W34517     IF ML254-FSCL-WK-END-DTE = SPACES                                    
W34517        OR ML254-DEPT-NBR     = ZEROES                                    
W34517        OR ML254-SUB-CL-NBR   = ZEROES                                    
W34517        OR ML254-LOC-NBR      = ZEROES                                    
W34517         DISPLAY 'INVALID DATA ON WEEKLY DETAIL LEDGER FILE:'             
W34517         DISPLAY 'ML254-FSCL-WK-END-DTE ' ML254-FSCL-WK-END-DTE           
W34517         DISPLAY 'ML254-DEPT-NBR        ' ML254-DEPT-NBR                  
W34517         DISPLAY 'ML254-SUB-CL-NBR      ' ML254-SUB-CL-NBR                
W34517         DISPLAY 'ML254-LOC-NBR         ' ML254-LOC-NBR                   
W34517         MOVE 'READ WEEKLY DTL LEDGER'   TO PV-OPERATION-ATTEMPTED        
W34517         MOVE '4010-READ-WKLY-DTL-LDGR ' TO PV-PARAGRAPH-NAME             
W34517         PERFORM 9999-ABEND                                               
W34517     END-IF.                                                              
W34517                                                                          
W31475     IF NOT-END-OF-WEEKLY-DTL OR NOT-END-OF-WEEKLY-DTL-CORR               
               COMPUTE PV-SLS-RTL-AMT =                                         
                   ML254-SLS-REG-RTL-AMT    +                                   
                   ML254-SLS-PROMO-RTL-AMT  +                                   
                   ML254-SLS-PROCLR-RTL-AMT +                                   
                   ML254-SLS-CLR-RTL-AMT    +                                   
                   ML254-SLS-OTH-RTL-AMT                                        
               END-COMPUTE                                                      
           END-IF.                                                              
                                                                                
           IF ML254-DEPT-NBR   NOT = PV-PREV-DEPT-NBR                           
               DISPLAY ' PROCESSING DEPT/DATE ' ML254-DEPT-NBR                  
                       '/' ML254-FSCL-WK-END-DTE                                
                       ' ... ' FUNCTION CURRENT-DATE                            
           END-IF.                                                              
                                                                                
           MOVE ML254-DEPT-NBR TO PV-PREV-DEPT-NBR.                             
                                                                                
      ******************************************************************        
      * READ TWKSHNK UNLOAD FILE.                                               
      ******************************************************************        
       4020-READ-TWKSHNK.                                                       
                                                                                
           READ TWKSHNK-EXTRACT      INTO ML079-WEEKLY-SHNK                     
               AT END      SET END-OF-TWKSHNK TO TRUE                           
               NOT AT END  ADD +1 TO PV-TWKSHNK-READ                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * WRITE OUT WEEKLY DETAIL LEDGER SHRINK (MLRD080 FORMAT)                  
      ******************************************************************        
       5000-WRITE-DTL-LDGR-SHNK.                                                
                                                                                
           WRITE WEEKLY-DTL-LDGR-SHNK-REC                                       
                                      FROM ML080-BRAND-VENDOR-SHNK.             
           ADD +1 TO PV-CURR-EXTRACTED.                                         
                                                                                
      ******************************************************************        
      * 9999-ABEND.                                                             
      *   THIS ROUTINE FORCES A NON-DB2 ABEND.                                  
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**'                                                         
           DISPLAY '**'                                                         
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**********************************'                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
