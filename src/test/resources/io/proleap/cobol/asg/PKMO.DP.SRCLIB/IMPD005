      ******************************************************************        
      *              IMPD005 - STORE EXTRACT PROCEDURE                          
      *   THIS PROCEDURE DIVISION WILL BE USED WITH IMWS005 TO EXTRACT          
      *   STORE INFORMATION.                                                    
      *   THE PROGRAMS WHICH USE THIS PROCEDURE MUST SUPPLY THE                 
      *   FOLLOWING:                                                            
      *     1.  MOVE EFFECTIVE DATE TO IM005-PV-EFF-DTE BEFORE                  
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     2.  MOVE PROGRAM NAME TO IM005-PC-PROGRAM-NAME BEFORE               
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     3.  MOVE STORE GROUP TYPE CODE TO IM005-PV-STR-GP-TYP-CDE           
      *         BEFORE PERFORMING IM005-B000-RCP-LOCATIONS.                     
      *     4.  MOVE STORE GROUP ID TO IM005-PV-STR-GP-ID BEFORE                
      *         PERFORMING IM005-B000-RCP-LOCATIONS.                            
      *     5.  THE PROGRAM MUST INCLUDE THE DCLGENS FOR THE FOLLOWING          
      *         TABLES: XMT_LOC                                                 
      *                 XMT_DC_LOC_ASGMT                                        
      *                 XMT_STR_GP_MBSHP                                        
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *                                                                         
      *   IM005-A000-GET-PROCESS-STORES                                         
      *      SHOULD BE PERFORMED AS PART OF INITIALIZATION TO PROGRAM.          
      *      THIS PARAGRAPH LOADS ALL DS(DEPARTMENT STORE) LOCATIONS            
      *      TO BE PROCESSED INTO A TABLE.                                      
      *                                                                         
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *   IM005-B000-RCP-LOCATIONS                                              
      *      THIS PARAGRAPH SHOULD BE PERFORMED FOR ALL RCP PRICE               
      *      CHANGES BEING PROCESSED.  FOR EACH LOCATION THAT SHOULD BE         
      *      PROCESSED FOR THE PRICE CHANGE, IM005-STR-GRP-STORE                
      *      (IM005-STORE-INDEX) WILL BE SET TO TRUE.                           
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
      * IM005-A000-GET-PROCESS-STORES - PROCESSING TO LOAD ALL STORE            
      * LOCATIONS TO BE PROCESSED INTO AN INTERNAL STORE TABLE                  
      ******************************************************************        
       IM005-A000-GET-PROCESS-STORES.                                           
                                                                                
           SET IM005-PS-NO-ABEND-ERROR TO TRUE.                                 
           SET IM005-PS-MORE-ROWS      TO TRUE.                                 
           MOVE 0 TO IM005-PV-MAX-STORE.                                        
                                                                                
           PERFORM IM005-Z100-OPEN-STORE-CSR.                                   
                                                                                
           IF IM005-PS-NO-ABEND-ERROR                                           
               PERFORM IM005-Z110-LOAD-STORE-TABLE                              
                 UNTIL IM005-PS-NO-MORE-ROWS                                    
                    OR IM005-PS-ABEND-ERROR                                     
           END-IF.                                                              
                                                                                
           IF IM005-PS-NO-ABEND-ERROR                                           
               PERFORM IM005-Z120-CLOSE-STORE-CSR                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * IM005-B000-RCP-LOCATIONS - PERM PRICE CHANGES WILL BE          *        
      *  GENERATED FOR ALL LOCATIONS THAT ARE ASSIGNED TO THE STORE    *        
      *  GROUP FOR LOCATIONS THAT SHOULD BE PROCESSED.                 *        
      ******************************************************************        
       IM005-B000-RCP-LOCATIONS.                                                
                                                                                
           MOVE LOW-VALUES TO IM005-GROUP-STORE-TABLE.                          
           SET IM005-PS-NO-ABEND-ERROR TO TRUE.                                 
           SET IM005-PS-MORE-ROWS      TO TRUE.                                 
           MOVE 0 TO IM005-PV-MAX-STR-GP-STR.                                   
                                                                                
           PERFORM IM005-Z200-OPEN-STORE-GRP-CSR.                               
                                                                                
           IF IM005-PS-NO-ABEND-ERROR                                           
               SET IM005-PS-MORE-ROWS  TO TRUE                                  
               PERFORM IM005-Z210-FETCH-STORE-GRP-TBL                           
                 UNTIL IM005-PS-NO-MORE-ROWS                                    
                    OR IM005-PS-ABEND-ERROR                                     
           END-IF.                                                              
                                                                                
           IF IM005-PS-NO-ABEND-ERROR                                           
               PERFORM IM005-Z220-CLOSE-STORE-GRP-CSR                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  OPEN THE STORE CURSOR                                         *        
      ******************************************************************        
       IM005-Z100-OPEN-STORE-CSR.                                               
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-NO-MORE-ROWS                                      
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
               EXEC SQL                                                         
                    OPEN IM005_STR_CSR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       SET IM005-PS-NO-MORE-ROWS TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'OPEN STORE CURSOR'                                 
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z100-OPEN-STORE-CSR'                         
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'OPEN STORE CURSOR'                                         
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z100-OPEN-STORE-CSR'                                 
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * IM005-Z110-LOAD-STORE-TABLE                                    *        
      *                                                                *        
      * RETRIEVES THE STORE TABLE DATA AND LOADS THE WORKING STORAGE   *        
      * TABLE                                                          *        
      ******************************************************************        
       IM005-Z110-LOAD-STORE-TABLE.                                             
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-NO-MORE-ROWS                                      
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
               EXEC SQL                                                         
                    FETCH IM005_STR_CSR                                         
                    INTO :XMT00001-LOC-NBR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       IF XMT00001-LOC-NBR <= IM005-PC-MAX-STORES               
                           SET IM005-STORE-INDEX TO XMT00001-LOC-NBR            
                           MOVE 'Y' TO                                          
                           IM005-PROCESS-STORE-IND (IM005-STORE-INDEX)          
                       ELSE                                                     
                           SET IM005-PS-ABEND-ERROR TO TRUE                     
                           MOVE +4101   TO IM005-PV-ABEND-CODE                  
                           MOVE 'STORE GREATER THAN MAX STORE'                  
                                            TO IM005-PV-ABEND-MESSAGE           
                           MOVE 'IM005-Z110-LOAD-STORE-TABLE'                   
                                            TO IM005-PV-ABEND-PARAGRAPH         
                       END-IF                                                   
                   WHEN SQLCODE = +100                                          
                       SET IM005-PV-MAX-STORE TO IM005-STORE-INDEX              
                       SET IM005-PS-NO-MORE-ROWS TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'FETCH STORE CURSOR'                                
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z110-LOAD-STORE-TABLE'                       
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'FETCH STORE CURSOR'                                        
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z110-LOAD-STORE-TABLE'                               
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
                                                                                
        EJECT                                                                   
      ******************************************************************        
      * IM005-Z120-CLOSE-STORE-CSR                                     *        
      *                                                                *        
      *  CLOSE THE STORE CURSOR                                        *        
      ******************************************************************        
       IM005-Z120-CLOSE-STORE-CSR.                                              
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
               EXEC SQL                                                         
                    CLOSE IM005_STR_CSR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'CLOSE STORE CURSOR'                                
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z120-CLOSE-STORE-CSR'                        
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'CLOSE STORE CURSOR'                                        
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z120-CLOSE-STORE-CSR'                                
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  OPEN THE STORE GROUP CURSOR                                   *        
      ******************************************************************        
       IM005-Z200-OPEN-STORE-GRP-CSR.                                           
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-NO-MORE-ROWS                                      
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
               EXEC SQL                                                         
                    OPEN IM005_STR_GRP_CSR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       SET IM005-PS-NO-MORE-ROWS TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'OPEN STORE GROUP CURSOR'                           
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z200-OPEN-STORE-GRP-CSR'                     
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'OPEN STORE GROUP CURSOR'                                   
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z200-OPEN-STORE-GRP-CSR'                             
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * IM005-Z210-FETCH-STORE-GRP-TBL                                 *        
      *                                                                *        
      * RETRIEVES THE STORE GROUP DATA AND PROCESSES IT                *        
      ******************************************************************        
       IM005-Z210-FETCH-STORE-GRP-TBL.                                          
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-NO-MORE-ROWS                                      
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
                                                                                
               EXEC SQL                                                         
                    FETCH IM005_STR_GRP_CSR                                     
                    INTO :XMT00003-LOC-NBR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       PERFORM IM005-Z215-LOAD-STORE-GRP-TBL                    
                   WHEN SQLCODE = +100                                          
                       SET IM005-PV-MAX-STR-GP-STR TO IM005-GP-STR-IDX          
                       SET IM005-PS-NO-MORE-ROWS TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'FETCH STORE GROUP CURSOR'                          
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z210-FETCH-STORE-GRP-TBL'                    
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'FETCH STORE GROUP CURSOR'                                  
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z210-FETCH-STORE-GRP-TBL'                            
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
                                                                                
        EJECT                                                                   
      ******************************************************************        
      * IM005-Z215-LOAD-STORE-GRP-TBL                                  *        
      *                                                                *        
      * LOADS THE WORKING STORAGE STORE GROUP TABLE                    *        
      ******************************************************************        
       IM005-Z215-LOAD-STORE-GRP-TBL.                                           
                                                                                
           IF XMT00003-LOC-NBR <= IM005-PC-MAX-STORES                           
               SET IM005-STORE-INDEX TO XMT00003-LOC-NBR                        
               IF IM005-PROCESS-STORE-IND (IM005-STORE-INDEX) = 'Y'             
                   SET IM005-GP-STR-IDX UP BY 1                                 
                   IF IM005-GP-STR-IDX <= IM005-PC-MAX-STORES                   
                       MOVE XMT00003-LOC-NBR TO                                 
                             IM005-STR-GRP-STORE (IM005-GP-STR-IDX)             
                   ELSE                                                         
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4102 TO IM005-PV-ABEND-CODE                        
                       MOVE 'STORE GREATER THAN MAX GROUP STORES'               
                                        TO IM005-PV-ABEND-MESSAGE               
                       MOVE 'IM005-Z215-LOAD-STORE-GRP-TBL'                     
                                        TO IM005-PV-ABEND-PARAGRAPH             
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4103   TO IM005-PV-ABEND-CODE                              
               MOVE 'STORE GREATER THAN MAX STORE'                              
                                TO IM005-PV-ABEND-MESSAGE                       
               MOVE 'IM005-Z215-LOAD-STORE-GRP-TBL'                             
                                TO IM005-PV-ABEND-PARAGRAPH                     
           END-IF.                                                              
                                                                                
        EJECT                                                                   
      ******************************************************************        
      * IM005-Z220-CLOSE-STORE-GRP-CSR                                 *        
      *                                                                *        
      *  CLOSE THE STORE GROUP CURSOR                                  *        
      ******************************************************************        
       IM005-Z220-CLOSE-STORE-GRP-CSR.                                          
                                                                                
           INITIALIZE IM005-PV-RETRY-COUNT.                                     
                                                                                
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR IM005-PS-ABEND-ERROR                                       
                  OR IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                
                                                                                
               EXEC SQL                                                         
                    CLOSE IM005_STR_GRP_CSR                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO IM005-PV-RETRY-COUNT                   
                   WHEN OTHER                                                   
                       SET IM005-PS-ABEND-ERROR TO TRUE                         
                       MOVE +4013     TO IM005-PV-ABEND-CODE                    
                       MOVE SQLCODE   TO IM005-PV-ABEND-SQLCODE                 
                       MOVE 'CLOSE STORE GROUP CURSOR'                          
                                      TO IM005-PV-ABEND-MESSAGE                 
                       MOVE 'IM005-Z220-CLOSE-STORE-GRP-CSR'                    
                                      TO IM005-PV-ABEND-PARAGRAPH               
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  IM005-PV-RETRY-COUNT > IM005-PC-MAX-RETRIES                      
               SET IM005-PS-ABEND-ERROR TO TRUE                                 
               MOVE +4013             TO IM005-PV-ABEND-CODE                    
               MOVE SQLCODE           TO IM005-PV-ABEND-SQLCODE                 
               MOVE 'CLOSE STORE GROUP CURSOR'                                  
                                      TO IM005-PV-ABEND-MESSAGE                 
               MOVE 'IM005-Z220-CLOSE-STORE-GRP-CSR'                            
                                      TO IM005-PV-ABEND-PARAGRAPH               
           END-IF.                                                              
