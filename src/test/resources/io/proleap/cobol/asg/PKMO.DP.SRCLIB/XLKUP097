000100 IDENTIFICATION DIVISION.                                                 
000400 PROGRAM-ID.             XLKUP097.                                        
000300 AUTHOR.                 VIGNESHWARAN MURUGAN                             
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            APRIL 2021.                                      
000800 DATE-COMPILED.                                                           
000900****************************************************************          
001000*    U S E R   D O M A I N   S Y N C / U P D A T E                        
001100*                                                                         
001200****************************************************************          
001300* THIS PROGRAM READS AN OUTPUT FILE FROM S042 OF XLK050 CONTAINS          
001400* SORTED RECORDS OF USER ID'S STARTS WITH 1 IN AN ORDERED SEQUENCE        
001600* TO DELETE ROWS FOR THE TK NUMBER FROM THE  DLT_RDPR_USR_LSTG            
001700* TABLE.                                                                  
001800* HERE THE SAME COPY BOOK IS USED FOR XLK050 AS IT IS CALLED              
001900* XLRD038.                                                                
002200* COMMITS ARE DONE AT 20 RECORD INTERVALS.                                
002800****************************************************************          
002900 ENVIRONMENT DIVISION.                                                    
003000 CONFIGURATION SECTION.                                                   
003100 SOURCE-COMPUTER.        IBM-3090.                                        
003200 OBJECT-COMPUTER.        IBM-3090.                                        
003300 INPUT-OUTPUT SECTION.                                                    
003400                                                                          
003500 FILE-CONTROL.                                                            
003600                                                                          
003700     SELECT XL-TRANS-FILE                                                 
003800         ASSIGN TO INP01.                                                 
003900                                                                          
004000 DATA DIVISION.                                                           
004100 FILE SECTION.                                                            
004200                                                                          
004300                                                                          
004400 FD  XL-TRANS-FILE                                                        
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     RECORD CONTAINS 250 CHARACTERS                                       
004800     BLOCK CONTAINS 0  RECORDS.                                           
004900 01  XL-TRANS-REC                     PIC X(250).                         
005000                                                                          
005100 WORKING-STORAGE SECTION.                                                 
005200 01  PS-SWITCHES.                                                         
005300     05  PS-EOF-TRANS-SW              PIC X(01) VALUE 'N'.                
005400         88  PS-EOF-TRANS                       VALUE 'Y'.                
006000                                                                          
006100     05  PS-COMMIT-SW                 PIC X(01) VALUE SPACE.              
006200         88  PS-COMMIT-NOW                      VALUE 'Y'.                
006300         88  PS-NO-COMMIT-YET                   VALUE 'N'.                
006400                                                                          
006500 01  DN-NULL-INDICATORS.                                                  
006600     05  DN-DIST-NBR                  PIC S9(04) COMP VALUE +0.           
006700                                                                          
006800 01  PA-PROGRAM-ACCUMULATORS.                                             
006900     05  PA-COMMIT-COUNT             PIC 9(07) VALUE ZERO.                
006900     05  PA-TOT-COMMITS-TAKEN        PIC 9(07) VALUE ZEROS.               
007300     05  PA-TOT-DEL-RECS-READ        PIC 9(07) VALUE ZERO.                
007300     05  PA-TOT-UID-NF-ASGMT         PIC 9(07) VALUE ZERO.                
007300     05  PA-TOT-UID-F-ASGMT          PIC 9(07) VALUE ZERO.                
007600     05  PA-TOT-LSTG-DB2-DELS        PIC 9(07) VALUE ZEROS.               
007300     05  PA-TOT-UID-NF-LSTG          PIC 9(07) VALUE ZEROS.               
007800                                                                          
007900 01  PV-PROGRAM-VARIABLES.                                                
008000     05  PV-RETRY-COUNTER            PIC S9(03) VALUE ZERO COMP-3.        
008100     05  PC-PROGRAM-NAME              PIC X(08) VALUE 'XLKUP097'.         
008110     05  PC-N                         PIC X(01) VALUE 'N'.                
008110     05  PC-USER                      PIC X(01).                          
008200     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3 COMP.           
008300     05  PV-DISPLAY                   PIC Z,ZZZ,ZZ9.                      
008400     05  PV-ABEND-CODE                PIC S9(04) COMP SYNC                
008500                                                 VALUE ZERO.              
008600                                                                          
008700 01  PV-ABEND-FIELDS.                                                     
008800     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'XLKUP097'.        
008900     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
009000     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
009100     05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.            
009200     05  PV-ABEND-STRUCTURE-2         PIC X(8)   VALUE SPACES.            
009300     05  PV-ABEND-STRUCTURE-3         PIC X(8)   VALUE SPACES.            
009400     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
009500     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
009600     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
009700     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
009800     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
009900     05  PV-SQLCODE                   PIC 9(9)  VALUE ZERO.               
010000/                                                                         
010100*    TRANSACTION RECORD                                                   
010200     COPY XLRD038.                                                        
010300/                                                                         
010400*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
010500     COPY DPWS004.                                                        
010600                                                                          
010700*   DB2 SQL COMMUNICATION AREA                                            
010800     EXEC SQL                                                             
010900          INCLUDE SQLCA                                                   
011000     END-EXEC.                                                            
011100     COPY SQLCA2.                                                         
011200/                                                                         
011300*    DLT_TEM_ASGMT                                                        
011400     EXEC SQL                                                             
011500          INCLUDE DLT00003                                                
011600     END-EXEC.                                                            
011700                                                                          
011800/                                                                         
011300*    DLT_RDPR_USR_LSTG                                                    
011400     EXEC SQL                                                             
011500          INCLUDE DLT00009                                                
011600     END-EXEC.                                                            
011700                                                                          
011800/                                                                         
011900 PROCEDURE DIVISION.                                                      
012000     PERFORM 1000-INITIALIZE.                                             
012100     PERFORM 2000-PROCESS UNTIL PS-EOF-TRANS.                             
012200     PERFORM 9000-EOJ.                                                    
012300     STOP RUN.                                                            
012400                                                                          
012500 1000-INITIALIZE.                                                         
012600                                                                          
012700*SET UP COUNTERS                                                          
012800     MOVE ZEROS TO PA-PROGRAM-ACCUMULATORS.                               
012900*OPEN INPUT                                                               
013000     OPEN INPUT XL-TRANS-FILE.                                            
013100*PRIME READ                                                               
013200     PERFORM 8000-READ-TRANS-FILE.                                        
013300     IF PS-EOF-TRANS                                                      
013400         DISPLAY ALL '*'                                                  
013500         DISPLAY '**  INP01 TRANS FILE IS EMPTY **'                       
013600         DISPLAY ALL '*'                                                  
013700     END-IF.                                                              
013800                                                                          
013900******************************************************************        
027500* APPLY USER DIFFERENCES TO THE DLT_RDPR_USR_LSTG                *        
027600******************************************************************        
027700 2000-PROCESS.                                                            
027800                                                                          
027900     MOVE XL038-U-USR-ID     TO DLT00003-KOHLS-USR-ID.                    
028000     PERFORM 7000-SELECT-USER-ID-DTA                                      
015700     PERFORM 8000-READ-TRANS-FILE.                                        
015900******************************************************************        
016000* SELECT DLT_TEM_ASGMT FOR USER-ID EXISTING  CHECK                        
016100******************************************************************        
027500 7000-SELECT-USER-ID-DTA.                                                 
027600                                                                          
027700     EXEC SQL                                                             
027800          SELECT '1'                                                      
027900            INTO :PC-USER                                                 
016711            FROM DLT_TEM_ASGMT                                            
016712          WHERE KOHLS_USR_ID = :DLT00003-KOHLS-USR-ID                     
016713          FETCH FIRST ROW ONLY                                            
016700     END-EXEC.                                                            
017000     EVALUATE TRUE                                                        
017100         WHEN SQLCODE = +0                                                
017450              DISPLAY 'LABOR DATA FOUND FOR USER ID: '                    
017450                      DLT00003-KOHLS-USR-ID                               
017200              ADD 01 TO PA-TOT-UID-F-ASGMT                                
017100         WHEN SQLCODE = +100                                              
017300              ADD 01 TO PA-TOT-UID-NF-ASGMT                               
017400              PERFORM 7100-DELETE                                         
017300         WHEN SQLWARN0 NOT = SPACES                                       
017400         WHEN SQLCODE  NOT = ZERO                                         
017500         DISPLAY '****************************'                           
017600         DISPLAY '** ABEND                  **'                           
017700         DISPLAY '** PROGRAM = XLKUP097     **'                           
017800         DISPLAY '** PARAGRAPH = 7500-COMMIT**'                           
017900         DISPLAY '** UNSUCCESSFUL COMMIT    **'                           
018000         DISPLAY '****************************'                           
018100         PERFORM 9900-SQL-ERROR                                           
018200     END-EVALUATE.                                                        
027500* DELETE                                                                  
027600****************************************************************          
027700 7100-DELETE.                                                             
027800     MOVE '7100-DELETE             '  TO PV-ABEND-PARAGRAPH.              
027900                                                                          
028000     MOVE XL038-U-USR-ID              TO DLT00009-USR-ID.                 
028100*****************************************************************         
027500*** DELETING USR_ID FROM THE LABOR TABLE                     **           
027600*****************************************************************         
027700     EXEC SQL                                                             
027800          DELETE FROM  DLT_RDPR_USR_LSTG                                  
027900          WHERE USR_ID    = :DLT00009-USR-ID                              
028000     END-EXEC                                                             
029300                                                                          
029400     EVALUATE TRUE                                                        
027500         WHEN SQLCODE = ZERO                                              
027600              ADD 1                TO PA-TOT-LSTG-DB2-DELS                
027700                                     PA-COMMIT-COUNT                      
027800                                                                          
027900              IF  PA-COMMIT-COUNT > 19                                    
028000                 PERFORM 7500-DB2-COMMIT                                  
027500              END-IF                                                      
027600                                                                          
027700         WHEN SQLCODE = +100                                              
027700              ADD 1               TO PA-TOT-UID-NF-LSTG                   
027800         WHEN SQLCODE = -904                                              
027900         WHEN SQLCODE = -911                                              
028000         WHEN SQLCODE = -913                                              
027500             CONTINUE                                                     
027600                                                                          
027700         WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
027800             DISPLAY '**************************************'             
027900             DISPLAY 'WARNING ISSUED ON DELETE OF DLT00009   '            
028000             DISPLAY 'USR-ID = ' DLT00009-USR-ID                          
027500             DISPLAY '**********************************'                 
027600             PERFORM 9800-SQL-WARNING                                     
027700                                                                          
027800         WHEN SQLCODE < ZERO                                              
027900             DISPLAY '**************************************'             
027500             DISPLAY 'ERROR ISSUED ON DELETE OF DLT00009   '              
027600             DISPLAY 'USR-ID = ' DLT00009-USR-ID                          
027700             DISPLAY '**************************************'             
027800             PERFORM 9900-SQL-ERROR                                       
027900                                                                          
028000     END-EVALUATE.                                                        
033200                                                                          
015900******************************************************************        
016000* COMMITTED RECORDS WILL REMAIN IN DB2 TABLES AFTER AN ABEND.             
016100******************************************************************        
016200 7500-DB2-COMMIT.                                                         
016300                                                                          
016500     EXEC SQL                                                             
016600          COMMIT                                                          
016700     END-EXEC.                                                            
016900                                                                          
017000     EVALUATE TRUE                                                        
017100         WHEN SQLCODE = +0                                                
017200              MOVE 0  TO PA-COMMIT-COUNT                                  
017200              ADD  1  TO PA-TOT-COMMITS-TAKEN                             
017300         WHEN SQLWARN0 NOT = SPACES                                       
017400         WHEN SQLCODE  NOT = ZERO                                         
017500         DISPLAY '****************************'                           
017600         DISPLAY '** ABEND                  **'                           
017700         DISPLAY '** PROGRAM = XLKUP097     **'                           
017800         DISPLAY '** PARAGRAPH = 7500-COMMIT**'                           
017900         DISPLAY '** UNSUCCESSFUL COMMIT    **'                           
018000         DISPLAY '****************************'                           
018100         PERFORM 9900-SQL-ERROR                                           
018200     END-EVALUATE.                                                        
050300                                                                          
050400******************************************************************        
050500** READ INPUT                                                             
050600******************************************************************        
050700 8000-READ-TRANS-FILE.                                                    
050800                                                                          
050900     READ XL-TRANS-FILE                                                   
051000         INTO XL038-TRANS-REC                                             
051100     AT END                                                               
051200         SET PS-EOF-TRANS             TO TRUE                             
051300     NOT AT END                                                           
051400         ADD 1                        TO PA-TOT-DEL-RECS-READ             
052600     END-READ.                                                            
052700                                                                          
052800******************************************************************        
052900** END OF JOB ROUTINE                                                     
053000******************************************************************        
053100 9000-EOJ.                                                                
053200                                                                          
053300*CLOSE OUTPUT FILE                                                        
053400     CLOSE XL-TRANS-FILE                                                  
053500                                                                          
054500*DISPLAY SUMMARY STATISTICS                                               
054600     DISPLAY ' '.                                                         
054700     DISPLAY '*************************************'.                     
054800     DISPLAY '**      XLKUP097 SUMMARY STATS     **'.                     
054900     DISPLAY '*************************************'.                     
055000     DISPLAY ' '.                                                         
055300     DISPLAY 'TOTAL INPUT DEL RECS READ          : '                      
055300                                            PA-TOT-DEL-RECS-READ.         
055400     DISPLAY '-------------------------------------'.                     
056010     DISPLAY 'TOTAL USER ID FOUND IN ASGMT       : '                      
056010                                            PA-TOT-UID-F-ASGMT.           
055900     DISPLAY 'TOTAL USER ID NOT FOUND IN ASGMT   : '                      
055900                                            PA-TOT-UID-NF-ASGMT.          
055900     DISPLAY 'TOTAL DELETES IN LSTG              : '                      
055900                                            PA-TOT-LSTG-DB2-DELS.         
055900     DISPLAY 'TOTAL NOT FOUND IN  LSTG           : '                      
055900                                            PA-TOT-UID-NF-LSTG.           
056010     DISPLAY 'TOTAL COMMITS TAKEN                : '                      
056010                                            PA-TOT-COMMITS-TAKEN.         
056100     DISPLAY '-------------------------------------'.                     
056200     DISPLAY ' '.                                                         
056300     DISPLAY '*************************************'.                     
056400                                                                          
056500******************************************************************        
056600** SQL WARNING                                                            
056700******************************************************************        
056800 9800-SQL-WARNING.                                                        
056900                                                                          
057000      MOVE SQLCODE               TO SQLCODE2.                             
057100      MOVE SQLERRD(3)            TO SQLERRD3.                             
057200      DISPLAY '** ABEND **       ** = SQLWARNING'.                        
057300      DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                  
057400      DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.               
057500      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
057600      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                         
057700      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                         
057800      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                         
057900      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                         
058000      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                         
058100      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                         
058200      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                         
058300      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                         
058400                                                                          
058500      MOVE 4013                  TO PV-ABEND-CODE                         
058600      CALL 'ILBOABN0' USING PV-ABEND-CODE.                                
058700                                                                          
058800 9900-SQL-ERROR.                                                          
058900                                                                          
059000      MOVE SQLCODE               TO SQLCODE2.                             
059100      MOVE SQLERRD(3)            TO SQLERRD3.                             
059200      DISPLAY '** ABEND **       ** = SQLERROR'.                          
059300      DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                  
059400      DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.               
059500      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
059600      DISPLAY '** SQLERRM        ** = ' SQLERRM.                          
059700      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                         
059800                                                                          
059900      MOVE 4013                  TO PV-ABEND-CODE                         
060000      CALL 'ILBOABN0' USING PV-ABEND-CODE.                                
