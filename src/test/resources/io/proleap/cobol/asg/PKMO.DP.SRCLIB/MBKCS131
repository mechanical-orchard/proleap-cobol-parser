      *                                                                 00010054
      *                                                                 00020054
      *                                                                 00030054
       IDENTIFICATION DIVISION.                                         00040054
                                                                        00050054
       PROGRAM-ID.    MBKCS131.                                         00060054
       AUTHOR.        MICHELLE WEBB.                                    00070054
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00080054
       DATE-WRITTEN.  10-14-91.                                         00090054
       DATE-COMPILED.                                                   00100054
                                                                        00110054
      ***************************************************************** 00120054
      *   RESPONSIBILITY AREA UPDATE PROGRAM                            00130054
      *                                                                 00140054
      * THIS PROGRAM UPDATES THE RESPONSIBILITIES FOR A PARTICULAR      00150054
      * MERCHANDISE AREA (GMA, DMA AND DPT ONLY). THE USER IS           00160054
      * ALLOWED TO ADD AND UPDATE RESPONSIBILITIES, BUT NEVER DELETE    00170054
      * THEM(MAKE THEM HISTORY INSTEAD BY CHANGING THE END DATE).       00180054
      *                                                                 00190054
      * THE SCREEN IS REDEFINED IN WORKING STORAGE TO TAKE ADVANTAGE    00200054
      * OF THE TABLE FORM. SCREEN DATA IS SAVED IN A TABLE IN COMMAREA. 00210054
      * ORIGINAL RESPONSIBILITIES READ FROM THE DATABASE ARE SAVED      00220054
      * IN THE ORIG-TSQ, WHICH IS USED TO DETERMINE WHICH ROWS TO       00230054
      * UPDATE, AND WHICH ONES TO ADD.                                  00240054
      *                                                                 00250054
      * A TSQ IS PASSED FROM THE LIST PROGRAM TO THIS PROGRAM CON-      00260054
      * TAINING ALL THE MERCHANDISE AREA KEYS SELECTED FROM THE LIST    00270054
      * SCREEN. BY PRESSING THE NEXT PFKEY, THE USER CAN BRING UP       00280054
      * THE NEXT MERCH. AREA RESPONSIBILITIES.                          00290054
      *                                                                 00300054
      * THIS PROGRAM CAN BE ACCESSED VIA THE MERCHANDISE AREA LIST      00310054
      * AND THE WORKER LIST PROGRAM                                     00320054
      *                                                                 00330054
      * NOTE: IF THE LENGTH OF THE COMMAREA TABLE IS CHANGED,           00340054
      *  REMEMBER TO CHANGE                                             00350054
      *  1. PV-SAVE-SCREEN-LINE (LENGTH)                                00360054
      *  2. PC-FIXED-TSQ-LENGTH                                         00370055
      *     (CAN'T GO BEYOND 32767 S9(4) COMP MAX).                     00380054
      *  3. PV-ORIG-TSQ-AREA AND PV-CURR-TSQ-AREA LAYOUTS               00381054
      *  4. PC-TOTAL-DATA-LINES                                         00382054
      ***************************************************************** 00390054
      *  06/18/98   DOUG DOUGLAS   STRATAGEM     WR#: 6041              00400054
      *             - UPGRADE FOR Y2K                                   00410054
      *             - CHANGE ++INCLUDE'S TO COPY'S   (STDS)             00420054
      ***************************************************************** 00430054
      *  02/17/00   PAUL KEBER                   WR#: 99999             00430154
      *             - ALLOW 4 PAGES OF LIST ITEMS INSTEAD OF 2 PAGES    00430254
      ***************************************************************** 00430354
      *  12/05/07   PAUL KEBER                  INC#: 146239            00430455
      *             - REWRITE TO ALLOW 455 LIST ITEMS INSTEAD OF 36.    00430554
      *               ASC SCREEN LINES ARE NOW IN PV-CURR-TSQ-AREA.     00430654
      ***************************************************************** 00430959
      *  12/05/07   COGNIZANT                   CHG#: 127047            00431059
      *             - CHANGED THE CURRENT LIMIT IN PAGING FROM 99 TO    00431159
      *               999.                                              00431259
      ***************************************************************** 00431359
262978* CHANGED 10/22/2021    BY JIM HUNTER      WR: CHG0262978         00431460
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE     00431560
262978* MAKING THE CALL DYNAMIC VS STATIC.                              00431660
262978******************************************************************00431760
      *                                                                 00431859
      *                                                                 00431959
      *                                                                 00432059
       ENVIRONMENT DIVISION.                                            00432159
      *                                                                 00432259
      *                                                                 00432359
      *                                                                 00432459
       DATA DIVISION.                                                   00432559
      *                                                                 00433054
      *                                                                 00434054
       WORKING-STORAGE SECTION.                                         00435054
                                                                        00436054
       01  PC-PROGRAM-CONSTANTS.                                        00437054
           05  PC-FIXED-TSQ-LENGTH            PIC S9(9)    VALUE +32760 00437154
                                                           COMP.        00437254
           05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.00438054
           05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072  00439054
                                                           COMP SYNC.   00440054
           05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE        00450054
           'MB131A  '.                                                  00460054
           05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE        00470054
           'MBKM131 '.                                                  00480054
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE        00490054
           'MBKCS131'.                                                  00500054
           05  PC-BYR-LEVEL                   PIC X(03)    VALUE 'BYR'. 00510054
           05  PC-INFINITY-DATE               PIC 9(08)                 00520054
                                                        VALUE 99990909. 00530054
           05  PC-DEPT-LEVEL-CD               PIC X(03)    VALUE 'DPT'. 00540054
           05  PC-DMM-LEVEL                   PIC X(03)    VALUE 'DMM'. 00550054
           05  PC-DMA-LEVEL-CD                PIC X(03)    VALUE 'DMA'. 00560054
           05  PC-DPT-LEVEL-CD                PIC X(03)    VALUE 'DPT'. 00570054
           05  PC-GMM-LEVEL                   PIC X(03)    VALUE 'GMM'. 00580054
           05  PC-GMA-LEVEL-CD                PIC X(03)    VALUE 'GMA'. 00590054
           05  PC-MONTH-DAYS                  PIC 9(03)    VALUE 30.    00600054
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE +3.    00610054
           05  PC-MAX-SCREEN-LINES            PIC S9(3)    VALUE +11.   00620054
           05  PC-TOTAL-DATA-LINES            PIC S9(03)   VALUE +455.  00630054
           05  PC-CONSECUTIVE-DAYS            PIC S9(03)   VALUE +1.    00640054
           05  PC-YEAR-DAYS                   PIC 9(03)    VALUE 365.   00670054
           05  PC-TSYMSG-00000                PIC 9(05)    VALUE 00000. 00680054
           05  PC-TSYMSG-00005                PIC 9(05)    VALUE 00005. 00690054
           05  PC-TSYMSG-00007                PIC 9(05)    VALUE 00007. 00700054
           05  PC-TSYMSG-00008                PIC 9(05)    VALUE 00008. 00710054
           05  PC-TSYMSG-00009                PIC 9(05)    VALUE 00009. 00720054
           05  PC-TSYMSG-00010                PIC 9(05)    VALUE 00010. 00730054
           05  PC-TSYMSG-00025                PIC 9(05)    VALUE 00025. 00740054
           05  PC-TSYMSG-00045                PIC 9(05)    VALUE 00045. 00750054
           05  PC-TSYMSG-00046                PIC 9(05)    VALUE 00046. 00760054
           05  PC-TSYMSG-00059                PIC 9(05)    VALUE 00059. 00770054
           05  PC-TSYMSG-00067                PIC 9(05)    VALUE 00067. 00780054
           05  PC-TSYMSG-00069                PIC 9(05)    VALUE 00069. 00790054
           05  PC-TSYMSG-00070                PIC 9(05)    VALUE 00070. 00800054
           05  PC-TSYMSG-00122                PIC 9(05)    VALUE 00122. 00810054
           05  PC-TSYMSG-00138                PIC 9(05)    VALUE 00138. 00820054
           05  PC-TSYMSG-00143                PIC 9(05)    VALUE 00143. 00830054
           05  PC-TSYMSG-00144                PIC 9(05)    VALUE 00144. 00840054
           05  PC-TSYMSG-00145                PIC 9(05)    VALUE 00145. 00850054
           05  PC-TSYMSG-00149                PIC 9(05)    VALUE 00149. 00860054
           05  PC-TSYMSG-00181                PIC 9(05)    VALUE 00181. 00870054
           05  PC-TSYMSG-00182                PIC 9(05)    VALUE 00182. 00880054
           05  PC-TSYMSG-00188                PIC 9(05)    VALUE 00188. 00890054
           05  PC-TSYMSG-00189                PIC 9(05)    VALUE 00189. 00900054
           05  PC-TSYMSG-00190                PIC 9(05)    VALUE 00190. 00910054
           05  PC-TSYMSG-00191                PIC 9(05)    VALUE 00191. 00920054
           05  PC-TSYMSG-99901                PIC 9(05)    VALUE 99901. 00930054
           EJECT                                                        00940054
                                                                        00950054
       01  PS-PROGRAM-SWITCHES.                                         00960054
           05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   00970054
               88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   00980054
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   00990054
               88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   01000054
               88  PS-SEND-DATA-ONLY                       VALUE '4'.   01010054
               88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   01020054
               88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   01030054
           05  PS-DONE-SW                     PIC X(01)    VALUE 'N'.   01040054
               88  PS-DONE                                 VALUE 'Y'.   01050054
               88  PS-NOT-DONE                             VALUE 'N'.   01060054
           05  PS-UPDATE-ROW-SW               PIC X(01)    VALUE 'N'.   01070054
               88  PS-UPDATE-ROW                           VALUE 'Y'.   01080054
               88  PS-NOT-UPDATE-ROW                       VALUE 'N'.   01090054
           05  PS-RSPLVL-FOUND-SW             PIC X(01)    VALUE 'N'.   01100054
               88  PS-RSPLVL-FOUND                         VALUE 'Y'.   01110054
               88  PS-RSPLVL-NOT-FOUND                     VALUE 'N'.   01120054
           05  PS-DATES-CONSECUTIVE-SW        PIC X(01)    VALUE 'N'.   01130054
               88  PS-DATES-CONSECUTIVE                    VALUE 'Y'.   01140054
               88  PS-DATES-NOT-CONSECUTIVE                VALUE 'N'.   01150054
           05  PS-DATES-STATUS-SW             PIC X(01)    VALUE ' '.   01160054
               88  PS-DATES-OK                             VALUE ' '.   01170054
               88  PS-DATES-GAP                            VALUE 'Y'.   01180054
               88  PS-DATES-OVERLAP                        VALUE 'N'.   01190054
           05  PS-REC-FOUND-SW                PIC X(01)    VALUE 'N'.   01200054
               88  PS-RECORD-FOUND                         VALUE 'Y'.   01210054
               88  PS-RECORD-NOT-FOUND                     VALUE 'N'.   01220054
           05  PS-MATCH-SW                    PIC X(01)    VALUE 'N'.   01230054
               88  PS-NEED-MATCH                           VALUE 'Y'.   01240054
               88  PS-NEED-NO-MATCH                        VALUE 'N'.   01250054
           05  PS-RECORDS-SW                  PIC X(01)    VALUE 'N'.   01260054
               88  PS-MORE-RECORDS                         VALUE 'Y'.   01270054
               88  PS-NO-MORE-RECORDS                      VALUE 'N'.   01280054
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   01290054
               88  PS-ERROR                                VALUE 'Y'.   01300054
               88  PS-NO-ERROR                             VALUE 'N'.   01310054
           05  PS-ST-DATE-ERR-SW              PIC X(01)    VALUE 'N'.   01320054
               88  PS-START-DATE-ERROR                     VALUE 'Y'.   01330054
               88  PS-NO-START-DATE-ERROR                  VALUE 'N'.   01340054
           05  PS-ED-DATE-ERR-SW              PIC X(01)    VALUE 'N'.   01350054
               88  PS-END-DATE-ERROR                       VALUE 'Y'.   01360054
               88  PS-NO-END-DATE-ERROR                    VALUE 'N'.   01370054
           05  PS-BO-DATE-ERR-SW              PIC X(01)    VALUE 'N'.   01380054
               88  PS-BOTH-DATE-ERROR                      VALUE 'Y'.   01390054
               88  PS-NO-BOTH-DATE-ERROR                   VALUE 'N'.   01400054
           05  PS-SORT-SW                     PIC X(01)    VALUE 'N'.   01410054
               88  PS-SORT-COMPLETE                        VALUE 'Y'.   01420054
               88  PS-SORT-NOT-COMPLETE                    VALUE 'N'.   01430054
           05  PS-DMM-LEVEL-SPECIFIED-SW      PIC X(01)    VALUE 'N'.   01440054
               88  PS-DMM-LEVEL-SPECIFIED                  VALUE 'Y'.   01450054
               88  PS-DMM-NOT-SPECIFIED                    VALUE 'N'.   01460054
           05  PS-GMM-LEVEL-SPECIFIED-SW      PIC X(01)    VALUE 'N'.   01470054
               88  PS-GMM-LEVEL-SPECIFIED                  VALUE 'Y'.   01480054
               88  PS-GMM-NOT-SPECIFIED                    VALUE 'N'.   01490054
           05  PS-BYR-SPECIFIED-SW            PIC X(01)    VALUE 'N'.   01500054
               88  PS-BYR-LEVEL-SPECIFIED                  VALUE 'Y'.   01510054
               88  PS-BYR-NOT-SPECIFIED                    VALUE 'N'.   01520054
           05  PS-INTER-APPL-SW               PIC X(01)    VALUE 'N'.   01530054
               88  PS-INTER-APPL-VALID                     VALUE 'Y'.   01540054
               88  PS-INTER-APPL-INVALID                   VALUE 'N'.   01550054
           05  PS-QUEUE-STATUS                PIC X(01)    VALUE 'Y'.   01560054
               88  PS-FULL-QUEUE                           VALUE 'Y'.   01570054
               88  PS-EMPTY-QUEUE                          VALUE 'N'.   01580054
           05  PS-ROW-FOUND-SW                PIC X(01)    VALUE 'Y'.   01590054
               88  PS-ROW-FOUND                            VALUE 'Y'.   01600054
               88  PS-ROW-NOT-FOUND                        VALUE 'N'.   01610054
           EJECT                                                        01620054
                                                                        01630054
       01  PV-PROGRAM-VARIABLES.                                        01640054
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     01650054
                                                           COMP SYNC.   01660054
           05  PV-COUNTER                     PIC S9(4)V   VALUE 0      01660154
                                                           COMP.        01660254
           05  PV-CURR-TSQ-LENGTH             PIC S9(4)    VALUE +0     01660354
                                                           COMP.        01660454
           05  PV-ORIG-TSQ-LENGTH             PIC S9(4)    VALUE +0     01660554
                                                           COMP.        01660654
           05  PV-BUYERS-SPECIFIED            PIC S9(3)    VALUE 0.     01691054
           05  PV-START-DATE                  PIC X(08)    VALUE SPACES.01700054
           05  PV-STRT-DATE REDEFINES PV-START-DATE.                    01710054
               10  PV-STRT-CC                 PIC 9(02).                01720054
               10  PV-STRT-YY                 PIC 9(02).                01730054
               10  PV-STRT-MM                 PIC 9(02).                01740054
               10  PV-STRT-DD                 PIC 9(02).                01750054
           05  PV-ROW-COUNT                   PIC S9(03)V  VALUE ZERO   01760054
                                                           COMP-3.      01770054
           05  PV-END-DATE-X                  PIC X(08)    VALUE SPACES.01780054
           05  PV-END-DATE REDEFINES PV-END-DATE-X.                     01790054
               10  PV-END-CC                  PIC 9(02).                01800054
               10  PV-END-YY                  PIC 9(02).                01810054
               10  PV-END-MM                  PIC 9(02).                01820054
               10  PV-END-DD                  PIC 9(02).                01830054
           05  PV-CHANGE-DATE.                                          01840054
               10  PV-CHANGE-CENT             PIC 9(02)    VALUE 0.     01850054
               10  PV-CHANGE-YY               PIC 9(02)    VALUE 0.     01860054
               10  PV-DASH1                   PIC X        VALUE '-'.   01870054
               10  PV-CHANGE-MM               PIC 9(02)    VALUE 0.     01880054
               10  PV-DASH2                   PIC X        VALUE '-'.   01890054
               10  PV-CHANGE-DD               PIC 9(02)    VALUE 0.     01900054
           05  PV-DMMS-SPECIFIED              PIC S9(3)    VALUE 0.     01910054
           05  PV-END-SCREEN                  PIC 99       VALUE 0.     01920054
           05  PV-ERROR-MSG                   PIC X(25).                01930054
           05  FILLER REDEFINES PV-ERROR-MSG.                           01940054
                  10  PV-ERR-MSG              PIC X(13).                01950054
                  10  PV-ERR-DKEY             PIC 9(06)V   COMP-3.      01960054
                  10  FILLER                  PIC X(08).                01970054
           05  PV-FILL-SUB                    PIC S9(04)   VALUE +0     01980054
                                                           COMP SYNC.   01990054
           05  PV-TEMP-FIELD                  PIC X(01)    VALUE SPACES.02000054
           05  PV-GMMS-SPECIFIED              PIC S9(3)    VALUE 0.     02010054
           05  PV-HIST-LEVEL                  PIC X(03)    VALUE SPACES.02020054
           05  PV-LEVEL-INDEX                 PIC 9        VALUE 0.     02030054
               88  PV-INDEX-GMA-LEVEL                      VALUE 1.     02040054
               88  PV-INDEX-DMA-LEVEL                      VALUE 2.     02050054
               88  PV-INDEX-DPT-LEVEL                      VALUE 3.     02060054
           05  PV-LEVEL-INDEX-END             PIC 9        VALUE 0.     02070054
           05  PV-MSG-NUMBER-X                PIC X(05)    VALUE SPACES.02080054
           05  PV-MAP-SUB                     PIC S9(04)   VALUE +0     02090054
                                                           COMP SYNC.   02100054
           05  PV-MAX-RECORDS-FILLED          PIC 99       VALUE  0.    02110054
           05  PV-MDSEAR-LIST-SEQ-NBR.                                  02120054
               10  PV-LIST-SEQ-OPERCO         PIC X(02)    VALUE SPACES.02130054
               10  PV-LIST-SEQ-GMA            PIC X(02)    VALUE SPACES.02140054
               10  PV-LIST-SEQ-DMA            PIC X(02)    VALUE SPACES.02150054
               10  PV-LIST-SEQ-DPT            PIC X(03)    VALUE SPACES.02160054
               10  PV-LIST-SEQ-MCL            PIC X(02)    VALUE SPACES.02170054
               10  PV-LIST-SEQ-SCL            PIC X(03)    VALUE SPACES.02180054
               10  PV-LIST-SEQ-ZERO           PIC X(01)    VALUE SPACES.02190054
           05  FILLER REDEFINES PV-MDSEAR-LIST-SEQ-NBR.                 02200054
               10 PV-MDSEAR-LIST-SEQ-NBR-N    PIC 9(15).                02210054
           05  PV-NUMBER-COMPARISONS          PIC S9(04)   VALUE +0     02220054
                                                           COMP SYNC.   02230054
           05  PV-PREV-LEVEL                  PIC X(03)    VALUE SPACES.02240054
           05  PV-RSPLVL-TO-FIND              PIC X(03)    VALUE SPACES.02250054
           05  PV-RETRY-COUNTER               PIC S9(3)    VALUE 0.     02260054
           05  PV-SAVE-SCREEN-LINE            PIC X(72)    VALUE SPACES.02270054
           05  PV-TEMP-SORT-LINE              PIC X(13)    VALUE SPACES.02280054
           05  PV-TENTATIVE-ITEM-AT-TOP       PIC S999     VALUE +0.    02290058
           05  PV-TEMP-SCREEN-LINE            PIC X(53)    VALUE SPACES.02300054
           05  PV-TEMP-LEVEL                  PIC X(03)    VALUE SPACES.02310054
           05  PV-TEMP-YEAR-DATE.                                       02320054
               10  PV-TY-CC                   PIC 9(02)    VALUE 0.     02330054
               10  PV-TY-YY                   PIC 9(02)    VALUE 0.     02340054
               10  PV-TY-MM                   PIC 9(02)    VALUE 0.     02350054
               10  PV-TY-DD                   PIC 9(02)    VALUE 0.     02360054
           05  PV-TEMP-DATE-NUM               PIC 9(08)    VALUE 0.     02370054
           05  PV-TEMP-DATE  REDEFINES PV-TEMP-DATE-NUM.                02380054
               10  PV-TMP-STRT-CC             PIC 9(02).                02390054
               10  PV-TMP-STRT-YY             PIC 9(02).                02400054
               10  PV-TMP-STRT-MM             PIC 9(02).                02410054
               10  PV-TMP-STRT-DD             PIC 9(02).                02420054
           05  PV-WORK-SUB                    PIC S9(04)   VALUE +0     02430054
                                                           COMP SYNC.   02440054
           05  PV-WORK-LEVEL                  PIC X(03)    VALUE SPACES.02450054
           05  PV-WORK-ID                     PIC X(08)    VALUE SPACES.02460054
           05  PV-TEMP-SUB                    PIC S9(04)   VALUE +0     02470054
                                                           COMP SYNC.   02480054
           05  PV-STORED-DATE.                                          02490054
               10  PV-STORED-CCYY             PIC 9999.                 02500054
           05  PV-STORED-DATE-X REDEFINES PV-STORED-DATE.               02510054
               10  PV-STORED-CC-X             PIC XX.                   02520054
               10  PV-STORED-YY-X             PIC XX.                   02530054
                                                                        02540054
           EJECT                                                        02550054
                                                                        02560054
      ******************************************************************02570054
      * TSQ PASSED BY LIST PROGRAM (LIST TSQ)                           02580054
      ******************************************************************02590054
       01  PV-LIST-TSQ-AREA.                                            02600054
           COPY MBWS003.                                                02610054
           EJECT                                                        02620054
                                                                        02630054
      ******************************************************************02630154
      * CURRENT TSQ - USED TO KEEP THE CURRENT ASC DATA THAT WAS        02630254
      * LOADED TO THE SCREEN. REFRESH THIS TSQ WHENEVER ENTERING THIS   02630354
      * PROGRAM. TABLE THAT CONTAINS ALL PERTINENT SCREEN INFO.         02630454
      ******************************************************************02630554
       01  PV-CURR-TSQ-AREA.                                            02630654
           05  CURR-SCREEN-LINE   OCCURS 455 TIMES                      02630754
                                            INDEXED BY CURR-INDEX,      02630854
                                                       CURR-INDEX2,     02630954
                                                       CURR-INDEX3.     02631054
               10  CURR-LEVEL                 PIC X(03).                02632054
               10  CURR-LEVEL-CHANGED-SW      PIC X.                    02632154
                   88  CURR-LEVEL-CHANGED             VALUE 'Y'.        02632254
                   88  CURR-LEVEL-NOT-CHANGED         VALUE 'N'.        02632354
                                                                        02634054
               10  CURR-START-DATE-CYMD       PIC X(08).                02635054
               10  CURR-START-DATE-CYMD-N REDEFINES                     02635254
                     CURR-START-DATE-CYMD     PIC 9(08).                02635354
               10  FILLER REDEFINES CURR-START-DATE-CYMD.               02635454
                   15  CURR-START-CYMD-CC     PIC X(02).                02635554
                   15  CURR-START-DATE        PIC X(06).                02635654
                   15  FILLER REDEFINES CURR-START-DATE.                02635754
                       20  CURR-START-YY      PIC X(02).                02635854
                       20  CURR-START-MM      PIC X(02).                02635954
                       20  CURR-START-DD      PIC X(02).                02636054
                   15  CURR-START-DATE-N REDEFINES                      02636154
                          CURR-START-DATE     PIC 9(06).                02636254
                   15  FILLER REDEFINES CURR-START-DATE-N.              02636354
                       20  CURR-START-YY-N    PIC 9(02).                02636454
                       20  CURR-START-MM-N    PIC 9(02).                02636554
                       20  CURR-START-DD-N    PIC 9(02).                02636654
                                                                        02637054
               10  CURR-ORIG-START-DATE       PIC S9(08) COMP.          02637154
               10  CURR-START-DATE-CHANGED-SW PIC X(01).                02637354
                   88  CURR-START-CHANGED             VALUE 'Y'.        02637454
                   88  CURR-START-NOT-CHANGED         VALUE 'N'.        02637554
               10  CURR-START-DATE-IN-FUTURE-SW PIC X(01).              02637654
                   88  CURR-PROTECT-START-DATE        VALUE 'Y'.        02637754
                   88  CURR-NO-PROTECT-START-DATE     VALUE 'N'.        02637854
                                                                        02637954
               10  CURR-END-DATE-CYMD         PIC X(08).                02638054
               10  FILLER REDEFINES CURR-END-DATE-CYMD.                 02638154
                   15  CURR-END-DATE-CYMD-N   PIC 9(08).                02638254
               10  FILLER REDEFINES CURR-END-DATE-CYMD.                 02638354
                   15  CURR-END-CYMD-CC       PIC X(02).                02638454
                   15  CURR-END-DATE          PIC X(06).                02638554
                   15  FILLER REDEFINES CURR-END-DATE.                  02638654
                       20  CURR-END-YY        PIC X(02).                02638754
                       20  CURR-END-MM        PIC X(02).                02638854
                       20  CURR-END-DD        PIC X(02).                02638954
                   15  FILLER REDEFINES CURR-END-DATE.                  02639054
                       20  CURR-END-DATE-N    PIC 9(06).                02639154
                       20 FILLER REDEFINES CURR-END-DATE-N.             02639254
                          25  CURR-END-YY-N   PIC 9(02).                02639354
                          25  CURR-END-MM-N   PIC 9(02).                02639454
                          25  CURR-END-DD-N   PIC 9(02).                02639554
               10  CURR-END-DATE-CHANGED-SW   PIC X(01).                02639654
                   88  CURR-END-CHANGED               VALUE 'Y'.        02639754
                   88  CURR-END-NOT-CHANGED           VALUE 'N'.        02639854
                                                                        02639954
               10  CURR-USERID                PIC X(08).                02640554
               10  CURR-WORKER-NBR            PIC 9(6)V  COMP-3.        02640654
               10  CURR-WORKER-CHANGED-SW     PIC X.                    02640754
                   88  CURR-WORKER-CHANGED            VALUE 'Y'.        02640854
                   88  CURR-WORKER-NOT-CHANGED        VALUE 'N'.        02640954
                                                                        02641154
               10  CURR-LAST-NAME             PIC X(18).                02641254
               10  CURR-FIRST-NAME            PIC X(12).                02641354
                                                                        02641454
               10  CURR-HISTORY-EXIST-FLAG    PIC X.                    02641754
                   88  CURR-HISTORY-EXISTS            VALUE 'Y'.        02641854
                   88  CURR-NO-HISTORY-EXIST  VALUE 'N'.                02641954
               10  CURR-ORIGINAL-RECORD-SW    PIC X.                    02642054
                   88  CURR-ORIG-RECORD               VALUE 'Y'.        02642154
                   88  CURR-NEW-RECORD                VALUE ' '.        02642254
                                                                        02642354
      ******************************************************************02647054
      * ORIGINAL TSQ - USED TO KEEP THE ORIGINAL DATABASE DATA THAT     02650054
      * WAS LOADED TO THE SCREEN. REFRESH THIS TSQ WHENEVER AN UPDATE   02660054
      * IS MADE TO THE RESPONSIBILITY DATA.                             02670054
      ******************************************************************02680054
       01  PV-ORIG-TSQ-AREA.                                            02690054
           05  ORIG-SCREEN-LINE   OCCURS 455 TIMES                      02700054
                                            INDEXED BY ORIG-INDEX,      02710054
                                                      ORIG-INDEX2.      02720054
               10  ORIG-LEVEL                 PIC X(03).                02730054
               10  FILLER                     PIC X.                    02740054
                                                                        02750054
               10  ORIG-START-DATE-CYMD       PIC X(08).                02760054
               10  FILLER                     PIC X(06).                02770054
                                                                        02780054
                                                                        02790054
               10  ORIG-END-DATE-CYMD         PIC X(08).                02800054
               10  FILLER                     PIC X(01).                02810054
                                                                        02820054
                                                                        02830054
               10  ORIG-USERID                PIC X(08).                02840054
               10  ORIG-WORKER-NBR            PIC 9(6)V  COMP-3.        02850054
               10  FILLER                     PIC X.                    02860054
                                                                        02870054
               10  ORIG-LAST-NAME             PIC X(18).                02880054
               10  ORIG-FIRST-NAME            PIC X(12).                02890054
                                                                        02900054
               10  ORIG-DARK-FLAGS            PIC X(02).                02910054
               10  FILLER REDEFINES ORIG-DARK-FLAGS.                    02920054
                   15  ORIG-HISTORY-EXIST-FLAG   PIC X.                 02930054
                       88 ORIG-HISTORY-EXISTS    VALUE 'Y'.             02940054
                       88 ORIG-NO-HISTORY-EXIST  VALUE 'N'.             02950054
                   15  ORIG-ORIGINAL-RECORD-SW   PIC X.                 02960054
                       88 ORIG-ORIG-RECORD       VALUE 'Y'.             02970054
                       88 ORIG-NEW-RECORD        VALUE ' '.             02980054
           EJECT                                                        02990054
                                                                        03000054
       01  DK-DATA-KEYS.                                                03010054
           05  DK-OPERCO-NBR                  PIC X(02)    VALUE SPACES.03020054
           05  DK-GMA-NBR                     PIC X(02)    VALUE SPACES.03030054
           05  DK-DMA-NBR                     PIC X(02)    VALUE SPACES.03040054
           05  DK-DEPT-NBR                    PIC X(03)    VALUE SPACES.03050054
           05  DK-MAJ-CL-NBR                  PIC X(02)    VALUE SPACES.03060054
           05  DK-SUB-CL-NBR                  PIC X(03)    VALUE SPACES.03070054
           05  DK-END-DATE                    PIC X(10)    VALUE SPACES.03080054
           05  DK-START-DATE                  PIC X(10)    VALUE SPACES.03090054
           05  DK-MDSELV-CDE                  PIC X(03)    VALUE SPACES.03100054
           05  DK-MDSEAR-DKEY                 PIC S9(06)V   COMP-3      03110054
                                                           VALUE 0.     03120054
           05  DK-RSPLVL-CDE                  PIC X(03)    VALUE SPACES.03130054
           05  DK-WORKER-NBR                  PIC S9(06)V  VALUE ZERO   03140054
                                                           COMP-3.      03150054
           05  DK-WORKER-UID                  PIC X(08)    VALUE SPACES.03160054
           EJECT                                                        03170054
                                                                        03180054
      ***************************************************************** 03190054
      *    STANDARD COMMAREA.                                           03200054
      ***************************************************************** 03210054
                                                                        03220054
           COPY DPWS020.                                                03230054
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                03240054
                                                                        03250054
      ***************************************************************** 03260054
      *    INTER-APPLICATION COMMAREA.                                  03270054
      ***************************************************************** 03280054
                                                                        03290054
               10  INTER-APPL-COMM-AREA       PIC X(200).               03300054
           COPY MBWS005.                                                03310054
                                                                        03320054
      ***************************************************************** 03330054
      *    APPLICATION SPECIFIC COMMAREA FOR MBKCS131 - 2865 BYTES      03340054
      ***************************************************************** 03350054
               10  ASC-APPL-SPEC-COMMAREA.                              03360054
                   15 ASC-COMPANY-NO                  PIC X(2).         03370054
                   15 ASC-GMA-NO                      PIC X(2).         03380054
                   15 ASC-GMA-DESC                    PIC X(25).        03390054
                   15 ASC-DMA-NO                      PIC X(2).         03400054
                   15 ASC-DMA-DESC                    PIC X(25).        03410054
                   15 ASC-DPT-NO                      PIC X(3).         03420054
                   15 ASC-DPT-DESC                    PIC X(25).        03430054
      *                                                                 03440054
                   15 ASC-MDSEAR-DKEY                 PIC S9(06)V       03450054
                                                      COMP-3.           03460054
      *                                                                 03470054
                   15 ASC-LEVEL-START-DATE-CYMD       PIC 9(8).         03480054
                   15 FILLER REDEFINES ASC-LEVEL-START-DATE-CYMD.       03490054
                      20  ASC-LEVEL-START-CC          PIC 9(02).        03500054
                      20  ASC-LEVEL-START-DATE        PIC 9(06).        03510054
                      20  FILLER REDEFINES ASC-LEVEL-START-DATE.        03520054
                          25  ASC-LEVEL-START-YY      PIC 9(02).        03530054
                          25  ASC-LEVEL-START-MM      PIC 9(02).        03540054
                          25  ASC-LEVEL-START-DD      PIC 9(02).        03550054
                                                                        03560054
                   15 ASC-LEVEL-END-DATE-CYMD         PIC 9(08).        03570054
                   15 FILLER REDEFINES ASC-LEVEL-END-DATE-CYMD.         03580054
                      20  ASC-LEVEL-END-CC            PIC 9(02).        03590054
                      20  ASC-LEVEL-END-DATE          PIC 9(06).        03600054
                      20  FILLER REDEFINES ASC-LEVEL-END-DATE.          03610054
                          25  ASC-LEVEL-END-YY        PIC 9(02).        03620054
                          25  ASC-LEVEL-END-MM        PIC 9(02).        03630054
                          25  ASC-LEVEL-END-DD        PIC 9(02).        03640054
                                                                        03650054
                   15 ASC-ALPHA-MDSEAR-LEVEL          PIC X(03).        03660054
                   15 ASC-LEVEL-TO-UPDATE             PIC 9.            03670054
                      88  ASC-UPD-GMA                 VALUE 1.          03680054
                      88  ASC-UPD-DMA                 VALUE 2.          03690054
                      88  ASC-UPD-DPT                 VALUE 3.          03700054
                      88  ASC-UPD-MCL                 VALUE 4.          03710054
                      88  ASC-UPD-SCL                 VALUE 5.          03720054
      *                                                                 03730054
                   15 ASC-LIST-TSQ-IDX                    PIC S9(4)     03740054
                                                            COMP.       03750054
      *                                                                 03760054
                   15 ASC-TSQ-INFO.                                     03770054
                       20  ASC-LIST-TSQ-NAME              PIC X(08).    03780054
                       20  FILLER REDEFINES ASC-LIST-TSQ-NAME.          03790054
                           25  ASC-LIST-TERM-ID           PIC X(04).    03800054
                           25  ASC-LIST-TASK-ID           PIC S9(07)    03810054
                                                            COMP-3.     03820054
                       20  ASC-LIST-TSQ-LENGTH            PIC S9(4)     03830054
                                                            COMP.       03840054
                       20  ASC-LIST-TSQ-ITEM-NBR          PIC S9(04)    03850054
                                                            COMP SYNC.  03860054
                       20  ASC-LIST-TSQ-MAX-ITEMS         PIC 9(03).    03870054
      *                                                                 03880054
                       20  ASC-ORIG-TSQ-NAME              PIC X(08).    03890054
                       20  FILLER REDEFINES ASC-ORIG-TSQ-NAME.          03900054
                           25  ASC-ORIG-TERM-ID           PIC X(04).    03910054
                           25  ASC-ORIG-TASK-NUMBER       PIC S9(05)    03920054
                                                              COMP-3.   03930054
                           25  ASC-ORIG-SEQ               PIC 9.        03940054
      *                                                                 03950354
                       20  ASC-ORIG-TSQ-ITEM-NBR          PIC S9(04)    03950454
                                                            COMP SYNC.  03950554
                       20  ASC-CURR-TSQ-NAME              PIC X(08).    03951054
                       20  FILLER REDEFINES ASC-CURR-TSQ-NAME.          03952054
                           25  ASC-CURR-TERM-ID           PIC X(04).    03953054
                           25  ASC-CURR-TASK-NUMBER       PIC S9(05)    03954054
                                                              COMP-3.   03955054
                           25  ASC-CURR-SEQ               PIC 9.        03956054
      *                                                                 03957054
                       20  ASC-CURR-TSQ-ITEM-NBR          PIC S9(04)    03958054
                                                            COMP SYNC.  03959054
                   15 ASC-TOP-OF-SCREEN                   PIC 999.      03970057
                   15 ASC-BOT-OF-SCREEN                   PIC 999.      03980057
      *                                                                 03990054
                   15 ASC-LINES-ENTERED                   PIC S9(04)    04000054
                                                          COMP.         04010054
                   15  FILLER                             PIC X(2710).  04790057
                                                                        04800054
                                                                        04810054
      ***************************************************************** 04820054
      *    ABEND PROCESSING COPYBOOK.                                   04830054
      ***************************************************************** 04840054
                                                                        04850054
           COPY DPWS013.                                                04860054
                                                                        04870054
      ***************************************************************** 04880054
      *    ATTRIBUTE SETTINGS COPYBOOK.                                 04890054
      ***************************************************************** 04900054
                                                                        04910054
           COPY DPWS015.                                                04920054
                                                                        04930054
      ***************************************************************** 04940054
      *                                                                 04950054
      ***************************************************************** 04960054
                                                                        04970054
           COPY DPWS006.                                                04980054
                                                                        04990054
      ***************************************************************** 05000054
      *    DATE EDIT ROUTINE  COPYBOOK.                                 05010054
      ***************************************************************** 05020054
                                                                        05030054
           COPY DPWS500.                                                05040054
                                                                        05050054
      ***************************************************************** 05060054
      *    CURRENT DATE AND TIME COPYBOOK.                              05070054
      ***************************************************************** 05080054
                                                                        05090054
           COPY DPWS017.                                                05100054
                                                                        05110054
      ***************************************************************** 05120054
      *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          05130054
      ***************************************************************** 05140054
                                                                        05150054
           COPY DPWS009.                                                05160054
                                                                        05170054
      ***************************************************************** 05180054
      *    FUNCTION KEYS COPYBOOK.                                      05190054
      ***************************************************************** 05200054
                                                                        05210054
           COPY DPWS016.                                                05220054
                                                                        05230054
      ***************************************************************** 05240054
      *    NUMERIC EDIT ROUTINE                                         05250054
      ***************************************************************** 05260054
                                                                        05270054
           COPY DPWS010I.                                               05280054
                                                                        05290054
      ***************************************************************** 05300054
      *    DPKUT045 COPYBOOK.                                           05310054
      *                                                                 05320054
      *    DATA AREA NECESSARY TO INTERACT WITH SUB-ROUTINE DPKUT045    05330054
      *                                                                 05340054
      *    DPKUT045 IS A UTILITY ROUTINE THAT CONVERTS THE EIBAID       05350054
      *    CHARACTER RECEIVED FROM THE TERMINAL INTO A INTEGER VALUE    05360054
      *    FROM 1 TO 29. THIS INTEGER VALUE IS STORED IN DP020-SRC-AID  05370054
      *    AND IS USED TO INTERROGATE THE DP020-FUNCTION-KEY-ARRAY      05380054
      *    TABLE IN THE STANDARD COMMAREA TO DETERMINE WHAT TO DO NEXT. 05390054
      *    IN ADDITION DPKUT045 WILL PARSE THE SCREEN INPUT COMMAND     05400054
      *    FIELD AND CONVERT RECOGNIZED SCREEN MANIPULATION COMMANDS    05410054
      *    INTO THE DP020-SRC-AID EQUIVALENT.                           05420054
      ***************************************************************** 05430054
                                                                        05440054
           COPY DPWS045.                                                05450054
                                                                        05460054
      ***************************************************************** 05470054
      *    SYSTEM MESSAGES RECORD LAYOUT                                05480054
      ***************************************************************** 05490054
                                                                        05500054
           COPY DPRD507I.                                               05510054
                                                                        05520054
      ***************************************************************** 05530054
      *    STANDARD ATTENTION IDENTIFIER LIST                           05540054
      ***************************************************************** 05550054
                                                                        05560054
           COPY DFHAID.                                                 05570054
                                                                        05580054
      ***************************************************************** 05590054
      *    MAP LAYOUT                                                   05600054
      ***************************************************************** 05610054
                                                                        05620054
           COPY MBKM131.                                                05630054
                                                                        05640054
       01  MD-MAP-DATA-AREA REDEFINES MB131AO.                          05650054
      ***************************************************************** 05660054
      *    FOR THE UTILITIES THAT RECEIVE AND SEND THE MAP TO THE       05670054
      *    TERMINAL TO FUNCTION CORRECTLY, CHANGE THE PIC CLAUSE OF     05680054
      *    THE FOLLOWING FILLER STATEMENT TO BE EQUAL TO THE TOTAL      05690054
      *    LENGTH OF THE MAP COPYBOOK.                                  05700054
      ***************************************************************** 05710054
           05  FILLER                         PIC X(1666).              05720054
      ***************************************************************** 05730054
      *    THIS MAP IS REDEFINED TO MAKE THE TABLE HANDLING EASIER      05740054
      *    THIS MAP IS REDEFINED TO MAKE THE TABLE HANDLING EASIER      05750054
      *    THIS MAP IS REDEFINED TO MAKE THE TABLE HANDLING EASIER      05760054
      ***************************************************************** 05770054
       01  MR-MAP-REDEF-AREA REDEFINES MB131AO.                         05780054
           05  FILLER                         PIC X(12).                05790054
           05  FILLER                         PIC X(85).                05800054
           05  MR-HEADER-DATA.                                          05810054
               10  MR-COMPANY-L               PIC S9(04)  COMP.         05820054
               10  MR-COMPANY-A               PIC  X(01).               05830054
               10  MR-COMPANY-C               PIC  X(01).               05840054
               10  MR-COMPANY-H               PIC  X(01).               05850054
               10  MR-COMPANY                 PIC  X(02).               05860054
               10  MR-GMA-NO-L                PIC S9(04)  COMP.         05870054
               10  MR-GMA-NO-A                PIC  X(01).               05880054
               10  MR-GMA-NO-C                PIC  X(01).               05890054
               10  MR-GMA-NO-H                PIC  X(01).               05900054
               10  MR-GMA-NO                  PIC  X(02).               05910054
               10  MR-GMA-DESC-L              PIC S9(04)  COMP.         05920054
               10  MR-GMA-DESC-A              PIC  X(01).               05930054
               10  MR-GMA-DESC-C              PIC  X(01).               05940054
               10  MR-GMA-DESC-H              PIC  X(01).               05950054
               10  MR-GMA-DESC                PIC  X(25).               05960054
               10  MR-DMAHDR-L                PIC S9(04)  COMP.         05970054
               10  MR-DMAHDR-A                PIC  X(01).               05980054
               10  MR-DMAHDR-C                PIC  X(01).               05990054
               10  MR-DMAHDR-H                PIC  X(01).               06000054
               10  MR-DMAHDR                  PIC  X(04).               06010054
               10  MR-DMA-NO-L                PIC S9(04)  COMP.         06020054
               10  MR-DMA-NO-A                PIC  X(01).               06030054
               10  MR-DMA-NO-C                PIC  X(01).               06040054
               10  MR-DMA-NO-H                PIC  X(01).               06050054
               10  MR-DMA-NO                  PIC  X(02).               06060054
               10  MR-DMA-DESC-L              PIC S9(04)  COMP.         06070054
               10  MR-DMA-DESC-A              PIC  X(01).               06080054
               10  MR-DMA-DESC-C              PIC  X(01).               06090054
               10  MR-DMA-DESC-H              PIC  X(01).               06100054
               10  MR-DMA-DESC                PIC  X(25).               06110054
               10  MR-DPTHDR-L                PIC S9(04)  COMP.         06120054
               10  MR-DPTHDR-A                PIC  X(01).               06130054
               10  MR-DPTHDR-C                PIC  X(01).               06140054
               10  MR-DPTHDR-H                PIC  X(01).               06150054
               10  MR-DPTHDR                  PIC  X(11).               06160054
               10  MR-DPT-NO-L                PIC S9(04)  COMP.         06170054
               10  MR-DPT-NO-A                PIC  X(01).               06180054
               10  MR-DPT-NO-C                PIC  X(01).               06190054
               10  MR-DPT-NO-H                PIC  X(01).               06200054
               10  MR-DPT-NO                  PIC  X(03).               06210054
               10  MR-DPT-DESC-L              PIC S9(04)  COMP.         06220054
               10  MR-DPT-DESC-A              PIC  X(01).               06230054
               10  MR-DPT-DESC-C              PIC  X(01).               06240054
               10  MR-DPT-DESC-H              PIC  X(01).               06250054
               10  MR-DPT-DESC                PIC  X(25).               06260054
           05  MR-SCREEN-LINE                 OCCURS 11 TIMES.          06270054
               10  MR-LEVEL-L                 PIC S9(04)  COMP.         06280054
               10  MR-LEVEL-A                 PIC  X(01).               06290054
               10  MR-LEVEL-C                 PIC  X(01).               06300054
               10  MR-LEVEL-H                 PIC  X(01).               06310054
               10  MR-LEVEL                   PIC  X(03).               06320054
               10  MR-STRT-MM-L               PIC S9(04)  COMP.         06330054
               10  MR-STRT-MM-A               PIC  X(01).               06340054
               10  MR-STRT-MM-C               PIC  X(01).               06350054
               10  MR-STRT-MM-H               PIC  X(01).               06360054
               10  MR-STRT-MM                 PIC  X(02).               06370054
               10  MR-STRT-DD-L               PIC S9(04)  COMP.         06380054
               10  MR-STRT-DD-A               PIC  X(01).               06390054
               10  MR-STRT-DD-C               PIC  X(01).               06400054
               10  MR-STRT-DD-H               PIC  X(01).               06410054
               10  MR-STRT-DD                 PIC  X(02).               06420054
               10  MR-STRT-YY-L               PIC S9(04)  COMP.         06430054
               10  MR-STRT-YY-A               PIC  X(01).               06440054
               10  MR-STRT-YY-C               PIC  X(01).               06450054
               10  MR-STRT-YY-H               PIC  X(01).               06460054
               10  MR-STRT-YY                 PIC  X(02).               06470054
               10  MR-END-MM-L                PIC S9(04)  COMP.         06480054
               10  MR-END-MM-A                PIC  X(01).               06490054
               10  MR-END-MM-C                PIC  X(01).               06500054
               10  MR-END-MM-H                PIC  X(01).               06510054
               10  MR-END-MM                  PIC  X(02).               06520054
               10  MR-END-DD-L                PIC S9(04)  COMP.         06530054
               10  MR-END-DD-A                PIC  X(01).               06540054
               10  MR-END-DD-C                PIC  X(01).               06550054
               10  MR-END-DD-H                PIC  X(01).               06560054
               10  MR-END-DD                  PIC  X(02).               06570054
               10  MR-END-YY-L                PIC S9(04)  COMP.         06580054
               10  MR-END-YY-A                PIC  X(01).               06590054
               10  MR-END-YY-C                PIC  X(01).               06600054
               10  MR-END-YY-H                PIC  X(01).               06610054
               10  MR-END-YY                  PIC  X(02).               06620054
               10  MR-WORKER-L                PIC S9(04)  COMP.         06630054
               10  MR-WORKER-A                PIC  X(01).               06640054
               10  MR-WORKER-C                PIC  X(01).               06650054
               10  MR-WORKER-H                PIC  X(01).               06660054
               10  MR-WORKER                  PIC  X(08).               06670054
               10  MR-LNAME-L                 PIC S9(04)  COMP.         06680054
               10  MR-LNAME-A                 PIC  X(01).               06690054
               10  MR-LNAME-C                 PIC  X(01).               06700054
               10  MR-LNAME-H                 PIC  X(01).               06710054
               10  MR-LNAME                   PIC  X(18).               06720054
               10  MR-FNAME-L                 PIC S9(04)  COMP.         06730054
               10  MR-FNAME-A                 PIC  X(01).               06740054
               10  MR-FNAME-C                 PIC  X(01).               06750054
               10  MR-FNAME-H                 PIC  X(01).               06760054
               10  MR-FNAME                   PIC  X(12).               06770054
           05  MR-MSG-L                       PIC S9(04)  COMP.         06780054
           05  MR-MSG-A                       PIC  X(01).               06790054
           05  MR-MSG-C                       PIC  X(01).               06800054
           05  MR-MSG-H                       PIC  X(01).               06810054
           05  MR-MSG                         PIC  X(78).               06820054
           05  MR-CMD-L                       PIC S9(04)  COMP.         06830054
           05  MR-CMD-A                       PIC  X(01).               06840054
           05  MR-CMD-C                       PIC  X(01).               06850054
           05  MR-CMD-H                       PIC  X(01).               06860054
           05  MR-CMD                         PIC  X(08).               06870054
           05  MR-KEY-L                       PIC S9(04)  COMP.         06880054
           05  MR-KEY-A                       PIC  X(01).               06890054
           05  MR-KEY-C                       PIC  X(01).               06900054
           05  MR-KEY-H                       PIC  X(01).               06910054
           05  MR-KEY                         PIC  X(25).               06920054
           05  MR-KLINE1-L                    PIC S9(04)  COMP.         06930054
           05  MR-KLINE1-A                    PIC  X(01).               06940054
           05  MR-KLINE1-C                    PIC  X(01).               06950054
           05  MR-KLINE1-H                    PIC  X(01).               06960054
           05  MR-KLINE1                      PIC  X(78).               06970054
           05  MR-KLINE2-L                    PIC S9(04)  COMP.         06980054
           05  MR-KLINE2-A                    PIC  X(01).               06990054
           05  MR-KLINE2-C                    PIC  X(01).               07000054
           05  MR-KLINE2-H                    PIC  X(01).               07010054
           05  MR-KLINE2                      PIC  X(78).               07020054
           EJECT                                                        07030054
      ***************************************************************** 07040054
      *    RESPONSIBILITY LEVEL   DB2 TABLE DEFINITION                  07050054
      ***************************************************************** 07060054
                                                                        07070054
           EXEC SQL                                                     07080054
               INCLUDE TRSPLVL                                          07090054
           END-EXEC.                                                    07100054
           EJECT                                                        07110054
      ***************************************************************** 07120054
      *    MBS WORKER TABLE       DB2 TABLE DEFINITION                  07130054
      ***************************************************************** 07140054
                                                                        07150054
           EXEC SQL                                                     07160054
               INCLUDE TWORKER                                          07170054
           END-EXEC.                                                    07180054
      ***************************************************************** 07190054
      *    MBS MERCHANDISE AREA   DB2 TABLE DEFINITION                  07200054
      ***************************************************************** 07210054
                                                                        07220054
           EXEC SQL                                                     07230054
               INCLUDE TMDSEAR                                          07240054
           END-EXEC.                                                    07250054
      ***************************************************************** 07260054
      *    MBS RESPONSIBILITY AREA DB2 TABLE DEFINITION                 07270054
      ***************************************************************** 07280054
                                                                        07290054
           EXEC SQL                                                     07300054
               INCLUDE TRSPBAR                                          07310054
           END-EXEC.                                                    07320054
                                                                        07330054
      ***************************************************************** 07340054
      *    RESPONSIBILITY AREA CURSOR                                   07350054
      ***************************************************************** 07360054
           EXEC SQL                                                     07370054
               DECLARE RSPBAR_CUR CURSOR FOR                            07380054
                  SELECT B.RSPLVL_CDE,                                  07390054
                         B.MDSEAR_DKEY,                                 07400054
                         B.WORKER_STRT_DTE,                             07410054
                         B.WORKER_END_DTE,                              07420054
                         B.WORKER_NBR,                                  07430054
                         B.WORKER_FIR_NAME,                             07440054
                         B.WORKER_LAST_NAME                             07450054
                  FROM TMDSEAR A,                                       07460054
                       TRSPBAR B                                        07470054
                  WHERE (B.MDSEAR_DKEY = A.MDSEAR_DKEY) AND             07480054
                        (A.OPERCO_NBR  = :DK-OPERCO-NBR) AND            07490054
                        (A.GMA_NBR     = :DK-GMA-NBR)   AND             07500054
                        (A.DMA_NBR     = :DK-DMA-NBR)   AND             07510054
                        (A.DEPT_NBR    = :DK-DEPT-NBR)  AND             07520054
                        (A.MAJ_CL_NBR  = :DK-MAJ-CL-NBR) AND            07530054
                        (A.SUB_CL_NBR  = :DK-SUB-CL-NBR) AND            07540054
                        (A.END_DTE     > DATE(CURRENT TIMESTAMP))       07550054
                  ORDER BY B.RSPLVL_CDE,                                07560054
                           B.WORKER_STRT_DTE,                           07570054
                           B.WORKER_LAST_NAME,                          07571054
                           B.WORKER_FIR_NAME                            07572054
           END-EXEC.                                                    07580054
                                                                        07590054
      ***************************************************************** 07600054
      *    DB2 COMMUNICATION AREA                                       07610054
      ***************************************************************** 07620054
                                                                        07630054
           EXEC SQL                                                     07640054
               INCLUDE SQLCA                                            07650054
           END-EXEC.                                                    07660054
                                                                        07670054
                                                                        07680054
       LINKAGE SECTION.                                                 07690054
                                                                        07700054
       01  DFHCOMMAREA.                                                 07710054
           05  FILLER                       OCCURS 1 TO 4072 TIMES      07720054
                                            DEPENDING ON EIBCALEN.      07730054
               10  FILLER                   PIC X.                      07740054
       EJECT                                                            07750054
      *                                                                 07760054
      *                                                                 07770054
      *                                                                 07780054
       PROCEDURE DIVISION.                                              07790054
                                                                        07800054
                                                                        07810054
                                                                        07820054
                                                                        07830054
       0000-MAIN-MODULE.                                                07840054
                                                                        07850054
      ******************************************************************07860054
      *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           07870054
      *                                                                 07880054
      ************* DO NOT ALTER THIS PARAGRAPH ************************07890054
      *                                                                 07900054
      ******************************************************************07910054
                                                                        07920054
           MOVE PC-FIXED-TSQ-LENGTH TO PV-CURR-TSQ-LENGTH               07921054
                                       PV-ORIG-TSQ-LENGTH.              07922054
           PERFORM DP015-0000-CHECK-COMMAREA.                           07930054
                                                                        07940054
      ******************************************************************07950054
      *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS  07960054
      *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER     07970054
      *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS      07980054
      *    PROGRAM WILL RETURN TO CICS.                                 07990054
      ******************************************************************08000054
           IF DP020-SRC-RETURN-W-TRAN-ID                                08010054
               PERFORM 1000-CONTROL-PROCESSING                          08020054
           END-IF.                                                      08030054
                                                                        08040054
           PERFORM DP003-0000-RETURN-TO-CICS.                           08050054
       EJECT                                                            08060054
       1000-CONTROL-PROCESSING.                                         08070054
                                                                        08080054
      ******************************************************************08090054
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT    08100054
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    08110054
      *                                                                 08120054
      *                                                                 08130054
      ************* DO NOT ALTER THIS PARAGRAPH ************************08140054
      *                                                                 08150054
      ******************************************************************08160054
                                                                        08170054
           EVALUATE TRUE                                                08180054
                                                                        08190054
               WHEN DP020-NEXT-ACT-INITIAL                              08200054
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 08210054
                                                                        08220054
                   PERFORM 1100-INITIALIZE-COMMAREA                     08230054
                   PERFORM 4100-SAVE-INITIAL-INTERAPPL                  08240054
                   IF PS-INTER-APPL-VALID                               08250054
                       MOVE EIBTRMID TO ASC-CURR-TERM-ID                08251054
                                        ASC-ORIG-TERM-ID                08251154
                       MOVE DP020-SRC-TASK-NUMBER                       08252054
                                     TO ASC-CURR-TASK-NUMBER            08253054
                                        ASC-ORIG-TASK-NUMBER            08254054
                       MOVE 1        TO ASC-CURR-SEQ                    08255054
                       MOVE 2        TO ASC-ORIG-SEQ                    08255154
                       PERFORM 2100-PROCESS-LIST-TSQ                    08260054
                       PERFORM 2000-BUILD-INITIAL-PANEL                 08270054
                       PERFORM 2200-DISPLAY-PANEL                       08280054
                   ELSE                                                 08290054
                       PERFORM 9999-RETURN-TO-PREV-TRAN                 08300054
                       PERFORM 4300-SETUP-INTER-APPL-COMM               08310054
                       PERFORM DP015-1000-START-NAVIGATOR               08320054
                   END-IF                                               08330054
                                                                        08340054
               WHEN DP020-NEXT-ACT-READ-MAP                             08350054
                   PERFORM 9520-READ-CURR-TSQ                           08351054
                   PERFORM 3000-PROCESS-MAP                             08360054
                                                                        08370054
               WHEN DP020-NEXT-ACT-RETURN                               08380054
                   PERFORM 9520-READ-CURR-TSQ                           08381054
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 08390054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   08410054
                   PERFORM 2200-DISPLAY-PANEL                           08420054
                                                                        08430054
               WHEN DP020-NEXT-ACT-ERROR                                08440054
                                                                        08450054
                   PERFORM 9520-READ-CURR-TSQ                           08451054
                   MOVE DP020-MSG-NUMBER-X  TO PV-MSG-NUMBER-X          08460054
                                                                        08470054
                   IF DP020-NAV-MNEMONIC = SPACES                       08480054
                       MOVE SPACES          TO ANAVCMDO                 08490054
                       MOVE DP015-GREEN     TO ANAVCMDC                 08500054
                       MOVE DP015-UNDERLINE TO ANAVCMDH                 08510054
                   ELSE                                                 08520054
                       MOVE DP015-RED       TO ANAVCMDC                 08530054
                       MOVE DP015-REVERSE   TO ANAVCMDH                 08540054
                   END-IF                                               08550054
                                                                        08560054
                   MOVE LOW-VALUES TO DP020-INTER-APPL-DATA-AREA        08570054
                                                                        08580054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   08590054
                                                                        08600054
                   SET PS-ERROR                TO TRUE                  08610054
                   SET PS-SEND-DATA-ONLY-FRSET TO TRUE                  08620054
                                                                        08630054
                   MOVE PV-MSG-NUMBER-X     TO DP020-MSG-NUMBER-X       08640054
      ******************************************************************08650054
      *    END OF CODE BLOCK DESIGNED TO SAVE MESSAGE FROM DPKCS008     08660054
      ******************************************************************08670054
                   MOVE -1         TO ANAVCMDL                          08680054
                                                                        08690054
                   PERFORM 2200-DISPLAY-PANEL                           08700054
                                                                        08710054
               WHEN OTHER                                               08720054
                   SET DP013-LOGIC-ABEND TO TRUE                        08730054
                   SET DP013-NO-ROLLBACK TO TRUE                        08740054
                                                                        08750054
                   MOVE 'B100-CONTROL-PROCESSING' TO                    08760054
                        DP013-PARAGRAPH                                 08770054
                   MOVE 'INVALID NEXT ACTION FOR ARCHITECTURE' TO       08780054
                           DP013-MESSAGE-TEXT(1)                        08790054
                                                                        08800054
                   PERFORM DP013-0000-PROCESS-ABEND                     08810054
                                                                        08820054
           END-EVALUATE.                                                08830054
       EJECT                                                            08840054
       1100-INITIALIZE-COMMAREA.                                        08850054
                                                                        08860054
      ******************************************************************08870054
      *                                                                 08880054
      ******************************************************************08890054
            INITIALIZE ASC-APPL-SPEC-COMMAREA.                          08900054
                                                                        08920054
       1700-RESET-ATTRIBUTES.                                           08930054
      ******************************************************************08940054
      * RESET ALL ATTRIBUTES ON SCREEN                                  08950054
      ******************************************************************08960054
                                                                        08970054
           MOVE DP015-ASK-BRT-OFF  TO MR-DMAHDR-A                       08980054
                                      MR-DPTHDR-A.                      08990054
           MOVE DP015-WHITE        TO MR-DMAHDR-C                       09000054
                                      MR-DPTHDR-C.                      09010054
                                                                        09020054
           MOVE DP015-ASK-NOR-OFF  TO MR-GMA-NO-A                       09030054
                                      MR-DMA-NO-A                       09040054
                                      MR-DPT-NO-A                       09050054
                                      MR-GMA-DESC-A                     09060054
                                      MR-DMA-DESC-A                     09070054
                                      MR-DPT-DESC-A.                    09080054
           MOVE DP015-BLUE         TO MR-GMA-NO-C                       09090054
                                      MR-DMA-NO-C                       09100054
                                      MR-DPT-NO-C                       09110054
                                      MR-GMA-DESC-C                     09120054
                                      MR-DMA-DESC-C                     09130054
                                      MR-DPT-DESC-C.                    09140054
                                                                        09150054
           IF ASC-UPD-GMA                                               09160054
              MOVE DP015-ASK-DRK-OFF  TO MR-DMAHDR-A                    09170054
                                         MR-DMA-NO-A                    09180054
                                         MR-DMA-DESC-A                  09190054
                                         MR-DPTHDR-A                    09200054
                                         MR-DPT-NO-A                    09210054
                                         MR-DPT-DESC-A                  09220054
              MOVE DP015-HL-OFF       TO MR-DMAHDR-H                    09230054
                                         MR-DMA-NO-H                    09240054
                                         MR-DMA-DESC-H                  09250054
                                         MR-DPTHDR-H                    09260054
                                         MR-DPT-NO-H                    09270054
                                         MR-DPT-DESC-H                  09280054
           ELSE                                                         09290054
              IF ASC-UPD-DMA                                            09300054
                  MOVE DP015-ASK-DRK-OFF TO MR-DPTHDR-A                 09310054
                                            MR-DPT-NO-A                 09320054
                                            MR-DPT-DESC-A               09330054
                  MOVE DP015-HL-OFF   TO MR-DPTHDR-H                    09340054
                                         MR-DPT-NO-H                    09350054
                                         MR-DPT-DESC-H                  09360054
              END-IF                                                    09370054
           END-IF.                                                      09380054
                                                                        09390054
                                                                        09400054
           PERFORM VARYING PV-MAP-SUB                                   09410054
                   FROM 1 BY 1                                          09420054
                   UNTIL PV-MAP-SUB > PC-MAX-SCREEN-LINES               09430054
                                                                        09440054
               MOVE DP015-GREEN  TO MR-LEVEL-C(PV-MAP-SUB)              09450054
                                    MR-STRT-MM-C(PV-MAP-SUB)            09460054
                                    MR-STRT-DD-C(PV-MAP-SUB)            09470054
                                    MR-STRT-YY-C(PV-MAP-SUB)            09480054
                                    MR-END-MM-C(PV-MAP-SUB)             09490054
                                    MR-END-DD-C(PV-MAP-SUB)             09500054
                                    MR-END-YY-C(PV-MAP-SUB)             09510054
                                    MR-WORKER-C(PV-MAP-SUB)             09520054
                                                                        09530054
               MOVE DP015-UNDERLINE TO MR-LEVEL-H(PV-MAP-SUB)           09540054
                                    MR-STRT-MM-H(PV-MAP-SUB)            09550054
                                    MR-STRT-DD-H(PV-MAP-SUB)            09560054
                                    MR-STRT-YY-H(PV-MAP-SUB)            09570054
                                    MR-END-MM-H(PV-MAP-SUB)             09580054
                                    MR-END-DD-H(PV-MAP-SUB)             09590054
                                    MR-END-YY-H(PV-MAP-SUB)             09600054
                                    MR-WORKER-H(PV-MAP-SUB)             09610054
                                                                        09620054
               MOVE DP015-UNP-ALP-NOR-OFF TO                            09630054
                                    MR-LEVEL-A(PV-MAP-SUB)              09640054
                                    MR-STRT-MM-A(PV-MAP-SUB)            09650054
                                    MR-STRT-DD-A(PV-MAP-SUB)            09660054
                                    MR-STRT-YY-A(PV-MAP-SUB)            09670054
                                    MR-END-MM-A(PV-MAP-SUB)             09680054
                                    MR-END-DD-A(PV-MAP-SUB)             09690054
                                    MR-END-YY-A(PV-MAP-SUB)             09700054
                                    MR-WORKER-A(PV-MAP-SUB)             09710054
           END-PERFORM.                                                 09720054
                                                                        09730054
       2000-BUILD-INITIAL-PANEL.                                        09740054
      ******************************************************************09760054
      * THIS WILL BUILD THE INITIAL PANEL OF DATA TO BE DISPLAYED       09770054
      * ON SCREEN.  ALL DATA THAT IS DISPLAYED IS ALSO SAVED IN THE     09780054
      * CURR-TSQ AND ORIG-TSQ.                                          09790054
      ******************************************************************09810054
                                                                        09820054
           PERFORM DP017-0000-GET-CURR-DATE-TIME                        09830054
           SET DP017-EDITED-YMD-DATE-DASH-1                             09840054
               DP017-EDITED-YMD-DATE-DASH-2 TO TRUE.                    09850054
                                                                        09851054
           PERFORM 2300-DETERMINE-LEVEL-UPDATE.                         09880054
                                                                        09890054
           PERFORM 1700-RESET-ATTRIBUTES.                               09900054
                                                                        09910054
           PERFORM 2400-RETRIEVE-DATA-FOR-PANEL.                        09920054
                                                                        09930054
      *    WRITE THE DATA READ FROM THE TRSPBAR TABLE OUT TO THE        09943054
      *    ORIGINAL TEMP STORAGE QUEUE. THIS TSQ IS TO BE USED FOR      09944054
      *    COMPARISON PURPOSES AT UPDATE TIME. IT IS REWRITTEN WHEN     09944154
      *    THE DATA IS UPDATED TO THE DATABASE.                         09945054
           MOVE PV-CURR-TSQ-AREA TO PV-ORIG-TSQ-AREA.                   09949054
           IF ASC-ORIG-TSQ-ITEM-NBR = +0                                09949154
               MOVE +1 TO ASC-ORIG-TSQ-ITEM-NBR                         09949254
               PERFORM 9511-WRITE-ORIG-TSQ                              09949354
           ELSE                                                         09949454
               PERFORM 9512-REWRITE-ORIG-TSQ                            09949554
           END-IF.                                                      09949654
                                                                        09949754
           MOVE 1 TO ASC-TOP-OF-SCREEN.                                 09960054
           PERFORM 3310-MOVE-COMMAREA-TO-SCREEN.                        09970054
                                                                        09980054
           PERFORM 2500-SET-SCREEN-COLORS.                              09990054
                                                                        10010054
       EJECT                                                            10020054
                                                                        10040054
       2100-PROCESS-LIST-TSQ.                                           10050054
      ******************************************************************10060054
      *    READ THE LIST TEMP STORAGE QUEUE FROM LIST PROGRAM.          10080054
      *    FILL IN THE KEYS IN THE APPL. COMM AREA                      10090054
      *                                                                 10100054
      *    PERFORMED BY : 2000-BUILD-INITIAL-PANEL                      10110054
      ******************************************************************10120054
                                                                        10130054
           MOVE +1 TO ASC-LIST-TSQ-ITEM-NBR.                            10140054
                                                                        10150054
           PERFORM 9500-READ-LIST-TSQ.                                  10160054
                                                                        10170054
           SET MB003-IDX TO 1.                                          10180054
           SET ASC-LIST-TSQ-IDX TO MB003-IDX.                           10190054
           MOVE MB003-MAX-ITEMS TO ASC-LIST-TSQ-MAX-ITEMS.              10200054
                                                                        10210054
           MOVE MB003-GMA-NUMBER(MB003-IDX)  TO ASC-GMA-NO.             10220054
           MOVE MB003-DMA-NUMBER(MB003-IDX)  TO ASC-DMA-NO.             10230054
           MOVE MB003-DEPT-NUMBER(MB003-IDX) TO ASC-DPT-NO.             10240054
                                                                        10250054
       EJECT                                                            10260054
                                                                        10270054
                                                                        10280054
       2200-DISPLAY-PANEL.                                              10290054
                                                                        10300054
      ******************************************************************10310054
      *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.       10320054
      *                                                                 10330054
      ******************************************************************10340054
                                                                        10350054
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                       10360054
                                                                        10370054
           PERFORM 4060-MASK-END-DATES.                                 10380054
                                                                        10390054
           PERFORM DP002-0000-FORMAT-TITLE-LINES.                       10400054
           PERFORM 3310-MOVE-COMMAREA-TO-SCREEN.                        10410054
                                                                        10420054
                                                                        10430054
           PERFORM DP005-0000-FORMAT-MSG-LINES.                         10440054
                                                                        10450054
           IF (DP020-SRC-AID = DP016-PA2) OR                            10460054
              (PS-SEND-MAP-N-DATA-FRSET)                                10470054
               PERFORM DP009-0000-LOAD-FK-LINES                         10480054
           END-IF.                                                      10490054
                                                                        10500054
           IF PS-NO-ERROR                                               10510054
              IF MR-LEVEL-A(1) = DP015-UNP-ALP-NOR-OFF                  10520054
                 MOVE -1 TO MR-LEVEL-L(1)                               10530054
              ELSE                                                      10540054
                IF MR-STRT-MM-A(1) = DP015-UNP-ALP-NOR-OFF              10550054
                    MOVE -1 TO MR-STRT-MM-L(1)                          10560054
                ELSE                                                    10570054
                    MOVE -1 TO MR-END-MM-L(1)                           10580054
                END-IF                                                  10590054
              END-IF                                                    10600054
           END-IF.                                                      10610054
                                                                        10620054
           PERFORM DP008-0000-WRITE-PANEL.                              10630054
           PERFORM 2600-SAVE-CURR-TSQ.                                  10640054
                                                                        10641054
       EJECT                                                            10650054
                                                                        10660054
                                                                        10670054
                                                                        10680054
       2300-DETERMINE-LEVEL-UPDATE.                                     10690054
      ******************************************************************10700054
      * SET LEVEL UPDATE FLAGS TO DETERMINE WHICH LEVEL IS BEING        10710054
      * UPDATED. THIS WILL BE USED IN SUBSEQUENT PARAGRAPHS.            10720054
      *                                                                 10730054
      * PERFORMED BY : 2000-BUILD-INITIAL-PANEL                         10740054
      ******************************************************************10750054
           IF ASC-DMA-NO = '00'                                         10760054
              SET ASC-UPD-GMA TO TRUE                                   10770054
              MOVE PC-GMA-LEVEL-CD TO ASC-ALPHA-MDSEAR-LEVEL            10780054
           ELSE                                                         10790054
              IF ASC-DPT-NO = '000'                                     10800054
                 SET ASC-UPD-DMA TO TRUE                                10810054
                 MOVE PC-DMA-LEVEL-CD TO ASC-ALPHA-MDSEAR-LEVEL         10820054
              ELSE                                                      10830054
                  SET ASC-UPD-DPT  TO TRUE                              10840054
                  MOVE PC-DPT-LEVEL-CD TO ASC-ALPHA-MDSEAR-LEVEL        10850054
              END-IF                                                    10860054
           END-IF.                                                      10870054
                                                                        10880054
       2400-RETRIEVE-DATA-FOR-PANEL.                                    10890054
      ******************************************************************10900054
      * GET THE MERCH. AREA DESCRIPTIONS OF EACH MERCHANDISE LEVEL      10910054
      * (GMA, DMA AND DPT).                                             10920054
      * GET THE PARENT MERCHANDISE AREA DKEY, AND PARENT START AND END  10930054
      * DATES.                                                          10940054
      ******************************************************************10950054
                                                                        10960054
                                                                        10970054
           PERFORM 2401-GET-LEVEL-DESCRIPTIONS                          10980054
              VARYING PV-LEVEL-INDEX                                    10990054
              FROM 1 BY 1                                               11000054
              UNTIL PV-LEVEL-INDEX > ASC-LEVEL-TO-UPDATE.               11010054
                                                                        11020054
           MOVE MDSEAR-DKEY TO ASC-MDSEAR-DKEY.                         11030054
           MOVE MDSEAR-STRT-DTE TO PV-CHANGE-DATE.                      11040054
           MOVE PV-CHANGE-CENT  TO ASC-LEVEL-START-CC.                  11050054
           MOVE PV-CHANGE-YY    TO ASC-LEVEL-START-YY.                  11060054
           MOVE PV-CHANGE-MM    TO ASC-LEVEL-START-MM.                  11070054
           MOVE PV-CHANGE-DD    TO ASC-LEVEL-START-DD.                  11080054
                                                                        11090054
           MOVE MDSEAR-END-DTE TO PV-CHANGE-DATE.                       11100054
           MOVE PV-CHANGE-CENT  TO ASC-LEVEL-END-CC.                    11110054
           MOVE PV-CHANGE-YY    TO ASC-LEVEL-END-YY.                    11120054
           MOVE PV-CHANGE-MM    TO ASC-LEVEL-END-MM.                    11130054
           MOVE PV-CHANGE-DD    TO ASC-LEVEL-END-DD.                    11140054
                                                                        11150054
                                                                        11160054
           PERFORM 2403-RETRIEVE-DATA-FOR-TABLE.                        11170054
           EJECT                                                        11180054
                                                                        11190054
                                                                        11200054
       2401-GET-LEVEL-DESCRIPTIONS.                                     11210054
      ******************************************************************11220054
      *  GET DESCRIPTIONS FOR MERCHANDISE AREA NUMBERS THAT             11230054
      *  WILL BE DISPLAYED ON THE SCREEN.                               11240054
      *                                                                 11250054
      *  PERFORMED BY :2400-RETRIEVE-DATA-FROM-PANEL                    11260054
      ******************************************************************11270054
           EVALUATE TRUE                                                11280054
                                                                        11290054
                WHEN PV-INDEX-GMA-LEVEL                                 11300054
                     MOVE ASC-COMPANY-NO TO DK-OPERCO-NBR               11310054
                     MOVE ASC-GMA-NO        TO DK-GMA-NBR               11320054
                     MOVE '000'             TO DK-DMA-NBR DK-DEPT-NBR   11330054
                                               DK-MAJ-CL-NBR            11340054
                                               DK-SUB-CL-NBR            11350054
                     MOVE 'GMA'             TO DK-MDSELV-CDE            11360054
                     PERFORM 9400-GET-MDSEAR-INFO                       11370054
                     MOVE MDSEAR-DESC       TO ASC-GMA-DESC             11380054
                                                                        11390054
                WHEN PV-INDEX-DMA-LEVEL                                 11400054
                     MOVE ASC-COMPANY-NO TO DK-OPERCO-NBR               11410054
                     MOVE ASC-GMA-NO        TO DK-GMA-NBR               11420054
                     MOVE ASC-DMA-NO        TO DK-DMA-NBR               11430054
                     MOVE '000'             TO DK-DEPT-NBR              11440054
                                               DK-MAJ-CL-NBR            11450054
                                               DK-SUB-CL-NBR            11460054
                     MOVE 'DMA'             TO DK-MDSELV-CDE            11470054
                     PERFORM 9400-GET-MDSEAR-INFO                       11480054
                     MOVE MDSEAR-DESC       TO ASC-DMA-DESC             11490054
                                                                        11500054
                WHEN PV-INDEX-DPT-LEVEL                                 11510054
                     MOVE ASC-COMPANY-NO TO DK-OPERCO-NBR               11520054
                     MOVE ASC-GMA-NO        TO DK-GMA-NBR               11530054
                     MOVE ASC-DMA-NO        TO DK-DMA-NBR               11540054
                     MOVE ASC-DPT-NO        TO DK-DEPT-NBR              11550054
                     MOVE '000'             TO DK-MAJ-CL-NBR            11560054
                                               DK-SUB-CL-NBR            11570054
                     MOVE 'DPT'             TO DK-MDSELV-CDE            11580054
                     PERFORM 9400-GET-MDSEAR-INFO                       11590054
                     MOVE MDSEAR-DESC       TO ASC-DPT-DESC             11600054
                                                                        11610054
           END-EVALUATE.                                                11620054
           EJECT                                                        11630054
                                                                        11640054
       2402-PROCESS-RSPBAR-DATA.                                        11650054
                                                                        11660054
      ******************************************************************11670054
      * 1) PROCESS THE RESPONSIBILITIES IN LEVEL/START DATE SEQUENCE    11680054
      * 2) BYPASS ALL HISTORY ROWS (BUT NOTE THE BYR,GMM AND DMM'S)     11690054
      * 3) MOVE ALL OTHER ROWS TO SCREEN TABLE IN COMMAREA              11700054
      * 4) SET HISTORY-FLAG IF NECESSARY (MEANING: THIS IS THE FIRST    11710054
      *    ACTIVE GMM, DMM OR BUYER ENCOUNTERED IN THE DATA, AND        11720054
      *    AN INACTIVE MATCHING LEVEL (PREDECESSOR) EXISTS.             11730054
      * 5) SET ORIGINAL-ROW FLAG TO TRUE (MEANING:ROW IN TABLE ALREADY  11740054
      *    EXISTS ON DATABASE.                                          11750054
      *                                                                 11760054
      * PERFORMED BY : 2400-RETRIEVE-DATA-FOR-PANEL                     11770054
      ******************************************************************11780054
                                                                        11790054
           IF RSPBAR-WORKER-END-DTE <  DP017-EDITED-DATE-YYYYMMDD       11800054
                                                                        11810054
              IF (RSPBAR-RSPLVL-CDE = PC-GMM-LEVEL) OR                  11820054
                 (RSPBAR-RSPLVL-CDE = PC-DMM-LEVEL) OR                  11830054
                 (RSPBAR-RSPLVL-CDE = PC-BYR-LEVEL)                     11840054
                 MOVE RSPBAR-RSPLVL-CDE TO PV-HIST-LEVEL                11850054
              END-IF                                                    11860054
           ELSE                                                         11870054
              ADD 1 TO ASC-LINES-ENTERED                                11880054
              IF ASC-LINES-ENTERED > PC-TOTAL-DATA-LINES                11890054
                 SET DP013-LOGIC-ABEND TO TRUE                          11900054
                 SET DP013-ROLLBACK TO TRUE                             11910054
                                                                        11920054
                 MOVE '2402-PROCESS-RSPBAR-DATA' TO                     11930054
                        DP013-PARAGRAPH                                 11940054
                 MOVE 'TOO MANY RSPBARS TO DISPLAY' TO                  11950054
                           DP013-MESSAGE-TEXT(1)                        11960054
                 MOVE 'BLEW TABLE SIZE IN COMMAREA ' TO                 11970054
                           DP013-MESSAGE-TEXT(2)                        11980054
                                                                        11990054
                 PERFORM DP013-0000-PROCESS-ABEND                       12000054
              END-IF                                                    12010054
                                                                        12020054
      ***************************************************************** 12030054
      * IF THIS IS THE FIRST BUYER, GMM, OR DMM THAT IS ACTIVE, AND     12040054
      * THERE WAS A PERSON ON HIS LEVEL THAT COVERED THE BEGINNING      12050054
      * OF THE MERCHANDISE AREA, BUT IS NO LONGER ACTIVE,               12060054
      * (HISTORY) THEN TURN ON THE HISTORY FLAG.                        12070054
      ***************************************************************** 12080054
                                                                        12090054
              IF (RSPBAR-RSPLVL-CDE = PV-HIST-LEVEL) AND                12100054
                 (RSPBAR-WORKER-STRT-DTE <=                             12110054
                            DP017-EDITED-DATE-YYYYMMDD)                 12120054
                 SET CURR-HISTORY-EXISTS(CURR-INDEX) TO TRUE            12130054
                 INITIALIZE PV-HIST-LEVEL                               12140054
              ELSE                                                      12150054
                 MOVE SPACES TO PV-HIST-LEVEL                           12160054
              END-IF                                                    12170054
      ***************************************************************** 12180054
      * THE USER SHOULD NOT BE ALLOWED TO CHANGE THE START DATE IF      12190054
      * THE DATE IS LESS THAN OR = TO CURRENT DATE. IF CURRENT DATE     12200054
      * IS NOT INCLUDED IN THIS TEST, BYR, GMM, OR DMM'S WITH HISTORY   12210054
      * THAT HAVE A START DATE = TODAY WILL BE ABLE TO BE CHANGED EVEN  12220054
      * THO THE HISTORY START DATE WOULD NOT BE INCLUDED IN THE COVERAGE12230054
      * TESTING.                                                        12240054
      ***************************************************************** 12250054
                                                                        12260054
              IF (RSPBAR-WORKER-STRT-DTE <= DP017-EDITED-DATE-YYYYMMDD) 12270054
                 SET CURR-PROTECT-START-DATE(CURR-INDEX) TO TRUE        12280054
              ELSE                                                      12290054
                 SET CURR-NO-PROTECT-START-DATE(CURR-INDEX) TO TRUE     12300054
              END-IF                                                    12310054
                                                                        12320054
              SET CURR-ORIG-RECORD(CURR-INDEX) TO TRUE                  12330054
              MOVE RSPBAR-RSPLVL-CDE TO CURR-LEVEL(CURR-INDEX)          12340054
              MOVE RSPBAR-WORKER-STRT-DTE  TO PV-CHANGE-DATE            12350054
              MOVE PV-CHANGE-CENT    TO CURR-START-CYMD-CC(CURR-INDEX)  12360054
              MOVE PV-CHANGE-YY      TO CURR-START-YY(CURR-INDEX)       12370054
              MOVE PV-CHANGE-MM      TO CURR-START-MM(CURR-INDEX)       12380054
              MOVE PV-CHANGE-DD      TO CURR-START-DD(CURR-INDEX)       12390054
              MOVE CURR-START-DATE-CYMD-N(CURR-INDEX) TO                12400054
                                CURR-ORIG-START-DATE(CURR-INDEX)        12410054
              MOVE RSPBAR-WORKER-END-DTE  TO PV-CHANGE-DATE             12420054
              MOVE PV-CHANGE-CENT    TO CURR-END-CYMD-CC(CURR-INDEX)    12430054
              MOVE PV-CHANGE-YY      TO CURR-END-YY(CURR-INDEX)         12440054
              MOVE PV-CHANGE-MM      TO CURR-END-MM(CURR-INDEX)         12450054
              MOVE PV-CHANGE-DD      TO CURR-END-DD(CURR-INDEX)         12460054
              MOVE RSPBAR-WORKER-NBR TO DK-WORKER-NBR                   12470054
              PERFORM 9010-GET-WORKER-INFO                              12480054
              MOVE WORKER-LAST-NAME  TO CURR-LAST-NAME(CURR-INDEX)      12490054
              MOVE WORKER-FIR-NAME   TO CURR-FIRST-NAME(CURR-INDEX)     12500054
              MOVE WORKER-UID-NBR    TO CURR-USERID(CURR-INDEX)         12510054
                                                                        12530154
              SET CURR-INDEX UP BY 1                                    12540254
                                                                        12540354
           END-IF.                                                      12541054
           PERFORM 9006-FETCH-RSPBAR-ROW.                               12550054
           EJECT                                                        12560054
                                                                        12570054
       2403-RETRIEVE-DATA-FOR-TABLE.                                    12580054
      ******************************************************************12590054
      *  GET THE RESPONSIBILITY AREA DATA FOR THE SCREEN.               12600054
      *  READ THE DATA IN RESPONSIBILITY LEVEL/START DATE ORDER.        12610054
      ******************************************************************12620054
                                                                        12630054
           INITIALIZE PV-CURR-TSQ-AREA.                                 12638054
                                                                        12639354
           MOVE ASC-COMPANY-NO    TO DK-OPERCO-NBR                      12640054
           MOVE ASC-GMA-NO        TO DK-GMA-NBR                         12650054
           MOVE ASC-DMA-NO        TO DK-DMA-NBR.                        12660054
           MOVE ASC-DPT-NO        TO DK-DEPT-NBR.                       12670054
           MOVE '000'             TO DK-MAJ-CL-NBR                      12680054
                                     DK-SUB-CL-NBR.                     12690054
           MOVE ASC-ALPHA-MDSEAR-LEVEL TO DK-MDSELV-CDE.                12700054
                                                                        12710054
           PERFORM 9005-OPEN-RSPBAR-CURSOR.                             12720054
                                                                        12730054
           SET CURR-INDEX TO 1.                                         12740054
           SET PS-ROW-FOUND TO TRUE.                                    12750054
           INITIALIZE ASC-LINES-ENTERED.                                12760054
                                                                        12770054
           PERFORM 9006-FETCH-RSPBAR-ROW.                               12780054
                                                                        12790054
           PERFORM 2402-PROCESS-RSPBAR-DATA                             12800054
                 UNTIL PS-ROW-NOT-FOUND.                                12810054
                                                                        12820054
           PERFORM 9007-CLOSE-RSPBAR-CURSOR.                            12830054
                                                                        12840054
                                                                        13040054
       2500-SET-SCREEN-COLORS.                                          13050054
      ******************************************************************13070054
      * SET ATTRIBUTES ON SCREEN FIELDS.                                13080054
      *                                                                 13090054
      * PERFORMED BY : 2000-BUILD-INITIAL-PANEL.                        13100054
      ******************************************************************13110054
                                                                        13120054
      ***                                                               13130054
      *  SET DESCRIPTION FOR LEVELS ABOVE LEVEL AND INCLUDING LEVEL     13140054
      *  TO BE UPDATED TO BLUE AUTOSKIP                                 13150054
      ***                                                               13160054
                                                                        13170054
           INITIALIZE PV-LEVEL-INDEX.                                   13180054
                                                                        13190054
      ***                                                               13200054
      *  SET LEVELS BELOW LEVEL TO BE UPDATED TO DARK - PROTECTED       13210054
      ***                                                               13220054
                                                                        13230054
           COMPUTE PV-LEVEL-INDEX = ASC-LEVEL-TO-UPDATE + 1             13240054
           IF PV-LEVEL-INDEX < 4                                        13250054
               PERFORM 2502-SET-LEVEL-TO-DARK                           13260054
                  VARYING PV-LEVEL-INDEX FROM PV-LEVEL-INDEX BY 1       13270054
                  UNTIL (PV-LEVEL-INDEX > 3)                            13280054
           END-IF.                                                      13290054
                                                                        13300054
           SET PS-MORE-RECORDS TO TRUE.                                 13310054
           INITIALIZE PV-MAP-SUB.                                       13320054
           PERFORM 2503-SET-TABLE-ATTRIBUTES                            13330054
                VARYING CURR-INDEX FROM 1 BY 1                          13340054
                UNTIL (PS-NO-MORE-RECORDS OR                            13350054
                       CURR-INDEX > PC-MAX-SCREEN-LINES).               13360054
                                                                        13370054
       2501-SET-LEVEL-TO-BLUE.                                          13380054
      ******************************************************************13400054
      * SET ATTRIBUTES ON SCREEN FIELDS.                                13410054
      *                                                                 13420054
      * PERFORMED BY : 2500-SET-SCREEN-COLORS.                          13430054
      ******************************************************************13440054
           EVALUATE TRUE                                                13450054
                WHEN PV-INDEX-GMA-LEVEL                                 13460054
                     MOVE DP015-BLUE        TO AGMAC  AGMADESC          13470054
                     MOVE DP015-HL-OFF      TO AGMAH  AGMADESH          13480054
                     MOVE DP015-ASK-NOR-OFF TO AGMAA  AGMADESA          13490054
                WHEN PV-INDEX-DMA-LEVEL                                 13500054
                     MOVE DP015-BLUE        TO ADMAC  ADMADESC          13510054
                     MOVE DP015-HL-OFF      TO ADMAH  ADMADESH          13520054
                     MOVE DP015-ASK-NOR-OFF TO ADMAA  ADMADESA          13530054
                WHEN PV-INDEX-DPT-LEVEL                                 13540054
                     MOVE DP015-BLUE        TO ADPTC ADPTDESC           13550054
                     MOVE DP015-HL-OFF      TO ADPTH ADPTDESH           13560054
                     MOVE DP015-ASK-NOR-OFF TO ADPTA ADPTDESA           13570054
           END-EVALUATE.                                                13580054
           EJECT                                                        13590054
                                                                        13600054
       2502-SET-LEVEL-TO-DARK.                                          13610054
      ******************************************************************13620054
      *                                                                 13630054
      *                                                                 13640054
      *                                                                 13650054
      ******************************************************************13660054
                                                                        13670054
           EVALUATE TRUE                                                13680054
                WHEN PV-INDEX-GMA-LEVEL                                 13690054
                     MOVE DP015-HL-OFF      TO AGMAH  AGMADESH          13700054
                     MOVE DP015-PRO-DRK-OFF TO AGMAA  AGMADESA          13710054
                WHEN PV-INDEX-DMA-LEVEL                                 13720054
                     MOVE DP015-HL-OFF      TO ADMAH  ADMADESH ADMAHDRH 13730054
                     MOVE DP015-PRO-DRK-OFF TO ADMAA  ADMADESA ADMAHDRA 13740054
                WHEN PV-INDEX-DPT-LEVEL                                 13750054
                     MOVE DP015-HL-OFF      TO ADPTH ADPTDESH ADPTHDRH  13760054
                     MOVE DP015-PRO-DRK-OFF TO ADPTA ADPTDESA ADPTHDRA  13770054
           END-EVALUATE.                                                13780054
                                                                        13790054
       2503-SET-TABLE-ATTRIBUTES.                                       13810054
      ******************************************************************13820054
      *  SET THE ATTRIBUTES TO THE APPROPRIATE SETTINGS                 13830054
      *  FOR USERID, START-DATE AND RESPONSIBILITY LEVEL FIELDS.        13840054
      *                                                                 13850054
      *  PERFORMED BY : 2500-SET-SCREEN-COLORS.                         13860054
      ******************************************************************13870054
                                                                        13880054
           ADD  +1 TO PV-MAP-SUB                                        13890054
                                                                        13900054
           IF CURR-LEVEL(CURR-INDEX) NOT = SPACES                       13910054
               IF CURR-ORIG-RECORD(CURR-INDEX)                          13920054
                  MOVE DP015-BLUE        TO MR-LEVEL-C(PV-MAP-SUB)      13930054
                                            MR-WORKER-C(PV-MAP-SUB)     13940054
                  MOVE DP015-HL-OFF      TO MR-LEVEL-H(PV-MAP-SUB)      13950054
                                            MR-WORKER-H(PV-MAP-SUB)     13960054
                  MOVE DP015-ASK-NOR-OFF TO MR-LEVEL-A(PV-MAP-SUB)      13970054
                                            MR-WORKER-A(PV-MAP-SUB)     13980054
                  IF CURR-PROTECT-START-DATE(CURR-INDEX)                13990054
                      MOVE DP015-BLUE        TO MR-STRT-MM-C(PV-MAP-SUB)14000054
                                                MR-STRT-DD-C(PV-MAP-SUB)14010054
                                                MR-STRT-YY-C(PV-MAP-SUB)14020054
                      MOVE DP015-HL-OFF      TO MR-STRT-MM-H(PV-MAP-SUB)14030054
                                                MR-STRT-DD-H(PV-MAP-SUB)14040054
                                                MR-STRT-YY-H(PV-MAP-SUB)14050054
                      MOVE DP015-ASK-NOR-OFF TO MR-STRT-MM-A(PV-MAP-SUB)14060054
                                                MR-STRT-DD-A(PV-MAP-SUB)14070054
                                                MR-STRT-YY-A(PV-MAP-SUB)14080054
                  END-IF                                                14090054
               END-IF                                                   14100054
           ELSE                                                         14110054
               SET PS-NO-MORE-RECORDS TO TRUE                           14120054
           END-IF.                                                      14130054
           EJECT                                                        14140054
                                                                        14140154
       2600-SAVE-CURR-TSQ.                                              14141054
      ******************************************************************14141154
      * WRITE THE DATA THAT IS DISPLAYED ON THE SCREENS OUT TO A        14141354
      * CURRENT TEMP STORAGE QUEUE.                                     14141454
      ******************************************************************14141654
                                                                        14141754
           IF ASC-CURR-TSQ-ITEM-NBR = +0                                14142054
               MOVE +1 TO ASC-CURR-TSQ-ITEM-NBR                         14142154
               PERFORM 9521-WRITE-CURR-TSQ                              14143054
           ELSE                                                         14144054
               PERFORM 9522-REWRITE-CURR-TSQ                            14145054
           END-IF.                                                      14146054
                                                                        14150054
                                                                        14160054
       3000-PROCESS-MAP.                                                14170054
                                                                        14180054
      ******************************************************************14190054
      *                                                                 14200054
      ******************************************************************14210054
                                                                        14220054
           SET PS-SEND-DATA-ONLY-FRSET TO TRUE.                         14230054
                                                                        14240054
           PERFORM DP007-0000-GET-PANEL.                                14250054
                                                                        14260054
                                                                        14270054
           IF (DP020-SRC-AID NOT = DP016-PF5)   AND                     14280054
              (DP020-SRC-AID NOT = DP016-CLEAR)                         14290054
               PERFORM 3200-MOVE-SCREEN-TO-COMMAREA                     14300054
           END-IF.                                                      14310054
                                                                        14320054
           PERFORM 3100-CHECK-LOCAL-FKEYS.                              14330054
       EJECT                                                            14340054
                                                                        14350054
       3100-CHECK-LOCAL-FKEYS.                                          14360054
                                                                        14370054
      ******************************************************************14380054
      *    TAKE THE APPROPRIATE COURSE OF ACTION BASED ON WHAT          14390054
      *    FUNCTION KEY THE USER HAS HIT.                               14400054
      *                                                                 14410054
      *    MODIFY THE EVALUATE CLAUSE BELOW BY ADDING A WHEN  CLAUSE    14420054
      *    FOR EACH AID KEY YOU PLAN ON HANDLING WITHIN THIS PROGRAM.   14430054
      *    DO NOT MODIFY OR DELETE ANY OF THE EXISTING WHEN  CLAUSES.   14440054
      *                                                                 14450054
      ******************************************************************14460054
                                                                        14470054
           EVALUATE TRUE                                                14480054
                                                                        14490054
               WHEN (DP020-FK-GO-TO-TRAN(DP020-SRC-AID)  OR             14500054
                     DP020-FK-RETURN(DP020-SRC-AID))                    14510054
                   IF DP020-FK-RETURN(DP020-SRC-AID)                    14520054
                       PERFORM 9513-DELETE-ORIG-TSQ                     14530054
                       PERFORM 9523-DELETE-CURR-TSQ                     14531054
                   END-IF                                               14540054
                   PERFORM 4300-SETUP-INTER-APPL-COMM                   14550054
                   PERFORM DP015-1000-START-NAVIGATOR                   14560054
                                                                        14570054
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     14580054
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 14590054
                   INITIALIZE ANAVCMDO ANAVKEYO                         14600054
                              DP020-NAV-KEY-DATA                        14610054
                              DP020-NAV-MNEMONIC                        14620054
                   PERFORM 2000-BUILD-INITIAL-PANEL                     14630054
                   PERFORM 2200-DISPLAY-PANEL                           14640054
                                                                        14650054
                                                                        14660054
               WHEN DP020-SRC-AID = DP016-ENTER                         14670054
                                                                        14680054
                   IF (DP020-NAV-MNEMONIC = SPACES)     OR              14690054
                      (DP020-NAV-MNEMONIC = LOW-VALUES)                 14700054
                       PERFORM 3300-EDIT-DATA-IN-COMMAREA               14710054
                       PERFORM 2200-DISPLAY-PANEL                       14720054
                   ELSE                                                 14730054
                       PERFORM 2600-SAVE-CURR-TSQ                       14731054
                       PERFORM 4300-SETUP-INTER-APPL-COMM               14740054
                       PERFORM DP015-1000-START-NAVIGATOR               14750054
                   END-IF                                               14760054
                                                                        14770054
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   14780054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   14790054
                   IF PS-NO-ERROR                                       14800054
                      PERFORM 3150-PAGE-BACKWARD                        14810054
                   END-IF                                               14820054
                   PERFORM 2200-DISPLAY-PANEL                           14830054
                                                                        14840054
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   14850054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   14860054
                   IF PS-NO-ERROR                                       14870054
                      PERFORM 3175-PAGE-FORWARD                         14880054
                   END-IF                                               14890054
                   PERFORM 2200-DISPLAY-PANEL                           14900054
                                                                        14910054
               WHEN DP020-SRC-AID = DP016-PF15                          14920054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   14930054
                   IF PS-NO-ERROR                                       14940054
                      PERFORM 6000-PROCESS-MAINTAINANCE                 14950054
                   END-IF                                               14960054
                   PERFORM 2200-DISPLAY-PANEL                           14970054
                                                                        14980054
                                                                        14990054
               WHEN DP020-SRC-AID = DP016-PF20                          15000054
                   PERFORM 3250-PROCESS-SELECTION-ARRAY                 15010054
                   PERFORM 2200-DISPLAY-PANEL                           15020054
                                                                        15030054
                                                                        15040054
               WHEN DP020-SRC-AID = DP016-PA2                           15050054
                   SET PS-SEND-DATA-ONLY TO TRUE                        15060054
                                                                        15070054
                   MOVE -1 TO ANAVCMDL                                  15080054
                                                                        15090054
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                15100054
                   PERFORM DP009-0000-LOAD-FK-LINES                     15110054
                   PERFORM DP008-0000-WRITE-PANEL                       15120054
                   PERFORM 2600-SAVE-CURR-TSQ                           15121054
                                                                        15130054
               WHEN DP020-SRC-AID = DP016-CLEAR                         15140054
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 15150054
                                                                        15160054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   15170054
                   PERFORM 2200-DISPLAY-PANEL                           15180054
                                                                        15190054
               WHEN OTHER                                               15200054
      *            -- INVALID FUNCTION KEY --                           15210054
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                   15220054
                   MOVE PC-TSYMSG-00045  TO DP020-MSG-NUMBER            15230054
                   SET DP020-MSG-FATAL TO TRUE                          15240054
                   PERFORM 2200-DISPLAY-PANEL                           15250054
                                                                        15260054
           END-EVALUATE.                                                15270054
       EJECT                                                            15280054
                                                                        15290054
       3150-PAGE-BACKWARD.                                              15300054
                                                                        15310054
      ******************************************************************15320054
      *  SET A NEW TOP AND BOTTOM OF SCREEN WHEN THE USER WANTS TO      15330054
      *  DISPLAY THE PREVIOUS SCREEN OF DATA. IF USER IS ALREADY ON     15340054
      *  FIRST-MOST SCREEN SEND ERROR MESSAGE 'AT TOP OF SCREEN'        15350054
      ******************************************************************15360054
                                                                        15370054
           COMPUTE PV-TENTATIVE-ITEM-AT-TOP = ASC-TOP-OF-SCREEN -       15380054
              PC-MAX-SCREEN-LINES.                                      15390054
           IF PV-TENTATIVE-ITEM-AT-TOP > ZERO                           15400054
              COMPUTE ASC-TOP-OF-SCREEN = ASC-TOP-OF-SCREEN -           15410054
                      PC-MAX-SCREEN-LINES                               15420054
              PERFORM 4001-SET-UP-SCREEN                                15430054
           ELSE                                                         15440054
              MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                  15450054
              MOVE -1 TO ANAVCMDL                                       15460054
              SET DP020-MSG-INFORMATIONAL  TO TRUE                      15470054
           END-IF.                                                      15480054
                                                                        15490054
       3175-PAGE-FORWARD.                                               15500054
                                                                        15510054
      ******************************************************************15520054
      *  ALLOW USER TO PAGE FORWARD ONE SCREEN OF DATA, ONLY IF CURRENT 15530054
      *  SCREEN IS FILLED UP.                                           15540054
      ******************************************************************15550054
                                                                        15560054
           COMPUTE PV-TENTATIVE-ITEM-AT-TOP = ASC-TOP-OF-SCREEN +       15570054
              PC-MAX-SCREEN-LINES.                                      15580054
                                                                        15590054
           IF (PV-TENTATIVE-ITEM-AT-TOP > ASC-LINES-ENTERED + 1)        15600054
           OR ( PV-TENTATIVE-ITEM-AT-TOP > PC-TOTAL-DATA-LINES)         15610054
      *     --- ALREADY AT END OF LIST ---   *                          15620054
              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                  15630054
              MOVE -1 TO ANAVCMDL                                       15640054
              SET DP020-MSG-INFORMATIONAL TO TRUE                       15650054
           ELSE                                                         15660054
               MOVE PV-TENTATIVE-ITEM-AT-TOP TO ASC-TOP-OF-SCREEN       15670054
               PERFORM 4001-SET-UP-SCREEN                               15680054
           END-IF.                                                      15690054
           EJECT                                                        15700054
                                                                        15710054
       3200-MOVE-SCREEN-TO-COMMAREA.                                    15720054
                                                                        15730054
      ******************************************************************15740054
      *                                                                 15750054
      ******************************************************************15760054
                                                                        15770054
           SET CURR-INDEX TO ASC-TOP-OF-SCREEN.                         15780054
                                                                        15790054
           PERFORM VARYING PV-MAP-SUB                                   15800054
              FROM 1 BY 1                                               15810054
              UNTIL (PV-MAP-SUB > PC-MAX-SCREEN-LINES)                  15820054
                                                                        15830054
               IF MR-LEVEL-L(PV-MAP-SUB) > 0                            15840054
                  MOVE MR-LEVEL(PV-MAP-SUB)   TO CURR-LEVEL(CURR-INDEX) 15850054
                  SET CURR-LEVEL-CHANGED(CURR-INDEX) TO TRUE            15860054
               ELSE                                                     15870054
                  IF MR-LEVEL-A(PV-MAP-SUB) = DP015-ERASE-EOF           15880054
                     MOVE SPACE TO CURR-LEVEL(CURR-INDEX)               15890054
                     SET CURR-LEVEL-CHANGED(CURR-INDEX) TO TRUE         15900054
                  END-IF                                                15910054
               END-IF                                                   15920054
      *                                                                 15930054
      *                                                                 15940054
               IF MR-STRT-MM-L(PV-MAP-SUB) > 0                          15950054
                  MOVE MR-STRT-MM(PV-MAP-SUB) TO                        15960054
                       CURR-START-MM(CURR-INDEX)                        15970054
                  SET CURR-START-CHANGED(CURR-INDEX) TO TRUE            15980054
               ELSE                                                     15990054
                  IF MR-STRT-MM-A(PV-MAP-SUB) = DP015-ERASE-EOF         16000054
                     MOVE SPACE TO CURR-START-MM(CURR-INDEX)            16010054
                     SET CURR-START-CHANGED(CURR-INDEX) TO TRUE         16020054
                  END-IF                                                16030054
               END-IF                                                   16040054
                                                                        16050054
               IF MR-STRT-DD-L(PV-MAP-SUB) > 0                          16060054
                  MOVE MR-STRT-DD(PV-MAP-SUB) TO                        16070054
                       CURR-START-DD(CURR-INDEX)                        16080054
                  SET CURR-START-CHANGED(CURR-INDEX) TO TRUE            16090054
               ELSE                                                     16100054
                  IF MR-STRT-DD-A(PV-MAP-SUB) = DP015-ERASE-EOF         16110054
                     MOVE SPACE TO CURR-START-DD(CURR-INDEX)            16120054
                     SET CURR-START-CHANGED(CURR-INDEX) TO TRUE         16130054
                  END-IF                                                16140054
               END-IF                                                   16150054
                                                                        16160054
               IF MR-STRT-YY-L(PV-MAP-SUB) > 0                          16170054
                  MOVE MR-STRT-YY(PV-MAP-SUB) TO                        16180054
                       CURR-START-YY(CURR-INDEX)                        16190054
                  SET CURR-START-CHANGED(CURR-INDEX) TO TRUE            16200054
               ELSE                                                     16210054
                  IF MR-STRT-YY-A(PV-MAP-SUB) = DP015-ERASE-EOF         16220054
                     MOVE SPACE TO CURR-START-YY(CURR-INDEX)            16230054
                     SET CURR-START-CHANGED(CURR-INDEX) TO TRUE         16240054
                  END-IF                                                16250054
               END-IF                                                   16260054
      *                                                                 16270054
      *                                                                 16280054
               IF MR-END-MM-L(PV-MAP-SUB) > 0                           16290054
                  MOVE MR-END-MM(PV-MAP-SUB) TO CURR-END-MM(CURR-INDEX) 16300054
                  SET CURR-END-CHANGED(CURR-INDEX) TO TRUE              16310054
               ELSE                                                     16320054
                  IF MR-END-MM-A(PV-MAP-SUB) = DP015-ERASE-EOF          16330054
                     MOVE SPACE TO CURR-END-MM(CURR-INDEX)              16340054
                     SET CURR-END-CHANGED(CURR-INDEX) TO TRUE           16350054
                  END-IF                                                16360054
               END-IF                                                   16370054
                                                                        16380054
               IF MR-END-DD-L(PV-MAP-SUB) > 0                           16390054
                  MOVE MR-END-DD(PV-MAP-SUB) TO CURR-END-DD(CURR-INDEX) 16400054
                  SET CURR-END-CHANGED(CURR-INDEX) TO TRUE              16410054
               ELSE                                                     16420054
                  IF MR-END-DD-A(PV-MAP-SUB) = DP015-ERASE-EOF          16430054
                     MOVE SPACE TO CURR-END-DD(CURR-INDEX)              16440054
                     SET CURR-END-CHANGED(CURR-INDEX) TO TRUE           16450054
                  END-IF                                                16460054
               END-IF                                                   16470054
                                                                        16480054
               IF MR-END-YY-L(PV-MAP-SUB) > 0                           16490054
                  MOVE MR-END-YY(PV-MAP-SUB) TO CURR-END-YY(CURR-INDEX) 16500054
                  SET CURR-END-CHANGED(CURR-INDEX) TO TRUE              16510054
               ELSE                                                     16520054
                  IF MR-END-YY-A(PV-MAP-SUB) = DP015-ERASE-EOF          16530054
                     MOVE SPACE TO CURR-END-YY(CURR-INDEX)              16540054
                     SET CURR-END-CHANGED(CURR-INDEX) TO TRUE           16550054
                  END-IF                                                16560054
               END-IF                                                   16570054
      *                                                                 16580054
      *                                                                 16590054
               IF MR-WORKER-L(PV-MAP-SUB) > 0                           16600054
                  MOVE MR-WORKER(PV-MAP-SUB) TO                         16610054
                       CURR-USERID(CURR-INDEX)                          16620054
                  SET CURR-WORKER-CHANGED(CURR-INDEX) TO TRUE           16630054
               ELSE                                                     16640054
                  IF MR-WORKER-A(PV-MAP-SUB) = DP015-ERASE-EOF          16650054
                     MOVE SPACE TO CURR-USERID(CURR-INDEX)              16660054
                     SET CURR-WORKER-CHANGED(CURR-INDEX) TO TRUE        16670054
                  END-IF                                                16680054
               END-IF                                                   16690054
      *                                                                 16700054
      *                                                                 16710054
               IF MR-LNAME-L(PV-MAP-SUB) > 0                            16720054
                  MOVE MR-LNAME(PV-MAP-SUB) TO                          16730054
                       CURR-LAST-NAME(CURR-INDEX)                       16740054
               ELSE                                                     16750054
                  IF MR-LNAME-A(PV-MAP-SUB) = DP015-ERASE-EOF           16760054
                     MOVE SPACE TO CURR-LAST-NAME(CURR-INDEX)           16770054
                  END-IF                                                16780054
               END-IF                                                   16790054
      *                                                                 16800054
      *                                                                 16810054
               IF MR-FNAME-L(PV-MAP-SUB) > 0                            16820054
                  MOVE MR-FNAME(PV-MAP-SUB) TO                          16830054
                       CURR-FIRST-NAME(CURR-INDEX)                      16840054
               ELSE                                                     16850054
                  IF MR-FNAME-A(PV-MAP-SUB) = DP015-ERASE-EOF           16860054
                     MOVE SPACE TO CURR-FIRST-NAME(CURR-INDEX)          16870054
                  END-IF                                                16880054
               END-IF                                                   16890054
      *                                                                 16900054
              SET CURR-INDEX UP BY 1                                    16910054
                                                                        16920054
           END-PERFORM.                                                 16930054
                                                                        16940054
       EJECT                                                            16950054
                                                                        16960054
       3250-PROCESS-SELECTION-ARRAY.                                    16970054
      ******************************************************************16980054
      *                                                                 16990054
      *    READ THE LIST TEMP STORAGE QUEUE FROM LIST PROGRAM.          17000054
      *    GET THE NEXT OCCURRENCE OF THE MERCHANDISE AREA KEY          17010054
      *    IN THE TSQ RECORD. IF THE KEY IS NOT SPACES FILL IN          17020054
      *    THE NEW KEY IN THE COMMAREA.                                 17030054
      *                                                                 17040054
      *    PERFORMED BY : 2000-BUILD-INITIAL-PANEL                      17050054
      ******************************************************************17060054
                                                                        17070054
                                                                        17080054
           PERFORM 9500-READ-LIST-TSQ.                                  17090054
                                                                        17100054
           ADD +1 TO ASC-LIST-TSQ-IDX.                                  17110054
           IF ASC-LIST-TSQ-IDX > MB003-MAX-ITEMS                        17120054
              ADD 1 TO ASC-LIST-TSQ-ITEM-NBR                            17130054
              PERFORM 9500-READ-LIST-TSQ                                17140054
              SET MB003-IDX TO 1                                        17150054
           ELSE                                                         17160054
              SET MB003-IDX TO ASC-LIST-TSQ-IDX                         17170054
           END-IF.                                                      17180054
                                                                        17190054
           IF MB003-KEY-DATA(MB003-IDX) = SPACES                        17200054
              SET MB003-IDX DOWN BY 1                                   17210054
              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                 17220054
              SET DP020-MSG-INFORMATIONAL TO TRUE                       17230054
           ELSE                                                         17240054
              MOVE MB003-GMA-NUMBER(MB003-IDX)  TO ASC-GMA-NO           17250054
              MOVE MB003-DMA-NUMBER(MB003-IDX)  TO ASC-DMA-NO           17260054
              MOVE MB003-DEPT-NUMBER(MB003-IDX) TO ASC-DPT-NO           17270054
              PERFORM 2000-BUILD-INITIAL-PANEL                          17280054
           END-IF.                                                      17290054
                                                                        17300054
       EJECT                                                            17310054
       3300-EDIT-DATA-IN-COMMAREA.                                      17320054
                                                                        17330054
      ******************************************************************17340054
      *  WILL DO GENERAL EDITS FOR THE DATA DISPLAYED ON SCREEN ONLY    17350054
      *                                                                 17360054
      *                                                                 17370054
      *                                                                 17380054
      ******************************************************************17390054
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                       17400054
                                                                        17410054
           SET PS-NO-ERROR TO TRUE.                                     17420054
                                                                        17430054
           MOVE +1 TO PV-FILL-SUB.                                      17440054
                                                                        17450054
           PERFORM 4000-PROCESS-SCREEN.                                 17460054
                                                                        17470054
           PERFORM 4001-SET-UP-SCREEN.                                  17480054
                                                                        17490054
           IF ASC-BOT-OF-SCREEN < ASC-LINES-ENTERED                     17500054
              MOVE ASC-BOT-OF-SCREEN TO PV-TEMP-SUB                     17510054
           ELSE                                                         17520054
              MOVE ASC-LINES-ENTERED  TO PV-TEMP-SUB                    17530054
           END-IF.                                                      17540054
                                                                        17550054
           PERFORM                                                      17560054
              VARYING CURR-INDEX FROM                                   17570054
              PV-TEMP-SUB BY -1                                         17580054
              UNTIL CURR-INDEX < ASC-TOP-OF-SCREEN                      17590054
                                                                        17600054
              IF CURR-WORKER-NOT-CHANGED(CURR-INDEX)                    17610054
                 CONTINUE                                               17620054
              ELSE                                                      17630054
                 PERFORM 4030-VALIDATE-WORKER-ID                        17640054
              END-IF                                                    17650054
                                                                        17660054
              SET PS-NO-END-DATE-ERROR TO TRUE                          17670054
              PERFORM 4022-EDIT-END-DATE                                17680054
                                                                        17690054
              SET PS-NO-START-DATE-ERROR TO TRUE                        17700054
              PERFORM 4021-EDIT-START-DATE                              17710054
                                                                        17720054
              IF (PS-NO-START-DATE-ERROR) AND                           17730054
                 (PS-NO-END-DATE-ERROR)                                 17740054
                  PERFORM 4025-COMPARE-DATES                            17750054
              END-IF                                                    17760054
                                                                        17770054
              IF CURR-LEVEL-NOT-CHANGED(CURR-INDEX)                     17780054
                 CONTINUE                                               17790054
              ELSE                                                      17800054
                 PERFORM 4010-EDIT-RESP-LEVEL                           17810054
              END-IF                                                    17820054
                                                                        17830054
              IF CURR-USERID(CURR-INDEX) NOT = SPACES                   17840054
                 PERFORM 4050-CHECK-DUPLICATE-ENTRIES                   17850054
              END-IF                                                    17860054
                                                                        17870054
              COMPUTE PV-TEMP-SUB = PV-TEMP-SUB - 1                     17880054
                                                                        17890054
           END-PERFORM.                                                 17900054
                                                                        17910054
           PERFORM 4040-EDIT-RESPON-ASSIGNMENTS.                        17920054
                                                                        17930054
                                                                        17940054
       EJECT                                                            17950054
                                                                        17960054
                                                                        17970054
       3301-MOVE-SCREEN-LINE.                                           17980054
                                                                        17990054
      ******************************************************************18000054
      *   ITERATIVELY MOVE EACH LINE OF DATA FROM COMMAREA TO SCREEN.   18010054
      *   PERFORMED BY : 3310-MOVE-COMMAREA-TO-SCREEN                   18020054
      ******************************************************************18030054
                                                                        18040054
           SET CURR-INDEX TO ASC-TOP-OF-SCREEN.                         18050054
                                                                        18060054
           PERFORM VARYING PV-MAP-SUB                                   18070054
              FROM 1 BY 1                                               18080054
              UNTIL (PV-MAP-SUB > PC-MAX-SCREEN-LINES)                  18090054
                                                                        18100054
              MOVE CURR-LEVEL(CURR-INDEX)   TO MR-LEVEL (PV-MAP-SUB)    18110054
              MOVE CURR-START-MM(CURR-INDEX) TO MR-STRT-MM(PV-MAP-SUB)  18120054
              MOVE CURR-START-DD(CURR-INDEX) TO MR-STRT-DD(PV-MAP-SUB)  18130054
              MOVE CURR-START-YY(CURR-INDEX) TO MR-STRT-YY(PV-MAP-SUB)  18140054
              IF CURR-END-DATE-CYMD-N(CURR-INDEX) = PC-INFINITY-DATE    18150054
                 MOVE SPACES TO MR-END-MM(PV-MAP-SUB)                   18160054
                                MR-END-DD(PV-MAP-SUB)                   18170054
                                MR-END-YY (PV-MAP-SUB)                  18180054
              ELSE                                                      18190054
                 MOVE CURR-END-MM(CURR-INDEX)  TO MR-END-MM(PV-MAP-SUB) 18200054
                 MOVE CURR-END-DD(CURR-INDEX)  TO MR-END-DD(PV-MAP-SUB) 18210054
                 MOVE CURR-END-YY(CURR-INDEX)  TO MR-END-YY(PV-MAP-SUB) 18220054
              END-IF                                                    18230054
              MOVE CURR-USERID(CURR-INDEX) TO MR-WORKER(PV-MAP-SUB)     18240054
              MOVE CURR-LAST-NAME(CURR-INDEX) TO MR-LNAME(PV-MAP-SUB)   18250054
              MOVE CURR-FIRST-NAME(CURR-INDEX)TO MR-FNAME(PV-MAP-SUB)   18260054
                                                                        18270054
              SET CURR-INDEX UP BY 1                                    18280054
                                                                        18290054
           END-PERFORM.                                                 18300054
                                                                        18310054
       EJECT                                                            18320054
                                                                        18330054
       3305-REMOVE-EMPTY-LINES.                                         18340054
                                                                        18350054
      ******************************************************************18360054
      * IF USER REMOVES THE RESPONSIBILITY LEVEL FROM THE SCREEN,       18370054
      * REMOVE THE ENTIRE LINE, AND MOVE THE LINES BELOW IT UP ONE.     18380054
      ******************************************************************18390054
           IF CURR-LEVEL(CURR-INDEX) > SPACES                           18400054
              IF PV-FILL-SUB < CURR-INDEX                               18410054
                 MOVE CURR-SCREEN-LINE(CURR-INDEX) TO                   18420054
                              CURR-SCREEN-LINE(PV-FILL-SUB)             18430054
                 INITIALIZE CURR-SCREEN-LINE(CURR-INDEX)                18440054
              END-IF                                                    18450054
              ADD  +1 TO PV-FILL-SUB                                    18460054
           ELSE                                                         18470054
              INITIALIZE CURR-SCREEN-LINE(CURR-INDEX)                   18480054
           END-IF.                                                      18490054
                                                                        18500054
       3310-MOVE-COMMAREA-TO-SCREEN.                                    18510054
                                                                        18520054
      ******************************************************************18530054
      *                                                                 18540054
      ******************************************************************18550054
                                                                        18560054
           MOVE ASC-COMPANY-NO          TO AOPERCOO.                    18570054
           MOVE ASC-GMA-NO              TO AGMAO.                       18580054
           MOVE ASC-GMA-DESC            TO AGMADESO.                    18590054
           MOVE ASC-DMA-NO              TO ADMAO.                       18600054
           MOVE ASC-DMA-DESC            TO ADMADESO.                    18610054
           MOVE ASC-DPT-NO              TO ADPTO.                       18620054
           MOVE ASC-DPT-DESC            TO ADPTDESO.                    18630054
           PERFORM 3301-MOVE-SCREEN-LINE.                               18640054
           EJECT                                                        18650054
                                                                        18660054
       4001-SET-UP-SCREEN.                                              18670054
      ******************************************************************18680054
      *  RESET THE SCREEN ATTRIBUTES,                                   18690054
      *  MOVE THE COMMAREA TO THE SCREEN, SET UP THE TOP AND BOTTOM OF  18700054
      *  SCREEN.                                                        18710054
      ******************************************************************18720054
           PERFORM 1700-RESET-ATTRIBUTES.                               18730054
                                                                        18740054
           PERFORM 3310-MOVE-COMMAREA-TO-SCREEN.                        18750054
           COMPUTE ASC-BOT-OF-SCREEN = (ASC-TOP-OF-SCREEN +             18760054
                         PC-MAX-SCREEN-LINES) - 1                       18770054
                                                                        18780054
           SET PS-MORE-RECORDS TO TRUE.                                 18790054
           INITIALIZE PV-MAP-SUB.                                       18800054
           PERFORM 2503-SET-TABLE-ATTRIBUTES                            18810054
                VARYING CURR-INDEX FROM ASC-TOP-OF-SCREEN BY 1          18820054
                UNTIL (PS-NO-MORE-RECORDS OR                            18830054
                       CURR-INDEX > ASC-BOT-OF-SCREEN ).                18840054
                                                                        18850054
       4000-PROCESS-SCREEN.                                             18860054
      ******************************************************************18870054
      *  REMOVE ALL EMPTY LINES FROM THE SCREEN. IF THE USER EOF'S OR   18880054
      *  MOVES SPACES TO THE RESPONSIBILITY AREA, REMOVE THE LINE FROM  18890054
      *  THE SCREEN, AND MOVE ALL THE OTHER LINES OF DATA UP ONE LINE   18900054
      *  ON THE SCREEN.                                                 18910054
      *                                                                 18920054
      ******************************************************************18930054
                                                                        18940054
           PERFORM 3305-REMOVE-EMPTY-LINES                              18950054
              VARYING CURR-INDEX                                        18960054
              FROM 1 BY 1                                               18970054
              UNTIL CURR-INDEX >                                        18980054
                    (ASC-LINES-ENTERED + PC-MAX-SCREEN-LINES).          18981054
                                                                        18990054
           COMPUTE ASC-LINES-ENTERED = PV-FILL-SUB - 1.                 19000054
                                                                        19010054
           PERFORM 3310-MOVE-COMMAREA-TO-SCREEN.                        19020054
                                                                        19030054
                                                                        19040054
       4010-EDIT-RESP-LEVEL.                                            19050054
      ******************************************************************19060054
      * VALIDATE THAT THE RESPONSIBILITY LEVEL IS A VALID ONE ON        19070054
      * THE DB2 DATABASE.                                               19080054
      ******************************************************************19090054
           MOVE CURR-LEVEL(CURR-INDEX) TO DK-RSPLVL-CDE.                19100054
           PERFORM 9000-GET-RESPBL-LEVEL.                               19110054
           IF SQLCODE = 0                                               19120054
              SET CURR-LEVEL-NOT-CHANGED(CURR-INDEX) TO TRUE            19130054
           ELSE                                                         19140054
              SET PS-ERROR         TO TRUE                              19150054
              SET DP020-MSG-FATAL  TO TRUE                              19160054
              COMPUTE PV-WORK-SUB =                                     19170054
                   ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)              19180054
              MOVE DP015-RED       TO MR-LEVEL-C(PV-WORK-SUB)           19190054
              MOVE DP015-REVERSE   TO MR-LEVEL-H(PV-WORK-SUB)           19200054
              MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                  19210054
              MOVE -1              TO MR-LEVEL-L(PV-WORK-SUB)           19220054
           END-IF.                                                      19230054
           EJECT                                                        19240054
                                                                        19250054
                                                                        19260054
                                                                        19270054
                                                                        19280054
                                                                        19290054
       4021-EDIT-START-DATE.                                            19300054
      ******************************************************************19310054
      * EDIT THE START DATE TO INSURE IT IS NUMERIC AND NOT ZERO.       19320054
      * IF IT IS BLANK, FILL IN THE CURRENT DATE.                       19330054
      ******************************************************************19340054
                                                                        19350054
           COMPUTE PV-WORK-SUB =                                        19360054
                  ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)               19370054
                                                                        19380054
           IF (CURR-START-MM(CURR-INDEX) = SPACES OR '00')              19390054
               CONTINUE                                                 19400054
           ELSE                                                         19410054
               MOVE CURR-START-MM(CURR-INDEX) TO DP010I-UNEDITED-FIELD  19420054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        19430054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      19440054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         19451060
262978              USING DP010I-NUMERIC-EDIT-AREA                      19452060
               IF DP010I-ERROR-DETECTED                                 19460054
                  SET PS-START-DATE-ERROR TO TRUE                       19470054
               ELSE                                                     19480054
                  MOVE DP010I-NUMERIC-FIELD  TO                         19490054
                                          CURR-START-MM-N(CURR-INDEX)   19500054
                  MOVE CURR-START-MM(CURR-INDEX) TO                     19510054
                                          MR-STRT-MM(PV-WORK-SUB)       19520054
               END-IF                                                   19530054
           END-IF.                                                      19540054
                                                                        19550054
                                                                        19560054
           IF (CURR-START-DD(CURR-INDEX) = SPACES OR '00')              19570054
              CONTINUE                                                  19580054
           ELSE                                                         19590054
               MOVE CURR-START-DD(CURR-INDEX) TO DP010I-UNEDITED-FIELD  19600054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        19610054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      19620054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         19631060
262978              USING DP010I-NUMERIC-EDIT-AREA                      19632060
               IF DP010I-ERROR-DETECTED                                 19640054
                  SET PS-START-DATE-ERROR TO TRUE                       19650054
               ELSE                                                     19660054
                  MOVE DP010I-NUMERIC-FIELD  TO                         19670054
                                          CURR-START-DD-N(CURR-INDEX)   19680054
                  MOVE CURR-START-DD(CURR-INDEX) TO                     19690054
                                          MR-STRT-DD(PV-WORK-SUB)       19700054
               END-IF                                                   19710054
           END-IF.                                                      19720054
                                                                        19730054
                                                                        19740054
           IF (CURR-START-YY(CURR-INDEX) = SPACES)                      19750054
              CONTINUE                                                  19760054
           ELSE                                                         19770054
               MOVE CURR-START-YY(CURR-INDEX) TO DP010I-UNEDITED-FIELD  19780054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        19790054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      19800054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         19811060
262978              USING DP010I-NUMERIC-EDIT-AREA                      19812060
               IF DP010I-ERROR-DETECTED                                 19820054
                  SET PS-START-DATE-ERROR TO TRUE                       19830054
               ELSE                                                     19840054
                  MOVE DP010I-NUMERIC-FIELD  TO                         19850054
                                          CURR-START-YY-N(CURR-INDEX)   19860054
                  MOVE CURR-START-YY(CURR-INDEX) TO                     19870054
                                          MR-STRT-YY(PV-WORK-SUB)       19880054
               END-IF                                                   19890054
           END-IF.                                                      19900054
                                                                        19910054
           IF PS-START-DATE-ERROR                                       19920054
              SET PS-ERROR         TO TRUE                              19930054
              SET DP020-MSG-FATAL  TO TRUE                              19940054
              MOVE DP015-RED       TO MR-STRT-MM-C(PV-WORK-SUB)         19950054
                                      MR-STRT-DD-C(PV-WORK-SUB)         19960054
                                      MR-STRT-YY-C(PV-WORK-SUB)         19970054
              MOVE DP015-REVERSE   TO MR-STRT-MM-H(PV-WORK-SUB)         19980054
                                      MR-STRT-DD-H(PV-WORK-SUB)         19990054
                                      MR-STRT-YY-H(PV-WORK-SUB)         20000054
              MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                  20010054
              MOVE -1              TO MR-STRT-MM-L(PV-WORK-SUB)         20020054
           ELSE                                                         20030054
              IF (CURR-START-DATE-N(CURR-INDEX) > 0) AND                20040054
                 (CURR-START-MM-N(CURR-INDEX) NUMERIC) AND              20050054
                 (CURR-START-DD-N(CURR-INDEX) NUMERIC) AND              20060054
                 (CURR-START-YY-N(CURR-INDEX) NUMERIC)                  20070054
                 PERFORM 4023-VALIDATE-START-DATE                       20080054
              ELSE                                                      20090054
                 IF CURR-START-DATE(CURR-INDEX) = SPACES                20100054
                    IF ASC-LEVEL-START-DATE-CYMD <=                     20110054
                        DP017-CURRENT-DATE-YYYYMMDD                     20120054
                        MOVE DP017-CURRENT-DATE-YYYYMMDD TO             20130054
                             CURR-START-DATE-CYMD-N(CURR-INDEX)         20140054
                    ELSE                                                20150054
                        MOVE ASC-LEVEL-START-DATE-CYMD TO               20160054
                               CURR-START-DATE-CYMD-N(CURR-INDEX)       20170054
                        MOVE CURR-START-MM(CURR-INDEX) TO               20180054
                               MR-STRT-MM(PV-WORK-SUB)                  20190054
                        MOVE CURR-START-DD(CURR-INDEX) TO               20200054
                               MR-STRT-DD(PV-WORK-SUB)                  20210054
                        MOVE CURR-START-YY(CURR-INDEX) TO               20220054
                               MR-STRT-YY(PV-WORK-SUB)                  20230054
                    END-IF                                              20240054
                 ELSE                                                   20250054
                   IF CURR-START-DATE-N(CURR-INDEX) = ZERO              20260054
                     SET PS-ERROR,                                      20270054
                         DP020-MSG-FATAL,                               20280054
                         PS-START-DATE-ERROR TO TRUE                    20290054
                     MOVE DP015-RED       TO MR-STRT-MM-C(PV-WORK-SUB)  20300054
                                             MR-STRT-DD-C(PV-WORK-SUB)  20310054
                                             MR-STRT-YY-C(PV-WORK-SUB)  20320054
                     MOVE DP015-REVERSE   TO MR-STRT-MM-H(PV-WORK-SUB)  20330054
                                             MR-STRT-DD-H(PV-WORK-SUB)  20340054
                                             MR-STRT-YY-H(PV-WORK-SUB)  20350054
                     MOVE PC-TSYMSG-00010 TO DP020-MSG-NUMBER           20360054
                     MOVE -1              TO MR-STRT-MM-L(PV-WORK-SUB)  20370054
                   ELSE                                                 20380054
                     SET PS-ERROR,                                      20390054
                         DP020-MSG-FATAL,                               20400054
                         PS-START-DATE-ERROR TO TRUE                    20410054
                     MOVE DP015-RED       TO MR-STRT-MM-C(PV-WORK-SUB)  20420054
                                             MR-STRT-DD-C(PV-WORK-SUB)  20430054
                                             MR-STRT-YY-C(PV-WORK-SUB)  20440054
                     MOVE DP015-REVERSE   TO MR-STRT-MM-H(PV-WORK-SUB)  20450054
                                             MR-STRT-DD-H(PV-WORK-SUB)  20460054
                                             MR-STRT-YY-H(PV-WORK-SUB)  20470054
                     MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER           20480054
                     MOVE -1              TO MR-STRT-MM-L(PV-WORK-SUB)  20490054
                   END-IF                                               20500054
                 END-IF                                                 20510054
              END-IF                                                    20520054
           END-IF.                                                      20530054
           EJECT                                                        20540054
                                                                        20550054
       4022-EDIT-END-DATE.                                              20560054
      ******************************************************************20570054
      * VALIDATE THE END DATE FOR A NUMERIC NON-ZERO DATE.              20580054
      * IF THE DATE IS SPACES FILL IN THE MERCHANDISE AREA END DATE.    20590054
      * IF THE MERCHANDISE AREA END DATE = SPACES, DISPLAY IT AS BLANKS 20600054
      ******************************************************************20610054
                                                                        20620054
           COMPUTE PV-WORK-SUB =                                        20630054
                  ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)               20640054
           IF (CURR-END-MM(CURR-INDEX) = SPACES OR '00')                20650054
               CONTINUE                                                 20660054
           ELSE                                                         20670054
               MOVE CURR-END-MM(CURR-INDEX) TO  DP010I-UNEDITED-FIELD   20680054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        20690054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      20700054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         20711060
262978              USING DP010I-NUMERIC-EDIT-AREA                      20712060
               IF DP010I-ERROR-DETECTED                                 20720054
                  SET PS-END-DATE-ERROR TO TRUE                         20730054
               ELSE                                                     20740054
                  MOVE DP010I-NUMERIC-FIELD  TO                         20750054
                                          CURR-END-MM-N(CURR-INDEX)     20760054
                  MOVE CURR-END-MM(CURR-INDEX) TO                       20770054
                                          MR-END-MM(PV-WORK-SUB)        20780054
               END-IF                                                   20790054
           END-IF.                                                      20800054
                                                                        20810054
                                                                        20820054
           IF (CURR-END-DD(CURR-INDEX) = SPACES OR '00')                20830054
              CONTINUE                                                  20840054
           ELSE                                                         20850054
               MOVE CURR-END-DD(CURR-INDEX) TO  DP010I-UNEDITED-FIELD   20860054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        20870054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      20880054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         20891060
262978              USING DP010I-NUMERIC-EDIT-AREA                      20892060
               IF DP010I-ERROR-DETECTED                                 20900054
                  SET PS-END-DATE-ERROR TO TRUE                         20910054
               ELSE                                                     20920054
                  MOVE DP010I-NUMERIC-FIELD  TO                         20930054
                                          CURR-END-DD-N(CURR-INDEX)     20940054
                  MOVE CURR-END-DD(CURR-INDEX) TO                       20950054
                                          MR-END-DD(PV-WORK-SUB)        20960054
               END-IF                                                   20970054
           END-IF.                                                      20980054
                                                                        20990054
                                                                        21000054
           IF (CURR-END-YY(CURR-INDEX) = SPACES)                        21010054
              CONTINUE                                                  21020054
           ELSE                                                         21030054
               MOVE CURR-END-YY(CURR-INDEX) TO  DP010I-UNEDITED-FIELD   21040054
               MOVE +2                 TO  DP010I-MAXIMUM-DIGITS        21050054
               SET DP010I-NEGATIVE-ALLOWED TO TRUE                      21060054
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                         21071060
262978              USING DP010I-NUMERIC-EDIT-AREA                      21072060
               IF DP010I-ERROR-DETECTED                                 21080054
                  SET PS-END-DATE-ERROR TO TRUE                         21090054
               ELSE                                                     21100054
                  MOVE DP010I-NUMERIC-FIELD  TO                         21110054
                                          CURR-END-YY-N(CURR-INDEX)     21120054
                  MOVE CURR-END-YY(CURR-INDEX) TO                       21130054
                                          MR-END-YY(PV-WORK-SUB)        21140054
               END-IF                                                   21150054
           END-IF.                                                      21160054
                                                                        21170054
           IF PS-END-DATE-ERROR                                         21180054
              SET PS-ERROR         TO TRUE                              21190054
              SET DP020-MSG-FATAL  TO TRUE                              21200054
              MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)          21210054
                                      MR-END-DD-C(PV-WORK-SUB)          21220054
                                      MR-END-YY-C(PV-WORK-SUB)          21230054
              MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)          21240054
                                      MR-END-DD-H(PV-WORK-SUB)          21250054
                                      MR-END-YY-H(PV-WORK-SUB)          21260054
              MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                  21270054
              MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)          21280054
           ELSE                                                         21290054
               IF CURR-END-DATE-N(CURR-INDEX) = ZERO                    21300054
                   SET PS-ERROR,                                        21310054
                       DP020-MSG-FATAL,                                 21320054
                       PS-END-DATE-ERROR TO TRUE                        21330054
                   MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)     21340054
                                           MR-END-DD-C(PV-WORK-SUB)     21350054
                                           MR-END-YY-C(PV-WORK-SUB)     21360054
                   MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)     21370054
                                           MR-END-DD-H(PV-WORK-SUB)     21380054
                                           MR-END-YY-H(PV-WORK-SUB)     21390054
                   MOVE PC-TSYMSG-00010 TO DP020-MSG-NUMBER             21400054
                   MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)     21410054
               ELSE                                                     21420054
                 IF CURR-END-DATE(CURR-INDEX) = SPACES                  21430054
                      MOVE ASC-LEVEL-END-DATE-CYMD TO                   21440054
                           CURR-END-DATE-CYMD-N(CURR-INDEX)             21450054
                      MOVE CURR-END-MM(CURR-INDEX) TO                   21460054
                           MR-END-MM(PV-WORK-SUB)                       21470054
                      MOVE CURR-END-DD(CURR-INDEX) TO                   21480054
                           MR-END-DD(PV-WORK-SUB)                       21490054
                      MOVE CURR-END-YY(CURR-INDEX) TO                   21500054
                           MR-END-YY(PV-WORK-SUB)                       21510054
                 ELSE                                                   21520054
                   IF CURR-END-MM-N(CURR-INDEX) NUMERIC AND             21530054
                      CURR-END-DD-N(CURR-INDEX) NUMERIC AND             21540054
                      CURR-END-YY-N(CURR-INDEX) NUMERIC AND             21550054
                      CURR-END-DATE-N(CURR-INDEX) > 0                   21560054
                      PERFORM 4024-VALIDATE-END-DATE                    21570054
                   ELSE                                                 21580054
                       SET PS-ERROR,                                    21590054
                           DP020-MSG-FATAL,                             21600054
                           PS-END-DATE-ERROR TO TRUE                    21610054
                       MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB) 21620054
                                               MR-END-DD-C(PV-WORK-SUB) 21630054
                                               MR-END-YY-C(PV-WORK-SUB) 21640054
                       MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB) 21650054
                                               MR-END-DD-H(PV-WORK-SUB) 21660054
                                               MR-END-YY-H(PV-WORK-SUB) 21670054
                       MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER         21680054
                       MOVE -1              TO MR-END-MM-L(PV-WORK-SUB) 21690054
                   END-IF                                               21700054
                END-IF                                                  21710054
               END-IF                                                   21720054
           END-IF.                                                      21730054
           EJECT                                                        21740054
                                                                        21750054
       4023-VALIDATE-START-DATE.                                        21760054
      ******************************************************************21770054
      * VALIDATE THAT THE START DATE IS A VALID CALENDAR DATE.          21780054
      ******************************************************************21790054
           COMPUTE PV-WORK-SUB =                                        21800054
                   ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1).             21810054
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         21820054
                                                                        21830054
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                    21840054
                                                                        21850054
           SET DPG52-LK-DTE-GREG            TO TRUE.                    21860054
           MOVE CURR-START-MM(CURR-INDEX)   TO  DPG52-GREG-DATE-MO-X.   21870054
           MOVE CURR-START-DD(CURR-INDEX)   TO  DPG52-GREG-DATE-DY-X.   21880054
           MOVE CURR-START-YY(CURR-INDEX)   TO  DPG52-GREG-DATE-YR-X.   21890054
                                                                        21900054
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                           21910054
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.        21920054
                                                                        21930054
           IF  DPG54-NO-ERROR                                           21940054
               MOVE DPG55-DB2-ISO-DATE-YR  TO  PV-STORED-CCYY           21950054
               MOVE PV-STORED-CC-X  TO  CURR-START-CYMD-CC(CURR-INDEX)  21960054
           ELSE                                                         21970054
      *        --  DATE NOT VALID --                                    21980054
               SET PS-ERROR TO TRUE                                     21990054
               SET PS-START-DATE-ERROR TO TRUE                          22000054
               MOVE DP015-RED        TO MR-STRT-MM-C(PV-WORK-SUB)       22010054
                                        MR-STRT-DD-C(PV-WORK-SUB)       22020054
                                        MR-STRT-YY-C(PV-WORK-SUB)       22030054
               MOVE DP015-REVERSE    TO MR-STRT-MM-H(PV-WORK-SUB)       22040054
                                        MR-STRT-DD-H(PV-WORK-SUB)       22050054
                                        MR-STRT-YY-H(PV-WORK-SUB)       22060054
               MOVE DP015-UNP-ALP-BRT-OFF TO                            22070054
                                        MR-STRT-MM-A(PV-WORK-SUB)       22080054
                                        MR-STRT-DD-A(PV-WORK-SUB)       22090054
                                        MR-STRT-YY-A(PV-WORK-SUB)       22100054
               MOVE PC-TSYMSG-00009           TO DP020-MSG-NUMBER       22110054
               SET DP020-MSG-FATAL TO TRUE                              22120054
               MOVE -1               TO MR-STRT-MM-L(PV-WORK-SUB)       22130054
           END-IF.                                                      22140054
           EJECT                                                        22150054
                                                                        22160054
       4024-VALIDATE-END-DATE.                                          22170054
      ******************************************************************22180054
      * VALIDATE THAT THE END DATE IS A VALID CALENDAR DATE.            22190054
      ******************************************************************22200054
           COMPUTE PV-WORK-SUB =                                        22210054
                  ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1).              22220054
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         22230054
                                                                        22240054
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                    22250054
                                                                        22260054
           SET DPG52-LK-DTE-GREG            TO TRUE.                    22270054
           MOVE CURR-END-MM(CURR-INDEX)     TO  DPG52-GREG-DATE-MO-X.   22280054
           MOVE CURR-END-DD(CURR-INDEX)     TO  DPG52-GREG-DATE-DY-X.   22290054
           MOVE CURR-END-YY(CURR-INDEX)     TO  DPG52-GREG-DATE-YR-X.   22300054
                                                                        22310054
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                           22320054
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.        22330054
                                                                        22340054
           IF  DPG54-NO-ERROR                                           22350054
               MOVE DPG55-DB2-ISO-DATE-YR  TO  PV-STORED-CCYY           22360054
               MOVE PV-STORED-CC-X  TO  CURR-END-CYMD-CC(CURR-INDEX)    22370054
           ELSE                                                         22380054
      *        --  DATE NOT VALID --                                    22390054
               SET PS-ERROR TO TRUE                                     22400054
               SET PS-END-DATE-ERROR TO TRUE                            22410054
               MOVE DP015-RED        TO MR-END-MM-C(PV-WORK-SUB)        22420054
                                        MR-END-DD-C(PV-WORK-SUB)        22430054
                                        MR-END-YY-C(PV-WORK-SUB)        22440054
               MOVE DP015-REVERSE    TO MR-END-MM-H(PV-WORK-SUB)        22450054
                                        MR-END-DD-H(PV-WORK-SUB)        22460054
                                        MR-END-YY-H(PV-WORK-SUB)        22470054
               MOVE DP015-UNP-ALP-BRT-OFF TO                            22480054
                                        MR-END-MM-A(PV-WORK-SUB)        22490054
                                        MR-END-DD-A(PV-WORK-SUB)        22500054
                                        MR-END-YY-A(PV-WORK-SUB)        22510054
               MOVE PC-TSYMSG-00009           TO DP020-MSG-NUMBER       22520054
               SET DP020-MSG-FATAL TO TRUE                              22530054
               MOVE -1               TO MR-END-MM-L(PV-WORK-SUB)        22540054
           END-IF.                                                      22550054
           EJECT                                                        22560054
       4025-COMPARE-DATES.                                              22570054
      ******************************************************************22580054
      * 1. CALCULATE NEW DATE = TODAY - 30 DAYS                         22590054
      * 2. CALCULATE NEW DATE = TODAY + 365 DAYS                        22600054
      * 3. IF END DATE NOT = 99990909, END DATE MUST BE LESS THAN       22610054
      *    CURRENT DATE + 365 DAYS.                                     22620054
      * 4. END DATE MUST BE LESS THAN MERCHANDISE AREA START DATE       22630054
      * 5. START-DATE MUST BE < TODAY'S DATE + 365                      22640054
      * 6. IF RESPONSIBILITY IS NOT NEW FOR MERCH. AREA, USER CANNOT    22650054
      *    CHANGE THE START DATE, SO DONT CHECK IT.                     22660054
      * 7. IF RESPONSIBILITY IS NEW FOR THIS MERCHANDISE AREA,          22670054
      *     A) START DATE MUST BE GREATER THAN TODAY - 30 DAYS          22680054
      *     B) START DATE MUST BE GREATER THAN MERCHANDISE AREA START   22690054
      *     C) START DATE MUST BE LESS THAN TODAY + 365 DAYS            22700054
      * 8. END DATE MUST BE GREATER THAN START DATE                     22710054
      ******************************************************************22720054
           SET PS-NO-BOTH-DATE-ERROR TO TRUE                            22730054
           COMPUTE PV-WORK-SUB =                                        22740054
                   ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1).             22750054
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         22760054
                                                                        22770054
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                    22780054
           SET DPG51-DECREMENT-DATE         TO TRUE.                    22790054
           MOVE PC-MONTH-DAYS               TO DPG51-INCR-DECR-DAYS-9.  22800054
                                                                        22810054
           SET DPG52-LK-DTE-ISO             TO TRUE.                    22820054
           MOVE DP017-CURRENT-DATE-YYYYMMDD TO DPG52-DB2-ISO-DATE.      22830054
                                                                        22840054
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                           22850054
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.        22860054
                                                                        22870054
           IF DPG54-NO-ERROR                                            22880054
              MOVE DPG55-DB2-ISO-DATE     TO  PV-TEMP-DATE              22890054
           ELSE                                                         22900054
              SET DP013-LOGIC-ABEND TO TRUE                             22910054
              SET DP013-ROLLBACK TO TRUE                                22920054
              MOVE '4025-COMPARE-DATES' TO DP013-PARAGRAPH              22930054
              MOVE 'INVALID CURRENT DATE' TO DP013-MESSAGE-TEXT(1)      22940054
              MOVE 'TRIED TO SUBTRACT 30 DAYS' TO DP013-MESSAGE-TEXT(2) 22950054
              PERFORM DP013-0000-PROCESS-ABEND                          22960054
           END-IF.                                                      22970054
                                                                        22980054
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         22990054
                                                                        23000054
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                    23010054
           SET DPG51-INCREMENT-DATE         TO TRUE.                    23020054
           MOVE PC-YEAR-DAYS                TO DPG51-INCR-DECR-DAYS-9.  23030054
                                                                        23040054
           SET DPG52-LK-DTE-ISO             TO TRUE.                    23050054
           MOVE DP017-CURRENT-DATE-YYYYMMDD TO DPG52-DB2-ISO-DATE.      23060054
                                                                        23070054
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                           23080054
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.        23090054
                                                                        23100054
           IF DPG54-NO-ERROR                                            23110054
              MOVE DPG55-DB2-ISO-DATE  TO  PV-TEMP-YEAR-DATE            23120054
           ELSE                                                         23130054
              SET DP013-LOGIC-ABEND TO TRUE                             23140054
              SET DP013-ROLLBACK TO TRUE                                23150054
              MOVE '4025-COMPARE-DATES' TO DP013-PARAGRAPH              23160054
              MOVE 'INVALID CURRENT DATE' TO DP013-MESSAGE-TEXT(1)      23170054
              MOVE 'TRIED TO ADD 365 DAYS' TO DP013-MESSAGE-TEXT(2)     23180054
              PERFORM DP013-0000-PROCESS-ABEND                          23190054
           END-IF.                                                      23200054
                                                                        23210054
           IF (CURR-END-DATE-CYMD-N(CURR-INDEX) > PV-TEMP-YEAR-DATE) AND23220054
              (CURR-END-DATE-CYMD-N(CURR-INDEX) NOT = PC-INFINITY-DATE) 23230054
               SET PS-ERROR         TO TRUE                             23240054
               SET PS-BOTH-DATE-ERROR TO TRUE                           23250054
               SET DP020-MSG-FATAL  TO TRUE                             23260054
               MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)         23270054
                                       MR-END-DD-C(PV-WORK-SUB)         23280054
                                       MR-END-YY-C(PV-WORK-SUB)         23290054
               MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)         23300054
                                       MR-END-DD-H(PV-WORK-SUB)         23310054
                                       MR-END-YY-H(PV-WORK-SUB)         23320054
               MOVE PC-TSYMSG-00025 TO DP020-MSG-NUMBER                 23330054
               MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)         23340054
           END-IF.                                                      23350054
                                                                        23360054
           IF CURR-END-DATE-CYMD-N(CURR-INDEX) >                        23370054
                      ASC-LEVEL-END-DATE-CYMD                           23380054
               SET PS-ERROR         TO TRUE                             23390054
               SET PS-BOTH-DATE-ERROR TO TRUE                           23400054
               SET DP020-MSG-FATAL  TO TRUE                             23410054
               MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)         23420054
                                       MR-END-DD-C(PV-WORK-SUB)         23430054
                                       MR-END-YY-C(PV-WORK-SUB)         23440054
               MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)         23450054
                                       MR-END-DD-H(PV-WORK-SUB)         23460054
                                       MR-END-YY-H(PV-WORK-SUB)         23470054
               MOVE PC-TSYMSG-00145 TO DP020-MSG-NUMBER                 23480054
               MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)         23490054
           ELSE                                                         23500054
              IF CURR-END-DATE-CYMD-N(CURR-INDEX) <                     23510054
                                   PV-TEMP-DATE                         23520054
                  SET PS-ERROR         TO TRUE                          23530054
                  SET PS-BOTH-DATE-ERROR TO TRUE                        23540054
                  SET DP020-MSG-FATAL  TO TRUE                          23550054
                  MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)      23560054
                                          MR-END-DD-C(PV-WORK-SUB)      23570054
                                          MR-END-YY-C(PV-WORK-SUB)      23580054
                  MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)      23590054
                                          MR-END-DD-H(PV-WORK-SUB)      23600054
                                          MR-END-YY-H(PV-WORK-SUB)      23610054
                  MOVE PC-TSYMSG-00191 TO DP020-MSG-NUMBER              23620054
                  MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)      23630054
              END-IF                                                    23640054
           END-IF.                                                      23650054
                                                                        23660054
           IF CURR-START-DATE-CYMD-N(CURR-INDEX) >                      23670054
                      PV-TEMP-YEAR-DATE                                 23680054
               SET PS-ERROR         TO TRUE                             23690054
               SET PS-BOTH-DATE-ERROR TO TRUE                           23700054
               SET DP020-MSG-FATAL  TO TRUE                             23710054
               MOVE DP015-RED    TO                                     23720054
                                  MR-STRT-MM-C(PV-WORK-SUB)             23730054
                                  MR-STRT-DD-C(PV-WORK-SUB)             23740054
                                  MR-STRT-YY-C(PV-WORK-SUB)             23750054
               MOVE DP015-REVERSE TO                                    23760054
                                  MR-STRT-MM-H(PV-WORK-SUB)             23770054
                                  MR-STRT-DD-H(PV-WORK-SUB)             23780054
                                  MR-STRT-YY-H(PV-WORK-SUB)             23790054
               MOVE PC-TSYMSG-00025 TO DP020-MSG-NUMBER                 23800054
               MOVE -1          TO MR-STRT-MM-L(PV-WORK-SUB)            23810054
           END-IF.                                                      23820054
                                                                        23830054
           IF (CURR-ORIG-RECORD(CURR-INDEX)) AND                        23840054
              (CURR-PROTECT-START-DATE(CURR-INDEX))                     23850054
              CONTINUE                                                  23860054
           ELSE                                                         23870054
              IF CURR-START-DATE-CYMD-N(CURR-INDEX) >                   23880054
                     PV-TEMP-DATE                                       23890054
                IF CURR-START-DATE-CYMD-N(CURR-INDEX) <                 23900054
                           ASC-LEVEL-START-DATE-CYMD                    23910054
                    SET PS-ERROR         TO TRUE                        23920054
                    SET PS-BOTH-DATE-ERROR TO TRUE                      23930054
                    SET DP020-MSG-FATAL  TO TRUE                        23940054
                    MOVE DP015-RED    TO                                23950054
                                       MR-STRT-MM-C(PV-WORK-SUB)        23960054
                                       MR-STRT-DD-C(PV-WORK-SUB)        23970054
                                       MR-STRT-YY-C(PV-WORK-SUB)        23980054
                    MOVE DP015-REVERSE TO                               23990054
                                       MR-STRT-MM-H(PV-WORK-SUB)        24000054
                                       MR-STRT-DD-H(PV-WORK-SUB)        24010054
                                       MR-STRT-YY-H(PV-WORK-SUB)        24020054
                    MOVE PC-TSYMSG-00144 TO DP020-MSG-NUMBER            24030054
                    MOVE -1          TO MR-STRT-MM-L(PV-WORK-SUB)       24040054
                ELSE                                                    24050054
                    CONTINUE                                            24060054
                END-IF                                                  24070054
              ELSE                                                      24080054
                SET PS-ERROR         TO TRUE                            24090054
                SET PS-BOTH-DATE-ERROR TO TRUE                          24100054
                SET DP020-MSG-FATAL  TO TRUE                            24110054
                MOVE DP015-RED       TO MR-STRT-MM-C(PV-WORK-SUB)       24120054
                                        MR-STRT-DD-C(PV-WORK-SUB)       24130054
                                        MR-STRT-YY-C(PV-WORK-SUB)       24140054
                MOVE DP015-REVERSE   TO MR-STRT-MM-H(PV-WORK-SUB)       24150054
                                        MR-STRT-DD-H(PV-WORK-SUB)       24160054
                                        MR-STRT-YY-H(PV-WORK-SUB)       24170054
                MOVE PC-TSYMSG-00191 TO DP020-MSG-NUMBER                24180054
                MOVE -1              TO MR-STRT-MM-L(PV-WORK-SUB)       24190054
              END-IF                                                    24200054
           END-IF.                                                      24210054
                                                                        24220054
           IF (PS-NO-BOTH-DATE-ERROR) AND                               24230054
              (CURR-END-DATE-CYMD-N(CURR-INDEX) <                       24240054
                         CURR-START-DATE-CYMD-N(CURR-INDEX))            24250054
              SET PS-ERROR         TO TRUE                              24260054
              SET DP020-MSG-FATAL  TO TRUE                              24270054
              MOVE DP015-RED       TO MR-END-MM-C(PV-WORK-SUB)          24280054
                                      MR-END-DD-C(PV-WORK-SUB)          24290054
                                      MR-END-YY-C(PV-WORK-SUB)          24300054
              MOVE DP015-REVERSE   TO MR-END-MM-H(PV-WORK-SUB)          24310054
                                      MR-END-DD-H(PV-WORK-SUB)          24320054
                                      MR-END-YY-H(PV-WORK-SUB)          24330054
              MOVE PC-TSYMSG-00143 TO DP020-MSG-NUMBER                  24340054
              MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)          24350054
           END-IF.                                                      24360054
                                                                        24370054
           IF PS-NO-BOTH-DATE-ERROR                                     24380054
              SET CURR-START-NOT-CHANGED(CURR-INDEX)                    24390054
                  CURR-END-NOT-CHANGED(CURR-INDEX) TO TRUE              24400054
           END-IF.                                                      24410054
           EJECT                                                        24420054
                                                                        24430054
                                                                        24440054
       4030-VALIDATE-WORKER-ID.                                         24450054
      ******************************************************************24460054
      * DETERMINE THAT THE USERID ID IS A VALID WORKER ON THE TWORKER   24470054
      * DB2 TABLE.                                                      24480054
      ******************************************************************24490054
           COMPUTE PV-WORK-SUB =                                        24500054
                  ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)               24510054
           IF CURR-USERID(CURR-INDEX) = SPACES                          24520054
              SET PS-ERROR         TO TRUE                              24530054
              SET DP020-MSG-FATAL  TO TRUE                              24540054
              INITIALIZE  MR-WORKER(PV-WORK-SUB)                        24550054
                          MR-FNAME(PV-WORK-SUB)                         24560054
                          MR-LNAME(PV-WORK-SUB)                         24570054
                          CURR-USERID(CURR-INDEX)                       24580054
                          CURR-FIRST-NAME(CURR-INDEX)                   24590054
                          CURR-LAST-NAME(CURR-INDEX)                    24600054
              MOVE DP015-RED       TO MR-WORKER-C(PV-WORK-SUB)          24610054
              MOVE DP015-REVERSE   TO MR-WORKER-H(PV-WORK-SUB)          24620054
              MOVE PC-TSYMSG-00007 TO DP020-MSG-NUMBER                  24630054
              MOVE -1              TO MR-WORKER-L(PV-WORK-SUB)          24640054
           ELSE                                                         24650054
              MOVE CURR-USERID(CURR-INDEX) TO DK-WORKER-UID             24660054
              MOVE ASC-COMPANY-NO           TO DK-OPERCO-NBR            24670054
              PERFORM 9015-GET-WORKER-NAME                              24680054
              IF SQLCODE = +0                                           24690054
                  SET CURR-WORKER-NOT-CHANGED(CURR-INDEX) TO TRUE       24700054
                  MOVE WORKER-FIR-NAME TO MR-FNAME (PV-WORK-SUB)        24710054
                                          CURR-FIRST-NAME(CURR-INDEX)   24720054
                  MOVE WORKER-LAST-NAME TO MR-LNAME(PV-WORK-SUB)        24730054
                                          CURR-LAST-NAME(CURR-INDEX)    24740054
                  MOVE WORKER-UID-NBR  TO CURR-USERID(CURR-INDEX)       24750054
              ELSE                                                      24760054
                  INITIALIZE  MR-FNAME(PV-WORK-SUB)                     24770054
                              MR-LNAME(PV-WORK-SUB)                     24780054
                              CURR-FIRST-NAME(CURR-INDEX)               24790054
                              CURR-LAST-NAME(CURR-INDEX)                24800054
                  SET PS-ERROR         TO TRUE                          24810054
                  SET DP020-MSG-FATAL  TO TRUE                          24820054
                  MOVE DP015-RED       TO MR-WORKER-C(PV-WORK-SUB)      24830054
                  MOVE DP015-REVERSE   TO MR-WORKER-H(PV-WORK-SUB)      24840054
                  MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER              24850054
                  MOVE -1              TO MR-WORKER-L(PV-WORK-SUB)      24860054
              END-IF                                                    24870054
           END-IF.                                                      24880054
           EJECT                                                        24890054
                                                                        24900054
                                                                        24910054
       4040-EDIT-RESPON-ASSIGNMENTS.                                    24920054
      ******************************************************************24930054
      *    COUNT THE NUMBER OF GMM'S, DMM'S AND BYR'S SPECIFIED.        24940054
      *    IF HAVEN'T ADDED REQUIRED ASSIGNMENT FOR THE LEVEL BEING     24950054
      *    ADDED, SEND ERROR MESSAGE.                                   24960054
      *                                                                 24970054
      *    PERFORMED BY : 3300-EDIT-DATA-IN-COMMAREA                    24980054
      ******************************************************************24990054
           PERFORM                                                      25000054
              VARYING CURR-INDEX                                        25010054
              FROM 1 BY 1                                               25020054
              UNTIL CURR-INDEX > ASC-LINES-ENTERED                      25030054
                                                                        25040054
              IF CURR-LEVEL(CURR-INDEX) = PC-GMM-LEVEL                  25050054
                 ADD 1 TO PV-GMMS-SPECIFIED                             25060054
              ELSE                                                      25070054
                 IF CURR-LEVEL(CURR-INDEX) = PC-DMM-LEVEL               25080054
                    ADD 1 TO PV-DMMS-SPECIFIED                          25090054
                 ELSE                                                   25100054
                    IF CURR-LEVEL(CURR-INDEX) = PC-BYR-LEVEL            25110054
                       ADD 1 TO PV-BUYERS-SPECIFIED                     25120054
                    END-IF                                              25130054
                 END-IF                                                 25140054
              END-IF                                                    25150054
           END-PERFORM.                                                 25160054
                                                                        25170054
                                                                        25180054
           IF (ASC-UPD-GMA) AND                                         25190054
              (PV-GMMS-SPECIFIED = 0)                                   25200054
              SET PS-ERROR         TO TRUE                              25210054
              SET DP020-MSG-FATAL  TO TRUE                              25220054
              MOVE PC-TSYMSG-00181 TO DP020-MSG-NUMBER                  25230054
              MOVE -1              TO MR-LEVEL-L(1)                     25240054
           ELSE                                                         25250054
              IF (ASC-UPD-DMA) AND                                      25260054
                 (PV-DMMS-SPECIFIED = 0)                                25270054
                 SET PS-ERROR         TO TRUE                           25280054
                 SET DP020-MSG-FATAL  TO TRUE                           25290054
                 MOVE PC-TSYMSG-00181 TO DP020-MSG-NUMBER               25300054
                 MOVE -1              TO MR-LEVEL-L(1)                  25310054
              ELSE                                                      25320054
                 IF (ASC-UPD-DPT) AND                                   25330054
                    (PV-BUYERS-SPECIFIED = 0)                           25340054
                    SET PS-ERROR         TO TRUE                        25350054
                    SET DP020-MSG-FATAL  TO TRUE                        25360054
                    MOVE PC-TSYMSG-00181 TO DP020-MSG-NUMBER            25370054
                    MOVE -1              TO MR-LEVEL-L(1)               25380054
                 END-IF                                                 25390054
              END-IF                                                    25400054
           END-IF.                                                      25410054
           EJECT                                                        25420054
                                                                        25430054
                                                                        25440054
                                                                        25450054
       4050-CHECK-DUPLICATE-ENTRIES.                                    25460054
      ******************************************************************25470054
      *  CHECK TO SEE IF DUPLICATE LEVEL-USERID COMBINATION EXISTS ON   25480054
      *  SCREEN. IF SO SEND ERROR MESSAGE. NO LEVEL-USERID COMBINATIONS 25490054
      *  CAN BOTH BE ACTIVE AT THE SAME TIME, ALTHOUGH YOU CAN REACTIVAT25500054
      *  A PERSON AFTER THEY HAVE BECOME INACTIVE.                      25510054
      *    PERFORMED BY : 3300-EDIT-DATA-IN-COMMAREA                    25520054
      ******************************************************************25530054
                                                                        25540054
                                                                        25550054
           MOVE CURR-LEVEL(CURR-INDEX) TO PV-WORK-LEVEL.                25560054
           MOVE CURR-USERID(CURR-INDEX) TO PV-WORK-ID.                  25570054
           MOVE ALL '-'              TO CURR-LEVEL(CURR-INDEX)          25580054
                                        CURR-USERID(CURR-INDEX).        25590054
           SET CURR-INDEX2 TO 1                                         25600054
           SEARCH CURR-SCREEN-LINE                                      25610054
              VARYING CURR-INDEX2                                       25620054
              AT END                                                    25630054
                  CONTINUE                                              25640054
              WHEN (PV-WORK-LEVEL = CURR-LEVEL(CURR-INDEX2) AND         25650054
                    PV-WORK-ID    = CURR-USERID(CURR-INDEX2))           25660054
      ***----- DUPLICATE LEVEL/USERID COMBINATION -----***              25670054
                                                                        25680054
                   COMPUTE PV-WORK-SUB =                                25690054
                     ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)            25700054
                   SET PS-ERROR         TO TRUE                         25710054
                   SET DP020-MSG-FATAL  TO TRUE                         25720054
                   MOVE PC-TSYMSG-00190 TO DP020-MSG-NUMBER             25730054
                   MOVE DP015-RED       TO MR-LEVEL-C(PV-WORK-SUB)      25740054
                                           MR-STRT-MM-C(PV-WORK-SUB)    25750054
                                           MR-STRT-DD-C(PV-WORK-SUB)    25760054
                                           MR-STRT-YY-C(PV-WORK-SUB)    25770054
                                           MR-END-MM-C(PV-WORK-SUB)     25780054
                                           MR-END-DD-C(PV-WORK-SUB)     25790054
                                           MR-END-YY-C(PV-WORK-SUB)     25800054
                                           MR-WORKER-C(PV-WORK-SUB)     25810054
                   MOVE DP015-REVERSE   TO MR-LEVEL-H(PV-WORK-SUB)      25820054
                                           MR-STRT-MM-H(PV-WORK-SUB)    25830054
                                           MR-STRT-DD-H(PV-WORK-SUB)    25840054
                                           MR-STRT-YY-H(PV-WORK-SUB)    25850054
                                           MR-END-MM-H(PV-WORK-SUB)     25860054
                                           MR-END-DD-H(PV-WORK-SUB)     25870054
                                           MR-END-YY-H(PV-WORK-SUB)     25880054
                                           MR-WORKER-H(PV-WORK-SUB)     25890054
                   MOVE -1              TO MR-END-MM-L(PV-WORK-SUB)     25900054
           END-SEARCH.                                                  25910054
           MOVE PV-WORK-LEVEL TO CURR-LEVEL(CURR-INDEX).                25920054
           MOVE PV-WORK-ID   TO CURR-USERID(CURR-INDEX).                25930054
           EJECT                                                        25940054
                                                                        25950054
                                                                        25960054
                                                                        25970054
       4060-MASK-END-DATES.                                             25980054
      ******************************************************************25990054
      *  IF THE END DATE 99990909, DISPLAY IT AS SPACES ON THE SCREEN.  26000054
      *    PERFORMED BY : 3300-EDIT-DATA-IN-COMMAREA                    26010054
      ******************************************************************26020054
           PERFORM VARYING                                              26030054
              CURR-INDEX FROM 1 BY 1                                    26040054
              UNTIL (CURR-INDEX > ASC-LINES-ENTERED)                    26050054
                                                                        26060054
              IF CURR-END-DATE-CYMD-N(CURR-INDEX) = PC-INFINITY-DATE    26070054
                  MOVE SPACES TO CURR-END-MM(CURR-INDEX)                26080054
                                 CURR-END-DD(CURR-INDEX)                26090054
                                 CURR-END-YY(CURR-INDEX)                26100054
                  IF CURR-INDEX >= ASC-TOP-OF-SCREEN                    26110054
                    AND CURR-INDEX <= ASC-BOT-OF-SCREEN                 26120054
                      SET PV-MAP-SUB TO CURR-INDEX                      26130054
                      COMPUTE PV-WORK-SUB =                             26140054
                             (PV-MAP-SUB - (ASC-TOP-OF-SCREEN + 1))     26150054
                      MOVE SPACES TO MR-END-MM (PV-WORK-SUB)            26160054
                                     MR-END-DD (PV-WORK-SUB)            26170054
                                     MR-END-YY (PV-WORK-SUB)            26180054
                  END-IF                                                26190054
              END-IF                                                    26200054
                                                                        26210054
                                                                        26220054
           END-PERFORM.                                                 26230054
                                                                        26240054
       4065-EDIT-ALL-END-DATES.                                         26250054
      ******************************************************************26260054
      *    MAKE SURE ALL THE BLANK END DATES IN COMMAREA                26270054
      *    ARE FILLED WITH THE PARENT MDSEAR END DATE.                  26280054
      *                                                                 26290054
      ******************************************************************26300054
           PERFORM VARYING                                              26310054
              CURR-INDEX FROM 1 BY 1                                    26320054
              UNTIL (CURR-INDEX > ASC-LINES-ENTERED)                    26330054
                                                                        26340054
              IF CURR-LEVEL(CURR-INDEX) NOT = SPACES                    26350054
                 IF CURR-END-DATE(CURR-INDEX) = SPACES                  26360054
                      MOVE ASC-LEVEL-END-DATE-CYMD TO                   26370054
                           CURR-END-DATE-CYMD-N(CURR-INDEX)             26380054
                      MOVE CURR-END-MM(CURR-INDEX) TO                   26390054
                           MR-END-MM(PV-WORK-SUB)                       26400054
                      MOVE CURR-END-DD(CURR-INDEX) TO                   26410054
                           MR-END-DD(PV-WORK-SUB)                       26420054
                      MOVE CURR-END-YY(CURR-INDEX) TO                   26430054
                           MR-END-YY(PV-WORK-SUB)                       26440054
                 END-IF                                                 26450054
              END-IF                                                    26460054
                                                                        26470054
           END-PERFORM.                                                 26480054
           EJECT                                                        26490054
                                                                        26500054
                                                                        26510054
       4100-SAVE-INITIAL-INTERAPPL.                                     26520054
      ******************************************************************26530054
      *    SAVE THE INTER-APPLICATION COMMAREA ON THE FIRST CYCLE       26540054
      *    OF THIS TRANSACTION.                                         26550054
      ******************************************************************26560054
                                                                        26570054
           IF (DP020-MB-MERCH-AREA-LIST) OR                             26580054
              (DP020-MB-WORKER-RSPBL-LIST)                              26590054
              SET PS-INTER-APPL-VALID        TO TRUE                    26600054
              MOVE MB005-LIST-QUEUE-NAME     TO ASC-LIST-TSQ-NAME       26610054
              MOVE MB005-LIST-QUEUE-LENGTH   TO ASC-LIST-TSQ-LENGTH     26620054
              MOVE MB005-CO-NUMBER           TO ASC-COMPANY-NO          26630054
                                                                        26640054
              IF MB005-LIST-QUEUE-LENGTH = 0                            26650054
                                                                        26660054
      **** TRIED TO NAVIGATE TO THIS PROGRAM WITHOUT MAKING SELECTIONS  26670054
      **** FROM THE LIST                                                26680054
                MOVE PC-TSYMSG-99901 TO DP020-MSG-NUMBER                26690054
                SET DP020-MSG-FATAL  TO TRUE                            26700054
                SET PS-INTER-APPL-INVALID TO TRUE                       26710054
                MOVE -1 TO ANAVCMDL                                     26720054
              END-IF                                                    26730054
           ELSE                                                         26740054
                MOVE PC-TSYMSG-99901 TO DP020-MSG-NUMBER                26750054
                SET DP020-MSG-FATAL  TO TRUE                            26760054
                SET PS-INTER-APPL-INVALID TO TRUE                       26770054
                MOVE -1 TO ANAVCMDL                                     26780054
           END-IF.                                                      26790054
                                                                        26800054
      ******************************************************************26850054
      ******************************************************************26860054
       EJECT                                                            26870054
                                                                        26880054
       4300-SETUP-INTER-APPL-COMM.                                      26890054
                                                                        26900054
      ******************************************************************26910054
      *                                                                 26920054
      ******************************************************************26930054
                                                                        26940054
           SET DP020-MB-MERCH-DEPT-MAINT TO TRUE                        26950054
           MOVE ASC-LIST-TSQ-NAME      TO MB005-LIST-QUEUE-NAME.        26960054
           MOVE ASC-LIST-TSQ-LENGTH    TO MB005-LIST-QUEUE-LENGTH.      26970054
           MOVE ASC-COMPANY-NO         TO MB005-CO-NUMBER.              26980054
           MOVE ASC-ALPHA-MDSEAR-LEVEL TO MB005-MDSEAR-LEVEL.           26990054
           COMPUTE MB005-NUMBER-PROCESSED-ITEMS =                       27000054
              (( ASC-LIST-TSQ-ITEM-NBR - 1) * ASC-LIST-TSQ-MAX-ITEMS)   27010054
               + ASC-LIST-TSQ-IDX.                                      27020054
                                                                        27030054
       5500-MANIPULATE-INPUT.                                           27040054
      ******************************************************************27050054
      *  SORT THE COMMAREA TABLE IN RESPONSIBILITY/ START DATE          27060054
      *  ORDER.                                                         27070054
      *                                                                 27080054
      *  PERFORMED BY 6000-PROCESS-MAINTAINANCE.                        27090054
      ******************************************************************27100054
                                                                        27110054
           COMPUTE PV-NUMBER-COMPARISONS = ASC-LINES-ENTERED - 1        27120054
           PERFORM 5575-SORT-LINE-TABLE                                 27130054
              UNTIL PS-SORT-COMPLETE OR                                 27140054
                    PV-NUMBER-COMPARISONS = ZERO.                       27150054
                                                                        27160054
           EJECT                                                        27170054
                                                                        27180054
       5575-SORT-LINE-TABLE.                                            27190054
      ******************************************************************27200054
      *  SORT THE COMMAREA TABLE IN RESPONSIBILITY/ START DATE          27210054
      *  ORDER.                                                         27220054
      *                                                                 27230054
      *  PERFORMED BY 5500-MANIPULATE-INPUT.                            27240054
      ******************************************************************27250054
           SET CURR-INDEX TO 1.                                         27260054
           SET PS-SORT-COMPLETE TO TRUE.                                27270054
           PERFORM 5580-COMPARE-KEYS                                    27280054
              UNTIL CURR-INDEX > PV-NUMBER-COMPARISONS.                 27290054
           SUBTRACT 1 FROM PV-NUMBER-COMPARISONS.                       27300054
                                                                        27310054
       5580-COMPARE-KEYS.                                               27320054
      ******************************************************************27330054
      *  SORT THE COMMAREA TABLE IN RESPONSIBILITY/ START DATE          27340054
      *  ORDER.                                                         27350054
      *                                                                 27360054
      *  PERFORMED BY 5575-SORT-LINE-TABLE.                             27370054
      ******************************************************************27380054
           IF CURR-LEVEL(CURR-INDEX) > CURR-LEVEL(CURR-INDEX + 1)       27390054
              MOVE CURR-SCREEN-LINE(CURR-INDEX) TO PV-SAVE-SCREEN-LINE  27400054
              MOVE CURR-SCREEN-LINE(CURR-INDEX + 1) TO                  27410054
                   CURR-SCREEN-LINE(CURR-INDEX)                         27420054
              MOVE PV-SAVE-SCREEN-LINE TO                               27430054
                    CURR-SCREEN-LINE(CURR-INDEX + 1)                    27431054
              SET PS-SORT-NOT-COMPLETE TO TRUE                          27440054
           ELSE                                                         27450054
             IF CURR-LEVEL(CURR-INDEX) = CURR-LEVEL(CURR-INDEX + 1)     27460054
                IF CURR-START-DATE-CYMD-N(CURR-INDEX) >                 27470054
                   CURR-START-DATE-CYMD-N(CURR-INDEX + 1)               27480054
                  MOVE CURR-SCREEN-LINE(CURR-INDEX) TO                  27490054
                        PV-SAVE-SCREEN-LINE                             27491054
                  MOVE CURR-SCREEN-LINE(CURR-INDEX + 1) TO              27500054
                       CURR-SCREEN-LINE(CURR-INDEX)                     27510054
                  MOVE PV-SAVE-SCREEN-LINE TO                           27520054
                           CURR-SCREEN-LINE(CURR-INDEX + 1)             27530054
                  SET PS-SORT-NOT-COMPLETE TO TRUE                      27540054
                END-IF                                                  27550054
             END-IF                                                     27560054
           END-IF.                                                      27570054
           SET CURR-INDEX UP BY 1.                                      27580054
                                                                        27590054
       EJECT                                                            27600054
                                                                        27610054
       6000-PROCESS-MAINTAINANCE.                                       27620054
                                                                        27630054
      ******************************************************************27640054
      *  THIS PARAGRAPH WILL EDIT THE COVERAGES ON THE PRIMARY          27650054
      *  RESPONSIBILITY FOR THIS MERCHANDISE AREA. IF THERE ARE NOT     27660054
      *  ANY ERRORS, DETERMINE WHICH ROWS SHOULD BE ADDED, AND WHICH    27670054
      *  SHOULD BE UPDATED. PERFORM UPDATES/ADDS.                       27680054
      ******************************************************************27690054
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                       27700054
           MOVE DP017-EDITED-DATE-YYYYMMDD TO  DK-END-DATE              27710054
                                                                        27720054
           PERFORM 5500-MANIPULATE-INPUT.                               27730054
                                                                        27740054
           PERFORM 4001-SET-UP-SCREEN.                                  27750054
                                                                        27760054
           PERFORM 4065-EDIT-ALL-END-DATES.                             27770054
                                                                        27780054
           IF PS-NO-ERROR                                               27790054
              PERFORM 6050-CHECK-DB2-DUPLICATES                         27800054
           END-IF.                                                      27810054
                                                                        27820054
           IF PS-NO-ERROR                                               27830054
              PERFORM 6200-EDIT-COVERAGES                               27840054
           END-IF.                                                      27850054
                                                                        27860054
           IF PS-NO-ERROR                                               27870054
              PERFORM 6100-DETERMINE-UPDATES                            27880054
              PERFORM 6150-COMMIT                                       27890054
              INITIALIZE PV-CURR-TSQ-AREA                               27900054
                         PV-LIST-TSQ-AREA                               27910054
              PERFORM 2403-RETRIEVE-DATA-FOR-TABLE                      27920054
              PERFORM 4001-SET-UP-SCREEN                                27930054
              MOVE PV-CURR-TSQ-AREA TO PV-ORIG-TSQ-AREA                 27940054
              PERFORM 9512-REWRITE-ORIG-TSQ                             27960054
           END-IF.                                                      27970054
                                                                        27980054
       EJECT                                                            27990054
                                                                        28000054
                                                                        28010054
       6050-CHECK-DB2-DUPLICATES.                                       28020054
      ******************************************************************28030054
      * - INSURE THAT THE RESPONSIBILITIES ON THE SCREEN WILL NOT       28040054
      *   CREATE A DUPLICATE KEY SITUATION ON THE DB2 TRSPBAR TABLE     28050054
      *                                                                 28060054
      *   PERFORMED BY: 6000-PROCESS-MAINTAINANCE                       28070054
      ******************************************************************28080054
           PERFORM VARYING CURR-INDEX                                   28090054
                   FROM 1 BY 1                                          28100054
                   UNTIL (CURR-INDEX > ASC-LINES-ENTERED)               28110054
              MOVE CURR-LEVEL(CURR-INDEX)        TO DK-RSPLVL-CDE       28120054
              MOVE ASC-MDSEAR-DKEY               TO DK-MDSEAR-DKEY      28130054
              MOVE CURR-USERID(CURR-INDEX)       TO DK-WORKER-UID       28140054
              MOVE ASC-COMPANY-NO                TO DK-OPERCO-NBR       28150054
              PERFORM 9015-GET-WORKER-NAME                              28160054
              IF SQLCODE = +0                                           28170054
                 CONTINUE                                               28180054
              ELSE                                                      28190054
                   SET DP013-LOGIC-ABEND TO TRUE                        28200054
                   SET DP013-ROLLBACK TO TRUE                           28210054
                                                                        28220054
                   MOVE '6050-GET-RPSBAR-ROW' TO                        28230054
                          DP013-PARAGRAPH                               28240054
                   MOVE 'TRIED TO GET WORKER NAME - INVALID' TO         28250054
                             DP013-MESSAGE-TEXT(1)                      28260054
                   STRING   'WORKER : ' DK-WORKER-UID                   28270054
                            DELIMITED BY SIZE                           28280054
                            INTO DP013-MESSAGE-TEXT(2)                  28290054
                   PERFORM DP013-0000-PROCESS-ABEND                     28300054
              END-IF                                                    28310054
                                                                        28320054
              MOVE WORKER-NBR            TO CURR-WORKER-NBR(CURR-INDEX) 28330054
              MOVE WORKER-NBR            TO DK-WORKER-NBR               28340054
                                                                        28350054
              MOVE CURR-START-CYMD-CC (CURR-INDEX) TO PV-CHANGE-CENT    28360054
              MOVE CURR-START-YY(CURR-INDEX)     TO PV-CHANGE-YY        28370054
              MOVE CURR-START-MM(CURR-INDEX)     TO PV-CHANGE-MM        28380054
              MOVE CURR-START-DD(CURR-INDEX)     TO PV-CHANGE-DD        28390054
              MOVE PV-CHANGE-DATE                TO DK-START-DATE       28400054
              PERFORM 9410-GET-RSPBAR-ROW                               28410054
              MOVE CURR-ORIGINAL-RECORD-SW(CURR-INDEX) TO PV-TEMP-FIELD 28420054
              IF (CURR-ORIG-RECORD(CURR-INDEX) AND PV-ROW-COUNT = 1)    28430054
                 IF (CURR-START-DATE-CYMD-N(CURR-INDEX) =               28440054
                     CURR-ORIG-START-DATE(CURR-INDEX))                  28450054
                     CONTINUE                                           28460054
                 ELSE                                                   28470054
                    SET PV-TEMP-SUB TO CURR-INDEX                       28480054
                    PERFORM 6206-SETUP-NEW-PAGE                         28490054
                    MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)      28500054
                                           MR-WORKER-C(PV-WORK-SUB)     28510054
                    MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)      28520054
                                           MR-WORKER-H(PV-WORK-SUB)     28530054
                    MOVE -1             TO MR-STRT-MM-L(PV-WORK-SUB)    28540054
                    MOVE PC-TSYMSG-00182 TO DP020-MSG-NUMBER            28550054
                    SET PS-ERROR TO TRUE                                28560054
                    SET DP020-MSG-FATAL TO TRUE                         28570054
                 END-IF                                                 28580054
              ELSE                                                      28590054
                 IF (CURR-ORIG-RECORD(CURR-INDEX) AND PV-ROW-COUNT > 1) 28600054
                       SET PV-TEMP-SUB TO CURR-INDEX                    28610054
                       PERFORM 6206-SETUP-NEW-PAGE                      28620054
                       MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)   28630054
                                              MR-WORKER-C(PV-WORK-SUB)  28640054
                       MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)   28650054
                                              MR-WORKER-H(PV-WORK-SUB)  28660054
                       MOVE -1             TO MR-WORKER-L(PV-WORK-SUB)  28670054
                       MOVE PC-TSYMSG-00182 TO DP020-MSG-NUMBER         28680054
                       SET PS-ERROR TO TRUE                             28690054
                       SET DP020-MSG-FATAL TO TRUE                      28700054
                 ELSE                                                   28710054
                    IF CURR-NEW-RECORD(CURR-INDEX) AND PV-ROW-COUNT > 0 28720054
                        SET PV-TEMP-SUB TO CURR-INDEX                   28730054
                        PERFORM 6206-SETUP-NEW-PAGE                     28740054
                        MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)  28750054
                                               MR-WORKER-C(PV-WORK-SUB) 28760054
                        MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)  28770054
                                               MR-WORKER-H(PV-WORK-SUB) 28780054
                        MOVE -1             TO MR-WORKER-L(PV-WORK-SUB) 28790054
                        MOVE PC-TSYMSG-00182 TO DP020-MSG-NUMBER        28800054
                        SET PS-ERROR TO TRUE                            28810054
                        SET DP020-MSG-FATAL TO TRUE                     28820054
                    END-IF                                              28830054
                 END-IF                                                 28840054
              END-IF                                                    28850054
           END-PERFORM.                                                 28860054
                                                                        28870054
       6100-DETERMINE-UPDATES.                                          28880054
      ******************************************************************28890054
      * - READ ORIGINAL TSQ BUILT IN BUILD INITIAL PANEL                28900054
      * - COMPARE WITH DATA NOW ON SCREEN (TABLE IN ASC)                28910054
      * - IF THERE HAVE BEEN CHANGES TO ORIGINAL DATA, DETERMINE WHETHER28920054
      *   AN UPDATE OR INSERT IS APPROPRIATE AND TAKE THAT ACTION.      28930054
      *                                                                 28940054
      *   PERFORMED BY: 6000-PROCESS-MAINTAINANCE                       28950054
      ******************************************************************28960054
                                                                        28970054
           PERFORM 9510-READ-ORIG-TSQ.                                  28980054
                                                                        28990054
           SET CURR-INDEX TO 1.                                         29000054
           SET ORIG-INDEX TO 1.                                         29010054
                                                                        29020054
           PERFORM 6101-MATCH-RECORDS                                   29030054
           VARYING CURR-INDEX FROM 1 BY 1                               29040054
           UNTIL (CURR-INDEX > ASC-LINES-ENTERED).                      29050054
                                                                        29060054
           IF (PS-NO-ERROR) AND (PS-UPDATE-ROW)                         29070054
              MOVE PC-TSYMSG-00059 TO DP020-MSG-NUMBER                  29080054
              SET DP020-MSG-INFORMATIONAL TO TRUE                       29090054
           ELSE                                                         29100054
              IF PS-NO-ERROR                                            29110054
                 MOVE PC-TSYMSG-00046 TO DP020-MSG-NUMBER               29120054
                 SET DP020-MSG-INFORMATIONAL TO TRUE                    29130054
              END-IF                                                    29140054
           END-IF.                                                      29150054
           EJECT                                                        29160054
                                                                        29170054
                                                                        29180054
       6101-MATCH-RECORDS.                                              29190054
      ******************************************************************29200054
      *  SEARCH THE ORIG TSQ TABLE TO DETERMINE IF ROW ON SCREEN        29210054
      *  ALREADY EXISTS ON DATABASE. IF IT IS ALREADY ON DATABASE,      29220054
      *  DETERMINE IF ANY CHANGES WERE MADE TO THE ROW. IF SO, UPDATE   29230054
      *  IF RECORD IS NEW, ADD IT.                                      29240054
      *                                                                 29250054
      *   PERFORMED BY: 6100-DETERMINE-UPDATES.                         29260054
      ******************************************************************29270054
           SET ORIG-INDEX TO 1.                                         29280054
                                                                        29290054
           SEARCH ORIG-SCREEN-LINE                                      29300054
               VARYING ORIG-INDEX                                       29310054
              AT END                                                    29320054
                 SET PS-RECORD-NOT-FOUND TO TRUE                        29330054
              WHEN                                                      29340054
                 ORIG-LEVEL(ORIG-INDEX) = CURR-LEVEL(CURR-INDEX) AND    29350054
                 ORIG-USERID(ORIG-INDEX) = CURR-USERID(CURR-INDEX)      29360054
                                                                        29370054
                 SET PS-RECORD-FOUND TO TRUE                            29380054
                 IF (CURR-END-DATE-CYMD(CURR-INDEX) =                   29390054
                     ORIG-END-DATE-CYMD(ORIG-INDEX)) AND                29400054
                    (CURR-START-DATE-CYMD(CURR-INDEX) =                 29410054
                     ORIG-START-DATE-CYMD(ORIG-INDEX))                  29420054
                    CONTINUE                                            29430054
                 ELSE                                                   29440054
                    SET PS-UPDATE-ROW TO TRUE                           29450054
                    PERFORM 7000-UPDATE-RSPBAR                          29460054
                 END-IF                                                 29470054
           END-SEARCH.                                                  29480054
                                                                        29490054
           IF PS-RECORD-NOT-FOUND                                       29500054
              IF CURR-NEW-RECORD(CURR-INDEX)                            29510054
                 PERFORM 7010-INSERT-RESPONSIBILITY                     29520054
                 SET PS-UPDATE-ROW TO TRUE                              29530054
              END-IF                                                    29540054
           END-IF.                                                      29550054
       6150-COMMIT.                                                     29560054
      ******************************************************************29570054
      *                                                                 29580054
      ******************************************************************29590054
                                                                        29600054
               EXEC CICS                                                29610054
                   SYNCPOINT                                            29620054
               END-EXEC.                                                29630054
                                                                        29640054
           EJECT                                                        29650054
       6200-EDIT-COVERAGES.                                             29660054
      ******************************************************************29670054
      *  INSURE THAT ALL GMMS DMMS AND BUYERS START AND END DATES       29680054
      *  COVER THE ENTIRE MERCHANDISE AREA START THRU END DATES.        29690054
      ******************************************************************29700054
                                                                        29710054
           IF ASC-UPD-GMA                                               29720054
              MOVE PC-GMM-LEVEL TO PV-RSPLVL-TO-FIND                    29730054
           ELSE                                                         29740054
              IF ASC-UPD-DMA                                            29750054
                 MOVE PC-DMM-LEVEL TO PV-RSPLVL-TO-FIND                 29760054
              ELSE                                                      29770054
                 MOVE PC-BYR-LEVEL TO PV-RSPLVL-TO-FIND                 29780054
              END-IF                                                    29790054
           END-IF.                                                      29800054
                                                                        29810054
           SET PS-NEED-NO-MATCH TO TRUE                                 29820054
           INITIALIZE PV-PREV-LEVEL.                                    29830054
           PERFORM 6201-PROCESS-COVERAGE                                29840054
              VARYING CURR-INDEX FROM 1                                 29850054
              BY 1                                                      29860054
              UNTIL (PS-RSPLVL-FOUND) OR                                29870054
                    (CURR-INDEX > ASC-LINES-ENTERED).                   29880054
                                                                        29890054
       6201-PROCESS-COVERAGE.                                           29900054
      ******************************************************************29910054
      *  THIS PARAGRAPH FINDS THE FIRST OCCURRENCE OF THE ASSIGNMENT    29920054
      *  AND STARTS THE PROCESS TO DETERMINE IF THERE IS FULL COVERAGE  29930054
      *  FOR THE ASSIGNMENT LEVEL.                                      29940054
      ******************************************************************29950054
           SET PS-NEED-NO-MATCH TO TRUE                                 29960054
           IF CURR-LEVEL(CURR-INDEX) = PV-RSPLVL-TO-FIND                29970054
                                                                        29980054
              SET PS-RSPLVL-FOUND TO TRUE                               29990054
              PERFORM 6202-CHECK-START-DATE                             30000054
                                                                        30010054
              IF PS-NO-ERROR                                            30020054
                 PERFORM 6203-CHECK-END-DATE                            30030054
              END-IF                                                    30040054
                                                                        30050054
           END-IF.                                                      30060054
           EJECT                                                        30070054
                                                                        30080054
                                                                        30090054
       6202-CHECK-START-DATE.                                           30100054
      ******************************************************************30110054
      *  IF THE HISTORY EXISTS FLAG IS YES, THEN THERE IS NO NEED TO    30120054
      *  CHECK THE START DATE - IT CANNOT CHANGE. OTHERWISE, THE        30130054
      *  START DATE NEEDS TO MATCH THE PARENT START DATE.               30140054
      *  PROCESS THE START DATE OF THE FIRST OCCURRENCE OF THE BYR,     30150054
      *  DMM, OR GMM ASSIGNMENT.                                        30160054
      ******************************************************************30170054
           IF CURR-HISTORY-EXISTS(CURR-INDEX)                           30180054
              CONTINUE                                                  30190054
           ELSE                                                         30200054
              IF CURR-START-DATE-CYMD-N(CURR-INDEX) NOT =               30210054
                 ASC-LEVEL-START-DATE-CYMD                              30220054
                 SET PS-ERROR,                                          30230054
                     DP020-MSG-FATAL TO TRUE                            30240054
                 SET PV-TEMP-SUB TO CURR-INDEX                          30250054
                 PERFORM 6206-SETUP-NEW-PAGE                            30260054
                 MOVE DP015-RED      TO MR-STRT-MM-C(PV-WORK-SUB)       30270054
                                        MR-STRT-DD-C(PV-WORK-SUB)       30280054
                                        MR-STRT-YY-C(PV-WORK-SUB)       30290054
                 MOVE DP015-REVERSE  TO MR-STRT-MM-H(PV-WORK-SUB)       30300054
                                        MR-STRT-DD-H(PV-WORK-SUB)       30310054
                                        MR-STRT-YY-H(PV-WORK-SUB)       30320054
                 MOVE -1             TO MR-STRT-MM-L(PV-WORK-SUB)       30330054
                 MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER               30340054
                 MOVE CURR-LEVEL(CURR-INDEX) TO PV-PREV-LEVEL           30350054
              END-IF                                                    30360054
           END-IF.                                                      30370054
                                                                        30380054
                                                                        30390054
       6203-CHECK-END-DATE.                                             30400054
      ******************************************************************30410054
      * THIS ROUTINE EDITS THE END DATE OF THE FIRST RECORD FOUND       30420054
      * FOR THE RESPONSIBILITY LEVEL. IT THEN STARTS PROCESSING ALL     30430054
      * OTHER ENTRIES ON THE SCREEN THAT ARE FOR THE SAME LEVEL.        30440054
      ******************************************************************30450054
                                                                        30460054
           IF CURR-END-DATE-CYMD-N(CURR-INDEX) NOT =                    30470054
                            ASC-LEVEL-END-DATE-CYMD                     30480054
              SET PS-NEED-MATCH TO TRUE                                 30490054
           END-IF.                                                      30500054
                                                                        30510054
           SET CURR-INDEX2 TO CURR-INDEX                                30520054
           SET CURR-INDEX2 UP BY 1                                      30530054
                                                                        30540054
      **************************************************************    30550054
      *     LAST RECORD ON SCREEN HAS AN END DATE THAT DOES NOT         30560054
      *     MATCH THE MERCHANDISE AREA LEVEL END DATE                   30570054
      *     END DATE ON CURR-INDEX ROW IS NOT FULL COVERAGE             30580054
      **************************************************************    30590054
                                                                        30600054
           IF (CURR-INDEX2 > ASC-LINES-ENTERED) AND                     30610054
              (PS-NEED-MATCH)                                           30620054
              SET PS-ERROR, PS-DONE,                                    30630054
                  DP020-MSG-FATAL TO TRUE                               30640054
              SET PV-TEMP-SUB TO CURR-INDEX                             30650054
              PERFORM 6206-SETUP-NEW-PAGE                               30660054
              MOVE DP015-RED      TO MR-END-MM-C(PV-WORK-SUB)           30670054
                                     MR-END-DD-C(PV-WORK-SUB)           30680054
                                     MR-END-YY-C(PV-WORK-SUB)           30690054
              MOVE DP015-REVERSE  TO MR-END-MM-H(PV-WORK-SUB)           30700054
                                     MR-END-DD-H(PV-WORK-SUB)           30710054
                                     MR-END-YY-H(PV-WORK-SUB)           30720054
              MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)           30730054
              MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER                  30740054
           ELSE                                                         30750054
      ****************************************************************  30760054
      *       PROCESS THE FOLLOWING RECORDS ON THE SCREEN THAT          30770054
      *       MATCH THE CURRENT RESPONSIBILITY LEVEL                    30780054
      *       IF ALREADY HAD END DATE MATCH PARENT END DATE - ERROR     30790054
      ****************************************************************  30800054
                                                                        30810054
               IF CURR-LEVEL(CURR-INDEX2) = PV-RSPLVL-TO-FIND           30820054
                                                                        30830054
                  IF PS-NEED-NO-MATCH                                   30840054
                     SET PS-ERROR,                                      30850054
                         DP020-MSG-FATAL TO TRUE                        30860054
                     SET PV-TEMP-SUB TO CURR-INDEX                      30870054
                                                                        30880054
                     PERFORM 6206-SETUP-NEW-PAGE                        30890054
                     MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)     30900054
                     MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)     30910054
                     MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)    30920054
                     SET PV-TEMP-SUB     TO CURR-INDEX2                 30930054
                                                                        30940054
                     IF (PV-TEMP-SUB <= ASC-BOT-OF-SCREEN) AND          30950054
                        (PV-TEMP-SUB >= ASC-TOP-OF-SCREEN)              30960054
                        COMPUTE PV-WORK-SUB =                           30970054
                          ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)       30980054
                       MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)   30990054
                       MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)   31000054
                     END-IF                                             31010054
                     MOVE PC-TSYMSG-00189 TO DP020-MSG-NUMBER           31020054
                                                                        31030054
                  ELSE                                                  31040054
                                                                        31050054
      **********************************************************        31060054
      *       PROCESS ALL OTHER MATCHING RESPONSIBILITY LEVELS *        31070054
      **********************************************************        31080054
                                                                        31090054
                     SET PS-NOT-DONE TO TRUE                            31100054
                     SET CURR-INDEX3 TO CURR-INDEX                      31110054
                     PERFORM 6204-CHECK-MATCHES                         31120054
                     UNTIL (CURR-INDEX2 > ASC-LINES-ENTERED) OR         31130054
                             (PS-DONE)                                  31140054
                                                                        31150054
      **************************************************************    31160054
      *       LAST ENTRY ON SCREEN DID NOT HAVE END DATE THAT EQUALS    31170054
      *       MDSEAR END DATE                                           31180054
      **************************************************************    31190054
                                                                        31200054
                     IF (PS-NO-ERROR) AND (PS-NEED-MATCH)               31210054
                         SET PS-ERROR,                                  31220054
                             DP020-MSG-FATAL TO TRUE                    31230054
                         SET PV-TEMP-SUB     TO CURR-INDEX3             31240054
                         PERFORM 6206-SETUP-NEW-PAGE                    31250054
                         MOVE DP015-RED      TO MR-END-MM-C(PV-WORK-SUB)31260054
                                                MR-END-DD-C(PV-WORK-SUB)31270054
                                                MR-END-YY-C(PV-WORK-SUB)31280054
                         MOVE DP015-REVERSE  TO MR-END-MM-H(PV-WORK-SUB)31290054
                                                MR-END-DD-H(PV-WORK-SUB)31300054
                                                MR-END-YY-H(PV-WORK-SUB)31310054
                         MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)31320054
                         MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER       31330054
                     END-IF                                             31340054
                  END-IF                                                31350054
               ELSE                                                     31360054
                  IF PS-NEED-MATCH                                      31370054
                      SET PS-ERROR,                                     31380054
                          DP020-MSG-FATAL TO TRUE                       31390054
                      SET PV-TEMP-SUB     TO CURR-INDEX                 31400054
                      PERFORM 6206-SETUP-NEW-PAGE                       31410054
                      MOVE DP015-RED      TO MR-END-MM-C(PV-WORK-SUB)   31420054
                                             MR-END-DD-C(PV-WORK-SUB)   31430054
                                             MR-END-YY-C(PV-WORK-SUB)   31440054
                      MOVE DP015-REVERSE  TO MR-END-MM-H(PV-WORK-SUB)   31450054
                                             MR-END-DD-H(PV-WORK-SUB)   31460054
                                             MR-END-YY-H(PV-WORK-SUB)   31470054
                      MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)   31480054
                      MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER          31490054
                  END-IF                                                31500054
               END-IF                                                   31510054
           END-IF.                                                      31520054
                                                                        31530054
            EJECT                                                       31540054
                                                                        31550054
       6204-CHECK-MATCHES.                                              31560054
      ******************************************************************31570054
      * TEST END DATE(INDEX3) AGAINST THE START DATE(INDEX2)            31580054
      * INDEX3 = FIRST LEVEL IN TABLE                                   31590054
      * INDEX2 = NEXT LEVEL IN TABLE                                    31600054
      *                                                                 31610054
      ******************************************************************31620054
           IF CURR-LEVEL(CURR-INDEX2) = CURR-LEVEL(CURR-INDEX3)         31630054
              IF PS-NEED-NO-MATCH                                       31640054
                 SET PS-ERROR,                                          31650054
                     DP020-MSG-FATAL TO TRUE                            31660054
                 SET PV-TEMP-SUB     TO CURR-INDEX3                     31670054
                                                                        31680054
                 PERFORM 6206-SETUP-NEW-PAGE                            31690054
                 MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)         31700054
                 MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)         31710054
                 MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)        31720054
                 SET PV-WORK-SUB     TO CURR-INDEX3                     31730054
                                                                        31740054
                 IF ((PV-WORK-SUB >= ASC-TOP-OF-SCREEN) AND             31750054
                     (PV-WORK-SUB <= ASC-BOT-OF-SCREEN))                31760054
                   COMPUTE PV-WORK-SUB =                                31770054
                          ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)       31780054
                   MOVE DP015-RED      TO MR-LEVEL-C(PV-WORK-SUB)       31790054
                   MOVE DP015-REVERSE  TO MR-LEVEL-H(PV-WORK-SUB)       31800054
                 END-IF                                                 31810054
                 MOVE PC-TSYMSG-00189 TO DP020-MSG-NUMBER               31820054
              ELSE                                                      31830054
                 MOVE CURR-END-DATE-CYMD(CURR-INDEX3) TO PV-END-DATE    31840054
                 MOVE CURR-START-DATE-CYMD(CURR-INDEX2) TO PV-START-DATE31850054
                 PERFORM 6205-CHECK-CONSECUTIVE-DATES                   31860054
                 IF PS-DATES-NOT-CONSECUTIVE                            31870054
                    SET PS-DONE,                                        31880054
                        PS-ERROR,                                       31890054
                        DP020-MSG-FATAL TO TRUE                         31900054
                    SET PV-TEMP-SUB     TO CURR-INDEX3                  31910054
                                                                        31920054
                    PERFORM 6206-SETUP-NEW-PAGE                         31930054
                    MOVE DP015-RED      TO MR-END-MM-C(PV-WORK-SUB)     31940054
                                           MR-END-DD-C(PV-WORK-SUB)     31950054
                                           MR-END-YY-C(PV-WORK-SUB)     31960054
                    MOVE DP015-REVERSE  TO MR-END-MM-H(PV-WORK-SUB)     31970054
                                           MR-END-DD-H(PV-WORK-SUB)     31980054
                                           MR-END-YY-H(PV-WORK-SUB)     31990054
                    MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)     32000054
                    SET PV-TEMP-SUB     TO CURR-INDEX2                  32010054
                                                                        32020054
                    IF ((PV-TEMP-SUB >= ASC-TOP-OF-SCREEN) AND          32030054
                        (PV-TEMP-SUB <= ASC-BOT-OF-SCREEN))             32040054
                       COMPUTE PV-WORK-SUB =                            32050054
                             ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1)    32060054
                       MOVE DP015-RED      TO MR-STRT-MM-C(PV-WORK-SUB) 32070054
                                              MR-STRT-DD-C(PV-WORK-SUB) 32080054
                                              MR-STRT-YY-C(PV-WORK-SUB) 32090054
                       MOVE DP015-REVERSE  TO MR-STRT-MM-H(PV-WORK-SUB) 32100054
                                              MR-STRT-DD-H(PV-WORK-SUB) 32110054
                                              MR-STRT-YY-H(PV-WORK-SUB) 32120054
                    END-IF                                              32130054
                    IF PS-DATES-OVERLAP                                 32140054
                       MOVE PC-TSYMSG-00189 TO DP020-MSG-NUMBER         32150054
                    ELSE                                                32160054
                       MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER         32170054
                    END-IF                                              32180054
                 ELSE                                                   32190054
                    IF CURR-END-DATE-CYMD(CURR-INDEX2) =                32200054
                       ASC-LEVEL-END-DATE-CYMD                          32210054
                       SET PS-NEED-NO-MATCH TO TRUE                     32220054
                    END-IF                                              32230054
                 END-IF                                                 32240054
              END-IF                                                    32250054
           ELSE                                                         32260054
                                                                        32270054
              SET PS-DONE TO TRUE                                       32280054
              SET PS-NEED-NO-MATCH TO TRUE                              32290054
                                                                        32300054
              IF CURR-END-DATE-CYMD(CURR-INDEX3) =                      32310054
                 ASC-LEVEL-END-DATE-CYMD                                32320054
                 SET PS-NEED-NO-MATCH TO TRUE                           32330054
              ELSE                                                      32340054
      *  END DATE DOES NOT COVER MERCHANDISE AREA END DATE *            32350054
                 SET PS-ERROR,                                          32360054
                     DP020-MSG-FATAL TO TRUE                            32370054
                 SET PV-TEMP-SUB     TO CURR-INDEX3                     32380054
                 PERFORM 6206-SETUP-NEW-PAGE                            32390054
                 MOVE DP015-RED      TO MR-END-MM-C(PV-WORK-SUB)        32400054
                                        MR-END-DD-C(PV-WORK-SUB)        32410054
                                        MR-END-YY-C(PV-WORK-SUB)        32420054
                 MOVE DP015-REVERSE  TO MR-END-MM-H(PV-WORK-SUB)        32430054
                                        MR-END-DD-H(PV-WORK-SUB)        32440054
                                        MR-END-YY-H(PV-WORK-SUB)        32450054
                 MOVE -1             TO MR-END-MM-L(PV-WORK-SUB)        32460054
                 MOVE PC-TSYMSG-00188 TO DP020-MSG-NUMBER               32470054
              END-IF                                                    32480054
           END-IF.                                                      32490054
           SET CURR-INDEX3 UP BY 1.                                     32500054
           SET CURR-INDEX2 UP BY 1.                                     32510054
           EJECT                                                        32520054
                                                                        32530054
                                                                        32540054
       6205-CHECK-CONSECUTIVE-DATES.                                    32550054
      ******************************************************************32560054
      * MAKE SURE THAT THE START DATE ON THE SECOND RECORD IS CONSECUTIV32570054
      * WITH THE END DATE ON THE FIRST RECORD.                          32580054
      *                                                                 32590054
      ******************************************************************32600054
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         32610054
                                                                        32620054
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                    32630054
                                                                        32640054
           SET DPG52-LK-DTE-ISO             TO TRUE.                    32650054
           MOVE PV-START-DATE TO DPG52-LK-DATE-INPUT.                   32660054
                                                                        32670054
           SET DPG53-LK-CMPR-DTE-ISO        TO TRUE.                    32680054
           MOVE PV-END-DATE-X     TO DPG53-LK-CMPR-DATE-INPUT.          32690054
                                                                        32700054
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                           32710054
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.        32720054
                                                                        32730054
           IF DPG54-NO-ERROR                                            32740054
              IF DPG55-DAYS-DIFFERENCE = PC-CONSECUTIVE-DAYS            32750054
                 SET PS-DATES-CONSECUTIVE TO TRUE                       32760054
              ELSE                                                      32770054
                 SET PS-DATES-NOT-CONSECUTIVE TO TRUE                   32780054
                 IF DPG55-DAYS-DIFFERENCE < PC-CONSECUTIVE-DAYS         32790054
                    SET PS-DATES-OVERLAP TO TRUE                        32800054
                 ELSE                                                   32810054
                    SET PS-DATES-GAP     TO TRUE                        32820054
                 END-IF                                                 32830054
              END-IF                                                    32840054
           ELSE                                                         32850054
              SET DP013-XCTL-DISPLAY-RESTART                            32860054
                  DP013-LOGIC-ABEND TO TRUE                             32870054
              MOVE '6205-CHECK-CONSECUTIVE-DATES' TO                    32880054
                   DP013-PARAGRAPH                                      32890054
              MOVE 'INVALID DATES IN DATE COMPARE' TO                   32900054
                   DP013-MESSAGE-TEXT(1)                                32910054
              MOVE DPG54-ERROR-MESSAGE TO  DP013-MESSAGE-TEXT(2)        32920054
              PERFORM DP013-0000-PROCESS-ABEND                          32930054
           END-IF.                                                      32940054
           EJECT                                                        32950054
                                                                        32960054
       6206-SETUP-NEW-PAGE.                                             32970054
      ******************************************************************32980054
      * THIS ROUTINE MOVES DISPLAY FROM THE CURRENT PAGE TO THE         32990054
      * PAGE WITH THE ERROR ON IT.                                      33000054
      ******************************************************************33010054
                                                                        33020054
           IF ((PV-TEMP-SUB >= ASC-TOP-OF-SCREEN) AND                   33030054
               (PV-TEMP-SUB <= ASC-BOT-OF-SCREEN))                      33040054
              CONTINUE                                                  33050054
           ELSE                                                         33060054
              PERFORM 6207-SET-NEW-TOP-SCREEN                           33070054
           END-IF                                                       33080054
           COMPUTE PV-WORK-SUB =                                        33090054
                ((PV-TEMP-SUB - ASC-TOP-OF-SCREEN) + 1).                33100054
                                                                        33110054
       6207-SET-NEW-TOP-SCREEN.                                         33120054
      ******************************************************************33130054
      * THIS ROUTINE MOVES DISPLAY FROM THE CURRENT PAGE TO THE         33140054
      * PAGE WITH THE ERROR ON IT.                                      33150054
      ******************************************************************33160054
      *                                                                 33170054
      ** ERROR IS ON PREVIOUS PAGE                                      33180054
      *                                                                 33190054
           IF PV-TEMP-SUB < ASC-TOP-OF-SCREEN                           33200054
              PERFORM UNTIL PV-TEMP-SUB >= ASC-TOP-OF-SCREEN            33210054
                  COMPUTE ASC-TOP-OF-SCREEN = ASC-TOP-OF-SCREEN -       33220054
                          PC-MAX-SCREEN-LINES                           33230054
              END-PERFORM                                               33240054
           ELSE                                                         33250054
      *                                                                 33260054
      ** ERROR IS ON NEXT PAGE                                          33270054
      *                                                                 33280054
              PERFORM UNTIL PV-TEMP-SUB <= ASC-BOT-OF-SCREEN            33290054
                  COMPUTE ASC-TOP-OF-SCREEN = ASC-TOP-OF-SCREEN +       33300054
                          PC-MAX-SCREEN-LINES                           33310054
                  COMPUTE ASC-BOT-OF-SCREEN = ASC-BOT-OF-SCREEN +       33320054
                          PC-MAX-SCREEN-LINES                           33330054
              END-PERFORM                                               33340054
           END-IF.                                                      33350054
                                                                        33360054
           PERFORM 4001-SET-UP-SCREEN.                                  33370054
           EJECT                                                        33380054
                                                                        33390054
       7000-UPDATE-RSPBAR.                                              33400054
                                                                        33410054
      ******************************************************************33420054
      *  THIS PARAGRAPH WILL INSERT A RESPONSIBILITY AREA ROW TO THE    33430054
      *  TRSPBAR TABLE FOR EVERY ASSIGNMENT ON THE SCREEN.              33440054
      *                                                                 33450054
      *  PERFORMED BY : 6000-PROCESS-MAINTAINANCE                       33460054
      ******************************************************************33470054
                                                                        33480054
            MOVE ASC-COMPANY-NO        TO DK-OPERCO-NBR.                33490054
            MOVE CURR-LEVEL(CURR-INDEX) TO DK-RSPLVL-CDE.               33500054
            MOVE CURR-WORKER-NBR(CURR-INDEX) TO DK-WORKER-NBR.          33510054
            MOVE ASC-MDSEAR-DKEY       TO DK-MDSEAR-DKEY.               33520054
                                                                        33530054
            IF CURR-END-DATE(CURR-INDEX) = SPACES                       33540054
               MOVE PC-INFINITY-DATE  TO                                33550054
                       CURR-END-DATE-CYMD-N(CURR-INDEX)                 33560054
            END-IF.                                                     33570054
                                                                        33580054
            MOVE CURR-START-CYMD-CC(CURR-INDEX) TO PV-CHANGE-CENT.      33590054
            MOVE CURR-START-MM(CURR-INDEX)  TO PV-CHANGE-MM.            33600054
            MOVE CURR-START-DD(CURR-INDEX)  TO PV-CHANGE-DD.            33610054
            MOVE CURR-START-YY(CURR-INDEX)  TO PV-CHANGE-YY.            33620054
            MOVE PV-CHANGE-DATE           TO RSPBAR-WORKER-STRT-DTE.    33630054
                                                                        33640054
            MOVE CURR-ORIG-START-DATE(CURR-INDEX) TO PV-TEMP-DATE-NUM.  33650054
            MOVE PV-TMP-STRT-CC                 TO PV-CHANGE-CENT.      33660054
            MOVE PV-TMP-STRT-YY                 TO PV-CHANGE-YY.        33670054
            MOVE PV-TMP-STRT-MM                 TO PV-CHANGE-MM.        33680054
            MOVE PV-TMP-STRT-DD                 TO PV-CHANGE-DD.        33690054
            MOVE PV-CHANGE-DATE                 TO DK-START-DATE.       33700054
                                                                        33710054
            MOVE CURR-END-CYMD-CC(CURR-INDEX) TO PV-CHANGE-CENT.        33720054
            MOVE CURR-END-MM(CURR-INDEX)  TO PV-CHANGE-MM.              33730054
            MOVE CURR-END-DD(CURR-INDEX)  TO PV-CHANGE-DD.              33740054
            MOVE CURR-END-YY(CURR-INDEX)  TO PV-CHANGE-YY.              33750054
            MOVE PV-CHANGE-DATE           TO RSPBAR-WORKER-END-DTE.     33760054
            PERFORM 9100-UPDATE-RSPBAR-ROW.                             33770054
           EJECT                                                        33780054
                                                                        33790054
                                                                        33800054
       7010-INSERT-RESPONSIBILITY.                                      33810054
                                                                        33820054
      ******************************************************************33830054
      *  THIS PARAGRAPH WILL INSERT A RESPONSIBILITY AREA ROW TO THE    33840054
      *  TRSPBAR TABLE FOR EVERY ASSIGNMENT ON THE SCREEN.              33850054
      *                                                                 33860054
      *  PERFORMED BY : 6000-PROCESS-MAINTAINANCE                       33870054
      ******************************************************************33880054
                                                                        33890054
            MOVE ASC-MDSEAR-DKEY          TO RSPBAR-MDSEAR-DKEY.        33900054
            MOVE ASC-COMPANY-NO           TO RSPBAR-OPERCO-NBR          33910054
                                             DK-OPERCO-NBR.             33920054
            MOVE CURR-USERID(CURR-INDEX)  TO DK-WORKER-UID.             33930054
            PERFORM 9015-GET-WORKER-NAME.                               33940054
            IF SQLCODE = +0                                             33950054
               CONTINUE                                                 33960054
            ELSE                                                        33970054
                 SET DP013-LOGIC-ABEND TO TRUE                          33980054
                 SET DP013-ROLLBACK TO TRUE                             33990054
                                                                        34000054
                 MOVE '7010-INSERT-RESPONSIBILITY' TO                   34010054
                        DP013-PARAGRAPH                                 34020054
                 MOVE 'TRIED TO GET WORKER NAME - INVALID' TO           34030054
                           DP013-MESSAGE-TEXT(1)                        34040054
                 STRING   'WORKER : ' DK-WORKER-UID                     34050054
                          DELIMITED BY SIZE                             34060054
                          INTO DP013-MESSAGE-TEXT(2)                    34070054
                 PERFORM DP013-0000-PROCESS-ABEND                       34080054
            END-IF.                                                     34090054
            MOVE WORKER-NBR               TO RSPBAR-WORKER-NBR.         34100054
            MOVE CURR-LEVEL(CURR-INDEX)   TO RSPBAR-RSPLVL-CDE.         34110054
            MOVE CURR-START-CYMD-CC(CURR-INDEX) TO PV-CHANGE-CENT.      34120054
            MOVE CURR-START-MM(CURR-INDEX) TO PV-CHANGE-MM.             34130054
            MOVE CURR-START-DD(CURR-INDEX) TO PV-CHANGE-DD.             34140054
            MOVE CURR-START-YY(CURR-INDEX) TO PV-CHANGE-YY.             34150054
            MOVE PV-CHANGE-DATE           TO RSPBAR-WORKER-STRT-DTE.    34160054
                                                                        34170054
            IF CURR-END-DATE(CURR-INDEX) = SPACES                       34180054
               MOVE PC-INFINITY-DATE  TO                                34190054
                       CURR-END-DATE-CYMD-N(CURR-INDEX)                 34200054
            END-IF.                                                     34210054
                                                                        34220054
            MOVE CURR-END-CYMD-CC(CURR-INDEX) TO PV-CHANGE-CENT.        34230054
            MOVE CURR-END-MM(CURR-INDEX)  TO PV-CHANGE-MM.              34240054
            MOVE CURR-END-DD(CURR-INDEX)  TO PV-CHANGE-DD.              34250054
            MOVE CURR-END-YY(CURR-INDEX)  TO PV-CHANGE-YY.              34260054
            MOVE PV-CHANGE-DATE           TO RSPBAR-WORKER-END-DTE.     34270054
            MOVE CURR-FIRST-NAME(CURR-INDEX) TO RSPBAR-WORKER-FIR-NAME. 34280054
            MOVE CURR-LAST-NAME(CURR-INDEX) TO RSPBAR-WORKER-LAST-NAME. 34290054
            PERFORM 9022-INSERT-RSPBAR-ROW.                             34300054
                                                                        34310054
           EJECT                                                        34320054
                                                                        34330054
       9000-GET-RESPBL-LEVEL.                                           34340054
      ******************************************************************34350054
      *                                                                 34360054
      ******************************************************************34370054
                                                                        34380054
           PERFORM WITH TEST AFTER                                      34390054
               VARYING PV-RETRY-COUNTER                                 34400054
               FROM 1 BY 1                                              34410054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              34420054
                       OR (SQLCODE = 0)                                 34430054
                       OR (SQLCODE = +100))                             34440054
                                                                        34450054
                                                                        34460054
               EXEC SQL                                                 34470054
                   SELECT RSPLVL_CDE                                    34480054
                   INTO :RSPLVL-CDE                                     34490054
                   FROM   TRSPLVL                                       34500054
                   WHERE (RSPLVL_CDE  = :DK-RSPLVL-CDE)                 34510054
               END-EXEC                                                 34520054
                                                                        34530054
               EVALUATE TRUE                                            34540054
                   WHEN SQLCODE = ZERO                                  34550054
                       SET PS-ROW-FOUND TO TRUE                         34560054
                   WHEN SQLCODE = -904                                  34570054
                   WHEN SQLCODE = -913                                  34580054
                        CONTINUE                                        34590054
                   WHEN SQLCODE = +100                                  34600054
                        CONTINUE                                        34610054
                   WHEN SQLWARN0 NOT = SPACES                           34620054
                   WHEN SQLCODE < 0                                     34630054
                   WHEN SQLCODE > 0                                     34640054
                       STRING ' RESPLVL CODE ' DK-RSPLVL-CDE            34650054
                              DELIMITED BY SIZE                         34660054
                              INTO DP013-MESSAGE-TEXT(2)                34670054
                       MOVE SQLCA TO DP013-SQLCA                        34680054
                       MOVE '9000-GET-RESPBLVL-CODE' TO                 34690054
                           DP013-PARAGRAPH                              34700054
                       MOVE 'TRSPLVL' TO DP013-DB2-TABLE-NAME(2)        34710054
                       SET DP013-DB2-ABEND TO TRUE                      34720054
                       SET DP013-NO-ROLLBACK TO TRUE                    34730054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           34740054
                       PERFORM DP013-0000-PROCESS-ABEND                 34750054
               END-EVALUATE                                             34760054
           END-PERFORM.                                                 34770054
                                                                        34780054
                                                                        34790054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         34800054
               MOVE '9000-GET-RSPBLVL-CODE' TO                          34810054
                                           DP013-PARAGRAPH              34820054
               STRING ' RSPLVL CODE ' DK-RSPLVL-CDE                     34830054
                      DELIMITED BY SIZE                                 34840054
                                      INTO DP013-MESSAGE-TEXT(1)        34850054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 34860054
                                           DP013-MESSAGE-TEXT(2)        34870054
               MOVE SQLCA TO DP013-SQLCA                                34880054
               MOVE 'TRSPLVL' TO DP013-DB2-TABLE-NAME(1)                34890054
               SET DP013-DB2-ABEND                                      34900054
                   DP013-NO-ROLLBACK                                    34910054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   34920054
               PERFORM DP013-0000-PROCESS-ABEND                         34930054
           END-IF.                                                      34940054
           EJECT                                                        34950054
                                                                        34960054
       9005-OPEN-RSPBAR-CURSOR.                                         34970054
      ******************************************************************34980054
      *                                                                 34990054
      ******************************************************************35000054
                                                                        35010054
           PERFORM WITH TEST AFTER                                      35020054
               VARYING PV-RETRY-COUNTER                                 35030054
               FROM 1 BY 1                                              35040054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              35050054
                       OR (SQLCODE = 0)                                 35060054
                       OR (SQLCODE = +100))                             35070054
                                                                        35080054
                                                                        35090054
               EXEC SQL                                                 35100054
                   OPEN RSPBAR_CUR                                      35110054
               END-EXEC                                                 35120054
                                                                        35130054
               EVALUATE TRUE                                            35140054
                   WHEN SQLCODE = ZERO                                  35150054
                       SET PS-ROW-FOUND TO TRUE                         35160054
                   WHEN SQLCODE = -904                                  35170054
                   WHEN SQLCODE = -913                                  35180054
                        CONTINUE                                        35190054
                   WHEN SQLCODE = +100                                  35200054
                        CONTINUE                                        35210054
                   WHEN SQLWARN0 NOT = SPACES                           35220054
                   WHEN SQLCODE < 0                                     35230054
                   WHEN SQLCODE > 0                                     35240054
                       MOVE SQLCA TO DP013-SQLCA                        35250054
                       MOVE '9005-OPEN-RSPBAR-CURSOR' TO                35260054
                           DP013-PARAGRAPH                              35270054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(2)        35280054
                       SET DP013-DB2-ABEND TO TRUE                      35290054
                       SET DP013-NO-ROLLBACK TO TRUE                    35300054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           35310054
                       PERFORM DP013-0000-PROCESS-ABEND                 35320054
               END-EVALUATE                                             35330054
           END-PERFORM.                                                 35340054
                                                                        35350054
                                                                        35360054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         35370054
               MOVE '9005-OPEN-RSPBAR-CURSOR' TO                        35380054
                                           DP013-PARAGRAPH              35390054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 35400054
                                           DP013-MESSAGE-TEXT(2)        35410054
               MOVE SQLCA TO DP013-SQLCA                                35420054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                35430054
               SET DP013-DB2-ABEND                                      35440054
                   DP013-NO-ROLLBACK                                    35450054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   35460054
               PERFORM DP013-0000-PROCESS-ABEND                         35470054
           END-IF.                                                      35480054
           EJECT                                                        35490054
                                                                        35500054
       9006-FETCH-RSPBAR-ROW.                                           35510054
      ******************************************************************35520054
      *                                                                 35530054
      ******************************************************************35540054
                                                                        35550054
           PERFORM WITH TEST AFTER                                      35560054
               VARYING PV-RETRY-COUNTER                                 35570054
               FROM 1 BY 1                                              35580054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              35590054
                       OR (SQLCODE = 0)                                 35600054
                       OR (SQLCODE = +100))                             35610054
                                                                        35620054
                                                                        35630054
               EXEC SQL                                                 35640054
                   FETCH RSPBAR_CUR                                     35650054
                   INTO  :RSPBAR-RSPLVL-CDE,                            35660054
                         :RSPBAR-MDSEAR-DKEY,                           35670054
                         :RSPBAR-WORKER-STRT-DTE,                       35680054
                         :RSPBAR-WORKER-END-DTE,                        35690054
                         :RSPBAR-WORKER-NBR,                            35700054
                         :RSPBAR-WORKER-FIR-NAME,                       35710054
                         :RSPBAR-WORKER-LAST-NAME                       35720054
               END-EXEC                                                 35730054
                                                                        35740054
               EVALUATE TRUE                                            35750054
                   WHEN SQLCODE = ZERO                                  35760054
                       SET PS-ROW-FOUND TO TRUE                         35770054
                   WHEN SQLCODE = -904                                  35780054
                   WHEN SQLCODE = -913                                  35790054
                        CONTINUE                                        35800054
                   WHEN SQLCODE = +100                                  35810054
                        SET PS-ROW-NOT-FOUND TO TRUE                    35820054
                   WHEN SQLWARN0 NOT = SPACES                           35830054
                   WHEN SQLCODE < 0                                     35840054
                   WHEN SQLCODE > 0                                     35850054
                       MOVE SQLCA TO DP013-SQLCA                        35860054
                       MOVE '9005-OPEN-RSPBAR-CURSOR' TO                35870054
                           DP013-PARAGRAPH                              35880054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(2)        35890054
                       SET DP013-DB2-ABEND TO TRUE                      35900054
                       SET DP013-NO-ROLLBACK TO TRUE                    35910054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           35920054
                       PERFORM DP013-0000-PROCESS-ABEND                 35930054
               END-EVALUATE                                             35940054
           END-PERFORM.                                                 35950054
                                                                        35960054
                                                                        35970054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         35980054
               MOVE '9005-OPEN-RSPBAR-CURSOR' TO                        35990054
                                           DP013-PARAGRAPH              36000054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 36010054
                                           DP013-MESSAGE-TEXT(2)        36020054
               MOVE SQLCA TO DP013-SQLCA                                36030054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                36040054
               SET DP013-DB2-ABEND                                      36050054
                   DP013-NO-ROLLBACK                                    36060054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   36070054
               PERFORM DP013-0000-PROCESS-ABEND                         36080054
           END-IF.                                                      36090054
           EJECT                                                        36100054
                                                                        36110054
       9007-CLOSE-RSPBAR-CURSOR.                                        36120054
      ******************************************************************36130054
      *                                                                 36140054
      ******************************************************************36150054
                                                                        36160054
           PERFORM WITH TEST AFTER                                      36170054
               VARYING PV-RETRY-COUNTER                                 36180054
               FROM 1 BY 1                                              36190054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              36200054
                       OR (SQLCODE = 0)                                 36210054
                       OR (SQLCODE = +100))                             36220054
                                                                        36230054
                                                                        36240054
               EXEC SQL                                                 36250054
                   CLOSE RSPBAR_CUR                                     36260054
               END-EXEC                                                 36270054
                                                                        36280054
               EVALUATE TRUE                                            36290054
                   WHEN SQLCODE = ZERO                                  36300054
                       SET PS-ROW-FOUND TO TRUE                         36310054
                   WHEN SQLCODE = -904                                  36320054
                   WHEN SQLCODE = -913                                  36330054
                        CONTINUE                                        36340054
                   WHEN SQLCODE = +100                                  36350054
                        CONTINUE                                        36360054
                   WHEN SQLWARN0 NOT = SPACES                           36370054
                   WHEN SQLCODE < 0                                     36380054
                   WHEN SQLCODE > 0                                     36390054
                       MOVE SQLCA TO DP013-SQLCA                        36400054
                       MOVE '9007-CLOSE-RSPBAR-CURSOR' TO               36410054
                           DP013-PARAGRAPH                              36420054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(2)        36430054
                       SET DP013-DB2-ABEND TO TRUE                      36440054
                       SET DP013-NO-ROLLBACK TO TRUE                    36450054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           36460054
                       PERFORM DP013-0000-PROCESS-ABEND                 36470054
               END-EVALUATE                                             36480054
           END-PERFORM.                                                 36490054
                                                                        36500054
                                                                        36510054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         36520054
               MOVE '9007-CLOSE-RSPBAR-CURSOR' TO                       36530054
                                           DP013-PARAGRAPH              36540054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 36550054
                                           DP013-MESSAGE-TEXT(2)        36560054
               MOVE SQLCA TO DP013-SQLCA                                36570054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                36580054
               SET DP013-DB2-ABEND                                      36590054
                   DP013-NO-ROLLBACK                                    36600054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   36610054
               PERFORM DP013-0000-PROCESS-ABEND                         36620054
           END-IF.                                                      36630054
           EJECT                                                        36640054
                                                                        36650054
                                                                        36660054
       9010-GET-WORKER-INFO.                                            36670054
      ******************************************************************36680054
      *                                                                 36690054
      ******************************************************************36700054
                                                                        36710054
           PERFORM WITH TEST AFTER                                      36720054
               VARYING PV-RETRY-COUNTER                                 36730054
               FROM 1 BY 1                                              36740054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              36750054
                       OR (SQLCODE = 0)                                 36760054
                       OR (SQLCODE = +100))                             36770054
                                                                        36780054
                                                                        36790054
               EXEC SQL                                                 36800054
                   SELECT UID_NBR,                                      36810054
                          LAST_NAME,                                    36820054
                          FIR_NAME                                      36830054
                   INTO :WORKER-UID-NBR,                                36840054
                        :WORKER-LAST-NAME,                              36850054
                        :WORKER-FIR-NAME                                36860054
                   FROM   TWORKER                                       36870054
                   WHERE (OPERCO_NBR = :DK-OPERCO-NBR) AND              36880054
                         (WORKER_NBR = :DK-WORKER-NBR)                  36890054
               END-EXEC                                                 36900054
                                                                        36910054
               EVALUATE TRUE                                            36920054
                   WHEN SQLCODE = ZERO                                  36930054
                       SET PS-ROW-FOUND TO TRUE                         36940054
                   WHEN SQLCODE = -904                                  36950054
                   WHEN SQLCODE = -913                                  36960054
                        CONTINUE                                        36970054
                   WHEN SQLCODE = +100                                  36980054
                        CONTINUE                                        36990054
                   WHEN SQLWARN0 NOT = SPACES                           37000054
                   WHEN SQLCODE < 0                                     37010054
                   WHEN SQLCODE > 0                                     37020054
                       STRING ' WORKER UID = ' DK-WORKER-UID            37030054
                              DELIMITED BY SIZE                         37040054
                              INTO DP013-MESSAGE-TEXT(2)                37050054
                       MOVE SQLCA TO DP013-SQLCA                        37060054
                       MOVE '9010-GET-WORKER-INFO' TO                   37070054
                           DP013-PARAGRAPH                              37080054
                       MOVE 'TWORKER' TO DP013-DB2-TABLE-NAME(2)        37090054
                       SET DP013-DB2-ABEND TO TRUE                      37100054
                       SET DP013-NO-ROLLBACK TO TRUE                    37110054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           37120054
                       PERFORM DP013-0000-PROCESS-ABEND                 37130054
               END-EVALUATE                                             37140054
           END-PERFORM.                                                 37150054
                                                                        37160054
                                                                        37170054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         37180054
               MOVE '9010-GET-WORKER-INFO' TO                           37190054
                                           DP013-PARAGRAPH              37200054
               STRING ' WORKER ID = ' DK-WORKER-UID                     37210054
                      DELIMITED BY SIZE                                 37220054
                                      INTO DP013-MESSAGE-TEXT(1)        37230054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 37240054
                                           DP013-MESSAGE-TEXT(2)        37250054
               MOVE SQLCA TO DP013-SQLCA                                37260054
               MOVE 'TWORKER' TO DP013-DB2-TABLE-NAME(1)                37270054
               SET DP013-DB2-ABEND                                      37280054
                   DP013-NO-ROLLBACK                                    37290054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   37300054
               PERFORM DP013-0000-PROCESS-ABEND                         37310054
           END-IF.                                                      37320054
           EJECT                                                        37330054
                                                                        37340054
       9015-GET-WORKER-NAME.                                            37350054
      ******************************************************************37360054
      *                                                                 37370054
      ******************************************************************37380054
                                                                        37390054
           PERFORM WITH TEST AFTER                                      37400054
               VARYING PV-RETRY-COUNTER                                 37410054
               FROM 1 BY 1                                              37420054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              37430054
                       OR (SQLCODE = 0)                                 37440054
                       OR (SQLCODE = +100))                             37450054
                                                                        37460054
                                                                        37470054
               EXEC SQL                                                 37480054
                   SELECT UID_NBR,                                      37490054
                          WORKER_NBR,                                   37500054
                          LAST_NAME,                                    37510054
                          FIR_NAME                                      37520054
                   INTO :WORKER-UID-NBR,                                37530054
                        :WORKER-NBR,                                    37540054
                        :WORKER-LAST-NAME,                              37550054
                        :WORKER-FIR-NAME                                37560054
                   FROM   TWORKER                                       37570054
                   WHERE (OPERCO_NBR = :DK-OPERCO-NBR) AND              37580054
                         (UID_NBR = :DK-WORKER-UID)                     37590054
               END-EXEC                                                 37600054
                                                                        37610054
               EVALUATE TRUE                                            37620054
                   WHEN SQLCODE = ZERO                                  37630054
                       SET PS-ROW-FOUND TO TRUE                         37640054
                   WHEN SQLCODE = -904                                  37650054
                   WHEN SQLCODE = -913                                  37660054
                        CONTINUE                                        37670054
                   WHEN SQLCODE = +100                                  37680054
                        CONTINUE                                        37690054
                   WHEN SQLWARN0 NOT = SPACES                           37700054
                   WHEN SQLCODE < 0                                     37710054
                   WHEN SQLCODE > 0                                     37720054
                       STRING ' WORKER UID = ' DK-WORKER-UID            37730054
                              DELIMITED BY SIZE                         37740054
                              INTO DP013-MESSAGE-TEXT(2)                37750054
                       MOVE SQLCA TO DP013-SQLCA                        37760054
                       MOVE '9010-GET-WORKER-INFO' TO                   37770054
                           DP013-PARAGRAPH                              37780054
                       MOVE 'TWORKER' TO DP013-DB2-TABLE-NAME(2)        37790054
                       SET DP013-DB2-ABEND TO TRUE                      37800054
                       SET DP013-NO-ROLLBACK TO TRUE                    37810054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           37820054
                       PERFORM DP013-0000-PROCESS-ABEND                 37830054
               END-EVALUATE                                             37840054
           END-PERFORM.                                                 37850054
                                                                        37860054
                                                                        37870054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         37880054
               MOVE '9010-GET-WORKER-INFO' TO                           37890054
                                           DP013-PARAGRAPH              37900054
               STRING ' WORKER ID = ' DK-WORKER-UID                     37910054
                      DELIMITED BY SIZE                                 37920054
                                      INTO DP013-MESSAGE-TEXT(1)        37930054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 37940054
                                           DP013-MESSAGE-TEXT(2)        37950054
               MOVE SQLCA TO DP013-SQLCA                                37960054
               MOVE 'TWORKER' TO DP013-DB2-TABLE-NAME(1)                37970054
               SET DP013-DB2-ABEND                                      37980054
                   DP013-NO-ROLLBACK                                    37990054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   38000054
               PERFORM DP013-0000-PROCESS-ABEND                         38010054
           END-IF.                                                      38020054
           EJECT                                                        38030054
                                                                        38040054
       9020-GET-DATALESS-KEY.                                           38050054
      ******************************************************************38060054
      *                                                                 38070054
      ******************************************************************38080054
                                                                        38090054
           PERFORM WITH TEST AFTER                                      38100054
               VARYING PV-RETRY-COUNTER                                 38110054
               FROM 1 BY 1                                              38120054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              38130054
                       OR (SQLCODE = 0)                                 38140054
                       OR (SQLCODE = +100))                             38150054
                                                                        38160054
                                                                        38170054
               EXEC SQL                                                 38180054
                   SELECT MAX(MDSEAR_DKEY)                              38190054
                   INTO :MDSEAR-DKEY                                    38200054
                   FROM TMDSEAR                                         38210054
               END-EXEC                                                 38220054
                                                                        38230054
               EVALUATE TRUE                                            38240054
                   WHEN SQLCODE = ZERO                                  38250054
                       SET PS-ROW-FOUND TO TRUE                         38260054
                   WHEN SQLCODE = -904                                  38270054
                   WHEN SQLCODE = -913                                  38280054
                        CONTINUE                                        38290054
                   WHEN SQLCODE = +100                                  38300054
                   WHEN SQLWARN0 NOT = SPACES                           38310054
                   WHEN SQLCODE < 0                                     38320054
                   WHEN SQLCODE > 0                                     38330054
                       MOVE 'ATTEMPTING TO GET MAX DKEY ON TABLE'       38340054
                              TO DP013-MESSAGE-TEXT(1)                  38350054
                       MOVE SQLCA TO DP013-SQLCA                        38360054
                       MOVE '9020-GET-DATALESS-KEY' TO                  38370054
                           DP013-PARAGRAPH                              38380054
                       MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)        38390054
                       SET DP013-DB2-ABEND TO TRUE                      38400054
                       SET DP013-NO-ROLLBACK TO TRUE                    38410054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           38420054
                       PERFORM DP013-0000-PROCESS-ABEND                 38430054
               END-EVALUATE                                             38440054
           END-PERFORM.                                                 38450054
                                                                        38460054
                                                                        38470054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         38480054
               MOVE '9020-GET-DATALESS-KEY' TO                          38490054
                                           DP013-PARAGRAPH              38500054
               MOVE 'ATTEMPTING TO GET MAX DKEY ON TABLE'               38510054
                                        TO DP013-MESSAGE-TEXT(1)        38520054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 38530054
                                           DP013-MESSAGE-TEXT(2)        38540054
               MOVE SQLCA TO DP013-SQLCA                                38550054
               MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)                38560054
               SET DP013-DB2-ABEND                                      38570054
                   DP013-NO-ROLLBACK                                    38580054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   38590054
               PERFORM DP013-0000-PROCESS-ABEND                         38600054
           END-IF.                                                      38610054
           EJECT                                                        38620054
                                                                        38630054
                                                                        38640054
                                                                        38650054
       9022-INSERT-RSPBAR-ROW.                                          38660054
      ******************************************************************38670054
      *    INSERT THE RESPONSIBILITY ROWS.                              38680054
      ******************************************************************38690054
                                                                        38700054
           PERFORM WITH TEST AFTER                                      38710054
               VARYING PV-RETRY-COUNTER                                 38720054
               FROM 1 BY 1                                              38730054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              38740054
                       OR (SQLCODE = 0)                                 38750054
                       OR (SQLCODE = +100))                             38760054
                                                                        38770054
                                                                        38780054
               EXEC SQL                                                 38790054
                   INSERT INTO TRSPBAR                                  38800054
                   (MDSEAR_DKEY,                                        38810054
                    WORKER_NBR,                                         38820054
                    RSPLVL_CDE,                                         38830054
                    WORKER_STRT_DTE,                                    38840054
                    WORKER_END_DTE,                                     38850054
                    WORKER_FIR_NAME,                                    38860054
                    WORKER_LAST_NAME,                                   38870054
                    OPERCO_NBR)                                         38880054
                    VALUES (:RSPBAR-MDSEAR-DKEY,                        38890054
                            :RSPBAR-WORKER-NBR,                         38900054
                            :RSPBAR-RSPLVL-CDE,                         38910054
                            :RSPBAR-WORKER-STRT-DTE,                    38920054
                            :RSPBAR-WORKER-END-DTE,                     38930054
                            :RSPBAR-WORKER-FIR-NAME,                    38940054
                            :RSPBAR-WORKER-LAST-NAME,                   38950054
                            :RSPBAR-OPERCO-NBR)                         38960054
               END-EXEC                                                 38970054
                                                                        38980054
               EVALUATE TRUE                                            38990054
                   WHEN SQLCODE = ZERO                                  39000054
                       SET PS-ROW-FOUND TO TRUE                         39010054
                   WHEN SQLCODE = -904                                  39020054
                   WHEN SQLCODE = -913                                  39030054
                        CONTINUE                                        39040054
                   WHEN SQLWARN0 NOT = SPACES                           39050054
                   WHEN SQLCODE < 0                                     39060054
                   WHEN SQLCODE > 0                                     39070054
                       MOVE 'ERROR TRYING TO INSERT RSPBAR ROW'         39080054
                                           TO DP013-MESSAGE-TEXT(1)     39090054
                       STRING ' RESPLVL CODE ' DK-RSPLVL-CDE            39100054
                              ' USERID '       DK-WORKER-UID            39110054
                              DELIMITED BY SIZE                         39120054
                              INTO DP013-MESSAGE-TEXT(2)                39130054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)        39140054
                       MOVE SQLCA TO DP013-SQLCA                        39150054
                       MOVE '9022-INSERT-RSPBAR-ROW' TO                 39160054
                           DP013-PARAGRAPH                              39170054
                       SET DP013-DB2-ABEND TO TRUE                      39180054
                       SET DP013-ROLLBACK TO TRUE                       39190054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           39200054
                       PERFORM DP013-0000-PROCESS-ABEND                 39210054
               END-EVALUATE                                             39220054
           END-PERFORM.                                                 39230054
                                                                        39240054
                                                                        39250054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         39260054
                                                                        39270054
               MOVE 'ERROR TRYING TO INSERT RSPBAR ROW'                 39280054
                                           TO DP013-MESSAGE-TEXT(1)     39290054
               STRING ' RESPLVL CODE ' DK-RSPLVL-CDE                    39300054
                      ' USERID '       DK-WORKER-UID                    39310054
                      DELIMITED BY SIZE                                 39320054
                      INTO DP013-MESSAGE-TEXT(2)                        39330054
               MOVE '9022-INSERT-RSPBAR-ROW' TO                         39340054
                                           DP013-PARAGRAPH              39350054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 39360054
                                           DP013-MESSAGE-TEXT(2)        39370054
               MOVE SQLCA TO DP013-SQLCA                                39380054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                39390054
               SET DP013-DB2-ABEND                                      39400054
                   DP013-ROLLBACK                                       39410054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   39420054
               PERFORM DP013-0000-PROCESS-ABEND                         39430054
           END-IF.                                                      39440054
           EJECT                                                        39450054
                                                                        39460054
       9100-UPDATE-RSPBAR-ROW.                                          39470054
      ******************************************************************39480054
      *    INSERT THE RESPONSIBILITY ROWS.                              39490054
      ******************************************************************39500054
                                                                        39510054
           PERFORM WITH TEST AFTER                                      39520054
               VARYING PV-RETRY-COUNTER                                 39530054
               FROM 1 BY 1                                              39540054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              39550054
                       OR (SQLCODE = 0)                                 39560054
                       OR (SQLCODE = +100))                             39570054
                                                                        39580054
                                                                        39590054
               EXEC SQL                                                 39600054
                   UPDATE TRSPBAR                                       39610054
                      SET WORKER_END_DTE = :RSPBAR-WORKER-END-DTE,      39620054
                          WORKER_STRT_DTE = :RSPBAR-WORKER-STRT-DTE     39630054
                      WHERE (MDSEAR_DKEY = :DK-MDSEAR-DKEY) AND         39640054
                            (WORKER_NBR  = :DK-WORKER-NBR) AND          39650054
                            (WORKER_STRT_DTE  = :DK-START-DATE) AND     39660054
                            (RSPLVL_CDE  = :DK-RSPLVL-CDE)              39670054
               END-EXEC                                                 39680054
                                                                        39690054
               EVALUATE TRUE                                            39700054
                   WHEN SQLCODE = ZERO                                  39710054
                       SET PS-ROW-FOUND TO TRUE                         39720054
                   WHEN SQLCODE = -904                                  39730054
                   WHEN SQLCODE = -913                                  39740054
                        CONTINUE                                        39750054
                   WHEN SQLWARN0 NOT = SPACES                           39760054
                   WHEN SQLCODE < 0                                     39770054
                   WHEN SQLCODE > 0                                     39780054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)        39790054
                       MOVE SQLCA TO DP013-SQLCA                        39800054
                       MOVE '9100-UPDATE-RSPBAR-ROW' TO                 39810054
                           DP013-PARAGRAPH                              39820054
                       STRING ' RESPLVL CODE ' DK-RSPLVL-CDE            39830054
                              ' USERID '       DK-WORKER-UID            39840054
                              DELIMITED BY SIZE                         39850054
                              INTO DP013-MESSAGE-TEXT(2)                39860054
                       SET DP013-DB2-ABEND TO TRUE                      39870054
                       SET DP013-ROLLBACK TO TRUE                       39880054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           39890054
                       PERFORM DP013-0000-PROCESS-ABEND                 39900054
               END-EVALUATE                                             39910054
           END-PERFORM.                                                 39920054
                                                                        39930054
                                                                        39940054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         39950054
                                                                        39960054
               MOVE '9100-UPDATE-RSPBAR-ROW' TO                         39970054
                                           DP013-PARAGRAPH              39980054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 39990054
                                           DP013-MESSAGE-TEXT(2)        40000054
               STRING ' RESPLVL CODE ' DK-RSPLVL-CDE                    40010054
                      ' USERID '       DK-WORKER-UID                    40020054
                      DELIMITED BY SIZE                                 40030054
                      INTO DP013-MESSAGE-TEXT(1)                        40040054
               MOVE SQLCA TO DP013-SQLCA                                40050054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                40060054
               SET DP013-DB2-ABEND                                      40070054
                   DP013-ROLLBACK                                       40080054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   40090054
               PERFORM DP013-0000-PROCESS-ABEND                         40100054
           END-IF.                                                      40110054
           EJECT                                                        40120054
                                                                        40130054
       9400-GET-MDSEAR-INFO.                                            40140054
      ******************************************************************40150054
      *                                                                 40160054
      ******************************************************************40170054
           PERFORM WITH TEST AFTER                                      40180054
               VARYING PV-RETRY-COUNTER                                 40190054
               FROM 1 BY 1                                              40200054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              40210054
                       OR (SQLCODE = 0)                                 40220054
                       OR (SQLCODE = +100))                             40230054
                                                                        40240054
                                                                        40250054
               EXEC SQL                                                 40260054
                   SELECT MDSEAR_DKEY,                                  40270054
                          STRT_DTE,                                     40280054
                          END_DTE,                                      40290054
                          MDSEAR_DESC                                   40300054
                   INTO :MDSEAR-DKEY,                                   40310054
                        :MDSEAR-STRT-DTE,                               40320054
                        :MDSEAR-END-DTE,                                40330054
                        :MDSEAR-DESC                                    40340054
                   FROM   TMDSEAR                                       40350054
                   WHERE ((OPERCO_NBR    = :DK-OPERCO-NBR) AND          40360054
                          (GMA_NBR       = :DK-GMA-NBR)    AND          40370054
                          (DMA_NBR       = :DK-DMA-NBR)    AND          40380054
                          (DEPT_NBR      = :DK-DEPT-NBR)   AND          40390054
                          (MAJ_CL_NBR    = :DK-MAJ-CL-NBR) AND          40400054
                          (SUB_CL_NBR    = :DK-SUB-CL-NBR) AND          40410054
                          (MDSELV_CDE    = :DK-MDSELV-CDE) AND          40420054
                          (END_DTE > DATE (CURRENT TIMESTAMP)))         40430054
               END-EXEC                                                 40440054
                                                                        40450054
               EVALUATE TRUE                                            40460054
                   WHEN SQLCODE = ZERO                                  40470054
                       SET PS-ROW-FOUND TO TRUE                         40480054
                   WHEN SQLCODE = -904                                  40490054
                   WHEN SQLCODE = -913                                  40500054
                        CONTINUE                                        40510054
                   WHEN SQLCODE = +100                                  40520054
                        SET PS-ROW-NOT-FOUND TO TRUE                    40530054
                   WHEN SQLWARN0 NOT = SPACES                           40540054
                   WHEN SQLCODE < 0                                     40550054
                   WHEN SQLCODE > 0                                     40560054
                       STRING ' GMA ' DK-GMA-NBR                        40570054
                              ' DMA ' DK-DMA-NBR                        40580054
                              ' DPT ' DK-DEPT-NBR                       40590054
                              ' MCL ' DK-MAJ-CL-NBR                     40600054
                              ' SCL ' DK-SUB-CL-NBR                     40610054
                              ' LEV=' DK-MDSELV-CDE                     40620054
                              ' DTE ' DK-END-DATE                       40630054
                              DELIMITED BY SIZE                         40640054
                              INTO DP013-MESSAGE-TEXT(1)                40650054
                       MOVE SQLCA TO DP013-SQLCA                        40660054
                       MOVE '9400-GET-MDSEAR-INFO' TO                   40670054
                           DP013-PARAGRAPH                              40680054
                       MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)        40690054
                       SET DP013-DB2-ABEND TO TRUE                      40700054
                       SET DP013-NO-ROLLBACK TO TRUE                    40710054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           40720054
                       PERFORM DP013-0000-PROCESS-ABEND                 40730054
               END-EVALUATE                                             40740054
           END-PERFORM.                                                 40750054
                                                                        40760054
                                                                        40770054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         40780054
               MOVE '9400-GET-MDSEAR-INFO' TO                           40790054
                                           DP013-PARAGRAPH              40800054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 40810054
                                           DP013-MESSAGE-TEXT(1)        40820054
               STRING ' GMA ' DK-GMA-NBR                                40830054
                      ' DMA ' DK-DMA-NBR                                40840054
                      ' DPT ' DK-DEPT-NBR                               40850054
                      ' MCL ' DK-MAJ-CL-NBR                             40860054
                      ' SCL ' DK-SUB-CL-NBR                             40870054
                      ' LEV=' DK-MDSELV-CDE                             40880054
                      ' DTE ' DK-END-DATE                               40890054
                      DELIMITED BY SIZE                                 40900054
                      INTO DP013-MESSAGE-TEXT(2)                        40910054
               MOVE SQLCA TO DP013-SQLCA                                40920054
               MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)                40930054
               SET DP013-DB2-ABEND                                      40940054
                   DP013-NO-ROLLBACK                                    40950054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   40960054
               PERFORM DP013-0000-PROCESS-ABEND                         40970054
           END-IF.                                                      40980054
                                                                        40990054
           EJECT                                                        41000054
                                                                        41010054
                                                                        41020054
       9410-GET-RSPBAR-ROW.                                             41030054
      ******************************************************************41040054
      *                                                                 41050054
      ******************************************************************41060054
           PERFORM WITH TEST AFTER                                      41070054
               VARYING PV-RETRY-COUNTER                                 41080054
               FROM 1 BY 1                                              41090054
               UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)              41100054
                       OR (SQLCODE = 0)                                 41110054
                       OR (SQLCODE = +100))                             41120054
                                                                        41130054
                                                                        41140054
               EXEC SQL                                                 41150054
                   SELECT COUNT(*)                                      41160054
                   INTO :PV-ROW-COUNT                                   41170054
                   FROM   TRSPBAR                                       41180054
                   WHERE ((MDSEAR_DKEY   = :DK-MDSEAR-DKEY) AND         41190054
                          (WORKER_NBR    = :DK-WORKER-NBR) AND          41200054
                          (RSPLVL_CDE    = :DK-RSPLVL-CDE) AND          41210054
                          (WORKER_STRT_DTE = :DK-START-DATE))           41220054
               END-EXEC                                                 41230054
                                                                        41240054
               EVALUATE TRUE                                            41250054
                   WHEN SQLCODE = ZERO                                  41260054
                       SET PS-ROW-FOUND TO TRUE                         41270054
                   WHEN SQLCODE = -904                                  41280054
                   WHEN SQLCODE = -913                                  41290054
                        CONTINUE                                        41300054
                   WHEN SQLCODE = +100                                  41310054
                        SET PS-ROW-NOT-FOUND TO TRUE                    41320054
                   WHEN SQLWARN0 NOT = SPACES                           41330054
                   WHEN SQLCODE < 0                                     41340054
                   WHEN SQLCODE > 0                                     41350054
                       STRING ' RSPLVL CODE ' DK-RSPLVL-CDE             41360054
                              ' WORKER ' DK-WORKER-UID                  41370054
                              ' START DTE ' DK-START-DATE               41380054
                              DELIMITED BY SIZE                         41390054
                              INTO DP013-MESSAGE-TEXT(1)                41400054
                       MOVE SQLCA TO DP013-SQLCA                        41410054
                       MOVE '9410-GET-RSPBAR-ROW' TO                    41420054
                           DP013-PARAGRAPH                              41430054
                       MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)        41440054
                       SET DP013-DB2-ABEND TO TRUE                      41450054
                       SET DP013-NO-ROLLBACK TO TRUE                    41460054
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE           41470054
                       PERFORM DP013-0000-PROCESS-ABEND                 41480054
               END-EVALUATE                                             41490054
           END-PERFORM.                                                 41500054
                                                                        41510054
                                                                        41520054
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         41530054
               MOVE '9410-GET-RSPBAR-ROW ' TO                           41540054
                                           DP013-PARAGRAPH              41550054
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 41560054
                                           DP013-MESSAGE-TEXT(1)        41570054
                      STRING  ' RSPLVL CODE ' DK-RSPLVL-CDE             41580054
                              ' WORKER ' DK-WORKER-UID                  41590054
                              ' START DTE ' DK-START-DATE               41600054
                      DELIMITED BY SIZE                                 41610054
                      INTO DP013-MESSAGE-TEXT(2)                        41620054
               MOVE SQLCA TO DP013-SQLCA                                41630054
               MOVE 'TRSPBAR' TO DP013-DB2-TABLE-NAME(1)                41640054
               SET DP013-DB2-ABEND                                      41650054
                   DP013-NO-ROLLBACK                                    41660054
                   DP013-XCTL-DISPLAY-RESTART TO TRUE                   41670054
               PERFORM DP013-0000-PROCESS-ABEND                         41680054
           END-IF.                                                      41690054
                                                                        41700054
           EJECT                                                        41710054
                                                                        41730054
       9500-READ-LIST-TSQ.                                              41740054
      ******************************************************************41750054
      *    READ TEMPORARY STORAGE QUEUE                                 41760054
      ******************************************************************41770054
           EXEC CICS                                                    41780054
               READQ TS                                                 41790054
               QUEUE(ASC-LIST-TSQ-NAME)                                 41800054
               INTO (PV-LIST-TSQ-AREA)                                  41810054
               LENGTH(ASC-LIST-TSQ-LENGTH)                              41820054
               ITEM(ASC-LIST-TSQ-ITEM-NBR)                              41830054
               RESP(PV-CICS-RESPONSE)                                   41840054
           END-EXEC.                                                    41850054
                                                                        41860054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    41870054
               SET DP013-LOGIC-ABEND  TO TRUE                           41880054
               SET DP013-NO-ROLLBACK  TO TRUE                           41890054
               MOVE '9500-READ-LIST-TSQ' TO DP013-PARAGRAPH             41900054
               MOVE 'RECEIVED ERROR MESSAGE WHILE READING TSQ' TO       41910054
                                            DP013-MESSAGE-TEXT(1)       41920054
               PERFORM DP013-0000-PROCESS-ABEND                         41930054
           END-IF.                                                      41940054
       EJECT                                                            41941054
                                                                        41941154
       9510-READ-ORIG-TSQ.                                              41942054
      ******************************************************************41943054
      *    READ TEMPORARY STORAGE QUEUE                                 41944054
      ******************************************************************41945054
           EXEC CICS                                                    41946054
               READQ TS                                                 41947054
               QUEUE(ASC-ORIG-TSQ-NAME)                                 41948054
               INTO (PV-ORIG-TSQ-AREA)                                  41949054
               LENGTH(PV-ORIG-TSQ-LENGTH)                               41949154
               ITEM(ASC-ORIG-TSQ-ITEM-NBR)                              41949354
               RESP(PV-CICS-RESPONSE)                                   41949454
           END-EXEC.                                                    41949554
                                                                        41949654
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    41949754
               SET DP013-LOGIC-ABEND  TO TRUE                           41949854
               SET DP013-NO-ROLLBACK  TO TRUE                           41949954
               MOVE '9510-READ-ORIG-TSQ' TO DP013-PARAGRAPH             41950054
               MOVE 'RECEIVED ERROR MESSAGE WHILE READING TSQ' TO       41950154
                                            DP013-MESSAGE-TEXT(1)       41950254
               PERFORM DP013-0000-PROCESS-ABEND                         41950354
           END-IF.                                                      41950454
       EJECT                                                            41951054
                                                                        41960054
       9511-WRITE-ORIG-TSQ.                                             41970054
      ******************************************************************41980054
      *    WRITE TEMPORARY STORAGE QUEUE                                41990054
      ******************************************************************42000054
           EXEC CICS                                                    42010054
               WRITEQ TS                                                42020054
               QUEUE(ASC-ORIG-TSQ-NAME)                                 42030054
               FROM (PV-ORIG-TSQ-AREA)                                  42040054
               LENGTH(PV-ORIG-TSQ-LENGTH)                               42050054
               ITEM(ASC-ORIG-TSQ-ITEM-NBR)                              42060054
               RESP(PV-CICS-RESPONSE)                                   42070054
           END-EXEC.                                                    42080054
                                                                        42090054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42100054
               SET DP013-LOGIC-ABEND  TO TRUE                           42110054
               SET DP013-NO-ROLLBACK  TO TRUE                           42120054
               MOVE '9511-WRITE-ORIG-TSQ' TO DP013-PARAGRAPH            42131054
               MOVE 'RECEIVED ERROR MESSAGE WHILE WRITING TSQ' TO       42140054
                                            DP013-MESSAGE-TEXT(1)       42150054
               PERFORM DP013-0000-PROCESS-ABEND                         42160054
           END-IF.                                                      42170054
       EJECT                                                            42180054
                                                                        42190054
       9512-REWRITE-ORIG-TSQ.                                           42200054
      ******************************************************************42210054
      *    REWRITE TEMPORARY STORAGE QUEUE                              42220054
      ******************************************************************42230054
           EXEC CICS                                                    42240054
               WRITEQ TS                                                42250054
               QUEUE(ASC-ORIG-TSQ-NAME)                                 42260054
               FROM (PV-ORIG-TSQ-AREA)                                  42270054
               LENGTH(PV-ORIG-TSQ-LENGTH)                               42280054
               ITEM(ASC-ORIG-TSQ-ITEM-NBR) REWRITE                      42290054
               RESP(PV-CICS-RESPONSE)                                   42300054
           END-EXEC.                                                    42310054
                                                                        42320054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42330054
               SET DP013-LOGIC-ABEND  TO TRUE                           42340054
               SET DP013-NO-ROLLBACK  TO TRUE                           42350054
               MOVE '9512-REWRITE-ORIG-TSQ' TO DP013-PARAGRAPH          42361054
               MOVE 'RECEIVED ERROR MESSAGE WHILE WRITING TSQ' TO       42370054
                                            DP013-MESSAGE-TEXT(1)       42380054
               PERFORM DP013-0000-PROCESS-ABEND                         42390054
           END-IF.                                                      42400054
                                                                        42630054
       9513-DELETE-ORIG-TSQ.                                            42640054
      ******************************************************************42650054
      *    DELETE TEMPORARY STORAGE QUEUE                               42660054
      ******************************************************************42670054
           EXEC CICS                                                    42680054
               DELETEQ TS                                               42690054
               QUEUE(ASC-ORIG-TSQ-NAME)                                 42700054
               RESP(PV-CICS-RESPONSE)                                   42710054
           END-EXEC.                                                    42720054
                                                                        42730054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42740054
               SET DP013-LOGIC-ABEND  TO TRUE                           42750054
               SET DP013-NO-ROLLBACK  TO TRUE                           42760054
               MOVE '9513-DELETE-ORIG-TSQ' TO DP013-PARAGRAPH           42770054
               MOVE 'RECEIVED ERROR MESSAGE WHILE DELETING TSQ' TO      42780054
                                            DP013-MESSAGE-TEXT(1)       42790054
               PERFORM DP013-0000-PROCESS-ABEND                         42800054
           END-IF.                                                      42810054
       EJECT                                                            42820054
                                                                        42830054
       9520-READ-CURR-TSQ.                                              42840054
      ******************************************************************42850054
      *    READ CURRENT TEMPORARY STORAGE QUEUE                         42860054
      ******************************************************************42870054
           EXEC CICS                                                    42880054
               READQ TS                                                 42890054
               QUEUE(ASC-CURR-TSQ-NAME)                                 42900054
               INTO (PV-CURR-TSQ-AREA)                                  42901054
               LENGTH(PV-CURR-TSQ-LENGTH)                               42902054
               ITEM(ASC-CURR-TSQ-ITEM-NBR)                              42903054
               RESP(PV-CICS-RESPONSE)                                   42904054
           END-EXEC.                                                    42905054
                                                                        42906054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42907054
               SET DP013-LOGIC-ABEND  TO TRUE                           42908054
               SET DP013-NO-ROLLBACK  TO TRUE                           42909054
               MOVE '9520-READ-CURR-TSQ' TO DP013-PARAGRAPH             42909154
               MOVE 'RECEIVED ERROR MESSAGE WHILE READING TSQ' TO       42909254
                                            DP013-MESSAGE-TEXT(1)       42909354
               PERFORM DP013-0000-PROCESS-ABEND                         42909454
           END-IF.                                                      42909554
       EJECT                                                            42909654
                                                                        42909754
       9521-WRITE-CURR-TSQ.                                             42909854
      ******************************************************************42909954
      *    WRITE CURRENT TEMPORARY STORAGE QUEUE                        42910054
      ******************************************************************42910154
           EXEC CICS                                                    42910254
               WRITEQ TS                                                42910354
               QUEUE(ASC-CURR-TSQ-NAME)                                 42910454
               FROM (PV-CURR-TSQ-AREA)                                  42910554
               LENGTH(PV-CURR-TSQ-LENGTH)                               42910654
               ITEM(ASC-CURR-TSQ-ITEM-NBR)                              42910754
               RESP(PV-CICS-RESPONSE)                                   42910854
           END-EXEC.                                                    42910954
                                                                        42911054
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42911154
               SET DP013-LOGIC-ABEND  TO TRUE                           42911254
               SET DP013-NO-ROLLBACK  TO TRUE                           42911354
               MOVE '9521-WRITE-CURR-TSQ' TO DP013-PARAGRAPH            42911454
               MOVE 'RECEIVED ERROR MESSAGE WHILE WRITING TSQ' TO       42911554
                                            DP013-MESSAGE-TEXT(1)       42911654
               PERFORM DP013-0000-PROCESS-ABEND                         42911754
           END-IF.                                                      42911854
       EJECT                                                            42911954
                                                                        42912054
       9522-REWRITE-CURR-TSQ.                                           42912154
      ******************************************************************42912254
      *    REWRITE CURRENT TEMPORARY STORAGE QUEUE                      42912354
      ******************************************************************42912454
           EXEC CICS                                                    42912554
               WRITEQ TS                                                42912654
               QUEUE(ASC-CURR-TSQ-NAME)                                 42912754
               FROM (PV-CURR-TSQ-AREA)                                  42912854
               LENGTH(PV-CURR-TSQ-LENGTH)                               42912954
               ITEM(ASC-CURR-TSQ-ITEM-NBR) REWRITE                      42913054
               RESP(PV-CICS-RESPONSE)                                   42913154
           END-EXEC.                                                    42913254
                                                                        42913354
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42913454
               SET DP013-LOGIC-ABEND  TO TRUE                           42913554
               SET DP013-NO-ROLLBACK  TO TRUE                           42913654
               MOVE '9522-REWRITE-CURR-TSQ' TO DP013-PARAGRAPH          42913754
               MOVE 'RECEIVED ERROR MESSAGE WHILE WRITING TSQ' TO       42913854
                                            DP013-MESSAGE-TEXT(1)       42913954
               PERFORM DP013-0000-PROCESS-ABEND                         42914054
           END-IF.                                                      42914154
                                                                        42914254
       9523-DELETE-CURR-TSQ.                                            42914354
      ******************************************************************42914454
      *    DELETE CURRENT TEMPORARY STORAGE QUEUE                       42914554
      ******************************************************************42914654
           EXEC CICS                                                    42914754
               DELETEQ TS                                               42914854
               QUEUE(ASC-CURR-TSQ-NAME)                                 42914954
               RESP(PV-CICS-RESPONSE)                                   42915054
           END-EXEC.                                                    42915154
                                                                        42915254
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    42915354
               SET DP013-LOGIC-ABEND  TO TRUE                           42915454
               SET DP013-NO-ROLLBACK  TO TRUE                           42915554
               MOVE '9523-DELETE-CURR-TSQ' TO DP013-PARAGRAPH           42915654
               MOVE 'RECEIVED ERROR MESSAGE WHILE DELETING TSQ' TO      42915754
                                            DP013-MESSAGE-TEXT(1)       42915854
               PERFORM DP013-0000-PROCESS-ABEND                         42915954
           END-IF.                                                      42916054
       EJECT                                                            42917054
                                                                        42920054
       9900-COMMIT.                                                     42930054
           EXEC CICS                                                    42940054
               SYNCPOINT                                                42950054
           END-EXEC.                                                    42960054
                                                                        42970054
       EJECT                                                            42981054
                                                                        42982054
       9999-RETURN-TO-PREV-TRAN.                                        42983054
      ******************************************************************42984054
      *                                                                 42985054
      ******************************************************************42986054
           MOVE DP016-PF3  TO DP020-SRC-AID.                            42987054
                                                                        42989054
       EJECT                                                            42990054
                                                                        43000054
      ******************************************************************43010054
      *    FILL IN SCREEN HEADER MODULE                                 43020054
      ******************************************************************43030054
                                                                        43040054
           COPY DPPD002.                                                43050054
       EJECT                                                            43060054
      ******************************************************************43070054
      *    RETURN TO CICS MODULE                                        43080054
      ******************************************************************43090054
                                                                        43100054
           COPY DPPD003.                                                43110054
       EJECT                                                            43120054
      ******************************************************************43130054
      *    FORMAT MESSAGE LINES MODULE                                  43140054
      ******************************************************************43150054
                                                                        43160054
           COPY DPPD005.                                                43170054
       EJECT                                                            43180054
      ******************************************************************43190054
      *    GET PANEL MODULE                                             43200054
      ******************************************************************43210054
                                                                        43220054
           COPY DPPD007.                                                43230054
       EJECT                                                            43240054
      ******************************************************************43250054
      *    WRITE PANEL MODULE                                           43260054
      ******************************************************************43270054
                                                                        43280054
           COPY DPPD008.                                                43290054
       EJECT                                                            43300054
      ******************************************************************43310054
      *    TOGGLE THE FUNCTION KEY LINE MODULE                          43320054
      ******************************************************************43330054
                                                                        43340054
           COPY DPPD009.                                                43350054
       EJECT                                                            43360054
      ******************************************************************43370054
      *    ABEND PROCESSOR MODULE                                       43380054
      ******************************************************************43390054
                                                                        43400054
           COPY DPPD013.                                                43410054
       EJECT                                                            43420054
      ******************************************************************43430054
      *    CHECK COMMUNICATION AREA MODULE                              43440054
      ******************************************************************43450054
                                                                        43460054
           COPY DPPD015.                                                43470054
       EJECT                                                            43480054
      ******************************************************************43490054
      *    UPDATE TIME AND DATE MODULE                                  43500054
      ******************************************************************43510054
                                                                        43520054
           COPY DPPD017.                                                43530054
       EJECT                                                            43540054
