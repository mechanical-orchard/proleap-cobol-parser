000050 IDENTIFICATION DIVISION.                                         UWRAP   
000100*------------------------                                         UWRAP   
000150 PROGRAM-ID. ETKUT103.                                            UWRAP   
000200************************************************                  UWRAP   
000250*          E D I * B E N C H M A R K           *                  UWRAP   
000300*                                              *                  UWRAP   
000350*                                              *                  UWRAP   
000400*        COPYRIGHT 1991,1992,1993,1994         *                  UWRAP   
000450*          GE INFORMATION SERVICES             *                  UWRAP   
000500************************************************                  UWRAP   
000550*                                                                 UWRAP   
000600*                                                                 UWRAP   
000650***********************************************************       UWRAP   
000700*    THIS PROGRAM WAS INSTALLED TO UNWRAP EDI DATA WHERE  *       UWRAP   
000750*    THERE ARE MULTIPLE SEGMENT TERMINATORS AND THE ISA   *       UWRAP   
000750*    DOES NOT NECESSARILY START IN COLUMN ONE.            *       UWRAP   
000750*                                                         *       UWRAP   
000750*    NOTE:  THIS PROGRAM WILL NOT WORK IF ANY TWO         *       UWRAP   
000750*           DOCUMENTS USE THE SAME SEPARATOR, ONE AS THE  *       UWRAP   
000750*           ELEMENT SEPARATOR AND THE OTHER AS THE        *       UWRAP   
000750*           SEGMENT TERMINATOR.                           *       UWRAP   
000750*                                                         *       UWRAP   
000650***********************************************************       UWRAP   
000550*                                                                 UWRAP   
000600*                                                                 UWRAP   
000650***********************************************************       UWRAP   
000700*    THE UNWRAP PROGRAM TAKES AN INPUT COMPRESSED FILE    *       UWRAP   
000750*    CONTAINING 80-BYTE WRAPPED RECORDS AND WRITES EACH   *       UWRAP   
000800*    SEGMENT TO A NEW LINE IN THE FINAL FLAT FILE.  THE   *       UWRAP   
000850*    WRAPPED RECORDS ARE UNWRAPPED ACCORDING TO THE       *       UWRAP   
000900*    SEGMENT TERMINATOR CHARACTER SPECIFIED IN THE INPUT  *       UWRAP   
000950*    PARAMETER FILE.  THE AUDIT CONTROL FILE SHOULD BE    *       UWRAP   
001000*    CHECKED AFTER EACH EXECUTION TO DETERMINE WHETHER    *       UWRAP   
001050*    OR NOT THE INPUT FILE WAS UNWRAPPED SUCCESSFULLY.    *       UWRAP   
001100*                                                         *       UWRAP   
001150*    PROGRAM INPUT:                                       *       UWRAP   
001200*      PARAMETER FILE -- SHOULD CONTAIN ONE 80-BYTE       *       UWRAP   
001250*          RECORD.  THE SEGMENT TERMINATOR FROM THE       *       UWRAP   
001300*          INPUT COMPRESSED FILE SHOULD BE PLACED IN      *       UWRAP   
001350*          POSITION ONE FOR A SPECIFIC CHARACTER WITH     *       UWRAP   
001400*          POSITIONS TWO AND THREE LEFT BLANK OR FOR      *       UWRAP   
001450*          HEX VALUES, THE INPUT TO THE PARM CARD WILL    *       UWRAP   
001500*          BE TWO CHARACTERS PRECEEDED BY AN 'X'.         *       UWRAP   
001550*          A 'Y' SHOULD BE PLACED IN POSITION FOUR IF     *       UWRAP   
001600*          YOU DO NOT WANT THE SEGMENT TERMINATOR         *       UWRAP   
001650*          PRINTED IN THE UNWRAPPED FILE.  IF YOU DO      *       UWRAP   
001700*          WANT THE SEGMENT TERMINATOR PRINTED IN THE     *       UWRAP   
001750*          UWRAPPED FILE, LEAVE POSITION FOUR BLANK.      *       UWRAP   
001800*                                                         *       UWRAP   
001850*          POSITIONS 5-7 OF THE PARAMETER FILE ARE        *       UWRAP   
001900*          USED TO INDICATE WHETHER THE FILE BEING        *       UWRAP   
001950*          UNWRAPPED CONTAINS RELEASE CHARACTERS AND      *       UWRAP   
002000*          THEREFORE, UNWRAP MUST KNOW THE VALUE OF THE   *       UWRAP   
002050*          RELEASE CHARACTER.  IF THE FILE DOESN'T        *       UWRAP   
002100*          CONTAIN A RELEASE CHARCTER, LEAVE THESE        *       UWRAP   
002150*          POSITIONS BLANK.  IF THE RELEASE CHARACTER     *       UWRAP   
002200*          IS A HEXADECIMAL CHARACTER, PLACE X IN         *       UWRAP   
002250*          POSITION 5 FOLLOWED BY THE TWO CHARACTERS      *       UWRAP   
002300*          THAT REPRESENT THE VALUE OF THE HEXADECIMAL    *       UWRAP   
002350*          CHARACTER.  IF THE RELEASE CHARACTER IS A      *       UWRAP   
002400*          PRINTABLE CHARACTER, PLACE THE VALUE IN        *       UWRAP   
002450*          POSITION 5.  UNWRAP WILL MOVE ANY CHARACTER    *       UWRAP   
002500*          THAT FOLLOWS THE RELEASE CHARACTER AND NOT     *       UWRAP   
002550*          RECOGNIZE THAT CHARACTER AS THE SEGMENT TERM.  *       UWRAP   
002600*      COMPRESSED FILE -- INPUT FILE TO BE UNWRAPPED.     *       UWRAP   
002650*          SHOULD CONTAIN 80-BYTE RECORDS WHICH ARE       *       UWRAP   
002700*          FULLY WRAPPED, AND EACH SEGMENT IN THE FILE    *       UWRAP   
002750*          MUST USE THE SAME CHARACTER AS THE SEGMENT     *       UWRAP   
002800*          TERMINATOR.                                    *       UWRAP   
002850***********************************************************       UWRAP   
002900*                                                         *       UWRAP   
002950*  IF YOU WISH TO WRITE TO A FINAL FLAT FILE WITH A       *       UWRAP   
003000*  RECORD LENGTH OTHER THAN 150-BYTES, MAKE THE FOLLOWING *       UWRAP   
003050*  CHANGES:                                               *       UWRAP   
003100*                                                         *       UWRAP   
003150*  1.) CHANGE FINAL-FLAT-REC FROM X(150) TO THE NEW RECORD*       UWRAP   
003200*      LENGTH.                                            *       UWRAP   
003250*                                                         *       UWRAP   
003300*  2.) CHANGE THE VALUE OF FLAT-RECORD-LENGTH FROM 150 TO *       UWRAP   
003350*      THE NEW RECORD LENGTH.                             *       UWRAP   
003400*                                                         *       UWRAP   
003450*  3.) SEGMENT-ARRAY SHOULD BE CHANGED FROM X(150) TO     *       UWRAP   
003500*      SIZE OF YOUR INPUT FILE RECORD LENGTH.             *       UWRAP   
003550*                                                         *       UWRAP   
003600*  4.) THE NUMBER OF OCCURS FOR SEG-BYTE SHOULD BE        *       UWRAP   
003650*      CHANGED FROM 150 TO THE NUMBER OF BYTES IN YOUR    *       UWRAP   
003700*      INPUT RECORD.                                      *       UWRAP   
003750*                                                         *       UWRAP   
003800*  5.) THE VALUE FOR AUDIT-MSG-1 SHOULD BE CHANGED TO     *       UWRAP   
003850*      REFLECT THE NEW RECORD SIZE.                       *       UWRAP   
003900*                                                         *       UWRAP   
003950*  NOTE:  NO CHANGE TO THE PROCEDURE DIVISION IS REQUIRED *       UWRAP   
004000*                                                         *       UWRAP   
004050***********************************************************       UWRAP   
004100*                                                                 UWRAP   
004150 ENVIRONMENT DIVISION.                                            UWRAP   
004200 CONFIGURATION SECTION.                                           UWRAP   
004250                                                                  UWRAP   
004300 INPUT-OUTPUT SECTION.                                            UWRAP   
004350 FILE-CONTROL.                                                    UWRAP   
004400*$ASSGN$.                                                         UWRAP   
004450     SELECT  I-PARM-FILE                                          UWRAP   
004500             ASSIGN TO  UT-S-INPARM.                              UWRAP   
004550*$ASSGN$.                                                         UWRAP   
004600     SELECT  I-COMPRESSED-FILE                                    UWRAP   
004650             ASSIGN TO  UT-S-INCOMP.                              UWRAP   
004700*$ASSGN$.                                                         UWRAP   
004750     SELECT  FNL-FLAT-FILE                                        UWRAP   
004800             ASSIGN TO  UT-S-FNFLAT.                              UWRAP   
004850*$ASSGN$.                                                         UWRAP   
004900     SELECT  AUDIT-CNTL-REPORT                                    UWRAP   
004950             ASSIGN TO  UT-S-AUDRPT.                              UWRAP   
005000*                                                                 UWRAP   
005050 DATA DIVISION.                                                   UWRAP   
005100 FILE SECTION.                                                    UWRAP   
005150*$FDBEG$                                                          UWRAP   
005200 FD  I-PARM-FILE                                                  UWRAP   
005250     LABEL RECORDS ARE STANDARD                                   UWRAP   
005300     BLOCK CONTAINS 0 RECORDS                                     UWRAP   
005350     RECORDING MODE IS F                                          UWRAP   
005400*$FDEND$                                                          UWRAP   
005450     DATA RECORD IS PARM-REC.                                     UWRAP   
005500 01  PARM-REC                          PIC X(80).                 UWRAP   
005550*$FDBEG$                                                          UWRAP   
005600 FD  I-COMPRESSED-FILE                                            UWRAP   
005650     LABEL RECORDS ARE STANDARD                                   UWRAP   
005700     BLOCK CONTAINS 0 RECORDS                                     UWRAP   
005750     RECORDING MODE IS F                                          UWRAP   
005800*$FDEND$                                                          UWRAP   
005850     DATA RECORD IS COMP-FILE-REC.                                UWRAP   
005900 01  COMP-FILE-REC                     PIC X(80).                 UWRAP   
005950*$FDBEG$                                                          UWRAP   
006000 FD  FNL-FLAT-FILE                                                UWRAP   
006050     LABEL RECORDS ARE STANDARD                                   UWRAP   
006100     BLOCK CONTAINS 0 RECORDS                                     UWRAP   
006150     RECORDING MODE IS F                                          UWRAP   
006200*$FDEND$                                                          UWRAP   
006250     DATA RECORD IS FINAL-FLAT-REC.                               UWRAP   
006300 01  FINAL-FLAT-REC                    PIC X(150).                UWRAP   
006350*$FDBEG$                                                          UWRAP   
006400 FD  AUDIT-CNTL-REPORT                                            UWRAP   
006450     LABEL RECORDS ARE STANDARD                                   UWRAP   
006500     BLOCK CONTAINS 0 RECORDS                                     UWRAP   
006550     RECORDING MODE IS F                                          UWRAP   
006600*$FDEND$                                                          UWRAP   
006650     DATA RECORD IS AUDIT-LINE.                                   UWRAP   
006700 01  AUDIT-LINE                        PIC X(132).                UWRAP   
006750*                                                                 UWRAP   
006800 WORKING-STORAGE  SECTION.                                        UWRAP   
006850 01  WS-VISLOAD-RTN  PIC X(8).                                    UWRAP   
006900                                                                  UWRAP   
006950 01  AUDIT-SWITCH                      PIC X      VALUE SPACES.   UWRAP   
007000     88  SEGMENT-TOO-LONG                         VALUE '1'.      UWRAP   
007050     88  SEG-TERM-MISSING                         VALUE '2'.      UWRAP   
007100     88  PROCESSING-COMPLETE                      VALUE '3'.      UWRAP   
007150                                                                  UWRAP   
007200 01  WS-COMPRESSED-RECORD              PIC X(80).                 UWRAP   
007250 01  WS-COMP-REC REDEFINES WS-COMPRESSED-RECORD.                  UWRAP   
007300     05  COMP-REC-CHAR                 PIC X  OCCURS 80 TIMES.    UWRAP   
007350                                                                  UWRAP   
007400 01  SEGMENT-ARRAY                     PIC X(150).                UWRAP   
007450 01  SEG-ARY-REDF REDEFINES SEGMENT-ARRAY.                        UWRAP   
007500     05  SEG-BYTE                      PIC X  OCCURS 150 TIMES.   UWRAP   
007550                                                                  UWRAP   
007600 01  WS-PARM-REC.                                                 UWRAP   
007650     05  SEG-TERMINATOR                PIC X(5).                  UWRAP   
007700     05  SEGMENT-TERM REDEFINES SEG-TERMINATOR.                   UWRAP   
007750         07  SEG-TERM                  PIC X.                     UWRAP   
007800         07  SEG-TERM-2                PIC X.                     UWRAP   
007800         07  SEG-TERM-3                PIC X.                     UWRAP   
007800         07  SEG-TERM-4                PIC X.                     UWRAP   
007800         07  SEG-TERM-5                PIC X.                     UWRAP   
007850     05  DROP-SEG-TERM                 PIC X.                     UWRAP   
007900     05  REL-CHARACTER                 PIC X(3).                  UWRAP   
007950     05  REL-CHAR-RED REDEFINES REL-CHARACTER.                    UWRAP   
008000         07  RELEASE-CHAR              PIC X.                     UWRAP   
008050         07  REL-CHAR-HEX              PIC X(2).                  UWRAP   
008100     05  FILLER                        PIC X(71).                 UWRAP   
008150                                                                  UWRAP   
008200 01  LENGTH-VARIABLES.                                            UWRAP   
008250     05  FLAT-RECORD-LENGTH            PIC 9(3)   VALUE 150.      UWRAP   
008300     05  COMPRESSED-RECORD-LENGTH      PIC 9(3)   VALUE 80.       UWRAP   
008350                                                                  UWRAP   
008400 01  SUBSCRIPTS.                                                  UWRAP   
008450     05  IN-SUB                        PIC S9(3)  COMP SYNC.      UWRAP   
008500     05  OUT-SUB                       PIC S9(3)  COMP SYNC.      UWRAP   
008550     05  SPLIT-SUB                     PIC S9(3)  COMP SYNC.      UWRAP   
008600                                                                  UWRAP   
008650 01  FLAGS.                                                       UWRAP   
008700     05  REACHED-TERMINATOR-FLAG       PIC XXX    VALUE 'NO '.    UWRAP   
008750         88  REACH-SEG-TERMINATOR                 VALUE 'YES'.    UWRAP   
008800     05  FIND-NEXT-INTERCHANGE-FLAG    PIC XXX    VALUE 'NO '.    UWRAP   
008850         88  LOOK-FOR-INTERCHANGE                 VALUE 'YES'.    UWRAP   
008900     05  EOF-FLAG                      PIC XXX    VALUE 'NO '.    UWRAP   
008950         88  END-OF-FILE                          VALUE 'YES'.    UWRAP   
009000     05  WS-RELEASE-CHARACTER          PIC XXX    VALUE 'NO '.    UWRAP   
009050         88  RELEASE-CHARACTER-USED               VALUE 'YES'.    UWRAP   
009100     05  WS-IGNORE-DELIMITER           PIC XXX    VALUE 'NO '.    UWRAP   
009150         88  IGNORE-DELIMITER                     VALUE 'YES'.    UWRAP   
009200                                                                  UWRAP   
009250 01  WS-OUTPUT-LINES.                                             UWRAP   
009300     05  WS-OUTPUT-LINE-1              PIC X(107).                UWRAP   
009350     05  WS-OUTPUT-LINES-2-5.                                     UWRAP   
009400         10  WS-OUTPUT-LINE-2          PIC X(107).                UWRAP   
009450         10  WS-OUTPUT-LINES-3-5.                                 UWRAP   
009500             15  WS-OUTPUT-LINE-3      PIC X(107).                UWRAP   
009550             15  WS-OUTPUT-LINES-4-5.                             UWRAP   
009600                 20  WS-OUTPUT-LINE-4  PIC X(107).                UWRAP   
009650                 20  WS-OUTPUT-LINE-5  PIC X(107).                UWRAP   
009700                                                                  UWRAP   
009750 01  CURR-DATE.                                                   UWRAP   
009800     05  CURR-YEAR                     PIC XX.                    UWRAP   
009850     05  CURR-MONTH                    PIC XX.                    UWRAP   
009900     05  CURR-DAY                      PIC XX.                    UWRAP   
009950                                                                  UWRAP   
010000 01  CURR-TIME.                                                   UWRAP   
010050     05  CURR-HOURS                    PIC XX.                    UWRAP   
010100     05  CURR-MINUTES                  PIC XX.                    UWRAP   
010150     05  CURR-SECONDS                  PIC XX.                    UWRAP   
010200                                                                  UWRAP   
010250 01  AUDIT-MESSAGES.                                              UWRAP   
010300     05  AUDIT-MSG-1.                                             UWRAP   
010350         10  FILLER                    PIC X(34)                  UWRAP   
010400             VALUE 'SEGMENT EXCEEDS 150 CHARACTERS --'.           UWRAP   
010450         10  FILLER                    PIC X(73)                  UWRAP   
010500             VALUE 'PROCESSING STOPPED'.                          UWRAP   
010550     05  AUDIT-MSG-2.                                             UWRAP   
010600         10  FILLER                    PIC X(35)                  UWRAP   
010650             VALUE 'END OF FILE REACHED BEFORE SEGMENT'.          UWRAP   
010700         10  FILLER                    PIC X(72)                  UWRAP   
010750             VALUE 'TERMINATOR FOUND -- PROCESSING STOPPED'.      UWRAP   
010800     05  AUDIT-MSG-3.                                             UWRAP   
010850         10  FILLER                    PIC X(3)   VALUE SPACES.   UWRAP   
010900         10  FILLER                    PIC X(37)                  UWRAP   
010950             VALUE 'INPUT FILE UNWRAPPED SUCCESSFULLY --'.        UWRAP   
011000         10  FILLER                    PIC X(37)                  UWRAP   
011050             VALUE 'PROCESSING COMPLETE'.                         UWRAP   
011100                                                                  UWRAP   
011150 01  RPT-LINE-1.                                                  UWRAP   
011200     05  FILLER              PIC X(9)   VALUE '   DATE:'.         UWRAP   
011250     05  DATE-OUT.                                                UWRAP   
011300         10  MONTH-OUT       PIC XX.                              UWRAP   
011350         10  FILLER          PIC X      VALUE '/'.                UWRAP   
011400         10  DAY-OUT         PIC XX.                              UWRAP   
011450         10  FILLER          PIC X      VALUE '/'.                UWRAP   
011500         10  YEAR-OUT        PIC XX.                              UWRAP   
011550     05  FILLER              PIC X(39)  VALUE SPACES.             UWRAP   
011600     05  FILLER              PIC X(66)                            UWRAP   
011650         VALUE 'AUDIT CONTROL REPORT'.                            UWRAP   
011700     05  FILLER              PIC X(10)  VALUE 'PAGE: 1'.          UWRAP   
011750                                                                  UWRAP   
011800 01  RPT-LINE-2.                                                  UWRAP   
011850     05  FILLER              PIC X(9)   VALUE '   TIME:'.         UWRAP   
011900     05  TIME-OUT.                                                UWRAP   
011950         10  HOURS-OUT       PIC XX.                              UWRAP   
012000         10  FILLER          PIC X      VALUE ':'.                UWRAP   
012050         10  MINUTES-OUT     PIC XX.                              UWRAP   
012100     05  FILLER              PIC X(118) VALUE SPACES.             UWRAP   
012150                                                                  UWRAP   
012200 01  MESSAGE-LINE.                                                UWRAP   
012250     05  FILLER              PIC X(21)                            UWRAP   
012300         VALUE '   REASON FOR ERROR:'.                            UWRAP   
012350     05  MESSAGE-OUT         PIC X(107).                          UWRAP   
012400     05  FILLER              PIC X(4)  VALUE SPACES.              UWRAP   
012450                                                                  UWRAP   
012500 01  MESSAGE-LINE-1.                                              UWRAP   
012550     05  FILLER              PIC X(21)                            UWRAP   
012600         VALUE '   RECORD IN ERROR:'.                             UWRAP   
012650     05  MESSAGE-AREA-1      PIC X(107).                          UWRAP   
012700     05  FILLER              PIC X(4)  VALUE SPACES.              UWRAP   
012750                                                                  UWRAP   
012800 01  MESSAGE-LINE-2.                                              UWRAP   
012850     05  FILLER              PIC X(21)  VALUE SPACES.             UWRAP   
012900     05  MESSAGE-AREA-2      PIC X(107).                          UWRAP   
012950     05  FILLER              PIC X(4)  VALUE SPACES.              UWRAP   
013100*                                                                 UWRAP   
013150 PROCEDURE DIVISION.                                              UWRAP   
013200                                                                  UWRAP   
013250 0100-MAINLINE.                                                   UWRAP   
013300     OPEN INPUT I-PARM-FILE                                       UWRAP   
013350                I-COMPRESSED-FILE.                                UWRAP   
013400     OPEN OUTPUT FNL-FLAT-FILE                                    UWRAP   
013450                 AUDIT-CNTL-REPORT.                               UWRAP   
013500     READ I-PARM-FILE INTO WS-PARM-REC.                           UWRAP   
013550     MOVE 'NO ' TO EOF-FLAG                                       UWRAP   
013600                   FIND-NEXT-INTERCHANGE-FLAG.                    UWRAP   
013650     PERFORM 0400-READ-RTN THRU 0400-EXIT.                        UWRAP   
013700     PERFORM 0200-UNWRAP-FILE THRU 0200-EXIT                      UWRAP   
013750         UNTIL END-OF-FILE.                                       UWRAP   
013800     IF AUDIT-SWITCH = SPACES                                     UWRAP   
013850         MOVE '3' TO AUDIT-SWITCH                                 UWRAP   
013900         PERFORM 0500-AUDIT-ROUTINE THRU 0500-EXIT.               UWRAP   
013950     CLOSE I-PARM-FILE                                            UWRAP   
014000           I-COMPRESSED-FILE                                      UWRAP   
014050           FNL-FLAT-FILE                                          UWRAP   
014100           AUDIT-CNTL-REPORT.                                     UWRAP   
014150     STOP RUN.                                                    UWRAP   
014200                                                                  UWRAP   
014250 0200-UNWRAP-FILE.                                                UWRAP   
014300     MOVE 1 TO OUT-SUB.                                           UWRAP   
014500     MOVE 'NO ' TO REACHED-TERMINATOR-FLAG.                       UWRAP   
014550     MOVE SPACES TO SEGMENT-ARRAY.                                UWRAP   
014600     PERFORM 0300-SPLIT-COMP-REC THRU 0300-EXIT                   UWRAP   
014650         VARYING IN-SUB FROM SPLIT-SUB BY 1                       UWRAP   
014700             UNTIL OUT-SUB > FLAT-RECORD-LENGTH                   UWRAP   
014750             OR REACH-SEG-TERMINATOR                              UWRAP   
014800             OR END-OF-FILE.                                      UWRAP   
014850     MOVE IN-SUB TO SPLIT-SUB.                                    UWRAP   
014900     IF NOT END-OF-FILE                                           UWRAP   
014950         IF OUT-SUB NOT > FLAT-RECORD-LENGTH                      UWRAP   
015000             WRITE FINAL-FLAT-REC FROM SEGMENT-ARRAY              UWRAP   
015050         ELSE                                                     UWRAP   
015100             MOVE '1' TO AUDIT-SWITCH                             UWRAP   
015150             PERFORM 0500-AUDIT-ROUTINE                           UWRAP   
015200             MOVE 'YES' TO EOF-FLAG                               UWRAP   
015250     ELSE                                                         UWRAP   
015300         IF SEGMENT-ARRAY NOT = SPACES                            UWRAP   
015350             MOVE '2' TO AUDIT-SWITCH                             UWRAP   
015400             PERFORM 0500-AUDIT-ROUTINE                           UWRAP   
015450             MOVE 'YES' TO EOF-FLAG.                              UWRAP   
015500 0200-EXIT.                                                       UWRAP   
015550     EXIT.                                                        UWRAP   
015600                                                                  UWRAP   
015650 0300-SPLIT-COMP-REC.                                             UWRAP   
015700     IF IN-SUB > COMPRESSED-RECORD-LENGTH                         UWRAP   
015750         MOVE 1 TO IN-SUB                                         UWRAP   
015800         PERFORM 0400-READ-RTN THRU 0400-EXIT                     UWRAP   
015850         IF END-OF-FILE                                           UWRAP   
015900             GO TO 0300-EXIT.                                     UWRAP   
015950     IF LOOK-FOR-INTERCHANGE                                      UWRAP   
016000        IF COMP-REC-CHAR(IN-SUB) = SPACES                         UWRAP   
016050           GO TO 0300-EXIT                                        UWRAP   
016100        ELSE                                                      UWRAP   
016150           MOVE 'NO ' TO FIND-NEXT-INTERCHANGE-FLAG.              UWRAP   
016200     MOVE COMP-REC-CHAR(IN-SUB) TO SEG-BYTE(OUT-SUB).             UWRAP   
016250     IF IGNORE-DELIMITER                                          UWRAP   
016300         MOVE 'NO ' TO WS-IGNORE-DELIMITER                        UWRAP   
016350          ADD 1 TO OUT-SUB                                        UWRAP   
016400          GO TO 0300-EXIT                                         UWRAP   
016450     ELSE                                                         UWRAP   
016500     IF RELEASE-CHARACTER-USED AND                                UWRAP   
016550        SEG-BYTE(OUT-SUB) = RELEASE-CHAR                          UWRAP   
016600            MOVE 'YES' TO WS-IGNORE-DELIMITER.                    UWRAP   
016650     IF SEG-BYTE(OUT-SUB) = SEG-TERM OR                           UWRAP   
016650        SEG-BYTE(OUT-SUB) = SEG-TERM-2 OR                         UWRAP   
016650        SEG-BYTE(OUT-SUB) = SEG-TERM-3 OR                         UWRAP   
016650        SEG-BYTE(OUT-SUB) = SEG-TERM-4 OR                         UWRAP   
016650        SEG-BYTE(OUT-SUB) = SEG-TERM-5                            UWRAP   
016700        IF DROP-SEG-TERM = 'Y'                                    UWRAP   
016750           MOVE SPACES TO SEG-BYTE(OUT-SUB)                       UWRAP   
016800           MOVE 'YES' TO REACHED-TERMINATOR-FLAG                  UWRAP   
016850                         FIND-NEXT-INTERCHANGE-FLAG               UWRAP   
016900        ELSE                                                      UWRAP   
016950           MOVE 'YES' TO REACHED-TERMINATOR-FLAG                  UWRAP   
017000                         FIND-NEXT-INTERCHANGE-FLAG.              UWRAP   
017050     ADD 1 TO OUT-SUB.                                            UWRAP   
017100 0300-EXIT.                                                       UWRAP   
017150     EXIT.                                                        UWRAP   
017200                                                                  UWRAP   
017250 0400-READ-RTN.                                                   UWRAP   
017300     MOVE 1 TO SPLIT-SUB.                                         UWRAP   
017350     READ I-COMPRESSED-FILE INTO WS-COMPRESSED-RECORD             UWRAP   
017400         AT END                                                   UWRAP   
017450             MOVE 'YES' TO EOF-FLAG.                              UWRAP   
017500 0400-EXIT.                                                       UWRAP   
017550     EXIT.                                                        UWRAP   
017600*                                                                 UWRAP   
017650 0500-AUDIT-ROUTINE.                                              UWRAP   
017700     ACCEPT CURR-DATE FROM DATE.                                  UWRAP   
017750     ACCEPT CURR-TIME FROM TIME.                                  UWRAP   
017800     MOVE CURR-MONTH TO MONTH-OUT.                                UWRAP   
017850     MOVE CURR-DAY TO DAY-OUT.                                    UWRAP   
017900     MOVE CURR-YEAR TO YEAR-OUT.                                  UWRAP   
017950     MOVE CURR-HOURS TO HOURS-OUT.                                UWRAP   
018000     MOVE CURR-MINUTES TO MINUTES-OUT.                            UWRAP   
018050     WRITE AUDIT-LINE FROM RPT-LINE-1                             UWRAP   
018100         AFTER PAGE.                                              UWRAP   
018150     WRITE AUDIT-LINE FROM RPT-LINE-2                             UWRAP   
018200         AFTER 1.                                                 UWRAP   
018250     IF SEGMENT-TOO-LONG                                          UWRAP   
018300         PERFORM 0600-PRINT-SEGMENT THRU 0600-EXIT                UWRAP   
018350         MOVE AUDIT-MSG-1 TO MESSAGE-OUT                          UWRAP   
018400         WRITE AUDIT-LINE FROM MESSAGE-LINE                       UWRAP   
018450             AFTER 3                                              UWRAP   
018500     ELSE                                                         UWRAP   
018550         IF SEG-TERM-MISSING                                      UWRAP   
018600             PERFORM 0600-PRINT-SEGMENT THRU 0600-EXIT            UWRAP   
018650             MOVE AUDIT-MSG-2 TO MESSAGE-OUT                      UWRAP   
018700             WRITE AUDIT-LINE FROM MESSAGE-LINE                   UWRAP   
018750                 AFTER 3                                          UWRAP   
018800         ELSE                                                     UWRAP   
018850             WRITE AUDIT-LINE FROM AUDIT-MSG-3                    UWRAP   
018900                 AFTER 5.                                         UWRAP   
018950 0500-EXIT.                                                       UWRAP   
019000     EXIT.                                                        UWRAP   
019050*                                                                 UWRAP   
019100 0600-PRINT-SEGMENT.                                              UWRAP   
019150     MOVE SEGMENT-ARRAY TO WS-OUTPUT-LINES.                       UWRAP   
019200     MOVE WS-OUTPUT-LINE-1 TO MESSAGE-AREA-1.                     UWRAP   
019250     WRITE AUDIT-LINE FROM MESSAGE-LINE-1                         UWRAP   
019300         AFTER 5.                                                 UWRAP   
019350     IF WS-OUTPUT-LINES-2-5 = SPACES                              UWRAP   
019400         GO TO 0600-EXIT.                                         UWRAP   
019450     MOVE WS-OUTPUT-LINE-2 TO MESSAGE-AREA-2.                     UWRAP   
019500     WRITE AUDIT-LINE FROM MESSAGE-LINE-2                         UWRAP   
019550         AFTER 1.                                                 UWRAP   
019600     IF WS-OUTPUT-LINES-3-5 = SPACES                              UWRAP   
019650         GO TO 0600-EXIT.                                         UWRAP   
019700     MOVE WS-OUTPUT-LINE-3 TO MESSAGE-AREA-2.                     UWRAP   
019750     WRITE AUDIT-LINE FROM MESSAGE-LINE-2                         UWRAP   
019800         AFTER 1.                                                 UWRAP   
019850     IF WS-OUTPUT-LINES-4-5 = SPACES                              UWRAP   
019900         GO TO 0600-EXIT.                                         UWRAP   
019950     MOVE WS-OUTPUT-LINE-4 TO MESSAGE-AREA-2.                     UWRAP   
020000     WRITE AUDIT-LINE FROM MESSAGE-LINE-2                         UWRAP   
020050         AFTER 1.                                                 UWRAP   
020100     IF WS-OUTPUT-LINE-5 = SPACES                                 UWRAP   
020150         GO TO 0600-EXIT.                                         UWRAP   
020200     MOVE WS-OUTPUT-LINE-5 TO MESSAGE-AREA-2.                     UWRAP   
020250     WRITE AUDIT-LINE FROM MESSAGE-LINE-2                         UWRAP   
020300         AFTER 1.                                                 UWRAP   
020350 0600-EXIT.                                                       UWRAP   
020400     EXIT.                                                        UWRAP   
