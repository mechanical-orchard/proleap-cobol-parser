000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT056.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL 2003.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*****************************************************************         
001100*    PROGRAM COMPARES RMS AND DOMAIN INFORMATION BY THE PACK    *         
001200*    SKU NUMBER AND PACK COMPONENT SKU NUMBER. ANY RECORDS ON   *         
001300*    RMS, BUT NOT ON DOMAIN OR ON DOMAIN BUT NOT ON RMS ARE     *         
001400*    WRITTEN TO AN ERROR FILE.  ANY RECORDS THAT MATCH ON THE   *         
001500*    BOTH OF THE KEY FIELDS, BUT HAS DATA THAT DOESN'T MATCH    *         
001600*    WILL BE WRITTEN TO AN ERROR FILE.                          *         
001700*                                                               *         
001800*****************************************************************         
001900 ENVIRONMENT DIVISION.                                                    
002000 INPUT-OUTPUT SECTION.                                                    
002100                                                                          
002200 FILE-CONTROL.                                                            
002300                                                                          
002400     SELECT RMS-SKU-FILE                                                  
002500         ASSIGN TO INP01.                                                 
002600                                                                          
002700     SELECT LOGISTIC-SKU-FILE                                             
002800         ASSIGN TO INP02.                                                 
002900                                                                          
003000     SELECT ERROR-FILE                                                    
003100         ASSIGN TO OUT01.                                                 
003200                                                                          
003300*****************************************************************         
003400 DATA DIVISION.                                                           
003500*****************************************************************         
003600 FILE SECTION.                                                            
003700                                                                          
003800 FD  RMS-SKU-FILE                                                         
003900     RECORDING MODE IS F                                                  
004000     BLOCK CONTAINS 0 RECORDS                                             
004100     LABEL RECORDS ARE STANDARD.                                          
004200                                                                          
004300 01  RMS-SKU-RECORD               PIC X(144).                             
004400                                                                          
004500 FD  LOGISTIC-SKU-FILE                                                    
004600     RECORDING MODE IS F                                                  
004700     BLOCK CONTAINS 0 RECORDS                                             
004800     LABEL RECORDS ARE STANDARD.                                          
004900                                                                          
005000 01  LOGISTIC-SKU-RECORD          PIC X(144).                             
005100                                                                          
005200                                                                          
005300 FD  ERROR-FILE                                                           
005400     RECORDING MODE IS F                                                  
005500     BLOCK CONTAINS 0 RECORDS                                             
005600     LABEL RECORDS ARE STANDARD.                                          
005700                                                                          
005800 01  ERROR-RECORD                 PIC X(144).                             
005900                                                                          
006000                                                                          
006100*****************************************************************         
006200 WORKING-STORAGE SECTION.                                                 
006300                                                                          
006400 01  PS-PROGRAM-SWITCHES.                                                 
006500     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
006600         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
006700         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
006800     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
006900         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
007000         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
007100     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
007200         88  PS-EMAIL-YES                        VALUE 'Y'.               
007300         88  PS-EMAIL-NO                         VALUE 'N'.               
007400                                                                          
007500 01  PC-PROGRAM-CONSTANTS.                                                
007600     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT056'.        
007700     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
007800                                                                          
007900 01  PV-PROGRAM-VARIABLES.                                                
008000     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
008100     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
008200     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
008300     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
008400     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
008500     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
008600     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
008700     05  PV-ALL-NINES                 PIC  9(09) VALUE 99999999.          
008800                                                                          
008900 01  PV-ABEND-FIELDS.                                                     
009000     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
009100                                                 COMP SYNC.               
009200     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT056'.        
009300     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
009400     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
009500     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
009600     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
009700     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
009800     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
009900                                                                          
010000*--------------------------------------------------------------*          
010100*    IMRD030 - INPUT AND OUTPUT RECORD LAYOUT FOR PACK DATA.   *          
010200*                                                              *          
010300*--------------------------------------------------------------*          
010400                                                                          
010500 01  PV-RMS-INPUT.                                                        
010600     COPY XLRD030.                                                        
010700                                                                          
010800 01  PV-LOGISTIC-INPUT.                                                   
010900     COPY XLRD030.                                                        
011000                                                                          
011100*****************************************************************         
011200 PROCEDURE DIVISION.                                                      
011300*****************************************************************         
011400     DISPLAY ' '.                                                         
011500     DISPLAY '**** BEGINNING XLKUT056 ****'.                              
011600     DISPLAY ' '.                                                         
011700                                                                          
011800     OPEN INPUT  RMS-SKU-FILE                                             
011900                 LOGISTIC-SKU-FILE                                        
012000          OUTPUT ERROR-FILE.                                              
012100     SET PS-EMAIL-NO       TO TRUE.                                       
012200                                                                          
012300     PERFORM 1000-READ-RMS-INPUT.                                         
012400     PERFORM 1100-READ-LOGISTIC-INPUT.                                    
012500                                                                          
012600     PERFORM 2000-PROCESS-INPUT-FILE                                      
012700       UNTIL PS-EOF-INPUT1-YES AND                                        
012800             PS-EOF-INPUT2-YES.                                           
012900                                                                          
013000     CLOSE RMS-SKU-FILE                                                   
013100           LOGISTIC-SKU-FILE                                              
013200           ERROR-FILE.                                                    
013300                                                                          
013400     DISPLAY ' '.                                                         
013500     DISPLAY '  INPUT RMS SKU REC    = '                                  
013600              PV-RECORDS-READ-1.                                          
013700     DISPLAY '  INPUT DOMAIN SKU REC = '                                  
013800              PV-RECORDS-READ-2.                                          
013900     DISPLAY ' '.                                                         
014000     DISPLAY 'FILES ARE MATCHED BY PACK SKU NUMBER'.                      
014100     DISPLAY '                     PACK COMPONENT SKU'.                   
014200     DISPLAY ' '.                                                         
014300     DISPLAY PC-FIVE-BLANKS                                               
014400             'RECORDS ON BOTH FILES ---------> '                          
014500              PV-RECORDS-ON-BOTH.                                         
014600     DISPLAY ' '.                                                         
014700     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
014800              PV-RECORDS-WRITTEN.                                         
014900     DISPLAY PC-FIVE-BLANKS                                               
015000             'RECORDS ON RMS ONLY -----------> '                          
015100              PV-RECORDS-ON-RMS-ONLY.                                     
015200     DISPLAY PC-FIVE-BLANKS                                               
015300             'RECORDS ON LEGACY ONLY --------> '                          
015400              PV-RECORDS-ON-LEGACY-ONLY.                                  
015500     DISPLAY PC-FIVE-BLANKS                                               
015600             'RECORDS WITH DATA MISMATCH ----> '                          
015700              PV-RECORDS-DATA-MISMATCH.                                   
015800                                                                          
015900     DISPLAY ' '.                                                         
016000     DISPLAY '****  END OF  XLKUT056  ****'.                              
016100     DISPLAY ' '.                                                         
016200                                                                          
016300*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
016400     IF PS-EMAIL-YES                                                      
016500         MOVE 4095 TO RETURN-CODE                                         
016600     END-IF.                                                              
016700     GOBACK.                                                              
016800                                                                          
016900*****************************************************************         
017000*  READ A RMS SKU RECORD                                                  
017100*****************************************************************         
017200 1000-READ-RMS-INPUT.                                                     
017300                                                                          
017400     MOVE '1000-READ-INPUT-FILE    ' TO PV-ABEND-PARAGRAPH.               
017500                                                                          
017600     INITIALIZE PV-RMS-INPUT.                                             
017700                                                                          
017800     READ RMS-SKU-FILE                                                    
017900          INTO PV-RMS-INPUT                                               
018000            AT END                                                        
018100               SET PS-EOF-INPUT1-YES TO TRUE                              
018200     END-READ.                                                            
018300                                                                          
018400     IF PS-EOF-INPUT1-YES                                                 
018500        MOVE PV-ALL-NINES TO XL030-PKSKU-PK-SKU-NBR                       
018600                          OF PV-RMS-INPUT                                 
018700        MOVE PV-ALL-NINES TO XL030-PKSKU-CPT-SKU-NBR                      
018800                          OF PV-RMS-INPUT                                 
018900     ELSE                                                                 
019000        ADD +1 TO PV-RECORDS-READ-1                                       
019100     END-IF.                                                              
019200                                                                          
019300*****************************************************************         
019400*  READ A LOGISTIC SKU RECORD                                             
019500*****************************************************************         
019600 1100-READ-LOGISTIC-INPUT.                                                
019700                                                                          
019800     MOVE '1100-READ-LOGISTIC-INPUT' TO PV-ABEND-PARAGRAPH.               
019900                                                                          
020000     INITIALIZE PV-LOGISTIC-INPUT.                                        
020100                                                                          
020200     READ LOGISTIC-SKU-FILE                                               
020300          INTO PV-LOGISTIC-INPUT                                          
020400            AT END                                                        
020500               SET PS-EOF-INPUT2-YES TO TRUE                              
020600     END-READ.                                                            
020700                                                                          
020800     IF PS-EOF-INPUT2-YES                                                 
020900        MOVE PV-ALL-NINES TO XL030-PKSKU-PK-SKU-NBR                       
021000                          OF PV-LOGISTIC-INPUT                            
021100        MOVE PV-ALL-NINES TO XL030-PKSKU-CPT-SKU-NBR                      
021200                          OF PV-LOGISTIC-INPUT                            
021300     ELSE                                                                 
021400        ADD +1 TO PV-RECORDS-READ-2                                       
021500     END-IF.                                                              
021600                                                                          
021700*****************************************************************         
021800*                                                               *         
021900*****************************************************************         
022000 2000-PROCESS-INPUT-FILE.                                                 
022100                                                                          
022200     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
022300                                                                          
022400     EVALUATE TRUE                                                        
022500         WHEN XL030-PKSKU-PK-SKU-NBR OF PV-RMS-INPUT <                    
022600              XL030-PKSKU-PK-SKU-NBR OF PV-LOGISTIC-INPUT                 
022700*             ************************************                        
022800*             *         SKU NUMBER                                        
022900*             *      ON RMS NOT ON DOMAIN                                 
023000*             ************************************                        
023100              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
025800              MOVE 'NOT ON DOMAIN PACK SKU'                               
                                    TO XL030-PKSKU-ERROR-MESSAGE                
025800                              OF PV-RMS-INPUT                             
023200              PERFORM 2100-WRITE-RMS-ERROR                                
023300              PERFORM 1000-READ-RMS-INPUT                                 
023400                                                                          
023500         WHEN XL030-PKSKU-PK-SKU-NBR OF PV-RMS-INPUT >                    
023600              XL030-PKSKU-PK-SKU-NBR OF PV-LOGISTIC-INPUT                 
023700*             ************************************                        
023800*             *          SKU NUMBER                                       
023900*             *       ON DOMAIN NOT ON RMS                                
024000*             ************************************                        
024100              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
025800              MOVE 'NOT ON RMS PACK SKU'                                  
                                          TO XL030-PKSKU-ERROR-MESSAGE          
025800                                    OF PV-LOGISTIC-INPUT                  
024200              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
024300              PERFORM 1100-READ-LOGISTIC-INPUT                            
024400                                                                          
024500         WHEN XL030-PKSKU-CPT-SKU-NBR OF PV-RMS-INPUT <                   
024600              XL030-PKSKU-CPT-SKU-NBR OF PV-LOGISTIC-INPUT                
024700*             ************************************                        
024800*             *         COMPONENT SKU NUMBER                              
024900*             *      ON RMS NOT ON DOMAIN                                 
025000*             ************************************                        
025100              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
025800              MOVE 'NOT ON DOMAIN PACK SKU'                               
                                    TO XL030-PKSKU-ERROR-MESSAGE                
025800                              OF PV-RMS-INPUT                             
025200              PERFORM 2100-WRITE-RMS-ERROR                                
025300              PERFORM 1000-READ-RMS-INPUT                                 
025400                                                                          
025500         WHEN XL030-PKSKU-CPT-SKU-NBR OF PV-RMS-INPUT >                   
025600              XL030-PKSKU-CPT-SKU-NBR OF PV-LOGISTIC-INPUT                
025700*             ************************************                        
025800*             *          COMPONENT SKU NUMBER                             
025900*             *       ON DOMAIN NOT ON RMS                                
026000*             ************************************                        
026100              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
025800              MOVE 'NOT ON RMS PACK SKU'                                  
                                          TO XL030-PKSKU-ERROR-MESSAGE          
025800                                    OF PV-LOGISTIC-INPUT                  
026200              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
026300              PERFORM 1100-READ-LOGISTIC-INPUT                            
026400                                                                          
026500         WHEN OTHER                                                       
026600*             ************************************                        
026700*             *       ON RMS AND ON DOMAIN                                
026800*             ************************************                        
026900              ADD 1 TO PV-RECORDS-ON-BOTH                                 
027000              IF XL030-PKSKU-PK-RATO-UNT-QTY OF PV-RMS-INPUT =            
027100                 XL030-PKSKU-PK-RATO-UNT-QTY OF PV-LOGISTIC-INPUT         
027200                 PERFORM 1000-READ-RMS-INPUT                              
027300                 PERFORM 1100-READ-LOGISTIC-INPUT                         
027400              ELSE                                                        
027500*                ************************************                     
027600*                *       DATA DOESN'T MATCH                               
027700*                ************************************                     
027800                 ADD 1 TO PV-RECORDS-DATA-MISMATCH                        
025800                 MOVE 'DATA MISMATCH - RMS'                               
                                            TO XL030-PKSKU-ERROR-MESSAGE        
025800                                      OF PV-RMS-INPUT                     
027900                 PERFORM 2100-WRITE-RMS-ERROR                             
025800                 MOVE 'DATA MISMATCH - DOMAIN'                            
                                            TO XL030-PKSKU-ERROR-MESSAGE        
025800                                      OF PV-LOGISTIC-INPUT                
028000                 PERFORM 2150-WRITE-LOGISTIC-ERROR                        
028100                 PERFORM 1000-READ-RMS-INPUT                              
028200                 PERFORM 1100-READ-LOGISTIC-INPUT                         
028300              END-IF                                                      
028400                                                                          
028500     END-EVALUATE.                                                        
028600                                                                          
028700*****************************************************************         
028800*  WRITE A RMS RECORD TO THE ERROR FILE                                   
028900*****************************************************************         
029000 2100-WRITE-RMS-ERROR.                                                    
029100                                                                          
029200     WRITE ERROR-RECORD FROM                                              
029300         PV-RMS-INPUT                                                     
029400     END-WRITE.                                                           
029500     ADD +1                 TO PV-RECORDS-WRITTEN.                        
029600                                                                          
029700*    SET SWITCH FOR E-MAIL NOTIFICATION OF RMS ERROR.                     
029800     SET PS-EMAIL-YES       TO TRUE.                                      
029900                                                                          
030000*****************************************************************         
030100*  WRITE A LOGISTIC RECORD TO THE ERROR FILE                              
030200*****************************************************************         
030300 2150-WRITE-LOGISTIC-ERROR.                                               
030400                                                                          
030500     WRITE ERROR-RECORD FROM                                              
030600         PV-LOGISTIC-INPUT                                                
030700     END-WRITE.                                                           
030800     ADD +1                 TO PV-RECORDS-WRITTEN.                        
030900                                                                          
031000*    SET SWITCH FOR E-MAIL NOTIFICATION OF LOGISTIC ERROR.                
031100     SET PS-EMAIL-YES       TO TRUE.                                      
031200                                                                          
