       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         ETKUP052.                                            
       DATE-WRITTEN.       08/07/2000.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:    THE PURPOSE OF THE MODULE IS TO UPDATE THE FUNCTIONAL        
      *         GROUP TRANSACTION STATUS TABLE, TFGTRST FOR DOCUMENT            
      *         TRACKING WITH THE DOCUMENT'S BYTE COUNT AT THE DETAIL           
      *         LEVEL.                                                          
      ******************************************************************        
0201JF* 02-09-2001 JON FIEDLER   (0201JF)                                       
0201JF* ADDED VENDOR NAME TO SYSOUT ERROR MESSAGE.                              
0201JF******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS ET027-EXTRACT-RECORD.                                 
                                                                                
           COPY ETRD027.                                                        
                                                                        ETKUT004
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                        ETKUT005
       01  PROGRAM-SWITCHES.                                            ETKUT004
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.           ETKUT004
               88  END-OF-FILE                     VALUE 'Y'.           ETKUT004
           05  ERRORS-FOUND-SW           PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
                                                                                
      ******************************************************************        
      * DB2 ERROR HANDLING ARGUMENTS.                                           
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR THE FUNCTIONAL GROUP TRANSACTION STATUS TABLE.         
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TFGTRST                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR THE TENTNME TABLE.                                     
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TABLE CURSOR FOR THE TTRDPRT TABLE.                                     
      *****************************************************************         
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      /*****************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE DOCUMENT TRACKING             
      *      BYTE COUNT EXTRACT DATASETS FOR INBOUND AND OUTBOUND EDI           
      *      TRADES.                                                            
      *                                                                         
      *         READ THE DOCUMENT TRACKING FUNCTIONAL GROUP TRANSACTION         
      *      STATUS TABLE USING THE TP-DKEY KEY AND TIMESTAMP, THE              
      *      FUNCTIONAL GROUP CONTROL, THE TRANSACTION SET CONTROL,             
      *      AND THE DETAIL TEXT ID. UPDATE THE ROW WHICH MATCHES THE           
      *      SEARCH CRITERIA WITH THE BYTE COUNT FOR THE DOCUMENT.              
      *                                                                         
      *         IF A PROBLEM OCCURS WHILE UPDATING TFGTRST, ABEND WITH A        
      *      4095 ERROR CODE. THE ERROR CODE TRIGGERS AN EMAIL TO THE           
      *      EDI TEAM FOR ERROR CORRECTION.                                     
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION                                          
              THRU 1000-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 2000-PROCESS-INPUT-FILE                                      
              THRU 2000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           PERFORM 9000-EOJ-TASKS                                               
              THRU 9000-EOJ-TASKS-EXIT.                                         
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-PROCESS INPUT FILE.                                                
      ******************************************************************        
       2000-PROCESS-INPUT-FILE.                                                 
                                                                                
           IF ET027-SOURCE IS EQUAL TO 'I'                                      
               PERFORM 3000-PROCESS-INBOUND-EDI                                 
                  THRU 3000-PROCESS-INBOUND-EDI-EXIT                            
           ELSE                                                                 
               PERFORM 3100-PROCESS-OUTBOUND-EDI                                
                  THRU 3100-PROCESS-OUTBOUND-EDI-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       2000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3000-PROCESS-INBOUND-EDI.                                               
      ******************************************************************        
       3000-PROCESS-INBOUND-EDI.                                                
                                                                                
           MOVE ET027-TP-DKEY           TO FGTRST-TP-DKEY.                      
           MOVE ET027-TP-DKEY-TMST      TO FGTRST-TP-DKEY-TMST.                 
           MOVE ET027-FUNC-CDE-CNTL-NBR TO FGTRST-FUNC-CDE-CNTL-NBR.            
           MOVE ET027-TRN-SET-CNTL-NBR  TO FGTRST-TRN-SET-CNTL-NBR.             
           MOVE ET027-TRN-DTL-TXT       TO FGTRST-TRN-DTL-TXT.                  
           MOVE ET027-BYTE-COUNT        TO FGTRST-CHARS-XMIT-NBR.               
                                                                                
           EXEC SQL                                                             
              UPDATE TFGTRST                                                    
                 SET CHARS_XMIT_NBR = :FGTRST-CHARS-XMIT-NBR                    
               WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
                 AND TP_DKEY_TMST = :FGTRST-TP-DKEY-TMST                        
                 AND FUNC_CDE_CNTL_NBR = :FGTRST-FUNC-CDE-CNTL-NBR              
                 AND TRN_SET_CNTL_NBR = :FGTRST-TRN-SET-CNTL-NBR                
                 AND TRN_DTL_TXT = :FGTRST-TRN-DTL-TXT                          
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE IS NOT EQUAL TO ZEROS                                     
              DISPLAY SPACES                                                    
              DISPLAY '*** ABEND - ETKUP052-1 ***'                              
              DISPLAY 'ERROR UPDATING TFGTRST TABLE'                            
              DISPLAY 'TP-DKEY = ' ET027-TP-DKEY                                
              DISPLAY 'TP-DKEY-TMST = ' ET027-TP-DKEY-TMST                      
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' ET027-FUNC-CDE-CNTL-NBR            
              DISPLAY 'TRN-SET-CNTL-NBR = ' ET027-TRN-SET-CNTL-NBR              
              DISPLAY 'TRN-DTL-TXT = ' ET027-TRN-DTL-TXT                        
              DISPLAY 'SQLCODE = ' SQLCODE                                      
0201JF        PERFORM 3200-GET-VENDOR-NAME                                      
0201JF        DISPLAY 'VENDOR NAME = ' ENTNME-ENT-NAME-DESC                     
              MOVE 'Y' TO ERRORS-FOUND-SW                                       
           END-IF.                                                              
                                                                                
       3000-PROCESS-INBOUND-EDI-EXIT.                                           
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3100-PROCESS-OUTBOUND-EDI.                                              
      ******************************************************************        
       3100-PROCESS-OUTBOUND-EDI.                                               
                                                                                
           MOVE ET027-TP-DKEY           TO FGTRST-TP-DKEY.                      
           MOVE ET027-FUNC-CDE-CNTL-NBR TO FGTRST-FUNC-CDE-CNTL-NBR.            
           MOVE ET027-TRN-SET-CNTL-NBR  TO FGTRST-TRN-SET-CNTL-NBR.             
           MOVE ET027-TRN-DTL-TXT       TO FGTRST-TRN-DTL-TXT.                  
           MOVE ET027-BYTE-COUNT        TO FGTRST-CHARS-XMIT-NBR.               
                                                                                
           EXEC SQL                                                             
              UPDATE TFGTRST                                                    
                 SET CHARS_XMIT_NBR = :FGTRST-CHARS-XMIT-NBR                    
               WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
                 AND FUNC_CDE_CNTL_NBR = :FGTRST-FUNC-CDE-CNTL-NBR              
                 AND TRN_SET_CNTL_NBR = :FGTRST-TRN-SET-CNTL-NBR                
                 AND TRN_DTL_TXT = :FGTRST-TRN-DTL-TXT                          
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE IS NOT EQUAL TO ZEROS                                     
              DISPLAY SPACES                                                    
              DISPLAY '*** ABEND - ETKUP052-2 ***'                              
              DISPLAY 'ERROR UPDATING TFGTRST TABLE'                            
              DISPLAY 'TP-DKEY = ' ET027-TP-DKEY                                
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' ET027-FUNC-CDE-CNTL-NBR            
              DISPLAY 'TRN-SET-CNTL-NBR = ' ET027-TRN-SET-CNTL-NBR              
              DISPLAY 'TRN-DTL-TXT = ' ET027-TRN-DTL-TXT                        
              DISPLAY 'SQLCODE = ' SQLCODE                                      
0201JF        PERFORM 3200-GET-VENDOR-NAME                                      
0201JF        DISPLAY 'VENDOR NAME = ' ENTNME-ENT-NAME-DESC                     
              MOVE 'Y' TO ERRORS-FOUND-SW                                       
           END-IF.                                                              
                                                                                
       3100-PROCESS-OUTBOUND-EDI-EXIT.                                          
           EXIT.                                                                
                                                                                
0201JF 3200-GET-VENDOR-NAME.                                                    
0201JF                                                                          
0201JF     EXEC SQL                                                             
0201JF        SELECT ENT_NAME_DESC                                              
0201JF          INTO :ENTNME-ENT-NAME-DESC                                      
0201JF          FROM TENTNME E                                                  
0201JF              ,TTRDPRT T                                                  
0201JF         WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
0201JF           AND T.ENT_ID = E.ENT_ID                                        
0201JF           AND TYPE_CDE = '01'                                            
0201JF     END-EXEC.                                                            
0201JF                                                                          
0201JF     IF SQLCODE NOT = +000                                                
0201JF        MOVE 'NOT FOUND' TO ENTNME-ENT-NAME-DESC                          
0201JF     END-IF.                                                              
0201JF                                                                          
      /*****************************************************************        
      * 8000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
                                                                        ETKUT004
           READ INPUT-FILE                                              ETKUT004
              AT END                                                    ETKUT004
                 SET END-OF-FILE TO TRUE.                               ETKUT004
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 9000-END OF JOB TASKS.                                                  
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE.                                         
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
       9000-EOJ-TASKS-EXIT.                                                     
           EXIT.                                                                
                                                                                
