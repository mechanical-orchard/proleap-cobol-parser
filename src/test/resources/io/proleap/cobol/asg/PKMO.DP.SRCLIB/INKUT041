       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    INKUT041.                                         00020015
       AUTHOR.        JIM ARENDT.                                       00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  09/02/2013.                                       00050012
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      * THIS PROGRAM WILL READ IN THE INVENTORY STORE LIST AND THE      00080011
      * PARTIAL SHIP FILE AND CHECK FOR ANY STORE NUMBER MATCHES.       00090013
      ******************************************************************00100001
      * DATE:09/2013    MADE BY:JIM ARENDT  INITIAL IMPLEMENTATION      00110012
      ******************************************************************00120001
P48331* CHG 89593 09/19/14 - MODIFIED ORDER NUMBER TO BE AS 10 DIGIT    00122016
P48331*                      ONE INSTEAD OF 9 TO AVOID TRUNCATION.      00123016
P48331******************************************************************00124016
       ENVIRONMENT DIVISION.                                            00130001
       CONFIGURATION SECTION.                                           00140001
       SOURCE-COMPUTER.    IBM-3090.                                    00150001
       OBJECT-COMPUTER.    IBM-3090.                                    00160001
       INPUT-OUTPUT SECTION.                                            00170001
       FILE-CONTROL.                                                    00180001
                                                                        00190001
           SELECT STORE-FILE                                            00200001
                  ASSIGN TO INP01.                                      00210001
                                                                        00220001
           SELECT PARTIAL-SHIP-FILE                                     00230001
                  ASSIGN TO INP02.                                      00240001
                                                                        00250001
           SELECT OUTPUT-FILE                                           00260001
                  ASSIGN TO OUT01.                                      00270001
                                                                        00280001
       DATA DIVISION.                                                   00290001
       FILE SECTION.                                                    00300001
                                                                        00310001
       FD  STORE-FILE                                                   00320001
           RECORDING MODE F                                             00330001
           BLOCK CONTAINS 0 RECORDS                                     00340001
           LABEL RECORDS ARE OMITTED                                    00350001
           DATA RECORD IS STORE-FILE-RECORD.                            00360001
       01  STORE-FILE-RECORD                   PIC  X(04).              00370005
                                                                        00380001
       FD  PARTIAL-SHIP-FILE                                            00390001
           RECORDING MODE F                                             00400001
           BLOCK CONTAINS 0 RECORDS                                     00410001
           LABEL RECORDS ARE OMITTED                                    00420001
           DATA RECORD IS PARTIAL-SHIP-RECORD.                          00430001
P48331 01  PARTIAL-SHIP-RECORD                 PIC  X(109).             00440016
                                                                        00450001
       FD  OUTPUT-FILE                                                  00460001
           RECORDING MODE IS F                                          00470001
           LABEL RECORDS OMITTED                                        00480001
           BLOCK CONTAINS 0 RECORDS                                     00490001
           DATA RECORD IS OUTPUT-RECORD.                                00500001
P48331 01  OUTPUT-RECORD                       PIC  X(109).             00510016
                                                                        00520001
       WORKING-STORAGE SECTION.                                         00530001
      ******************************************************************00540001
      * STORE INPUT RECORD                                              00550001
      ******************************************************************00560001
        01 IN-STORE-REC.                                                00570001
           05 IN-SR-STORE-NBR                  PIC  9(04) VALUE ZEROES. 00580005
                                                                        00590001
      ******************************************************************00600001
      * OUTPUT PARTIAL SHIP RECORD                                      00610010
      ******************************************************************00620001
           COPY INRD040.                                                00630015
                                                                        00640001
      ******************************************************************00650001
      * PROGRAM CONSTANTS                                               00660001
      ******************************************************************00670001
       01  PC-CONSTANTS-FIELDS.                                         00680001
           05  PC-PROGRAM-NAME                 PIC  X(08) VALUE         00690001
                                                          'INKUT041'.   00700015
           05  PC-MAXIMUM-RETRY-COUNT          PIC S9(04) VALUE +3      00710001
                                                          COMP SYNC.    00720001
                                                                        00730001
      ******************************************************************00740001
      * PROGRAM SWITCHES                                                00750001
      ******************************************************************00760001
       01  PS-SWITCH-AREAS.                                             00770001
           05  PS-FIRST-TIME-SW                PIC  X(01) VALUE 'Y'.    00780001
               88  PS-FIRST-TIME                          VALUE 'Y'.    00790001
               88  PS-NOT-FIRST-TIME                      VALUE 'N'.    00800001
           05  PS-END-OF-STORE-SW              PIC  X(01) VALUE 'N'.    00810001
               88  PS-END-OF-STORE                        VALUE 'Y'.    00820001
               88  PS-NOT-END-OF-STORE                    VALUE 'N'.    00830001
           05  PS-END-OF-PARTIAL-SW            PIC  X(01) VALUE 'N'.    00840001
               88  PS-END-OF-PARTIAL                      VALUE 'Y'.    00850001
               88  PS-NOT-END-OF-PARTIAL                  VALUE 'N'.    00860001
           05  PS-STORE-FOUND-SW               PIC  X(01) VALUE 'N'.    00870001
               88  PS-STORE-FOUND                         VALUE 'Y'.    00880001
               88  PS-STORE-NOT-FOUND                     VALUE 'N'.    00890001
                                                                        00900001
      ******************************************************************00910001
      * PROGRAM ACCUMULATORS                                            00920001
      ******************************************************************00930001
       01  PA-PROGRAM-ACCUMULATORS.                                     00940001
           05  PA-STORE-FILE-IN-RECS           PIC  9(09) VALUE ZEROS.  00950001
           05  PA-PARTIAL-SHIP-IN-RECS         PIC  9(09) VALUE ZEROS.  00960001
           05  PA-PARTIAL-SHIP-BYPASSED        PIC  9(09) VALUE ZEROS.  00970001
           05  PA-OUTPUT-RECORDS-WRITTEN       PIC  9(09) VALUE ZEROS.  00980001
                                                                        00990001
      ******************************************************************01000001
      * PROGRAM VARIABLES                                               01010001
      ******************************************************************01020001
       01  PV-VARIABLE-FIELDS.                                          01030001
           05  PV-PARAGRAPH-NAME               PIC  X(30)  VALUE SPACE. 01040002
           05  PV-OPERATION-MSG                PIC  X(30)  VALUE SPACE. 01050002
           05  PV-ERROR-TEXT                   PIC  X(100) VALUE SPACE. 01060001
           05  PV-EDIT-FIELD                   PIC  ZZZ,ZZZ,ZZ9.        01070001
           05  PV-SAVE-STORE-NBR               PIC  9(04)  VALUE ZERO.  01080002
                                                                        01090001
      ******************************************************************01100001
      **   STORE FILE TABLE                                             01110001
      ******************************************************************01120001
           05  PV-SF-TABLE.                                             01130001
               10  PV-SF-ENTRY            OCCURS 2000 TIMES             01140001
                                           INDEXED BY PV-SF-INDX.       01150001
                   15  PV-SF-STORE-NBR         PIC  9(04).              01160002
                                                                        01170001
      ******************************************************************01180001
      * ABEND INFORMATION                                               01190001
      ******************************************************************01200001
       01  ABEND-CODE                          PIC S9(04) VALUE ZEROS   01210001
                                                          COMP SYNC.    01220001
           88  ABEND-4001-WRITE-ERROR                     VALUE +4001.  01230001
           88  ABEND-4002-READ-ERROR                      VALUE +4002.  01240001
           88  ABEND-4003-CTRL-CARD-ERROR                 VALUE +4003.  01250001
           88  ABEND-4004-TABLE-ERROR                     VALUE +4004.  01260001
           88  ABEND-4005-OPEN-ERROR                      VALUE +4005.  01270001
           88  ABEND-4006-CLOSE-ERROR                     VALUE +4006.  01280001
           88  ABEND-4007-SEQUENCE-ERROR                  VALUE +4007.  01290001
           88  ABEND-4008-DATA-ERROR                      VALUE +4008.  01300001
           88  ABEND-4009-KEY-DATA-ERROR                  VALUE +4009.  01310001
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.  01320001
           88  ABEND-4019-CAL-SUB-ERROR                   VALUE +4019.  01330001
           88  ABEND-4020-MQ-ERROR                        VALUE +4020.  01340001
           88  ABEND-4444-FORCED-ABEND                    VALUE +4444.  01350001
                                                                        01360001
      ******************************************************************01370001
      ****ABEND ROUTINE INFORMATION                                     01380001
           COPY DPWS004.                                                01390001
                                                                        01400001
       PROCEDURE DIVISION.                                              01410001
      ******************************************************************01420001
       0000-MAINLINE-PROCESSING.                                        01430001
                                                                        01440001
           PERFORM 1000-INITIALIZATION.                                 01450001
                                                                        01460001
           PERFORM 2000-LOAD-STORE-TABLE                                01470001
              UNTIL PS-END-OF-STORE.                                    01480001
                                                                        01490001
           SET PS-FIRST-TIME  TO TRUE.                                  01500003
           PERFORM 4000-PARTIAL-SHIP-LOOP                               01510001
              UNTIL PS-END-OF-PARTIAL.                                  01520001
                                                                        01530001
           PERFORM 9000-WRAPUP.                                         01540001
                                                                        01550001
           GOBACK.                                                      01560001
                                                                        01570001
      ******************************************************************01580001
      * INITIALIZATION ROUTINE                                          01590001
      ******************************************************************01600001
       1000-INITIALIZATION.                                             01610001
                                                                        01620001
           DISPLAY ' '.                                                 01630001
           DISPLAY '***************************************'.           01640001
           DISPLAY '*** PROGRAM NAME: '         PC-PROGRAM-NAME.        01650001
           DISPLAY ' '.                                                 01660001
                                                                        01670001
           OPEN INPUT  STORE-FILE                                       01680001
                       PARTIAL-SHIP-FILE.                               01690001
           OPEN OUTPUT OUTPUT-FILE.                                     01700001
                                                                        01710001
           INITIALIZE PV-SF-TABLE.                                      01720001
           SET PS-FIRST-TIME       TO TRUE.                             01730001
                                                                        01740001
      ******************************************************************01750001
      * READ A RECORD FROM THE STORE INPUT FILE AND LOAD IT INTO AN     01760001
      * INTERNAL TABLE                                                  01770001
      ******************************************************************01780001
       2000-LOAD-STORE-TABLE.                                           01790001
                                                                        01800001
           PERFORM 3000-READ-STORE-FILE.                                01810001
                                                                        01820001
           IF PS-END-OF-STORE                                           01830001
              CONTINUE                                                  01840001
           ELSE                                                         01850001
              PERFORM 2250-PROCESS-STORE-CONT                           01860001
           END-IF.                                                      01870001
                                                                        01880001
      ******************************************************************01890001
       2250-PROCESS-STORE-CONT.                                         01900001
                                                                        01910001
           SET PV-SF-INDX                  TO +1.                       01920001
           SEARCH PV-SF-ENTRY                                           01930001
              VARYING PV-SF-INDX                                        01940001
                AT END                                                  01950001
                  DISPLAY '** ABEND **'                                 01960001
                  DISPLAY '** 2250-PROCESS-STORE-CONT **'               01970001
                  DISPLAY '** INTERNAL STORE TABLE IS FULL **'          01980002
                  DISPLAY '** STORE NBR = ' IN-SR-STORE-NBR             01990003
                  DISPLAY ' '                                           02000001
                  MOVE +4004               TO ABEND-CODE                02010001
                  CALL 'ILBOABN0' USING ABEND-CODE                      02020001
                WHEN PV-SF-STORE-NBR (PV-SF-INDX) = ZEROES              02030001
                  MOVE IN-SR-STORE-NBR     TO                           02040003
                                           PV-SF-STORE-NBR (PV-SF-INDX) 02050001
           END-SEARCH.                                                  02060001
                                                                        02070001
      ******************************************************************02080001
      * READ A RECORD FROM THE STORE INPUT FILE                         02090001
      ******************************************************************02100001
       3000-READ-STORE-FILE.                                            02110001
                                                                        02120001
           READ STORE-FILE INTO IN-STORE-REC                            02130001
               AT END                                                   02140001
                   SET PS-END-OF-STORE         TO TRUE                  02150001
           END-READ.                                                    02160001
                                                                        02170001
           IF PS-END-OF-STORE                                           02180001
              IF PS-FIRST-TIME                                          02190001
                 DISPLAY '** INPUT STORE FILE IS EMPTY **'              02200001
              END-IF                                                    02210001
           ELSE                                                         02220001
              SET PS-NOT-FIRST-TIME    TO TRUE                          02230001
              ADD 1                    TO PA-STORE-FILE-IN-RECS         02240001
           END-IF.                                                      02250001
                                                                        02260001
      ******************************************************************02270001
      * READ A RECORD FROM THE PARTIAL SHIP INPUT FILE                  02280001
      ******************************************************************02290001
       3100-READ-PARTIAL-SHIP-FILE.                                     02300001
                                                                        02310001
           READ PARTIAL-SHIP-FILE INTO IN040-PARTIAL-SHIP-REC           02320015
               AT END                                                   02330001
                   SET PS-END-OF-PARTIAL       TO TRUE                  02340001
           END-READ.                                                    02350001
                                                                        02360001
           IF PS-END-OF-PARTIAL                                         02370001
              IF PS-FIRST-TIME                                          02380001
                 DISPLAY ' ** INPUT PARTIAL SHIP FILE IS EMPTY **'      02390001
              END-IF                                                    02400001
           ELSE                                                         02410001
              SET PS-NOT-FIRST-TIME    TO TRUE                          02420001
              ADD 1                    TO PA-PARTIAL-SHIP-IN-RECS       02430001
           END-IF.                                                      02440001
                                                                        02450001
      ******************************************************************02460001
      * PROCESSING DRIVER                                               02470001
      ******************************************************************02480001
       4000-PARTIAL-SHIP-LOOP.                                          02490001
                                                                        02500001
           PERFORM 3100-READ-PARTIAL-SHIP-FILE.                         02510001
                                                                        02520001
           IF PS-END-OF-PARTIAL                                         02530008
              CONTINUE                                                  02540008
           ELSE                                                         02550008
              IF IN040-SHIP-NODE = PV-SAVE-STORE-NBR                    02560015
                 IF PS-STORE-FOUND                                      02570008
                    PERFORM 8000-WRITE-OUTPUT-RECORD                    02580008
                 ELSE                                                   02590008
                    ADD 1 TO PA-PARTIAL-SHIP-BYPASSED                   02600008
                 END-IF                                                 02610008
              ELSE                                                      02620008
                 PERFORM 5200-SEARCH-STORE-TABLE                        02630008
                 IF PS-STORE-FOUND                                      02640008
                    PERFORM 8000-WRITE-OUTPUT-RECORD                    02650008
                 ELSE                                                   02660008
                    ADD 1 TO PA-PARTIAL-SHIP-BYPASSED                   02670008
                 END-IF                                                 02680008
              END-IF                                                    02690008
           END-IF.                                                      02700001
                                                                        02710001
      ******************************************************************02720001
      * USING THE STORE NUMBER FROM THE PARTIAL SHIP INPUT FILE, SEARCH 02730001
      * THE INTERNAL STORE TABLE TO CHECK FOR EXISTANCE.                02740001
      ******************************************************************02750001
       5200-SEARCH-STORE-TABLE.                                         02760001
                                                                        02770001
           SET PV-SF-INDX                  TO +1.                       02780003
           SEARCH PV-SF-ENTRY                                           02790001
              VARYING PV-SF-INDX                                        02800003
                 AT END                                                 02810001
                    SET PS-STORE-NOT-FOUND TO TRUE                      02820003
                    MOVE IN040-SHIP-NODE   TO PV-SAVE-STORE-NBR         02830015
                 WHEN PV-SF-STORE-NBR (PV-SF-INDX) = IN040-SHIP-NODE    02840015
                    SET PS-STORE-FOUND     TO TRUE                      02850003
                    MOVE IN040-SHIP-NODE   TO PV-SAVE-STORE-NBR         02860015
           END-SEARCH.                                                  02870001
                                                                        02880001
      ******************************************************************02890013
      * WRITE THE OUTPUT RECORD                                         02900001
      ******************************************************************02910013
       8000-WRITE-OUTPUT-RECORD.                                        02920001
                                                                        02930001
           WRITE OUTPUT-RECORD FROM IN040-PARTIAL-SHIP-REC.             02940015
                                                                        02950001
           ADD +1              TO PA-OUTPUT-RECORDS-WRITTEN.            02960001
                                                                        02970001
      ******************************************************************02980001
      * CLOSE OUTPUT FILE.                                              02990001
      ******************************************************************03000001
       9000-WRAPUP.                                                     03010001
                                                                        03020001
           CLOSE STORE-FILE                                             03030001
                 PARTIAL-SHIP-FILE                                      03040001
                 OUTPUT-FILE.                                           03050001
                                                                        03060001
           PERFORM 9100-DISPLAY-COUNTS.                                 03070001
                                                                        03080001
      ******************************************************************03090001
      * DISPLAY PROGRAM COUNTS                                          03100001
      ******************************************************************03110001
       9100-DISPLAY-COUNTS.                                             03120001
                                                                        03130001
           MOVE PA-STORE-FILE-IN-RECS           TO PV-EDIT-FIELD.       03140001
           DISPLAY ' STORE FILE RECORDS READ...:'  PV-EDIT-FIELD.       03150001
           MOVE PA-PARTIAL-SHIP-IN-RECS         TO PV-EDIT-FIELD.       03160001
           DISPLAY ' PARTIAL SHIP RECS READ....:'  PV-EDIT-FIELD.       03170001
           MOVE PA-PARTIAL-SHIP-BYPASSED        TO PV-EDIT-FIELD.       03180001
           DISPLAY ' PARTIAL SHIP SKIPPED......:'  PV-EDIT-FIELD.       03190001
           DISPLAY ' '.                                                 03200001
           MOVE PA-OUTPUT-RECORDS-WRITTEN       TO PV-EDIT-FIELD.       03210001
           DISPLAY ' OUTPUT RECORDS WRITTEN....:'  PV-EDIT-FIELD.       03220001
           DISPLAY ' '.                                                 03230001
           DISPLAY '***************************************'.           03240001
           DISPLAY ' '.                                                 03250001
