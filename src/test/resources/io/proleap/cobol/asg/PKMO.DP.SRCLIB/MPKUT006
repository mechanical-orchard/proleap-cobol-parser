00001  IDENTIFICATION DIVISION.                                         08/16/95
00002                                                                   MPKUT006
00003  PROGRAM-ID.    MPKUT006                                             LV027
00004                                                                   MPKUT006
00005  AUTHOR.        W A EHMKE    - KOHLS DEPARTMENT STORES.           MPKUT006
00006                                                                   MPKUT006
00007  DATE-WRITTEN.  JANUARY  1989.                                    MPKUT006
00008                                                                   MPKUT006
00009 ******************************************************************MPKUT006
00010 *    THIS PROGRAM EXTRACTS DATA FROM THE BATCH DEPT. DB, THE     *MPKUT006
00011 *    BATCH BUYER DB, AND THE ON-LINE DEPT. DATA BASE.  THE DATA  *MPKUT006
00012 *    WILL BE PASSED ON TO GENERATE THE 'KOHLS DEPARTMENT         *MPKUT006
00013 *    RELATIONSHIP' REPORTS.  THE EXTRACT RECORD WILL BE FORMATED *MPKUT006
00014 *    WITH A SORT KEY FOLLOWED BY THE REPORT DATA.  THE SORT KEY  *MPKUT006
00015 *    WILL CONTAIN A CONSTANT '1' FOR DATA FOR REPORT '1', A      *MPKUT006
00016 *    CONSTANT '2' FOR DATA FOR REPORT '2', AND A CONSTANT '3'    *MPKUT006
00017 *    FOR DATA FOR REPORT '3'.                                    *MPKUT006
00018 *                                                                *MPKUT006
00019 *    THE DATA WILL BE EXTRACTED AS FOLLOWS:                      *MPKUT006
00020 *                                                                *MPKUT006
00021 *    1) MANAGER #               BATCH DEPT. DB.                  *MPKUT006
00022 *    2) MPS DIVISIONAL #        ONLINE DEPT. DB.                 *MPKUT006
00023 *    3) MANAGER NAME            BATCH BUYER DB.                  *MPKUT006
00024 *    4) BUYER #                 BATCH DEPT DB.                   *MPKUT006
00025 *    5) BUYER NAME              BATCH BUYER DB.                  *MPKUT006
00026 *    6) DEPT. #                 BATCH DEPT. DB.                  *MPKUT006
00027 *    7) DEPT. DESCRIPTION       BATCH DEPT. DB.                  *MPKUT006
00028 *    8) DIVISION #              BATCH DEPT. DB.                  *MPKUT006
00029 *                                                                *MPKUT006
00030 *    BASIC LOGIC IS AS FOLLOWS:                                  *MPKUT006
00031 *                                                                *MPKUT006
00032 *    1) START PROGRAM; OPEN FILES.                               *MPKUT006
00033 *                                                                *MPKUT006
00034 *    2) READ THE B. DEPT. DB. USING 'GN' WITH UN-QUAL SSA.       *MPKUT006
00035 *                                                                *MPKUT006
00036 *    3) READ THE B. BUYER DB. USING 'GU' QUAL SSA WITH           *MPKUT006
00037 *       BUYER # FROM B. DEPT. DB.                                *MPKUT006
00038 *                                                                *MPKUT006
00039 *    4) READ THE B. BUYER DB. USING 'GU' QUAL SSA WITH           *MPKUT006
00040 *       MANAGER # FROM B. DEPT. DB.                              *MPKUT006
00041 *                                                                *MPKUT006
00042 *    5) READ THE ON-LINE DEPT. DB. USING 'GU' QUAL SSA WITH      *MPKUT006
00043 *       OPERATING COMPANY = '03' AND DEPT. FROM THE B. DEPT DB.  *MPKUT006
00044 *                                                                *MPKUT006
00045 *    6) FORMAT THE DETAIL EXTRACT RECORD.                        *MPKUT006
00046 *                                                                *MPKUT006
00047 *    7) FORMAT DETAIL EXTRACT RECORD SORT KEY FOR REPORT 1 SEQ.  *MPKUT006
00048 *                                                                *MPKUT006
00049 *    8) WRITE EXTRACT RECORD.                                    *MPKUT006
00050 *                                                                *MPKUT006
00051 *    9) FORMAT DETAIL EXTRACT RECORD SORT KEY FOR REPORT 2 SEQ.  *MPKUT006
00052 *                                                                *MPKUT006
00053 *   10) WRITE EXTRACT RECORD.                                    *MPKUT006
00054 *                                                                *MPKUT006
00055 *   11) FORMAT DETAIL EXTRACT RECORD SORT KEY FOR REPORT 3 SEQ.  *MPKUT006
00056 *                                                                *MPKUT006
00057 *   12) WRITE EXTRACT RECORD.                                    *MPKUT006
00058 *                                                                *MPKUT006
00059 *   11) REPEAT '1' THROUGH '12' ABOVE UNTIL THE B. DEPT. DB.     *MPKUT006
00060 *       HAS A STATUS CODE OF 'GB'                                *MPKUT006
00061 *                                                                *MPKUT006
00062 *   12) CLOSE FILES; END PROGRAM.                                *MPKUT006
00063 *                                                                *MPKUT006
00064 *                                                                *MPKUT006
00065 ******************************************************************MPKUT006
00066                                                                   MPKUT006
00067      EJECT                                                        MPKUT006
00068                                                                   MPKUT006
00069  ENVIRONMENT DIVISION.                                            MPKUT006
00070 *                                                                 MPKUT006
00071  CONFIGURATION SECTION.                                           MPKUT006
00072 *                                                                 MPKUT006
00073  SOURCE-COMPUTER.  IBM-3090.                                      MPKUT006
00074  OBJECT-COMPUTER.  IBM-3090.                                      MPKUT006
00075 *                                                                 MPKUT006
00076  INPUT-OUTPUT SECTION.                                            MPKUT006
00077 *                                                                 MPKUT006
00078  FILE-CONTROL.                                                    MPKUT006
00079      SELECT MASTER-EXTRACT-FILE ASSIGN TO OUT01.                  MPKUT006
00080 *                                                                 MPKUT006
00081  DATA DIVISION.                                                   MPKUT006
00082 *                                                                 MPKUT006
00083  FILE SECTION.                                                    MPKUT006
00084 *                                                                 MPKUT006
00085  FD  MASTER-EXTRACT-FILE                                          MPKUT006
00086      LABEL RECORDS ARE STANDARD                                   MPKUT006
00087      BLOCK CONTAINS 0  RECORDS                                    MPKUT006
00088      RECORDING MODE IS F                                          MPKUT006
00089      RECORD CONTAINS 87  CHARACTERS.                              MPKUT006
00090                                                                   MPKUT006
00091  01  MASTER-EXTRACT-RECORD.                                       MPKUT006
00092      05  MASTER-EXTRACT-KEY              PIC X(14).               MPKUT006
00093      05  MASTER-EXTRACT-DATA             PIC X(73).               MPKUT006
00094 *                                                                 MPKUT006
00095      EJECT                                                        MPKUT006
00096 *                                                                 MPKUT006
00097  WORKING-STORAGE SECTION.                                         MPKUT006
00098 *                                                                 MPKUT006
00099  01  SWITCHES.                                                       CL**9
00100      05  END-OF-DPT-CRS-SW               PIC X(1)  VALUE 'N'.        CL**9
00101          88  END-OF-DPT-CRS           VALUE 'Y'.                     CL**9
00102 *                                                                    CL**9
00103  01  SA-SAVE-AREAS.                                                  CL**9
00104      05  SA-MGR-NAME                     PIC X(20) VALUE SPACES.     CL**9
00105      05  SA-BUYER-NAME                   PIC X(20) VALUE SPACES.     CL**9
00106      05  SA-DIVISION-NBR.                                            CL*19
00107          10  FILLER                      PIC X(1)  VALUE SPACES.     CL*19
00108          10  SA-DIV-NBR                  PIC X(1)  VALUE SPACES.     CL*19
00109      05  SA-BUYER-NBR.                                               CL*15
00110          10  FILLER                      PIC X(1)  VALUE SPACES.     CL*14
00111          10  SA-BYR-NBR                  PIC X(2)  VALUE SPACES.     CL*14
00112 *                                                                    CL**2
00113  01  PV-PROGRAM-VARIABLES.                                           CL**2
00114      05  PV-BUYER-NBR                    PIC X(3)  VALUE SPACES.     CL**2
00115      05  FILLER REDEFINES PV-BUYER-NBR.                              CL**4
00116          10  PV-BUYER-LEADING-0          PIC X(1).                   CL**4
00117          10  PV-BUYER-NUMBER             PIC X(2).                   CL**4
00118      05  PV-DEPT-NBR                     PIC X(3)  VALUE SPACES.     CL**2
00119      05  PV-MANAGER-NBR                  PIC X(2)  VALUE SPACES.     CL**4
00120      05  PV-RETRY-COUNT                  PIC S9(4) VALUE ZERO        CL**3
00121                                              COMP SYNC.              CL**3
00122      05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.     CL**9
00123      05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.     CL**9
00124 *                                                                 MPKUT006
00125  01  PC-PROGRAM-CONSTANTS.                                           CL**3
00126      05  PC-MAXIMUM-RETRY-COUNT          PIC S9(4)    VALUE +3       CL**3
00127                                              COMP SYNC.              CL**3
00128 *                                                                    CL**9
00129  01  PE-PROGRAM-ERROR-MESSAGES.                                      CL**9
00130      05  PE-MSG-01                   PIC  X(30)    VALUE             CL**9
00131          'OPEN DPT_CSR ERROR         '.                              CL**9
00132      05  PE-MSG-02                   PIC  X(30)    VALUE             CL**9
00133          'FETCH DPT_CSR ERROR        '.                              CL**9
00134      05  PE-MSG-03                   PIC  X(30)    VALUE             CL**9
00135          'CLOSE DPT_CSR ERROR        '.                              CL**9
00136      05  PE-MSG-04                   PIC  X(30)    VALUE             CL*10
00137          'SELECT TMDSEAR, TRSPBAR    '.                              CL*10
00138 *                                                                    CL**3
00139  01  DER-DETAIL-EXTRACT-RECORD.                                   MPKUT006
00140      05  DER-SORT-KEY.                                            MPKUT006
00141          10  DER-SRT-RPT-CTL             PIC X     VALUE SPACES.  MPKUT006
00142          10  DER-SRT-DIVISION-NBR        PIC X     VALUE SPACES.  MPKUT006
00143          10  DER-SRT-MPS-DIV-NBR         PIC X(5)  VALUE SPACES.  MPKUT006
00144          10  DER-SRT-MANAGER-NBR         PIC X(2)  VALUE SPACES.  MPKUT006
00145          10  DER-SRT-BUYER-NBR           PIC X(2)  VALUE SPACES.  MPKUT006
00146          10  DER-SRT-DEPT-NBR            PIC X(3)  VALUE SPACES.  MPKUT006
00147      05  DER-SORT-KEY-RED-1 REDEFINES DER-SORT-KEY.               MPKUT006
00148          10  DER-SRT-RPT-CTL-RED-1       PIC X.                   MPKUT006
00149          10  DER-SRT-DIVISION-NBR-RED-1  PIC X.                   MPKUT006
00150          10  DER-SRT-MANAGER-NBR-RED-1   PIC X(2).                MPKUT006
00151          10  DER-SRT-MPS-DIV-NBR-RED-1   PIC X(5).                MPKUT006
00152          10  DER-SRT-BUYER-NBR-RED-1     PIC X(2).                MPKUT006
00153          10  DER-SRT-DEPT-NBR-RED-1      PIC X(3).                MPKUT006
00154      05  DER-SORT-KEY-RED-2 REDEFINES DER-SORT-KEY.               MPKUT006
00155          10  DER-SRT-RPT-CTL-RED-2       PIC X.                   MPKUT006
00156          10  DER-SRT-DEPT-NBR-RED-2      PIC X(3).                MPKUT006
00157          10  DER-SRT-BUYER-NBR-RED-2     PIC X(2).                MPKUT006
00158          10  DER-SRT-MPS-DIV-NBR-RED-2   PIC X(5).                MPKUT006
00159          10  DER-SRT-MANAGER-NBR-RED-2   PIC X(2).                MPKUT006
00160          10  DER-SRT-DIVISION-NBR-RED-2  PIC X.                   MPKUT006
00161      05  DER-DETAIL-EXTRACT-DATA.                                 MPKUT006
00162          10  DER-DET-MGR-NBR             PIC X(2)  VALUE SPACES.  MPKUT006
00163          10  DER-DET-MPS-DIV-NBR         PIC X(5)  VALUE SPACES.  MPKUT006
00164          10  DER-DET-MGR-NAME            PIC X(20) VALUE SPACES.  MPKUT006
00165          10  DER-DET-BUYER-NBR           PIC X(2)  VALUE SPACES.  MPKUT006
00166          10  DER-DET-BUYER-NAME          PIC X(20) VALUE SPACES.  MPKUT006
00167          10  DER-DET-DEPT-NBR            PIC X(3)  VALUE SPACES.  MPKUT006
00168          10  DER-DET-DEPT-DESC           PIC X(20) VALUE SPACES.  MPKUT006
00169          10  DER-DET-DIV-NBR             PIC X     VALUE SPACES.  MPKUT006
00170 *                                                                 MPKUT006
00171      EJECT                                                        MPKUT006
00172 *                                                                 MPKUT006
00173 *                                                                 MPKUT006
00174 **************************************                            MPKUT006
00175 *                                                                 MPKUT006
00176 *                                                                 MPKUT006
00177  01  ABEND-CODE              PIC S9(04)  VALUE ZERO  COMP SYNC.   MPKUT006
00178                                                                   MPKUT006
00179 *                                                                    CL**2
00180 ***************************************************************      CL**2
00181 *  DCLGEN FOR THE DB2 MERCHANDISE AREA TABLE                         CL**2
00182 ***************************************************************      CL**2
00183 *                                                                    CL**2
00184      EXEC SQL                                                        CL**2
00185          INCLUDE TMDSEAR                                             CL**2
00186      END-EXEC.                                                       CL**2
00187                                                                      CL**2
00188 *                                                                    CL**2
00189 ***************************************************************      CL**2
00190 *  DCLGEN FOR THE DB2 RESPONSIBLE AREA TABLE                         CL**2
00191 ***************************************************************      CL**2
00192 *                                                                    CL**2
00193      EXEC SQL                                                        CL**2
00194          INCLUDE TRSPBAR                                             CL**2
00195      END-EXEC.                                                       CL**2
00196                                                                      CL*23
00197 *                                                                    CL*23
00198 ***************************************************************      CL*23
00199 *  DCLGEN FOR THE DB2 APPLICATAION AREA TABLE                        CL*23
00200 ***************************************************************      CL*23
00201 *                                                                    CL*23
00202      EXEC SQL                                                        CL*23
00203          INCLUDE TAPLAVL                                             CL*23
00204      END-EXEC.                                                       CL*23
00205                                                                      CL**2
00206 *                                                                    CL**5
00207 ***************************************************************      CL**5
00208 *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                   CL**5
00209 ***************************************************************      CL**5
00210 *                                                                    CL**5
00211      EXEC SQL                                                        CL**5
00212          INCLUDE SQLCA                                               CL**5
00213      END-EXEC.                                                       CL**5
00214 *                                                                    CL**5
00215 ***************************************************************      CL**5
00216 *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING              CL**5
00217 *  SQL CODES FOR DISPLAY                                             CL**5
00218 ***************************************************************      CL**5
00219 *                                                                    CL**5
00220      COPY SQLCA2.                                                    CL**5
00221                                                                      CL**9
00222      COPY DPWS004.                                                   CL*12
00223                                                                      CL*12
00224 *                                                                    CL**9
00225 ***************************************************************      CL**9
00226 *  DECLARE CURSOR TO SELECT ALL DEPTS FROM THE TMDSEAR AND           CL**9
00227 *  TMDSPBAR TABLES.                                                  CL**9
00228 ***************************************************************      CL**9
00229 *                                                                    CL**9
00230      EXEC SQL                                                        CL**9
00231          DECLARE DPT_CSR CURSOR FOR                                  CL*11
00232              SELECT  GMA_NBR                                         CL*19
00233                   ,  DMA_NBR                                         CL*19
00234                   ,  DEPT_NBR                                        CL*13
00235                   ,  MDSEAR_DESC                                     CL**9
00236                   ,  BYR_NBR                                         CL*10
00237                   ,  WORKER_FIR_NAME                                 CL**9
00238                   ,  WORKER_LAST_NAME                                CL**9
00239              FROM  TMDSEAR, TRSPBAR, TAPLAVL                         CL*22
00240              WHERE (MDSELV_CDE       = 'DPT'                         CL*10
00241                AND  CURRENT DATE BETWEEN STRT_DTE AND END_DTE        CL**9
00242                AND  RSPLVL_CDE       = 'BYR'                         CL**9
00243                AND  CURRENT DATE BETWEEN WORKER_STRT_DTE AND         CL**9
00244                                          WORKER_END_DTE              CL**9
00245                AND  APLSYS_CDE       = 'ML'                          CL*24
00246                AND  CURRENT DATE BETWEEN MDSEAR_STRT_DTE AND         CL*22
00247                                          MDSEAR_END_DTE              CL*22
00248                AND  TMDSEAR.OPERCO_NBR  = '03'                       CL**9
00249                AND  TMDSEAR.OPERCO_NBR  = TRSPBAR.OPERCO_NBR         CL**9
00250                AND  TMDSEAR.OPERCO_NBR  = TAPLAVL.OPERCO_NBR         CL*25
00251                AND  TMDSEAR.MDSEAR_DKEY = TRSPBAR.MDSEAR_DKEY        CL*22
00252                AND  TMDSEAR.MDSEAR_DKEY = TAPLAVL.MDSEAR_DKEY)       CL*25
00253      END-EXEC.                                                       CL**9
00254 *                                                                    CL**9
00255 ***************************************************************   MPKUT006
00256 *                                                                 MPKUT006
00257                                                                   MPKUT006
00258      EJECT                                                        MPKUT006
00259  PROCEDURE DIVISION.                                              MPKUT006
00260                                                                   MPKUT006
00261 ******************************************************************MPKUT006
00262 * 0000-MAINLINE HIGHEST LEVEL CONTROL                                CL**9
00263 ******************************************************************MPKUT006
00264                                                                   MPKUT006
00265  0000-MAINLINE.                                                      CL**9
00266                                                                   MPKUT006
00267      PERFORM 1000-INITIAL-PROCESS.                                   CL**9
00268                                                                   MPKUT006
00269      PERFORM 3000-DATA-EXTRACTION-RTN                                CL**9
00270              UNTIL END-OF-DPT-CRS.                                   CL**9
00271                                                                   MPKUT006
00272      PERFORM 9000-EOJ.                                               CL*10
00273                                                                   MPKUT006
00274      GOBACK.                                                      MPKUT006
00275                                                                   MPKUT006
00276 ******************************************************************   CL**9
00277 * 1000-INITIAL-PROCESS.                                              CL**9
00278 *     DO FIRST TIME PROGRAM INITIALIZATION TASKS.                    CL**9
00279 *        -- OPEN OUTPUT FILE                                         CL**9
00280 *        -- OPEN THE DPT_CRS AND FETCH THE FIRST ROW                 CL**9
00281 ******************************************************************   CL**9
00282                                                                      CL**9
00283  1000-INITIAL-PROCESS.                                               CL**9
00284                                                                      CL**9
00285      OPEN OUTPUT MASTER-EXTRACT-FILE.                                CL**9
00286                                                                   MPKUT006
00287 *                                                                    CL**9
00288      PERFORM 7100-OPEN-DPT-CSR.                                      CL**9
00289      PERFORM 7110-FETCH-DPT-CSR.                                     CL**9
00290                                                                      CL**9
00291 *                                                                 MPKUT006
00292 ******************************************************************MPKUT006
00293 * 3000-DATA-EXTRACTION-RTN.                                          CL*10
00294 *   THIS FETCHS ALL ROWS AND WRITES THE EXTRACTED DATA.              CL**9
00295 ******************************************************************MPKUT006
00296 *                                                                 MPKUT006
00297  3000-DATA-EXTRACTION-RTN.                                           CL**9
00298                                                                   MPKUT006
00299      MOVE SPACES TO SA-BUYER-NAME.                                   CL*16
00300      STRING RSPBAR-WORKER-FIR-NAME DELIMITED BY ' '                  CL**9
00301                   ' ' DELIMITED BY SIZE                              CL**9
00302             RSPBAR-WORKER-LAST-NAME DELIMITED BY ' '                 CL**9
00303       INTO SA-BUYER-NAME.                                            CL**9
00304                                                                      CL**9
00305      MOVE MDSEAR-GMA-NBR TO SA-DIVISION-NBR.                         CL*19
00306      MOVE MDSEAR-BYR-NBR TO SA-BUYER-NBR.                            CL*16
00307 *                                                                    CL**9
00308      PERFORM 4000-GET-MANAGER-NAME.                                  CL**9
00309 *                                                                    CL**9
00310      PERFORM 5000-SETUP-DET-EXT-RECORD.                              CL**9
00311      PERFORM 5100-SETUP-EXT-RPT-KEY-1.                               CL**9
00312      PERFORM 8000-WRITE-EXT-RECORD.                                  CL**9
00313 *                                                                    CL**9
00314      PERFORM 5200-SETUP-EXT-RPT-KEY-2.                               CL**9
00315      PERFORM 8000-WRITE-EXT-RECORD.                                  CL**9
00316 *                                                                    CL**9
00317      PERFORM 5300-SETUP-EXT-RPT-KEY-3.                               CL**9
00318      PERFORM 8000-WRITE-EXT-RECORD.                                  CL**9
00319                                                                   MPKUT006
00320      PERFORM 7110-FETCH-DPT-CSR.                                     CL**9
00321                                                                   MPKUT006
00322                                                                   MPKUT006
00323 *                                                                    CL**2
00324 * ****************************************************************   CL**2
00325 * 4000-GET-MANAGER-NAME.                                             CL**9
00326 ******************************************************************MPKUT006
00327 *                                                                 MPKUT006
00328                                                                   MPKUT006
00329  4000-GET-MANAGER-NAME.                                              CL**9
00330                                                                   MPKUT006
00331      MOVE MDSEAR-DMA-NBR TO PV-MANAGER-NBR.                          CL*12
00332                                                                   MPKUT006
00333      PERFORM                                                         CL**4
00334        WITH TEST AFTER                                               CL**4
00335        VARYING PV-RETRY-COUNT                                        CL**4
00336        FROM 1 BY 1                                                   CL**4
00337        UNTIL (SQLCODE = ZERO   OR                                    CL**4
00338               SQLCODE = +100   OR                                    CL**4
00339              PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT)                CL**4
00340                                                                      CL**4
00341          EXEC SQL                                                    CL**4
00342              SELECT  WORKER_FIR_NAME                                 CL**4
00343                   ,  WORKER_LAST_NAME                                CL**4
00344              INTO   :RSPBAR-WORKER-FIR-NAME                          CL**4
00345                   , :RSPBAR-WORKER-LAST-NAME                         CL**4
00346              FROM  TMDSEAR, TRSPBAR                                  CL**4
00347              WHERE (DMA_NBR          = :PV-MANAGER-NBR               CL*10
00348                AND  MDSELV_CDE       = 'DMA'                         CL**4
00349                AND  CURRENT DATE BETWEEN STRT_DTE AND END_DTE        CL**4
00350                AND  RSPLVL_CDE       = 'DMM'                         CL**4
00351                AND  CURRENT DATE BETWEEN WORKER_STRT_DTE AND         CL**4
00352                                          WORKER_END_DTE              CL**4
00353                AND  TMDSEAR.OPERCO_NBR  = '03'                       CL*10
00354                AND  TMDSEAR.OPERCO_NBR  = TRSPBAR.OPERCO_NBR         CL*10
00355                AND  TMDSEAR.MDSEAR_DKEY = TRSPBAR.MDSEAR_DKEY)       CL*10
00356          END-EXEC                                                    CL**4
00357                                                                      CL**4
00358          EVALUATE TRUE                                               CL**4
00359              WHEN SQLCODE = -904                                     CL**4
00360              WHEN SQLCODE = -911                                     CL**4
00361                   CONTINUE                                           CL**4
00362              WHEN SQLCODE = +100                                     CL**4
00363                   MOVE '   NO MANAGER FOUND ' TO SA-MGR-NAME         CL**4
00364              WHEN SQLWARN0 NOT = SPACES                              CL**4
00365              WHEN SQLCODE < 0                                        CL**4
00366              WHEN SQLCODE > 0                                        CL**4
00367                   MOVE '4000-GET-MANAGER-NAME'                       CL*10
00368                                      TO PV-ABEND-PARAGRAPH           CL*10
00369                   MOVE PE-MSG-04     TO PV-DB2-OPERATION-ATTEMPTED   CL*10
00370                   DISPLAY 'MANAGER NUMBER: ' PV-MANAGER-NBR          CL**4
00371                   PERFORM 9900-SQL-ERROR                             CL**9
00372              WHEN SQLCODE = 0                                        CL**4
00373                   MOVE SPACES TO SA-MGR-NAME                         CL*16
00374                   STRING RSPBAR-WORKER-FIR-NAME DELIMITED BY ' ',    CL**4
00375                         ' ' DELIMITED BY SIZE,                       CL**4
00376                          RSPBAR-WORKER-LAST-NAME DELIMITED BY ' ',   CL**4
00377                   INTO SA-MGR-NAME                                   CL**4
00378          END-EVALUATE                                                CL**4
00379      END-PERFORM.                                                    CL**4
00380                                                                      CL**4
00381                                                                      CL**4
00382      IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      CL**4
00383        MOVE '4000-GET-MANAGER-NAME'                                  CL*10
00384                           TO PV-ABEND-PARAGRAPH                      CL*10
00385        MOVE PE-MSG-04     TO PV-DB2-OPERATION-ATTEMPTED              CL*10
00386        DISPLAY 'MANAGER NUMBER: ', PV-MANAGER-NBR                    CL**4
00387        PERFORM 9900-SQL-ERROR                                        CL**9
00388      END-IF.                                                         CL**7
00389                                                                      CL**4
00390                                                                   MPKUT006
00391 *                                                                 MPKUT006
00392 ******************************************************************MPKUT006
00393 * 5000-SETUP-DET-EXT-RECORD                                          CL**9
00394 ******************************************************************MPKUT006
00395 *                                                                 MPKUT006
00396                                                                   MPKUT006
00397  5000-SETUP-DET-EXT-RECORD.                                          CL**9
00398                                                                   MPKUT006
00399      INITIALIZE DER-DETAIL-EXTRACT-RECORD.                           CL*20
00400      MOVE MDSEAR-DMA-NBR  TO DER-DET-MGR-NBR.                        CL*10
00401      MOVE SA-MGR-NAME     TO DER-DET-MGR-NAME.                       CL*10
00402      MOVE SA-BYR-NBR      TO DER-DET-BUYER-NBR.                      CL*14
00403      MOVE SA-BUYER-NAME   TO DER-DET-BUYER-NAME.                     CL*10
00404      MOVE MDSEAR-DEPT-NBR TO DER-DET-DEPT-NBR.                       CL*10
00405      MOVE MDSEAR-DESC     TO DER-DET-DEPT-DESC.                      CL*10
00406      MOVE SA-DIV-NBR      TO DER-DET-DIV-NBR.                        CL*21
00407      MOVE SPACES          TO DER-DET-MPS-DIV-NBR.                    CL*27
00408                                                                   MPKUT006
00409                                                                   MPKUT006
00410 *                                                                 MPKUT006
00411 ******************************************************************MPKUT006
00412 * 5100-SETUP-EXT-RPT-KEY-1 (REPORT 1 MPS SEQUENCE)                   CL**9
00413 ******************************************************************MPKUT006
00414 *                                                                 MPKUT006
00415                                                                   MPKUT006
00416  5100-SETUP-EXT-RPT-KEY-1.                                           CL**9
00417                                                                   MPKUT006
00418      MOVE '1' TO DER-SRT-RPT-CTL.                                 MPKUT006
00419                                                                   MPKUT006
00420      MOVE SA-DIV-NBR      TO DER-SRT-DIVISION-NBR.                   CL*20
00421      MOVE SPACES          TO DER-SRT-MPS-DIV-NBR.                    CL*26
00422      MOVE MDSEAR-DMA-NBR  TO DER-SRT-MANAGER-NBR.                    CL*10
00423      MOVE SA-BYR-NBR      TO DER-SRT-BUYER-NBR.                      CL*14
00424      MOVE MDSEAR-DEPT-NBR TO DER-SRT-DEPT-NBR.                       CL*12
00425                                                                   MPKUT006
00426 *                                                                 MPKUT006
00427 ******************************************************************MPKUT006
00428 * 5200-SETUP-EXT-RPT-KEY-2  (REPORT 2 MANAGER SEQUENCE)              CL**9
00429 ******************************************************************MPKUT006
00430 *                                                                 MPKUT006
00431                                                                   MPKUT006
00432  5200-SETUP-EXT-RPT-KEY-2.                                           CL**9
00433                                                                   MPKUT006
00434      MOVE '2' TO DER-SRT-RPT-CTL-RED-1.                           MPKUT006
00435                                                                   MPKUT006
00436      MOVE SA-DIV-NBR      TO DER-SRT-DIVISION-NBR-RED-1.             CL*20
00437      MOVE SPACES          TO DER-SRT-MPS-DIV-NBR-RED-1.              CL*26
00438      MOVE MDSEAR-DMA-NBR  TO DER-SRT-MANAGER-NBR-RED-1               CL*10
00439      MOVE SA-BYR-NBR      TO DER-SRT-BUYER-NBR-RED-1.                CL*14
00440      MOVE MDSEAR-DEPT-NBR TO DER-SRT-DEPT-NBR-RED-1.                 CL*12
00441                                                                   MPKUT006
00442 *                                                                 MPKUT006
00443 ******************************************************************MPKUT006
00444 * 5300-SETUP-EXT-RPT-KEY-3  (REPORT 3 DEPARTMENT SEQ)                CL**9
00445 ******************************************************************MPKUT006
00446 *                                                                 MPKUT006
00447                                                                   MPKUT006
00448  5300-SETUP-EXT-RPT-KEY-3.                                           CL**9
00449                                                                   MPKUT006
00450      MOVE '3' TO DER-SRT-RPT-CTL-RED-2.                           MPKUT006
00451                                                                   MPKUT006
00452      MOVE MDSEAR-DEPT-NBR TO DER-SRT-DEPT-NBR-RED-2.                 CL*12
00453      MOVE SA-BYR-NBR      TO DER-SRT-BUYER-NBR-RED-2.                CL*14
00454      MOVE MDSEAR-DMA-NBR  TO DER-SRT-MANAGER-NBR-RED-2.              CL*10
00455      MOVE SA-DIV-NBR      TO DER-SRT-DIVISION-NBR-RED-2.             CL*20
00456      MOVE SPACES          TO DER-SRT-MPS-DIV-NBR-RED-2.              CL*26
00457                                                                   MPKUT006
00458                                                                   MPKUT006
00459 ******************************************************************   CL**9
00460 *  OPEN THE DPT_CSR                                                  CL**9
00461 ******************************************************************   CL**9
00462 *                                                                    CL**9
00463  7100-OPEN-DPT-CSR.                                                  CL**9
00464      EXEC SQL                                                        CL**9
00465           OPEN DPT_CSR                                               CL**9
00466      END-EXEC.                                                       CL**9
00467 *                                                                    CL**9
00468      EVALUATE TRUE                                                   CL**9
00469          WHEN SQLCODE = ZERO                                         CL**9
00470               CONTINUE                                               CL**9
00471          WHEN OTHER                                                  CL**9
00472               MOVE '7110-OPEN-DPT-CRS'                               CL**9
00473                                  TO PV-ABEND-PARAGRAPH               CL**9
00474               MOVE PE-MSG-01     TO PV-DB2-OPERATION-ATTEMPTED       CL**9
00475               PERFORM 9900-SQL-ERROR                                 CL**9
00476      END-EVALUATE.                                                   CL**9
00477                                                                      CL**9
00478 ******************************************************************   CL**9
00479 *  FETCH THE NEXT ROW IN THE RESULTS TABLE USING THE DPT_CSR         CL**9
00480 ******************************************************************   CL**9
00481 *                                                                    CL**9
00482  7110-FETCH-DPT-CSR.                                                 CL**9
00483      PERFORM                                                         CL**9
00484        WITH TEST AFTER                                               CL**9
00485        VARYING PV-RETRY-COUNT                                        CL**9
00486        FROM 1 BY 1                                                   CL**9
00487        UNTIL (SQLCODE = ZERO   OR                                    CL**9
00488               SQLCODE = +100   OR                                    CL**9
00489              PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT)                CL**9
00490                                                                      CL**9
00491          EXEC SQL                                                    CL**9
00492               FETCH DPT_CSR                                          CL**9
00493                INTO :MDSEAR-GMA-NBR                                  CL*19
00494                   , :MDSEAR-DMA-NBR                                  CL*19
00495                   , :MDSEAR-DEPT-NBR                                 CL*10
00496                   , :MDSEAR-DESC                                     CL**9
00497                   , :MDSEAR-BYR-NBR                                  CL*10
00498                   , :RSPBAR-WORKER-FIR-NAME                          CL**9
00499                   , :RSPBAR-WORKER-LAST-NAME                         CL**9
00500          END-EXEC                                                    CL**9
00501 *                                                                    CL**9
00502          EVALUATE TRUE                                               CL**9
00503              WHEN SQLCODE = -904                                     CL**9
00504              WHEN SQLCODE = -911                                     CL**9
00505              WHEN SQLCODE = ZERO                                     CL**9
00506                   CONTINUE                                           CL**9
00507              WHEN SQLCODE = +100                                     CL**9
00508                   SET END-OF-DPT-CRS TO TRUE                         CL**9
00509              WHEN SQLWARN0 NOT = SPACES                              CL**9
00510              WHEN SQLCODE < 0                                        CL**9
00511              WHEN SQLCODE > 0                                        CL**9
00512                   MOVE '7110-FETCH-DPT-CSR'                          CL**9
00513                                      TO PV-ABEND-PARAGRAPH           CL**9
00514                   MOVE PE-MSG-02     TO PV-DB2-OPERATION-ATTEMPTED   CL**9
00515                   PERFORM 9900-SQL-ERROR                             CL*10
00516          END-EVALUATE                                                CL**9
00517      END-PERFORM.                                                    CL**9
00518                                                                      CL**9
00519      IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      CL**9
00520        MOVE '7110-FETCH-DPT-CSR'                                     CL**9
00521                           TO PV-ABEND-PARAGRAPH                      CL**9
00522        MOVE PE-MSG-02     TO PV-DB2-OPERATION-ATTEMPTED              CL**9
00523        PERFORM 9900-SQL-ERROR                                        CL*10
00524      END-IF.                                                         CL**9
00525                                                                      CL**9
00526 *                                                                    CL**9
00527 ******************************************************************   CL**9
00528 *  CLOSE THE DPT_CSR                                                 CL**9
00529 ******************************************************************   CL**9
00530  7120-CLOSE-DPT-CSR.                                                 CL**9
00531      EXEC SQL                                                        CL**9
00532           CLOSE DPT_CSR                                              CL**9
00533      END-EXEC.                                                       CL**9
00534 *                                                                    CL**9
00535      IF  SQLCODE = ZERO                                              CL**9
00536          CONTINUE                                                    CL**9
00537      ELSE                                                            CL**9
00538          MOVE '7120-CLOSE-DPT-CSR'                                   CL**9
00539                                  TO PV-ABEND-PARAGRAPH               CL**9
00540          MOVE PE-MSG-03          TO PV-DB2-OPERATION-ATTEMPTED       CL**9
00541          PERFORM 9900-SQL-ERROR                                      CL*10
00542      END-IF.                                                         CL**9
00543 *                                                                    CL**9
00544 *                                                                 MPKUT006
00545 ******************************************************************MPKUT006
00546 * 8000-WRITE-EXT-RECORD                                          *   CL**9
00547 ******************************************************************MPKUT006
00548 *                                                                 MPKUT006
00549                                                                   MPKUT006
00550  8000-WRITE-EXT-RECORD.                                              CL**9
00551      WRITE MASTER-EXTRACT-RECORD FROM DER-DETAIL-EXTRACT-RECORD.  MPKUT006
00552                                                                   MPKUT006
00553 *                                                                    CL**9
00554 ******************************************************************   CL**9
00555 * 9000-EOJ.                                                      *   CL**9
00556 ******************************************************************   CL**9
00557 *                                                                    CL**9
00558  9000-EOJ.                                                           CL*12
00559      CLOSE MASTER-EXTRACT-FILE.                                      CL**9
00560      PERFORM 7120-CLOSE-DPT-CSR.                                     CL**9
00561                                                                      CL**9
00562 ******************************************************************   CL**3
00563 *  FORMAT A DB2 ERROR MESSAGE, AND ABEND THE PROGRAM.            *   CL**9
00564 ******************************************************************   CL**3
00565  9900-SQL-ERROR.                                                     CL**9
00566      DISPLAY '** ABEND **'.                                          CL**3
00567      DISPLAY '** MPKUT006 - SQL ERROR'.                              CL**9
00568      DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                CL**9
00569      DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL**9
00570 *                                                                    CL**9
00571 ******************************************************************   CL**9
00572 *  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-      CL**9
00573 *  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE           CL**9
00574 *  'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE       CL**9
00575 *  FORMAT.                                                           CL**9
00576 ******************************************************************   CL**9
00577      COPY DPPD004.                                                   CL**9
00578 *                                                                    CL**9
00579      EXEC SQL                                                        CL**9
00580           COMMIT                                                     CL**9
00581      END-EXEC.                                                       CL**9
00582 *                                                                    CL**9
00583      MOVE +4013                  TO ABEND-CODE.                      CL**9
00584 *                                                                    CL**9
00585      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**9
00586                                                                      CL**3
00587 *                                                                 MPKUT006
00588 ******************************************************************MPKUT006
