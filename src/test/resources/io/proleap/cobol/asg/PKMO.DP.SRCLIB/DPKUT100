00001 ******************************************************************01/27/93
00002  IDENTIFICATION DIVISION.                                         DPKUT100
00003 ******************************************************************   LV001
00004                                                                   DPKUT100
00005  PROGRAM-ID.             DPKUT100.                                   CL*17
00006  AUTHOR.                 RICK TULLOCH.                               CL*17
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 DPKUT100
00008  DATE-WRITTEN.           10-19-87.                                DPKUT100
00009  DATE-COMPILED.                                                   DPKUT100
00010                                                                   DPKUT100
00011 ******************************************************************DPKUT100
00012 *     THIS SUBROUTINE WILL EDIT A FREE-FORM CHARACTER FIELD TO   *   CL*17
00013 *  IDENTIFY A SINGLE NUMERIC FIELD CONTAINED THEREIN, AND        *   CL*17
00014 *  CREATE A SIGNED NUMERIC REPRESENTATION OF THAT FIELD.  THE    *   CL*17
00015 *  CHARACTER FIELD CAN CONTAIN EDIT CHARACTERS (A LEADING OR     *   CL*17
00016 *  TRAILING SIGN, A LEADING DOLLAR SIGN, A DECIMAL POINT, OR     *   CL*17
00017 *  COMMAS), BUT THEY MUST FOLLOW THE NORMAL CONVENTIONS FOR THE  *   CL*17
00018 *  USE OF THOSE CHARACTERS IN ORDER FOR THE NUMERIC FIELD TO     *   CL*17
00019 *  BE CONSIDERED VALID.  NUMERIC FIELD CONSTRAINTS ARE ALSO      *   CL*17
00020 *  SET BEFORE CALLING THIS ROUTINE TO DETERMINE THE BEST         *   CL*17
00021 *  NUMERIC FIELD REPRESENTATION TO STORE THE NUMERIC RESULT, AND *   CL*17
00022 *  TO ALLOW THE SUBROUTINE TO CARRY OUT ADDITIONAL EDITS ON      *   CL*17
00023 *  BEHALF OF THE CALLING PROGRAM.                                *   CL*17
00024 *                                                                *   CL*17
00025 *       THE PARAMETER LIST TO USE WHEN CALLING THIS SUBROUTINE   *   CL*17
00026 *  IS DEFINED AS FOLLOWS:                                        *   CL*17
00027 *                                                                *   CL*16
00028 *       FIELDS TO BE SENT TO THE SUBROUTINE:                     *   CL*17
00029 *                                                                *DPKUT100
00030 *            THE CHARACTER FIELD TO BE EDITED       X(21)        *   CL*17
00031 *            NUMERIC FIELD CONSTRAINTS:                          *   CL*17
00032 *                THE MAXIMUM NUMBER OF DIGIT        S9(4) COMP   *   CL*17
00033 *                     POSITIONS (LEFT OF THE                     *   CL*17
00034 *                     DECIMAL POINT) ALLOWED                     *   CL*17
00035 *                     IN THE NUMERIC FIELD                       *   CL*17
00036 *                THE MAXIMUM NUMBER OF DECIMAL      S9(4) COMP   *   CL*17
00037 *                     POSITIONS (RIGHT OF THE                    *   CL*17
00038 *                     DECIMAL POINT) ALLOWED                     *   CL*17
00039 *                     IN THE NUMERIC FIELD                       *   CL*17
00040 *                AN INDICATION OF WHETHER OR NOT    X(1)         *   CL*17
00041 *                     NEGATIVE VALUES ARE ALLOWED                *   CL*17
00042 *                     IN THE NUMERIC FIELD                       *   CL*17
00043 *                                                                *DPKUT100
00044 *       RESPONSES FROM THE SUBROUTINE:                           *   CL*17
00045 *                                                                *DPKUT100
00046 *            AN INDICATION OF THE SUCCESS OR        X(1)         *   CL*17
00047 *                 FAILURE OF THE CALL, AND, IN THE               *   CL*17
00048 *                 CASE OF AN ERROR, THE SPECIFIC                 *   CL*17
00049 *                 TYPE OF ERROR DETECTED                         *   CL*17
00050 *            AN INDICATION OF THE NUMERIC FIELD     X(1)         *   CL*17
00051 *                 REPRESENTATION THAT WAS USED TO                *   CL*17
00052 *                 STORE THE NUMERIC REPRESENTATION               *   CL*17
00053 *                 OF THE FIELD                                   *   CL*17
00054 *            THE NUMERIC REPRESENTATION OF THE      S9(11)V9(6)  *   CL*17
00055 *                 FIELD, IN ANY OF FOUR                   COMP-3 *   CL**2
00056 *                 REPRESENTATIONS, DEPENDING ON     S9(13)V9(4)  *   CL*17
00057 *                 INPUT CONSTRAINTS PASSED (THE           COMP-3 *   CL**2
00058 *                 VARIOUS REPRESENTATIONS ARE       S9(15)V9(2)  *   CL*17
00059 *                 REDEFINED ON TOP OF EACH OTHER)         COMP-3 *   CL**2
00060 *                                                   S9(17)       *   CL*17
00061 *                                                         COMP-3 *   CL**2
00062 *            THE NUMBER OF DIGITS FOUND IN THE      S9(4) COMP   *   CL*17
00063 *                 NUMERIC FIELD                                  *   CL*17
00064 *            THE NUMBER OF DECIMALS FOUND IN THE    S9(4) COMP   *   CL*17
00065 *                 NUMERIC FIELD                                  *   CL*17
00066 *            AN INDICATION OF WHETHER OR NOT A      X(1)         *   CL*17
00067 *                 DECIMAL POINT WAS DETECTED                     *   CL*17
00068 *            AN INDICATION OF WHETHER OR NOT A      X(1)         *   CL*17
00069 *                 SIGN WAS DETECTED, AND WHAT SIGN               *   CL*17
00070 *                 WAS DETECTED                                   *   CL*17
00071 *            AN INDICATION OF THE LOCATION OF A     X(1)         *   CL*17
00072 *                 DETECTED SIGN (WHETHER IT WAS A                *   CL*17
00073 *                 LEADING SIGN, OR A TRAILING SIGN)              *   CL*17
00074 *                                                                *DPKUT100
00075 *  IF THERE IS AN ERROR IN ANY OF THE PARAMETERS SENT TO THIS    *   CL*17
00076 *  SUBROUTINE, OR IF THE FIELD PROVES TO BE NON-NUMERIC, OR      *   CL*17
00077 *  THE FIELD INCORRECTLY USES NUMERIC EDIT CHARACTERS, ZERO WILL *   CL*17
00078 *  BE RETURNED AS THE NUMERIC REPRESENTATION OF THE FIELD.  IF   *   CL*17
00079 *  AN ERROR IS DETECTED WHEN EVALUATING THE NUMERIC FIELD        *   CL*17
00080 *  AGAINST THE PASSED CONSTRAINTS, THE NUMERIC FIELD WILL BE     *   CL*17
00081 *  PASSED BACK, BUT IT WILL BE TRUNCATED, IF NECESSARY, TO FIT   *   CL**3
00082 *  THE DESIGNATED RETURN REPRESENTATION.                         *   CL**3
00083 ******************************************************************DPKUT100
00084                                                                   DPKUT100
00085 ******************************************************************DPKUT100
00086  ENVIRONMENT DIVISION.                                            DPKUT100
00087 ******************************************************************DPKUT100
00088                                                                   DPKUT100
00089  CONFIGURATION SECTION.                                           DPKUT100
00090                                                                   DPKUT100
00091  SOURCE-COMPUTER.        IBM-4381.                                DPKUT100
00092  OBJECT-COMPUTER.        IBM-4381.                                DPKUT100
00093                                                                   DPKUT100
00094  INPUT-OUTPUT SECTION.                                            DPKUT100
00095                                                                   DPKUT100
00096  FILE-CONTROL.                                                    DPKUT100
00097                                                                   DPKUT100
00098 ******************************************************************DPKUT100
00099  DATA DIVISION.                                                   DPKUT100
00100 ******************************************************************DPKUT100
00101                                                                   DPKUT100
00102  WORKING-STORAGE SECTION.                                         DPKUT100
00103                                                                      CL*12
00104 *                                                                    CL*12
00105 *  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)    CL*12
00106 *                                                                    CL*12
00107                                                                      CL*12
00108 *    COPY DPWS010I.                                                  CL*14
00109      COPY DPWS010I.                                                  CL**4
00110                                                                   DPKUT100
00111  01  PS-PROGRAM-SWITCHES.                                         DPKUT100
00112      05  PS-BLANK-OR-UNENTERD-FIELD-IND                           DPKUT100
00113                                  PIC X(1)     VALUE 'N'.          DPKUT100
00114          88  PS-NON-BLANK-FIELD               VALUE 'N'.          DPKUT100
00115          88  PS-BLANK-OR-UNENTERED-FIELD      VALUE 'Y'.          DPKUT100
00116                                                                   DPKUT100
00117  01  PC-PROGRAM-CONSTANTS.                                        DPKUT100
00118      05  PC-REQUIRES-11-DIGITS   PIC S9(4)    VALUE +11              CL**2
00119                                               COMP SYNC.             CL**2
00120      05  PC-MAXIMUM-DECIMAL-W-11-DIGITS                              CL**2
00121                                  PIC S9(4)    VALUE +6               CL**2
00122                                               COMP SYNC.             CL**2
00123      05  PC-REQUIRES-13-DIGITS   PIC S9(4)    VALUE +13              CL**2
00124                                               COMP SYNC.             CL**2
00125      05  PC-MAXIMUM-DECIMAL-W-13-DIGITS                              CL**2
00126                                  PIC S9(4)    VALUE +4               CL**2
00127                                               COMP SYNC.             CL**2
00128      05  PC-REQUIRES-15-DIGITS   PIC S9(4)    VALUE +15              CL**2
00129                                               COMP SYNC.             CL**2
00130      05  PC-MAXIMUM-DECIMAL-W-15-DIGITS                              CL**2
00131                                  PIC S9(4)    VALUE +2               CL**2
00132                                               COMP SYNC.             CL**2
00133      05  PC-REQUIRES-17-DIGITS   PIC S9(4)    VALUE +17              CL**2
00134                                               COMP SYNC.             CL**2
00135      05  PC-MAXIMUM-DECIMAL-W-17-DIGITS                              CL**2
00136                                  PIC S9(4)    VALUE +0               CL**2
00137                                               COMP SYNC.             CL**2
00138      05  PC-MAXIMUM-UNEDITED-CHARACTERS                           DPKUT100
00139                                  PIC S9(4)    VALUE +22           DPKUT100
00140                                               COMP SYNC.          DPKUT100
00141      05  PC-MAXIMUM-NUMERIC-CHARACTERS                            DPKUT100
00142                                  PIC S9(4)    VALUE +23           DPKUT100
00143                                               COMP SYNC.          DPKUT100
00144      05  PC-DIGITS-AFTER-COMMA   PIC S9(4)    VALUE +3            DPKUT100
00145                                               COMP SYNC.          DPKUT100
00146      05  PC-FIRST-NUMERIC-FIELD-DECIMAL                           DPKUT100
00147                                  PIC S9(4)    VALUE +18           DPKUT100
00148                                               COMP SYNC.          DPKUT100
00149                                                                   DPKUT100
00150  01  PV-PROGRAM-VARIABLES.                                           CL**2
00151      05  PV-CURRENT-INDEX-LOCATION                                   CL**4
00152                                  PIC S9(4)    VALUE ZEROS            CL**4
00153                                               COMP SYNC.             CL**4
00154                                                                      CL**2
00155  01  UF-UNEDITED-FIELD-AREA.                                      DPKUT100
00156      05  UF-UNEDITED-FIELD       PIC X(22)    VALUE SPACES.          CL**3
00157      05  FILLER                  REDEFINES UF-UNEDITED-FIELD.        CL**4
00158          10  UF-UNEDITED-CHARACTER                                DPKUT100
00159                                  OCCURS 22 TIMES                  DPKUT100
00160                                  INDEXED BY UF-CURRENT-INDEX      DPKUT100
00161                                             UF-STARTING-LOCATION     CL**3
00162                                             UF-DECIMAL-LOCATION.  DPKUT100
00163              15  UF-UNEDITED-CHARACTER-AREA.                      DPKUT100
00164                  88  UF-FIELD-DELIMITER       VALUE SPACE,        DPKUT100
00165                                                     LOW-VALUES.   DPKUT100
00166                  88  UF-DOLLAR-SIGN           VALUE '$'.          DPKUT100
00167                  88  UF-SIGN                  VALUE '+', '-'.     DPKUT100
00168                  88  UF-DECIMAL-POINT         VALUE '.'.          DPKUT100
00169                  88  UF-COMMA                 VALUE ','.          DPKUT100
00170                  20  UF-NUMERIC-DIGIT                             DPKUT100
00171                                  PIC 9(1).                        DPKUT100
00172                                                                   DPKUT100
00173  01  NF-NUMERIC-FIELD-AREA.                                          CL*12
00174      05  NF-NUMERIC-FIELD-WORK   PIC X(23)    VALUE SPACES.          CL*12
00175      05  NF-NUMERIC-DIGIT-REDEF  REDEFINES NF-NUMERIC-FIELD-WORK.    CL*12
00176          10  NF-NUMERIC-DIGIT    OCCURS 23 TIMES                     CL*12
00177                                  INDEXED BY NF-INDEX                 CL*12
00178                                  PIC 9(1).                           CL*12
00179      05  FILLER                  REDEFINES NF-NUMERIC-FIELD-WORK.    CL*12
00180          10  FILLER              PIC X(6).                           CL*12
00181          10  NF-NUMERIC-FIELD-11-DIG-6-DEC                           CL*12
00182                                  PIC S9(11)V9(6).                    CL*12
00183      05  FILLER                  REDEFINES NF-NUMERIC-FIELD-WORK.    CL*12
00184          10  FILLER              PIC X(4).                           CL*12
00185          10  NF-NUMERIC-FIELD-13-DIG-4-DEC                           CL*12
00186                                  PIC S9(13)V9(4).                    CL*12
00187          10  FILLER              PIC X(2).                           CL*12
00188      05  FILLER                  REDEFINES NF-NUMERIC-FIELD-WORK.    CL*12
00189          10  FILLER              PIC X(2).                           CL*12
00190          10  NF-NUMERIC-FIELD-15-DIG-2-DEC                           CL*12
00191                                  PIC S9(15)V9(2).                    CL*12
00192          10  FILLER              PIC X(4).                           CL*12
00193      05  FILLER                  REDEFINES NF-NUMERIC-FIELD-WORK.    CL*12
00194          10  NF-NUMERIC-FIELD-17-DIG-0-DEC                           CL*12
00195                                  PIC S9(17).                         CL*12
00196          10  FILLER              PIC X(6).                           CL*12
00197                                                                   DPKUT100
00198  LINKAGE SECTION.                                                 DPKUT100
00199                                                                   DPKUT100
00200  01  NE-NUMERIC-EDIT-PARAMETER-LIST                                  CL*13
00201                                  PIC X(50).                          CL*13
00202                                                                   DPKUT100
00203 ******************************************************************DPKUT100
00204  PROCEDURE DIVISION USING NE-NUMERIC-EDIT-PARAMETER-LIST.            CL*13
00205 ******************************************************************DPKUT100
00206                                                                   DPKUT100
00207  0000-MAINLINE.                                                   DPKUT100
00208      PERFORM 1000-INITIALIZE.                                     DPKUT100
00209                                                                   DPKUT100
00210      IF DP010I-NO-ERROR-DETECTED                                     CL*12
00211          PERFORM 2000-PROCESS-UNEDITED-FIELD                      DPKUT100
00212      END-IF.                                                      DPKUT100
00213                                                                   DPKUT100
00214      IF ((DP010I-NO-ERROR-DETECTED)                                  CL*12
00215              AND (PS-NON-BLANK-FIELD))                            DPKUT100
00216          PERFORM 3000-CONVERT-TO-NUMERIC                          DPKUT100
00217      END-IF.                                                      DPKUT100
00218                                                                      CL*12
00219      IF DP010I-NO-ERROR-DETECTED                                     CL*13
00220          PERFORM 4000-CHECK-CONSTRAINTS                              CL*13
00221      END-IF.                                                         CL*13
00222                                                                      CL*12
00223 *                                                                    CL*12
00224 *  MOVE THE RESULTS OF THE NUMERIC EDIT INTO THE RETURN AREA.        CL*12
00225 *                                                                    CL*12
00226                                                                      CL*12
00227      EVALUATE TRUE                                                   CL*12
00228          WHEN DP010I-11-DIG-6-DEC-FORMAT                             CL*12
00229              MOVE NF-NUMERIC-FIELD-11-DIG-6-DEC TO                   CL*12
00230                        DP010I-NUM-FIELD-11-DIG-6-DEC                 CL*15
00231          WHEN DP010I-13-DIG-4-DEC-FORMAT                             CL*12
00232              MOVE NF-NUMERIC-FIELD-13-DIG-4-DEC TO                   CL*12
00233                        DP010I-NUM-FIELD-13-DIG-4-DEC                 CL*15
00234          WHEN DP010I-15-DIG-2-DEC-FORMAT                             CL*12
00235              MOVE NF-NUMERIC-FIELD-15-DIG-2-DEC TO                   CL*12
00236                        DP010I-NUM-FIELD-15-DIG-2-DEC                 CL*15
00237          WHEN DP010I-17-DIG-0-DEC-FORMAT                             CL*12
00238              MOVE NF-NUMERIC-FIELD-17-DIG-0-DEC TO                   CL*12
00239                        DP010I-NUM-FIELD-17-DIG-0-DEC                 CL*15
00240      END-EVALUATE.                                                   CL*12
00241                                                                      CL*13
00242      MOVE DP010I-NUMERIC-EDIT-AREA TO                                CL*13
00243                NE-NUMERIC-EDIT-PARAMETER-LIST.                       CL*13
00244                                                                   DPKUT100
00245  0500-EXIT-PROGRAM.                                               DPKUT100
00246      EXIT PROGRAM.                                                DPKUT100
00247                                                                   DPKUT100
00248 ******************************************************************DPKUT100
00249 *  INITIALIZE ALL NUMERIC EDIT SUBROUTINE RESPONSE FIELDS AND    *   CL*17
00250 *  REQUIRED NUMERIC EDIT WORK FIELDS.  VALIDATE THE SUBROUTINE   *   CL*17
00251 *  PARAMETERS THAT WERE PASSED IN THE PARAMETER LIST.  DETERMINE *   CL*17
00252 *  NUMERIC FORMAT THAT IS REQUIRED TO STORE THE NUMERIC          *   CL*17
00253 *  REPRESENTATION OF THE FIELD.                                  *   CL*17
00254 ******************************************************************DPKUT100
00255                                                                   DPKUT100
00256  1000-INITIALIZE.                                                 DPKUT100
00257      MOVE NE-NUMERIC-EDIT-PARAMETER-LIST TO                          CL*13
00258                DP010I-NUMERIC-EDIT-AREA.                             CL*13
00259                                                                      CL*12
00260      INITIALIZE PV-CURRENT-INDEX-LOCATION                            CL*12
00261                 UF-UNEDITED-FIELD-AREA                               CL*12
00262                 DP010I-NUMERIC-FIELD                                 CL*12
00263                 DP010I-DIGIT-COUNT                                   CL*12
00264                 DP010I-DECIMAL-COUNT.                                CL*12
00265      MOVE ZEROS TO NF-NUMERIC-FIELD-WORK.                            CL*12
00266      SET PS-NON-BLANK-FIELD                                       DPKUT100
00267          DP010I-NO-ERROR-DETECTED                                    CL*12
00268          DP010I-11-DIG-6-DEC-FORMAT                                  CL*12
00269          DP010I-NO-DECIMAL-POINT-FOUND                               CL*12
00270          DP010I-NO-SIGN-FOUND                                        CL*12
00271          DP010I-NO-SIGN-LOCATED TO TRUE.                             CL*12
00272                                                                   DPKUT100
00273      EVALUATE TRUE                                                   CL**2
00274          WHEN DP010I-MAXIMUM-DIGITS <= PC-REQUIRES-11-DIGITS         CL*12
00275              IF DP010I-MAXIMUM-DECIMALS >                            CL*12
00276                                    PC-MAXIMUM-DECIMAL-W-11-DIGITS    CL**2
00277                  SET DP010I-MAX-FIELD-SIZE-ERROR TO TRUE             CL*13
00278              ELSE                                                    CL*12
00279                  SET DP010I-11-DIG-6-DEC-FORMAT TO TRUE              CL*12
00280              END-IF                                                  CL*12
00281          WHEN DP010I-MAXIMUM-DIGITS <= PC-REQUIRES-13-DIGITS         CL*12
00282              IF DP010I-MAXIMUM-DECIMALS >                            CL*12
00283                                    PC-MAXIMUM-DECIMAL-W-13-DIGITS    CL**2
00284                  SET DP010I-MAX-FIELD-SIZE-ERROR TO TRUE             CL*13
00285              ELSE                                                    CL*12
00286                  SET DP010I-13-DIG-4-DEC-FORMAT TO TRUE              CL*12
00287              END-IF                                                  CL**2
00288          WHEN DP010I-MAXIMUM-DIGITS <= PC-REQUIRES-15-DIGITS         CL*12
00289              IF DP010I-MAXIMUM-DECIMALS >                            CL*12
00290                                    PC-MAXIMUM-DECIMAL-W-15-DIGITS    CL**2
00291                  SET DP010I-MAX-FIELD-SIZE-ERROR TO TRUE             CL*13
00292              ELSE                                                    CL*12
00293                  SET DP010I-15-DIG-2-DEC-FORMAT TO TRUE              CL*12
00294              END-IF                                                  CL**2
00295          WHEN DP010I-MAXIMUM-DIGITS <= PC-REQUIRES-17-DIGITS         CL*12
00296              IF DP010I-MAXIMUM-DECIMALS >                            CL*12
00297                                    PC-MAXIMUM-DECIMAL-W-17-DIGITS    CL**2
00298                  SET DP010I-MAX-FIELD-SIZE-ERROR TO TRUE             CL*13
00299              ELSE                                                    CL*12
00300                  SET DP010I-17-DIG-0-DEC-FORMAT TO TRUE              CL*12
00301              END-IF                                                  CL**2
00302          WHEN OTHER                                                  CL**8
00303              SET DP010I-MAX-FIELD-SIZE-ERROR TO TRUE                 CL*13
00304      END-EVALUATE.                                                   CL**2
00305                                                                      CL**2
00306      IF ((NOT DP010I-NEGATIVE-ALLOWED)                               CL*12
00307             AND (NOT DP010I-NEGATIVE-NOT-ALLOWED))                   CL*12
00308          SET DP010I-NEG-ALLOWED-IND-ERROR TO TRUE                    CL*13
00309      END-IF.                                                      DPKUT100
00310                                                                   DPKUT100
00311      MOVE DP010I-UNEDITED-FIELD TO UF-UNEDITED-FIELD.                CL*12
00312 *1000-EXIT.                                                       DPKUT100
00313                                                                   DPKUT100
00314 ******************************************************************DPKUT100
00315 *  PROCESS THE UNEDITED FIELD.  FIND THE LOCATION OF THE FIRST   *DPKUT100
00316 *  CHARACTER IN THE FIELD BEING EDITED.  PROCESS EACH CHARACTER  *DPKUT100
00317 *  UNTIL EITHER AN ERROR IS DETECTED, OR A FIELD ENDING          *DPKUT100
00318 *  DELIMITER IS DETECTED.  CHECK FOR CHARACTERS BEYOND THE       *DPKUT100
00319 *  DELIMITED FIELD, AND INDICATE AN ERROR IF ANY ARE FOUND.      *DPKUT100
00320 *  THE FIELD BEING PROCESSED INTERNALLY BY THE PROGRAM IS LARGER *DPKUT100
00321 *  THAN THE PASSED UNEDITED FIELD.  NO CHECKS FOR MAXIMUM INDEX  *   CL*17
00322 *  VALUES ARE REQUIRED, SINCE A FIELD DELIMITER WILL ALWAYS BE   *   CL*17
00323 *  PRESENT IN THE INTERNAL UNEDITED FIELD.                       *   CL*17
00324 ******************************************************************DPKUT100
00325                                                                   DPKUT100
00326  2000-PROCESS-UNEDITED-FIELD.                                     DPKUT100
00327      SET UF-CURRENT-INDEX                                         DPKUT100
00328          UF-STARTING-LOCATION TO 1.                               DPKUT100
00329      SEARCH UF-UNEDITED-CHARACTER                                 DPKUT100
00330          WHEN (NOT UF-FIELD-DELIMITER (UF-CURRENT-INDEX))         DPKUT100
00331              SET UF-STARTING-LOCATION TO UF-CURRENT-INDEX         DPKUT100
00332      END-SEARCH.                                                  DPKUT100
00333                                                                   DPKUT100
00334      IF UF-CURRENT-INDEX = UF-STARTING-LOCATION                   DPKUT100
00335          PERFORM 2100-PROCESS-UNEDITED-CHARS                      DPKUT100
00336              UNTIL ((DP010I-ERROR-DETECTED)                          CL*12
00337                      OR (UF-FIELD-DELIMITER (UF-CURRENT-INDEX)))     CL**3
00338      ELSE                                                            CL**2
00339          SET PS-BLANK-OR-UNENTERED-FIELD TO TRUE                     CL**2
00340      END-IF.                                                      DPKUT100
00341                                                                   DPKUT100
00342      EVALUATE TRUE                                                   CL**2
00343          WHEN DP010I-ERROR-DETECTED                                  CL*12
00344          WHEN PS-BLANK-OR-UNENTERED-FIELD                            CL**2
00345              CONTINUE                                                CL**2
00346          WHEN ((DP010I-DIGIT-COUNT = ZERO)                           CL*12
00347                  AND (DP010I-DECIMAL-COUNT = ZERO))                  CL*12
00348              SET DP010I-NO-NUM-SOME-NON-NUM TO TRUE                  CL*13
00349          WHEN DP010I-NO-DECIMAL-POINT-FOUND                          CL*12
00350              SET UF-DECIMAL-LOCATION TO UF-CURRENT-INDEX             CL**2
00351              IF DP010I-TRAILING-SIGN                                 CL*12
00352                  SET UF-DECIMAL-LOCATION DOWN BY 1                   CL**2
00353              END-IF                                                  CL**2
00354      END-EVALUATE.                                                   CL**2
00355                                                                      CL**2
00356      IF ((DP010I-NO-ERROR-DETECTED)                                  CL*12
00357              AND (PS-NON-BLANK-FIELD))                               CL**2
00358          SEARCH UF-UNEDITED-CHARACTER                             DPKUT100
00359              WHEN (NOT UF-FIELD-DELIMITER (UF-CURRENT-INDEX))     DPKUT100
00360                  SET DP010I-CHAR-AFTER-DELIMITER TO TRUE             CL*16
00361          END-SEARCH                                               DPKUT100
00362      END-IF.                                                      DPKUT100
00363 *2000-EXIT.                                                       DPKUT100
00364                                                                   DPKUT100
00365 ******************************************************************DPKUT100
00366 *  EVALUATE THE CURRENT UNEDITED CHARACTER.  THE FOLLOWING ARE   *DPKUT100
00367 *  CONSIDERED ERRORS:                                            *DPKUT100
00368 *                                                                *DPKUT100
00369 *       1.  MORE THAN ONE SIGN OR DECIMAL POINT.                 *DPKUT100
00370 *       2.  A SIGN, IN OTHER THAN THE STARTING POSITION, THAT IS *DPKUT100
00371 *                NOT FOLLOWED BY A SPACE OR LOW-VALUES.          *DPKUT100
00372 *       3.  A COMMA THAT IS NOT FOLLOWED BY THREE DIGITS AND     *DPKUT100
00373 *                ANOTHER COMMA, A SIGN, A DECIMAL POINT, OR      *DPKUT100
00374 *                THE END OF THE FIELD.                           *DPKUT100
00375 *       4.  A COMMA THAT IS FOUND AFTER A DECIMAL POINT.         *DPKUT100
00376 *       5.  A DOLLAR SIGN THAT IS FOUND OTHER THAN THE STARTING  *DPKUT100
00377 *                POSITION, OR IN THE SECOND POSITION PRECEDED    *DPKUT100
00378 *                BY A SIGN.                                      *DPKUT100
00379 *       6.  A DOLLAR SIGN THAT IS NOT FOLLOWED BY A NUMERIC      *DPKUT100
00380 *                DIGIT.                                          *DPKUT100
00381 *       7.  A NON-NUMERIC CHARACTER OTHER THAN A SIGN, COMMA,    *DPKUT100
00382 *                DECIMAL POINT, DOLLAR SIGN, SPACE, OR           *DPKUT100
00383 *                LOW-VALUES.                                     *DPKUT100
00384 *                                                                *DPKUT100
00385 *  AFTER THE EVALUATION, SET THE INDEX TO THE NEXT DIGIT IF NO   *DPKUT100
00386 *  ERROR HAS BEEN DETECTED, AND THE END OF THE NUMERIC FIELD TO  *DPKUT100
00387 *  BE EDITED HAS NOT BEEN ENCOUNTERED.                           *DPKUT100
00388 ******************************************************************DPKUT100
00389                                                                   DPKUT100
00390  2100-PROCESS-UNEDITED-CHARS.                                     DPKUT100
00391      SET PV-CURRENT-INDEX-LOCATION TO UF-CURRENT-INDEX.              CL**4
00392                                                                   DPKUT100
00393      EVALUATE TRUE                                                DPKUT100
00394          WHEN UF-UNEDITED-CHARACTER (UF-CURRENT-INDEX) NUMERIC    DPKUT100
00395              IF DP010I-DECIMAL-POINT-FOUND                           CL*12
00396                  ADD +1 TO DP010I-DECIMAL-COUNT                      CL*12
00397              ELSE                                                 DPKUT100
00398                  ADD +1 TO DP010I-DIGIT-COUNT                        CL*12
00399              END-IF                                               DPKUT100
00400          WHEN UF-SIGN (UF-CURRENT-INDEX)                          DPKUT100
00401              IF DP010I-NO-SIGN-FOUND                                 CL*13
00402                  IF ((UF-CURRENT-INDEX = UF-STARTING-LOCATION)       CL*13
00403                          OR (UF-FIELD-DELIMITER                      CL*13
00404                                          (UF-CURRENT-INDEX + 1)))    CL*13
00405                      MOVE UF-UNEDITED-CHARACTER (UF-CURRENT-INDEX)   CL*13
00406                                TO DP010I-SIGN-IND                    CL*13
00407                      IF UF-CURRENT-INDEX = UF-STARTING-LOCATION      CL*13
00408                          SET DP010I-LEADING-SIGN TO TRUE             CL*13
00409                      ELSE                                            CL*13
00410                          SET DP010I-TRAILING-SIGN TO TRUE            CL*13
00411                      END-IF                                          CL*13
00412                  ELSE                                                CL*13
00413                      SET DP010I-INVALID-SIGN-LOC TO TRUE             CL*13
00414                  END-IF                                              CL*13
00415              ELSE                                                    CL*13
00416                  SET DP010I-MULTIPLE-SIGNS-FOUND TO TRUE             CL*13
00417              END-IF                                               DPKUT100
00418          WHEN UF-DECIMAL-POINT (UF-CURRENT-INDEX)                 DPKUT100
00419              IF DP010I-NO-DECIMAL-POINT-FOUND                        CL*12
00420                  SET UF-DECIMAL-LOCATION TO UF-CURRENT-INDEX      DPKUT100
00421                  SET DP010I-DECIMAL-POINT-FOUND TO TRUE              CL*12
00422              ELSE                                                 DPKUT100
00423                  SET DP010I-MULTIPLE-DEC-PTS-FOUND TO TRUE           CL*13
00424              END-IF                                               DPKUT100
00425          WHEN UF-DOLLAR-SIGN (UF-CURRENT-INDEX)                   DPKUT100
00426              EVALUATE TRUE                                        DPKUT100
00427                  WHEN (UF-CURRENT-INDEX = UF-STARTING-LOCATION)      CL**4
00428                  WHEN (((PV-CURRENT-INDEX-LOCATION - 1) =            CL**5
00429                                          UF-STARTING-LOCATION)       CL**4
00430                          AND (UF-SIGN (UF-STARTING-LOCATION)))       CL**3
00431                      IF ((UF-UNEDITED-CHARACTER                   DPKUT100
00432                                (UF-CURRENT-INDEX + 1) NOT NUMERIC)DPKUT100
00433                              AND (NOT UF-DECIMAL-POINT            DPKUT100
00434                                    (UF-CURRENT-INDEX + 1)))          CL**5
00435                          SET DP010I-INVALID-DOLLAR-SIGN-USE TO       CL*13
00436                                    TRUE                              CL*13
00437                      END-IF                                       DPKUT100
00438                  WHEN OTHER                                       DPKUT100
00439                      SET DP010I-INVALID-DOLLAR-SIGN-LOC TO TRUE      CL*13
00440              END-EVALUATE                                         DPKUT100
00441          WHEN UF-COMMA (UF-CURRENT-INDEX)                         DPKUT100
00442              EVALUATE TRUE                                           CL**3
00443                  WHEN DP010I-DECIMAL-POINT-FOUND                     CL*12
00444                  WHEN ((PV-CURRENT-INDEX-LOCATION +                  CL**4
00445                                PC-DIGITS-AFTER-COMMA)                CL**4
00446                                > PC-MAXIMUM-UNEDITED-CHARACTERS)  DPKUT100
00447                  WHEN (UF-UNEDITED-CHARACTER                      DPKUT100
00448                                (UF-CURRENT-INDEX + 1) NOT NUMERIC)DPKUT100
00449                  WHEN (UF-UNEDITED-CHARACTER                      DPKUT100
00450                                (UF-CURRENT-INDEX + 2) NOT NUMERIC)DPKUT100
00451                  WHEN (UF-UNEDITED-CHARACTER                      DPKUT100
00452                                (UF-CURRENT-INDEX + 3) NOT NUMERIC)DPKUT100
00453                      SET DP010I-INVALID-COMMA-LOC TO TRUE            CL*13
00454                  WHEN UF-FIELD-DELIMITER (UF-CURRENT-INDEX + 4)   DPKUT100
00455                  WHEN UF-COMMA (UF-CURRENT-INDEX + 4)             DPKUT100
00456                  WHEN UF-DECIMAL-POINT (UF-CURRENT-INDEX + 4)     DPKUT100
00457                  WHEN UF-SIGN (UF-CURRENT-INDEX + 4)              DPKUT100
00458                      CONTINUE                                     DPKUT100
00459                  WHEN OTHER                                       DPKUT100
00460                      SET DP010I-INVALID-COMMA-LOC TO TRUE            CL*13
00461              END-EVALUATE                                         DPKUT100
00462          WHEN OTHER                                               DPKUT100
00463              SET DP010I-INVALID-CHARACTER TO TRUE                    CL*13
00464      END-EVALUATE.                                                DPKUT100
00465                                                                   DPKUT100
00466      SET UF-CURRENT-INDEX UP BY 1.                                DPKUT100
00467 *2100-EXIT.                                                       DPKUT100
00468                                                                   DPKUT100
00469 ******************************************************************DPKUT100
00470 *  CONVERT THE VALIDATED FIELD TO A NUMERIC REPRESENTATION.      *   CL*13
00471 *  THE NUMERIC FIELD HAS A STANDARD IMPLIED DECIMAL POSITION.    *   CL*13
00472 *  BEGIN BY MOVING ALL DIGITS, RIGHT TO LEFT FROM THE DECIMAL    *   CL*13
00473 *  LOCATION (THE END OF THE FIELD, FOR WHOLE NUMBERS), TO THE    *   CL*13
00474 *  DIGIT POSITIONS OF THE NUMERIC REPRESENTATION.  NEXT, MOVE    *   CL*13
00475 *  ALL DECIMAL POSITIONS, LEFT TO RIGHT FROM THE DECIMAL         *   CL*13
00476 *  LOCATION, TO THE DECIMAL POSITIONS OF THE NUMERIC             *   CL*13
00477 *  REPRESENTATION.  ALL NON-NUMERIC EDIT CHARACTERS WILL BE      *   CL*13
00478 *  SKIPPED DURING THE MOVE.  AT THE END OF THE CONVERSION,       *   CL*13
00479 *  SIGN THE FIELD, REFERENCING THE FORMAT THAT IS THE MINIMUM    *   CL*13
00480 *  REQUIREMENT TO SUPPORT THE PARAMETER-PASSED FIELD SIZE        *   CL*13
00481 *  CONSTRAINTS.                                                  *   CL*13
00482 ******************************************************************DPKUT100
00483                                                                   DPKUT100
00484  3000-CONVERT-TO-NUMERIC.                                         DPKUT100
00485      SET NF-INDEX TO PC-FIRST-NUMERIC-FIELD-DECIMAL.              DPKUT100
00486      SET NF-INDEX DOWN BY 1.                                         CL*10
00487      SET UF-CURRENT-INDEX TO UF-DECIMAL-LOCATION.                 DPKUT100
00488                                                                   DPKUT100
00489      PERFORM DP010I-DIGIT-COUNT TIMES                                CL*12
00490          PERFORM                                                  DPKUT100
00491                  UNTIL UF-UNEDITED-CHARACTER (UF-CURRENT-INDEX)   DPKUT100
00492                                                          NUMERIC  DPKUT100
00493              SET UF-CURRENT-INDEX DOWN BY 1                       DPKUT100
00494          END-PERFORM                                              DPKUT100
00495          MOVE UF-NUMERIC-DIGIT (UF-CURRENT-INDEX) TO              DPKUT100
00496                    NF-NUMERIC-DIGIT (NF-INDEX)                    DPKUT100
00497          SET UF-CURRENT-INDEX                                     DPKUT100
00498              NF-INDEX DOWN BY 1                                   DPKUT100
00499      END-PERFORM.                                                 DPKUT100
00500                                                                   DPKUT100
00501      SET NF-INDEX TO PC-FIRST-NUMERIC-FIELD-DECIMAL.              DPKUT100
00502      SET UF-CURRENT-INDEX TO UF-DECIMAL-LOCATION.                 DPKUT100
00503                                                                   DPKUT100
00504      PERFORM DP010I-DECIMAL-COUNT TIMES                              CL*12
00505          PERFORM                                                  DPKUT100
00506                  UNTIL UF-UNEDITED-CHARACTER (UF-CURRENT-INDEX)   DPKUT100
00507                                                          NUMERIC  DPKUT100
00508              SET UF-CURRENT-INDEX UP BY 1                         DPKUT100
00509          END-PERFORM                                              DPKUT100
00510          MOVE UF-NUMERIC-DIGIT (UF-CURRENT-INDEX) TO              DPKUT100
00511                    NF-NUMERIC-DIGIT (NF-INDEX)                    DPKUT100
00512          SET UF-CURRENT-INDEX                                     DPKUT100
00513              NF-INDEX UP BY 1                                     DPKUT100
00514      END-PERFORM.                                                 DPKUT100
00515                                                                      CL**2
00516      EVALUATE TRUE                                                   CL**2
00517          WHEN (NOT DP010I-NEGATIVE-SIGN-FOUND)                       CL*12
00518              CONTINUE                                                CL**2
00519          WHEN DP010I-11-DIG-6-DEC-FORMAT                             CL*12
00520              COMPUTE NF-NUMERIC-FIELD-11-DIG-6-DEC =                 CL**2
00521                        (NF-NUMERIC-FIELD-11-DIG-6-DEC * (-1))        CL**2
00522          WHEN DP010I-13-DIG-4-DEC-FORMAT                             CL*12
00523              COMPUTE NF-NUMERIC-FIELD-13-DIG-4-DEC =                 CL**2
00524                        (NF-NUMERIC-FIELD-13-DIG-4-DEC * (-1))        CL**2
00525          WHEN DP010I-15-DIG-2-DEC-FORMAT                             CL*12
00526              COMPUTE NF-NUMERIC-FIELD-15-DIG-2-DEC =                 CL**2
00527                        (NF-NUMERIC-FIELD-15-DIG-2-DEC * (-1))        CL**2
00528          WHEN DP010I-17-DIG-0-DEC-FORMAT                             CL*12
00529              COMPUTE NF-NUMERIC-FIELD-17-DIG-0-DEC =                 CL**2
00530                        (NF-NUMERIC-FIELD-17-DIG-0-DEC * (-1))        CL**2
00531      END-EVALUATE.                                                   CL**2
00532 *3000-EXIT.                                                       DPKUT100
00533                                                                      CL*13
00534 ******************************************************************   CL*13
00535 *  CHECK THE VALIDATED NUMERIC FIELD AGAINST THE PARAMETER-      *   CL*13
00536 *  PASSED NUMERIC FIELD CONSTRAINTS.  SET AN ERROR INDICATION    *   CL*17
00537 *  IF THE NUMERIC FIELD FAILS TO ALL OF THE PASSED CONSTRAINTS.  *   CL*17
00538 ******************************************************************   CL*13
00539                                                                      CL*13
00540  4000-CHECK-CONSTRAINTS.                                             CL*13
00541      EVALUATE TRUE                                                   CL*13
00542          WHEN ((DP010I-NEGATIVE-SIGN-FOUND)                          CL*13
00543                  AND (DP010I-NEGATIVE-NOT-ALLOWED))                  CL*13
00544              SET DP010I-NEG-NOT-ALLOWED TO TRUE                      CL*13
00545          WHEN DP010I-DIGIT-COUNT > DP010I-MAXIMUM-DIGITS             CL*13
00546              SET DP010I-MAX-DIGITS-EXCEEDED TO TRUE                  CL*13
00547          WHEN DP010I-DECIMAL-COUNT > DP010I-MAXIMUM-DECIMALS         CL*13
00548              SET DP010I-MAX-DECIMALS-EXCEEDED TO TRUE                CL*13
00549      END-EVALUATE.                                                   CL*13
00550 *4000-EXIT.                                                          CL*13
