       IDENTIFICATION DIVISION.                                         09/16/94
                                                                           LV020
       PROGRAM-ID.             IMKUP023.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           09/21/2001.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKUP023 - LEGACY ITEM MAINTENANCE TVNDSTL UPDATE TRIGGER RPC     CL**1
      ******************************************************************   CL**1
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT LEGACY        CL**1
      * ITEM MAINTENANCE CHANGE VPN (STYLE NUMBER) OR CHANGE SUPPLIER      CL**1
      * TRANSACTION RECORDS AND INSERT THEM TO THE ITEM EVENT MESSAGE      CL**1
      * BRIDGE DB2 TABLE.                                                  CL**1
      ******************************************************************   CL**1
      * 12/22/2005  P. KEBER      WR29786 - PRICE TIER AND LIFESTYLE            
      *   CHANGE TO FILL IM008-SUPPLR-LBL-SUP ON THE 020 MESSAGE.               
      ******************************************************************   CL**1
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUP023'.         
           05  PC-MAXIMUM-RETRY-COUNT  PIC S9(04)     VALUE +2                  
                                                           COMP SYNC.           
           05  PC-INTERNAL-UPC-CDE     PIC  X(01)     VALUE 'I'.                
           05  PC-OPERCO-NBR           PIC  X(03)     VALUE '03'.               
           05  PC-ORIGIN-CNTRY         PIC  X(03)     VALUE 'US '.              
           05  PC-SPACES               PIC  X(03)     VALUE '   '.              
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                              *    CL**1
      ******************************************************************   CL**1
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DB-ERROR-SW          PIC X(01)      VALUE 'N'.           CL**1
               88  PS-DB-ERROR                        VALUE 'Y'.           CL**1
               88  PS-NO-DB-ERROR                     VALUE 'N'.           CL**1
           05  PS-END-OF-TMDARVS-CSR-SW PIC X(01)     VALUE 'N'.                
               88  PS-END-OF-TMDARVS-CSR              VALUE 'Y'.                
               88  PS-PROCESS-TMDARVS-CSR             VALUE 'N'.                
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-VS-ID                PIC S9(09)  VALUE ZERO                   
                                                   COMP.                        
           05  PV-ENT-ID               PIC S9(09)  VALUE ZERO                   
                                                   COMP.                        
           05  PV-RETRY-COUNT          PIC S9(04)  VALUE ZERO                   
                                                   COMP.                        
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACES.                
           05  PV-DB2-OPERATION        PIC  X(30)  VALUE SPACES.                
           05  PV-LITERAL              PIC  X(01)  VALUE SPACE.                 
           05  PV-MSG-TXT-TEXT         PIC  X(999) VALUE SPACE.                 
                                                                                
           05  PV-TEXTENT-NOT-FOUND-MSG.                                   CL**1
               10  FILLER                      PIC X(34)   VALUE           CL**1
                   'TEXTENT ROW NOT FOUND FOR ENT ID: '.                        
               10  PV-TEXTENT-NFM-ENT-ID       PIC Z(09)   VALUE ZERO.     CL**1
                                                                                
           05  PV-TMDSEAR-NOT-FOUND-MSG.                                   CL**1
               10  FILLER                      PIC X(40)   VALUE           CL**1
                   'TMDSEAR ROW NOT FOUND FOR SUB CLASS ID: '.                  
               10  PV-TMDSEAR-NFM-SUB-CL-ID    PIC Z(06)   VALUE ZERO.     CL**1
                                                                                
      *****************************************************************         
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                                  
      ******************************************************************        
           COPY IMRD008.                                                        
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  EXTERNAL ENTITY TABLE                                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TEXTENT                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE AREA VENDOR STYLE TABLE                                    
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE AREA TABLE                                                 
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TMDARVS CURSOR DECLARATION                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE TMDARVS_CSR CURSOR FOR                                     
               SELECT SUB_CL_MDSEAR_ID,                                         
                      RMS_STYL_ID,                                              
                      LBL_SEQ_NBR                                               
               FROM TMDARVS                                                     
               WHERE VS_ID = :PV-VS-ID                                          
                 AND ACTV_IND = 'Y'                                             
           END-EXEC.                                                            
                                                                           CL**1
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  LS-VS-ID                     PIC S9(9)   COMP.                       
       01  LS-ENT-ID-NEW                PIC S9(9)   COMP.                       
       01  LS-ENT-ID-OLD                PIC S9(9)   COMP.                       
       01  LS-VS-NBR-NEW                PIC  X(20).                             
       01  LS-VS-NBR-OLD                PIC  X(20).                             
                                                                                
       PROCEDURE DIVISION USING  LS-VS-ID                                       
                               , LS-ENT-ID-NEW                                  
                               , LS-ENT-ID-OLD                                  
                               , LS-VS-NBR-NEW                                  
                               , LS-VS-NBR-OLD.                                 
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           SET PS-NO-DB-ERROR TO TRUE.                                          
                                                                                
      *    DETERMINE IF THIS IS A SUPPLIER CHANGE BUSINESS EVENT                
           IF LS-ENT-ID-OLD NOT = LS-ENT-ID-NEW                                 
               PERFORM 2000-PROCESS-SUPPLIER-CHG                                
           END-IF.                                                              
                                                                                
      *    DETERMINE IF THIS IS A VENDOR PRODUCT NUMBER CHANGE BUSINESS         
      *    EVENT                                                                
           IF LS-VS-NBR-OLD NOT = LS-VS-NBR-NEW                                 
               PERFORM 2100-PROCESS-VPN-CHG                                     
           END-IF.                                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  PROCESS THE SUPPLIER CHANGE BUSINESS EVENT                             
      ******************************************************************        
       2000-PROCESS-SUPPLIER-CHG.                                               
                                                                                
           SET IM008-BUS-EVENT-SUPPLIER-CHG TO TRUE.                            
           MOVE SPACES TO IM008-CRE-TMST.                                       
                                                                                
      *    GET TEXTENT INFO FOR ITEM KEY DATA                                   
           MOVE LS-ENT-ID-OLD       TO PV-ENT-ID.                               
           PERFORM 8000-GET-EXTENT-DATA.                                        
           MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR.                  
                                                                                
      *    FILL IN NON-CHANGING ITEM KEY DATA FIELDS                            
           INITIALIZE IM008-ITEM-KEY-DATA.                                      
           MOVE LS-VS-ID            TO IM008-ITEM-KEY-VS-ID.                    
           MOVE LS-ENT-ID-OLD       TO IM008-ITEM-KEY-ENT-ID.                   
           MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR.                  
           MOVE LS-VS-NBR-OLD       TO IM008-ITEM-KEY-VS-NBR.                   
           MOVE PC-ORIGIN-CNTRY     TO IM008-ITEM-KEY-ORIGIN-CNTRY.             
                                                                                
      *    GET TEXTENT INFO FOR SUPPLIER CHANGE DATA                            
           IF PS-NO-DB-ERROR                                                    
               MOVE LS-ENT-ID-NEW   TO PV-ENT-ID                                
               PERFORM 8000-GET-EXTENT-DATA                                     
           END-IF.                                                              
                                                                                
      *    FILL IN THE SUPPLIER CHANGE DATA                                     
           MOVE LS-ENT-ID-NEW       TO IM008-SUPPLR-CHG-ENT-ID-NEW.             
           MOVE EXTENT-DUN-NBR      TO IM008-SUPPLR-CHG-DUN-NBR-NEW.            
           MOVE LOW-VALUES          TO IM008-SUPPLR-CHG-FILLER.                 
                                                                                
      *    GET TMDARVS ROWS CONTAINING THE VS_ID                                
           IF PS-NO-DB-ERROR                                                    
               SET PS-PROCESS-TMDARVS-CSR TO TRUE                               
               PERFORM 7100-OPEN-TMDARVS-CURSOR                                 
               IF PS-NO-DB-ERROR                                                
                   PERFORM 7110-FETCH-TMDARVS-CURSOR                            
                       UNTIL PS-END-OF-TMDARVS-CSR                              
                          OR PS-DB-ERROR                                        
               END-IF                                                           
               IF PS-NO-DB-ERROR                                                
                   PERFORM 7120-CLOSE-TMDARVS-CURSOR                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE A SUPPLIER CHANGE BUSINESS EVENT                      
      ******************************************************************        
       2050-CREATE-SUPPLIER-CHG.                                                
                                                                                
      *    GET TMDSEAR INFO FOR ITEM KEY DATA                                   
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8100-GET-MSDEAR-INFO                                     
           END-IF.                                                              
                                                                                
      *    FILL IN THE ITEM KEY DATA FIELDS                                     
           MOVE MDARVS-SUB-CL-MDSEAR-ID                                         
                                    TO IM008-ITEM-KEY-SUBCL-MDSE-ID.            
           MOVE MDSEAR-DEPT-NBR     TO IM008-ITEM-KEY-DEPT.                     
           MOVE MDSEAR-MAJ-CL-NBR   TO IM008-ITEM-KEY-CLASS.                    
           MOVE MDSEAR-SUB-CL-NBR   TO IM008-ITEM-KEY-SUBCL.                    
           MOVE MDARVS-RMS-STYL-ID  TO IM008-ITEM-KEY-RMS-STYL-ID.              
                                                                                
      *    FILL IN THE SUPPLIER CHANGE DATA                                     
           MOVE MDARVS-LBL-SEQ-NBR  TO IM008-SUPPLR-LBL-SUP.                    
                                                                                
      *    INSERT SUPPLIER CHANGE BUSINESS EVENT RECORD TO THE ITEM             
      *    EVENT MESSAGE BRIDGE DB2 TABLE.                                      
           IF PS-NO-DB-ERROR                                                    
               COMPUTE IMT00001-MSG-TXT-LEN =                                   
                  LENGTH OF IMRD008-RECORD -                                    
                  LENGTH OF IM008-SUPPLR-CHG-FILLER                             
               PERFORM 8900-INSERT-IMT-EVNT-MSG                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  PROCESS THE VENDOR PRODUCT NUMBER CHANGE BUSINESS EVENT                
      ******************************************************************        
       2100-PROCESS-VPN-CHG.                                                    
                                                                                
           SET IM008-BUS-EVENT-VPN-CHG TO TRUE.                                 
           MOVE SPACES TO IM008-CRE-TMST.                                       
                                                                                
      *    GET TEXTENT INFO FOR ITEM KEY DATA                                   
           MOVE LS-ENT-ID-OLD       TO PV-ENT-ID.                               
           PERFORM 8000-GET-EXTENT-DATA.                                        
           MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR.                  
                                                                                
      *    FILL IN NON-CHANGING ITEM KEY DATA FIELDS                            
           INITIALIZE IM008-ITEM-KEY-DATA.                                      
           MOVE LS-VS-ID            TO IM008-ITEM-KEY-VS-ID.                    
           MOVE LS-ENT-ID-OLD       TO IM008-ITEM-KEY-ENT-ID.                   
           MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR.                  
           MOVE LS-VS-NBR-OLD       TO IM008-ITEM-KEY-VS-NBR.                   
                                                                                
      *    FILL IN THE VENDOR PRODUCT NUMBER CHANGE DATA                        
           MOVE LS-VS-NBR-NEW       TO IM008-VPN-CHG-VS-NBR-NEW.                
           MOVE LOW-VALUES          TO IM008-VPN-CHG-FILLER.                    
                                                                                
      *    GET TMDARVS ROWS CONTAINING THE VS_ID                                
           IF PS-NO-DB-ERROR                                                    
               SET PS-PROCESS-TMDARVS-CSR TO TRUE                               
               PERFORM 7100-OPEN-TMDARVS-CURSOR                                 
               IF PS-NO-DB-ERROR                                                
                   PERFORM 7110-FETCH-TMDARVS-CURSOR                            
                       UNTIL PS-END-OF-TMDARVS-CSR                              
                          OR PS-DB-ERROR                                        
               END-IF                                                           
               IF PS-NO-DB-ERROR                                                
                   PERFORM 7120-CLOSE-TMDARVS-CURSOR                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE A VENDOR PRODUCT NUMBER CHANGE BUSINESS EVENT         
      ******************************************************************        
       2150-CREATE-VPN-CHG.                                                     
                                                                                
      *    GET TMDSEAR INFO FOR ITEM KEY DATA                                   
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8100-GET-MSDEAR-INFO                                     
           END-IF.                                                              
                                                                                
      *    FILL IN THE ITEM KEY DATA FIELDS                                     
           MOVE MDARVS-SUB-CL-MDSEAR-ID                                         
                                    TO IM008-ITEM-KEY-SUBCL-MDSE-ID.            
           MOVE MDSEAR-DEPT-NBR     TO IM008-ITEM-KEY-DEPT.                     
           MOVE MDSEAR-MAJ-CL-NBR   TO IM008-ITEM-KEY-CLASS.                    
           MOVE MDSEAR-SUB-CL-NBR   TO IM008-ITEM-KEY-SUBCL.                    
           MOVE MDARVS-RMS-STYL-ID  TO IM008-ITEM-KEY-RMS-STYL-ID.              
                                                                                
      *    INSERT VENDOR PRODUCT NUMBER CHANGE BUSINESS EVENT RECORD            
      *    TO THE ITEM EVENT MESSAGE BRIDGE DB2 TABLE.                          
           IF PS-NO-DB-ERROR                                                    
               COMPUTE IMT00001-MSG-TXT-LEN =                                   
                  LENGTH OF IMRD008-RECORD -                                    
                  LENGTH OF IM008-VPN-CHG-FILLER                                
               PERFORM 8900-INSERT-IMT-EVNT-MSG                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  OPEN THE TMDARVS CURSOR                                       *        
      ******************************************************************        
       7100-OPEN-TMDARVS-CURSOR.                                                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           MOVE LS-VS-ID TO PV-VS-ID.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL SQLCODE = +0                                           
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   OPEN TMDARVS_CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '7100-OPEN-TMDARVS-CURSOR'                         C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'OPEN TMDARVS CURSOR'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '7100-OPEN-TMDARVS-CURSOR'                             CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'OPEN TMDARVS CURSOR'                                  CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FETCH A TMDARVS CURSOR ROW                                    *        
      ******************************************************************        
       7110-FETCH-TMDARVS-CURSOR.                                               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   FETCH  TMDARVS_CSR                                           
                   INTO  :MDARVS-SUB-CL-MDSEAR-ID                               
                        ,:MDARVS-RMS-STYL-ID                                    
                        ,:MDARVS-LBL-SEQ-NBR                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-END-OF-TMDARVS-CSR TO TRUE                        
                   WHEN SQLCODE = 0                                             
                       IF IM008-BUS-EVENT-SUPPLIER-CHG                          
                           PERFORM 2050-CREATE-SUPPLIER-CHG                     
                       ELSE                                                     
                           PERFORM 2150-CREATE-VPN-CHG                          
                       END-IF                                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '7110-FETCH-TMDARVS-CURSOR'                        C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'FETCH TMDARVS CURSOR'                              
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '7110-FETCH-TMDARVS-CURSOR'                            CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'FETCH TMDARVS CURSOR'                                 CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CLOSE THE TMDARVS CURSOR                                      *        
      ******************************************************************        
       7120-CLOSE-TMDARVS-CURSOR.                                               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL SQLCODE = +0                                           
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   CLOSE TMDARVS_CSR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '7120-CLOSE-TMDARVS-CURSOR'                        C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'CLOSE TMDARVS CURSOR'                         CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '7120-CLOSE-TMDARVS-CURSOR'                            CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'CLOSE TMDARVS CURSOR'                                 CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET TEXTENT INFO                                                       
      ******************************************************************        
       8000-GET-EXTENT-DATA.                                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  EXTENT.DUN_NBR                                       
                     INTO :EXTENT-DUN-NBR                                       
                     FROM TEXTENT EXTENT                                        
                    WHERE EXTENT.ENT_ID = :PV-ENT-ID                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE '8000-GET-EXTENT-DATA'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE PV-ENT-ID TO PV-TEXTENT-NFM-ENT-ID             CL**1
                       MOVE PV-TEXTENT-NOT-FOUND-MSG                       CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8000-GET-EXTENT-DATA'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TEXTENT'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8000-GET-EXTENT-DATA'                                 CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TEXTENT'                                  CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET TMSDEAR INFO                                                       
      ******************************************************************        
       8100-GET-MSDEAR-INFO.                                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  DEPT_NBR                                             
                         , MAJ_CL_NBR                                           
                         , SUB_CL_NBR                                           
                     INTO :MDSEAR-DEPT-NBR                                      
                         ,:MDSEAR-MAJ-CL-NBR                                    
                         ,:MDSEAR-SUB-CL-NBR                                    
                     FROM TMDSEAR                                               
                    WHERE MDSEAR_DKEY = :MDARVS-SUB-CL-MDSEAR-ID                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE '8100-GET-MSDEAR-INFO'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE MDARVS-SUB-CL-MDSEAR-ID                        CL*11
                                      TO PV-TMDSEAR-NFM-SUB-CL-ID          CL**1
                       MOVE PV-TMDSEAR-NOT-FOUND-MSG                       CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8100-GET-MSDEAR-INFO'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TMSDEAR'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8100-GET-MSDEAR-INFO'                                 CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TMSDEAR'                                  CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE               
      *  BRIDGE DB2 TABLE.                                                      
      ******************************************************************        
       8900-INSERT-IMT-EVNT-MSG.                                                
                                                                                
           MOVE IM008-ITEM-KEY-RMS-STYL-ID                                      
                                    TO IMT00001-RMS-STYL-ID.                    
           MOVE IM008-BUSINESS-EVENT-ID                                         
                                    TO IMT00001-BUS-EVNT-TYP-CDE.               
           MOVE IMRD008-RECORD      TO IMT00001-MSG-TXT-TEXT.                   
                                                                                
           PERFORM WITH TEST AFTER                                              
                 UNTIL SQLCODE = +0                                             
                    OR PS-DB-ERROR                                              
                    OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                  
           EXEC SQL                                                             
                INSERT INTO IMT_EVNT_MSG_BRG                                    
                       ( CRE_TMST                                               
                       ,RMS_STYL_ID                                             
                       ,BUS_EVNT_TYP_CDE                                        
                       ,MSG_TXT )                                               
                VALUES                                                          
                      ( CURRENT TIMESTAMP                                       
                      ,:IMT00001-RMS-STYL-ID                                    
                      ,:IMT00001-BUS-EVNT-TYP-CDE                               
                      ,:IMT00001-MSG-TXT)                                       
           END-EXEC                                                             
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    CONTINUE                                                    
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    ADD +1 TO PV-RETRY-COUNT                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '8900-INSERT-IMT-EVNT-MSG'                          C  
                                   TO PV-PARAGRAPH-NAME                  CL**1  
                    MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                  CL*11  
                                   TO PV-DB2-OPERATION                   CL**1  
                    PERFORM 9998-SQL-ROLLBACK                            CL**1  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8900-INSERT-IMT-EVNT-MSG'                             CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                         CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
       9998-SQL-ROLLBACK.                                                       
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ROLLBACK.                                               
                                                                                
                                                                                
       9999-ROLLBACK.                                                           
                                                                                
           DISPLAY '** IMKUP023 SQLERROR IN PARAGRAPH: '                        
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** IMKUP023 OPERATION: ' PV-DB2-OPERATION.                  
                                                                                
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
                                                                                
           SET PS-DB-ERROR TO TRUE.                                             
