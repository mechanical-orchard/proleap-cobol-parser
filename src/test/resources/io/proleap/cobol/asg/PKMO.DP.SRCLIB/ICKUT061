       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ICKUT061.                                                 
       AUTHOR.        R. KELLEN.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/01/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * ICKUT061 - DETERMINE ACTIVE COUNTS FOR MAINT OF O/H DATA                
      *****************************************************************         
      *        THIS PROGRAM WILL MATCH A FILE OF IC MAINTENENCE RECORDS         
      *        TO A FILE CONTAINING TRUCK MANIFEST DATA FOR THE PURPOSE         
      *        OF ADDING TRUCK_ACKN_QTY TO THE MAINTENENCE FILE.BOTH            
      *        FILES ARE SORTED INTO SKU/LOCATION SEQUENCE. THIS PROGRAM        
      *        DOES NOT USE DB2.                                                
      *****************************************************************         
      *                                                                         
      *****************************************************************         
      * 10/14/08 - R KELLEN         -  WR NO. WR30388                           
      *    - NEW PROGRAM.                                                       
      *                                                                         
      *                                                                         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT IC-COUNT-QTY-MAINT-FILE-I  ASSIGN TO INP01.                   
                                                                                
           SELECT UPDTD-TRUCK-MNFST-FILE     ASSIGN TO INP02.                   
                                                                                
           SELECT IC-COUNT-QTY-MAINT-FILE-O  ASSIGN TO OUT01.                   
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  IC-COUNT-QTY-MAINT-FILE-I                                            
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS NIGHTLY-HOURLY-CTL-REC-I.                             
       01  NIGHTLY-HOURLY-CTL-REC-I   PIC X(60).                                
                                                                                
       FD  UPDTD-TRUCK-MNFST-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS UPDTD-TRUCK-MNFST-REC.                                
       01  UPDTD-TRUCK-MNFST-REC      PIC  X(23).                               
                                                                                
       FD  IC-COUNT-QTY-MAINT-FILE-O                                            
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS IC-COUNT-QTY-MAINT-FILE-O.                            
       01  IC-COUNT-QTY-MAINT-REC-O   PIC  X(60).                               
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE                
                                                 'ICKUT061'.                    
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-MAINT-FL-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-MAINT-FL                      VALUE 'Y'.           
           05  PS-END-OF-MAN-FL-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-MAN-FL                        VALUE 'Y'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
                                                                                
                                                                                
      *                                                                         
      *****************************************************************         
      *    INPUT COPYBOOKS                                                      
      *****************************************************************         
           COPY ICRD058.                                                        
      *****************************************************************         
      *    OUTPUT COPYBOOKS                                                     
      *****************************************************************         
           COPY ICRD057.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-COUNT-RECS                                      
             UNTIL PS-END-OF-MAINT-FL.                                          
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
           OPEN INPUT  IC-COUNT-QTY-MAINT-FILE-I                                
                       UPDTD-TRUCK-MNFST-FILE                                   
                OUTPUT IC-COUNT-QTY-MAINT-FILE-O.                               
           PERFORM 7000-READ-MAINT-FILE.                                        
           PERFORM 7050-READ-TRUCK-MNFST-FILE.                                  
      ******************************************************************        
      * 2000-PROCESS-COUNT-RECS.                                       *        
      *  ==> MATCHES MANIFEST RECORDS TO IC MAINTENENCE RECORDS.       *        
      ******************************************************************        
       2000-PROCESS-COUNT-RECS.                                                 
           MOVE '2000-PROCESS-COUNT-RECS' TO PV-PARAGRAPH-NAME.                 
           PERFORM 7050-READ-TRUCK-MNFST-FILE                                   
             UNTIL ICRD058-SKU-NBR >= ICRD057-SKU-NBR                           
                OR PS-END-OF-MAN-FL.                                            
           IF ICRD058-SKU-NBR = ICRD057-SKU-NBR                                 
             PERFORM 7050-READ-TRUCK-MNFST-FILE                                 
               UNTIL ICRD058-LOC-NBR >= ICRD057-LOC-NBR                         
                  OR ICRD058-SKU-NBR NOT = ICRD057-SKU-NBR                      
                  OR PS-END-OF-MAN-FL.                                          
           MOVE 0 TO ICRD057-ACPTD-TRUK-UNT-QTY.                                
           IF ICRD058-SKU-NBR = ICRD057-SKU-NBR                                 
             AND ICRD058-LOC-NBR = ICRD057-LOC-NBR                              
TEST  *      DISPLAY ' MNFST FOUND - ' ICRD058-SKU-NBR ' - '                    
TEST  *                                ICRD058-LOC-NBR ' - '                    
TEST  *                                ICRD058-MNFST-QTY                        
             MOVE ICRD058-MNFST-QTY TO ICRD057-ACPTD-TRUK-UNT-QTY.              
           PERFORM 7100-WRITE-QTY-MAINT-FILE.                                   
           PERFORM 7000-READ-MAINT-FILE.                                        
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSES FILES                                              *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
           CLOSE IC-COUNT-QTY-MAINT-FILE-I                                      
                 UPDTD-TRUCK-MNFST-FILE                                         
                 IC-COUNT-QTY-MAINT-FILE-O.                                     
                                                                                
      *----------------------------------------------------------               
      * 7000-READ-MAINT-FILE.                                                   
      *----------------------------------------------------------               
       7000-READ-MAINT-FILE.                                                    
           READ IC-COUNT-QTY-MAINT-FILE-I                                       
             INTO ICRD057-IC-CNT-QTY-MAINT-FILE                                 
             AT END SET PS-END-OF-MAINT-FL TO TRUE                              
              MOVE ALL '9' TO ICRD057-SKU-NBR                                   
                              ICRD057-LOC-NBR.                                  
                                                                                
      *----------------------------------------------------------               
      * 7050-READ-TRUCK-MNFST-FILE.                                             
      *----------------------------------------------------------               
       7050-READ-TRUCK-MNFST-FILE.                                              
           READ UPDTD-TRUCK-MNFST-FILE                                          
             INTO ICRD058-REFORMAT-MANIFEST-FILE                                
             AT END SET PS-END-OF-MAN-FL TO TRUE                                
              MOVE ALL '9' TO ICRD058-SKU-NBR                                   
                              ICRD058-LOC-NBR.                                  
                                                                                
      *----------------------------------------------------------               
      * 7100-WRITE-QTY-MAINT-FILE.                                              
      *----------------------------------------------------------               
       7100-WRITE-QTY-MAINT-FILE.                                               
           WRITE IC-COUNT-QTY-MAINT-REC-O                                       
             FROM ICRD057-IC-CNT-QTY-MAINT-FILE.                                
                                                                                
