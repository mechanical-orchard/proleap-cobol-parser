000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SUKUT014.                                        
000600 AUTHOR.                 STEPHANIE HERON.                                 
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            OCTOBER, 1997.                                   
000900 DATE-COMPILED.                                                           
001000*****************************************************************         
001100*                                                               *         
001200*  THIS PROGRAM WILL CREATE AN EXTRACT FILE CONTIANING ALL      *         
001300*  INPROCESS AND POSTPONED AUDITS.                              *         
001400*****************************************************************         
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 CONFIGURATION SECTION.                                                   
001800                                                                          
001900 SOURCE-COMPUTER.        IBM-3090.                                        
002000 OBJECT-COMPUTER.        IBM-3090.                                        
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300                                                                          
002400 FILE-CONTROL.                                                            
002500*                                                                         
002600     SELECT SU-AUDIT-EXTRACT-FILE                                         
002700         ASSIGN TO OUT01.                                                 
002800*                                                                         
002900*****************************************************************         
003000 DATA DIVISION.                                                           
003100*****************************************************************         
003200*                                                                         
003300 FILE SECTION.                                                            
003400*                                                                         
003500 FD  SU-AUDIT-EXTRACT-FILE                                                
003600     RECORDING MODE IS F                                                  
003700     BLOCK CONTAINS 0  RECORDS.                                           
003800 01  SU-AUDIT-EXTRACT-RECORD         PIC  X(200).                         
003900*                                                                         
004000                                                                          
004100 WORKING-STORAGE SECTION.                                                 
004200*                                                                         
004300 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
004400                                                   COMP SYNC.             
004500*                                                                         
004600 01  PROGRAM-SWITCHES.                                                    
004700     05  PS-END-OF-AUDCTN-CURSOR-SW  PIC  X(01)    VALUE 'N'.             
004800         88  PS-END-OF-AUDCTN-CURSOR               VALUE 'Y'.             
004900         88  PS-NOT-END-AUDCTN-CURSOR              VALUE 'N'.             
005300     05  PS-END-OF-AUDIT-CURSOR-SW PIC    X(01)    VALUE 'N'.             
005400         88  PS-END-OF-AUDIT-CURSOR                VALUE 'Y'.             
005500         88  PS-NOT-END-OF-AUDIT-CURSOR            VALUE 'N'.             
005600     05  PS-FIRST-FETCH-SW         PIC    X(01)    VALUE 'N'.             
005700         88  FIRST-FETCH                           VALUE 'Y'.             
005800         88  NOT-FIRST-FETCH                       VALUE 'N'.             
005810     05  PS-NO-DETAIL-SW           PIC    X(01)    VALUE 'N'.             
005820         88  NO-DETAIL                             VALUE 'Y'.             
005830         88  DETAIL-FOUND                          VALUE 'N'.             
005840     05  PS-UPC-FOUND-SW             PIC  X(01)    VALUE 'Y'.     00871001
005850         88  PS-UPC-FOUND                          VALUE 'Y'.     00872001
005860         88  PS-UPC-NOT-FOUND                      VALUE 'N'.     00873001
005900*                                                                         
006000 01  PC-CONSTANTS.                                                        
006100     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
006200         'SUKUT014'.                                                      
006210     05  PC-E                        PIC  X(01)    VALUE 'E'.             
006300*                                                                         
006400 01  PV-MISC-FIELDS.                                                      
006500     05  PV-LINE-SUB                 PIC S9(03)    VALUE ZERO             
006600                                                   COMP-3.                
006700     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
006800                                                   COMP SYNC.             
006900     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
007000     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
007100     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
007200     05  PV-AUDIT-NBR-X              PIC  X(09).                          
007300     05  PV-AUDIT-NBR REDEFINES PV-AUDIT-NBR-X                            
007400                                     PIC  9(09).                          
007500     05  PV-UPC-COUNT                    PIC S9(09)  VALUE +0             
007600                                                     COMP-3.              
007610     05  PV-EXCEPT-CNT                   PIC S9(09)  VALUE +0             
007620                                                     COMP-3.              
007630     05  PV-UPC-NBR-DISP              PIC 9(15)  VALUE ZERO.            01
007700 EJECT                                                                    
007800*                                                                         
007900 01  DK-DB-KEYS.                                                          
008000    05  DK-SHIPT-UNT-ID             PIC S9(18) COMP-3.                    
008100    05  DK-AUD-CNTL-NBR             PIC S9(09) COMP.                      
008110    05  DK-UPC-NBR                  PIC S9(15)V COMP-3.                 1 
008200                                                                          
008300 01  CC-CARDIN.                                                           
008400     05  CC-AUDIT-CNTL-NBR           PIC 9(09).                           
008500     05  FILLER                      PIC X(62).                           
008600                                                                          
008700*                                                                         
044800 01  SU003-COPYBOOK.                                                      
044801     COPY SURD003.                                                        
044802                                                                          
044803*                                                                         
044804******************************************************************        
044805*  DB2 FORMATTED MESSAGE AREA                                             
044806******************************************************************        
044807     COPY DPWS004.                                                        
044808*                                                                         
044809*                                                                         
044810*  COPY BOOK FOR THE SHIPPING UNIT AUDIT TRANSACTION TABLE                
044811*                                                                         
044812     EXEC SQL                                                             
044813          INCLUDE TSUATRN                                                 
044814     END-EXEC.                                                            
044815                                                                          
044816*                                                                         
044817*  COPY BOOK FOR THE SHIPPING UNIT CONTENT AUDIT TABLE                    
044818*                                                                         
044819     EXEC SQL                                                             
044820          INCLUDE TSUAUPC                                                 
044821     END-EXEC.                                                            
044822                                                                          
044823*                                                                         
044824*  COPY BOOK FOR THE SHIPPING UNIT  AUDIT TABLE                           
044825*                                                                         
044826     EXEC SQL                                                             
044827          INCLUDE TSUNAUD                                                 
044828     END-EXEC.                                                            
044829 EJECT                                                                    
044830*                                                                         
044831*  COPY BOOK FOR THE SHIPPING UNIT AUDIT EXCEPTIONS                       
044832*                                                                         
044833     EXEC SQL                                                             
044834          INCLUDE TSUEXCP                                                 
044835     END-EXEC.                                                            
044836 EJECT                                                                    
044839                                                                          
018000******************************************************************        
018100*   REMOVED REFERENCES TO TUPC                        WR # 21361 *        
018100*   ADDED INCLUDE TO XLV_UPC                                     *        
018200******************************************************************        
044840     EXEC SQL                                                             
044841          INCLUDE XLV00003                                                
044842     END-EXEC.                                                            
019800******************************************************************        
019900*  CHANGES MADE FOR WR # 21361 ENDS HERE                         *        
020000******************************************************************        
044843 EJECT                                                                    
044844*  DB2 COMMUNICATION AREA                                                 
044845*                                                                         
044846     EXEC SQL                                                             
044847         INCLUDE SQLCA                                                    
044848     END-EXEC.                                                            
044849 EJECT                                                                    
044850*  DB2 COMMUNICATION AREA2                                                
044851*                                                                         
044852     COPY SQLCA2.                                                         
044853 EJECT                                                                    
044854                                                                          
044855                                                                          
044856     EXEC SQL                                                             
044857       DECLARE AUDIT-CSR CURSOR FOR                                       
044858           SELECT AUD_CNTL_NBR                                            
044859                 ,OPER_UNT_ID                                             
044860           FROM   TSUNAUD                                                 
044861           WHERE  AUD_STAT_CDE IN ('IP', 'PP')                            
044862           ORDER BY 1                                                     
044863     END-EXEC.                                                            
044864                                                                          
044865     EXEC SQL                                                             
044866       DECLARE AUDIT-CTN-CSR CURSOR FOR                                   
044867           SELECT A.AUD_CNTL_NBR                                          
044868                 ,B.SHIPT_UNT_ID                                          
044869                 ,B.TRN_NBR                                               
044870                 ,B.REC_SEQ_NBR                                           
044871                 ,B.DEST_OPER_UNT_ID                                      
044872                 ,C.UPC_NBR                                               
044873                 ,C.SCN_TMST                                              
044874           FROM   TSUNAUD A                                               
044875                 ,TSUATRN B                                               
044876                 ,TSUAUPC C                                               
044877           WHERE  A.AUD_CNTL_NBR = :SUNAUD-AUD-CNTL-NBR                   
044878             AND  A.AUD_CNTL_NBR = B.AUD_CNTL_NBR                         
044879             AND  A.AUD_CNTL_NBR = C.AUD_CNTL_NBR                         
044880             AND  B.SHIPT_UNT_ID = C.SHIPT_UNT_ID                         
044881             AND  C.SCN_TYP_CDE <> :PC-E                                  
044882           ORDER BY                                                       
044883                     A.AUD_CNTL_NBR                                       
044884                    ,B.SHIPT_UNT_ID                                       
044885                    ,B.TRN_NBR                                            
044886                    ,B.REC_SEQ_NBR                                        
044887                    ,B.DEST_OPER_UNT_ID                                   
044888                    ,C.UPC_NBR                                            
044889                    ,C.SCN_TMST                                           
044890     END-EXEC.                                                            
044891*                                                                         
044892 PROCEDURE DIVISION.                                                      
044893*                                                                         
044894 0000-MAINLINE-PROCESSING.                                                
044895******************************************************************        
044896**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
044897******************************************************************        
044898*                                                                         
044899     PERFORM 1000-INITIALIZATION.                                         
044900*                                                                         
044901     PERFORM 2000-PROCESS-AUDIT                                           
044902         UNTIL PS-END-OF-AUDIT-CURSOR.                                    
044903*                                                                         
044904     PERFORM 2400-CLOSE-AUDIT-CURSOR.                                     
044905*                                                                         
044906     GOBACK.                                                              
044907*                                                                         
044908*                                                                         
044909 1000-INITIALIZATION.                                                     
044910******************************************************************        
044911**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
044912******************************************************************        
044913                                                                          
044914     OPEN OUTPUT SU-AUDIT-EXTRACT-FILE.                                   
044915     PERFORM 2100-OPEN-AUDIT-CURSOR.                                      
044916     PERFORM 2200-FETCH-AUDIT-CURSOR.                                     
044917     PERFORM 2300-OPEN-AUDCTN-CURSOR.                                     
044918     SET FIRST-FETCH TO TRUE.                                             
044919     PERFORM 2400-FETCH-AUDCTN-CURSOR.                                    
044920                                                                          
044921*                                                                         
044922 2000-PROCESS-AUDIT.                                                      
044923                                                                          
044924     PERFORM UNTIL  PS-END-OF-AUDCTN-CURSOR                               
044925        INITIALIZE SU003-EXTRACT-RECORD                                   
044926        IF NO-DETAIL                                                      
044927           SET DETAIL-FOUND TO TRUE                                       
044928        ELSE                                                              
044929           MOVE SUATRN-SHIPT-UNT-ID   TO SU003-SHIPT-UNT-ID               
044930           MOVE SUATRN-TRN-NBR        TO SU003-PO-NBR                     
044931           MOVE SUATRN-REC-SEQ-NBR    TO SU003-REC-SEQ-NBR                
044932           MOVE SUATRN-DEST-OPER-UNT-ID                                   
044933                                      TO SU003-DEST-OPER-UNT-ID           
044934           MOVE SUAUPC-UPC-NBR        TO SU003-UPC                        
044935                                         DK-UPC-NBR                       
044936           PERFORM 3340-GET-INTR-UPC-ID                                 04
018000******************************************************************        
018100*   REPLACED THE IF CONDITON TO USE INTRA UPC ID      WR # 21361 *        
018100*   FROM XLV_UPC VIEW INSTEAD FROM TUPC.                         *        
018200******************************************************************        
044942                                                                  04894001
044937           IF  PS-UPC-FOUND                                             04
044938               MOVE XLV00003-INTR-UPC-ID  TO SU003-INTR-UPC-ID          04
044939           ELSE                                                         04
044940               MOVE SU003-UPC             TO SU003-INTR-UPC-ID          04
044941           END-IF                                                       04
019800******************************************************************        
019900*  CHANGES MADE FOR WR # 21361 ENDS HERE                         *        
020000******************************************************************        
044942                                                                  04894001
044943           MOVE SUAUPC-SCN-TMST       TO SU003-SCN-TMST                   
044944           PERFORM 2050-GET-EXCEPT-COUNT                                  
044945           IF PV-EXCEPT-CNT > 0                                           
044946              MOVE 1                  TO SU003-UNT-ERR-CNT-COMBO          
044947           END-IF                                                         
044948        END-IF                                                            
044949        MOVE SUNAUD-AUD-CNTL-NBR   TO SU003-AUD-CNTL-NBR                  
044950        MOVE SUNAUD-OPER-UNT-ID    TO SU003-AUD-OPER-UNT-ID               
044951        WRITE SU-AUDIT-EXTRACT-RECORD                                     
044952          FROM SU003-EXTRACT-RECORD                                       
044953        PERFORM 2400-FETCH-AUDCTN-CURSOR                                  
044954     END-PERFORM                                                          
044955     PERFORM 2500-CLOSE-AUDCTN-CURSOR.                                    
044956     SET PS-NOT-END-AUDCTN-CURSOR TO TRUE.                                
044957                                                                          
044958     PERFORM 2200-FETCH-AUDIT-CURSOR.                                     
044959                                                                          
044960     IF PS-END-OF-AUDIT-CURSOR                                            
044961        CONTINUE                                                          
044962     ELSE                                                                 
044963        PERFORM 2300-OPEN-AUDCTN-CURSOR                                   
044964        SET FIRST-FETCH TO TRUE                                           
044965        PERFORM 2400-FETCH-AUDCTN-CURSOR                                  
044966     END-IF.                                                              
044967*                                                                         
044968 2050-GET-EXCEPT-COUNT.                                                   
044969     MOVE '2050-GET-EXCEPT-COUNT    '                                     
044970                                 TO PV-ABEND-PARAGRAPH.                   
044971     EXEC SQL                                                             
044972         SELECT COUNT(*)                                                  
044973           INTO :PV-EXCEPT-CNT                                            
044974           FROM TSUEXCP                                                   
044975          WHERE AUD_CNTL_NBR = :SUNAUD-AUD-CNTL-NBR                       
044976            AND SHIPT_UNT_ID = :SUATRN-SHIPT-UNT-ID                       
044977            AND UPC_NBR      = :SUAUPC-UPC-NBR                            
044978            AND SCN_TMST     = :SUAUPC-SCN-TMST                           
044979     END-EXEC.                                                            
044980                                                                          
044981     EVALUATE TRUE                                                        
044982         WHEN SQLCODE = 0                                                 
044983              CONTINUE                                                    
044984         WHEN SQLCODE = +100                                              
044985              MOVE ZEROES TO PV-EXCEPT-CNT                                
044986         WHEN SQLWARN0 NOT = SPACES                                       
044987         WHEN SQLCODE < ZERO                                              
044988         WHEN SQLCODE > ZERO                                              
044989             MOVE '2100-OPEN-AUDIT-CURSOR         '                       
044990                             TO PV-PARAGRAPH-NAME                         
044991             MOVE 'OPEN THE AUDIT_CURSOR         '                        
044992                             TO PV-DB2-OPERATION-ATTEMPTED                
044993             DISPLAY 'AUD CNTL NBR: ' SUNAUD-AUD-CNTL-NBR                 
044994             DISPLAY 'SHIP UNIT ID: ' SUATRN-SHIPT-UNT-ID                 
044995             DISPLAY 'UPC NBR:      ' SUAUPC-UPC-NBR                      
044996             DISPLAY 'SCN TMST:     ' SUAUPC-SCN-TMST                     
044997             PERFORM 9100-SQL-ABEND                                       
044998     END-EVALUATE.                                                        
044999                                                                          
045000 2100-OPEN-AUDIT-CURSOR.                                                  
045001     MOVE '2100-OPEN-AUDIT-CURSOR   '                                     
045002                                 TO PV-ABEND-PARAGRAPH.                   
045003     EXEC SQL                                                             
045004         OPEN AUDIT-CSR                                                   
045005     END-EXEC.                                                            
045006     EVALUATE TRUE                                                        
045007         WHEN SQLCODE = -904                                              
045008         WHEN SQLCODE = -911                                              
045009             CONTINUE                                                     
045010         WHEN SQLWARN0 NOT = SPACES                                       
045011         WHEN SQLCODE < ZERO                                              
045012         WHEN SQLCODE > ZERO                                              
045013             MOVE '2100-OPEN-AUDIT-CURSOR         '                       
045014                             TO PV-PARAGRAPH-NAME                         
045015             MOVE 'OPEN THE AUDIT_CURSOR         '                        
045016                             TO PV-DB2-OPERATION-ATTEMPTED                
045017             PERFORM 9100-SQL-ABEND                                       
045018     END-EVALUATE.                                                        
045019                                                                          
045020 2200-FETCH-AUDIT-CURSOR.                                                 
045021     MOVE '2200-FETCH-AUDIT-CURSOR   '                                    
045022                                 TO PV-ABEND-PARAGRAPH.                   
045023     EXEC SQL                                                             
045024          FETCH AUDIT-CSR                                                 
045025            INTO  :SUNAUD-AUD-CNTL-NBR                                    
045026                 ,:SUNAUD-OPER-UNT-ID                                     
045027     END-EXEC.                                                            
045028                                                                          
045029     EVALUATE TRUE                                                        
045030       WHEN SQLCODE = ZEROS                                               
045031         CONTINUE                                                         
045032       WHEN SQLCODE = +100                                                
045033           SET PS-END-OF-AUDIT-CURSOR TO TRUE                             
045034       WHEN SQLWARN0 NOT = SPACES                                         
045035       WHEN SQLCODE < ZERO                                                
045036       WHEN SQLCODE > ZERO                                                
045037           MOVE '2200-FETCH-AUDIT-CURSOR'                                 
045038                               TO PV-PARAGRAPH-NAME                       
045039           MOVE 'FETCH AUDIT CURSOR '                                     
045040                               TO PV-DB2-OPERATION-ATTEMPTED              
045041           PERFORM 9100-SQL-ABEND                                         
045042     END-EVALUATE.                                                        
045043                                                                          
045044 2400-CLOSE-AUDIT-CURSOR.                                                 
045045     MOVE '2400-CLOSE-AUDIT-CURSOR   '                                    
045046                                 TO PV-ABEND-PARAGRAPH.                   
045047     EXEC SQL                                                             
045048         CLOSE AUDIT-CSR                                                  
045049     END-EXEC.                                                            
045050     EVALUATE TRUE                                                        
045051         WHEN SQLCODE = -904                                              
045052         WHEN SQLCODE = -911                                              
045053             CONTINUE                                                     
045054         WHEN SQLWARN0 NOT = SPACES                                       
045055         WHEN SQLCODE < ZERO                                              
045056         WHEN SQLCODE > ZERO                                              
045057             MOVE '2400-CLOSE-CURSOR '                                    
045058                             TO PV-PARAGRAPH-NAME                         
045059             MOVE 'CLOSE THE CARTON_CURSOR        '                       
045060                             TO PV-DB2-OPERATION-ATTEMPTED                
045061             PERFORM 9100-SQL-ABEND                                       
045062          WHEN SQLCODE = ZERO                                             
045063              CONTINUE                                                    
045064     END-EVALUATE.                                                        
045065*                                                                         
045066 2300-OPEN-AUDCTN-CURSOR.                                                 
045067     MOVE '2300-OPEN-AUDCTN-CURSOR  '                                     
045068                                 TO PV-ABEND-PARAGRAPH.                   
045069     EXEC SQL                                                             
045070         OPEN AUDIT-CTN-CSR                                               
045071     END-EXEC.                                                            
045072     EVALUATE TRUE                                                        
045073         WHEN SQLCODE = -904                                              
045074         WHEN SQLCODE = -911                                              
045075             CONTINUE                                                     
045076         WHEN SQLWARN0 NOT = SPACES                                       
045077         WHEN SQLCODE < ZERO                                              
045078         WHEN SQLCODE > ZERO                                              
045079             MOVE '2300-OPEN-AUDCTN-CURSOR        '                       
045080                             TO PV-PARAGRAPH-NAME                         
045081             MOVE 'OPEN THE AUDIT-CTN-CSR        '                        
045082                             TO PV-DB2-OPERATION-ATTEMPTED                
045083             DISPLAY 'AUD CNTL NBR  ' SUNAUD-AUD-CNTL-NBR                 
045084             PERFORM 9100-SQL-ABEND                                       
045085     END-EVALUATE.                                                        
045086                                                                          
045087 2400-FETCH-AUDCTN-CURSOR.                                                
045088     MOVE '2400-FETCH-AUDCTN-CURSOR  '                                    
045089                                 TO PV-ABEND-PARAGRAPH.                   
045090     EXEC SQL                                                             
045091          FETCH AUDIT-CTN-CSR                                             
045092            INTO  :SUNAUD-AUD-CNTL-NBR                                    
045093                 ,:SUATRN-SHIPT-UNT-ID                                    
045094                 ,:SUATRN-TRN-NBR                                         
045095                 ,:SUATRN-REC-SEQ-NBR                                     
045096                 ,:SUATRN-DEST-OPER-UNT-ID                                
045097                 ,:SUAUPC-UPC-NBR                                         
045098                 ,:SUAUPC-SCN-TMST                                        
045099     END-EXEC.                                                            
045100                                                                          
045101     EVALUATE TRUE                                                        
045102       WHEN SQLCODE = ZEROS                                               
045103           SET NOT-FIRST-FETCH TO TRUE                                    
045104       WHEN SQLCODE = +100                                                
045105           IF FIRST-FETCH                                                 
045106              SET NO-DETAIL TO TRUE                                       
045107              SET NOT-FIRST-FETCH TO TRUE                                 
045108           ELSE                                                           
045109              SET PS-END-OF-AUDCTN-CURSOR TO TRUE                         
045110           END-IF                                                         
045111       WHEN SQLWARN0 NOT = SPACES                                         
045112       WHEN SQLCODE < ZERO                                                
045113       WHEN SQLCODE > ZERO                                                
045114           MOVE '2400-FETCH-AUDCTN-CURSOR'                                
045115                               TO PV-PARAGRAPH-NAME                       
045116           MOVE 'FETCH AUDIT CTN CURSOR'                                  
045117                               TO PV-DB2-OPERATION-ATTEMPTED              
045118           PERFORM 9100-SQL-ABEND                                         
045119     END-EVALUATE.                                                        
045120                                                                          
045121 2500-CLOSE-AUDCTN-CURSOR.                                                
045122     MOVE '2500-CLOSE-AUDCTN-CURSOR  '                                    
045123                                 TO PV-ABEND-PARAGRAPH.                   
045124     EXEC SQL                                                             
045125         CLOSE AUDIT-CTN-CSR                                              
045126     END-EXEC.                                                            
045127     EVALUATE TRUE                                                        
045128         WHEN SQLCODE = -904                                              
045129         WHEN SQLCODE = -911                                              
045130             CONTINUE                                                     
045131         WHEN SQLWARN0 NOT = SPACES                                       
045132         WHEN SQLCODE < ZERO                                              
045133         WHEN SQLCODE > ZERO                                              
045134             MOVE '2500-CLOSE-CURSOR '                                    
045135                             TO PV-PARAGRAPH-NAME                         
045136             MOVE 'CLOSE THE CARTON_CURSOR        '                       
045137                             TO PV-DB2-OPERATION-ATTEMPTED                
045138             PERFORM 9100-SQL-ABEND                                       
045139          WHEN SQLCODE = ZERO                                             
045140              CONTINUE                                                    
045141     END-EVALUATE.                                                        
045142*                                                                 06509901
045143*  FOR NON PACK-BY-STORE ORDERS, THIS CURSOR WILL GO GET THE      06510001
045144*  DEST OPER UNIT ID FROM THE TSHPUNT TABLE.  THIS WAS ADDED      06511001
045145*  BECAUSE WHEN THE UCC-128 AUDIT ENHANCEMENTS WERE DONE IN       06511101
045146*  MARCH OF 2002, A CHANGE WAS MADE TO BRING FORWARD THE STORE    06511201
045147*  NUMBER FROM THE CROSS REFERENCE TABLE TO THE AUDIT APPLICATION.06511301
045148*  THE STORE NUMBER WAS THEN USED THROUGH THE EXISTING REPORTING  06511401
045149*  INSTEAD OF THE DESTINATION OPER UNIT ID AS IT DID PRIOR TO THE 06511501
045150*  CHANGES, THIS CAUSED RECEIVER LEVEL OVER AND SHORT PROBLEMS    06511601
045151*  WHEN IT SHOULD NOT HAVE.                                       06511701
045152*                                                                 06511801
018900******************************************************************        
019000*   REPLACED TUPC WITH XLV_UPC IN THE SQL             WR # 21361 *        
019100******************************************************************        
045153 3340-GET-INTR-UPC-ID.                                            06511901
045154                                                                  06512001
045155     SET  PS-UPC-NOT-FOUND          TO TRUE.                      06512101
045156     MOVE '3340-GET-INTR-UPC-ID'                                  06512201
045157                                 TO PV-ABEND-PARAGRAPH.           06512301
045158     EXEC SQL                                                     06512401
045159         SELECT INTR_UPC_ID                                       06512501
045160           INTO :XLV00003-INTR-UPC-ID                             06512601
045161           FROM XLV_UPC                                           06512701
045162          WHERE UPC_NBR      = :DK-UPC-NBR                        06512801
045164     END-EXEC.                                                    06513001
045165                                                                  06513101
045166     EVALUATE TRUE                                                06513201
045167         WHEN SQLCODE = ZEROS                                     06513301
045168           SET PS-UPC-FOUND TO TRUE                               06513401
045169         WHEN SQLCODE = +100                                      06513501
045170           CONTINUE                                               06513601
045171         WHEN SQLWARN0 NOT = SPACES                               06513701
045172         WHEN SQLCODE < ZERO                                      06513801
045173         WHEN SQLCODE > ZERO                                      06513901
045174             MOVE '3340-GET-INTR-UPC-ID'                          06514001
045175                             TO PV-PARAGRAPH-NAME                 06514101
045176             MOVE DK-UPC-NBR TO PV-UPC-NBR-DISP                   06514201
045177             DISPLAY 'UPC NUMBER: ' PV-UPC-NBR-DISP               06514301
045178             PERFORM 9100-SQL-ABEND                               06514401
045179     END-EVALUATE.                                                06514501
045180                                                                  06514601
019800******************************************************************        
019900*  CHANGES MADE FOR WR # 21361 ENDS HERE                         *        
020000******************************************************************        
045181 9000-EOJ-ROUTINE.                                                        
045182                                                                          
045183     CLOSE SU-AUDIT-EXTRACT-FILE.                                         
045184                                                                          
045185                                                                          
045186 9100-SQL-ABEND.                                                          
045187******************************************************************        
045188*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
045189*  ERROR OR WARNING CODE OCCURS.                                          
045190******************************************************************        
045191     DISPLAY '9100-SQL-ABEND'.                                            
045192     DISPLAY '** ABEND     **'.                                           
045193     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
045194     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
045195     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
045196                                                                          
045197******************************************************************        
045198* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
045199* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
045200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
045201* FORMAT.                                                                 
045202******************************************************************        
045203     COPY DPPD004.                                                        
045204                                                                          
045210     MOVE +4013                  TO ABEND-CODE.                           
045300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
