       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    FSKUP865.                                                 
       AUTHOR.        COGNIZANT.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01-11-2013.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *          FSKUP865 - YEAR TO DATE FIELD INITIALIZE               *       
      ******************************************************************        
      *                                                                *        
      *   THIS PROGRAM INITIALIZES THE YEAR TO DATE FIELDS PRESENT IN  *        
      *   TSKST TABLE. THIS PROGRAM IS INVOKED FROM FSK865 WHICH IS AN *        
      *   YEARLY JOB AND RUNS ON FIRST DAY OF THE FISCAL YEAR.         *        
      *                                                                *        
      *   INPUT FILES:    NONE                                         *        
      *                                                                *        
      *   OUTPUT FILES:   NONE                                         *        
      *                                                                *        
      *   I/O:            TSKST    DB2 TABLE   SELECT/UPDATE           *        
      *                                                                *        
      *   GENERAL LOGIC DESCRIPTION:                                   *        
      *                                                                *        
      *   THIS PROGRAM RETRIEVES RECORDS FROM TSKST IF THE             *        
      *   YTD_SALES_UNIT_QTY, YTD_SALES_AMT FIELDS ARE NON ZERO. IT    *        
      *   INITIALIZES THE VALUE FOR BOTH THE FIELDS AND UPDATES THE    *        
      *   THE TABLE.                                                   *        
      *                                                                *        
      ******************************************************************        
      * CHANGE HISTORY:                                                *        
      * ---------------                                                *        
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES          *        
      * ------- ---------- ----------- --------------------------------*        
      * PK10122 01-11-2013 COGNIZANT   ORIGINAL IMPLEMENTATION         *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
       01  ABEND-CODE                       PIC S9(04)  VALUE ZERO              
                                                        COMP                    
                                                        SYNC.                   
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TABLE-SW           PIC X(01)   VALUE 'N'.              
               88  PS-END-OF-TABLE                      VALUE 'Y'.              
           05  PS-FIRST-COMMIT-SW           PIC X(01)   VALUE 'N'.              
               88  PS-FIRST-COMMIT                      VALUE 'Y'.              
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME                  PIC X(06)   VALUE 'FSK865'.         
           05  PC-PROGRAM-NAME              PIC X(08)   VALUE                   
                                                             'FSKUP865'.        
           05  PV-CURRENT-TIMESTAMP         PIC X(26)   VALUE SPACES.           
           05  PV-COMMIT-TIMESTAMP          PIC X(26)   VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC X(35)   VALUE SPACES.           
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(35)   VALUE SPACES.           
           05  PV-TSKST-FETCH-CNT           PIC S9(15)  VALUE +0  COMP.         
           05  PV-COMMIT-CNT                PIC S9(15)  VALUE +0  COMP.         
           05  PV-TSKST-UPDATE-CNT          PIC S9(15)  VALUE +0  COMP.         
           05  PV-TSKST-UPDATE-CNT-DIS      PIC S9(15)  VALUE +0  COMP.         
           05  PV-ITEM-NMBR                 PIC S9(15)V COMP-3 VALUE 0.         
           05  PV-LOC-NBR                   PIC S9(4)   COMP VALUE 0.           
           05  PV-ITM-NMBR                  PIC 9(15)   VALUE 0.                
           05  PV-LOC-NMBR                  PIC 9(4)    VALUE 0.                
           05  PV-ITM-NMBR-DIS              PIC 9(15)   VALUE 0.                
           05  PV-LOC-NMBR-DIS              PIC 9(4)    VALUE 0.                
           05  PV-UPD-COUNT                 PIC 9(4)    VALUE 0.                
      *                                                                         
       01  PV-RECORD-VAR.                                                       
           05  PV-TSKST-ITEM-NMBR           PIC  S9(15)V COMP-3                 
                                            OCCURS 100 TIMES.                   
           05  PV-TSKST-LOC-NBR             PIC  S9(4) COMP                     
                                            OCCURS 100 TIMES.                   
           05  PV-TSKST-YTD-SALES-UNIT-QTY  PIC  S9(7)V COMP-3                  
                                            OCCURS 100 TIMES.                   
           05  PV-TSKST-YTD-SALES-AMT       PIC  S9(7)V9(2) COMP-3              
                                            OCCURS 100 TIMES.                   
                                                                                
      ******************************************************************        
      *  COMMUNICATION AREAS FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ******************************************************************        
      *  SQL ERROR AREA                                                *        
      ******************************************************************        
           COPY SQLCA2.                                                         
      ******************************************************************        
      *  TSKST TABLE                                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
      ******************************************************************        
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES               *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04) VALUE +34  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-PREV-DTL-KEY.                                             
                   15 CR-ITEM-NMBR          PIC S9(15)V COMP-3 VALUE 0.         
                   15 CR-LOC-NBR            PIC S9(4)  COMP VALUE 0.            
                   15 CR-TSKST-FETCH-CNT    PIC S9(15) VALUE +0  COMP.          
                   15 CR-COMMIT-CNT         PIC S9(15) VALUE +0  COMP.          
                   15 CR-TSKST-UPDATE-CNT   PIC S9(15) VALUE +0  COMP.          
           EJECT                                                                
      *                                                                         
      ******************************************************************        
      *  ERROR MESSAGE AREA FOR DB2                                    *        
      ******************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
           EXEC SQL                                                             
               DECLARE TSKST_CSR CURSOR WITH ROWSET POSITIONING                 
                                        WITH HOLD FOR                           
                SELECT ITEM_NMBR                                                
                     , LOC_NBR                                                  
                     , YTD_SALES_UNIT_QTY                                       
                     , YTD_SALES_AMT                                            
                 FROM  TSKST                                                    
                 WHERE                                                          
                      (YTD_SALES_UNIT_QTY <> 0  OR                              
                       YTD_SALES_AMT      <> 0) AND                             
                       ITEM_NMBR          >= :PV-ITEM-NMBR                      
                   FOR UPDATE OF YTD_SALES_UNIT_QTY, YTD_SALES_AMT              
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-TSKST                                           
             UNTIL PS-END-OF-TABLE.                                             
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      * DETERMINE IF THIS IS A 'RESTART' (TCKRSTINF-CKPT-STAT-CODE = 9)*        
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
           PERFORM 8000-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      ******************************************************************        
      *  IF TH  PROGRAM IS IN A RESTART STATUS, SELECT SAVED DATA      *        
      *  FROM THE RESTART INFO TABLE.                                  *        
      ******************************************************************        
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
               PERFORM 8100-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC                                    
                                     TO CR-CHECKPOINT-RESTART-AREA              
               MOVE CR-ITEM-NMBR     TO PV-ITEM-NMBR                            
                                        PV-ITM-NMBR                             
               MOVE CR-LOC-NBR       TO PV-LOC-NBR                              
                                        PV-LOC-NMBR                             
               DISPLAY SPACES                                                   
               DISPLAY '** FSKUP865 IS BEING RESTARTED **'                      
               DISPLAY 'LAST UPDATED RECORD'                                    
               DISPLAY 'ITEM NUMBER: '  PV-ITM-NMBR                             
               DISPLAY 'LOC NUMBER:  '  PV-LOC-NMBR                             
               DISPLAY 'NUMBER OF RECS UPDATED BEFORE PREVIOUS COMMIT:'         
                                        CR-TSKST-UPDATE-CNT                     
               DISPLAY '*********************************'                      
               ADD CR-TSKST-UPDATE-CNT                                          
                                     TO PV-TSKST-FETCH-CNT                      
               ADD CR-TSKST-UPDATE-CNT                                          
                                     TO PV-TSKST-UPDATE-CNT                     
           ELSE                                                                 
               MOVE ZEROS            TO PV-ITEM-NMBR                            
               MOVE ZEROS            TO PV-LOC-NBR                              
               SET  PS-FIRST-COMMIT  TO TRUE                                    
           END-IF.                                                              
                                                                                
           PERFORM 8400-GET-COMMIT-TIMESTAMP.                                   
      *                                                                         
      ******************************************************************        
      * 2000-PROCESS-TSKST.                                            *        
      * SELECTS RECORD FROM TSKST TABLE                                *        
      ******************************************************************        
      *                                                                         
       2000-PROCESS-TSKST.                                                      
                                                                                
           PERFORM 2100-OPEN-TSK-CSR                                            
           PERFORM 2200-FETCH-TSKST-CSR                                         
             UNTIL PS-END-OF-TABLE                                              
           PERFORM 2300-CLOSE-TSK-CSR.                                          
      *                                                                         
      ******************************************************************        
      * 2100-OPEN-TSK-CSR.                                             *        
      * OPENS TSKST_CSR CURSOR                                         *        
      ******************************************************************        
      *                                                                         
       2100-OPEN-TSK-CSR.                                                       
           MOVE '2100-OPEN-TSK-CSR.' TO PV-PARAGRAPH-NAME.                      
                                                                                
           EXEC SQL                                                             
               OPEN TSKST_CSR                                                   
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE     = 0                                              
              WHEN SQLCODE     = -904                                           
                   CONTINUE                                                     
              WHEN SQLWARN NOT = SPACES                                         
              WHEN SQLCODE     = -911                                           
              WHEN SQLCODE NOT = ZERO                                           
                   MOVE '2100-OPEN-TSK-CSR.'                                    
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN OPERATION'                                        
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 2200-FETCH-TSKST-CSR.                                          *        
      * FETCHES RECORD FROM TSKST TABLE                                *        
      ******************************************************************        
      *                                                                         
       2200-FETCH-TSKST-CSR.                                                    
           MOVE '2200-FETCH-TSKST-CSR.' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
               FETCH NEXT ROWSET FROM TSKST_CSR                                 
                     FOR 100 ROWS                                               
                INTO :PV-TSKST-ITEM-NMBR                                        
                   , :PV-TSKST-LOC-NBR                                          
                   , :PV-TSKST-YTD-SALES-UNIT-QTY                               
                   , :PV-TSKST-YTD-SALES-AMT                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE     = 0                                              
                   ADD SQLERRD(3)    TO PV-TSKST-FETCH-CNT                      
                   IF PV-TSKST-FETCH-CNT <= 100                                 
                      DISPLAY '**** STARTING RECORD UPDATE *****'               
                      DISPLAY 'ITEM NUMBER: '                                   
                                        PV-TSKST-ITEM-NMBR(1)                   
                      DISPLAY 'LOC NUMBER:  '                                   
                                        PV-TSKST-LOC-NBR(1)                     
                      DISPLAY '*********************************'               
                   END-IF                                                       
                   PERFORM 3000-UPDATE-RECORDS                                  
              WHEN SQLCODE     = +100                                           
                   IF SQLERRD(3) >= 1                                           
                      ADD SQLERRD(3) TO PV-TSKST-FETCH-CNT                      
                      PERFORM 3000-UPDATE-RECORDS                               
                   END-IF                                                       
                   SET PS-END-OF-TABLE                                          
                                     TO TRUE                                    
              WHEN SQLWARN NOT = SPACES                                         
              WHEN SQLCODE     = -911                                           
              WHEN SQLCODE NOT = ZERO                                           
                   MOVE '2200-FETCH-TSKST-CSR    '                              
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH RECS FROM TSKST'                                 
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 2100-OPEN-TSK-CSR.                                             *        
      * CLOSE TSKST_CSR CURSOR                                         *        
      ******************************************************************        
      *                                                                         
       2300-CLOSE-TSK-CSR.                                                      
           MOVE '2300-CLOSE-TSK-CSR.' TO PV-PARAGRAPH-NAME.                     
                                                                                
           EXEC SQL                                                             
               CLOSE TSKST_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE     = 0                                              
              WHEN SQLCODE     = -904                                           
                   CONTINUE                                                     
              WHEN SQLWARN NOT = SPACES                                         
              WHEN SQLCODE     = -911                                           
              WHEN SQLCODE NOT = ZERO                                           
                   MOVE '2300-CLOSE-TSK-CSR. '                                  
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'CLOSE OPERATION'                                       
                                      TO PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 3000-UPDATE-RECORDS.                                           *        
      * SETS YTD_SALES_UNIT_QTY AND YTD_SALES_AMT AS 0                 *        
      ******************************************************************        
      *                                                                         
       3000-UPDATE-RECORDS.                                                     
                                                                                
           EXEC SQL                                                             
               UPDATE TSKST                                                     
                  SET YTD_SALES_UNIT_QTY = 0                                    
                    , YTD_SALES_AMT      = 0                                    
                WHERE CURRENT OF TSKST_CSR                                      
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE     = 0                                              
                    ADD SQLERRD(3)   TO PV-TSKST-UPDATE-CNT                     
                                        PV-TSKST-UPDATE-CNT-DIS                 
                    IF PV-TSKST-UPDATE-CNT-DIS >= 1000000                       
                       DISPLAY PV-TSKST-UPDATE-CNT ' RECORDS UPDATED'           
                       INITIALIZE PV-TSKST-UPDATE-CNT-DIS                       
                    END-IF                                                      
                   CONTINUE                                                     
              WHEN SQLCODE     = -904                                           
              WHEN SQLWARN NOT = SPACES                                         
              WHEN SQLCODE     = -911                                           
              WHEN SQLCODE NOT = ZERO                                           
                   MOVE '3000-UPDATE-RECORDS '                                  
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'UPDATE YTD FIELDS IN TSKST'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE                                                         
                                                                                
           MOVE SQLERRD(3)           TO PV-UPD-COUNT                            
           MOVE PV-TSKST-ITEM-NMBR(PV-UPD-COUNT)   TO CR-ITEM-NMBR              
           MOVE PV-TSKST-LOC-NBR(PV-UPD-COUNT)     TO CR-LOC-NBR                
           MOVE PV-TSKST-FETCH-CNT   TO CR-TSKST-FETCH-CNT                      
           MOVE PV-COMMIT-CNT        TO CR-COMMIT-CNT                           
           MOVE PV-TSKST-UPDATE-CNT  TO CR-TSKST-UPDATE-CNT                     
                                                                                
           PERFORM 8200-COMMIT-PROCESSING.                                      
      *                                                                         
      ******************************************************************        
      * 8000-INIT-CHECKPOINT-SELECT.                                   *        
      * SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF     *        
      * THE PROGRAM IS IN A RESTART CONDITION.                         *        
      ******************************************************************        
      *                                                                         
       8000-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      CKPT_STAT_CODE                                            
                 INTO                                                           
                     :TCKRSTCNTL-CKPT-STAT-CODE                                 
                 FROM TCKRSTCNTL                                                
                WHERE JOB_NAME  = :PC-JOB-NAME                                  
                  AND PLAN_NAME = :PC-PROGRAM-NAME                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE    = ZERO                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE '8000-INIT-CHECKPOINT-SELECT '                         
                                     TO  PV-PARAGRAPH-NAME                      
                    MOVE 'ERROR ON SELECT OF TCKRSTCNTL '                       
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 8100-SELECT-RESTART-INFO.                                      *        
      * SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO        *        
      * OBTAIN THE PROGRAMS SAVED VARIABLES.                           *        
      ******************************************************************        
      *                                                                         
       8100-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                    WORK_STRG_DESC                                              
               INTO                                                             
                   :TCKRSTINF-WORK-STRG-DESC                                    
               FROM TCKRSTINF                                                   
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE    = ZERO                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE '8100-SELECT-RESTART-INFO '                            
                                     TO  PV-PARAGRAPH-NAME                      
                    MOVE 'ERROR ON SELECT OF TCKRSTINF '                        
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
           EJECT                                                                
      *                                                                         
      ******************************************************************        
      * 8200-COMMIT-PROCESSING.                                        *        
      * CHECK IF WE NEED TO TAKE A COMMIT.  IF SO, COMMIT THIS UNIT    *        
      * OF WORK.                                                       *        
      ******************************************************************        
      *                                                                         
       8200-COMMIT-PROCESSING.                                                  
                                                                                
           PERFORM 8500-SET-CURRENT-TIMESTAMP.                                  
                                                                                
           IF PV-CURRENT-TIMESTAMP   > PV-COMMIT-TIMESTAMP                      
               IF PS-FIRST-COMMIT                                               
                   MOVE '9'          TO TCKRSTCNTL-CKPT-STAT-CODE               
                   PERFORM 8300-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N'          TO PS-FIRST-COMMIT-SW                      
               END-IF                                                           
               PERFORM 8600-COMMIT                                              
               PERFORM 8400-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 8300-UPDATE-CHECKPOINT-STATUS.                                 *        
      * UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE              *        
      ******************************************************************        
      *                                                                         
       8300-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTCNTL                                                  
                SET CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                 
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE    = ZERO                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE '8300-UPDATE-CHECKPOINT-STATUS '                       
                                     TO  PV-PARAGRAPH-NAME                      
                    MOVE 'ERROR ON UPDATE OF TCKRSTCNTL '                       
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 8400-GET-COMMIT-TIMESTAMP.                                     *        
      * GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL TABLE     *        
      ******************************************************************        
      *                                                                         
       8400-GET-COMMIT-TIMESTAMP.                                               
                                                                                
           EXEC SQL                                                             
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE          = 0                                              
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '8400-GET-COMMIT-TIMESTAMP '                                 
                                     TO  PV-PARAGRAPH-NAME                      
              MOVE 'ERROR ON SELECT OF TIMESTAMP '                              
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 8500-SET-CURRENT-TIMESTAMP.                                    *        
      * GET THE CURRENT TIMESTAMP FROM DB2                             *        
      ******************************************************************        
      *                                                                         
       8500-SET-CURRENT-TIMESTAMP.                                              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           IF SQLCODE          = 0                                              
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '8500-SET-CURRENT-TIMESTAMP '                                
                                     TO  PV-PARAGRAPH-NAME                      
              MOVE 'ERROR ON SET OF TIMESTAMP '                                 
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 8600-COMMIT.                                                   *        
      * PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.                *        
      * TAKE A COMMIT.                                                 *        
      ******************************************************************        
      *                                                                         
       8600-COMMIT.                                                             
                                                                                
           MOVE CR-CHECKPOINT-RESTART-AREA                                      
                                     TO TCKRSTINF-WORK-STRG-DESC                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE     = 0                                                   
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '8600-COMMIT '                                               
                                     TO  PV-PARAGRAPH-NAME                      
              MOVE 'ERROR ON UPDATE OF TCKRSTINF '                              
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE     = 0                                                   
               ADD +1                TO PV-COMMIT-CNT                           
           ELSE                                                                 
               MOVE '8600-COMMIT '                                              
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'ERROR ON COMMIT '                                          
                                     TO  PV-DB2-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
           EJECT                                                                
      *                                                                         
      ******************************************************************        
      * 9000-EOJ-ROUTINE.                                              *        
      * RESET STATUS CODE, DISPLAY FINAL COUNTS                        *        
      ******************************************************************        
      *                                                                         
       9000-EOJ-ROUTINE.                                                        
      ******************************************************************        
      *    COMMIT ANY FINAL CHANGES THAT MAY HAVE BEEN MADE            *        
      ******************************************************************        
                                                                                
           PERFORM 8200-COMMIT-PROCESSING                                       
                                                                                
           MOVE '0'                  TO TCKRSTCNTL-CKPT-STAT-CODE.              
           PERFORM 8300-UPDATE-CHECKPOINT-STATUS.                               
      *                                                                         
           DISPLAY '                                             '.             
           DISPLAY '******* END OF JOB STATISTICS ********'.                    
           DISPLAY '                                             '.             
           DISPLAY 'TSKST RECORDS READ     :'                                   
                                        PV-TSKST-FETCH-CNT.                     
           DISPLAY 'TSKST RECORDS UPDATED  :'                                   
                                        PV-TSKST-UPDATE-CNT.                    
           DISPLAY '                                             '.             
           DISPLAY 'COMMITS TAKEN          :'                                   
                                        PV-COMMIT-CNT.                          
           DISPLAY '                                             '.             
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
      *                                                                         
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE     *        
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                *        
      ******************************************************************        
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY '******************'                                         
           DISPLAY 'LAST UPDATED RECORD'                                        
           MOVE CR-ITEM-NMBR  TO PV-ITM-NMBR-DIS                                
           MOVE CR-LOC-NBR    TO PV-LOC-NMBR-DIS                                
           DISPLAY 'ITEM NUMBER: '  PV-ITM-NMBR-DIS                             
           DISPLAY 'LOC NUMBER:  '  PV-LOC-NMBR-DIS                             
           DISPLAY 'TSKST RECORDS READ     :'                                   
                                        PV-TSKST-FETCH-CNT.                     
           DISPLAY 'TSKST RECORDS UPDATED  :'                                   
                                        PV-TSKST-UPDATE-CNT.                    
           DISPLAY '                                             '.             
           DISPLAY 'COMMITS TAKEN          :'                                   
                                        PV-COMMIT-CNT.                          
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
