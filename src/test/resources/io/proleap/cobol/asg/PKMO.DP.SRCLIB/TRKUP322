000100*************************                                         00010006
000200 IDENTIFICATION DIVISION.                                         00020006
000300*************************                                         00030006
000400                                                                  00040006
000500 PROGRAM-ID.      TRKUP322.                                       00050006
000600 AUTHOR.          ABRAHAM IVERSON.                                00060006
000700 DATE-WRITTEN.    09/02/03.                                       00070006
000800                                                                  00080006
000900******************************************************************00090006
001000* CHANGE TIMCNUP ROWS FOR CONTAINER/POS SENT IN THE OLD FORMAT,   00100006
001100* ALL COMPONENT UPCS.  CHANGE THEM TO BE IN THE NEW FORMAT, ONE           
001200* UPC FOR EACH CONTAINER/PO/PREPACK.  THIS WILL SAVE OUR USERS            
001300* TIME IN SENDING ALL THESE PO'S AS PO LEVEL SPLITS AND WILL SAVE         
001400* OUR DECONSOLIDATOR FROM HAVING TO PROCESS THE MERCHANDISE AS PO         
001500* SPLITS.  IT WILL RESULT IN THE MERCHANDISE MOVING MUCH FASTER           
001600* AND MORE ACCURATELY.                                                    
001700*                                                                         
001800* THIS IS A TEMPORARY PROGRAM AND WON'T BE NEEDED AFTER ANY DATA          
001900* SENT BY THE CONSOLIDATORS IN THE OLD FORMAT, COMPONENT UPCS HAS         
002000* BEEN PROCESSED AND DC SPLITS HAVE BEEN SENT.  THE TIME FRAME            
002100* WILL LIKELY EXTEND TO 1/1/2004 BECAUSE FILLING OF QUOTA                 
002200* CATEGORIES CAUSED THE PREMATURE SHIPPING OF MERCHANDISE NEEDED          
002300* DURING THE FALL/WINTER OF 2003.                                         
002400******************************************************************00150006
002401*  11/05/2003 TKMA643 - VISWA KARUTURI                           *   CL**1
002402*     REMOVED LOGIC THAT PERTAINS TO COLUMNS FRGT_CHRG_AMT,      *   CL**1
002403*     CONSLR_CHRG_AMT,INV_1ST_COST_AMT AND LBL_IND.              *   CL**1
002404******************************************************************00150006
002500                                                                  00160006
002600 ENVIRONMENT DIVISION.                                            00170006
002700 CONFIGURATION SECTION.                                           00180006
002800                                                                  00190006
002900 SOURCE-COMPUTER. IBM-3090.                                       00200006
003000 OBJECT-COMPUTER. IBM-3090.                                       00210006
003100                                                                  00220006
003200 INPUT-OUTPUT SECTION.                                            00230006
003300 FILE-CONTROL.                                                    00240006
003400                                                                  00250006
004800***************                                                   00310006
004900 DATA DIVISION.                                                   00320006
005000***************                                                   00330006
005100 FILE SECTION.                                                    00340006
007510************************                                          00510006
007520 WORKING-STORAGE SECTION.                                         00520006
007530************************                                          00530006
007540                                                                  00540006
013000 01  PS-PROGRAM-SWITCHES.                                         00550006
019300     05  PS-PACK-UPC-MATCH-FOUND-SW       PIC X(01)  VALUE 'N'.   00580006
019400         88  PS-PACK-UPC-MATCH-FOUND                 VALUE 'Y'.   00590006
019500         88  PS-PACK-UPC-MATCH-FOUND-NO              VALUE 'N'.   00600006
019501     05  PS-END-OF-PREPACK-CUR-SW         PIC X(01)  VALUE 'N'.           
019502         88  PS-END-OF-PREPACK-CUR                   VALUE 'Y'.           
019503         88  PS-END-OF-PREPACK-CUR-NO                VALUE 'N'.           
019504     05  PS-END-OF-COMPONENT-CUR-SW       PIC X(01)  VALUE 'N'.           
019505         88  PS-END-OF-COMPONENT-CUR                 VALUE 'Y'.           
019506         88  PS-END-OF-COMPONENT-CUR-NO              VALUE 'N'.           
019507     05  PS-ALL-COMPONENTS-AUDITED-SW     PIC X(01)  VALUE 'N'.           
019508         88  PS-ALL-COMPONENTS-AUDITED               VALUE 'Y'.           
019509         88  PS-ALL-COMPONENTS-AUDITED-NO            VALUE 'N'.           
033835                                                                  00610006
033836 01  PC-PROGRAM-CONSTANTS.                                        00620006
033837     05  PC-PGM-NAME               PIC X(08) VALUE 'TRKUP322'.    00630006
370594     05  PC-A                      PIC X(01) VALUE 'A'.                   
370595     05  PC-D                      PIC X(01) VALUE 'D'.                   
370596                                                                  00660006
370597 01  PV-PROGRAM-VARIABLES.                                        00670006
370598     05  PV-CURRENT-TMST               PIC X(26)  VALUE SPACES.   00680006
370599     05  PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.           
370600     05  PV-DB2-OPERATION-MESSAGE      PIC X(79)  VALUE SPACES.           
370601     05  PV-DB2-TABLE-NAME-1           PIC X(18)  VALUE SPACES.           
370602     05  PV-DB2-TABLE-NAME-2           PIC X(18)  VALUE SPACES.           
370603     05  PV-DB2-TABLE-NAME-3           PIC X(18)  VALUE SPACES.           
370604     05  PV-PACK-UPC-MATCH-COUNT       PIC S9(4)  COMP VALUE 0.           
370605     05  PV-TTL-PCK-UNITS-SENT         PIC S9(13) VALUE +0 COMP-3.        
370606     05  PV-TTL-PCK-CARTS-SENT         PIC S9(13) VALUE +0 COMP-3.        
370607                                                                          
370608 01  PV-NEW-IMCNUP-PREPK-REC.                                     00670006
370609     05 PV-NEW-IMCNUP-CONTNR-ID           PIC   X(12).                    
370610     05 PV-NEW-IMCNUP-CONTNR-SEQ-NBR      PIC  S9(04) COMP.               
370620     05 PV-NEW-IMCNUP-PO-NBR              PIC  S9(09) COMP.               
370630     05 PV-NEW-IMCNUP-VS-NBR              PIC   X(20).                    
370640     05 PV-NEW-IMCNUP-UPC-NBR             PIC  S9(15)V COMP-3.            
370650     05 PV-NEW-IMCNUP-PO-LNE-NBR          PIC  S9(09) COMP.               
370660     05 PV-NEW-IMCNUP-UPC-CART-CNT        PIC  S9(09) COMP.               
370670     05 PV-NEW-IMCNUP-UPC-CBMT-QTY        PIC  S9(07)V9(2) COMP-3.        
370680     05 PV-NEW-IMCNUP-UPC-WT-KGRM-QTY     PIC  S9(09) COMP.               
370690     05 PV-NEW-IMCNUP-UPC-UNT-QTY         PIC  S9(09) COMP.               
370700     05 PV-NEW-IMCNUP-CRE-BY-USR-ID       PIC   X(08).                    
370800     05 PV-NEW-IMCNUP-CRE-TMST            PIC   X(26).                    
370900     05 PV-NEW-IMCNUP-LAST-UPD-BY-USR     PIC   X(08).                    
371000     05 PV-NEW-IMCNUP-LAST-UPD-TMST       PIC   X(26).                    
371100     05 PV-NEW-IMCNUP-SKU-NBR             PIC  S9(08)V COMP-3.            
371101     05 PV-NEW-IMCNUP-ASN-PKGNG-CDE       PIC   X(02).                    
371102                                                                          
371103                                                                          
371104                                                                          
371105                                                                          
371106                                                                          
371107 01  ABEND-CODE                           PIC S9(4) VALUE ZEROS   00850006
371108                                                    COMP SYNC.    00860006
371109     88  PC-CONTROL-CARD-ERROR                      VALUE +4001.  00870006
371110     88  PC-DB2-ERROR                               VALUE +4013.  00880006
371111                                                                  00890006
371112*---------------------------------------------------------------*         
371113*---------------------------------------------------------------*         
371114     EXEC SQL                                                             
371115          INCLUDE TPURCHS                                                 
371116     END-EXEC.                                                            
371117*---------------------------------------------------------------*         
371118*---------------------------------------------------------------*         
371119     EXEC SQL                                                             
371120          INCLUDE TPLNSHP                                                 
371121     END-EXEC.                                                            
371122*---------------------------------------------------------------*         
371123*---------------------------------------------------------------*         
371124     EXEC SQL                                                             
371125          INCLUDE TIMCNTK                                                 
371126     END-EXEC.                                                            
371127*---------------------------------------------------------------*         
371128*---------------------------------------------------------------*         
371129     EXEC SQL                                                             
371130          INCLUDE TIMCNPO                                                 
371131     END-EXEC.                                                            
371132*---------------------------------------------------------------*         
371133*---------------------------------------------------------------*         
371134     EXEC SQL                                                             
371135          INCLUDE TIMCNRL                                                 
371136     END-EXEC.                                                            
371137*---------------------------------------------------------------*         
371138*---------------------------------------------------------------*         
371139     EXEC SQL                                                             
371140          INCLUDE TIMPORL                                                 
371141     END-EXEC.                                                            
371142*---------------------------------------------------------------*         
371143*---------------------------------------------------------------*         
371144     EXEC SQL                                                             
371145          INCLUDE TSHPTPO                                                 
371146     END-EXEC.                                                            
371147*---------------------------------------------------------------*         
371148*---------------------------------------------------------------*         
371149     EXEC SQL                                                             
371150          INCLUDE TSHIPMT                                                 
371151     END-EXEC.                                                            
371152*---------------------------------------------------------------*         
371153*---------------------------------------------------------------*         
371154     EXEC SQL                                                             
371155          INCLUDE TVHTRIP                                                 
371156     END-EXEC.                                                            
371157*---------------------------------------------------------------*         
371158*---------------------------------------------------------------*         
371159     EXEC SQL                                                             
371160          INCLUDE TVEHICL                                                 
371161     END-EXEC.                                                            
371162*---------------------------------------------------------------*         
371163*---------------------------------------------------------------*         
371164     EXEC SQL                                                             
371165          INCLUDE TIMCNUP                                                 
371166     END-EXEC.                                                            
371167*---------------------------------------------------------------*         
371168*---------------------------------------------------------------*         
371169     EXEC SQL                                                             
371170          INCLUDE TAUCNUP                                                 
371171     END-EXEC.                                                            
371172*---------------------------------------------------------------*         
371173*---------------------------------------------------------------*         
371174     EXEC SQL                                                             
371175          INCLUDE TPOPRPK                                                 
371176     END-EXEC.                                                            
371177*---------------------------------------------------------------*         
371178*---------------------------------------------------------------*         
371179     EXEC SQL                                                             
371180          INCLUDE TPURITM                                                 
371181     END-EXEC.                                                            
371182*---------------------------------------------------------------*         
371183*---------------------------------------------------------------*         
371184     EXEC SQL                                                             
371185          INCLUDE TUPCSKU                                                 
371186     END-EXEC.                                                            
371187*---------------------------------------------------------------*         
371188*---------------------------------------------------------------*         
371189     EXEC SQL                                                             
371190          INCLUDE TUPC                                                    
371191     END-EXEC.                                                            
371192*---------------------------------------------------------------*         
371193*  ABEND PROCESSING COPYBOOK.                                    *        
371194*---------------------------------------------------------------*         
371195     COPY DPWS004.                                                        
371196*---------------------------------------------------------------* 01180006
371197*  DB2 COMMUNICATION AREA.                                        01190006
371198*---------------------------------------------------------------* 01200006
371199     EXEC SQL                                                     01210006
371200         INCLUDE SQLCA                                            01220006
371201     END-EXEC.                                                    01230006
371202*---------------------------------------------------------------* 01250006
371203* THE PURPOSE OF THIS CURSOR IS TO GET ALL CONTAINER AND          01260006
371204* PURCHASE ORDERS THAT MAY NEED COPONENT PACKS CHANGED TO                 
371205* PREPACKS.                                                               
371206*---------------------------------------------------------------* 01270006
371207     EXEC SQL                                                     01280006
371208          DECLARE PRPK-SPLT-CSR CURSOR WITH HOLD FOR              01290006
371209           SELECT CNTK.CONTNR_ID                                          
371210                 ,CNPO.PO_NBR                                             
371220           FROM   TPURCHS PURC                                            
371230                 ,TPLNSHP PLNS                                            
371240                 ,TIMCNTK CNTK                                            
371250                 ,TIMCNPO CNPO                                            
371260                 ,TIMCNRL CNRL                                            
371270                 ,TIMPORL IMPO                                            
371280           WHERE  PURC.VER_STATUS_CDE   = 'A'                             
371290             AND  PURC.STATUS_CDE       IN ('AC','AP','CA','CM')          
371300             AND  PURC.PKGNG_CDE        IN ('M','I')                      
371400             AND  PURC.PO_NBR           = CNPO.PO_NBR                     
371500             AND  PURC.PO_NBR           = PLNS.PO_NBR                     
371600             AND  PLNS.VER_STATUS_CDE   = 'A'                             
371700             AND  PLNS.STRT_FCTRY_DTE   > '2003-05-01'                    
371800             AND (CNRL.DCONSLR_RLSE_CDE IN ('S','N')                      
371900              OR (CNRL.DCONSLR_RLSE_CDE = 'D'                             
372000             AND  CNRL.DCONSLR_ACTL_DTE = '9999-09-09'))                  
372100             AND  CNPO.CONTNR_ID        = CNRL.CONTNR_ID                  
372200             AND  CNPO.PO_NBR           = CNRL.PO_NBR                     
372300             AND  CNPO.CONTNR_SEQ_NBR   = CNRL.CONTNR_SEQ_NBR             
372400             AND  IMPO.PO_NBR           = PURC.PO_NBR                     
372500             AND  IMPO.VER_STATUS_CDE   = 'A'                             
372600             AND (IMPO.DCONSLR_RLSE_CDE = 'N'                             
372700              OR  IMPO.DCONSLR_ACTL_DTE = '9999-09-09')                   
372800             AND  CNTK.CONTNR_ID        = CNPO.CONTNR_ID                  
372900             AND  CNTK.CONTNR_SEQ_NBR   = CNPO.CONTNR_SEQ_NBR             
373000             AND  1 > (SELECT COUNT(*)                                    
373100                       FROM   TSHPTPO A                                   
373200                             ,TSHIPMT B                                   
373300                             ,TVHTRIP C                                   
373400                             ,TVEHICL D                                   
373500                       WHERE  A.PO_NBR        = PURC.PO_NBR               
373600                         AND  B.SHIPT_ID      = A.SHIPT_ID                
373700                         AND  B.PRO_NBR       = CNRL.CONTNR_ID            
373800                         AND  C.TRIP_ID       = B.TRIP_ID                 
373900                         AND  D.CURR_TRIP_ID  = C.TRIP_ID                 
374000                         AND  D.VEH_TYP_CDE   IN ('VS','AV'))             
374001           ORDER BY 1,2                                                   
374002     END-EXEC.                                                    01350006
374003*                                                                 01360006
374004*---------------------------------------------------------------* 01250006
374005*  THIS CURSOR SUMS QUANTITIES FOR COMPONENT PACKS TO BE PLACED           
374006*  ON THE NEW PREPACK.                                                    
374007*---------------------------------------------------------------* 01250006
374008     EXEC SQL                                                             
374009          DECLARE COMPONENT_CUR CURSOR FOR                                
374010           SELECT                                                         
374011                  POP.PO_PREPK_ID                                         
374012                 ,POP.PREPK_UPC_NBR                                       
374013                 ,SUM(CNUP.UPC_CART_CNT)                                  
374014                 ,SUM(CNUP.UPC_CBMT_QTY)                                  
374015                 ,SUM(CNUP.UPC_WT_KGRM_QTY)                               
374016                 ,SUM(CNUP.UPC_UNT_QTY)                                   
374017                 ,POP.PK_SKU_NBR                                          
374018             FROM TIMCNUP   CNUP                                          
374019                 ,TPOPRPK   POP                                           
374020                 ,TPURITM   PUR                                           
374021                 ,TUPCSKU   SKU                                           
374022                 ,TUPC      UPC                                           
374023             WHERE POP.PO_NBR          = :IMCNPO-PO-NBR                   
374024               AND PUR.PO_NBR          =  POP.PO_NBR                      
374025               AND PUR.PO_PREPK_ID     =  POP.PO_PREPK_ID                 
374026               AND PUR.VER_STATUS_CDE  = 'A'                              
374027               AND SKU.ITEM_NBR        =  PUR.ITM_NBR                     
374028               AND SKU.UPC_NBR        >=  000400000000000                 
374029               AND SKU.UPC_NBR        <=  000499999999999                 
374030               AND POP.PREPK_UPC_NBR   >  0                               
374031               AND SKU.UPC_NBR         =  UPC.UPC_NBR                     
374032               AND UPC.UPC_TYP_CDE     = 'I'                              
374033               AND CNUP.PO_NBR         =  POP.PO_NBR                      
374034               AND CNUP.CONTNR_ID      = :IMCNTK-CONTNR-ID                
374035               AND CNUP.UPC_NBR        =  UPC.UPC_NBR                     
374036             GROUP BY POP.PO_PREPK_ID                                     
374037                     ,POP.PREPK_UPC_NBR                                   
374038                     ,POP.PK_SKU_NBR                                      
374039     END-EXEC.                                                            
374040*                                                                 01360006
374041*---------------------------------------------------------------* 01250006
374042* THIS CURSOR AUDITS THE COMPONENT PACKS.                                 
374043*---------------------------------------------------------------* 01250006
374044     EXEC SQL                                                             
374045          DECLARE AUDIT_IMCNUP_CUR CURSOR FOR                             
374046           SELECT DISTINCT                                                
374047                  CNUP.CONTNR_ID                                          
374048                 ,CNUP.CONTNR_SEQ_NBR                                     
374049                 ,CNUP.PO_NBR                                             
374050                 ,CNUP.VS_NBR                                             
374060                 ,CNUP.UPC_NBR                                            
374070                 ,CNUP.PO_LNE_NBR                                         
374080                 ,CNUP.UPC_CART_CNT                                       
374090                 ,CNUP.UPC_CBMT_QTY                                       
374100                 ,CNUP.UPC_WT_KGRM_QTY                                    
374200                 ,CNUP.UPC_UNT_QTY                                        
374300                 ,CNUP.LAST_UPD_BY_USR_ID                                 
374400                 ,CNUP.LAST_UPD_TMST                                      
374500                 ,CNUP.SKU_NBR                                            
374501                 ,CNUP.ASN_PKGNG_CDE                                      
374502             FROM TIMCNUP   CNUP                                          
374503                 ,TPOPRPK   POP                                           
374504                 ,TPURITM   PUR                                           
374505                 ,TUPCSKU   SKU                                           
374506                 ,TUPC      UPC                                           
374507             WHERE POP.PO_NBR          = :IMCNPO-PO-NBR                   
374508               AND PUR.PO_NBR          =  POP.PO_NBR                      
374509               AND PUR.PO_PREPK_ID     =  POP.PO_PREPK_ID                 
374510               AND PUR.VER_STATUS_CDE  = 'A'                              
374511               AND SKU.ITEM_NBR        =  PUR.ITM_NBR                     
374512               AND SKU.UPC_NBR        >=  000400000000000                 
374513               AND SKU.UPC_NBR        <=  000499999999999                 
374514               AND POP.PREPK_UPC_NBR   >  0                               
374515               AND SKU.UPC_NBR         =  UPC.UPC_NBR                     
374516               AND UPC.UPC_TYP_CDE     = 'I'                              
374517               AND CNUP.PO_NBR         =  POP.PO_NBR                      
374518               AND CNUP.CONTNR_ID      = :IMCNTK-CONTNR-ID                
374519               AND CNUP.UPC_NBR        =  UPC.UPC_NBR                     
374520     END-EXEC.                                                            
374521***************************************************************** 01370006
374522 PROCEDURE DIVISION.                                              01380006
374523***************************************************************** 01390006
374524                                                                  01400006
374525*--------------------------------------------------------------*  01410006
374526* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *  01420006
374527* PROGRAM.                                                     *  01430006
374528*--------------------------------------------------------------*  01440006
374529 0000-PROGRAM-MAINLINE.                                           01450006
374530                                                                  01460006
374540     PERFORM 1000-INITIALIZE.                                     01470006
374550                                                                          
374551     PERFORM 2110-OPEN-PRPK-SPLT-CURSOR.                                  
374552     PERFORM 2000-PROCESS-PRPK-SPLT-CURSOR                                
374553       UNTIL PS-END-OF-PREPACK-CUR.                                       
374554     PERFORM 2120-CLOSE-PRPK-SPLT-CURSOR.                                 
374555                                                                          
374556     GOBACK.                                                      01490006
374557*--------------------------------------------------------------*  01500006
374558 1000-INITIALIZE.                                                 01510006
374559     CONTINUE.                                                    01550006
374560                                                                          
374570*--------------------------------------------------------------*  01640006
374580* FETCH THE DRIVING CURSOR, THEN CHECK IF THE PREPACK EXISTS ON           
374590* TIMCNUP.  IF IT DOES NOT PROCESS THE COMPONENT PACKS, ELSE              
374600* SKIP IT.                                                                
374700*--------------------------------------------------------------*  01640006
374800 2000-PROCESS-PRPK-SPLT-CURSOR.                                   01650006
374900                                                                          
375000     PERFORM 2100-FETCH-PRPK-SPLT-CURSOR.                                 
375100                                                                          
375200     IF PS-END-OF-PREPACK-CUR                                             
375300        CONTINUE                                                          
375400     ELSE                                                                 
375500        DISPLAY 'PO NUMBER = ' IMCNPO-PO-NBR ' ' IMCNTK-CONTNR-ID         
375600        PERFORM 3000-CHECK-FOR-PACK-UPC-MATCH                             
375700        IF PS-PACK-UPC-MATCH-FOUND-NO                                     
375800           PERFORM 4110-OPEN-COMPONENT-CURSOR                             
375900           SET PS-END-OF-COMPONENT-CUR-NO TO TRUE                         
376000           PERFORM 4000-PROCESS-COMPONENT-CUR                             
376100             UNTIL PS-END-OF-COMPONENT-CUR                                
376200           PERFORM 4120-CLOSE-COMPONENT-CURSOR                            
376300        ELSE                                                              
376400           CONTINUE                                                       
376500        END-IF                                                            
376600     END-IF.                                                              
376700                                                                          
376800*--------------------------------------------------------------*  01640006
376900* FETCH FOR DRIVING CURSOR                                                
377000*--------------------------------------------------------------*  01640006
377100 2100-FETCH-PRPK-SPLT-CURSOR.                                     01650006
473541                                                                          
473542     EXEC SQL                                                             
473543         FETCH PRPK-SPLT-CSR                                              
473544          INTO  :IMCNTK-CONTNR-ID                                         
473545               ,:IMCNPO-PO-NBR                                            
473572     END-EXEC.                                                            
473573                                                                          
473574     EVALUATE TRUE                                                        
473575         WHEN SQLCODE = ZERO                                              
473578              SET PS-END-OF-PREPACK-CUR-NO TO TRUE                        
473579         WHEN SQLCODE = +100                                              
473580              SET PS-END-OF-PREPACK-CUR    TO TRUE                        
473581         WHEN SQLWARN0 NOT = SPACES                                       
473582         WHEN SQLCODE < ZERO                                              
473583         WHEN SQLCODE > ZERO                                              
473584             MOVE '2100-FETCH-PRPK-SPLT-CURSOR'                           
473585                                 TO PV-PARAGRAPH-NAME                     
473586             MOVE 'ERROR FETCHING WITH THE PRPK-SPLT-CSR'                 
473587                                 TO PV-DB2-OPERATION-MESSAGE              
473588             MOVE 'TPURCHS'      TO PV-DB2-TABLE-NAME-1                   
473589             MOVE 'TIMCNTK'      TO PV-DB2-TABLE-NAME-2                   
473590             MOVE 'TIMCNPO'      TO PV-DB2-TABLE-NAME-3                   
473591             PERFORM 9100-DB2-ABEND                                       
473592     END-EVALUATE.                                                        
473593 EJECT                                                                    
473594                                                                          
473595*--------------------------------------------------------------*  01640006
473596* OPEN DRIVER CURSOR.                                                     
473597*--------------------------------------------------------------*  01640006
473598 2110-OPEN-PRPK-SPLT-CURSOR.                                              
473599                                                                          
473600     EXEC SQL                                                             
473601          OPEN PRPK-SPLT-CSR                                              
473602     END-EXEC.                                                            
473603                                                                          
473604     EVALUATE TRUE                                                        
476377         WHEN SQLCODE = ZERO                                              
476378             CONTINUE                                                     
476379         WHEN SQLWARN0 NOT = SPACES                                       
476380         WHEN SQLCODE < ZERO                                              
476381         WHEN SQLCODE > ZERO                                              
476382             MOVE '2110-OPEN-PRPK-SPLT-CURSOR'                            
476383                                      TO PV-PARAGRAPH-NAME                
476384             MOVE 'ATTEMPTING TO OPEN THE PRPK-SPLT-CSR'                  
476385                                      TO PV-DB2-OPERATION-MESSAGE         
476386             MOVE 'TPURCHS'           TO PV-DB2-TABLE-NAME-1              
476387             MOVE 'TIMCNTK'           TO PV-DB2-TABLE-NAME-2              
476388             MOVE 'TIMCNPO'           TO PV-DB2-TABLE-NAME-3              
476389             PERFORM 9100-DB2-ABEND                                       
476390     END-EVALUATE.                                                        
476400                                                                          
476401*--------------------------------------------------------------*  01640006
476402* CLOSE DRIVING CURSOR.                                                   
476403*--------------------------------------------------------------*  01640006
476404 2120-CLOSE-PRPK-SPLT-CURSOR.                                             
476405                                                                          
476406     EXEC SQL                                                             
476407         CLOSE PRPK-SPLT-CSR                                              
476408     END-EXEC.                                                            
476409                                                                          
476410     EVALUATE TRUE                                                        
476411         WHEN SQLCODE = ZERO                                              
476412             CONTINUE                                                     
476413         WHEN SQLWARN0 NOT = SPACES                                       
476414         WHEN SQLCODE < ZERO                                              
476415         WHEN SQLCODE > ZERO                                              
476416             MOVE '2120-CLOSE-PRPK-SPLT-CURSOR'                           
476417                                      TO PV-PARAGRAPH-NAME                
476418             MOVE 'ATTEMPTING TO CLOSE THE PRPK-SPLT-CSR'                 
476419                                      TO PV-DB2-OPERATION-MESSAGE         
476420             MOVE 'TPURCHS'           TO PV-DB2-TABLE-NAME-1              
476421             MOVE 'TIMCNTK'           TO PV-DB2-TABLE-NAME-2              
476422             MOVE 'TIMCNPO'           TO PV-DB2-TABLE-NAME-3              
476423             PERFORM 9100-DB2-ABEND                                       
476424     END-EVALUATE.                                                        
476425                                                                          
476426*--------------------------------------------------------------*  01640006
476427* CHECK IF ANY PREPACK FOR THE PO IS ON TIMCNUP.                          
476428*--------------------------------------------------------------*  01640006
476429 3000-CHECK-FOR-PACK-UPC-MATCH.                                           
476430                                                                          
476440     EXEC SQL                                                             
476450          SELECT COUNT(*)                                                 
476460          INTO  :PV-PACK-UPC-MATCH-COUNT                                  
476470          FROM   TPOPRPK POP                                              
476480                ,TIMCNUP CNUP                                             
476490          WHERE  POP.PO_NBR        = :IMCNPO-PO-NBR                       
476500            AND  CNUP.CONTNR_ID    = :IMCNTK-CONTNR-ID                    
476600            AND  POP.PREPK_UPC_NBR = CNUP.UPC_NBR                         
476700     END-EXEC.                                                            
476701                                                                          
476702     EVALUATE TRUE                                                        
476703          WHEN SQLCODE = ZERO                                             
476704              IF PV-PACK-UPC-MATCH-COUNT > 0                              
476705                 SET PS-PACK-UPC-MATCH-FOUND    TO TRUE                   
476706              ELSE                                                        
476707                 SET PS-PACK-UPC-MATCH-FOUND-NO TO TRUE                   
476708              END-IF                                                      
476709          WHEN SQLWARN0 NOT = SPACES                                      
476710          WHEN SQLCODE < ZERO                                             
476711          WHEN SQLCODE > ZERO                                             
476712              MOVE '3000-CHECK-FOR-PACK-UPC-MATCH'                        
476713                                   TO  PV-PARAGRAPH-NAME                  
476714              MOVE 'ERROR ISSUED ON SELECT'                               
476715                                   TO  PV-DB2-OPERATION-MESSAGE           
476716              MOVE 'TIMCNUP'       TO  PV-DB2-TABLE-NAME-1                
476717              MOVE 'TPOPRPK'       TO  PV-DB2-TABLE-NAME-1                
476718              PERFORM 9100-DB2-ABEND                                      
476719     END-EVALUATE.                                                        
476720                                                                          
476730*--------------------------------------------------------------*  01640006
476740* IF NO PREPACK FOR THE PURCHASE ORDER IS ON TIMCNUP, PROCESS             
476750* THE COMPONENT PACKS.                                                    
476760*--------------------------------------------------------------*  01640006
476770 4000-PROCESS-COMPONENT-CUR.                                      01650006
476780                                                                          
476790     PERFORM 4100-FETCH-COMPONENT-CURSOR.                                 
476800                                                                          
476900     IF PS-END-OF-COMPONENT-CUR-NO                                        
477000        DISPLAY '   PREPK ID  =  ' PURITM-PO-PREPK-ID                     
477100        DISPLAY '   PREPK UPC =  ' PV-NEW-IMCNUP-UPC-NBR                  
477200        DISPLAY '   SUM CART  =  ' PV-NEW-IMCNUP-UPC-CART-CNT     01640006
477300        DISPLAY '   SUM CBMT  =  ' PV-NEW-IMCNUP-UPC-CBMT-QTY             
477400        DISPLAY '   SUM KGRM  =  ' PV-NEW-IMCNUP-UPC-WT-KGRM-QTY          
477500        DISPLAY '   SUM UNIT  =  ' PV-NEW-IMCNUP-UPC-UNT-QTY              
477600                                                                          
477700        PERFORM 5110-OPEN-AUDIT-IMCNUP-CURSOR                             
477800        SET PS-ALL-COMPONENTS-AUDITED-NO TO TRUE                          
477900        PERFORM 5000-AUDIT-IMCNUP-ROWS                                    
478000          UNTIL PS-ALL-COMPONENTS-AUDITED                                 
478100        PERFORM 5120-CLOSE-AUDIT-IMCNUP-CURSOR                            
478200                                                                          
478300        PERFORM 6000-INSERT-NEW-IMCNUP-PREPK                              
478400                                                                          
478500     END-IF.                                                              
478600                                                                          
478700*--------------------------------------------------------------*  01640006
478800* FETCH FOR COMPONENT PACK CURSOR.                                        
478900*--------------------------------------------------------------*  01640006
478901 4100-FETCH-COMPONENT-CURSOR.                                     01650006
478902                                                                          
478903     EXEC SQL                                                             
478904         FETCH COMPONENT_CUR                                              
478905          INTO :PURITM-PO-PREPK-ID                                        
644049              ,:PV-NEW-IMCNUP-UPC-NBR                                     
644050              ,:PV-NEW-IMCNUP-UPC-CART-CNT                                
644060              ,:PV-NEW-IMCNUP-UPC-CBMT-QTY                                
644070              ,:PV-NEW-IMCNUP-UPC-WT-KGRM-QTY                             
644080              ,:PV-NEW-IMCNUP-UPC-UNT-QTY                                 
644081              ,:POPRPK-PK-SKU-NBR                                         
644082     END-EXEC.                                                            
644083                                                                          
644084     EVALUATE TRUE                                                        
644085         WHEN SQLCODE = ZERO                                              
644086              SET PS-END-OF-COMPONENT-CUR-NO TO TRUE                      
644087         WHEN SQLCODE = +100                                              
644088              SET PS-END-OF-COMPONENT-CUR    TO TRUE                      
644089         WHEN SQLWARN0 NOT = SPACES                                       
644090         WHEN SQLCODE < ZERO                                              
644091         WHEN SQLCODE > ZERO                                              
644092             MOVE '4100-FETCH-COMPONENT-CURSOR'                           
644093                                 TO PV-PARAGRAPH-NAME                     
644094             MOVE 'ERROR FETCHING WITH THE COMPONENT_CUR'                 
644095                                 TO PV-DB2-OPERATION-MESSAGE              
644096             MOVE 'TIMCNUP'      TO PV-DB2-TABLE-NAME-1                   
644097             MOVE 'TPOPRPK'      TO PV-DB2-TABLE-NAME-2                   
644098             MOVE 'TPURITM'      TO PV-DB2-TABLE-NAME-3                   
644099             PERFORM 9100-DB2-ABEND                                       
644100     END-EVALUATE.                                                        
644200 EJECT                                                                    
644300                                                                          
644301*--------------------------------------------------------------*  01640006
644302* OPEN FOR COMPONENT PACK CURSOR.                                         
644303*--------------------------------------------------------------*  01640006
644304 4110-OPEN-COMPONENT-CURSOR.                                              
644305                                                                          
644306     EXEC SQL                                                             
644307          OPEN COMPONENT_CUR                                              
644308     END-EXEC.                                                            
644309                                                                          
644310     EVALUATE TRUE                                                        
644311         WHEN SQLCODE = ZERO                                              
644312             CONTINUE                                                     
644313         WHEN SQLWARN0 NOT = SPACES                                       
644314         WHEN SQLCODE < ZERO                                              
644315         WHEN SQLCODE > ZERO                                              
644316             MOVE '4110-OPEN-COMPONENT-CURSOR'                            
644317                                      TO PV-PARAGRAPH-NAME                
644318             MOVE 'ATTEMPTING TO OPEN THE COMPONENT_CUR'                  
644319                                      TO PV-DB2-OPERATION-MESSAGE         
644320             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
644321             MOVE 'TPOPRPK'           TO PV-DB2-TABLE-NAME-2              
644322             MOVE 'TPURITM'           TO PV-DB2-TABLE-NAME-3              
644323             PERFORM 9100-DB2-ABEND                                       
644324     END-EVALUATE.                                                        
644325                                                                          
644326*--------------------------------------------------------------*  01640006
644327* CLOSE FOR COMPONENT PACK CURSOR.                                        
644328*--------------------------------------------------------------*  01640006
644329 4120-CLOSE-COMPONENT-CURSOR.                                             
644330                                                                          
644331     EXEC SQL                                                             
644332         CLOSE COMPONENT_CUR                                              
644333     END-EXEC.                                                            
644334                                                                          
644335     EVALUATE TRUE                                                        
644336         WHEN SQLCODE = ZERO                                              
644337             CONTINUE                                                     
644338         WHEN SQLWARN0 NOT = SPACES                                       
644339         WHEN SQLCODE < ZERO                                              
644340         WHEN SQLCODE > ZERO                                              
644341             MOVE '4120-CLOSE-COMPONENT-CURSOR'                           
644342                                      TO PV-PARAGRAPH-NAME                
644343             MOVE 'ATTEMPTING TO CLOSE THE COMPONENT_CUR'                 
644344                                      TO PV-DB2-OPERATION-MESSAGE         
644345             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
644346             MOVE 'TPOPRPK'           TO PV-DB2-TABLE-NAME-2              
644347             MOVE 'TPURITM'           TO PV-DB2-TABLE-NAME-3              
644348             PERFORM 9100-DB2-ABEND                                       
644349     END-EVALUATE.                                                        
644350                                                                          
644360*--------------------------------------------------------------*  01640006
644370* AFTER COMPONENT PACKS ARE PROCESSED AUDIT THEM.                         
644380*--------------------------------------------------------------*  01640006
644390 5000-AUDIT-IMCNUP-ROWS.                                          01650006
644400                                                                          
644500     PERFORM 5100-FETCH-AUDIT-IMCNUP-CURSOR.                              
644600                                                                          
644700     IF PS-ALL-COMPONENTS-AUDITED-NO                                      
644800        PERFORM 5200-INSERT-AUDIT-ROW                                     
644900        PERFORM 5300-DELETE-IMCNUP-ROW                                    
645000        CONTINUE                                                          
645100     END-IF.                                                              
645200                                                                          
645300*--------------------------------------------------------------*  01640006
645400* FETCH FOR AUDIT CUROSR.                                                 
645500*--------------------------------------------------------------*  01640006
645501 5100-FETCH-AUDIT-IMCNUP-CURSOR.                                  01650006
645502                                                                          
645503     EXEC SQL                                                             
645504         FETCH AUDIT_IMCNUP_CUR                                           
645505          INTO :AUCNUP-CONTNR-ID                                          
645506              ,:AUCNUP-CONTNR-SEQ-NBR                                     
645507              ,:AUCNUP-PO-NBR                                             
645508              ,:AUCNUP-VS-NBR                                             
645509              ,:AUCNUP-UPC-NBR                                            
645510              ,:AUCNUP-PO-LNE-NBR                                         
645520              ,:AUCNUP-UPC-CART-CNT                                       
645530              ,:AUCNUP-UPC-CBMT-QTY                                       
645540              ,:AUCNUP-UPC-WT-KGRM-QTY                                    
645550              ,:AUCNUP-UPC-UNT-QTY                                        
645560              ,:AUCNUP-LAST-UPD-BY-USR-ID                                 
645570              ,:AUCNUP-LAST-UPD-TMST                                      
645580              ,:AUCNUP-SKU-NBR                                            
645581              ,:AUCNUP-ASN-PKGNG-CDE                                      
645582     END-EXEC.                                                            
645583                                                                          
645584     EVALUATE TRUE                                                        
645585         WHEN SQLCODE = ZERO                                              
645586              SET PS-ALL-COMPONENTS-AUDITED-NO TO TRUE                    
645587         WHEN SQLCODE = +100                                              
645588              SET PS-ALL-COMPONENTS-AUDITED    TO TRUE                    
645589         WHEN SQLWARN0 NOT = SPACES                                       
645590         WHEN SQLCODE < ZERO                                              
645591         WHEN SQLCODE > ZERO                                              
645592             MOVE '5100-FETCH-AUDIT-IMCNUP-CURSOR'                        
645593                                 TO PV-PARAGRAPH-NAME                     
645594             MOVE 'ERROR FETCHING WITH THE AUDIT_IMCNUP_CUR'              
645595                                 TO PV-DB2-OPERATION-MESSAGE              
645596             MOVE 'TIMCNUP'      TO PV-DB2-TABLE-NAME-1                   
645597             MOVE 'TPOPRPK'      TO PV-DB2-TABLE-NAME-2                   
645598             MOVE 'TPURITM'      TO PV-DB2-TABLE-NAME-3                   
645599             PERFORM 9100-DB2-ABEND                                       
645600     END-EVALUATE.                                                        
645700 EJECT                                                                    
645800                                                                          
645801*--------------------------------------------------------------*  01640006
645802* OPEN FOR AUDIT CUROSR.                                                  
645803*--------------------------------------------------------------*  01640006
645804 5110-OPEN-AUDIT-IMCNUP-CURSOR.                                           
645805                                                                          
645806     EXEC SQL                                                             
645807          OPEN AUDIT_IMCNUP_CUR                                           
645808     END-EXEC.                                                            
645809                                                                          
645810     EVALUATE TRUE                                                        
645811         WHEN SQLCODE = ZERO                                              
645812             CONTINUE                                                     
645813         WHEN SQLWARN0 NOT = SPACES                                       
645814         WHEN SQLCODE < ZERO                                              
645815         WHEN SQLCODE > ZERO                                              
645816             MOVE '5110-OPEN-AUDIT-IMCNUP-CURSOR'                         
645817                                      TO PV-PARAGRAPH-NAME                
645818             MOVE 'ATTEMPTING TO OPEN THE AUDIT_IMCNUP_CUR'               
645819                                      TO PV-DB2-OPERATION-MESSAGE         
645820             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
645821             MOVE 'TPOPRPK'           TO PV-DB2-TABLE-NAME-2              
645822             MOVE 'TPURITM'           TO PV-DB2-TABLE-NAME-3              
645823             PERFORM 9100-DB2-ABEND                                       
645824     END-EVALUATE.                                                        
645825                                                                          
645826*--------------------------------------------------------------*  01640006
645827* CLOSE FOR AUDIT CUROSR.                                                 
645828*--------------------------------------------------------------*  01640006
645829 5120-CLOSE-AUDIT-IMCNUP-CURSOR.                                          
645830                                                                          
645831     EXEC SQL                                                             
645832         CLOSE AUDIT_IMCNUP_CUR                                           
645833     END-EXEC.                                                            
645834                                                                          
645835     EVALUATE TRUE                                                        
645836         WHEN SQLCODE = ZERO                                              
645837             CONTINUE                                                     
645838         WHEN SQLWARN0 NOT = SPACES                                       
645839         WHEN SQLCODE < ZERO                                              
645840         WHEN SQLCODE > ZERO                                              
645841             MOVE '5120-CLOSE-AUDIT-IMCNUP-CURSOR'                        
645842                                      TO PV-PARAGRAPH-NAME                
645843             MOVE 'ATTEMPTING TO CLOSE THE AUDIT_IMCNUP_CUR'              
645844                                      TO PV-DB2-OPERATION-MESSAGE         
645845             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
645846             MOVE 'TPOPRPK'           TO PV-DB2-TABLE-NAME-2              
645847             MOVE 'TPURITM'           TO PV-DB2-TABLE-NAME-3              
645848             PERFORM 9100-DB2-ABEND                                       
645849     END-EVALUATE.                                                        
645850                                                                          
645860*--------------------------------------------------------------*  01640006
797278* INSERT AUDIT ROW FOR COMPONENT PACK.                                    
797279*--------------------------------------------------------------*  01640006
797280 5200-INSERT-AUDIT-ROW.                                                   
797287                                                                          
797288         EXEC SQL                                                         
797289             INSERT INTO TAUCNUP                                          
797290                   (CONTNR_ID,                                            
797291                    CONTNR_SEQ_NBR,                                       
797292                    PO_NBR,                                               
797293                    VS_NBR,                                               
797294                    UPC_NBR,                                              
797295                    PO_LNE_NBR,                                           
797296                    UPC_CART_CNT,                                         
797297                    UPC_CBMT_QTY,                                         
797298                    UPC_WT_KGRM_QTY,                                      
797299                    UPC_UNT_QTY,                                          
797303                    LAST_UPD_BY_USR_ID,                                   
797304                    LAST_UPD_TMST,                                        
797305                    AUD_FUNC_CDE,                                         
797306                    AUD_TMST,                                             
797308                    SKU_NBR,                                              
797309                    ASN_PKGNG_CDE)                                        
797311             VALUES(                                                      
797312                   :AUCNUP-CONTNR-ID                                      
797313                  ,:AUCNUP-CONTNR-SEQ-NBR                                 
797314                  ,:AUCNUP-PO-NBR                                         
797315                  ,:AUCNUP-VS-NBR                                         
797316                  ,:AUCNUP-UPC-NBR                                        
797317                  ,:AUCNUP-PO-LNE-NBR                                     
797318                  ,:AUCNUP-UPC-CART-CNT                                   
797319                  ,:AUCNUP-UPC-CBMT-QTY                                   
797320                  ,:AUCNUP-UPC-WT-KGRM-QTY                                
797330                  ,:AUCNUP-UPC-UNT-QTY                                    
797340                  ,:AUCNUP-LAST-UPD-BY-USR-ID                             
797350                  ,:AUCNUP-LAST-UPD-TMST                                  
797360                  ,:PC-D                                                  
797370                  ,CURRENT TIMESTAMP                                      
797380                  ,:AUCNUP-SKU-NBR                                        
797381                  ,:AUCNUP-ASN-PKGNG-CDE)                                 
797382         END-EXEC                                                         
797383                                                                          
797384     EVALUATE TRUE                                                        
797385          WHEN SQLCODE = ZERO                                             
797386              CONTINUE                                                    
797387          WHEN SQLWARN0 NOT = SPACES                                      
797388          WHEN SQLCODE < ZERO                                             
797389          WHEN SQLCODE > ZERO                                             
797390              MOVE '5200-INSERT-AUDIT-ROW'                                
797391                                   TO  PV-PARAGRAPH-NAME                  
797392              MOVE 'ERROR ISSUED ON INSERT'                               
797393                                   TO  PV-DB2-OPERATION-MESSAGE           
797394              MOVE 'TAUCNUP'       TO  PV-DB2-TABLE-NAME-1                
797395              PERFORM 9100-DB2-ABEND                                      
797396     END-EVALUATE.                                                        
797397                                                                          
797398*----------------------------------------------------------------*        
797399* DELETE COMPONENT ROW AFTER AUDITED.                                     
797400*----------------------------------------------------------------*        
797500 5300-DELETE-IMCNUP-ROW.                                                  
797501                                                                          
797502         EXEC SQL                                                         
797503             DELETE FROM TIMCNUP                                          
797504             WHERE  CONTNR_ID      = :AUCNUP-CONTNR-ID                    
797505               AND  CONTNR_SEQ_NBR = :AUCNUP-CONTNR-SEQ-NBR               
797506               AND  PO_NBR         = :AUCNUP-PO-NBR                       
797507               AND  UPC_NBR        = :AUCNUP-UPC-NBR                      
797508         END-EXEC                                                         
797509                                                                          
797510     EVALUATE TRUE                                                        
797520          WHEN SQLCODE = ZERO                                             
797521              CONTINUE                                                    
797522          WHEN SQLWARN0 NOT = SPACES                                      
797523          WHEN SQLCODE < ZERO                                             
797524          WHEN SQLCODE > ZERO                                             
797525              MOVE '5300-DELETE-IMCNUP-ROW'                               
797526                                   TO  PV-PARAGRAPH-NAME                  
797527              MOVE 'ERROR ISSUED ON DELETE'                               
797528                                   TO  PV-DB2-OPERATION-MESSAGE           
797529              MOVE 'TIMCNUP'       TO  PV-DB2-TABLE-NAME-1                
797530              PERFORM 9100-DB2-ABEND                                      
797540     END-EVALUATE.                                                        
797541                                                                          
797542*----------------------------------------------------------------*        
797543* INSERT NEW PREPACK ROW.                                                 
797544*----------------------------------------------------------------*        
797545 6000-INSERT-NEW-IMCNUP-PREPK.                                            
797546                                                                          
797547         EXEC SQL                                                         
797548             INSERT INTO TIMCNUP                                          
797549                    (CONTNR_ID                                            
797550                    ,CONTNR_SEQ_NBR                                       
797551                    ,PO_NBR                                               
797552                    ,VS_NBR                                               
797553                    ,UPC_NBR                                              
797554                    ,PO_LNE_NBR                                           
797555                    ,UPC_CART_CNT                                         
797556                    ,UPC_CBMT_QTY                                         
797557                    ,UPC_WT_KGRM_QTY                                      
797558                    ,UPC_UNT_QTY                                          
797559                    ,CRE_BY_USR_ID                                        
797560                    ,CRE_TMST                                             
797561                    ,LAST_UPD_BY_USR_ID                                   
797562                    ,LAST_UPD_TMST                                        
797563                    ,SKU_NBR                                              
797564                    ,ASN_PKGNG_CDE)                                       
797565             VALUES(                                                      
797566                   :IMCNTK-CONTNR-ID                                      
797567                  ,:AUCNUP-CONTNR-SEQ-NBR                                 
797568                  ,:IMCNPO-PO-NBR                                         
797569                  ,' '                                                    
797570                  ,:PV-NEW-IMCNUP-UPC-NBR                                 
797580                  ,0                                                      
797590                  ,:PV-NEW-IMCNUP-UPC-CART-CNT                            
797600                  ,:PV-NEW-IMCNUP-UPC-CBMT-QTY                            
797700                  ,:PV-NEW-IMCNUP-UPC-WT-KGRM-QTY                         
797800                  ,:PV-NEW-IMCNUP-UPC-UNT-QTY                             
797900                  ,:PC-PGM-NAME                                           
798000                  ,CURRENT TIMESTAMP                                      
798100                  ,:PC-PGM-NAME                                           
798200                  ,CURRENT TIMESTAMP                                      
798300                  ,:POPRPK-PK-SKU-NBR                                     
798301                  ,'AS')                                                  
798302         END-EXEC                                                         
798303                                                                          
798304     EVALUATE TRUE                                                        
798305          WHEN SQLCODE = ZERO                                             
798306              CONTINUE                                                    
798307          WHEN SQLWARN0 NOT = SPACES                                      
798308          WHEN SQLCODE < ZERO                                             
798309          WHEN SQLCODE > ZERO                                             
798310              MOVE '6000-INSERT-NEW-IMCNUP-PREPK'                         
798311                                   TO  PV-PARAGRAPH-NAME                  
798312              MOVE 'ERROR ISSUED ON INSERT'                               
798313                                   TO  PV-DB2-OPERATION-MESSAGE           
798314              MOVE 'TIMCNUP'       TO  PV-DB2-TABLE-NAME-1                
798315              PERFORM 9100-DB2-ABEND                                      
798316     END-EVALUATE.                                                        
798317                                                                          
798318                                                                          
798319*----------------------------------------------------------------*        
798320*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
798321*  ERROR OR WARNING CODE OCCURS.                                          
798322*----------------------------------------------------------------*        
798323 9100-DB2-ABEND.                                                          
798324                                                                          
798325     DISPLAY '9100-DB2-ABEND'.                                            
798326     DISPLAY '** ABEND           **'.                                     
798327     DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.                      
798328     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
798329     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME-1.              
798330     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-2.              
798331     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-3.              
798332     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE.         
798333                                                                          
798334*----------------------------------------------------------------*        
798335* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
798336* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
798337* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
798338* FORMAT.                                                                 
798339*----------------------------------------------------------------*        
798340     COPY DPPD004.                                                        
798341                                                                          
798342     SET PC-DB2-ERROR TO TRUE.                                            
798343     CALL 'ILBOABN0' USING ABEND-CODE.                                    
798344*                                                                         
